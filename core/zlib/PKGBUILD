# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=zlib
pkgname=(zlib minizip)
epoch=1
pkgver=1.3.2
pkgrel=2
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=(x86_64)
license=(Zlib)
url="https://www.zlib.net/"
source=(https://github.com/madler/zlib/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz{,.asc}
        https://github.com/madler/zlib/commit/36ff1be48ef696cc67b0855f7c8537ce0276210d.patch)
sha512sums=('cf3d49fbabddc57cca99858feeca8f910e9de42a16014cddd406814d2d24ee33fee2af3805d7efbb1b04b05f55818092b000daf82502b675df65f2512c353f73'
            'SKIP'
            '39a4ad7f473e4c170fb91a2e14860eaa6e728e9b710185bf9dbb23b949064fe7e51e36a15ec5ea110760ca8af15f8d2450e02e1a442af66516fe3e8b2debda0d')
b2sums=('fd3a6e4c275a925f4814de487075c136e28421ea34ca7f93141781e7a414b392cd849fbf96cfcb24e825441df5f3c64be54276c0f617dc2d5c865d4ab260e83c'
        'SKIP'
        '489546e224668e177e1358c7bb56bf9206a292d78cdb5fbf437533d07a4dffc60984499abd17fc3c776a09da78caacc9c9b35bf20194b2d4fe8c9224d18da5d9')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')  # Mark Adler <madler@alumni.caltech.edu>

prepare() {
  cd $pkgbase-$pkgver/contrib/minizip
  cp Makefile Makefile.orig
  cp ../README.contrib readme.txt

# Fix missing header
  patch -p3 < "$srcdir"/36ff1be48ef696cc67b0855f7c8537ce0276210d.patch
  autoreconf -fiv
}

build() {
  cd $pkgbase-$pkgver

  CFLAGS+=" -ffat-lto-objects"
  ./configure --prefix=/usr
  make

  cd contrib/minizip
  ./configure --prefix=/usr --enable-static=no
  make
}

check() {
  make test -C $pkgbase-$pkgver

  make -f Makefile.orig test -C $pkgbase-$pkgver/contrib/minizip
}

package_zlib() {
  depends=(glibc)
  provides=(libz.so)

  make install DESTDIR="$pkgdir" -C $pkgbase-$pkgver
  install -D -m644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_minizip() {
  pkgdesc='Mini zip and unzip based on zlib'
  depends=(
    glibc
    zlib
  )

  make install DESTDIR="$pkgdir" -C $pkgbase-$pkgver/contrib/$pkgname
  install -D -m644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # https://github.com/madler/zlib/pull/229
  rm "$pkgdir/usr/include/minizip/crypt.h"
}

# vim: ts=2 sw=2 et:
