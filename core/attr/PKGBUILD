# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=attr
pkgver=2.5.2
pkgrel=1
pkgdesc='Extended attribute support library for ACL support'
arch=('x86_64')
url='https://savannah.nongnu.org/projects/attr'
license=('LGPL-2.1-or-later AND GPL-2.0-or-later')
depends=('glibc')
makedepends=('git' 'gettext')
provides=('libattr.so')
backup=('etc/xattr.conf')
validpgpkeys=('600CD204FBCEA418BD2CA74F154343260542DF34'  # Brandon Philips <brandon@ifup.co>
              'B902B5271325F892AC251AD441633B9FE837F581'  # Frysinger <vapier@gentoo.org>
              '259B3792B3D6D319212CC4DCD5BF9FEB0313653A') # Andreas Gruenbacher <andreas.gruenbacher@gmail.com>
source=("git+https://git.savannah.nongnu.org/git/${pkgname}.git#tag=v${pkgver}?signed")
sha256sums=('406ba69eb08ef5a246b0c2f3b247f880ed56136a140343f668285129e699fa7b')

prepare() {
  cd $pkgname

  # remove the nfs entries from /etc/xattr.conf
  # http://lists.gnu.org/archive/html/bug-coreutils/2019-03/msg00008.html
  sed -i '/nfs/d' xattr.conf
  
  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

check() {
  cd $pkgbase

  make -k check
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
}
