# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Ser <contact@emersion.fr>

pkgname=lib32-libdisplay-info
pkgver=0.2.0
pkgrel=1
pkgdesc='EDID and DisplayID library - 32-bit'
url="https://gitlab.freedesktop.org/emersion/libdisplay-info"
arch=(x86_64)
license=(MIT)
depends=(lib32-glibc lib32-gcc-libs libdisplay-info)
makedepends=(hwdata
             meson
             ninja
             python)
provides=(libdisplay-info.so)
_archive="libdisplay-info-$pkgver"
source=("$url/-/releases/$pkgver/downloads/$_archive.tar.xz"{,.sig})
sha256sums=('5a2f002a16f42dd3540c8846f80a90b8f4bdcd067a94b9d2087bc2feae974176'
            'SKIP')
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48')

build() {
	arch-meson --cross-file lib32 "$_archive" build
	ninja -C build
}

check() {
	meson test -C build --no-rebuild --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
	rm -rv "$pkgdir"/usr/{bin,include}

	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$_archive/LICENSE"
}
