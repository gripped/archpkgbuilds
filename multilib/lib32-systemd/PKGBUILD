# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lib32-systemd
_pkgbasename=systemd
_tag='255.5'
# Upstream versioning is incompatible with pacman's version comparisons so we
# replace tildes with the empty string to make sure pacman's version comparing
# does the right thing for rc versions:
# ➜ vercmp 255~rc1 255
# 1
# ➜ vercmp 255rc1 255
# -1
pkgver="${_tag/~/}"
pkgrel=1
pkgdesc='system and service manager (32-bit)'
arch=('x86_64')
url='https://www.github.com/systemd/systemd'
license=('LGPL-2.1-or-later')
depends=("systemd-libs=${pkgver}" 'lib32-gcc-libs' 'lib32-libcap' 'lib32-libgcrypt'
         'lib32-libxcrypt' 'lib32-xz' 'lib32-zstd')
makedepends=('git' 'gperf' 'intltool' 'lib32-acl' 'lib32-bzip2'
             'lib32-curl' 'lib32-dbus' 'lib32-gcc-libs' 'lib32-glib2'
             'lib32-gnutls' 'lib32-libelf' 'lib32-libidn2' 'lib32-pcre2'
             'libxslt' 'meson' 'python-jinja')
checkdepends=("systemd=${pkgver}")
provides=('libsystemd.so' 'libudev.so')
validpgpkeys=('63CDA1E5D3FC22B998D20DD6327F26951A015CC4'  # Lennart Poettering <lennart@poettering.net>
              'A9EA9081724FFAE0484C35A1A81CEA22BC8C7E2E'  # Luca Boccassi <luca.boccassi@gmail.com>
              '9A774DB5DB996C154EBBFBFDA0099A18E29326E1'  # Yu Watanabe <watanabe.yu+github@gmail.com>
              '5C251B5FC54EB2F80F407AAAC54CA336CFEB557E') # Zbigniew Jędrzejewski-Szmek <zbyszek@in.waw.pl>
source=("git+https://github.com/systemd/systemd-stable#tag=v${_tag}?signed"
        "git+https://github.com/systemd/systemd#tag=v${_tag%.*}?signed")
sha512sums=('ab0d47a29d60cb88f0934a9204c71cd78e2f5f568b9da532fdd4f8da55a352fce51cbcbaf17dc1a6f5b3c43ed7579876c724abcc2af5d8c4d3979f2ede60982f'
            'd430427987309483c99062adb02741d25239ba5fbb97053ef817c0c5a0a935328af9c8b651de2b119b0e851dcf6623f01343859735ff81d7013ab0133e67c7ea')

_backports=(
)

_reverts=(
)

prepare() {
  cd "$_pkgbasename-stable"

  # add upstream repository for cherry-picking
  git remote add -f upstream ../systemd

  local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  local _timeservers=({0..3}.arch.pool.ntp.org)
  local _nameservers=(
    # We use these public name services, ordered by their
    # privacy policy (hopefully):
    #  * Cloudflare (https://1.1.1.1/)
    #  * Quad9 (https://www.quad9.net/)
    #  * Google (https://developers.google.com/speed/public-dns/)
    '1.1.1.1#cloudflare-dns.com'
    '9.9.9.9#dns.quad9.net'
    '8.8.8.8#dns.google'
    '2606:4700:4700::1111#cloudflare-dns.com'
    '2620:fe::9#dns.quad9.net'
    '2001:4860:4860::8888#dns.google'
  )
 
  local _meson_options=(
    --libexecdir	/usr/lib32
    --libdir		/usr/lib32

    -Dversion-tag="${_tag_name}-${pkgrel}-arch"
    # We use the version without tildes as the shared library tag because
    # pacman looks at the shared library version.
    -Dshared-lib-tag="${pkgver}-${pkgrel}"
    -Dmode=release

    # components & features
    -Dapparmor=false
    -Daudit=false
    -Dbacklight=false
    -Dbinfmt=false
    -Dblkid=false
    -Dbootloader=false
    -Dbpf-framework=false
    -Dcoredump=false
    -Dcreate-log-dirs=false
    -Defi=false
    -Denvironment-d=false
    -Dfirstboot=false
    -Dhibernate=false
    -Dhomed=false
    -Dhostnamed=false
    -Dhtml=false
    -Dhwdb=false
    -Dima=false
    -Dimportd=false
    -Dkmod=false
    -Dldconfig=false
    -Dlibcryptsetup=false
    -Dlibcryptsetup-plugins=false
    -Dlibfido2=false
    -Dlibidn2=true
    -Dlibiptc=false
    -Dlocaled=false
    -Dlogind=false
    -Dlz4=false
    -Dmachined=false
    -Dmachined=true
    -Dman=false
    -Dmicrohttpd=false
    -Dnetworkd=false
    -Dnss-myhostname=true
    -Dnss-mymachines=true
    -Dnss-resolve=true
    -Dnss-systemd=true
    -Doomd=false
    -Dpam=false
    -Dpasswdqc=false
    -Dportabled=false
    -Dpstore=false
    -Dpwquality=false
    -Dqrencode=false
    -Dquotacheck=false
    -Drandomseed=false
    -Dremote=false
    -Drepart=false
    -Dresolve=false
    -Dresolve=true
    -Drfkill=false
    -Dseccomp=false
    -Dselinux=false
    -Dsysext=false
    -Dsysusers=false
    -Dtimedated=false
    -Dtimesyncd=false
    -Dtmpfiles=false
    -Dtpm2=false
    -Dtpm=false
    -Dtranslations=false
    -Dukify=false
    -Duserdb=false
    -Dutmp=false
    -Dvconsole=false
    -Dxdg-autostart=false
    -Dxenctrl=false
    -Dxkbcommon=false

    -Ddbuspolicydir=/usr/share/dbus-1/system.d
    -Ddefault-hierarchy=unified
    -Ddefault-kill-user-processes=false
    -Ddefault-locale='C.UTF-8'
    -Dfallback-hostname='archlinux'
    -Dnologin-path=/usr/bin/nologin
    -Dntp-servers="${_timeservers[*]}"
    -Ddns-servers="${_nameservers[*]}"
    -Drpmmacrosdir=no
    -Dsysvinit-path=
    -Dsysvrcnd-path=
  )

  arch-meson "$_pkgbasename-stable" build "${_meson_options[@]}"

  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,lib32/systemd,share}
}
