# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lib32-freeglut
pkgver=3.6.0
pkgrel=2
pkgdesc="Free OpenGL Utility Toolkit (32-bit)"
url="https://freeglut.sourceforge.net/"
arch=(x86_64)
license=(X11)
depends=(
  freeglut
  lib32-glibc
  lib32-libgl
  lib32-libx11
  lib32-libxi
  lib32-libxrandr
  lib32-libxxf86vm
)
makedepends=(
  cmake
  git
  lib32-glu
  lib32-mesa
  ninja
)
provides=(lib32-glut)
conflicts=(lib32-glut)
source=("git+https://github.com/FreeGLUTProject/freeglut#tag=v$pkgver")
b2sums=('2ce2bc7ff759405be701f1c43e4fe2154fce6a4ffe3801ba1f51ff362698b248b9a3d42929c44825a6f3f67029852a284cad49acbdc14908ac6c651c6c56e571')

prepare() {
  cd freeglut

  # Fix build with CMake 4+
  git cherry-pick -n 2294389397912c9a6505a88221abb7dca0a4fb79

  # Fix build with GCC 15+
  git cherry-pick -n 800772e993a3ceffa01ccf3fca449d3279cde338
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_LIBDIR=lib32
    -D CMAKE_INSTALL_PREFIX=/usr
    -D FREEGLUT_BUILD_STATIC_LIBS=OFF
  )

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  cmake -S freeglut -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir/usr/include"

  ln -s glut.pc "$pkgdir/usr/lib32/pkgconfig/freeglut.pc"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" freeglut/COPYING
}

# vim:set sw=2 sts=-1 et:
