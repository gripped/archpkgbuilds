# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# NOTE:
# The package version must match the one expected by wine.
# Otherwise, wine will complain about a missing mono and will
# ask to download it. Do not update the package to the latest
# version without checking the version required by wine. This
# can be checked here (change wine version accordingly):
# https://gitlab.winehq.org/wine/wine/-/blob/wine-9.12/dlls/appwiz.cpl/addons.c#L59

pkgname=wine-mono
pkgver=10.0.0
pkgrel=1
pkgdesc="Wine's built-in replacement for Microsoft's .NET Framework"
arch=('x86_64')
url='https://gitlab.winehq.org/wine/wine/-/wikis/Wine-Mono'
license=('GPL-2.0-or-later' 'LGPL-2.1-only' 'MPL-1.1')
depends=('wine')
source=("https://dl.winehq.org/wine/${pkgname}/${pkgver}/${pkgname}-${pkgver}-x86.tar.xz")
sha512sums=('e7e5867d34b64a9603fb919280d31cc3ae726fcfd22b4972bbfeda470aaddeb618b50f7d63b0a0ea15fd40f7d5e242c335bd84504a67d4e49b457591741acf5c')

package() {
  install -d -m755 "${pkgdir}/usr/share/wine/mono"
  cp -r -a --no-preserve='ownership' "${pkgname}-${pkgver}" "${pkgdir}/usr/share/wine/mono"
}
