# Maintainer: Campbell Jones <arch at serebit dot com>
# Co-Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>
_pkgname=MangoHud
pkgname=lib32-mangohud
_pkgver=0.6.9-1
_tarpkgver="${_pkgver%-*}"
pkgver=${_pkgver//-/.}
pkgrel=4
pkgdesc="32-bit libraries for MangoHud and MangoApp"
arch=('x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('glfw-x11' 'glslang' 'lib32-cmocka' 'lib32-libglvnd' 'libxnvctrl' 'meson' 'nlohmann-json' 'python-mako')
depends=('lib32-dbus' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-vulkan-icd-loader')
conflicts=('lib32-mangoapp')
replaces=('lib32-mangoapp')
source=("$pkgname-$pkgver.tar.xz::https://github.com/flightlessmango/MangoHud/releases/download/v$_pkgver/$_pkgname-v$_pkgver-Source.tar.xz"
        "3f8f036ee8773ae1af23dd0848b6ab487b5ac7de-reversed.patch")
sha256sums=('30d9336b60cbc7fdc2a1ba86ec62b9fb7f2986a2b0f6196ca347f5c13e583c6d'
            '244ef8fffb27042d521993f9e9894f852d3061a980345e5c8c6a3f148dd26707')

prepare() {
    cd "$_pkgname-v$_tarpkgver"

    # Use system cmocka instead of subproject
    sed -i "s/  cmocka = subproject('cmocka')//g" meson.build
    sed -i "s/cmocka_dep = cmocka.get_variable('cmocka_dep')/cmocka_dep = dependency('cmocka')/g" meson.build

    # Fix build with GCC 13
    patch --forward --reverse --strip=1 --input="$srcdir/3f8f036ee8773ae1af23dd0848b6ab487b5ac7de-reversed.patch"
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
    export LLVM_CONFIG="/usr/bin/llvm-config32"

    arch-meson "$_pkgname-v$_tarpkgver" build \
        -Dmangoapp_layer=true \
        -Dinclude_doc=false \
        --libdir lib32

    meson compile -C build
}

package() {    
    meson install -C build --tags runtime,mangoapp --destdir "$pkgdir"

    install -Dm 0644 "$srcdir/$_pkgname-v$_tarpkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
