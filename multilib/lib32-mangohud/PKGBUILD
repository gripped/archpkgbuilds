# Maintainer: Campbell Jones <arch at serebit dot com>
# Co-Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>
_pkgname=MangoHud
pkgname=lib32-mangohud
pkgver=0.8.1
pkgrel=1
pkgdesc="32-bit libraries for MangoHud and MangoApp"
arch=('x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('git' 'glfw-x11' 'glslang' 'lib32-cmocka' 'lib32-libglvnd' 'libxnvctrl' 'lib32-libxrandr' 'meson' 'nlohmann-json' 'python-mako' 'vulkan-headers')
depends=('lib32-dbus' 'lib32-gcc-libs' 'lib32-glew' 'lib32-libx11' 'lib32-libxkbcommon' 'lib32-vulkan-icd-loader' 'lib32-wayland')
conflicts=('lib32-mangoapp')
replaces=('lib32-mangoapp')
source=("$pkgname-$pkgver.tar.xz::https://github.com/flightlessmango/MangoHud/releases/download/v$pkgver/$_pkgname-v$pkgver-Source.tar.xz")
b2sums=('d817531618d51638adef441c013c02ad0f9b6d5b45d97693e26ce518b3a16c0a096f87add32137b422645993bb0280047f24bec2d998ad4733383008f4f5426b')

prepare() {
    cd "$_pkgname-v$pkgver"

    # Use system cmocka instead of subproject
    sed -i "s/  cmocka = subproject('cmocka')//g" meson.build
    sed -i "s/cmocka_dep = cmocka.get_variable('cmocka_dep')/cmocka_dep = dependency('cmocka')/g" meson.build
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
    export LLVM_CONFIG="/usr/bin/llvm-config32"

    arch-meson "$_pkgname-v$pkgver" build \
        -Dinclude_doc=false \
        --libdir lib32

    meson compile -C build
}

package() {
    meson install -C build --tags runtime,mangoapp --destdir "$pkgdir"

    # mangoplot is provided by mangohud, no option to disable in the build
    rm -rf "$pkgdir/usr/bin"

    install -Dm 0644 "$srcdir/$_pkgname-v$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
