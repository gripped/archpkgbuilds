# Maintainer: Campbell Jones <arch at serebit dot com>
# Contributor: Joshua Ashton <joshua at froggi dot es>

_pkgname=renderdoc
pkgname=lib32-$_pkgname
pkgver=1.30
pkgrel=1
pkgdesc="OpenGL and Vulkan debugging tool (capture-only, 32-bit)"
arch=(x86_64)
url="https://github.com/baldurk/renderdoc"
license=("MIT")
makedepends=("cmake" "python" "ninja")
depends=("lib32-libx11" "lib32-libxcb" "lib32-mesa" "lib32-libgl" "lib32-xcb-util-keysyms")
provides=("lib32-renderdoc-minimal")
conflicts=("lib32-renderdoc-minimal")
replaces=("lib32-renderdoc-minimal")
options=("!lto")
source=("https://github.com/baldurk/$_pkgname/archive/v${pkgver}.tar.gz"
        "https://github.com/baldurk/$_pkgname/releases/download/v${pkgver}/v${pkgver}.tar.gz.asc")
validpgpkeys=('1B039DB9A4718A2D699DE031AC612C3120C34695')
b2sums=('2e9364a8eabca64384e066709c98be8df0ed3c970c173074f9d38eea21d4dc31a50b862818879ccb9f32be8b839643c2d01ddda70d9da8c338c2fc305549eaa9'
        'SKIP')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG="${CARCH}-pc-linux-gnu-pkg-config"

    cmake -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib32 \
        -DLIB_SUFFIX=32 \
        -DVULKAN_JSON_SUFFIX=".32" \
        -DENABLE_RENDERDOCCMD=OFF \
        -DENABLE_QRENDERDOC=OFF \
        -DENABLE_PYRENDERDOC=OFF \
        -DBUILD_VERSION_STABLE=ON \
        -DBUILD_VERSION_DIST_CONTACT="https://archlinux.org/packages/$pkgname" \
        -DBUILD_VERSION_DIST_NAME="Arch" \
        -DBUILD_VERSION_DIST_VER="$pkgver" \
        -DVULKAN_LAYER_FOLDER="/etc/vulkan/implicit_layer.d" \
        -B"${_pkgname}-$pkgver"/build \
        -H"${_pkgname}-$pkgver"
    cmake --build "${srcdir}/renderdoc-${pkgver}"/build
}

package() {
    DESTDIR="$pkgdir" cmake --install "${_pkgname}-$pkgver"/build
    install -Dm644 "${_pkgname}-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

    # Remove these or they conflict with the base renderdoc package.
    rm -r "$pkgdir/usr/share/doc"
    rm -r "$pkgdir/usr/include"
}
