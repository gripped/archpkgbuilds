# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-freetype2
pkgname=(
  lib32-freetype2
)
pkgver=2.14.0
pkgrel=1
epoch=
pkgdesc="Font rasterization library (32-bit)"
url="https://www.freetype.org/"
arch=(x86_64)
license=('FTL OR GPL-2.0-or-later')
depends=(
  freetype2
  lib32-brotli
  lib32-bzip2
  lib32-glibc
  lib32-libpng
  lib32-zlib
)
makedepends=(
  lib32-harfbuzz
  meson
)
source=(
  https://download-mirror.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz{,.sig}
  0001-Enable-table-validation-modules.patch
  0002-Enable-subpixel-rendering.patch
  0003-Enable-long-PCF-family-names.patch
)
b2sums=('70b79290b16bc05e156032dc8a9136bddb21c1f9fc84692959c65f2bc3dc51df681587aee672de9eb393ec77edae05f8e4fdab642a9938dade9eb1186f6d010e'
        'SKIP'
        'f45ec7d03193b446d8b46c8d981f330843a1ab2c83a91a5011cb328b26b4fc4c4b5729f32f3270018cf5ba8a162712bd0ebc2cd67f97b906e46ce293aeda466f'
        'b9481bfe770104b181a59be8cf30c90d329447d3ba04bd7dc641a54057cf2a9024c1a881d096b7ff940e9b467960ff3e08e611686d9a01136523fbb34299d057'
        '1ced8e4ef522b111759953f6b4b38864b1a705e5b6705222282b0e6857a4ee7ac8bdcec35248f53dd8b628fabb8861ee82c4ac957f4c71229bac94d8aaf82984')
validpgpkeys=(
  E30674707856409FF1948010BE6C3AAC63AD8E3F # Werner Lemberg <wl@gnu.org>
)

prepare() {
  patch -d freetype-$pkgver -Np1 < 0001-Enable-table-validation-modules.patch
  patch -d freetype-$pkgver -Np1 < 0002-Enable-subpixel-rendering.patch
  patch -d freetype-$pkgver -Np1 < 0003-Enable-long-PCF-family-names.patch
}

build() {
  local meson_options=(
    --cross-file lib32
    -D default_library=shared
    -D error_strings=true
    -D harfbuzz=dynamic
  )

  arch-meson freetype-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_lib32-freetype2() {
  optdepends=(
    'lib32-harfbuzz: Improved autohinting'
  )
  provides=(
    libfreetype.so
  )

  meson install -C build --destdir="$pkgdir"

  rm -r "$pkgdir"/usr/{include,share}

  install -Dm644 freetype-$pkgver/docs/FTL.TXT \
    -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
