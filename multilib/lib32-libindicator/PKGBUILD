# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Jameson Pugh <imntreal@gmail.com>

_pkgname=libindicator
pkgname=lib32-${_pkgname}
pkgver=12.10.1
pkgrel=11
pkgdesc='Set of symbols and convenience functions for Ayatana indicators (32-bit)'
url='https://launchpad.net/libindicator'
arch=('x86_64')
license=('GPL-3.0-only')
depends=("${_pkgname}-gtk3" 'lib32-gtk3')
makedepends=('glib2-devel' 'python')
conflicts=("lib32-${_pkgname}-gtk3")
provides=("lib32-${_pkgname}-gtk3=$pkgver")
replaces=("lib32-${_pkgname}-gtk3")
source=(https://launchpad.net/${_pkgname}/${pkgver%.*}/${pkgver}/+download/${_pkgname}-${pkgver}.tar.gz{,.asc}
        fix-build.patch)
sha512sums=('d6d77d0309b15cf6b52539323920ab0c1594cb1c1cef8a8d67cd0f76f8ceeeac28eb6db6227563df1932e6f1fadcffac68d82982182b745257dfaf91f1c945af'
            'SKIP'
            'bfb6942ce9d113c0dbe00fb3faed610c5b61f2cb1f30735418b3bea17b2ae7b8f672770e6aa93d7d4b2a2dbe2b26be41f0acb85503a7aea0c72c1da3c2bc1662')
validpgpkeys=('6FC05581A37D71FCECE165DB5BE41E162CD6358E')  # Charles Kerr <charles.kerr@canonical.com>

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -i 's/-Werror//' {libindicator,tools}/Makefile.am
  patch -p1 -i ../fix-build.patch
  autoreconf -fi
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd ${_pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --localstatedir=/var \
    --libexecdir=/usr/lib32/libindicator \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --with-gtk=3 \
    --disable-static \
    --disable-tests
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make -j1 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

# vim: ts=2 sw=2 et:
