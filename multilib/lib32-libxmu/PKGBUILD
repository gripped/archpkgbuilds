# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxmu
pkgname=lib32-$_pkgbasename
pkgver=1.3.1
pkgrel=1
pkgdesc="X11 miscellaneous micro-utility library (32-bit)"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('lib32-libxext' 'lib32-libxt' $_pkgbasename 'lib32-libx11' 'lib32-glibc' 'xorgproto')
makedepends=('xorg-util-macros' 'gcc-multilib')
license=('MIT-open-group AND SMLNJ AND X11 AND ISC')
source=(${url}/releases/individual/lib/libXmu-${pkgver}.tar.xz{,.sig})
sha512sums=('5210acad4ec19d93a2ac906fbb062e69853c8ced8e6d3428586cb737a3485280489f238a54676a286aa0b6698af6406504274ffd08d9b5803cad57cf9476c802'
            'SKIP')
validpgpkeys=('3AB285232C46AE43D8E192F4DAB0F78EA6E7E2D2'  # Alan Coopersmith <alan.coopersmith@oracle.com>
              '3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

build() {
  cd "${srcdir}/libXmu-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXmu-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
