# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Frank Tetzel <frank.tetzel@mailbox.tu-dresden.de>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=zsnes
pkgver=2.0.12
pkgrel=2
pkgdesc='Super Nintendo emulator'
url='https://github.com/xyproto/zsnes'
arch=(x86_64)
license=(GPL-2.0-only)
depends=(lib32-libpng lib32-opengl-driver lib32-sdl lib32-sdl12-compat)
makedepends=(git mesa nasm setconf)
optdepends=('lib32-alsa-lib: sound support for ALSA'
            'lib32-pipewire: sound support for Pipewire'
            'lib32-libpulse: sound support for Pulse Audio')
source=("git+$url#commit=fb8713efa2807dc56b0e3e88bbb09a62ca22bcfb") # tag: 2.0.12
b2sums=('1ff139d259e914d03bb224fc1d91d2c6c9decbb6d0fbf09e7d014b243352570ef402d87b1a590697f1af49281a41b31946d2a56506124bf20aa5d1d561fe3314')

build() {
  export CFLAGS=${CFLAGS/-fcf-protection/}
  export CXXFLAGS=${CXXFLAGS/-fcf-protection/}
  export CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=3/}
  export CXXFLAGS=${CXXFLAGS/-Wp,-D_FORTIFY_SOURCE=3/}
  export CFLAGS+=' -finline-functions -w'
  export CXXFLAGS+=' -finline-functions -w'

  make -C $pkgname
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname install
}
