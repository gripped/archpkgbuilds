# Maintainer:

pkgdesc='Client-side decorations library for Wayland clients'
pkgname=lib32-libdecor
pkgver=0.2.0
pkgrel=1
url="https://gitlab.gnome.org/jadahl/libdecor"
license=(MIT)
arch=(x86_64)
depends=(libdecor lib32-wayland lib32-pango lib32-dbus lib32-gtk3)
makedepends=(cmake ninja meson wayland-protocols)
source=("https://gitlab.gnome.org/libdecor/libdecor/-/archive/${pkgver}/libdecor-${pkgver}.tar.gz")
sha512sums=('27365c9029caed1064c36490c2007afe5af6ef60c0538a06755be200f122caf444c5dba951fcb954095600469466f75eb75f6f1a642acde065723755a7656a3d')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  meson -Ddemo=false build libdecor-$pkgver \
    --prefix=/usr --libdir=/usr/lib32
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -fr "$pkgdir"/usr/include
  install -Dm644 libdecor-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
