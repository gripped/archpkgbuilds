# Maintainer:

pkgdesc='Client-side decorations library for Wayland clients'
pkgname=lib32-libdecor
pkgver=0.2.1
pkgrel=1
url="https://gitlab.gnome.org/jadahl/libdecor"
license=(MIT)
arch=(x86_64)
depends=(libdecor lib32-wayland lib32-pango lib32-dbus lib32-gtk3)
makedepends=(cmake ninja meson wayland-protocols)
source=("https://gitlab.freedesktop.org/libdecor/libdecor/-/archive/${pkgver}/libdecor-${pkgver}.tar.gz")
sha512sums=('eb3d612c967f6c400562e400775cd38fce8ef433087f3cd6a72300aa083e483a8d3e1fd198d7bc1fbb2ca8a045eed5b146e0bc23352e048a9f49e719cf57faa7')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  meson -Ddemo=false build libdecor-$pkgver \
    --prefix=/usr --libdir=/usr/lib32
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -fr "$pkgdir"/usr/include
  install -Dm644 libdecor-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
