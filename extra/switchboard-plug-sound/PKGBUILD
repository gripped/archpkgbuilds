# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sound
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Sound Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sound
license=(LGPL-2.1-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  libcanberra
  libadwaita-1.so
  libgee
  libgranite-7.so
  libpulse
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=06694e6f32fe2b73d9f2002f983ed0ca5b742ffc
source=(git+https://github.com/elementary/switchboard-plug-sound.git#tag=${_tag})
b2sums=('b575e757cbfe361d6a5860404827ac1322b90d82efda29753a82cafd08397057f5dcc8c7bc45b9ab7ed4022030f452217c9ebc4dbd54eb747d6dbfd1d8853f42')

pkgver() {
  cd switchboard-plug-sound
  git describe --tags
}

build() {
  arch-meson switchboard-plug-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
