# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sound
pkgver=8.0.2
pkgrel=1
pkgdesc='Switchboard Sound Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sound
license=(LGPL-2.1-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  libcanberra
  libadwaita-1.so
  libgee
  libgranite-7.so
  libpulse
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=1870054cfea66932cefa05bad5a89e829ac56cbd
source=(git+https://github.com/elementary/switchboard-plug-sound.git#tag=${_tag})
b2sums=('a2d8a6307b69c36178f6ffb2ee0b018999039ada37375f6ba02e0fe9d4b6d4a5147ed9c2df041e7a76c31e4ea6002230d6d550ba7c3d907901c3fd33e3d2ea93')

pkgver() {
  cd switchboard-plug-sound
  git describe --tags
}

build() {
  arch-meson switchboard-plug-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
