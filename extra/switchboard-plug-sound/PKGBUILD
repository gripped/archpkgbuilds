# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sound
pkgver=8.0.1
pkgrel=1
pkgdesc='Switchboard Sound Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sound
license=(LGPL-2.1-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  libcanberra
  libadwaita-1.so
  libgee
  libgranite-7.so
  libpulse
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=e5d18962a00b956f339a82e4be635f5f02fb35c3
source=(git+https://github.com/elementary/switchboard-plug-sound.git#tag=${_tag})
b2sums=('08340618b41c34d017f80c46e77cb8b70d640305f5223439539ece11c5b354e3e28eda4493e9f4d3a8d1ca921581bce39b1ef4efc3c2c5462ec3b28697d5f1a1')

pkgver() {
  cd switchboard-plug-sound
  git describe --tags
}

build() {
  arch-meson switchboard-plug-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
