# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Contributor: Carlos Aznarán <caznaranl@uni.pe>
# Contributor: Michel Zou <xantares09@hotmail.com>

_name=multiprocess
pkgname=python-$_name
pkgver=0.70.18
pkgrel=1
pkgdesc="Better multiprocessing and multithreading in Python"
url="https://github.com/uqfoundation/multiprocess"
arch=(any)
license=(BSD-3-Clause)
depends=(
  python
  python-dill
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-tests
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('4604607f74b1a0d999ba9ecfd62c533cc003e4035bc9ad9639a33738060deee12289b53011afe0ee93a7f510fd80f10fdb316e1605db6fd53ac675fe5fe4e28a')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  local _pyversion=$(python -c "import sys; print(f'{sys.version_info.major}.{sys.version_info.minor}')")
  test-env/bin/python -m pytest py${_pyversion}/${_name}/tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -Dm 644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
