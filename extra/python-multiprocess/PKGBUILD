# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Contributor: Carlos Aznarán <caznaranl@uni.pe>
# Contributor: Michel Zou <xantares09@hotmail.com>

_name=multiprocess
pkgname=python-$_name
pkgver=0.70.19
pkgrel=1
pkgdesc="Better multiprocessing and multithreading in Python"
url="https://github.com/uqfoundation/multiprocess"
arch=(any)
license=(BSD-3-Clause)
depends=(
  python
  python-dill
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-tests
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('cb7b4e8c1223be11126073199c6126bc964f637262f2d8f62c0a4588e56800e574cf6440fa3b83070c367deb58e53daa946e52ba02fdd5b13c7c1a0d74394187')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  local _pyversion=$(python -c "import sys; print(f'{sys.version_info.major}.{sys.version_info.minor}')")
  test-env/bin/python -m pytest py${_pyversion}/${_name}/tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -Dm 644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
