# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-hlint-plugin
pkgname=haskell-hls-hlint-plugin
pkgver=2.1.0.0
pkgrel=77
pkgdesc="Hlint integration plugin with Haskell Language Server"
url="https://hackage.haskell.org/package/hls-hlint-plugin"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-aeson' 'haskell-apply-refact' 'haskell-data-default'
         'haskell-extra' 'haskell-ghc' 'haskell-ghc-exactprint' 'haskell-ghc-lib-parser-ex'
         'haskell-ghcide' 'haskell-hashable' 'hlint' 'haskell-hls-plugin-api' 'haskell-hslogger'
         'haskell-lens' 'haskell-lsp' 'haskell-refact' 'haskell-regex-tdfa' 'haskell-temporary'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils' 'haskell-lsp-types' 'haskell-row-types')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('6ad94abee0617401ac4222d0a87b85df90bbf592d062c81ad767f9dd50fa69acbe514a776a168661bcbbf7efe274978af16d1449809002c6d69edf29af30b35b')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u Diff -r ghc-lib-parser:ghc,ghc-boot
  sed -e 's/"ghc-lib-parser" //' -e 's/ghc_lib_parser/ghc/' -i src/Ide/Plugin/Hlint.hs
  # sed -i '/HLINT_ON_GHC_LIB/d' $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
