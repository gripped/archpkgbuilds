# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Andrew Steinke <rkcf@rkcf.me>

pkgname=python-lxc
_pkgname=python3-lxc
pkgver=5.0.0
pkgrel=6
pkgdesc="Python3 bindings for LXC"
url="https://github.com/lxc/python3-lxc"
arch=('x86_64')
license=('LGPL-2.1-only')
depends=('python' 'lxc' 'glibc')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools' 'python-poetry-core')
source=("${url}/archive/${_pkgname}-${pkgver}.tar.gz"
        'replace_PyOS_AfterFork_function.patch::https://github.com/lxc/python3-lxc/commit/018c4ed482cfccbb89e39fe0dbaad9fe98ee5dd4.patch?full_index=1')
sha512sums=('167ee953382e4cf4f97f21f65548ffd302b21daa46587e4ba5a66fd42a9a8c9238e2ede1b0a4c2fc0b89404c46cb4f7bd207142b8e61389cbd3363368c4fd4bb'
            '29f6e80799ee6da6abd7ddd078ecd5fa45ef4bd8ad237637c2916e89cbd3ccdf627985f2f8a8dedccf9b4edd88d353cfe5a4228011d5bd8fb431be18ca9151cb')

prepare() {
	cd "${_pkgname}-${_pkgname}-${pkgver}"
	# https://github.com/lxc/python3-lxc/issues/35
	# https://github.com/lxc/python3-lxc/commit/018c4ed482cfccbb89e39fe0dbaad9fe98ee5dd4
	patch -Np1 -i "${srcdir}/replace_PyOS_AfterFork_function.patch"
}

build() {
	cd "${_pkgname}-${_pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_pkgname}-${_pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl
}

