# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-default-settings
pkgver=8.0.1
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(any)
url=https://github.com/elementary/default-settings.git
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  dconf
  polkit
)
makedepends=(
  git
  meson
)
_tag=8f95f465b674fe3723d53951c3c3a94b1a56f604
source=(pantheon-default-settings::git+https://github.com/elementary/default-settings.git#tag=${_tag})
b2sums=('f41d91c26d0a0c074d623e0c9b65f27190d7f5ef384c00be47a965ad646c5830d50400ee2f6984835d46c39cbed6190039a21f9ef82a2b89075fffd8f4940f4c')

pkgver() {
  cd pantheon-default-settings
  git describe --tags
}

build() {
  arch-meson pantheon-default-settings build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm "${pkgdir}"/usr/share/cups/data/default-testpage.pdf
}

# vim: ts=2 sw=2 et:
