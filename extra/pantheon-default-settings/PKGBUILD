# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-default-settings
pkgver=8.0.2
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(any)
url=https://github.com/elementary/default-settings.git
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  dconf
  polkit
)
makedepends=(
  git
  meson
)
_tag=72a3cfcbc62e7172e935f4332e683b6acb43282f
source=(pantheon-default-settings::git+https://github.com/elementary/default-settings.git#tag=${_tag})
b2sums=('9bf1ba918e8aefb3ddbf3d554f260f89b6f5ca47d98f9b93c05a8579faa4da6f949d7b886ae005e32377c73f99884d93446869ea86de1752684470f78ef0eb92')

pkgver() {
  cd pantheon-default-settings
  git describe --tags
}

build() {
  arch-meson pantheon-default-settings build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm "${pkgdir}"/usr/share/cups/data/default-testpage.pdf
}

# vim: ts=2 sw=2 et:
