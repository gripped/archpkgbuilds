# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-default-settings
pkgver=8.1.0
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(any)
url=https://github.com/elementary/default-settings.git
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  dconf
  polkit
)
makedepends=(
  git
  meson
)
_tag=65201f8e2064b65a6ea2421f74d93d04e47bd685
source=(pantheon-default-settings::git+https://github.com/elementary/default-settings.git#tag=${_tag})
b2sums=('55b4c7f63d1e2f2e3dda049cf14009acd76dced3381760ea9b1190542a90195eedce2556c3c100e8f8537882667c2fd64ae811a82fa40a6822f7210411033766')

pkgver() {
  cd pantheon-default-settings
  git describe --tags
}

build() {
  arch-meson pantheon-default-settings build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm "${pkgdir}"/usr/share/cups/data/default-testpage.pdf
}

# vim: ts=2 sw=2 et:
