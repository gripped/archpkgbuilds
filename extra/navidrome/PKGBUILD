# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thorben GÃ¼nther <echo YWRtaW5AeGVucm94Lm5ldAo= | base64 -d>

pkgname=navidrome
pkgver=0.55.0
pkgrel=1
pkgdesc='A web-based music collection server and streamer'
arch=('x86_64')
url='https://www.navidrome.org'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'gcc-libs'
  'zlib'
  'ffmpeg'
  'taglib'
)
makedepends=(
  'git'
  'go'
  'npm'
  'nodejs'
)
backup=('etc/navidrome/navidrome.toml')
options=('!lto')
source=(
  "$pkgname::git+https://github.com/navidrome/navidrome#tag=v$pkgver"
  'systemd.service'
  'sysusers.conf'
  'tmpfiles.conf'
  'config.toml'
)
sha512sums=('0676f614f844147b40134467a688d9cc7e11c17fd6926cae31a2957190b71624d9cbf5bf7a94cef967055e7a276bf44258751a9d12cac0052589854cee8af56c'
            '6b9d0c7f9827890cd90c740fd1bfcfed2f058b0544884e177dbd3c3f6006f6b629cba42be25474e37451873d854f5792697c130ab9d2756d18bf2feb520fb856'
            '73bd6fa8cb54666dfbd14af1e290301d3393f98f54f94fa554ec24c1ff60f1055c2620a44a8d672e5ddfb5ace6825440e4912ef8e64d9c3fa285ecfb08d7a705'
            '7cc7033219d87982e3f60216a68bedd5232ce8e1612cc4fea87fa8057ad5007014b11ecdabb01b0ee33541c0bd2690ec00a6657c05944dc1d8813440c99f5aff'
            '96caf0c526b63d055ba64dfdb0236910c4740e05c97717b665c4c016f8ca8e96448cffa29067e558b0089a0adb8b535c35ede42ecbe1f8b787e83570efdad73d')
b2sums=('f96d8f82a4a12073b3fd0c220bfbc31ef6f635e50f62f706c03f63927fca7a41e2c18295bfda8bd60ec9e217830554f78c34bc982ec21979b4294dd0e3dfe750'
        'e274f0c9ac104981b2e8b2a43143e680f417e0cc22135bbe527cd66151e8fea24cd00187f4b404c641f7eade671ebe635666cc1c2a693f1a53588545585df8b0'
        'd313232bf7baab8c24985a436cc7b445f98ffbf9ba424cc379e9cde789c67f29cc21ff8ec848adc9a59083798410aadad37b35ce8c8613fc51ae1eaccbaae11b'
        'f0b2c04e069801cf6c30b9d9116cf8470d52b50aa18497a449aff5bd379e3f95369a287f207c347aa5cb75f22cc682b5d1ca9717b333fba8662563806508279c'
        '3fdae76022dfc81d0d2fb2f975cca128fe018bd730a1acbf7ff5cfbd0faa3a147e8306520951a954b60f81c68982fc5518e6e1ed3c51bf328f5eeba7b271b6be')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # frontend
  pushd ui
  npm install
  npm run build
  popd

  # backend

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/navidrome/navidrome/consts.gitSha=$(git rev-parse HEAD) \
    -X github.com/navidrome/navidrome/consts.gitTag=$pkgver" \
    -tags=netgo \
    -o build \
    .
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # configuration
  install -vDm644 config.toml "$pkgdir/etc/$pkgname/$pkgname.toml"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "$pkgname/build/$pkgname"
}
