# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: acieroid
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: William J. Bowman <bluephoenix47@gmail.com>

pkgbase=rocq
pkgname=(
  rocq
  rocqide
)
pkgver=9.1.0
pkgrel=2
pkgdesc="Interactive theorem prover, or proof assistant"
arch=('x86_64')
url="https://rocq-prover.org/"
license=('LGPL-2.1-or-later')
makedepends=(
  'cairo'
  'dune'
  'fontconfig'
  'freetype2'
  'gdk-pixbuf2'
  'glib2'
  'glibc'
  'gmp'
  'gtk3'
  'gtksourceview3'
  'lablgtk3'
  'ocaml'
  'ocaml-findlib'
  'ocaml-zarith'
  'pango'
)
options=(
  '!debug'
  'staticlibs'
)
source=(
  "https://github.com/rocq-prover/rocq/archive/V$pkgver/$pkgbase-$pkgver.tar.gz"
  "rocqide.desktop"
)
b2sums=('3dc0cc1584f3c042324ec85eb4b5afd236a9f8391b059d6fc163dea846496bade7e7d2894795d348d44882409d30cb70a7120b47c0b3a1a7e1dc18c048a6f30e'
        'd0c410885cfeeb1e2339e0b5df54bd15b21b07e6cb57b579821e01d5e9ec50db5563fa9abfe1c87539131a0a7129c1a4abd8a055c60ef540ce8411c52c809627')

build() {
  cd $pkgbase-$pkgver
  make dunestrap
  dune build -p rocq-runtime,coq-core,rocq-core,coqide-server,rocqide
}

package_rocq() {
  depends=(
    'glibc'
    'gmp'
    'ocaml'
    'ocaml-findlib'
  )
  replaces=('coq')
  provides=('coq')
  conflicts=('coq')

  cd $pkgbase-$pkgver
  dune install rocq-runtime coq-core rocq-core \
    --prefix=/usr \
    --destdir="$pkgdir" \
    --mandir=/usr/share/man \
    --docdir=/usr/share/doc
}

package_rocqide() {
  pkgdesc+=" (GTK-based GUI)"
  depends=(
    'cairo'
    'fontconfig'
    'freetype2'
    'gdk-pixbuf2'
    'glib2'
    'glibc'
    'gmp'
    'gtk3'
    'gtksourceview3'
    'pango'
    'rocq'
  )
  replaces=('coqide')
  provides=('coqide')
  conflicts=('coqide')

  cd $pkgbase-$pkgver
  dune install coqide-server rocqide \
    --prefix=/usr \
    --destdir="$pkgdir" \
    --mandir=/usr/share/man \
    --docdir=/usr/share/doc

  install -vDm644 -t "$pkgdir/usr/share/applications" ../"$pkgname.desktop"
  install -vDm644 ide/rocqide/coq.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
