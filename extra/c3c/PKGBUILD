# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Eduardo Jose Gomez Hernandez <eduardo@edujgh.net>

pkgname=c3c
pkgver=0.6.1
pkgrel=2
pkgdesc='Compiler for the C3 language'
arch=(x86_64)
url='https://github.com/c3lang/c3c'
license=(LGPL-3.0-or-later MIT)
depends=(lld llvm-libs)
makedepends=(clang cmake git libedit llvm ninja)
source=("git+$url#commit=bdc9f339c9088099de74dafdb87122dc769a4b32"
        CMakeLists.txt)
b2sums=('9a6b1155fceb691abd3294640f515fe9b0ce3e14bb2115b13b9a0a27a139d38ba3e0ece824a45a9673d8e57dc8f2ef97b21c0ce66d5e6576f8d9166281673637'
        '93abca74e2431efee27aa3a9441a86ebad74c0bfaa431393b052daf12fb48f45e4cfefba267579c222d886b81c791fb37930eef7edf381c3af5da47bad1e6418')

prepare() {
  ln -sf "$srcdir/CMakeLists.txt" $pkgname/CMakeLists.txt
}

build() {
  cd $pkgname
  cmake -B build -D CMAKE_BUILD_TYPE=Release -D CMAKE_BUILD_WITH_INSTALL_RPATH=ON -G Ninja
  ninja -C build
}

package() {
  cd $pkgname
  install -Dm755 build/c3c "$pkgdir/usr/lib/$pkgname/c3c"
  cp -r lib "$pkgdir/usr/lib/$pkgname/lib"
  install -d "$pkgdir/usr/bin"
  ln -s "/usr/lib/$pkgname/c3c" "$pkgdir/usr/bin/c3c"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 LICENSE_STDLIB "$pkgdir/usr/share/licenses/$pkgname/LICENSE_STDLIB"
}
