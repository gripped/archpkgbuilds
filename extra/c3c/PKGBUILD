# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Eduardo Jose Gomez Hernandez <eduardo@edujgh.net>

pkgname=c3c
pkgver=0.7.7
pkgrel=1
pkgdesc='Compiler for the C3 language'
arch=(x86_64)
url='https://github.com/c3lang/c3c'
license=(LGPL-3.0-or-later MIT)
depends=(curl lld llvm-libs)
makedepends=(clang cmake git libedit llvm)
source=("git+$url#tag=v$pkgver")
b2sums=('e126cc3360039e1b7674aa7c94d7cf7b6ffeacd3ed018f46a73a5521009c5e1559175a87b4616b9508f9a27d6b7548e51b01c7675d7567a66c713cda1c296296')

prepare() {
  cd $pkgname
  # Pretend that git is missing and define the git hash manually
  sed -i '/find_package(Git /d' CMakeLists.txt git_hash.cmake
  sed -i "s/unknown/$(git rev-parse HEAD)/g" git_hash.cmake
}

build() {
  cmake \
    -B build \
    -D C3_LINK_DYNAMIC=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS_RELEASE="$CFLAGS" \
    -D CMAKE_CXX_FLAGS_RELEASE="$CXXFLAGS" \
    -S $pkgname
  make -C build
}

package() {
  install -Dm755 build/c3c "$pkgdir/usr/lib/$pkgname/c3c"
  cp -r build/lib "$pkgdir/usr/lib/$pkgname/lib"
  install -d "$pkgdir/usr/bin"
  ln -s "/usr/lib/$pkgname/c3c" "$pkgdir/usr/bin/c3c"
  cd $pkgname
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 LICENSE_SRC -t "$pkgdir/usr/share/licenses/$pkgname"
}
