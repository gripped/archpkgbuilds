# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Eduardo Jose Gomez Hernandez <eduardo@edujgh.net>

pkgname=c3c
pkgver=0.7.3
pkgrel=1
pkgdesc='Compiler for the C3 language'
arch=(x86_64)
url='https://github.com/c3lang/c3c'
license=(LGPL-3.0-or-later MIT)
depends=(curl lld llvm-libs)
makedepends=(clang cmake git libedit llvm)
source=("git+$url#tag=v$pkgver")
b2sums=('efb20550cde5998756c7316f6c2f31ff3631836a65bb09b31353ea3da4c45064eab9cda009d8ba233422fb8d2e8df414e857562845f897f2acab80103e065a12')

prepare() {
  cd $pkgname
  # Pretend that git is missing and define the git hash manually
  sed -i '/find_package(Git /d' CMakeLists.txt git_hash.cmake
  sed -i "s/unknown/$(git rev-parse HEAD)/g" git_hash.cmake
}

build() {
  cmake \
    -B build \
    -D C3_LINK_DYNAMIC=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_CXX_FLAGS_RELEASE="$CXXFLAGS" \
    -D CMAKE_C_FLAGS_RELEASE="$CFLAGS" \
    -S $pkgname
  make -C build
}

package() {
  install -Dm755 build/c3c "$pkgdir/usr/lib/$pkgname/c3c"
  cp -r build/lib "$pkgdir/usr/lib/$pkgname/lib"
  install -d "$pkgdir/usr/bin"
  ln -s "/usr/lib/$pkgname/c3c" "$pkgdir/usr/bin/c3c"
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 $pkgname/LICENSE_STDLIB "$pkgdir/usr/share/licenses/$pkgname/LICENSE_STDLIB"
}
