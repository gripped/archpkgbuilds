# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Eduardo Jose Gomez Hernandez <eduardo@edujgh.net>

pkgname=c3c
pkgver=0.7.9
pkgrel=1
pkgdesc='Compiler for the C3 language'
arch=(x86_64)
url='https://github.com/c3lang/c3c'
license=(LGPL-3.0-or-later MIT)
depends=(curl lld llvm-libs)
makedepends=(clang cmake git libedit llvm)
source=("git+$url#tag=v$pkgver")
b2sums=('22c8db754bbafab25a7bb2b9053950850a1ee353be124ed9e10999ecaac5bcc1b0ea8a66dd6d91e09b8d7b58102a92dc0f34cea31a66a639ad628171af5ae03d')

prepare() {
  cd $pkgname
  # Pretend that git is missing and define the git hash manually
  sed -i '/find_package(Git /d' CMakeLists.txt git_hash.cmake
  sed -i "s/unknown/$(git rev-parse HEAD)/g" git_hash.cmake
}

build() {
  cmake \
    -B build \
    -D C3_LINK_DYNAMIC=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS_RELEASE="$CFLAGS" \
    -D CMAKE_CXX_FLAGS_RELEASE="$CXXFLAGS" \
    -S $pkgname
  make -C build
}

package() {
  install -Dm755 build/c3c "$pkgdir/usr/lib/$pkgname/c3c"
  cp -r build/lib "$pkgdir/usr/lib/$pkgname/lib"
  install -d "$pkgdir/usr/bin"
  ln -s "/usr/lib/$pkgname/c3c" "$pkgdir/usr/bin/c3c"
  cd $pkgname
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 LICENSE_SRC -t "$pkgdir/usr/share/licenses/$pkgname"
}
