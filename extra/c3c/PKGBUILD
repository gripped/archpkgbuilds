# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Eduardo Jose Gomez Hernandez <eduardo@edujgh.net>

pkgname=c3c
pkgver=0.6.4
pkgrel=1
pkgdesc='Compiler for the C3 language'
arch=(x86_64)
url='https://github.com/c3lang/c3c'
license=(LGPL-3.0-or-later MIT)
depends=(lld llvm-libs)
makedepends=(clang cmake git libedit llvm)
source=("git+$url#commit=528fecef4d0b7b5aff7529c58f8367d19a08bddc"
        CMakeLists.txt)
b2sums=('553c05f8bcb0c268e3bd3e74cf9fde0ac1807863c1c21406f02b3143698c0da058b33a33128fc1688e88ccaff38799b7976c05ed058325edca15a6b1edc7ee6d'
        'e034d2a4dc01d88307ac84067f9a810b6bbed296f4ee0f29722d9f2ff7d34d38f1e7dd5bae41f3d08fa9ef00661d4f09521d2fa1ca3e0f639d419b1ab545b6d4')

prepare() {
  ln -f -s "$srcdir/CMakeLists.txt" $pkgname/CMakeLists.txt
}

build() {
  cmake \
    -B build \
    -D C3_LINK_DYNAMIC=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_CXX_FLAGS_RELEASE="$CXXFLAGS" \
    -D CMAKE_C_FLAGS_RELEASE="$CFLAGS" \
    -S $pkgname
  make -C build
}

package() {
  install -Dm755 build/c3c "$pkgdir/usr/lib/$pkgname/c3c"
  cp -r build/lib "$pkgdir/usr/lib/$pkgname/lib"
  install -d "$pkgdir/usr/bin"
  ln -s "/usr/lib/$pkgname/c3c" "$pkgdir/usr/bin/c3c"
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 $pkgname/LICENSE_STDLIB "$pkgdir/usr/share/licenses/$pkgname/LICENSE_STDLIB"
}
