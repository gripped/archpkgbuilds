# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libbraiding
pkgver=1.3.1
pkgrel=1
pkgdesc='Library to compute several properties of braids, including centralizer and conjugacy check'
arch=(x86_64)
url='https://github.com/miguelmarco/libbraiding'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         glibc)
makedepends=(git)
source=(git+https://github.com/miguelmarco/libbraiding#tag=$pkgver)
sha256sums=('d458b3c44044887543f182f7f2f948346db5bcbd3dff551aa3cc488629787094')

prepare() {
  cd $pkgname
  sed -e '/^s/d' -i configure.ac # Fix build
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
