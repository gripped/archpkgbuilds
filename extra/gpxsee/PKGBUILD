# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Raffaele Zamorano
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Nikolay Korotkiy <sikmir@gmail.com>

pkgname=gpxsee
_pkgname=GPXSee
pkgver=14.0
pkgrel=1
pkgdesc='GPS log file viewer and analyzer'
arch=(x86_64)
url='https://www.gpxsee.org/'
license=(GPL-3.0-only)
depends=('qt6-imageformats' 'qt6-positioning' 'qt6-svg'
         'qt6-serialport' 'hicolor-icon-theme')
makedepends=('qt6-tools')
optdepends=('qtpbfimageplugin-qt6: Mapbox vector tiles (MVT) files support')
source=(https://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('4b1ae41c98af7e74186fe0eeb087256b9f2df16b9a8221ef9a33b043df7e1044')

build() {
  cd $_pkgname-$pkgver
  /usr/lib/qt6/bin/lrelease gpxsee.pro
  qmake6 gpxsee.pro PREFIX=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
