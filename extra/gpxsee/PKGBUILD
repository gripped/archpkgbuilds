# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Raffaele Zamorano
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Nikolay Korotkiy <sikmir@gmail.com>
# Contributor: Martin Tůma <tumic@gpxsee.org>

pkgname=gpxsee
_pkgname=GPXSee
pkgver=15.7
pkgrel=1
pkgdesc='GPS log file viewer and analyzer'
arch=(x86_64)
url='https://www.gpxsee.org/'
license=(GPL-3.0-only)
depends=('qt6-imageformats' 'qt6-positioning' 'qt6-svg'
         'qt6-serialport' 'hicolor-icon-theme')
makedepends=('qt6-tools')
source=(https://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('e36f8128a726e6921daea205a296929d5f5baf8b78c10e970d2ca8a8bf4dddab')

build() {
  cd $_pkgname-$pkgver
  /usr/lib/qt6/bin/lrelease gpxsee.pro
  qmake6 gpxsee.pro PREFIX=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
