# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=devede
_pkgname=devedeng
pkgver=4.21.0
pkgrel=2
pkgdesc='Program to create Video DVDs and CDs'
arch=('any')
url='https://rastersoft.com/programas/devede.html'
license=('GPL-3.0-only')
depends=(
  'cdrtools'
  'dvdauthor'
  'ffmpeg'
  'gdk-pixbuf2'
  'glib2'
  'gtk3'
  'hicolor-icon-theme'
  'python'
  'python-cairo'
  'python-gobject'
  'python-setuptools'
  'vcdimager'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-gettext'
  'python-wheel'
)
source=("git+https://gitlab.com/rastersoft/$_pkgname.git#tag=$pkgver")
b2sums=('a161641b7814f90f32b8ea103f7faa6b109487132bb2d9a07d6deae80f887548db8101a94a2a1ca90d9e971a133d4b0a616465c9d6dab46ccc804d8fd755e6f4')

prepare() {
  cd $_pkgname
  sed -i "/share', 'pixmaps/d;s|/usr', 'share|share|" setup.py
}

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
