# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>
# Contributor: Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>

pkgname=cgit
pkgver=1.3
pkgrel=1
pkgdesc='A web interface for git written in plain C'
arch=('x86_64')
url='https://git.zx2c4.com/cgit/'
license=('GPL-2.0-only')
depends=('glibc'
         'luajit' #'libluajit-5.1.so'
         'zlib-ng' 'libz-ng.so')
makedepends=('git' 'curl' 'asciidoc')
optdepends=('groff: about page using man page syntax'
            'python-pygments: syntax highlighting support'
            'python-docutils: about page formatted with reStructuredText'
            'python-markdown: about page formatted with markdown'
            'lua51-luaossl: for lua filters'
            'gzip: gzip compressed snapshots'
            'bzip2: bzip2 compressed snapshots'
            'lzip: lzip compressed snapshots'
            'xz: xz compressed snapshots'
            'zstd: zstd compressed snapshots'
            'mime-types: serve file with correct content-type header')
install=cgit.install
validpgpkeys=('AB9942E6D4A4CFC3412620A749FC7012A5DE03AE')
source=("git+https://git.zx2c4.com/cgit.git#tag=v${pkgver}"
        'git+https://github.com/git/git.git'
        'cgit.tmpfiles'
        'apache.example.conf')
sha256sums=('a285188cb3b9cda138e418ff07dddecfc0302d13418a9193c6d64548f981d3be'
            'SKIP'
            '4004b72d433e5810b046fc6019171a11a0dae3e9c6b29a44f16ed41705c46c3d'
            '89927d462c0504863c163eb8a210e5d65db30ee6e4300ff6a2f92460e4f20a62')

prepare() {
  cd cgit/

  git config --file=.gitmodules submodule.git.url ../git/
  git submodule init
  git -c protocol.file.allow=always submodule update
}

build() {
  cd cgit/

  ZLIB_NG=1
  export ZLIB_NG

  make \
    all doc-man
}

check() {
  cd cgit/

  make test
}

package() {
  cd cgit/

  make \
    CGIT_SCRIPT_PATH="/usr/share/webapps/${pkgname}" \
    DESTDIR="${pkgdir}" \
    prefix='/usr' \
    filterdir="/usr/lib/${pkgname}/filters" \
    install install-man

  install -vDm0644 "${srcdir}/cgit.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -vDm0644 "${srcdir}/apache.example.conf" "${pkgdir}/etc/webapps/${pkgname}/apache.example.conf"
  install -d "${pkgdir}/usr/lib/${pkgname}"
  mv "${pkgdir}/usr/share/webapps/${pkgname}/cgit.cgi" "${pkgdir}/usr/lib/${pkgname}"
  ln -sf ../../../lib/${pkgname}/cgit.cgi "${pkgdir}/usr/share/webapps/${pkgname}/cgit.cgi"
}
