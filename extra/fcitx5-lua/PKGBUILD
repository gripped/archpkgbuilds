# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-lua
pkgver=5.0.15
pkgrel=1
pkgdesc="Lua support for Fcitx 5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-lua"
license=('GPL')
depends=('fcitx5' 'lua')
makedepends=('git' 'extra-cmake-modules' 'ninja')
source=("git+https://github.com/fcitx/fcitx5-lua.git#tag=$pkgver?signed")
sha512sums=('604c645a14f54e515a865bc976572fe48218edf2c54a7b6d30f21894531a38f676026e12e180a122ef31c6f929616e7bed9cb62a176c6ea5ab32ed0792154090')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname
  ninja test
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
