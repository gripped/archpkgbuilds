# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=vec
pkgname=haskell-vec
pkgver=0.5.1
pkgrel=15
pkgdesc="Vec: length-indexed (sized) list"
url="https://github.com/phadej/vec"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-adjunctions' 'haskell-boring'
         'haskell-distributive' 'haskell-fin' 'haskell-hashable' 'haskell-indexed-traversable'
         'haskell-semigroupoids')
makedepends=('ghc' 'uusi' 'haskell-inspection-testing' 'haskell-tagged')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('a8d97752f8307e4788fdfd92db122f7b4672883814ad2fb9c5fb57a4084dbcd92eee9c22481962d35f03fa29a6df566e97c51ac4ac77f4773f764ef8437fc046')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u inspection-testing
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
