# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=listenbrainz-mpd
pkgver=2.3.9
pkgrel=2
pkgdesc="A ListenBrainz submission client for MPD"
arch=(x86_64)
url="https://codeberg.org/elomatreb/listenbrainz-mpd"
license=(AGPL-3.0-only)
depends=(
  gcc-libs
  glibc
  mpd
  openssl
  sqlite
)
makedepends=(
  asciidoctor
  rust
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('5344baf8cb6145a582b011ecf19cbc3b8553302cf01dd688ade805c0fe23777657140d94ae55797308c2c756fbd80dc7508e686a179d80693cfbfce0e802cba8')
b2sums=('acfdac0341e923400484d8242b686c0f94f38e341ddda3483e8fb8f40a62a81a99dbae41f664c892e06951d511135467ee9de89b06059e12a1e78bcc78c77745')

prepare() {
  cd $pkgname
  cargo fetch --locked --target host-tuple
}

build() {
  cd $pkgname
  cargo build --frozen --release --features=shell_completion,systemd
  asciidoctor --backend=manpage $pkgname.adoc
}

chetck() {
  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname.service -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 $pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 {{CHANGELOG,README}.md,config.toml.sample} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 generated_completions/$pkgname.bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm 644 generated_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 generated_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/AGPL-3.0-only.txt"
}
