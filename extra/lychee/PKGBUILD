# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=lychee
pkgver=0.19.0
pkgrel=1
pkgdesc='Fast, async, resource-friendly link checker written in Rust'
arch=('x86_64')
url=https://lychee.cli.rs
license=('Apache-2.0 OR MIT')
depends=(
  'gcc-libs'
  'openssl'
)
makedepends=(
  'git'
  'rust'
)
checkdepends=('cargo-nextest')
conflicts=(
  'lychee-link-checker'
  'lychee-rs'
)
replaces=(
  'lychee-link-checker'
  'lychee-rs'
)
options=('!lto')
source=("git+https://github.com/lycheeverse/lychee.git#tag=lychee-v$pkgver")
b2sums=('bd5cde748ddedb0dcb5a11e427a6b85dde6e013d397ec71f0bf7abc8a26f93a1b07945e3033185611abdc697a27b68d90dea0e71da4e1d07c7bc6ed6ba2b1dee')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --frozen
}

check() {
  cd $pkgname
  # Avoid vendoring openssl, which is enabled by vendored-openssl feature
  export OPENSSL_NO_VENDOR=1
  test_expr="(!test(test_exclude_example_domains) and !test(test_detailed_json_output_on_error) and !test(test_skip_cache_unknown_status_code))"
  cargo nextest run --all-targets --all-features -E "$test_expr" --test-threads 1
  cargo nextest run --filter-expr 'test(test_exclude_example_domains)' --test-threads 1
  cargo test --doc
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
