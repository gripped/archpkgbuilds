# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Severen Redwood <me@severen.dev>
# Contributor: Tomasz Jakub Rup <tomasz.rup@gmail.com>

pkgname=pnpm
pkgver=9.1.3
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=('any')
url=https://pnpm.io
license=('MIT')
depends=('node-gyp')
makedepends=('git' 'pnpm')
source=("git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver?signed")
b2sums=('e68a77618f553380f03ea37ae63c6b9fd254e8fd6d25a65f137fa135bc94637221466b2b94bf068ffded289e207ad58c2d30c6480cc7c839c9f13b55c5ae35e3')
validpgpkeys=('7B74D1299568B586BA9962B5649E4D4AF74E7DEC') # Zoltan Kochan <z@kochan.io>

prepare() {
  cd $pkgname/$pkgname
  # https://github.com/typicode/husky/issues/1444
  export UV_USE_IO_URING=0
  pnpm install --frozen-lockfile
}

build() {
  cd $pkgname/$pkgname
  pnpm run compile
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$mod_dir/dist}
  ln -s $mod_dir/bin/$pkgname.cjs "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/bin/pnpx.cjs "$pkgdir"/usr/bin/pnpx

  cd $pkgname/$pkgname
  cp -r bin package.json "$pkgdir"/$mod_dir
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
  cd dist
  cp -r $pkgname.cjs pnpmrc templates worker.js "$pkgdir"/$mod_dir/dist
}
