# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Severen Redwood <me@severen.dev>
# Contributor: Tomasz Jakub Rup <tomasz.rup@gmail.com>

pkgname=pnpm
pkgver=8.6.9
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=('any')
url=https://pnpm.io
license=('MIT')
depends=("nodejs")
makedepends=('pnpm')
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('5ce717f620096fa607e674c32116c07725336e9cc351718e74df62de0fdafffe591958a5dee00087d53e3cac12038982d7a9b4679cdc578a5f0b6ce28099bbde')

build() {
  cd $pkgname-$pkgver/$pkgname
  pnpm install --frozen-lockfile
  pnpm run compile
}

package() {
  local _npmdir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$_npmdir/dist}
  ln -s $_npmdir/bin/$pkgname.cjs "$pkgdir"/usr/bin/$pkgname
  ln -s $_npmdir/bin/pnpx.cjs "$pkgdir"/usr/bin/pnpx

  cd $pkgname-$pkgver/$pkgname
  cp -r bin package.json "$pkgdir"/$_npmdir
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
  cd dist
  cp -r $pkgname.cjs pnpmrc scripts "$pkgdir"/$_npmdir/dist
}
