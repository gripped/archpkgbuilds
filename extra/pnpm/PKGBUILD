# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Severen Redwood <me@severen.dev>
# Contributor: Tomasz Jakub Rup <tomasz.rup@gmail.com>

pkgname=pnpm
pkgver=8.7.0
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=('any')
url=https://pnpm.io
license=('MIT')
depends=("nodejs")
makedepends=('pnpm')
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('350ae56eb429b37c8502de694aabc3754845dc7d8ab901bad04487b387a627c21be68cdba75c64aad8421325a9318da9cb0e736b43282eaca11c4bc1cc402df8')

build() {
  cd $pkgname-$pkgver/$pkgname
  pnpm install --frozen-lockfile
  pnpm run compile
}

package() {
  local _npmdir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$_npmdir/dist}
  ln -s $_npmdir/bin/$pkgname.cjs "$pkgdir"/usr/bin/$pkgname
  ln -s $_npmdir/bin/pnpx.cjs "$pkgdir"/usr/bin/pnpx

  cd $pkgname-$pkgver/$pkgname
  cp -r bin package.json "$pkgdir"/$_npmdir
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
  cd dist
  cp -r $pkgname.cjs pnpmrc scripts "$pkgdir"/$_npmdir/dist
}
