# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-pcbnewtransition
pkgver=0.4.2
pkgrel=3
pkgdesc='Library that allows you to support KiCAD 6/7/8 in your KiCAD plugins'
arch=('any')
url='https://github.com/yaqwsx/pcbnewTransition'
license=('MIT')
depends=('python' 'kicad')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'remove-versioneer.patch'
)
sha512sums=('dfdf6a769712d3c024f0d6244ada1f28378f59220c7e2a82fa45154f1643949732e6627b4c7eea0277faa6a179e15e355665e4cff2cc684a37f2e8eeebcc0a22'
            '9f344e54dd2dca076202282a2af455465bc6f91609a2b86474309e6a3c3b9000b670255b187039453ae74bf9c55ac246372a8ec7379d252685ce952eb84b4dd6')
b2sums=('429e4bdcd48adb3b96ad361b7709de05feef87e418a567f98cc123a3cacd6211483f8c0fae85415193f0a0338d4f0e12ef1d72947bb7e840ee131d7b6ff7016d'
        '6df0290e0d59fc0a483d33ce8a9a5295dc3969c033b094ac46ba6b4d91c366e3ea7df385ee071236952205a8b3152e026147d067589ffbf0ed834024d9dcd064')

prepare() {
  cd "$pkgname"

  # remove dependency on versioneer
  patch -p1 -i "$srcdir/remove-versioneer.patch"
  rm versioneer.py pcbnewTransition/_version.py

  # update version string
  sed \
    -e "s/@VERSION@/$pkgver/" \
    -i setup.py \
    -i pcbnewTransition/__init__.py
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
