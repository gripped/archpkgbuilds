# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-pcbnewtransition
pkgver=0.5.0
pkgrel=1
pkgdesc='Library that allows you to support KiCAD 6/7/8 in your KiCAD plugins'
arch=('any')
url='https://github.com/yaqwsx/pcbnewTransition'
license=('MIT')
depends=('python' 'kicad')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'remove-versioneer.patch'
)
sha512sums=('76644b3c048fa3158346e5388adf4a51576ca11e2925d7fdcc2ebf1e4f2565cc497cef139b9b55b63c196b7337add0086f546d44114fc91d685a83d4dcc4d9d6'
            '9f344e54dd2dca076202282a2af455465bc6f91609a2b86474309e6a3c3b9000b670255b187039453ae74bf9c55ac246372a8ec7379d252685ce952eb84b4dd6')
b2sums=('f41ce7e12b404896eb417c441ccc5b00346e124569fe9a6a6ad81d69a8d0a3e59d0ffd98bd5c60b2e043cbb4532588940bdeb272a670b42e30af064088919bd6'
        '6df0290e0d59fc0a483d33ce8a9a5295dc3969c033b094ac46ba6b4d91c366e3ea7df385ee071236952205a8b3152e026147d067589ffbf0ed834024d9dcd064')

prepare() {
  cd "$pkgname"

  # remove dependency on versioneer
  patch -p1 -i "$srcdir/remove-versioneer.patch"
  rm versioneer.py pcbnewTransition/_version.py

  # update version string
  sed \
    -e "s/@VERSION@/$pkgver/" \
    -i setup.py \
    -i pcbnewTransition/__init__.py
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
