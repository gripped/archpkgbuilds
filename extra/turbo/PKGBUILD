# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=turbo
pkgver=2.2.3
pkgrel=1
pkgdesc="Incremental bundler and build system optimized for JavaScript and TypeScript"
url='https://turbo.build/'
arch=('x86_64')
license=('MPL-2.0')
depends=(
  'glibc'
  'gcc-libs'
  'zlib' 'libz.so'
)
makedepends=(
  'capnproto'
  'cargo'
  'protobuf'
)
options=(!lto)
source=(https://github.com/vercel/turborepo/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f6b314585f377d13d6b72ca89f3d9ef4d36281c369596be17390036efcce782d')
b2sums=('6b00254fab237d2c0f41b4ddc1637bbc26567047e21b77ff537eb9746c1581d2a64cfea2651e97da29a152876c2172a75af0b68d57079a42319966b045344fca')

prepare() {
  cd turborepo-${pkgver}
  RUSTC_BOOTSTRAP=1 cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd turborepo-${pkgver}
  RUSTC_BOOTSTRAP=1 cargo build -p turbo --frozen --release
}

package() {
  cd turborepo-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/turbo

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  export TURBO_TELEMETRY_DISABLED=1
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
