# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=turbo
pkgver=1.12.5
pkgrel=1
pkgdesc="Incremental bundler and build system optimized for JavaScript and TypeScript"
url='https://turbo.build/'
arch=('x86_64')
license=('MPL-2.0')
depends=(
  'glibc'
  'gcc-libs'
  'zlib' 'libz.so'
)
makedepends=(
  'capnproto'
  'cargo'
  'protobuf'
)
options=(!lto)
source=(https://github.com/vercel/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('13a1f64779f5c08f7c11e2e4c221bbce81a71459b32631c85d3d4911953f913d')
b2sums=('2a357eca4b4caf0497bfa9e375ebb8e0c8c818ba67ef568caa5c41b2a4b2501f3938002517614221810b7f5e39197ad4ebe96a09c624bbbfbd2441ebf21c6ddb')

prepare() {
  cd "${pkgname}-${pkgver}"
  rm rust-toolchain
  RUSTC_BOOTSTRAP=1 cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  RUSTC_BOOTSTRAP=1 cargo build -p turbo --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/turbo

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  export TURBO_TELEMETRY_DISABLED=1
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
