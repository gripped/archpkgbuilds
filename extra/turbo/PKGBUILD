# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=turbo
pkgver=2.1.2
pkgrel=1
pkgdesc="Incremental bundler and build system optimized for JavaScript and TypeScript"
url='https://turbo.build/'
arch=('x86_64')
license=('MPL-2.0')
depends=(
  'glibc'
  'gcc-libs'
  'zlib' 'libz.so'
)
makedepends=(
  'capnproto'
  'cargo'
  'protobuf'
)
options=(!lto)
source=(https://github.com/vercel/turborepo/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/vercel/turborepo/commit/6d188656da005efc999fd3ba803db3777f8ea079.diff)
sha256sums=('109caa97a6ca44938c1a121eccf323ebf454488466f0375d7a9c30dd806f1bca'
            '3cedf68990f679cddf7b5c15228d0bc7c6ffa530feb65fcf6d7d333fe1a6730a')
b2sums=('7dbc719879dae0dc2c1a09432b7a78f0103bdf1eec4715e0c70a5cf650552939549e3bb7d2e58056951417b7c2cc900e11234df4d4eacae45abe8d9732c182d9'
        '238d0c200057aec6ee82b0c15a611c5256e1272ba6ede726a2186da606691b0ad475b95320809efce713431c98e28174df1ab3d28d1887a48fa4f1dfb915c511')

prepare() {
  cd turborepo-${pkgver}
  patch -Np1 -i ../6d188656da005efc999fd3ba803db3777f8ea079.diff
  RUSTC_BOOTSTRAP=1 cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd turborepo-${pkgver}
  RUSTC_BOOTSTRAP=1 cargo build -p turbo --frozen --release
}

package() {
  cd turborepo-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/turbo

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  export TURBO_TELEMETRY_DISABLED=1
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
