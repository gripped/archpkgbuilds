# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=turbo
pkgver=2.0.10
pkgrel=1
pkgdesc="Incremental bundler and build system optimized for JavaScript and TypeScript"
url='https://turbo.build/'
arch=('x86_64')
license=('MPL-2.0')
depends=(
  'glibc'
  'gcc-libs'
  'zlib' 'libz.so'
)
makedepends=(
  'capnproto'
  'cargo'
  'protobuf'
)
options=(!lto)
source=(https://github.com/vercel/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7eba5d7aa1d5f5c133fa24b1e601589d314be9e3c78dd5f06b245920a2868d48')
b2sums=('c0f630ad092eda535b784527a3fcfc7ad5de1af154388fb6f8959b174bd71a7e07a264d2f494d178e0e01e2482bf2dcf0f3c46aa56d29e7ab7fc0915d1ad5109')

prepare() {
  cd "${pkgname}-${pkgver}"
  RUSTC_BOOTSTRAP=1 cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  RUSTC_BOOTSTRAP=1 cargo build -p turbo --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/turbo

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  export TURBO_TELEMETRY_DISABLED=1
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
