# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=turbo
pkgver=2.0.8
pkgrel=1
pkgdesc="Incremental bundler and build system optimized for JavaScript and TypeScript"
url='https://turbo.build/'
arch=('x86_64')
license=('MPL-2.0')
depends=(
  'glibc'
  'gcc-libs'
  'zlib' 'libz.so'
)
makedepends=(
  'capnproto'
  'cargo'
  'protobuf'
)
options=(!lto)
source=(https://github.com/vercel/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        enable-lazy_cell.patch)
sha256sums=('d20a558e1feec5d776b0a47edbdb9838fcb4b32084f735e61f419e8a096345b9'
            'a4ab6cbb148edd5b095df33e8ecf05997c8b5f885ff97492f8e2ef0c2ff2f3c8')
b2sums=('159ee86662587c90c52932f974d97ac858b3215f0020e646793d5e7a92ee1e1b4a546dd4529abbc341a05544e91fd83a922d889d62fb8834c181a6f6bd04bfab'
        '72f51c7d9fa3fff4cd39c97b3886bd3adb4e100ceb1a0cdf7e1583d9ff467bf32e2cae57d057e498aa65a06bea13678985d77ce279e618054ea8040b660ad153')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../enable-lazy_cell.patch
  RUSTC_BOOTSTRAP=1 cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  RUSTC_BOOTSTRAP=1 cargo build -p turbo --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/turbo

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  export TURBO_TELEMETRY_DISABLED=1
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
