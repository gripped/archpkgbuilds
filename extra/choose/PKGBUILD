# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Filipe Nascimento <flipee at tuta dot io>

pkgname=choose
pkgver=1.3.6
pkgrel=1
pkgdesc='A human-friendly and fast alternative to cut and (sometimes) awk'
arch=('x86_64')
url='https://github.com/theryangeary/choose'
license=('GPL-3.0-only')
depends=('gcc-libs')
makedepends=('git' 'rust')
checkdepends=('diffutils')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('71093889dbdd18f901ea42b6ebe0cd19508314dcd0a8af510bb5116c604437b62358bea19eed2862525438d9d9479391e73d0064451cdfca4ec2ebd7b479212d')
b2sums=('94b8e9170f7e17d937bfcf780f6fa83f7a03d16c12412d057605f27e445efb24f6fa9512dcf0ae9b7f3863ef94130068fdb0ba7f528cda30c7e34ff29068c348')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

check() {
  cd "$pkgname"

  cargo test --frozen --release

  ./test/e2e_test.sh
}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" target/release/choose

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" readme.md
}
