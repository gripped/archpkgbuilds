# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Filipe Nascimento <flipee at tuta dot io>

pkgname=choose
pkgver=1.3.7
pkgrel=2
pkgdesc='A human-friendly and fast alternative to cut and (sometimes) awk'
arch=(x86_64)
url='https://github.com/theryangeary/choose'
license=(GPL-3.0-only)
depends=(libgcc)
makedepends=(git rust)
checkdepends=(diffutils)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('98d4529595f8773f762b1add90c1c2709d2b5f4da94fbbe64c1febf3e3e73dd0cc480aa40d54635a073adaadd460c09b086bb4cd94557307391c58f2836c8838')
b2sums=('f5cf53f677d132927ecbf8be27a79fc2b3856816525cabdd8c7a8cbed1aea965e220f11eac472572876bd8c39e6c3de366f84a786bacb0087ad9109ce5524a31')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

check() {
  cd "$pkgname"

  cargo test --frozen --release

  ./test/e2e_test.sh
}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" target/release/choose

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" readme.md
}
