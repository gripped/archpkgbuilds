# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=httpx-retries
pkgname=python-$_name
pkgver=0.4.5
pkgrel=3
license=(MIT)
pkgdesc="A retry layer for HTTPX"
arch=(any)
url="https://github.com/will-ockmore/httpx-retries"
depends=(
  python
  python-httpx
)
makedepends=(
  python-build
  python-installer
  python-wheel
  python-hatchling
  python-hatch-fancy-pypi-readme
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
b2sums=('977c6194a3f26552e0b6f7c0a7f83a858f041c879124fa41717fed51ca2d8a3c1c8442aac840c122481ba1d77118a370d04657155119a2ea0f98354f3cc6ffd2')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
