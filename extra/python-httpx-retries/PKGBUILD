# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=httpx-retries
pkgname=python-$_name
pkgver=0.4.6
pkgrel=1
license=(MIT)
pkgdesc="A retry layer for HTTPX"
arch=(any)
url="https://github.com/will-ockmore/httpx-retries"
depends=(
  python
  python-httpx
)
makedepends=(
  python-build
  python-installer
  python-wheel
  python-hatchling
  python-hatch-fancy-pypi-readme
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
b2sums=('38d98e875d4459310fc5530428a616ba7b074d6c58ced104f52c4e6490c2b88e8364856c2b323fa2252431c96778ff94786d98a4ce3831c0468c7b80dbd89220')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
