# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=wishlist
pkgver=0.15.2
pkgrel=1
pkgdesc="The SSH directory"
arch=('x86_64')
url="https://github.com/charmbracelet/wishlist"
license=('MIT')
depends=(
  'glibc'
)
makedepends=(
  'go'
)
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/charmbracelet/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('78735bb9f20e92ae24b84114a3e2c135e058c31ae9b18579fe1ea17f651f200701a867793f3324231e78251ad17198a0dabd1c4c22cb34e842415c36b271dcc5')
b2sums=('719cf9f3bc91365b186ef32959d2ca94ba2e04a4755e4d492f3c1d371e3db5dece7f92d1e6306891768aa4211feae11a56fec84b3e9c8f416f69def9705b4328')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -o build/ \
    -buildmode=pie \
    -mod=readonly \
    -ldflags "-compressdwarf=false -linkmode=external" \
    ./...
}

check() {
  cd "${pkgname}-${pkgver}"
  build/wishlist --help
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/wishlist "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim: ts=2 sw=2 et:
