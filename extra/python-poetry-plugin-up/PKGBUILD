# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Matthew Gamble <git@matthewgamble.net>

pkgname=python-poetry-plugin-up
pkgver=0.8.0
pkgrel=1
pkgdesc='Poetry plugin that updates dependencies and bumps their versions in pyproject.toml file'
url=https://github.com/MousaZeidBaker/poetry-plugin-up
license=(MIT)
depends=(
  python
  python-cleo
  python-poetry
  python-poetry-core
  python-tomlkit
)
makedepends=(
  git
  python-build
  python-installer
)
_tag=8c337e8426b8f19cbe366ad3cf957385bfdf4c2a
source=(git+https://github.com/MousaZeidBaker/poetry-plugin-up.git#tag=${_tag})
b2sums=('8a3bd29ab29e9c564246f028582dec2cad85afabf4d3fb6dc1e692a9bebc2a7b88ba55a8c2b05cf1ffaabdf17d9d7bf753e683673347b5146d82d4943f9730b6')
arch=(any)

pkgver() {
  cd poetry-plugin-up
  git describe --tags | sed 's/^v//'
}

build() {
  cd poetry-plugin-up
  python -m build --wheel --no-isolation
}

package() {
  python -m installer -d "${pkgdir}" poetry-plugin-up/dist/*.whl
  install -Dm 644 poetry-plugin-up/LICENSE -t "${pkgdir}"/usr/share/licenses/python-poetry-plugin-up/
}

# vim: ts=2 sw=2 et:
