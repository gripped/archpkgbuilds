# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Matthew Gamble <git@matthewgamble.net>

pkgname=python-poetry-plugin-up
pkgver=0.9.0
pkgrel=2
pkgdesc='Poetry plugin that updates dependencies and bumps their versions in pyproject.toml file'
url=https://github.com/MousaZeidBaker/poetry-plugin-up
license=(MIT)
depends=(
  python
  python-cleo
  python-poetry
  python-poetry-core
  python-tomlkit
)
makedepends=(
  git
  python-build
  python-installer
)
_tag=642392837335b1cac3c839ee714d6a6c063c5d7a
source=(git+https://github.com/MousaZeidBaker/poetry-plugin-up.git#tag=${_tag})
b2sums=('1368d264de46b765fe8afe17c86a7bea65801fe342752c6eb779887b1f7a3d6a12abd632a7917a9cd45c0dd1b6d01f7dd95f1ac1bd5fb7105d5e18064f538dce')
arch=(any)

pkgver() {
  cd poetry-plugin-up
  git describe --tags | sed 's/^v//'
}

build() {
  cd poetry-plugin-up
  python -m build --wheel --no-isolation
}

package() {
  python -m installer -d "${pkgdir}" poetry-plugin-up/dist/*.whl
  install -Dm 644 poetry-plugin-up/LICENSE -t "${pkgdir}"/usr/share/licenses/python-poetry-plugin-up/
}

# vim: ts=2 sw=2 et:
