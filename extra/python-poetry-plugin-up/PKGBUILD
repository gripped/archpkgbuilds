# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Matthew Gamble <git@matthewgamble.net>

pkgname=python-poetry-plugin-up
pkgver=0.7.2
pkgrel=1
pkgdesc='Poetry plugin that updates dependencies and bumps their versions in pyproject.toml file'
url=https://github.com/MousaZeidBaker/poetry-plugin-up
license=(MIT)
depends=(
  python
  python-cleo
  python-poetry
  python-poetry-core
  python-tomlkit
)
makedepends=(
  git
  python-build
  python-installer
)
_tag=846b41a1d26e33c85d4bd799c7624af67f1256a1
source=(git+https://github.com/MousaZeidBaker/poetry-plugin-up.git#tag=${_tag})
b2sums=('391331eb79092caab3d1583d18ca568c29772ff63a9af6031593dcf85f5e30e95a5db4aadf955b6e3377a7a02b0c199fa910b186810ff93c52272658efacbf77')
arch=(any)

pkgver() {
  cd poetry-plugin-up
  git describe --tags | sed 's/^v//'
}

build() {
  cd poetry-plugin-up
  python -m build --wheel --no-isolation
}

package() {
  python -m installer -d "${pkgdir}" poetry-plugin-up/dist/*.whl
  install -Dm 644 poetry-plugin-up/LICENSE -t "${pkgdir}"/usr/share/licenses/python-poetry-plugin-up/
}

# vim: ts=2 sw=2 et:
