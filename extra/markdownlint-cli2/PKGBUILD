# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Brody <archfan at brodix dot de>

pkgname=markdownlint-cli2
pkgver=0.16.0
pkgrel=1
pkgdesc='Fast, flexible, configuration-based command-line interface for linting Markdown/CommonMark files with the markdownlint library'
arch=(any)
url=https://github.com/DavidAnson/markdownlint-cli2
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  jq
  npm
)
options=(!emptydirs)
source=("git+$url.git#tag=v$pkgver")
b2sums=('ef1c7395dc5ab82e636db5c03a048022d9584a2e191a081c6c074f5724421e49ac399a206ee0286209a1b229d5fd8c767e2122be18f1a3f13c39ebf7cdfd9da0')

prepare() {
  cd $pkgname
  npm install
}

check() {
  cd $pkgname
  npm test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/{bin,share/{doc,licenses}/$pkgname},$mod_dir}
  ln -s $mod_dir/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/{CHANGELOG,README}.md "$pkgdir"/usr/share/doc/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname

  cd $pkgname
  npm prune --production

  readarray -t mod_files < <(jq -r '.files[]' package.json)
  cp --parents -r "${mod_files[@]}" node_modules package.json "$pkgdir"/$mod_dir
}
