# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=vvave
pkgver=4.0.1
pkgrel=1
pkgdesc='Multi-platform media player'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         mauikit
         mauikit-accounts
         mauikit-filebrowsing
         qt6-base
         qt6-declarative
         qt6-multimedia
         taglib)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6d449edb022fdea89ddd44a0822bd658d7d070675bee83c01b004452150a0e38'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
