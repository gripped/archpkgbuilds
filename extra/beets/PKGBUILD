# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=beets
pkgver=2.2.0
pkgrel=1
pkgdesc='Flexible music library manager and tagger'
arch=(any)
url=https://beets.io/
license=(MIT)
depends=(
  python
  python-confuse
  python-jellyfish
  python-mediafile
  python-munkres
  python-musicbrainzngs
  python-platformdirs
  python-unidecode
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-sphinx
  python-wheel
)
checkdepends=(
  bash-completion
  imagemagick
  python-beautifulsoup4
  python-discogs-client
  python-flask
  python-librosa
  python-mpd2
  python-numba
  python-py7zr
  python-pylast
  python-pytest
  python-reflink
  python-requests-oauthlib
  python-responses
  python-xdg
)
optdepends=(
  'bash-completion: Bash completion'
  'chromaprint: Chromaprint/Acoustid plugin'
  'ffmpeg: Convert, ReplayGain plugins'
  'gst-plugins-bad: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-good: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-ugly: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-libav: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-python: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'imagemagick: Thumbnails plugin'
  'python-beautifulsoup4: Lyrics plugin'
  'python-discogs-client: Discogs plugin'
  'python-flask: Web plugin'
  'python-gobject: ReplayGain plugin'
  'python-librosa: AutoBPM plugin'
  'python-mpd2: MPDStats plugin'
  'python-pyacoustid: Chromaprint/Acoustid plugin'
  'python-pylast: LastGenre plugin'
  'python-requests: Chromaprint/Acoustid, BPD, FetchArt plugins'
  'python-requests-oauthlib: Beatport plugin'
  'python-xdg: Thumbnails plugin'
)
_tag=01f1fafc888457a440a22a41320f02e9014e07c4
source=(
  git+https://github.com/beetbox/beets.git#tag=${_tag}
  beet
)
b2sums=('9edea1550c30152e79a9338708f3e109cf750658eb873a4a6de8cd43c61d1cd22d156e958f4875c13d39deffc8fb1ca015b64d5bb48e2b5926bd91111d89ef16'
        '1b36ba07d85f0fbcae0a3d8253ce2e5d7ecf8a17c6f0bb946579663de546d53cd4fe5da84ee8d880d7f95f48e66ef6010232a995d86d0c52b52218cd991a7782')

prepare() {
  cd beets
  git cherry-pick -n bcc79a5b09225050ce7c88f63dfa56f49f8782a8
}

pkgver() {
  cd beets
  git describe --tags | sed 's/^v//'
}

build() {
  cd beets
  make -C docs man
  python -m build --wheel --no-isolation
}

check() {
  cd beets
  local pytest_args=(
    --override-ini="addopts="
    --ignore=test/plugins/test_player.py
    --deselect=test/test_ui.py::CompletionTest::test_completion
    # Requires sunau, removed in Python 3.13
    --deselect=test/plugins/test_autobpm.py
    # Requires aifc, removed in Python 3.13
    --deselect=test/plugins/test_aura.py
  )
  pytest "${pytest_args[@]}"
}

package() {
  python -m installer --destdir="$pkgdir" beets/dist/*.whl
  install -Dm 644 beet -t "${pkgdir}"/usr/share/bash-completion/completions/
  install -Dm 644 beets/extra/_beet -t "${pkgdir}"/usr/share/zsh/site-functions/
  install -Dm 644 beets/docs/_build/man/beet.1 -t "${pkgdir}"/usr/share/man/man1/
  install -Dm 644 beets/docs/_build/man/beetsconfig.5 -t "${pkgdir}"/usr/share/man/man5/
  install -Dm 644 beets/LICENSE -t "${pkgdir}"/usr/share/licenses/beets/
}

# vim: ts=2 sw=2 et:
