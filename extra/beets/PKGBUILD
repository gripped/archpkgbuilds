# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>
# Contributor: Garrett Figueroa <djspacewhale@4d2.org>

pkgname=beets
pkgver=2.5.1
pkgrel=3
pkgdesc='Flexible music library manager and tagger'
arch=(any)
url=https://beets.io/
license=(MIT)
depends=(
  python
  python-confuse
  python-jellyfish
  python-lap
  python-mediafile
  python-munkres
  python-musicbrainzngs
  python-platformdirs
  python-typing_extensions
  python-unidecode
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-sphinx
  python-wheel
)
checkdepends=(
  bash-completion
  imagemagick
  python-beautifulsoup4
  python-discogs-client
  python-flask
  python-langdetect
  python-librosa
  python-mpd2
  python-numba
  python-py7zr
  python-pylast
  python-pytest
  python-reflink
  python-requests-oauthlib
  python-requests-mock
  python-responses
  python-xdg
)
optdepends=(
  'bash-completion: Bash completion'
  'chromaprint: Chromaprint/Acoustid plugin'
  'ffmpeg: Convert, ReplayGain plugins'
  'gst-plugins-bad: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-good: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-ugly: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-libav: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-python: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'imagemagick: Thumbnails plugin'
  'python-beautifulsoup4: Lyrics plugin'
  'python-discogs-client: Discogs plugin'
  'python-flask: Web plugin'
  'python-gobject: ReplayGain plugin'
  'python-langdetect: Lyrics plugin'
  'python-librosa: AutoBPM plugin'
  'python-mpd2: MPDStats plugin'
  'python-pyacoustid: Chromaprint/Acoustid plugin'
  'python-pylast: LastGenre plugin'
  'python-requests: Chromaprint/Acoustid, BPD, FetchArt, Lyrics plugins'
  'python-requests-oauthlib: Beatport plugin'
  'python-xdg: Thumbnails plugin'
)
source=(
  "$pkgname::git+https://github.com/beetbox/beets.git#tag=v$pkgver"
  beet
  fix-failing-tests.patch
)
sha512sums=('7899b4c1a95bc4efee85b27ca7e34815314c5a035f0fadd5d2e1a83551bddaf1a7e36489e58497baaa62715d895b2eb3482a85fac723aa79e2f3c97880f30d86'
            '8f51ebcc253b0cf698adffae4d2d1a0ff29dcd2d503d0c5d284e9c5cd5981b0ffd190fce154b112ac3cbb419fc0813f2ccbd7e67e3abbdce043c0f6a967274ad'
            '64e97a1063660a429ab66876bbc082bc23a880b1c8e00109570be9c9315fb61f1a6e7f442a6a97f80f09ac762a75b7f57cca468cc18f97c4ba98cf063ce91d89')
b2sums=('f7a8cc35696380364acd0c6dc9ad1681f2076d0ab3c233f84ead5b18c46c148f4d1d8a263b39fc7ef615d0ccb579653cebedb06fec4c914c179bf4b50380f09b'
        '1b36ba07d85f0fbcae0a3d8253ce2e5d7ecf8a17c6f0bb946579663de546d53cd4fe5da84ee8d880d7f95f48e66ef6010232a995d86d0c52b52218cd991a7782'
        'da0ff333d20d4225cccbe9d1934b6e5bc09aeca8555c2be670652377f0dae028cebffa4b103df67defc9653a0aeec3556e3dd4c6f235a15fac0ffab44281e81c')

prepare() {
  cd "$pkgname"

  git cherry-pick -n ec141dbfd6079209e994d0a4ad97fedcc3a2ab1b # Fix tests with python 3.14
  patch -p1 -i "$srcdir/fix-failing-tests.patch"
}

build() {
  cd beets

  python -m build --wheel --no-isolation

}

check() {
  cd beets
  local pytest_args=(
    --override-ini="addopts="
    --ignore=test/plugins/test_player.py
    --deselect=test/test_ui.py::CompletionTest::test_completion
    --deselect=test/plugins/test_embedart.py::EmbedartCliTest::test_reject_different_art
    # Requires sunau, removed in Python 3.13
    --deselect=test/plugins/test_autobpm.py
    # Requires aifc, removed in Python 3.13
    --deselect=test/plugins/test_aura.py
  )
  pytest "${pytest_args[@]}"
}

package() {
  python -m installer --destdir="$pkgdir" beets/dist/*.whl
  install -Dm 644 beet -t "${pkgdir}"/usr/share/bash-completion/completions/
  install -Dm 644 beets/extra/_beet -t "${pkgdir}"/usr/share/zsh/site-functions/
  install -Dm 644 beets/LICENSE -t "${pkgdir}"/usr/share/licenses/beets/
}

# vim: ts=2 sw=2 et:
