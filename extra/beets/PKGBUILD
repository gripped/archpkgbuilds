# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>
# Contributor: Garrett Figueroa <djspacewhale@4d2.org>

pkgname=beets
pkgver=2.6.1
pkgrel=1
pkgdesc='Flexible music library manager and tagger'
arch=(any)
url=https://beets.io/
license=(MIT)
depends=(
  python
  python-confuse
  python-jellyfish
  python-lap
  python-mediafile
  python-munkres
  python-musicbrainzngs
  python-packaging
  python-platformdirs
  python-requests
  python-requests-ratelimiter
  python-typing_extensions
  python-unidecode
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-sphinx
  python-wheel
)
checkdepends=(
  bash-completion
  imagemagick
  python-beautifulsoup4
  python-discogs-client
  python-flask
  python-langdetect
  python-librosa
  python-mpd2
  python-numba
  python-py7zr
  python-pylast
  python-pytest
  python-reflink
  python-requests-oauthlib
  python-requests-mock
  python-responses
  python-titlecase
  python-xdg
  gst-python
)
optdepends=(
  'bash-completion: Bash completion'
  'chromaprint: Chromaprint/Acoustid plugin'
  'ffmpeg: Convert, ReplayGain plugins'
  'gst-plugins-bad: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-good: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-ugly: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-libav: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-python: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'imagemagick: Thumbnails plugin'
  'python-beautifulsoup4: Lyrics plugin'
  'python-discogs-client: Discogs plugin'
  'python-flask: Web plugin'
  'python-gobject: ReplayGain plugin'
  'python-langdetect: Lyrics plugin'
  'python-librosa: AutoBPM plugin'
  'python-mpd2: MPDStats plugin'
  'python-pyacoustid: Chromaprint/Acoustid plugin'
  'python-pylast: LastGenre plugin'
  'python-requests-oauthlib: Beatport plugin'
  'python-xdg: Thumbnails plugin'
  'python-titlecase: Titlecase plugin'
)
source=(
  "$pkgname::git+https://github.com/beetbox/beets.git#tag=v$pkgver"
  beet
)
sha512sums=('47e612bc0ae7f74deb1b1390048df5e669c7f0bacc9c7bfa7878b62589aa3f38a42268442b55e93f2789180b03b8d41288cc7541566d58ff379e424ad2ec8559'
            '8f51ebcc253b0cf698adffae4d2d1a0ff29dcd2d503d0c5d284e9c5cd5981b0ffd190fce154b112ac3cbb419fc0813f2ccbd7e67e3abbdce043c0f6a967274ad')
b2sums=('3219d89bafc7eab63d2e5460634fefa2a599f24de6e085dcd4ebfd585e69ef4eb1580d18ea200916f5742f1618bf8588c577c125ce2e30f866cb89a0067dbea3'
        '1b36ba07d85f0fbcae0a3d8253ce2e5d7ecf8a17c6f0bb946579663de546d53cd4fe5da84ee8d880d7f95f48e66ef6010232a995d86d0c52b52218cd991a7782')

build() {
  cd beets

  python -m build --wheel --no-isolation

}

check() {
  cd beets
  local pytest_args=(
    --override-ini="addopts="
    --ignore=test/plugins/test_player.py
    --deselect=test/test_ui.py::CompletionTest::test_completion
    --deselect=test/plugins/test_embedart.py::EmbedartCliTest::test_reject_different_art
    --deselect=test/plugins/test_bpd.py::BPDReflectionTest::test_cmd_decoders
    --deselect=test/plugins/test_aura.py
  )
  pytest "${pytest_args[@]}"
}

package() {
  python -m installer --destdir="$pkgdir" beets/dist/*.whl
  install -Dm 644 beet -t "${pkgdir}"/usr/share/bash-completion/completions/
  install -Dm 644 beets/extra/_beet -t "${pkgdir}"/usr/share/zsh/site-functions/
  install -Dm 644 beets/LICENSE -t "${pkgdir}"/usr/share/licenses/beets/
}

# vim: ts=2 sw=2 et:
