# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pdfminer
pkgname=python-${_pkg}
pkgver=20250506
pkgrel=2
pkgdesc="Python PDF Parser"
arch=(any)
url="https://github.com/pdfminer/pdfminer.six"
license=(MIT)
depends=(python-charset-normalizer python-cryptography)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm python-wheel cython)
checkdepends=(python-pytest)
# Provided tarballs do not include tests
#source=(${url}/releases/download/${pkgver}/${_pkg}.six-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('beb5e6f649e849d607392200f689830d73b58c542be31d16359a9763608e6c7f')

build() {
  cd ${_pkg}.six-${pkgver}
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver \
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkg}.six-${pkgver}
  PYTHONPATH="${PWD}"/build/lib:"${PWD}" pytest -vv --color=yes
}

package() {
  cd ${_pkg}.six-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Fix ownership
  chmod a+r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/pdfminer/cmap/*
}
