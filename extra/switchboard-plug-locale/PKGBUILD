# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-locale
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Locale Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-locale
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dconf
  glib2
  glibc
  gnome-desktop-4
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libibus
  libswitchboard-3.so
  polkit
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=6d1dd1303dd169c4c995f7c5011de05ea2123f58
source=(git+https://github.com/elementary/switchboard-plug-locale.git#tag=${_tag})
b2sums=('fa2b32e4158abfdd11517f4b30ded15f526f4a0256967a3f5e4d801781b95fcfd2ae677abe04c1119abe17b441c5a34073581282c871aec0a71322c9f310da73')

pkgver() {
  cd switchboard-plug-locale
  git describe --tags
}

build() {
  arch-meson switchboard-plug-locale build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
