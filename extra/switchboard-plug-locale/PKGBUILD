# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-locale
pkgver=8.0.1
pkgrel=2
pkgdesc='Switchboard Locale Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-locale
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dconf
  glib2
  glibc
  gnome-desktop-4
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libibus
  libswitchboard-3.so
  polkit
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=e8b2bff6a1e7b06c8b1ad2cff0be8dd94366cf87
source=(git+https://github.com/elementary/switchboard-plug-locale.git#tag=${_tag})
b2sums=('403f19b40e64cda522c0b4782afa5881c61ce8ee7ec4a6100ed1ffd3d3003856b7be5b6c86075370719ee366d79e4e299e7c6f5e30c50c58b58166b245da7eca')

pkgver() {
  cd switchboard-plug-locale
  git describe --tags
}

build() {
  arch-meson switchboard-plug-locale build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
