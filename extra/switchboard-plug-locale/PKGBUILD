# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-locale
pkgver=8.0.2
pkgrel=1
pkgdesc='Switchboard Locale Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-locale
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dconf
  glib2
  glibc
  gnome-desktop-4
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libibus
  libswitchboard-3.so
  polkit
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=d32242abbaa8296c101bd90e3c87e09d5c0635f0
source=(git+https://github.com/elementary/switchboard-plug-locale.git#tag=${_tag})
b2sums=('dd73fa2fc47a30983f3c1e2fa7ab6723c004e5a6f5c7baf80624388382640cf326913e2da0ed3ad7411c5dc4b91f10b42fd6656a12b8d03054bc195c24200b94')

pkgver() {
  cd switchboard-plug-locale
  git describe --tags
}

build() {
  arch-meson switchboard-plug-locale build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
