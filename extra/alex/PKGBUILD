# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.5.1.0
pkgrel=1
pkgdesc='Lexical analyser generator for Haskell'
arch=(x86_64)
url='https://hackage.haskell.org/package/alex'
license=(BSD-3-Clause)
depends=(ghc-libs)
makedepends=(ghc happy)
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('aa8aed9aff5bf3b4038d6ad31a0b4748df15dfe925c3be171070dc6a97ed357b04810f94467aa583cb75ba67f23244a0e0656c5294f4c2946e71f28b548586bb')

build() {
  cd $pkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'
  runhaskell Setup build $MAKEFLAGS
}

check() {
  cd $pkgname-$pkgver
  HC="ghc -dynamic" runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/alex/

  # Doc only contains the license
  rm -rf "$pkgdir/usr/share/doc"
}

# getver: hackage.haskell.org/package/alex/changelog
# vim: ts=2 sw=2 et:
