# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Lorenzo Gaifas <brisvag at gmail dot com>

pkgname=python-superqt
_pkgname=${pkgname#python-}
pkgver=0.7.0
pkgrel=1
pkgdesc='Missing widgets and components for Qt-python'
arch=(any)
url='https://pyapp-kit.github.io/superqt/'
license=(BSD-3-Clause)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
)
depends=(
  python
  python-pint
  python-pygments
  python-qtpy
  python-typing_extensions
)
checkdepends=(
  python-numpy
  python-pytest
  python-pytest-qt
  python-pyqt6
)
source=("git+https://github.com/pyapp-kit/superqt#tag=v$pkgver")
sha256sums=('d483d9132a988090889d6cfdc2871b07292eb2af094240bcabec29ad5da637dd')

build() {
  cd "$_pkgname"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname"
  PYTHONPATH="$PWD/src" QT_QPA_PLATFORM=offscreen pytest \
    --deselect tests/test_eliding_label.py \
    --deselect tests/test_eliding_line_edit.py
}

package() {
  cd "$_pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
