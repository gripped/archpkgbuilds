# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Lorenzo Gaifas <brisvag at gmail dot com>

pkgname=python-superqt
pkgver=0.7.8
pkgrel=1
pkgdesc='Missing widgets and components for Qt-python'
arch=(any)
url='https://pyapp-kit.github.io/superqt/'
license=(BSD-3-Clause)
depends=(
  python
  python-pint
  python-pygments
  python-qtpy
)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
)
checkdepends=(
  python-numpy
  python-pytest
  python-pytest-qt
  python-pyqt6
)
source=("git+https://github.com/pyapp-kit/superqt#tag=v$pkgver")
b2sums=('b2b7a394c2fedb448b54eab092f337a1a784376466c5414bf0789792c62307645c8bc104fb8a35683006e02c5fe4815be9f00043ed395a3536d154e425dae3a4')

build() {
  cd ${pkgname#python-}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}
  PYTHONPATH="$PWD/src" QT_QPA_PLATFORM=offscreen pytest \
    --deselect tests/test_eliding_label.py \
    --deselect tests/test_eliding_line_edit.py \
    --ignore tests/test_iconify.py
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
