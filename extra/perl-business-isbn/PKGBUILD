# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-business-isbn
pkgver=3.012
pkgrel=2
pkgdesc='Perl package to deal with International Standard Book Numbers'
arch=(any)
url='https://github.com/briandfoy/business-isbn'
license=(Artistic-2.0)
depends=(perl perl-business-isbn-data)
makedepends=(git)
checkdepends=(perl-test-pod)
optdepends=('perl-libwww: for access to the xISBN service')
options=(!emptydirs)
source=("$pkgname::git+$url#tag=release-${pkgver}")
sha512sums=('9025f30db9d22c342676d7d0dd8497883ef12fd06fc236715425a1df14899d3529e10cd753d91e815213f65b564fb759313eeacb37784b75248868905d938373')
b2sums=('54d9f26c8a48b3020befdc165ceddeb6864695b104590b256969ffe9753566792cad27beb5580b41411161c740bc88f1af4c54e520d3b90ff5beda4d39e65fc3')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
