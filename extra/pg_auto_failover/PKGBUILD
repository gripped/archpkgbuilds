# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Marat Bakeev <hawara@gmail.com>

pkgname=pg_auto_failover
pkgver=2.2
pkgrel=2
pkgdesc="extension and service for PostgreSQL that monitors and manages automated failover for a Postgres cluster."
arch=('x86_64')
url="https://github.com/citusdata/pg_auto_failover"
license=('PostgreSQL')
depends=('postgresql' 'ncurses' 'postgresql-libs' 'glibc')
optdepends=(
  'python-pygments: /usr/share/doc/pg_auto_failover/conf.py'
  'python-sphinx: /usr/share/doc/pg_auto_failover/conf.py'
)
makedepends=('clang' 'llvm')
options=('!buildflags')
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/citusdata/pg_auto_failover/archive/refs/tags/v$pkgver.tar.gz"
	postgresql-18.patch
)
sha256sums=('0f4018564e620592fcfb43d52ea2bc3ccba33bd824352fa9c7e55eb2ba0a4f6c'
            '4cc70efadef112356d8acf0153739bfdaf106f3585d4e7478f07f35e1667b01d')

prepare() {
	# PostgreSQL 18 (untested)
	patch -d "$pkgname-$pkgver" -Np1 < postgresql-18.patch
}

build() {
	cd "$pkgname-$pkgver"

	# Work around build failure
	# snprintf.c:559:2: error: #error "Don't know how to print 64bit integers"
	CFLAGS+=" -DHAVE_LONG_INT_64"

	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install

	install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set noet sw=0:
