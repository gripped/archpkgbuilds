# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Guilhem Saurel <guilhem@saurel.me>
# Contributor: Michel Zou
# Contributor: Oliver Goethel
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
#
# Upstream disappeared:
#     https://www.salome-platform.org/downloads
#     https://files.salome-platform.org/Salome/other/med-4.1.1.tar.gz
#
# Using a fork on GitHub by one of the FreeCAD developers instead.

pkgname=med-openmpi
pkgver=5.0.0
pkgrel=1
pkgdesc='Library for reading and writing MED files'
url='https://github.com/chennes/med'
license=(LGPL)
arch=(x86_64)
provides=(med)
conflicts=(med)
replaces=(med)
depends=(hdf5-openmpi python tk)
makedepends=(cmake gcc-fortran git ninja openmpi swig)
source=("git+$url#commit=621956ec1a18d66346b40e91679021e4595ffa1b"
        hdf5-1.12.patch
        https://src.fedoraproject.org/rpms/med/raw/rawhide/f/med-swig-4.3.0.patch
        https://src.fedoraproject.org/rpms/med/raw/rawhide/f/med-py3.13.patch)
b2sums=('93fdb2cf5d2a44a67fd8bdebccb70491728b0d0c0ce88f7a0164048096f44451df39ff169dd6fde1831d714ddf97459c8d8e04c492a32443612d2a6637e969b6'
        '9fb63135d64d7a905cfad012a5d5fc2ecc8605ea0e30adeeb21d29d5e36ecedbe91f7b6254a53ae11d74336633c352b7021bdebda8d772640b5fafb70ad6796f'
        'bb5a095e1e61b0c16561860eb931836a85a93b2389fd29a487eb260a5890c1c80fe175fea21116ae6590d4b303d6737f871fc48256ca7b590162b23079bebcc8'
        'db04bc0ef21f1d06e282ce32797409746167555a549dd08cc05e36db2f474d342cd4057e7d99b37ea3d0f268518b3daa96d0ea410ac5cc09966ce1267248e415')

prepare() {
  cd med
  patch -p1 -i ../hdf5-1.12.patch # Fix build with hdf5 1.12 (Fedora)
  patch -p1 -i ../med-swig-4.3.0.patch
  patch -p1 -i ../med-py3.13.patch
  sed -i 's/if H5_VERS_MINOR > 10/if 0/g' src/ci/MEDfileCompatibility.c
  # install cmake files to /usr/lib${LIB_SUFFIX}/cmake
  sed -i 's,share/cmake,lib${LIB_SUFFIX}/cmake,g' CMakeLists.txt
}

build() {
  export CPPFLAGS+=' -DH5_USE_110_API'
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_SKIP_RPATH=ON \
    -D MEDFILE_BUILD_PYTHON=ON \
    -D MEDFILE_BUILD_TESTS=OFF \
    -D MEDFILE_INSTALL_DOC=OFF \
    -D MEDFILE_USE_MPI=ON \
    -G Ninja \
    -S med
  ninja -C build
}

check() {
  cd build
  LD_LIBRARY_PATH="$srcdir/build/src" ctest -E '._Python' --output-on-failure
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
