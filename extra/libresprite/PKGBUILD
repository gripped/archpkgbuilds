# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Eragon <eragon at eragon dot re>
# Contributor: MintStudios <mintstudios dot dev at gmail dot com>
# Contributor: Kuba Kowalik <bell@0x07.pl>

pkgname=libresprite
pkgver=1.2
pkgrel=2
pkgdesc='Animated sprite editor and pixel art tool, based on the last GPL2 commit of Aseprite'
arch=(x86_64)
url='https://github.com/LibreSprite/LibreSprite'
license=(GPL-2.0-only)
depends=(freetype2 giflib libjpeg-turbo libpng libwebp libxi pixman sdl2 sdl2_image tinyxml2)
makedepends=(cmake git ninja)
source=("$pkgname::git+$url#tag=v$pkgver"
        "simpleini::git+https://github.com/aseprite/simpleini#commit=c94997d4d64394833e4b9d21f4573c66600c9a9a"
        "duktape::git+https://github.com/libresprite/duktape#commit=6f715553e706b61e611aa4ae8e6fe90626800dae"
        "flic::git+https://github.com/aseprite/flic#commit=876ef60df5fec606f8eb0638ee893e4967db4673"
        "clip::git+https://github.com/aseprite/clip#commit=94693e2414a2c69a8ca16f065240c80a94cc6221"
        "undo::git+https://github.com/aseprite/undo#commit=f39b188e29d0f9adaa49c8705c0f492939d967a9")
b2sums=('024ff789c5db4a90b552dbede30edf742a67de12ef657f27d795725fa93e9aa221f443ca13426d0e2f92d6eb2d7ec5b1e55e6220a329b5c6de67c61422be4727'
        '1605b2800bfad15e5b9250d49118aca83d2040c1b389c9dd8659d2e75ed083cd597467a16ddf7905252e6970a47afc5468cbdb374d9147e35e3f62d618ed6ee1'
        '5f8cd378ea441c678afa7460d087ee05f94b355d779fc6b872c75142a77bf0965acc0996882f0e34b37e94099a47c83bc3d5eb8e4124e1ad176ad0dc7f30c63c'
        'e62e0dadfe48758108bd81364387c796eb9ccc26ff0da6cf6983c32bdc04c4449dbfcea998bf7092eda1c2320ee517427744ed73634a22ea735f79b9f2000ace'
        '05e1747ac77c4cea09a2a457a480a86d1931970c95d22d4e957b4d084ffa172a7bc86d6178a742687ce59697e65d42e760d422bb47f2421c542dfe2a5c309a12'
        '7718ca21369fb57940f9065f59f9899f5ef1cfd7363fa9bff197f9d9b7ce1d479c65e8eccd3538b8c38781b55f62ffad35560154c491e5c1357aaf30fac48044')

prepare() {
  cd $pkgname
  rm -r third_party/{duktape,simpleini} src/{clip,flic,undo}
  cp -r "$srcdir/duktape" third_party/
  cp -r "$srcdir/simpleini" third_party/
  cp -r "$srcdir/clip" src/
  cp -r "$srcdir/flic" src/
  cp -r "$srcdir/undo" src/
}

build() {
  mkdir -p build
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -D USE_SDL2_BACKEND=ON \
    -D WITH_WEBP_SUPPORT=ON \
    -D WITH_DESKTOP_INTEGRATION=ON \
    -S $pkgname
  make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install
}
