# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tombi
pkgver=0.7.25
pkgrel=1
pkgdesc='TOML Formatter / Linter / Language Server'
arch=(x86_64)
url=https://tombi-toml.github.io/tombi/
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://github.com/tombi-toml/tombi.git#tag=v$pkgver")
b2sums=('a45cbe9ec78fd43e7c2f95c6fe2327872580410e89e34e18f982c2e3223772584f0013469d325b60c85c223e8697cb078b63ebb4f20d9e5a2b002c7b0fd2a536')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
