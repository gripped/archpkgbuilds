# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tombi
pkgver=0.6.40
pkgrel=1
pkgdesc='TOML Formatter / Linter / Language Server'
arch=(x86_64)
url=https://tombi-toml.github.io/tombi/
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://github.com/tombi-toml/tombi.git#tag=v$pkgver")
b2sums=('15248a789e280091633d764a91d7f35c63e3a6618047f7cdc08362d3386140d6741b2784b7af04311fe27966a6e6e5ab43d2e4b6bf28b7955fc379056ed26d31')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
