# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tombi
pkgver=0.6.13
pkgrel=1
pkgdesc='TOML Formatter / Linter / Language Server'
arch=(x86_64)
url=https://tombi-toml.github.io/tombi/
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://github.com/tombi-toml/tombi.git#tag=v$pkgver")
b2sums=('4c2b732af0888bf5af46e6571c67b8f5e5f683731b44dfe2d51fc2d867f916f037aa0abe695ad7bafeb477907d836292165bf15b3ee0f6b32d8620509a92ffca')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
