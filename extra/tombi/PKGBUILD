# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tombi
pkgver=0.5.3
pkgrel=1
pkgdesc='TOML Formatter / Linter / Language Server'
arch=(x86_64)
url=https://tombi-toml.github.io/tombi/
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://github.com/tombi-toml/tombi.git#tag=v$pkgver")
b2sums=('84e2702fe08bec0c121b40705f520843dca33f4e9ac57c4596e1cdd7a97826b1281904c5ffec8534f8cbb11fd00d6b3f2cb337688f3b2188ba8c356074c3771f')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
