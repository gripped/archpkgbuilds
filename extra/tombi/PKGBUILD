# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tombi
pkgver=0.7.14
pkgrel=1
pkgdesc='TOML Formatter / Linter / Language Server'
arch=(x86_64)
url=https://tombi-toml.github.io/tombi/
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://github.com/tombi-toml/tombi.git#tag=v$pkgver")
b2sums=('c9c0b80756dd977e1c8b5fe214fc13c053025b1fe9a6b9e5d2dd8f1d4896b72a98dcb398c40b972f247a470789555d1f64313ab5af9a6c471c34323f853e0b7c')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
