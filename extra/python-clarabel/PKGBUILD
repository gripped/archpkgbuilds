# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=clarabel
pkgname=python-$_pyname
pkgver=0.7.0
pkgrel=1
pkgdesc='Interior-point solver for convex conic optimisation problems'
arch=(x86_64)
url='https://github.com/oxfordcontrol/Clarabel.rs'
license=(Apache)
depends=(gcc-libs
         glibc)
makedepends=(python-build
             python-installer
             python-maturin
             rust)
source=(https://github.com/oxfordcontrol/Clarabel.rs/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('397641bef5d0f1e626d47e187124f5108f72ff882af04682c5342376e4c7fc40')

prepare() {
  cd Clarabel.rs-$pkgver
  cargo generate-lockfile # https://github.com/oxfordcontrol/Clarabel.rs/pull/64
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd Clarabel.rs-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd Clarabel.rs-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
