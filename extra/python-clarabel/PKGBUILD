# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=clarabel
pkgname=python-$_pyname
pkgver=0.10.0
pkgrel=1
pkgdesc='Interior-point solver for convex conic optimisation problems'
arch=(x86_64)
url='https://github.com/oxfordcontrol/Clarabel.rs'
license=(Apache-2.0)
depends=(gcc-libs
         glibc)
makedepends=(git
             python-build
             python-installer
             python-maturin
             rust)
source=(git+https://github.com/oxfordcontrol/Clarabel.rs#tag=v$pkgver)
sha256sums=('1585bc4f605801a5716af3a6921387c231670189cfad3539de2f3037aa799ea8')

prepare() {
  cd Clarabel.rs
  cargo generate-lockfile # https://github.com/oxfordcontrol/Clarabel.rs/pull/64
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd Clarabel.rs
  python -m build --wheel --no-isolation
}

package() {
  cd Clarabel.rs
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
