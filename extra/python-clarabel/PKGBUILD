# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=clarabel
pkgname=python-$_pyname
pkgver=0.6.0.post1
pkgrel=1
pkgdesc='Interior-point solver for convex conic optimisation problems'
arch=(x86_64)
url='https://github.com/oxfordcontrol/Clarabel.rs'
license=(Apache)
depends=(gcc-libs
         glibc)
makedepends=(python-build
             python-installer
             python-maturin
             rust)
source=(https://github.com/oxfordcontrol/Clarabel.rs/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('a10d3636a9424b25221a0cf12f8f65427e95c5b305d8855970f97b2fd3c9561b')

prepare() {
  cd Clarabel.rs-$pkgver
  cargo generate-lockfile # https://github.com/oxfordcontrol/Clarabel.rs/pull/64
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd Clarabel.rs-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd Clarabel.rs-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
