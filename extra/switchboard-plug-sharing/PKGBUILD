# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sharing
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Sharing Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sharing
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gnome-user-share
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=488644b42d7e3ddbab2b8d9714d40ade3ee203b2
source=(git+https://github.com/elementary/switchboard-plug-sharing.git#tag=${_tag})
b2sums=('770b88e9e5f99c27a46786caabb6dc65fc1bd65b568e956a9a5d0f5038ab64987708b3a1425291af1c9c651be55afea0c93496c94f6e3625d1e746654a8a7d99')

pkgver() {
  cd switchboard-plug-sharing
  git describe --tags
}

build() {
  arch-meson switchboard-plug-sharing build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
