# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sharing
pkgver=8.0.1
pkgrel=1
pkgdesc='Switchboard Sharing Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sharing
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gnome-user-share
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=14ed1606ce3f52ec37b54deb1f49251f906cd306
source=(git+https://github.com/elementary/switchboard-plug-sharing.git#tag=${_tag})
b2sums=('2336bc09651b8b7dd0040a35337e3289e0c4587e67cf9a5fa5355ba7c1005f75e858a2cbe885a192baf91d24cc091475a2572f991aa7af84d586dea6ded0988a')

pkgver() {
  cd switchboard-plug-sharing
  git describe --tags
}

build() {
  arch-meson switchboard-plug-sharing build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
