# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sharing
pkgver=8.0.2
pkgrel=1
pkgdesc='Switchboard Sharing Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sharing
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gnome-user-share
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=427494869ab6c7787140cf07622a2087173f033a
source=(git+https://github.com/elementary/switchboard-plug-sharing.git#tag=${_tag})
b2sums=('8094254049284a26d528d9d1df1eb069f58662afcacdee5db067fc4138015dc62f47a086c60d75a91c381b14743353d17904790cbf47799780ae4fa285eaf99e')

pkgver() {
  cd switchboard-plug-sharing
  git describe --tags
}

build() {
  arch-meson switchboard-plug-sharing build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
