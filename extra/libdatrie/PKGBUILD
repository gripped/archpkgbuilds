# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Chaiwat Suttipongsakul <cwt114@gmail.com>

pkgname=libdatrie
pkgver=0.2.13
pkgrel=4
pkgdesc='Double-array trie library'
arch=(x86_64)
url='https://linux.thai.net/projects/datrie'
license=(LGPL-2.1-or-later)
depends=(glibc)
makedepends=(
  autoconf-archive
  doxygen
  git
)
source=("git+https://github.com/tlwg/libdatrie.git#tag=v$pkgver")
b2sums=(3f726110c37b379d0fbf7fff5de534e46da75642baa848f72514fc98e4af0a80d8b7f0bc1a5f797eeef6e4505bb839a65b9d44019b3523270a414df4363e7003)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
