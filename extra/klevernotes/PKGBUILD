# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=klevernotes
pkgver=1.2.5
pkgrel=2
pkgdesc='Note-taking and management application using markdown'
arch=(x86_64)
url='https://apps.kde.org/klevernotes/'
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  kcolorscheme
  kconfig
  kcoreaddons
  ki18n
  kiconthemes
  kio
  kirigami
  kirigami-addons
  kitemmodels
  qqc2-desktop-style
  qt6-base
  qt6-declarative
  qt6-webchannel
  qt6-webengine
)
makedepends=(
  cmake
  extra-cmake-modules
  git
  ninja
)
source=("git+https://invent.kde.org/office/klevernotes.git?signed#tag=v$pkgver")
b2sums=(cd7cc579553735981235fbca5b8e98682e96cbd4975d2921de73c9c2c9743ea06a0f7ff1b997c40021964dc7fff5e5486fd2ba434098912c10461aeb92b5238e)
validpgpkeys=(311DA4D94B93A7AEA0496DAF5F812C367AC0A802) # Louis Schul <schul9louis@gmail.com>

build() {
  cmake -S $pkgname -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
