# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Maxime Schmitt <maxime.schmitt91@gmail.com>

pkgname=nvtop
pkgver=3.3.1
pkgrel=1
pkgdesc="GPUs process monitoring for AMD, Intel and NVIDIA"
arch=('x86_64')
url="https://github.com/Syllo/nvtop"
license=('GPL-3.0-or-later')
depends=('ncurses' 'systemd-libs')
makedepends=('git' 'cmake' 'libdrm' 'systemd')
checkdepends=('gtest')
source=("git+https://github.com/Syllo/nvtop.git#tag=$pkgver")
sha512sums=('5a9f1932273b2a78a6d5114f8d93a3b00fa5bb6f0c185000859ef1b2b3b57d09b278da78233f29d957f4a5863d5de40a52466c77fe52bb40742d7adb9058307b')

build() {
  cd $pkgname

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DNVIDIA_SUPPORT=ON -DAMDGPU_SUPPORT=ON -DBUILD_TESTING=ON .
  make
}

check() {
  ctest --test-dir $pkgname --output-on-failure
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
