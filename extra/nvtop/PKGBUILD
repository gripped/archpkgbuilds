# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Maxime Schmitt <maxime.schmitt91@gmail.com>

pkgname=nvtop
pkgver=3.3.0
pkgrel=1
pkgdesc="GPUs process monitoring for AMD, Intel and NVIDIA"
arch=('x86_64')
url="https://github.com/Syllo/nvtop"
license=('GPL-3.0-or-later')
depends=('ncurses' 'systemd-libs')
makedepends=('git' 'cmake' 'libdrm' 'systemd')
checkdepends=('gtest')
source=("git+https://github.com/Syllo/nvtop.git#tag=$pkgver")
sha512sums=('38000dc7382b4ec6ccd498dc209773d9345c66b1c6dfaab181cf5370b98d27ad01d64d93e60de481f4d1d7b135f519837f85d2cb42b4c59b6727a02f13e09b85')

build() {
  cd $pkgname

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DNVIDIA_SUPPORT=ON -DAMDGPU_SUPPORT=ON -DBUILD_TESTING=ON .
  make
}

check() {
  ctest --test-dir $pkgname --output-on-failure
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
