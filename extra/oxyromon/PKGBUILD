# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=oxyromon
pkgver=0.20.2
pkgrel=1
pkgdesc='Rusty ROM OrgaNizer'
arch=(x86_64)
url=https://github.com/alucryd/oxyromon
license=(GPL-3.0-only)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bchunk
  cmake
  ctrtool
  dolphin-emu-tool
  git
  mame-tools
  maxcso
  p7zip
  qt6-base
  rust
  wit
  yarn
)
optdepends=(
  'bchunk: BIN to ISO support'
  'cdrtools: IRD support'
  'ctrtool: CIA support'
  'dolphin-emu-tool: RVZ support'
  'mame-tools: CHD support'
  'maxcso: CSO support'
  'p7zip: 7Z and ZIP support'
  'wit: WBFS support'
)
_tag=168af6e3764b32df6e17e651cfa6cdf6bdf8042b
source=(git+https://github.com/alucryd/oxyromon.git#tag=${_tag})
validpgpkeys=(95220BE99CE6FF778AE0DC670F65C7D881506130) # Maxime Gauduin <alucryd@archlinux.org>
b2sums=('30894057f0746a79c8df9c8b2c8723d6d12b491c9fdd6af085b8d479ce86d3c43024e87b556ec843517caaa8d34b4c822773db9078c2265d37183f287df841c5')
options=(!lto)

prepare() {
  cd oxyromon
  yarn install \
    --frozen-lockfile
  cargo fetch \
    --locked
}

pkgver() {
  cd oxyromon
  git describe --tags
}

build() {
  cd oxyromon
  yarn build
  cargo build \
    --features server \
    --release \
    --frozen
}

check() {
  cargo test \
    --features server \
    --release \
    --frozen \
    --manifest-path oxyromon/Cargo.toml
}

package() {
  cargo install \
    --features server \
    --frozen \
    --offline \
    --no-track \
    --path oxyromon \
    --root "${pkgdir}"/usr
}

# vim: ts=2 sw=2 et:
