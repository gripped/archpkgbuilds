# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=oxyromon
pkgver=0.20.1
pkgrel=1
pkgdesc='Rusty ROM OrgaNizer'
arch=(x86_64)
url=https://github.com/alucryd/oxyromon
license=(GPL3)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bchunk
  cmake
  ctrtool
  dolphin-emu-tool
  git
  mame-tools
  maxcso
  p7zip
  qt6-base
  rust
  wit
  yarn
)
optdepends=(
  'bchunk: BIN to ISO support'
  'cdrtools: IRD support'
  'ctrtool: CIA support'
  'dolphin-emu-tool: RVZ support'
  'mame-tools: CHD support'
  'maxcso: CSO support'
  'p7zip: 7Z and ZIP support'
  'wit: WBFS support'
)
_tag=9e8da505458de2a2447142845305b1fa2b70a611
source=(git+https://github.com/alucryd/oxyromon.git#tag=${_tag})
validpgpkeys=(95220BE99CE6FF778AE0DC670F65C7D881506130) # Maxime Gauduin <alucryd@archlinux.org>
b2sums=('1791bf25f5c906f5c3a80d331d8746ddabd2a4b3f2ad7c2a3dd0e5d0efaaef8616b3f574786ba760aa86ea211835ccec10389e168d2577fa41b016b1db6b7bc8')
options=(!lto)

prepare() {
  cd oxyromon
  yarn install \
    --frozen-lockfile
  cargo fetch \
    --locked
}

pkgver() {
  cd oxyromon
  git describe --tags
}

build() {
  cd oxyromon
  yarn build
  cargo build \
    --features server \
    --release \
    --frozen
}

check() {
  cargo test \
    --features server \
    --release \
    --frozen \
    --manifest-path oxyromon/Cargo.toml
}

package() {
  cargo install \
    --features server \
    --frozen \
    --offline \
    --no-track \
    --path oxyromon \
    --root "${pkgdir}"/usr
}

# vim: ts=2 sw=2 et:
