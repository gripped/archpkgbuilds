# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Fabiano Furtado < fusca14 (at) gmail (dot) com >
# Contributor: Manuel Barrio Linares <mbarriolinares at gmail dot com>

pkgname=wifite
pkgver=2.8.2
pkgrel=2
epoch=2
pkgdesc='Tool to attack multiple WEP and WPA encrypted networks at the same time'
url='https://github.com/kimocoder/wifite2'
arch=('any')
license=('GPL-2.0-only')
depends=(
  python
  python-chardet
  python-requests
  python-psutil
  aircrack-ng
  iproute2
  iw)
makedepends=(
  python-build
  python-installer
  python-poetry-core
)
optdepends=(
  'wireshark-cli: detecting WPS networks and inspecting handshake capture files'
  'reaver: WPS Pixie-Dust & brute-force attacks'
  'bully: WPS Pixie-Dust & brute-force attacks'
  'cowpatty: detect WPA handshakes'
  'pyrit: detect WPA handshakes'
  'macchanger: change MAC for attacks'
  'hashcat: cracking PMKID hashes'
  'hcxdumptool: capturing PMKID hashes'
  'hcxtools: converting PMKID packet captures into hashcat format'
  'john: For CPU (OpenCL)/GPU cracking passwords fast'
)
provides=(wifite2)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "pyproject.patch")
sha512sums=('93e2fa2fb9629ddfa2d7c40ed48a0a2c15ca075a45700d8fdadedb0aa199435c9726a5f511fcb70beb2c1be812fee32f3c28e28f03a52470c73b6100c3022e2f'
            '8ec9d789bc1e11c7cbcd6fe207775cc62cced24ed1e4b5cdf2c84f79b313c91b588e27ca9d9cb71c67b3ead858a5aca6b39c7444f3e5043d216169ebfbda0331')
b2sums=('a102712fe7749d8f41e0b1a8b45ea0a06566a8115a66a1faf6ec3be5479caa37a6a9df58bb6985131ca58ee57394b7f8ee746f3762213680d8c6ed9addcfa4f2'
        'e09580258e860d59c3f2efd2f29bc36dce4dce4a4b291c2784654fda08ace72ed53131426fd30a76dfe7f5cfa89ac61db16d777497d8424a2578e59a35fb06df')

prepare() {
  cd "${pkgname}2-${pkgver}"
  sed 's|sbin|bin|g' -i setup.cfg

  # Apply the Poetry entry-point patch
  # Backport of upstream pyproject changes from
  # https://github.com/kimocoder/wifite2/commit/c367565d978cee53f81de0975453b0bf10c71374  
  patch -p1 -i "${srcdir}/pyproject.patch"
}

build() {
  cd "${pkgname}2-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname}2-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 wordlist-probable.txt -t "${pkgdir}/usr/share/dict/"
}

# vim: ts=2 sw=2 et:
