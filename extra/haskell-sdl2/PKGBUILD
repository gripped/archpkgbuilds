# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=sdl2
pkgname=haskell-sdl2
pkgver=2.5.5.1
pkgrel=14
pkgdesc="Both high- and low-level bindings to the SDL library"
url="https://github.com/haskell-game/sdl2.git"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'sdl2' 'haskell-statevar' 'haskell-linear' 'haskell-vector')
makedepends=('ghc' 'pulseaudio' 'xorg-server-xvfb' 'haskell-weigh')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('25b3e2410129c16c2cea56137319206fa1b792dec6634f07a7ea4d9031ffc0cc')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --datasubdir=haskell-sdl2 --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -f-examples -f-no-linear -f-opengl-example -frecent-ish -fpkgconfig

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # TODO: what happened with sdl2?
  xvfb-run runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
