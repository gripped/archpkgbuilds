# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-data
pkgname=haskell-generic-data
pkgver=1.1.0.1
pkgrel=22
pkgdesc="Deriving instances with GHC.Generics and related utilities"
url="https://github.com/Lysxia/generic-data"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ap-normalize' 'haskell-base-orphans' 'haskell-contravariant'
         'haskell-show-combinators')
makedepends=('ghc' 'haskell-generic-lens' 'haskell-glob' 'haskell-one-liner'
             'haskell-show-combinators' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0c34e611d9b0fcf4489c48f0e319b792f8554f5cd1b4ee8ae8b409989f920acc2c5485ee591b26644cb006da2f324c731a12e961e75e2b0a5863d51d8b1c9996')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
