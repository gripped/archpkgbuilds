# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Dct Mei <dctxmei@yandex.com>
# Contributor: Eric S. Londres <elondres@stevens.edu>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cillian Berragan <cjberragan@gmail.com>
# Contributor: Felix Golatofski <contact@xdfr.de>

pkgname=nyxt
pkgver=3.11.7
pkgrel=1
pkgdesc='A keyboard-driven web browser designed for power users'
arch=('x86_64')
url='https://nyxt.atlas.engineer'
license=('BSD-3-Clause' 'CC-BY-SA-3.0')
depends=(
  'glibc'
  'hicolor-icon-theme'
  'enchant'
  'glib-networking'
  'gobject-introspection-runtime'
  'gsettings-desktop-schemas'
  'libfixposix'
  'webkit2gtk-4.1'
  'zstd'
)
# If someday Nyxt works with other Lisps, replace 'sbcl' with 'common-lisp'.
# NOTE: sbcl provides both common-lisp and cl-asdf.
makedepends=(
  'git'
  'sbcl'
  'cl-asdf'
)
optdepends=(
  'gstreamer: for HTML5 audio/video'
  'gst-plugins-base: for HTML5 audio/video'
  'gst-plugins-good: for HTML5 audio/video'
  'gst-plugins-bad: for HTML5 audio/video'
  'gst-plugins-ugly: for HTML5 audio/video'
)
options=('!strip' '!makeflags')
source=(
  "$pkgname-$pkgver.tar.xz::https://github.com/atlas-engineer/nyxt/releases/download/$pkgver/nyxt-$pkgver-source-with-submodules.tar.xz"
  'fix-ftbfs-sb-ext.patch'
)
sha512sums=('853cb4ec7fe0e04a9f4672170965d6e71c9c81d77382b08f3abe8bb68ce6d44b6ba2a8c3ed6d7d47b2a0a6a92cc3c312189ea7a0635f1a7a3c1ebcea8f710a40'
            'f4b3113ea63f6f678bf8915f07e8fb9df9600c6b509f35d278d2a392781e8683c62265987ff5caa59b6800bb985f175a307db541d3a59a84366a3620b91d4ecd')
b2sums=('6e7dbe65601525fafa493cc86689062a3157812f8d7991bee42926291efeaddb699596d6eaa7207c633ec2e6beb3cd32884ba1ab0990aafeace8080b5cb242c1'
        '1f0642fa26e0e9bc1393130a3b9cc9894e8b2e4514b923a83838f8a0f0a3f84157272f680c7f4221f865b4c831b486d8ea72a0d04fb82ebd7360aaddeec58b78')

prepare() {
  # FTBFS due to sbcl 2.45
  # https://github.com/atlas-engineer/nyxt/issues/3408
  patch -p1 -i "$srcdir/fix-ftbfs-sb-ext.patch"
}

build() {
  make all
}

package() {
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" licenses/*
}
