# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Omar Sandoval <osandov at osandov dot com>

pkgname=libkdumpfile
pkgver=0.5.6
pkgrel=1
pkgdesc='Library for kernel core dump file access'
arch=('x86_64')
url='https://codeberg.org/ptesarik/libkdumpfile'
license=('LGPL-3.0-or-later OR GPL-2.0-or-later')
depends=(
    'binutils'
    'glibc'
    'lzo'
    'snappy'
    'zlib'
    'zstd'
)
makedepends=(
    'git'
)
source=(git+${url}.git#tag=v${pkgver})
sha256sums=('5664e4e9801713bd78ab96811c20562cd95747208075532ef883ec2bc4800980')

prepare() {
    cd "$pkgname"
    autoreconf -fiv
}

build() {
    cd "$pkgname"
    ./configure --prefix=/usr --without-python
    make
}

check() {
    cd "$pkgname"
    make check
}

package() {
    cd "$pkgname"
    make DESTDIR="$pkgdir" install
}
