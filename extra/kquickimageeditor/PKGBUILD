# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kquickimageeditor
pkgver=0.3.0
pkgrel=1
pkgdesc='QML image editing components'
license=(LGPL)
arch=(x86_64)
url='https://invent.kde.org/libraries/kquickimageeditor'
depends=(gcc-libs
         glibc
         qt5-base
         qt5-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('330ebf3653883eeebde54385d783c51c2d30ff77d41b294ece79af7e9c21c6f8'
            'SKIP')
validpgpkeys=(39FFA93CAE9C6AFC212AD00202325448204E452A) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
