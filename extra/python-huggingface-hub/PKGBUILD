# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Congributor: Butui Hu <hot123tea123@gmail.com>

_name=huggingface_hub
pkgname=python-huggingface-hub
pkgver=0.29.0
pkgrel=1
epoch=1
pkgdesc='All the open source things related to the Hugging Face Hub'
arch=(any)
url='https://github.com/huggingface/huggingface_hub'
license=(Apache-2.0)
groups=(huggingface)
depends=(
  python
  python-aiohttp
  python-filelock
  python-fsspec
  python-numpy
  python-packaging
  python-pydantic
  python-requests
  python-tqdm
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  git
  git-lfs
  python-fastapi
  python-jedi
  python-jinja
  python-pillow
  python-pytest
  python-pytest-asyncio
  python-pytest-env
  python-pytest-mock
  python-pytest-vcr
)
optdepends=(
  python-gradio
  python-graphviz
  python-hf-transfer
  python-inquirerpy
  python-jinja
  python-pillow
  python-pydot
  python-pytorch
  python-safetensors
  python-tensorflow
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('cdcda0ee263bd7dc27c729becde0b1fdf2b6292ca68ebe5a5e009e395bdbb8265188501ff8e38e4b0b62780408ffc88b6b1806c893bcc6535eeb478575013139')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --override-ini="addopts="
    -W ignore::DeprecationWarning
    # ignore test which requires python-inquirerpy which is not packaged
    --ignore tests/test_command_delete_cache.py
    # skip tests that connect to the real HuggingFace Hub
    # https://github.com/huggingface/huggingface_hub/issues/2820
    --deselect tests/test_hf_api.py
    --deselect tests/test_cache_no_symlinks.py
    --deselect tests/test_file_download.py
    --deselect tests/test_utils_cache.py
    # skip hanging test https://github.com/huggingface/huggingface_hub/issues/1973
    --deselect tests/test_repository.py::TestRepositoryShared::test_clone_from_missing_repo
    # skip tests that require gradio but are not marked as such
    --deselect tests/test_webhooks_server.py::test_deserialize_payload_example_with_comment
    --deselect tests/test_webhooks_server.py::test_deserialize_payload_example_without_comment
    --deselect tests/test_webhooks_server.py::test_deserialize_payload_example_with_updated_refs
    # skip failing test
    --deselect tests/test_inference_client.py::TestInferenceClient::test_text_to_image_default[nebius,text-to-image]
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
