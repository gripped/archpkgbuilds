# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Congributor: Butui Hu <hot123tea123@gmail.com>

_name=huggingface_hub
pkgname=python-huggingface-hub
pkgver=0.33.5
pkgrel=1
epoch=1
pkgdesc='All the open source things related to the Hugging Face Hub'
arch=(any)
url='https://github.com/huggingface/huggingface_hub'
license=(Apache-2.0)
groups=(huggingface)
depends=(
  python
  python-aiohttp
  python-filelock
  python-fsspec
  python-hf-xet
  python-numpy
  python-packaging
  python-pydantic
  python-requests
  python-tqdm
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  git
  git-lfs
  python-authlib
  python-fastapi
  python-httpx
  python-itsdangerous
  python-jedi
  python-jinja
  python-pillow
  python-pytest
  python-pytest-asyncio
  python-pytest-env
  python-pytest-mock
  python-pytest-vcr
  python-pytest-xdist
)
optdepends=(
  python-authlib
  python-gradio
  python-graphviz
  python-hf-transfer
  python-httpx
  python-inquirerpy
  python-jinja
  python-pillow
  python-pydot
  python-pytorch
  python-safetensors
  python-tensorflow
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('dd6be6079e2fd968d5f1eb4e44ee5fc328ace9d7a415d9f5c40ec1bff087b37587b3c023706fc62100b792c86db56de973e955fd8aedaab4fd19b0d2ca454c39')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --override-ini="addopts="
    -W ignore::DeprecationWarning
    # distribute tests across multiple CPUs
    -n auto
    # ignore test which requires python-inquirerpy which is not packaged
    --ignore tests/test_command_delete_cache.py
    # skip tests that connect to the real HuggingFace Hub
    # https://github.com/huggingface/huggingface_hub/issues/2820
    --deselect tests/test_cache_no_symlinks.py
    --deselect tests/test_file_download.py
    --deselect tests/test_hf_api.py
    --deselect tests/test_hf_file_system.py
    --deselect tests/test_inference_client.py
    --deselect tests/test_repocard.py
    --deselect tests/test_repository.py
    --deselect tests/test_snapshot_download.py
    --deselect tests/test_utils_cache.py
    # skip hanging test https://github.com/huggingface/huggingface_hub/issues/1973
    --deselect tests/test_repository.py::TestRepositoryShared::test_clone_from_missing_repo
    # skip tests that require gradio but are not marked as such
    --deselect tests/test_webhooks_server.py::test_deserialize_payload_example_with_comment
    --deselect tests/test_webhooks_server.py::test_deserialize_payload_example_without_comment
    --deselect tests/test_webhooks_server.py::test_deserialize_payload_example_with_updated_refs
    # EOFError: Ran out of input
    --deselect tests/test_utils_strict_dataclass.py::test_autocompletion_init_with_kwargs
    --deselect tests/test_utils_strict_dataclass.py::test_autocompletion_init_without_kwargs
    --deselect tests/test_utils_strict_dataclass.py::test_autocompletion_attribute_with_kwargs
    --deselect tests/test_utils_strict_dataclass.py::test_autocompletion_attribute_without_kwargs
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
