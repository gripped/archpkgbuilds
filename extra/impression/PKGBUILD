# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=impression
pkgver=3.5.4
pkgrel=1
pkgdesc='Application to create bootable drives from disk images'
arch=(x86_64)
url='https://apps.gnome.org/Impression/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  udisks2
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/adhami3310/Impression.git#tag=v$pkgver")
b2sums=('6adae016edb940555baa82321fd8b60b34d513e7fe8d947f23de5ab6a5aa5e339e1ae95293766dcf96a369b48d4dc85cfa470c5c190bd0d7c2a88aa34f98778f')

prepare() {
  cd Impression

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Impression build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
