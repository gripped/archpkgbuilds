# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=impression
pkgver=3.3.0
pkgrel=2
pkgdesc='Application to create bootable drives from disk images'
arch=(x86_64)
url='https://apps.gnome.org/Impression/'
license=(GPL-3.0-only)
depends=(
  dbus
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  udisks2
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/adhami3310/Impression.git#tag=v$pkgver")
b2sums=(1ca5e4e7ec12520338bfea5cf8e67cdada8af783ba690cb2150e71600e422ecc42d478a50e0dd996d9c800a0f41a6c0485e5ae458db93dee0bbb6039929323cf)

prepare() {
  cd Impression

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Impression build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
