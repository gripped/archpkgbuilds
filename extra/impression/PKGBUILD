# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=impression
pkgver=3.5.5
pkgrel=1
pkgdesc='Application to create bootable drives from disk images'
arch=(x86_64)
url='https://apps.gnome.org/Impression/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  udisks2
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/adhami3310/Impression.git#tag=v$pkgver")
b2sums=(8085e9c9b5554fa0eaa74a3c40fcb4519fcd55ffd90304453b89d1354e9705e587000409fc10eb708ca1583722ed7ecd26398b83e3205a82ecb6fa97340aac32)

prepare() {
  cd Impression

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  arch-meson Impression build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
