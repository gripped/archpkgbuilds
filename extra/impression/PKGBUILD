# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=impression
pkgver=3.4.0
pkgrel=1
pkgdesc='Application to create bootable drives from disk images'
arch=(x86_64)
url='https://apps.gnome.org/Impression/'
license=(GPL-3.0-only)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  udisks2
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/adhami3310/Impression.git#tag=v$pkgver")
b2sums=(586f0a8db239e43d00e0ec9d90d1f1cda88aac29cf6ad51c603c33d696197bdfb1c686881bfab88421ec9ed2e392260006f5b6f518e89b6ca620fad5a1254546)

prepare() {
  cd Impression

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.com/adhami3310/Impression/-/merge_requests/90
  git cherry-pick -n a19efc547eb78d4b5f959e10079606a5a55cf72f

  # https://gitlab.com/adhami3310/Impression/-/merge_requests/91
  git cherry-pick -n 5ac98bb2aec7492a8fbf6685b6289d908c890e6d
}

build() {
  arch-meson Impression build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
