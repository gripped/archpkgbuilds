# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=impression
pkgver=3.5.3
pkgrel=1
pkgdesc='Application to create bootable drives from disk images'
arch=(x86_64)
url='https://apps.gnome.org/Impression/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  udisks2
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/adhami3310/Impression.git#tag=v$pkgver")
b2sums=(9408d8d2f8faf4786d94e0e4ac57d60f05b1b3e7d454c2682e9b3597b29d4407ca9b9153a3fdddfea53ea502f362761c1a5f28b2c5d9e9112346206b12316e04)

prepare() {
  cd Impression

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Impression build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
