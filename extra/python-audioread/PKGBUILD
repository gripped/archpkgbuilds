# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>
# Contributor: Jose Riha <jose1711i@gmail.com>

pkgname=python-audioread
pkgver=3.0.1
pkgrel=3
pkgdesc="Cross-library (GStreamer + Core Audio + MAD + FFmpeg) audio decoding for Python"
arch=(any)
url=https://github.com/beetbox/audioread
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-flit-core
  python-gobject
  python-installer
  python-pymad
  python-wheel
)
optdepends=(
  'gst-python: GStreamer backend'
  'python-gobject: GStreamer backend'
  'python-pymad: MAD backend'
)
_tag=724b884c0083edb753a3d473164922d75d7d3beb
source=(git+https://github.com/beetbox/audioread.git#tag=${_tag})
b2sums=('4585f05c0b7e4769cf6fd8f9745ac7f00083cfe7267097ab6a260bdd6a696e9e8b29eb616b1b78f38430037771e202413173108290cee4b0983511744a0c1827')

build() {
  cd audioread
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" audioread/dist/*.whl
  install -Dm 644 audioread/LICENSE -t "${pkgdir}"/usr/share/licenses/python-audioread/
}

# vim:set ts=2 sw=2 et:
