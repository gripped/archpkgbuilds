# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Contributor: Aaron McDaniel (mcd1992) <'aur' at the domain 'fgthou.se'>

pkgname=rz-cutter
_gitcommit=209c26b862ab0bb3b7308618243a9796d3f1109a
pkgver=2.3.4
pkgrel=2
pkgdesc='Qt and C++ GUI for rizin reverse engineering framework'
url='https://github.com/rizinorg/cutter'
arch=('x86_64')
license=('GPL3')
depends=(
  rizin
  capstone
  qt6-base
  qt6-svg
  qt6-5compat
  python
  pyside6
  graphviz
  gcc-libs
  syntax-highlighting
)
makedepends=('git' 'cmake' 'ninja' 'shiboken6' 'qt6-tools')
optdepends=('rz-ghidra: ghidra decompiler plugin')
source=("git+https://github.com/rizinorg/cutter#commit=${_gitcommit}"
        "git+https://github.com/rizinorg/cutter-translations"
         pyside-6.7.patch)
sha512sums=('4b3e053eb1914ffed353c5534b65a305b13af043336ba409def663824767b6f36d78e47a2fe573babfa7a8cfc3dda609a18d4268d5b9416e49be890c8144c40f'
            'SKIP'
            'e5024c972d3df603a9a404b6214b7e8622a1e06bfdd02f15304404e75b6eecefc05c032dd738b34d534851c6ba2235ac02463c6207eecae145252b4ef44502ac')
b2sums=('450145432e0d09c482d9ead14d57bf94ef32843c99538e8c53264e1952c2b58bfb7954f89555c46d80d09a5cd154af5ba8385f6763175df3fa6f17eb7bc788a8'
        'SKIP'
        'abba422f724da8aedef314aabebd301f799c26e2f5239c1bcba402de3e78e7a9d2ba6256e6aef17c8f37dfa1cea999b6728b97c833ff86065e72b7343cc70d15')

pkgver() {
  cd cutter
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd cutter
  git config 'submodule.src/translations.url' "${srcdir}/cutter-translations"
  git -c protocol.file.allow=always submodule update --init src/translations

  patch -p1 -i ../pyside-6.7.patch # Fix build with PySide 6.7
}

build() {
  cd cutter
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCUTTER_ENABLE_PYTHON=ON \
    -DCUTTER_ENABLE_PYTHON_BINDINGS=ON \
    -DCUTTER_USE_BUNDLED_RIZIN=OFF \
    -DCUTTER_USE_ADDITIONAL_RIZIN_PATHS=OFF \
    -DCUTTER_ENABLE_CRASH_REPORTS=OFF \
    -DCUTTER_ENABLE_GRAPHVIZ=ON \
    -DCUTTER_QT6=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd cutter
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
