# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: İrem Ünlü < betulunlu0018 ~at~ gmail ~dot~ com >

pkgname=evcxr_repl
_pkgname=evcxr
pkgver=0.21.0
pkgrel=1
pkgdesc="A Rust REPL based on evcxr"
arch=('x86_64')
url="https://github.com/evcxr/evcxr"
license=('Apache-2.0')
depends=('gcc-libs' 'rust-src')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('d22511cbb25ce5c8af6ce0b32ced94110b1651bb9445a9e00c7199862b74e093')
options=('!lto')

prepare() {
  cd "$_pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --release --frozen --package "$pkgname"
}

check() {
  cd "$_pkgname-$pkgver"
  cargo test --frozen --package "$pkgname"
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
