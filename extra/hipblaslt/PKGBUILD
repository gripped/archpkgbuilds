# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
pkgname=hipblaslt
pkgver=6.3.2
pkgrel=1
pkgdesc='General matrix-matrix operations beyond a traditional BLAS library'
arch=('x86_64')
url='https://rocm.docs.amd.com/projects/hipBLASLt/en/latest/index.html'
license=('MIT')
depends=('rocm-core' 'glibc' 'gcc-libs' 'hip-runtime-amd' 'hipblas' 'rocblas')
makedepends=('rocm-cmake' 'python' 'cmake' 'git'
             'msgpack-cxx' 'python-msgpack' 'python-joblib' 'python-pyaml' 'hipblas-common')
_git='https://github.com/ROCm/hipBLASLt'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz"
        "$pkgname-find-msgpack5.patch"
        "$pkgname-fix-wrong-amdclang-path.patch"
       )
sha256sums=('cc4875b1a5cf1708a7576c42aff6b4cb790cb7337f5dc2df33119a4aadcef027'
            '8e6d134a1d45de814099dfe591296de5e1d47f9906ccf9c51d7f2228e992c5f4'
            '336455e150798d7bbbb54dab6c5dc850e17346ae4bed5500723592633f232993')
_dirname="$(basename "$_git")-$(basename "${source[0]}" ".tar.gz")"

prepare() {
  cd "$_dirname"
  patch -Np1 -i "$srcdir/$pkgname-find-msgpack5.patch"
  patch -Np1 -i "$srcdir/$pkgname-fix-wrong-amdclang-path.patch"
}

build() {   
  export PATH="/opt/rocm/lib/llvm/bin:$PATH"
  local cmake_args=(
    -Wno-dev
    -B build
    -S "$_dirname"
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_C_COMPILER=/opt/rocm/lib/llvm/bin/amdclang
    -D CMAKE_CXX_COMPILER=/opt/rocm/lib/llvm/bin/amdclang++
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
    -D Tensile_CODE_OBJECT_VERSION=default
  )
  # -fcf-protection is not supported by HIP, see
  # https://rocm.docs.amd.com/projects/llvm-project/en/latest/reference/rocmcc.html#support-status-of-other-clang-options
  CXXFLAGS="${CXXFLAGS} -fcf-protection=none" cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$_dirname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
