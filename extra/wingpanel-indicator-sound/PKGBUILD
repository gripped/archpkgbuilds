# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-sound
pkgver=8.0.0
pkgrel=1
pkgdesc='Wingpanel Sound Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-sound
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libcanberra
  libgee
  libgranite.so
  libnotify
  libpulse
  libwingpanel.so
)
makedepends=(
  git
  gobject-introspection
  intltool
  meson
  vala
)
_tag=f9742a5ccd6980a401d4b66da05b10d7593e12de
source=(wingpanel-indicator-sound::git+https://github.com/elementary/wingpanel-indicator-sound.git#tag=${_tag})
b2sums=('0a9620ba978c243c02e4351604ea0ca0a8b262fde85e65d26a6c58f3c2bca74c140b6e90347fbff63729390462c4c1f5f47ed78d790868285c9881e7180d3aac')

pkgver() {
  cd wingpanel-indicator-sound
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
