# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-sound
pkgver=8.0.1
pkgrel=1
pkgdesc='Wingpanel Sound Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-sound
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libcanberra
  libgee
  libgranite.so
  libnotify
  libpulse
  libwingpanel.so
)
makedepends=(
  git
  gobject-introspection
  intltool
  meson
  vala
)
_tag=0a5960bbb4dcd7454d3ee47e6defbc1684301077
source=(wingpanel-indicator-sound::git+https://github.com/elementary/wingpanel-indicator-sound.git#tag=${_tag})
b2sums=('944ab0c9a9e2692eeca64edbfd38f9f02df11466a55e70f2d0533462f7ca078093f6844c0290b30d000d34edad795a06bd3c66b518c3f1e9ccc54d21069ec77e')

pkgver() {
  cd wingpanel-indicator-sound
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
