# Maintainer: Konstantin Gizdov < arch at kge dot pw >
# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Contributor: Jan Kašpar < jan.kaspar at gmail dot com >
# Contributor: Alex Pearce < alex at alexpearce dot me >

pkgname=xrootd
pkgdesc="Software framework for fast, low latency, scalable and fault tolerant data access."
pkgver=5.7.2
pkgrel=1
arch=(x86_64)
url="https://xrootd.slac.stanford.edu/"
license=(LGPL-3.0-or-later)
depends=(
  bash
  curl
  davix
  e2fsprogs libcom_err.so
  fuse2
  gcc-libs
  glibc
  krb5 libkrb5.so
  libxcrypt libcrypt.so
  libxml2 libxml2.so
  openssl libssl.so libcrypto.so
  perl
  python
  readline libreadline.so
  systemd-libs libsystemd.so
  tinyxml
  util-linux-libs libuuid.so
  zlib libz.so
)
makedepends=(
  cmake
  git
  gtest
  ninja
  python-pip
  python-setuptools
  yasm
)
checkdepends=(
  procps-ng  # some tests use ps
)
source=("$pkgname::git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver")
b2sums=('f5bcee8c9d592a1e961bca7b31b613e060585e7d514dce8f427c96e6ffb40a62454226326be898c666bcef6b427a0e7d0db20f37339c12e617307d7233764c24')

build() {
  local cmake_options=(
    -B build
    -S $pkgname
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_LIBDIR=lib
    # fail CMake configuration if enabled components cannot be built
    -D FORCE_ENABLED=ON
    -D ENABLE_TESTS=ON
    # disable plugins that have unpackaged dependencies
    -D ENABLE_MACAROONS=OFF
    -D ENABLE_SCITOKENS=OFF
    -D ENABLE_VOMS=OFF
    -D USE_SYSTEM_ISAL=OFF
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  local excluded_tests=""
  local ctest_flags=(
    --test-dir build
    # show the stdout and stderr when the test fails
    --output-on-failure
    # execute tests in parallel
    --parallel $(nproc)
    # exclude problematic tests
    --exclude-regex "$excluded_tests"
  )
  ctest "${ctest_flags[@]}"
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
