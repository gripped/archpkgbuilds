# Maintainer: David Runge <dvzrv@archlinux.org>

_name=authheaders
pkgname=python-authheaders
pkgver=0.15.3
pkgrel=1
pkgdesc="Library for the generation of email authentication headers"
arch=(any)
url="https://github.com/ValiMail/authentication-headers"
license=(
  MPL-2.0
  ZPL-2.1
  Zlib
)
depends=(
  publicsuffix-list
  python
  python-authres
  python-dkim
  python-dnspython
  python-publicsuffix2
  python-setuptools  # used via pkg_resources
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(python-pytest)
source=(
  $url/releases/download/$pkgver/$_name-$pkgver.tar.gz{,.asc}
  psddmarc.csv::https://www.psddmarc.org/psddmarc-participants.csv
)
sha512sums=('e45af947faeb6416160b1b0ee270fb486c3f2d0853ce886e4e66fd57a814838213d73515417f4be25cf3d2e29faa20f183dae7e083aff14bf4c390c0ecddc2f2'
            'SKIP'
            'f3840255c1901111a563ca87ae155853d7e00d82a7e479520b789779c3c0d7273123a52abb419085d0561f25ffdaca70de76167935189a1cc20189d1f2f60b8a')
b2sums=('aca9ff0481c0517d7b2531d7a1860bb6288b094dcceae6bb465c9cde8edb7aa6a14aba4ccf8b7ab7d3e13ada51dd2f7baaff6e4406c0d04c4898c0f9ce234c5b'
        'SKIP'
        'e86e6add9beac29a86249b884dde7a28b996df0ae8c88b986c8ca2b8b20c58f8a6dfcfe6c149e03dfd8472b8612ac9baac43a46c78d13266cf95138f6dec594e')
validpgpkeys=('E7729BFFBE85400FEEEE23B178D7DEFB9AD59AF1') # Donald Scott Kitterman <scott@kitterman.com>

prepare() {
  cd $_name-$pkgver
  # embed psddmarc.csv: https://github.com/ValiMail/authentication-headers/issues/4
  cp -av ../psddmarc.csv $_name/
  printf 'location = "/usr/share/publicsuffix/public_suffix_list.dat"\n' > $_name/findpsl.py
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  local python_stdlib_basepath="$(python -c "from sysconfig import get_path; print(get_path('stdlib'))")"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
