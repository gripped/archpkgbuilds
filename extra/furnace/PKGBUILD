# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=furnace
pkgver=0.6.7
pkgrel=2
pkgdesc='A multi-system chiptune tracker compatible with DefleMask modules'
arch=('x86_64')
url='https://github.com/tildearrow/furnace'
license=('GPL-2.0-or-later')
groups=('pro-audio')
depends=(
  'fftw'
  'fmt'
  'freetype2'
  'glibc'
  'gcc-libs'
  'hicolor-icon-theme'
  'libglvnd'
  'libsndfile'
  'portaudio'
  'rtmidi'
  'sdl2'
  'zlib'
)
makedepends=(
  'cmake'
  'git'
  'jack'
)
optdepends=('jack: JACK audio support')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-superctr-adpcm::git+https://github.com/superctr/adpcm'
)
sha512sums=('43167c7a036607a051734c13cc3f8b3924fb1795989aefa7377483daeb0b5d49833533d420d160375884fd2b2f457aa069af31bc8120ceaa344e4f46660f8b44'
            'SKIP')
b2sums=('953a199c028a4f8893b181d22b45f6badf2d10fb34f145119480925f031c89b4883fd538eb637cd9adb9470fae0a7d019119df88fad2d10177bfe29a2fb800c9'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodules
  git submodule init extern/adpcm
  git config submodule.extern/adpcm.url "$srcdir/github.com-superctr-adpcm"
  git -c protocol.file.allow=always submodule update
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_GUI=ON
    -D SYSTEM_FFTW=ON
    -D SYSTEM_FMT=ON
    -D SYSTEM_FREETYPE=ON
    -D SYSTEM_LIBSNDFILE=ON
    -D SYSTEM_PORTAUDIO=ON
    -D SYSTEM_RTMIDI=ON
    -D SYSTEM_SDL2=ON
    -D SYSTEM_ZLIB=ON
    -D WITH_JACK=ON
    -S "$pkgname"
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build  
}
