# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=furnace
pkgver=0.6.8
pkgrel=1
pkgdesc='A multi-system chiptune tracker compatible with DefleMask modules'
arch=('x86_64')
url='https://github.com/tildearrow/furnace'
license=('GPL-2.0-or-later' 'LicenseRef-OriginalAuthors')
groups=('pro-audio')
depends=(
  'fftw'
  'fmt'
  'freetype2'
  'glibc'
  'gcc-libs'
  'hicolor-icon-theme'
  'libglvnd'
  'libsndfile'
  'portaudio'
  'rtmidi'
  'sdl2'
  'zlib'
)
makedepends=(
  'cmake'
  'git'
  'jack'
)
optdepends=('jack: JACK audio support')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-superctr-adpcm::git+https://github.com/superctr/adpcm'
)
sha512sums=('10a3feefa3f5f85ee2bf5161f4dae60e62d60b71db749472eda34dca17ebbb6727b192620baf103e44b2c4c96181b38970ce2c12cb1e792e08c1d57c3523bebb'
            'SKIP')
b2sums=('1ab2d64a82d89d836b25983ff607ebf3b7f142c9089bfb96fa3af278fa6c324b0fd0409751a40835d91483b0f44969d517bbf06b43965d79d0d6a25873cb6ddb'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodules
  git submodule init extern/adpcm
  git config submodule.extern/adpcm.url "$srcdir/github.com-superctr-adpcm"
  git -c protocol.file.allow=always submodule update
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_GUI=ON
    -D SYSTEM_FFTW=ON
    -D SYSTEM_FMT=ON
    -D SYSTEM_FREETYPE=ON
    -D SYSTEM_LIBSNDFILE=ON
    -D SYSTEM_PORTAUDIO=ON
    -D SYSTEM_RTMIDI=ON
    -D SYSTEM_SDL2=ON
    -D SYSTEM_ZLIB=ON
    -D WITH_JACK=ON
    -D WITH_DEMOS=ON
    -S "$pkgname"
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/demos/README.md"
}
