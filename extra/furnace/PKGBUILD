# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=furnace
pkgver=0.6.3
pkgrel=1
pkgdesc='A multi-system chiptune tracker compatible with DefleMask modules'
arch=('x86_64')
url='https://github.com/tildearrow/furnace'
license=('GPL-2.0-or-later')
groups=('pro-audio')
depends=(
  'fftw'
  'fmt'
  'freetype2'
  'glibc'
  'gcc-libs'
  'hicolor-icon-theme'
  'libglvnd'
  'libsndfile'
  'portaudio'
  'rtmidi'
  'sdl2'
  'zlib'
)
makedepends=(
  'cmake'
  'git'
  'jack'
)
optdepends=('jack: JACK audio support')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-superctr-adpcm::git+https://github.com/superctr/adpcm'
)
sha512sums=('64425b74c0b75a4444fe601563d4533723f33e7f7af912baae4b346ebe5dc806fe490fa3b9cae649a04967a47bd03a9fa81db88c9774cb3ebf6cdd1ae9fca734'
            'SKIP')
b2sums=('8f726620d0c19a470dac9f80889c410569a2686b17f0bd753bd6ce019fa83019c73dc254e01f50b012f1fa396e735ee00dd79dd3972667932b2931dba12ca987'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodules
  git submodule init extern/adpcm
  git config submodule.extern/adpcm.url "$srcdir/github.com-superctr-adpcm"
  git -c protocol.file.allow=always submodule update
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_GUI=ON
    -D SYSTEM_FFTW=ON
    -D SYSTEM_FMT=ON
    -D SYSTEM_FREETYPE=ON
    -D SYSTEM_LIBSNDFILE=ON
    -D SYSTEM_PORTAUDIO=ON
    -D SYSTEM_RTMIDI=ON
    -D SYSTEM_SDL2=ON
    -D SYSTEM_ZLIB=ON
    -D WITH_JACK=ON
    -S "$pkgname"
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build  
}
