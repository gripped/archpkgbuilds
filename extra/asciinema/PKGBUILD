# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Marcin Kulik <marcin@asciinema.org>

pkgname=asciinema
pkgver=3.0.0rc.5
pkgrel=1
pkgdesc='Record and share terminal sessions'
arch=(any)
url='https://asciinema.org'
license=(GPL-3.0-only)
makedepends=(cargo git)
options=(!lto)
source=("git+https://github.com/$pkgname/$pkgname#tag=v${pkgver/rc/-rc}")
b2sums=('786b22babb42e28d60606c7ab28598b9219328704415ecf8729726115bfc0aebdaa5a68029a10def3425d336700e88091d9261afcd4b71f36c6ce5b78d13398b')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  env \
    ASCIINEMA_GEN_DIR="$PWD" \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_LTO=true \
    cargo build --frozen --release
  mv completion/asciinema{.bash,}
}

check() {
  cd $pkgname
  local skipped=(
    pty::tests::exec_basic # PTY interaction
    pty::tests::exec_extra_env
    pty::tests::exec_no_output
    pty::tests::exec_quick # PTY interaction
    pty::tests::exec_winsize_override
    pty::tests::spawn_basic # PTY interaction
  )
  cargo test --frozen -- ${skipped[@]/#/--skip }
}

package() {
  cd $pkgname
  install -Dm0755 target/release/$pkgname -t "$pkgdir/usr/bin"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm0644 -t "$pkgdir/usr/share/bash-completion/completions/" completion/$pkgname
  install -Dm0644 -t "$pkgdir/usr/share/elvish/lib/" completion/$pkgname.elv
  install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" completion/$pkgname.fish
  install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" completion/_$pkgname
  install -Dm0644 -t "$pkgdir/usr/share/man/man1/" man/*.1
}
