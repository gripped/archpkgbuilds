# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Marcin Kulik <marcin@asciinema.org>

pkgname=asciinema
pkgver=3.0.0
pkgrel=2
pkgdesc='Record and share terminal sessions'
arch=(x86_64)
url='https://asciinema.org'
license=(GPL-3.0-only)
makedepends=(cargo git)
depends=(glibc gcc-libs)
options=(!lto)
source=("git+https://github.com/$pkgname/$pkgname#tag=v$pkgver")
b2sums=('28c917c2046782056db63773f2ed7bec93ad12b470449b6c791ab23a457957d09ab1e3cf539875a5db2a9ea176d263f154a57942414791a765817080a0b45e24')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  env \
    ASCIINEMA_GEN_DIR="$PWD" \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_LTO=true \
    cargo build --frozen --release
  mv completion/asciinema{.bash,}
}

check() {
  cd $pkgname
  local skipped=(
    pty::tests::exec_basic # PTY interaction
    pty::tests::exec_extra_env
    pty::tests::exec_no_output
    pty::tests::exec_quick # PTY interaction
    pty::tests::exec_winsize_override
    pty::tests::spawn_basic # PTY interaction
  )
  cargo test --frozen -- ${skipped[@]/#/--skip }
}

package() {
  cd $pkgname
  install -Dm0755 -t "$pkgdir/usr/bin/" target/release/$pkgname
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm0644 -t "$pkgdir/usr/share/man/man1/" man/*.1
  cd completion
  install -Dm0644 -t "$pkgdir/usr/share/bash-completion/completions/" $pkgname
  install -Dm0644 -t "$pkgdir/usr/share/elvish/lib/" $pkgname.elv
  install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" $pkgname.fish
  install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" _$pkgname
}
