# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor Sefa Eyeoglu <contact@scrumplex.net>

pkgname=libquotient
pkgver=0.9.6.1
pkgrel=1
pkgdesc='A Qt library to write cross-platform clients for Matrix'
license=(LGPL-2.1-or-later)
arch=(x86_64)
url='https://quotient-im.github.io/libQuotient/'
depends=(glibc
         libolm
         libstdc++
         openssl
         qt6-base
         qtkeychain-qt6)
makedepends=(cmake
             git)
source=(git+https://github.com/quotient-im/libQuotient#tag=$pkgver)
sha256sums=('27bbcd83ace2d379acfad7b4c5d4b8b085356a27b38a095f3bebbb133bb9b3b0')

build() {
  cmake -B build -S libQuotient \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
