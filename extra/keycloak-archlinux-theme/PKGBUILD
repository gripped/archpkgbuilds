# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=keycloak-archlinux-theme
pkgver=1.5.0
pkgrel=1
_java=21 # match keycloak's JDK
pkgdesc="Keycloak Arch Linux theme"
arch=('any')
url="https://gitlab.archlinux.org/archlinux/keycloak-archlinux-theme"
license=('Apache-2.0')
depends=('keycloak>=23')
makedepends=('gradle' 'git' "java-environment=${_java}")
source=("git+$url.git?signed#tag=v$pkgver")
sha256sums=('816d86789697a0d1c31d7233b5fbbf657c9a93ea805395227309fefdc56f133b')
validpgpkeys=(
  2191B89431BAC0A8B96DE93D244740D17C7FD0EC # artafinde@archlinux.org
  05C7775A9E8B977407FE08E69D4C5AA15426DA0A # freswa@archlinux.org
  )

build() {
  cd "${pkgname}"
  export PATH="/usr/lib/jvm/java-${_java}-openjdk/bin:$PATH"
  export JAVA_HOME="/usr/lib/jvm/java-${_java}-openjdk"
  gradle -Dorg.gradle.daemon=false build
}

package() {
  cd "${pkgname}"
  install -vdm 755 "${pkgdir}"/usr/share/java/keycloak/providers/
  install -vDm 644 "${srcdir}/${pkgname}"/lib/build/libs/keycloak-archlinux-theme.jar \
    "${pkgdir}"/usr/share/java/keycloak/providers/keycloak-archlinux-theme.jar
}

# vim:set ts=2 sw=2 et:
