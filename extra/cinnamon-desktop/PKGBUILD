# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=6.6.1
pkgrel=2
pkgdesc='Library with common API for various Cinnamon modules'
arch=(x86_64)
url='https://github.com/linuxmint/cinnamon-desktop'
license=('GPL-2.0-or-later AND LGPL-2.0-or-later')
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libpulse
  libx11
  libxkbfile
  systemd-libs
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
)
source=("git+https://github.com/linuxmint/cinnamon-desktop.git#tag=$pkgver")
b2sums=(60d012b7e5838b3b58225942c8a68a42d36940628078c4e8676e67753d2dc9d8d3d27da88c8f0d3f8e5e4b96c563f1c320ea59bf2da9fae7b3ce84ec734205ba)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
