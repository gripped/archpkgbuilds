# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=spytrap-adb
pkgver=0.3.1
pkgrel=1
pkgdesc="Test a phone for stalkerware using adb and usb debugging to scan for suspicious apps and configuration"
url='https://github.com/spytrap-org/spytrap-adb'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'android-tools'
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/spytrap-org/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('737400d11f25c927fc7245e3cdca213f33edb48844baae077158af91f87509d3')
b2sums=('f10a02bd764ab17636dba6b632620e9d50f1b4a375a6155f414ebfaaf261692cb70dc7b8c5ed3401a4ffc9218360ef658bff0d5db0d4abf1bfa1c52ee52d7f01')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/spytrap-adb

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
