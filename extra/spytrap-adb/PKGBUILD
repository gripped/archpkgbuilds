# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=spytrap-adb
pkgver=0.3.4
pkgrel=1
pkgdesc="Test a phone for stalkerware using adb and usb debugging to scan for suspicious apps and configuration"
url='https://github.com/spytrap-org/spytrap-adb'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'android-tools'
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/spytrap-org/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8553beb5c403f3cbcf7b5e7ded052e4b31efe8e798ec81e2bcc16aba315f5429')
b2sums=('dc75536e46db9ebaffe5c8d77d45b01d95a18160385d43dfe0aaf7357b310cd5f702fad7e7cba613c4d1f359370cc811f94a567753950902a2bb8f2a09c4fa00')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/spytrap-adb

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
