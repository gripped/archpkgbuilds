# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=spytrap-adb
pkgver=0.3.3
pkgrel=1
pkgdesc="Test a phone for stalkerware using adb and usb debugging to scan for suspicious apps and configuration"
url='https://github.com/spytrap-org/spytrap-adb'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'android-tools'
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/spytrap-org/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('440182e5387085b5ef3635b1d167bd4e3b238961398ef855cef6bfc84a51d41a')
b2sums=('679b5efa38cffa446e79c9bea071ea1e45b7d947904468b54c4d443a236b55ead19320ce183f25a4ddccece74f8a26d08b567467962c9a9bb8e3ed03422f1015')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/spytrap-adb

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
