# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=6.0.6
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
# startdde: org.deepin.dde.Display1
depends=('deepin-qt5integration' 'dtkdeclarative' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base'
         'qt5-svg' 'qt5-declarative' 'ffmpeg4.4' 'ffmpegthumbnailer' 'freeimage' 'gio-qt'
         'udisks2-qt5' 'gcc-libs' 'startdde')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-album.git#tag=$pkgver")
sha512sums=('7f3c723737394f00ea1749c1af118085a4710ee0f94509cdc0258ba79eb1a4e77ac762d4f46e2e86d4824047a45a634b7938efd3bc5aed6068057daae4b08b11')

build() {
  cd deepin-album
  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
  ninja
}

package() {
  cd deepin-album
  DESTDIR="$pkgdir" ninja install
}
