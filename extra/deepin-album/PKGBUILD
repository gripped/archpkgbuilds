# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=6.0.5
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
# startdde: org.deepin.dde.Display1
depends=('deepin-qt5integration' 'dtkdeclarative' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base'
         'qt5-svg' 'qt5-declarative' 'ffmpeg4.4' 'ffmpegthumbnailer' 'freeimage' 'gio-qt'
         'udisks2-qt5' 'gcc-libs' 'startdde')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-album.git#tag=$pkgver")
sha512sums=('aedec43da88968202b597904914d5c10cbdb506f3f3957e1cc130c1d5f3341e37dc9a84e4468eb3279534a6c202ac011c85c61fd94ca1c20fbb9aa01166dcf3a')

build() {
  cd deepin-album
  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
  ninja
}

package() {
  cd deepin-album
  DESTDIR="$pkgdir" ninja install
}
