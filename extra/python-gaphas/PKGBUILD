# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-gaphas
pkgver=4.1.1
pkgrel=3
pkgdesc='Diagramming widget library for Python'
arch=(any)
url='https://github.com/gaphor/gaphas'
license=(Apache-2.0)
depends=(
  glib2
  graphene
  gtk4
  python
  python-cairo
  python-gobject
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-sphinx-furo
  python-wheel
)
checkdepends=(
  python-pytest
  xorg-server-xvfb
)
source=("git+https://github.com/gaphor/gaphas.git#tag=$pkgver")
b2sums=(5afe87251a7ce29666dfb533d5e2a04f7d2999e521ef80b21fc8424473d9b45414634677561de83c46bb6df99eabc2ac132d4fe60bd6953ee47d167349ba273d)

build() {
  cd gaphas
  python -m build --wheel --no-isolation
}

check() {
  cd gaphas
  PYTHONPATH="$PWD/dist/gaphas-$pkgver-py3-none-any.whl" \
    dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    pytest --ignore=tests/test_architecture.py
}

package() {
  cd gaphas
  python -m installer --destdir="$pkgdir" dist/*.whl
  python -m sphinx -b html docs "$pkgdir/usr/share/doc/$pkgname/html"
  rm -r "$pkgdir/usr/share/doc/$pkgname/html/.doctrees"
}
