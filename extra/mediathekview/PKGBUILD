# Maintainer: David Runge <dvzrv@archlinux.org>

_name=MediathekView
pkgname=mediathekview
pkgver=14.3.1
pkgrel=1
pkgdesc="Access the Mediathek of many German TV stations"
arch=(any)
url="https://github.com/mediathekview/mediathekview"
license=(
  0BSD
  GPL-3.0-or-later
)
_java_version=25
depends=(
  bash
  hicolor-icon-theme
  java-runtime=$_java_version
  ttf-font
  xdg-user-dirs
)
makedepends=(
  maven
  strip-nondeterminism
)
conflicts=(mediathek)
provides=(mediathek)
replaces=(mediathek)
optdepends=(
  'libnotify: to use desktop notifications'
  'mplayer: for recording streams'
  'ttf-dejavu: for default font'
  'vlc: for stream playback'
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  de.$pkgname.$_name.desktop
  $pkgname.sh
  LICENSE
  $pkgname-14.3.1-include-dependency.patch
)
sha512sums=('fbce28a2bf0156a8f30655b48e61d9ea939b96f0fb6222c4eb68e3b8feb943c5175780c4399e9985ab4d12c114dd81a320bee47d047c395d618a69ea179d6148'
            '87057f321b25b6ae842a371558d03bd2970a27bb4de378aea2b91a47c3d6168b2ff4ebbb2f75bf4c39dfb108e342311d7e53ff91ea0f4b25d29ffc9c8d0e5c96'
            'f4d372847834683730800304b1a0648e760c308f6aa06db5a2923d27722a7e183c69db8fb0616e6c19be0d071d8723a46fd9382660885bf8b257d879582b0768'
            'a33658d9271e5c537ccd41bf540b463ad2a5eca4a060c80486ff42a736f0aa042d10436e7177c34d792177cb11285243dee1f31c4df54fb0bfaabbc306406930'
            '92e6857c2cd77652fd6a8faa80f6a505424cdd0c2fead21613a30b423f358735b1e044d8feb3b1e565a201fab18db8992b4a01d176a2cbb49c4265ea8c0910f8')
b2sums=('4a5099e66f2f72737935a9892f7caa82f030d5be3a6d75bb6caf3a4a1e124913638c4d31713913c954ab95dad82ef7b5630bc9df2bc0c62a14bbdc9eeb72d912'
        '321db8d1b226ab183a7c16e0566de6edddb582e29cfd6d351139c09f86fa9a7167f06ac212e423ba6a288d2d72a0211543abc3d03689f5012040948a04da7e27'
        '6c7f255bed0015fbb1e5084cd61360f56cc77adfe76e599981305daa11bf98aca4ac6755c22da32a4747ca8dcc34b23baa8ffb8ef921d5c2c6c03c2afa61cdfc'
        'a29664104e1ee73ca0aee1d633e9095d92a57c92787f8d8740bdb7211ba3205782ed8677f539bdb8cae3dd75a3694be3132e185fa3fc4b3f401e1f88eb776101'
        'be97cf5045734a3b1f20e2797109ca1c0d71cf186978c16ee3418a6cc7cea8f797600907b0c3b826397a18a9913ab064eda59ea174081fe7b1f3294f05ebf663')

prepare() {
  # Ensure that dependencies can be loaded from a dependency subdirectory (as they are no longer bundled).
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-14.3.1-include-dependency.patch
  # set java version for wrapper script
  sed "s/JAVA_VERSION/$_java_version/" $pkgname.sh > $pkgname
}

build() {
  cd $_name-$pkgver
  ./mvnw clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} +
}

package() {
  local _size

  cd $_name-$pkgver
  # jar
  install -vDm 644 target/$_name.jar -t "$pkgdir/usr/share/java/$pkgname/"
  install -vDm 644 target/dependency/*.jar -t "$pkgdir/usr/share/java/$pkgname/dependency/"
  # script
  install -vDm 755 ../$pkgname -t "$pkgdir/usr/bin/"
  # XDG desktop file
  install -vDm 644 ../de.mediathekview.$_name.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 ../LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/0BSD.txt"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 target/${_name}@x$_size.png "$pkgdir/usr/share/icons/hicolor/${_size}x$_size/apps/$pkgname.png"
  done
  install -vDm 644 res/$_name.svg -t "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  # docs
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
