# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Jeffrey Tolar <tolar.jeffrey at gmail dot com>

pkgname=verilator
pkgver=5.036
pkgrel=1
pkgdesc='The fastest free Verilog HDL simulator'
url='https://www.veripool.org/verilator/'
arch=('x86_64')
license=('LGPL')
depends=('perl')
optdepends=('systemc')
makedepends=('git' 'help2man' 'python' 'systemc')
checkdepends=('python-distro')
source=("git+https://github.com/verilator/verilator.git#tag=v$pkgver")
sha512sums=('0d302f32cd463e4c378f263c8789d37c66f346324e282f3e6c89190c3bfc24e8708d2796927231ea426ceea567b596e8f95ef365233c7a6cfb945312a3cb146c')

prepare() {
  cd verilator
  autoconf
}

build() {
  cd verilator
  ./configure --prefix=/usr
  make
}

check() {
  cd verilator
  make test
}

package() {
  cd verilator
  make install DESTDIR="$pkgdir"
}
