# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Jeffrey Tolar <tolar.jeffrey at gmail dot com>

pkgname=verilator
pkgver=5.040
pkgrel=1
pkgdesc='The fastest free Verilog HDL simulator'
url='https://www.veripool.org/verilator/'
arch=('x86_64')
license=('LGPL-3.0-or-later')
depends=('perl' 'python')
optdepends=('systemc')
makedepends=('git' 'help2man' 'systemc')
checkdepends=('python-distro')
source=("git+https://github.com/verilator/verilator.git#tag=v$pkgver")
sha512sums=('6cf57975fc8044589da486db8ea692cd36d92c1c56cf6627aebd5f9f70a76a127c2193a4412422d4cb12db555691e6af1c0428182e883ecc45812e7f998f8c76')

prepare() {
  cd verilator
  autoconf
}

build() {
  cd verilator
  ./configure --prefix=/usr
  make
}

check() {
  cd verilator
  make test
}

package() {
  cd verilator
  make install DESTDIR="$pkgdir"
}
