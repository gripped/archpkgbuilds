# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Jeffrey Tolar <tolar.jeffrey at gmail dot com>

pkgname=verilator
pkgver=5.034
pkgrel=1
pkgdesc='The fastest free Verilog HDL simulator'
url='https://www.veripool.org/verilator/'
arch=('x86_64')
license=('LGPL')
depends=('perl')
optdepends=('systemc')
makedepends=('git' 'help2man' 'python' 'systemc')
source=("git+https://github.com/verilator/verilator.git#tag=v$pkgver")
sha512sums=('db083dacbd06d0c1d5bc4c441091ae8b0b43a9cf678ea6177bd6243c755fd51733f0163382703bc0d979551eb66b24a834b4710455444eef4e167774f24a66c7')

prepare() {
  cd verilator
  autoconf
}

build() {
  cd verilator
  ./configure --prefix=/usr
  make
}

check() {
  cd verilator
  make test
}

package() {
  cd verilator
  make install DESTDIR="$pkgdir"
}
