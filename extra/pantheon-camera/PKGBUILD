# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-camera
pkgver=8.0.1
pkgrel=1
pkgdesc='The Pantheon Camera Application'
arch=(x86_64)
url=https://github.com/elementary/camera
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gstreamer
  gst-plugins-base-libs
  gst-plugins-good
  gtk4
  hicolor-icon-theme
  libcanberra.so
  libgranite-7.so
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
)
_tag=16a397b62b944359d0fdd131a3659ea793a2b6a4
source=(pantheon-camera::git+https://github.com/elementary/camera.git#tag=${_tag})
b2sums=('3b9cf52b395e8756b6c18b52fd079dc7d4e11c53c4b0d8eb89f1b3169ddc10ed593e4b687ff10d65cb8e5fbb4cdc6654c8282907e1d189c75c5e5cb8864d616a')

pkgver() {
  cd pantheon-camera
  git describe --tags
}

build() {
  arch-meson pantheon-camera build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
