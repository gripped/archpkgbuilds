# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtkdeclarative
pkgver=5.7.13
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkdeclarative"
license=('LGPL3')
depends=('dtkgui' 'dtkcore' 'qt5-declarative' 'qt5-quickcontrols2' 'qt5-base' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'doxygen' 'gtest' 'gmock')
source=("git+https://github.com/linuxdeepin/dtkdeclarative.git#tag=$pkgver")
sha512sums=('86e6c8864613f638fac5ec00744f1d2b72c6a7a8dbf5ce57014c9111927028a301c62f42f3452762c2f630178cd7cccd74dc83074afcdaf1817fdb6af375bdd8')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DDTK_VERSION=$pkgver \
    -DBUILD_DOCS=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt
  ninja
}

check() {
  cd $pkgname
  # ninja test
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
