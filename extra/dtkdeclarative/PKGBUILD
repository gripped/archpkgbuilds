# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtkdeclarative
pkgver=5.7.24
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkdeclarative"
license=('LGPL-3.0-or-later')
depends=('dtkgui' 'dtkcore' 'qt5-declarative' 'qt5-quickcontrols2' 'qt5-base' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'doxygen' 'gtest' 'gmock')
source=("git+https://github.com/linuxdeepin/dtkdeclarative.git#tag=$pkgver")
sha512sums=('b5cdd7aff0e94ee7373ab28969d2289f8869497ca874f2eb14d8aaec697cfb144dd7f6ffae79420f52b40e4ffd723dcbf38d4d7e2f18cb7885dc9f2bbe8c774e')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_DOCS=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt
  ninja
}

check() {
  cd $pkgname
  # ninja test
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
