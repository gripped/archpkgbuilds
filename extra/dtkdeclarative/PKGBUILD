# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtkdeclarative
pkgver=5.7.5
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkdeclarative"
license=('LGPL3')
depends=('dtkgui' 'dtkcore' 'qt5-declarative' 'qt5-quickcontrols2' 'qt5-base' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'doxygen' 'gtest' 'gmock')
source=("git+https://github.com/linuxdeepin/dtkdeclarative.git#tag=$pkgver")
sha512sums=('ee50cd4ef71eac432aad34d9cb082c0fd1269a1c651af5de25a5f0f9c25a271f59d62920c0e53222a1a4eb66e064b496990e735c9548ae5fc7bd5b68bbaa0d74')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DDTK_VERSION=$pkgver \
    -DBUILD_DOCS=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt
  ninja
}

check() {
  cd $pkgname
  # ninja test
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
