# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtkdeclarative
pkgver=5.7.12
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkdeclarative"
license=('LGPL3')
depends=('dtkgui' 'dtkcore' 'qt5-declarative' 'qt5-quickcontrols2' 'qt5-base' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'doxygen' 'gtest' 'gmock')
source=("git+https://github.com/linuxdeepin/dtkdeclarative.git#tag=$pkgver")
sha512sums=('7d29573fd3c890f8cbccb691310300f3f19974e164f294cbcc801ba7e7a8adc3e8a649b6f40cf0ca5ca002f50d8f4e8ac64be3e1c53f977b5311c23da541c39c')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DDTK_VERSION=$pkgver \
    -DBUILD_DOCS=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt
  ninja
}

check() {
  cd $pkgname
  # ninja test
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
