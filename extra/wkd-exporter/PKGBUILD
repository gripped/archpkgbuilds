# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=wkd-exporter
pkgver=0.3.2
pkgrel=2
pkgdesc="Exports an OpenPGP keyring into an advanced WKD directory path"
arch=(x86_64)
url="https://gitlab.archlinux.org/archlinux/wkd-exporter"
license=('Apache-2.0 OR MIT')
groups=(openpgp-tools)
depends=(
  glibc
  libgcc
)
makedepends=(
  just
  rust
  rust-script
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha256sums=('e3f27df74227bc1a4ba586b58de7f91cac1ed996a9fcb5aca4e9e5f30ba32d12')

prepare() {
  cd $pkgname-v$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname-v$pkgver
  cargo build --frozen --release --all-features
  just generate manpages
  just generate shell_completions
}

check() {
  cd $pkgname-v$pkgver
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-v$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE* -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 target/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 target/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/_$pkgname"
  install -vDm 644 target/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 target/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
}
