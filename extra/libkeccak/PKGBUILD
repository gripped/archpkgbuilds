# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mattias Andrée <`base64 -d`(bWFhbmRyZWUK)@kth.se>

pkgname=libkeccak
pkgver=1.4.2
pkgrel=1
pkgdesc='Keccak-family hashing library'
arch=('x86_64')
url='https://codeberg.org/maandree/libkeccak'
license=('custom:ISC')
depends=('glibc')
makedepends=('git')
source=("git+https://codeberg.org/maandree/libkeccak#tag=${pkgver}?signed")
b2sums=('4ac4d914b389a977121a8a03759f488415d64ec37d76d88ff20204e58810ca76216c9d91f23b325015c756ae4583672f07b95f5c8edc96cb50a5c09ad9fe71e7')
validpgpkeys=(
  '3683C4B70CFA859F0173F2CCE0DD13EBFC7D5E3E' # Mattias Andrée <maandree@kth.se>
)

prepare() {
  cd ${pkgname}
  sed -i 's#FLAGS\s\+=#FLAGS +=#g' optimised.mk
}

build() {
  cd ${pkgname}
  make CONFIGFILE=optimised.mk PREFIX=/usr
}

check() {
  cd ${pkgname}
  make CONFIGFILE=optimised.mk check
}

package() {
  cd ${pkgname}
  make CONFIGFILE=optimised.mk PREFIX=/usr DESTDIR="${pkgdir}" install
}
