# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Fernando Munoz <f[at]beford.net>
# Contributor: Alexandre Bique <bique.alexandre[at]gmail.com>

pkgbase=isomd5sum
pkgname=('isomd5sum' 'python-isomd5sum')
pkgver=1.2.5
pkgrel=3
arch=('x86_64')
license=('GPL-2.0-or-later')
makedepends=('popt' 'python')
url='https://github.com/rhinstaller/isomd5sum'
source=("$pkgbase-$pkgver.tar.gz::https://github.com/rhinstaller/isomd5sum/archive/$pkgver.tar.gz")
options=('!emptydirs')
sha512sums=('aa6bb16be18b42925ac1834aa4d558fafdba7751639acfb7344a79f7ead1125d810f13c442458526f394d8790369b1c48ab41cb73881808740263161caa99655')

prepare() {
  sed -i -e 's/^\(LIBDIR = \)lib64/\1lib/' $pkgbase-$pkgver/Makefile
}

build() {
  cd $pkgbase-$pkgver
  CFLAGS+=' -ffat-lto-objects' \
  make all
}

package_isomd5sum() {
  depends=('popt')
  pkgdesc="Utilities for working with md5sum implanted in ISO images"

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-bin install-devel
}

package_python-isomd5sum() {
  depends=('python')
  pkgdesc="Python3 bindings for isomd5sum"

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-python
}
