# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=lua-zlib
_rockname=${pkgbase##lua-}
pkgname=(lua-$_rockname lua53-$_rockname lua52-$_rockname lua51-$_rockname)
epoch=1
pkgver=1.3
pkgrel=1
_rockrel=0
url="https://github.com/brimworks/$pkgbase"
pkgdesc='Simple streaming interface to zlib'
arch=(x86_64)
license=(MIT)
depends=(glibc
         zlib)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$pkgbase-$pkgver"
_rock="$_archive-$_rockrel.linux-$CARCH.rock"
source=("$url/archive/v$pkgver/$_archive.tar.gz"
        "https://luarocks.org/manifests/brimworks/$_archive-$_rockrel.rockspec")
sha256sums=('d026eda33b7c3456696bcecdc0b26d7bd779de4f5a4d4215ebe3897095c6c9c8'
            '8806be122f5621a657078e8038c7c4ff58720aedcb21da8fdd2b01304981b31a')

build() {
	cd "$_archive"
	for LUAVER in 5.{1,2,3,4}; do
		luarocks --lua-version "$LUAVER" \
			make --pack-binary-rock --deps-mode none -- "$srcdir/${source[1]##*/}"
		install -Dm0644 -t "lua-$LUAVER/" "$_rock"
	done
}

_package() {
	cd "$_archive"
	pkgdesc+=" for Lua $1"
	conflicts=("lua${1/./}-lzlib")
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		install --deps-mode none --no-manifest -- "lua-$1/$_rock"
	install -Dm0664 -t "$pkgdir/usr/share/licenses/$pkgname/" README
}

package_lua-zlib() {
	_package 5.4
	conflicts=(lua-lzlib)
}

package_lua51-zlib() {
	_package 5.1
}

package_lua52-zlib() {
	_package 5.2
}

package_lua53-zlib() {
	_package 5.3
}
