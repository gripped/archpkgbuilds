# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=papers
pkgname=(
  papers
  papers-lib-docs
)
pkgver=47.4
pkgrel=1
pkgdesc='Document viewer for PDF and other document formats aimed at the GNOME desktop'
arch=(x86_64)
url='https://apps.gnome.org/Papers/'
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  djvulibre
  exempi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libarchive
  libnautilus-extension
  libtiff
  pango
  poppler-glib
)
makedepends=(
  appstream
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  itstool
  meson
  rust
)
source=("git+https://gitlab.gnome.org/GNOME/Incubator/papers.git#tag=$pkgver")
b2sums=('b76baddcbe4d5405e04660fcbb74e52935efa8417bd1f5084bbf2d109961556850e9e4017de85cffdebe24460bcad7b5dbeb47709f4e9c7af2ba050bbad61195')

prepare() {
  cd $pkgbase

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')" \
      --manifest-path=shell-rs/Cargo.toml
}

build() {
  arch-meson $pkgbase build \
    -D xps=disabled

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package_papers() {
  meson install -C build --destdir "$pkgdir" --no-rebuild

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_papers-lib-docs() {
  pkgdesc+=" (library API documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
