# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-pgrx
pkgver=0.15.0
pkgrel=1
pkgdesc='Build PostgreSQL extensions with Rust'
arch=('x86_64')
url='https://github.com/pgcentralfoundation/pgrx'
license=('MIT')
depends=('gcc-libs' 'openssl' 'clang' 'llvm' 'bzip2')
makedepends=('git' 'rust')
optdepends=('postgresql: to compile with system postgresql')
replaces=('cargo-pgx')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('88b7ab4239cab1c515bce3f8774ce86cd49c5eb937124812212c78e8bfa23114f0ce59de14898e61e663d2fab515963612946b8f2d258ec2b2cda9c397d2c3dc')
b2sums=('925cd503707983abbe75657339e472bb54bb4cddda16aa8a19b726ddc290d0f6236f2b0a44f7ea3ca1d36525481f689e2756ae16b5636c163bad917d0e062f9e')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --package "$pkgname"
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
