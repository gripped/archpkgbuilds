# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-pgrx
pkgver=0.16.1
pkgrel=1
pkgdesc='Build PostgreSQL extensions with Rust'
arch=(x86_64)
url='https://github.com/pgcentralfoundation/pgrx'
license=(MIT)
depends=(
  glibc
  gcc-libs
  openssl
  clang
  llvm
  bzip2
  xz
)
makedepends=(
  git
  rust
)
optdepends=('postgresql: to compile with system postgresql')
replaces=(cargo-pgx)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('c1e29b2876d5b8b85e0d27b650de2786c349e239ae8e20d52c37246d7b687d09f7319abad3be61a50bd663c9557c158bec5f70c66be40ca93dd04d1d70ff7acb')
b2sums=('0964749e6971b8102149cba79f9ac73f4edcd8343d6be1c100939da3c223b2a5a2c2411f9a4938a4d8646f61eb1f81f9645c4adc540823e036a7b5fe421ad286')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --package "$pkgname"
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
