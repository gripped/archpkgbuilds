# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-pgrx
pkgver=0.12.4
pkgrel=1
pkgdesc='Build PostgreSQL extensions with Rust'
arch=('x86_64')
url='https://github.com/pgcentralfoundation/pgrx'
license=('MIT')
depends=('gcc-libs' 'openssl' 'clang' 'llvm' 'bzip2')
makedepends=('git' 'rust')
optdepends=('postgresql: to compile with system postgresql')
replaces=('cargo-pgx')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('33dc4e34f2aa4ea2fa39e18b3e6b212739ef4cd1c1f1b07807e2b38f49a348dcb6d0184c2101b558b00a3386c9d2b9765413a303f4576b0bfb68460667018a7d')
b2sums=('2c77cb99733f18fa25da49e49270e1d33249f73f9b34e6168390d6fbc3eb8f0f1e5cf2e9f0596c086bdf25f3a8f872816b833c44288191767e1fb84dc882da3c')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --package "$pkgname"
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
