# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-pgrx
pkgver=0.14.3
pkgrel=1
pkgdesc='Build PostgreSQL extensions with Rust'
arch=('x86_64')
url='https://github.com/pgcentralfoundation/pgrx'
license=('MIT')
depends=('gcc-libs' 'openssl' 'clang' 'llvm' 'bzip2')
makedepends=('git' 'rust')
optdepends=('postgresql: to compile with system postgresql')
replaces=('cargo-pgx')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('23fa9874d295cfe300bd78e5712c6af52bade882a15560847772b2ececa2ada506128f003686204ebb83a12847bd1dc4084673f067e333652e51d94eb0c724f1')
b2sums=('aa9416414807fb870a940cdf0d8f4e1cc343bdbecdc29a52619f2044bd89fb5b58ef77f6777c608951070b091a2b059c85b12203c0006f4ee20576434723089b')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --package "$pkgname"
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
