# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-pgrx
pkgver=0.16.0
pkgrel=1
pkgdesc='Build PostgreSQL extensions with Rust'
arch=(x86_64)
url='https://github.com/pgcentralfoundation/pgrx'
license=(MIT)
depends=(
  glibc
  gcc-libs
  openssl
  clang
  llvm
  bzip2
  xz
)
makedepends=(
  git
  rust
)
optdepends=('postgresql: to compile with system postgresql')
replaces=(cargo-pgx)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('f2eebc6310a00a47ffd863cdee38efd9b94763c261e48d1488ccf4af45eae11faac5899f2cc4ea57be24a29c5bbe1004f6a97a6d2c18e7fc2dde6c9728db525c')
b2sums=('534a97a131a7010937504b4ee4c99c35fa753c885b4fa39d908db1d544e9426408da1602f1ef3ffd1a76767c305633c68ecdd3da70a2c94660f4339c675ed931')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --package "$pkgname"
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
