# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-pgrx
pkgver=0.12.9
pkgrel=1
pkgdesc='Build PostgreSQL extensions with Rust'
arch=('x86_64')
url='https://github.com/pgcentralfoundation/pgrx'
license=('MIT')
depends=('gcc-libs' 'openssl' 'clang' 'llvm' 'bzip2')
makedepends=('git' 'rust')
optdepends=('postgresql: to compile with system postgresql')
replaces=('cargo-pgx')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('ed0b174fdb28f4fbe5cc651677e58488c4bba7f1e409e783ce62b6909f3ad5df2f803de84fa2d270ad20a34f4c4d02993279373adfb1ec96d78dc9a7e20d6a77')
b2sums=('2738ca0bca59f9a652848d91ed81922a2dc0224813679d02160caa9ad72500d0e383b92f547b24efb37f48ba32c470b4f4b876675d356963fffc8b2b178b3fb2')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --package "$pkgname"
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
