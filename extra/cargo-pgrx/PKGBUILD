# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-pgrx
pkgver=0.17.0
pkgrel=1
pkgdesc='Build PostgreSQL extensions with Rust'
arch=(x86_64)
url='https://github.com/pgcentralfoundation/pgrx'
license=(MIT)
depends=(
  glibc
  gcc-libs
  openssl
  clang
  llvm
  bzip2
  xz
)
makedepends=(
  git
  rust
)
optdepends=('postgresql: to compile with system postgresql')
replaces=(cargo-pgx)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('f79d38dd89ba382f1e6950a839d295bdfe9698e5758fa2e7398ca01eb585afde3d53d4bc0a856cc9304066a4d30f52bf687d5b1606541a3e41fe5d662daaa47d')
b2sums=('18f2409e6270ba0a238d8984769fc33f0cf129333520250107194438d0c6ac7d3b466beecb88cd9d0e3a474f466ac0b09b4ecb9d00c8901f44fcc991fbea1ba0')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --package "$pkgname"
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
