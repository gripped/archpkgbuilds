# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=libcyaml
pkgver=1.4.2
pkgrel=1
pkgdesc='C library for reading and writing YAML'
arch=('x86_64')
url='https://github.com/tlsa/libcyaml'
license=('ISC')
provides=('libcyaml.so')
depends=('glibc' 'libyaml')
makedepends=('git')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('951b434e2c4e38c0da1e934f6c487f86f6072fc8e3f8f879e160c1afb5ec991210ab57b154a1eaa97f0c22dc377a2aef0fce0784ea00464adf98f929f95a21d8')
b2sums=('0cddc28db547b9bd71d4f6b90a2589ab8c10f0e658aa1668a19aec332fcacf1629cccc20d0fd1fa3f4f86735cee90c830708b7dcc9823f16c69855ba5bfa16a6')

build() {
  cd "$pkgname"

  make VARIANT=release
}

check() {
  cd "$pkgname"

  make test
}

package() {
  cd "$pkgname"

  # don't install static library
  make install-shared VARIANT=release DESTDIR="$pkgdir" PREFIX=/usr

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
