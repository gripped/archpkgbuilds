# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=ccid
pkgver=1.7.1
pkgrel=1
pkgdesc='A generic USB Chip/Smart Card Interface Devices driver'
arch=('x86_64')
url='https://ccid.apdu.fr/'
license=('LGPL' 'GPL')
depends=('pcsclite' 'libusb' 'flex')
makedepends=('meson')
backup=('etc/reader.conf.d/libccidtwin')
validpgpkeys=('F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E') # Ludovic Rousseau <ludovic.rousseau@free.fr>
source=("https://ccid.apdu.fr/files/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('32799ab16fe6e493c9452be3823f21810fbe80b884021a6f6f3fa69f26be5c86'
            'SKIP')

build() {
  arch-meson "${pkgname}-${pkgver}" build \
    -Dclass=true \
    -Dserial=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"

  install -D -m0644 "${pkgname}-${pkgver}/src/92_pcscd_ccid.rules" "${pkgdir}/usr/lib/udev/rules.d/92_pcscd_ccid.rules"
}
