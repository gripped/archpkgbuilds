# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: John Gerritse <tyrannis dot hawk at gmail dot com>

pkgname=python-pyspellchecker
_name=${pkgname#python-}
pkgver=0.8.3
pkgrel=2
pkgdesc='Pure python spell checker based on work by Peter Norvig'
arch=(any)
url=https://github.com/barrust/pyspellchecker
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("git+$url.git#tag=v$pkgver")
b2sums=('0a7fa77a6ed59ffe88b9514476396eea68aa57feb4bc73732d969bc43d439e57a4af0af8607d10e9d1a119094538f303bee69f05ca8d23525c6c7aa0e2e0fa7f')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  pytest
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
