# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hexpatch
_pkgname=HexPatch
pkgver=1.12.1
pkgrel=1
pkgdesc="A binary patcher and editor with a TUI"
url="https://github.com/Etto48/HexPatch"
depends=('gcc-libs')
makedepends=('cargo' 'cmake' 'python')
arch=('x86_64')
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c5019bd9893fff452a11f550c5e30689a9db932a948df182cfb922209ce75debf0aa6f84c236cd4a8c2cf018037ae56fa99853ffb3dfc91ff56c5ff21976d9d1')
options=('!lto')

prepare() {
  cd "${_pkgname}-${pkgver}"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" --locked
}

build() {
  cd "${_pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${_pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm 755 "target/release/hex-patch" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 docs/README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
