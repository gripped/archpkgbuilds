# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hexpatch
_pkgname=HexPatch
pkgver=1.10.0
pkgrel=1
pkgdesc="A binary patcher and editor with a TUI"
url="https://github.com/Etto48/HexPatch"
depends=('gcc-libs')
makedepends=('cargo' 'cmake' 'python')
arch=('x86_64')
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e3738834e56e8b43678fdd761f548dfe240f79cf16b3a3e12f863bc3e3bc2d351c5d49e0f5bed995a77524420a5c702bd39521aac4e71059d8f7579f4114ebf1')
options=('!lto')

prepare() {
  cd "${_pkgname}-${pkgver}"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" --locked
}

build() {
  cd "${_pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${_pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm 755 "target/release/hex-patch" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 docs/README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
