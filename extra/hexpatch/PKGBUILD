# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hexpatch
_pkgname=HexPatch
pkgver=1.9.5
pkgrel=1
pkgdesc="A binary patcher and editor with a TUI"
url="https://github.com/Etto48/HexPatch"
depends=('gcc-libs')
makedepends=('cargo' 'cmake' 'python')
arch=('x86_64')
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('b2297d38d4383bcc1aba715cbe0a165b40f47981da8e4c883cf46394338391d9ad96449c44d49a6e2bce634dcea4aa8417ac6cb247d9c69806590b57558f34ea')
options=('!lto')

prepare() {
  cd "${_pkgname}-${pkgver}"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" --locked
}

build() {
  cd "${_pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${_pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm 755 "target/release/hex-patch" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 docs/README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
