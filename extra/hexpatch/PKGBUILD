# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hexpatch
_pkgname=HexPatch
pkgver=1.12.3
pkgrel=1
pkgdesc="A binary patcher and editor with a TUI"
url="https://github.com/Etto48/HexPatch"
depends=('gcc-libs')
makedepends=('cargo' 'cmake' 'python')
arch=('x86_64')
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a0c3ebb817f418befbd2d37f5df9af8ff55418629889415c6a1cef03616e551819be765de125e84b9801ea1e5e2717b73f6360974cb28ca0c5938161b88764f6')
options=('!lto')

prepare() {
  cd "${_pkgname}-${pkgver}"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" --locked
}

build() {
  cd "${_pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${_pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm 755 "target/release/hex-patch" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 docs/README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
