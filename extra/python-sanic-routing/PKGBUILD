# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Qirui Wang <wqr.prg@gmail.com>
# Contributor: Carl George < arch at cgtx dot us >

pkgname=python-sanic-routing
pkgver=23.6.0
pkgrel=1
pkgdesc='Core routing component for Sanic'
arch=("any")
url='https://sanicframework.org/'
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-sanic python-pytest-asyncio)
source=("https://github.com/sanic-org/sanic-routing/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('801c9aa35b812fc43c9b0574f8859bd9a3d97256a6c2f060afa6ac7f1cec7821')

build() {
    cd sanic-routing-$pkgver
    python setup.py build
}

check() {
    cd sanic-routing-$pkgver
    python -m pytest
}

package() {
    cd sanic-routing-$pkgver
    python setup.py install --skip-build --root="$pkgdir" --optimize=1
    install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
