# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=parcellite
pkgver=1.2.5
pkgrel=2
pkgdesc="Lightweight GTK clipboard manager"
arch=('x86_64')
url="https://github.com/rickyrockrat/parcellite"
license=('GPL-3.0-or-later')
depends=('gtk3')
makedepends=('intltool' 'psmisc')
optdepends=('xdotool')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rickyrockrat/parcellite/archive/refs/tags/$pkgver.tar.gz"
        'port-to-gtk-3.patch')
sha256sums=('01de9e5e608012700611c827559b3b3817aa9d1704e363dee1576b64e3f84530'
            '83c7133c650c7e4f16edab5ce2717c5e66613183b7d6a854498322dcd9024ac6')

prepare() {
  cd "$srcdir/parcellite-$pkgver"
  patch -Np1 -i ../port-to-gtk-3.patch
}

build() {
  cd "$srcdir/parcellite-$pkgver"
  make VERSION=$pkgver
}

package() {
  cd "$srcdir/parcellite-$pkgver"
  make DESTDIR="$pkgdir" prefix=/usr install
}
