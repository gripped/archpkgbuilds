# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-texteditor
pkgver=4.0.1
pkgrel=1
pkgdesc='MauiKit Text Editor components'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         mauikit
         qt6-base
         qt6-declarative
         sonnet
         syntax-highlighting)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c88dd89c4720a7dd28ca82540d35ea167f7ee9a4addf11ea0633c8715246d06c'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
