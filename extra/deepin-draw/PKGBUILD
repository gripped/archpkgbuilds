# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=6.5.12
pkgrel=1
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'gcc-libs' 'deepin-picker'
         'adobe-source-han-sans-otc-fonts')
makedepends=('git' 'cmake' 'qt6-tools' 'clang' 'qt6-declarative')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-draw.git#tag=$pkgver")
sha512sums=('d7089cf179b3ae1fcaa75961f03ac3947eadeb39578b3c4493aa1675343872d5c24ce44054e89696b12661def2269862d35bd10d85a59d0d2b0c192fdc2b8ade')

prepare() {
  cd deepin-draw
  # respect our LDFLAGS
  sed -e '/SET(CMAKE_EXE_LINKER_FLAGS/d' -i CMakeLists.txt src/CMakeLists.txt
}

build() {
  cd deepin-draw
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-draw
  make DESTDIR="$pkgdir" install
}
