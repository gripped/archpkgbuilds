# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=6.5.13
pkgrel=1
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'gcc-libs' 'deepin-picker'
         'adobe-source-han-sans-otc-fonts')
makedepends=('git' 'cmake' 'qt6-tools' 'clang' 'qt6-declarative')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-draw.git#tag=$pkgver")
sha512sums=('fe714e2205c9deda31940e618e6190b1d478fecf48b26a1dc5efd7f5e4199e91dda76fad7e02306dce2c113f704ba44a45fd1f4c81f50c23b883b34b293836a3')

prepare() {
  cd deepin-draw
  # respect our LDFLAGS
  sed -e '/SET(CMAKE_EXE_LINKER_FLAGS/d' -i CMakeLists.txt src/CMakeLists.txt
}

build() {
  cd deepin-draw
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-draw
  make DESTDIR="$pkgdir" install
}
