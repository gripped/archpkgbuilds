# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=6.5.5
pkgrel=1
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg'
         'gcc-libs' 'deepin-picker' 'adobe-source-han-sans-otc-fonts' 'kimageformats5')
makedepends=('git' 'cmake' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-draw.git#tag=$pkgver")
sha512sums=('c34f74cd4254216c3050fb21951e7d496101c64843508f1e7e78f187061350c0874395fa995a3a69478090b0bc1459971284f214d485c8623b0375dfc7ca81b5')

prepare() {
  cd deepin-draw
  git cherry-pick -n 98dd279cc876cf85e804778ed1be4087201c1e03
  # respect our LDFLAGS
  sed -e '/set(CMAKE_CXX_FLAGS/d' -e '/set(CMAKE_C_FLAGS/d' -e '/SET(CMAKE_EXE_LINKER_FLAGS/d' \
      -i src/CMakeLists.txt
}

build() {
  cd deepin-draw

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-draw
  make DESTDIR="$pkgdir" install
}
