# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=6.0.11
pkgrel=1
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg'
         'gcc-libs' 'deepin-picker' 'adobe-source-han-sans-otc-fonts' 'kimageformats5')
makedepends=('git' 'cmake' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-draw.git#tag=$pkgver")
sha512sums=('d473ff1f530f9961ac60b56b7b85a8a7398b1ef315221325cd7f8c60a2f8b8903110d58dd02670a3cc87f3a849df145e40ebc21183bf3643a3a03ccc7d5f733e')

prepare() {
  cd deepin-draw
  git cherry-pick -n 98dd279cc876cf85e804778ed1be4087201c1e03
  # respect our LDFLAGS
  sed -e '/set(CMAKE_CXX_FLAGS/d' -e '/set(CMAKE_C_FLAGS/d' -e '/SET(CMAKE_EXE_LINKER_FLAGS/d' \
      -i src/CMakeLists.txt
}

build() {
  cd deepin-draw

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-draw
  make DESTDIR="$pkgdir" install
}
