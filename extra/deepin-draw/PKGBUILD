# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=6.5.2
pkgrel=1
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg'
         'gcc-libs' 'deepin-picker' 'adobe-source-han-sans-otc-fonts' 'kimageformats5')
makedepends=('git' 'cmake' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-draw.git#tag=$pkgver")
sha512sums=('3924728585a7706f8f72b3f9525bc3d127fe7fc1054fa94461fdf689b4c8c9170178de85d361be8c44c91982dbec8c010a85f9e8fe50e1916f265a487266ae81')

prepare() {
  cd deepin-draw
  git cherry-pick -n 98dd279cc876cf85e804778ed1be4087201c1e03
  # respect our LDFLAGS
  sed -e '/set(CMAKE_CXX_FLAGS/d' -e '/set(CMAKE_C_FLAGS/d' -e '/SET(CMAKE_EXE_LINKER_FLAGS/d' \
      -i src/CMakeLists.txt
}

build() {
  cd deepin-draw

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-draw
  make DESTDIR="$pkgdir" install
}
