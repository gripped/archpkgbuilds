# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Joey Dumont <joey.dumont@gmail.com>

pkgname=python-h5py-openmpi
_pkgname=h5py
pkgver=3.15.1
pkgrel=3
pkgdesc="General-purpose Python bindings for the HDF5 library (OpenMPI version)"
arch=(x86_64)
url="https://www.h5py.org/"
license=(BSD-3-Clause)
depends=(
  glibc
  hdf5-openmpi
  liblzf
  python
  python-mpi4py
  python-numpy
)
makedepends=(
  cython
  python-build
  python-installer
  python-pkgconfig
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytables
  python-pytest
  python-pytest-mpi
)
conflicts=(python-h5py)
provides=(python-h5py)
source=(
  "https://github.com/h5py/h5py/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
  remove-version-check.patch
)
sha256sums=('55a03d8fc1db535b243cccda3e6237844da25a17a2df3bc0ea9db6fbbbd7bf2b'
            '621f108e5e95dc250047501447d0dc633fbf3f8bf19e4c4b4c4d5a286864664c')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's/mpi4py ==/mpi4py >=/g' setup.py
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" setup_build.py
  # Drop annoying version check
  patch -p1 -i ../remove-version-check.patch
}

build() {
  cd $_pkgname-$pkgver
  CC=mpicc HDF5_MPI="ON" H5PY_SYSTEM_LZF=1 \
    python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  local python_version=$(test-env/bin/python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  local site_packages=test-env/lib/python$python_version/site-packages
  H5PY_TEST_CHECK_FILTERS=1 test-env/bin/python -m pytest "$site_packages/h5py" \
    --deselect="$site_packages/h5py/tests/test_file2.py::TestCache::test_defaults" \
    --deselect="$site_packages/h5py/tests/test_file2.py::TestCache::test_nbytes" \
    --deselect="$site_packages/h5py/tests/test_file2.py::TestCache::test_w0"
}

package() {
  cd $_pkgname-$pkgver
  HDF5_MPI="ON" python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
