# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Paulo Matias <matias.archlinux-br.org>
# Contributor: Leslie P. Polzer <polzer.gnu.org>
# Contributor: erm67 <erm67.yahoo.it>
# Contributor: Daniel J Griffiths
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=djview
pkgver=4.12.3
pkgrel=1
pkgdesc='Viewer for DjVu documents'
arch=('x86_64')
url='https://djvu.sourceforge.net/djview4.html'
license=('GPL2')
depends=('qt6-base' 'qt6-5compat' 'djvulibre')
makedepends=('qt6-tools')
source=("https://downloads.sourceforge.net/djvu/$pkgname-$pkgver.tar.gz"
        'djvulibre-djview4.appdata.xml')
sha256sums=('17bfb9731ab8070e01235381f08e57b1231ffb5f49eb031873ebfa189cdaf47d'
            '8d14017cd80fbf5c1520aed88f0db8b1ab091987b4f69543d8eaf40801e739f9')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-nsdejavu QMAKE=qmake6
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../djvulibre-djview4.appdata.xml "$pkgdir/usr/share/metainfo/djvulibre-djview4.appdata.xml"
  ln -s djview "$pkgdir/usr/bin/djview4"
}
