# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Gilles Gagniard <gilles@gagniard.org>
# Contributor: JD Steffen <jd at steffennet dot org>

pkgname=simutrans
pkgver=124.3.1
pkgrel=1
pkgdesc="Transportation simulation game"
arch=('x86_64')
url="https://www.simutrans.com/"
license=('custom:Artistic')
depends=('gcc-libs' 'zlib' 'sdl2' 'libpng' 'bzip2' 'freetype2' 'fontconfig' 'simutrans-pak64' 'hicolor-icon-theme')
makedepends=('dos2unix' 'cmake' 'git')
optdepends=('timidity++: play MIDI music')
source=(git+https://github.com/simutrans/simutrans#tag=$pkgver
        settings-folder.patch)
sha256sums=('64ba30530ba678d0020e22e9d64ef921171245c45d615ffb3ca50eead1592b77'
            'c38efe293ab3e6439a204545b0102abbb6db04c72a11486bdd391bc4d5cf3d2a')

prepare() {
  # Some files are distributed in DOS format
  find . -type f -exec dos2unix -q '{}' \;

  # Adjust paths
  patch -d simutrans/src/simutrans -Np0 -i "$srcdir"/settings-folder.patch
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -Dm644 simutrans/simutrans/license.txt "$pkgdir/usr/share/licenses/simutrans/license.txt"
}
