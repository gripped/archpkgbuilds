# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Katie Wolfe <wolfe@katie.host>

pkgname=fennel
pkgver=1.6.0
pkgrel=1
pkgdesc='A Lua Lisp language'
arch=('any')
url='https://fennel-lang.org'
license=('MIT')
depends=('lua')
makedepends=('git')
source=("$pkgname::git+https://git.sr.ht/~technomancy/fennel#tag=$pkgver")
sha256sums=('452ced6b67926c945169650cb931d69bfd2d2fb77c3a379434b8ef293b671ea1')

build() {
  cd "$pkgname"

  make build
}

check() {
  cd "$pkgname"

  make test
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
