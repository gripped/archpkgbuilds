# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>
pkgname=texstudio
pkgver=4.9.0
pkgrel=1
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL-3.0-only')
depends=('poppler-qt6' 'qt6-svg' 'libxkbcommon-x11' 'hicolor-icon-theme' 'hunspell' 'quazip-qt6' 'desktop-file-utils'
         'qt6-declarative' 'qt6-5compat')
makedepends=('qt6-tools' 'mercurial' 'imagemagick' 'librsvg' 'ninja' 'cmake')
optdepends=('evince: pdf reader'
            'languagetool: spelling and grammar checking'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/texstudio-org/texstudio/archive/${pkgver}.tar.gz"
        fs54269.patch
        fs72345.patch)
sha512sums=('06226e767d007137e08378a4f4a73c508c34db47eb20bb333152d044096dc3e5aecba44bef02ee1168ce35699af22bf5a8d4ffe1777df35015c967f2c011aa3e'
            '247e85f668b06c8a2def4e7456f0ddc8d35c7990484c1c037f7cf43fdee91419b5df66e968ef1e0c637fe685722bbad9171bdd79fad7a10912ae329aff088b68'
            'db176ba3e3b07a70c39c858b5edac3c2cec2278e7253d0a47ec7ec027586dae6354bd1114e5aac6857b85c23215af4417c43ca10ffe011ac5b6d92cccef3447b')

prepare() {
    cd texstudio-${pkgver}
    patch -Np1 -i "${srcdir}"/fs72345.patch
    patch -Np1 -d src -i "${srcdir}"/fs54269.patch
}

build() {
    cmake -B build -S texstudio-${pkgver} \
        -GNinja \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -Wno-dev

    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install

    for res in $(/bin/ls /usr/share/icons/hicolor | grep '[0-9]\+x[0-9]\+'); do
        mkdir -p "${pkgdir}"/usr/share/icons/hicolor/${res}/apps
        convert +set date:create +set date:modify -background none -density 1200 -resize ${res} \
            "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/texstudio.svg \
            "${pkgdir}"/usr/share/icons/hicolor/${res}/apps/texstudio.png
    done

    # Remove the included dictionaries in favor of hunspell (FS#78778)
    rm -rf "${pkgdir}"/usr/share/texstudio/{*.dic,*.aff}
}
