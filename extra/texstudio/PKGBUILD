# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>
pkgname=texstudio
pkgver=4.8.7
pkgrel=1
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt6' 'qt6-svg' 'libxkbcommon-x11' 'hicolor-icon-theme' 'hunspell' 'quazip-qt6' 'desktop-file-utils'
         'qt6-declarative' 'qt6-5compat')
makedepends=('qt6-tools' 'mercurial' 'imagemagick' 'librsvg' 'ninja' 'cmake')
optdepends=('evince: pdf reader'
            'languagetool: spelling and grammar checking'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/texstudio-org/texstudio/archive/${pkgver}.tar.gz"
        fs54269.patch
        fs72345.patch)
sha512sums=('bdfc59b3c08d6ff15eb00184455f4b5e2cfb353e2624c28363f525d2d31ebf7c741aa0a9ae3236a07cb910bc93db2460c3191e6c6e66b4e6da24e40396151e23'
            '247e85f668b06c8a2def4e7456f0ddc8d35c7990484c1c037f7cf43fdee91419b5df66e968ef1e0c637fe685722bbad9171bdd79fad7a10912ae329aff088b68'
            '6975eebd43b32fb7aa04f973562e66f6a83df0fb6a0b4d8221e124c9cc6959df11ec517e4c862921619c8b1c55800257017e26001c03186b84c567fc19c98726')

prepare() {
    cd texstudio-${pkgver}/src
    patch -Np1 -i "${srcdir}"/fs54269.patch
    patch -Np1 -i "${srcdir}"/fs72345.patch
}

build() {
    cmake -B build -S texstudio-${pkgver} \
        -GNinja \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -Wno-dev

    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install

    for res in $(/bin/ls /usr/share/icons/hicolor | grep '[0-9]\+x[0-9]\+'); do
        mkdir -p "${pkgdir}"/usr/share/icons/hicolor/${res}/apps
        convert +set date:create +set date:modify -background none -density 1200 -resize ${res} \
            "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/texstudio.svg \
            "${pkgdir}"/usr/share/icons/hicolor/${res}/apps/texstudio.png
    done

    # Remove the included dictionaries in favor of hunspell (FS#78778)
    rm -rf "${pkgdir}"/usr/share/texstudio/{*.dic,*.aff}
}
