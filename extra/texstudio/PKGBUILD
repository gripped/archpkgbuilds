# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>
pkgname=texstudio
pkgver=4.8.9
pkgrel=1
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL-3.0-only')
depends=('poppler-qt6' 'qt6-svg' 'libxkbcommon-x11' 'hicolor-icon-theme' 'hunspell' 'quazip-qt6' 'desktop-file-utils'
         'qt6-declarative' 'qt6-5compat')
makedepends=('qt6-tools' 'mercurial' 'imagemagick' 'librsvg' 'ninja' 'cmake')
optdepends=('evince: pdf reader'
            'languagetool: spelling and grammar checking'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/texstudio-org/texstudio/archive/${pkgver}.tar.gz"
        fs54269.patch
        fs72345.patch)
sha512sums=('b552e36bf489ac974311e06173f2779cb640d16b88f923d73ff910cc7533b7c4c3374fd7a37e56cc03de3bf8a035e71708f11e14584c880c128505169fca51f7'
            '247e85f668b06c8a2def4e7456f0ddc8d35c7990484c1c037f7cf43fdee91419b5df66e968ef1e0c637fe685722bbad9171bdd79fad7a10912ae329aff088b68'
            'e2a1eee3c951d67cb0a5d39bb7a3bcd13cd8195c5d74cce170522c857e8b5515c018cc9f3fbdc33b40ef82b43dccb18c9d5ea07e846b3c77e6c941b70ceb1288')

prepare() {
    cd texstudio-${pkgver}
    patch -Np1 -i "${srcdir}"/fs72345.patch
    patch -Np1 -d src -i "${srcdir}"/fs54269.patch
}

build() {
    cmake -B build -S texstudio-${pkgver} \
        -GNinja \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -Wno-dev

    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install

    for res in $(/bin/ls /usr/share/icons/hicolor | grep '[0-9]\+x[0-9]\+'); do
        mkdir -p "${pkgdir}"/usr/share/icons/hicolor/${res}/apps
        convert +set date:create +set date:modify -background none -density 1200 -resize ${res} \
            "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/texstudio.svg \
            "${pkgdir}"/usr/share/icons/hicolor/${res}/apps/texstudio.png
    done

    # Remove the included dictionaries in favor of hunspell (FS#78778)
    rm -rf "${pkgdir}"/usr/share/texstudio/{*.dic,*.aff}
}
