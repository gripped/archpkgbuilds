# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=vector
pkgver=0.53.0
pkgrel=1
pkgdesc="A high-performance observability data pipeline"
arch=("x86_64")
_target="x86_64-unknown-linux-gnu"
url="https://vector.dev"
license=("MPL-2.0")
options=(!lto) # TODO: Build with LTO
backup=(
    "etc/vector/vector.toml" # Versions <0.35.0
    "etc/vector/vector.yaml"
    "etc/default/vector"
)
depends=("gcc-libs" "zlib")
# https://github.com/vectordotdev/vector/blob/master/docs/DEVELOPING.md#bring-your-own-toolbox
makedepends=(
    "rustup"
    "protobuf"
    "python"
    "perl"
    "cmake"
    "git"
    "clang"
)
checkdepends=(
    "cargo-nextest"
)
source=(
    "git+https://github.com/vectordotdev/vector#tag=v${pkgver}"
    "${pkgname}.sysusers"
    "${pkgname}.tmpfiles"
)
sha512sums=('b72f096a10aafe831dce470d79a067b9d5ee24df9751521ab608f2db9526d473b012cd8e3ab4d217ee2338a7a33d0b540f01a217e76e67bcb5aab70817740c0d'
            '9179cf571762819773d8f14ce457d20a54e5a7dcaf24ea88a11723af63c31775a3b59fc17fca41de547fc0cc3a72bf7501a8abfb4a95558f1e8789b12b951733'
            'c192492df09d131f9174d2acbb7f265c280eb6d678589b8c93bacc47ae55c51573a5477d715897f8580ced420730992fb68bee78b374f1cc042888ea6b5512fd')

pkgver() {
  cd "${pkgname}"
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${pkgname}"

    cargo fetch \
        --locked
}

build() {
    cd "${pkgname}"

    # # Required for rdkafka-sys
    export CMAKE_POLICY_VERSION_MINIMUM=3.5

    # https://github.com/vectordotdev/vector/issues/22888
    export CFLAGS="${CFLAGS} -std=gnu17"

    cargo build \
        --frozen \
        --release \
        --locked \
        --target "${_target}"

    # Since cargo nextest drops our release build, we need to back it up
    cp -r target/${_target}/release releasebuild
}

check() {
    cd "${pkgname}"

    # Unit-Tests only, integration tests require services.
    # UDP syslog/socket sources currently take too long, so we skip them.
    # Test sources::file::tests::file_start_position_server_restart_unfinalized is flaky,
    # see https://github.com/vectordotdev/vector/issues/23813
    cargo nextest run \
        --workspace \
        --fail-fast \
        --test-threads num-cpus \
        --frozen \
        --release \
        --locked \
        --no-default-features \
        --features "target-${_target}" \
        --target "${_target}" \
        -- \
        --skip sources::socket::test::multicast_udp_message \
        --skip sources::socket::test::multicast_and_unicast_udp_message \
        --skip sources::socket::test::multiple_multicast_addresses_udp_message \
        --skip sources::syslog::test::test_udp_syslog \
        --skip sources::file::tests::file_start_position_server_restart_unfinalized
}

package() {
    install -Dm644 "${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
    install -Dm644 "${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

    cd "${pkgname}"

    for f in LICENSE NOTICE; do
        install -Dm644 "$f" "${pkgdir}/usr/share/licenses/${pkgname}/$f"
    done

    for f in CHANGELOG PRIVACY README RELEASES SECURITY VERSIONING; do
        install -Dm644 "$f.md" "${pkgdir}/usr/share/doc/${pkgname}/$f.md"
    done

    install -Dm755 "releasebuild/vector" "${pkgdir}/usr/bin/vector"

    install -Dm644 "config/vector.yaml" "${pkgdir}/etc/vector/vector.yaml"
    chmod 0550 "${pkgdir}/etc/vector"
    cp -r config/examples "${pkgdir}/usr/share/doc/${pkgname}/examples"

    install -Dm644 "distribution/systemd/vector.service" "${pkgdir}/usr/lib/systemd/system/vector.service"
    install -Dm644 "distribution/systemd/hardened-vector.service" "${pkgdir}/usr/lib/systemd/system/hardened-vector.service"
    install -Dm644 "distribution/systemd/vector.default" "${pkgdir}/etc/default/vector"
}
