# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=miniserve
pkgver=0.31.0
pkgrel=1
pkgdesc="Tool to serve files via HTTP"
arch=('x86_64')
url="https://github.com/svenstaro/miniserve"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('rust')
options=(!lto)
source=(miniserve-$pkgver.tar.gz::https://github.com/svenstaro/miniserve/archive/v${pkgver}.tar.gz)
sha512sums=('be379204172347b88a04a14481a48c0e07051fe97a3102a9f3348d39ae61e2462068a5ab734bc7df4deccb3181281e7aa07f19cf04e65f88d798b77003724d25')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo test --release --locked -- --test-threads=1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/miniserve "$pkgdir"/usr/bin/miniserve

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/miniserve --print-completions bash > "$pkgdir"/usr/share/bash-completion/completions/miniserve
  target/release/miniserve --print-completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_miniserve
  target/release/miniserve --print-completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/miniserve.fish

  mkdir -p "$pkgdir"/usr/share/man/man1
  target/release/miniserve --print-manpage | gzip > "$pkgdir"/usr/share/man/man1/$pkgname.1.gz

  install -Dm644 packaging/miniserve@.service "$pkgdir"/usr/lib/systemd/system/miniserve@.service

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
