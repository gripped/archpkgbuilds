# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Phil Pirozhkov <pirjsuka@gmail.com>

pkgname=mtd-utils
pkgver=2.3.0
pkgrel=1
pkgdesc='Utilities for dealing with MTD devices'
url='http://www.linux-mtd.infradead.org/'
arch=(x86_64)
license=(GPL-2.0-only)
depends=(
  bash
  glibc
  lzo
  openssl
  util-linux-libs
  zlib
  zstd
)
makedepends=(
  cmocka
  git
)
source=("git+git://git.infradead.org/mtd-utils.git#tag=v${pkgver}?signed")
b2sums=('d3943d1131f23a4282e2b36c0768283e0089f57549ee01f87893f943adb399c14bbf5998b73f1bce17c4e6e949ba3130fce08c6451b4adc0924243c5cfe39f2a')
validpgpkeys=('13063F723C9E584AEACD5B9BBCE5DC3C741A02D1') # David Oberhollenzer <david.oberhollenzer@sigma-star.at>

prepare() {
  cd ${pkgname}
  autoreconf -fiv
}

build() {
  cd ${pkgname}
  # Tests fail with fortification enabled
  export CFLAGS="${CFLAGS/-Wp,-D_FORTIFY_SOURCE=?/}"
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sbindir=/usr/bin \
    --enable-unit-tests \
    --with-tests
  make
}

check() {
  cd ${pkgname}
  make check -j1
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
