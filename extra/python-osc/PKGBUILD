# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-osc
pkgver=1.9.3
pkgrel=1
pkgdesc='Open Sound Control server and client implementations in pure Python'
arch=(any)
url="https://github.com/attwad/python-osc"
license=(Unlicense)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('792fa9cfa6f56b8975174f42718db257b99b586fae1e14f24c5bbf673dbfaafd8374b5fcee060db890f02e990658b49ed3ec8942f863cf4e394a3f214455946f')
b2sums=('9f20910f59b951f0a33ea39e58c4b9ef77912c0c9474f30498d9d0a9d8b8f9334048950d5e1dab087d5cb4ee1e7937f9c2d9a9a37fb0517455e82f7f89d44ee0')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
 }

check() {
  cd $pkgname-$pkgver
  pytest -v
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
