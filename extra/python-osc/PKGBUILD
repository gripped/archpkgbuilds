# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-osc
pkgver=1.9.0
pkgrel=3
pkgdesc='Open Sound Control server and client implementations in pure Python'
arch=(any)
url="https://github.com/attwad/python-osc"
license=(Unlicense)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('5c0b0dbbaf892d05c4e20ad0cb9525ef440f6b8b5aaf8070770e8f97ffb9f126de857e1a79e0b8c1ab64436a07b63cc0b45d7f106b2ddb8456fca23314b3d867')
b2sums=('1b3f244ab6d6cd12af4a118d7ee0a608d2a77ae6887cf13395f530285a1beceddc481733059f56da2854f701ebbc5ef39bce6686a11852e386bb33dc2c4d20ec')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
 }

check() {
  cd $pkgname-$pkgver
  pytest -v
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
