# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Fredy Garc√≠a <frealgagu at gmail dot com>
# Contributor: Maxim Baz <${pkgname} at maximbaz dot com>
# Contributor: Stefan Cocora <stefan dot cocora at gmail dot com>

pkgname=skaffold
pkgver=2.17.1
pkgrel=1
pkgdesc="A command line tool that facilitates continuous development for Kubernetes applications"
arch=("x86_64")
url="https://github.com/GoogleContainerTools/skaffold"
license=("Apache-2.0")
depends=(
  "docker"
  "glibc"
  "kompose"
)
makedepends=(
  "git"
  "go"
  "go-licenses"
  "python"
)
optdepends=(
  "kubectl: For Kubernetes support"
  "minikube: To use Minikube"
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('c95a20081b9499709f93ad2032544a025b961493c0a9fed37ad572fcc1059763948b05b4d1c32969e4c839da28fe2254a7f1f6241adb483463ece38200104527')
b2sums=('0bd734f4dd8610f8446562752cc9615a5ed53ddbc60b652ed661be06dacc6d9a156a4bf7034e1125ea70ba2a966374477cabcde3c7c935c65cf9a1c4f284478f')

prepare() {
  cd "${pkgname}"
  # Remove checks that requires running Docker.
  sed -i "/test-generated-proto/d" hack/checks.sh
  sed -i "/hack\/check-docs.sh/d" hack/checks.sh
  # Fails for some reason
  sed -i "/hack\/check-schema-changes.sh/d" hack/checks.sh

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "${pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="${srcdir}"

  local tags="timetzdata osusergo netgo release"
  local version_package="github.com/GoogleContainerTools/skaffold/v2/pkg/skaffold/version"
  local ld_flags=" \
    -X ${version_package}.gitCommit=$(git rev-parse HEAD) \
    -X ${version_package}.version=${pkgver} \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "${ld_flags}" -tags "${tags}" ./cmd/skaffold/skaffold.go
}

check() {
  cd "${pkgname}"
  # Skip integration tests, requires Java, a running Docker daemon and
  # connection to a Kubernetes cluster.
  local tests=$(
    go list ./... \
      | grep -v github.com/GoogleContainerTools/skaffold/v2/integration \
      | grep -v github.com/GoogleContainerTools/skaffold/v2/pkg/skaffold/kubernetes/portforward	
  )
  export PATH="${PWD}:${PATH}"
  # shellcheck disable=SC2086
  go test -v -short -timeout=90s $tests
  ./hack/checks.sh
}

package() {
  cd "${pkgname}"
  install -vDm755 -t "${pkgdir}/usr/bin" skaffold
  ./skaffold completion bash \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/skaffold"
  ./skaffold completion zsh \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_skaffold"
  ./skaffold completion fish \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/skaffold.fish"
}
