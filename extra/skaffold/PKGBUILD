# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Fredy Garc√≠a <frealgagu at gmail dot com>
# Contributor: Maxim Baz <${pkgname} at maximbaz dot com>
# Contributor: Stefan Cocora <stefan dot cocora at gmail dot com>

pkgname=skaffold
pkgver=2.14.1
pkgrel=1
pkgdesc="A command line tool that facilitates continuous development for Kubernetes applications"
arch=("x86_64")
url="https://github.com/GoogleContainerTools/skaffold"
license=("Apache-2.0")
depends=(
  "docker"
  "glibc"
  "kompose"
)
makedepends=(
  "git"
  "go"
  "go-licenses"
  "python"
)
optdepends=(
  "kubectl: For Kubernetes support"
  "minikube: To use Minikube"
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('9cad97815dbbfa2c4eec83cf9db2f114f8bbe0f4a983c9f08d0b8a7837db7d39edaefcd8278235f5046686a0e33b042f3423f4b596f2ef33892b12360a848a14')
b2sums=('e1bf268f59e7d2f59520b82d2d0317039a453cc4618613b917f195a643dd648e08e72809bbbaa03de041f1440e97657f1bfe084e40d661f4a2d70b989402d4d9')

prepare() {
  cd "${pkgname}"
  # Remove checks that requires running Docker.
  sed -i "/test-generated-proto/d" hack/checks.sh
  sed -i "/hack\/check-docs.sh/d" hack/checks.sh

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "${pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="${srcdir}"

  local tags="timetzdata osusergo netgo release"
  local version_package="github.com/GoogleContainerTools/skaffold/v2/pkg/skaffold/version"
  local ld_flags=" \
    -X ${version_package}.gitCommit=$(git rev-parse HEAD) \
    -X ${version_package}.version=${pkgver} \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "${ld_flags}" -tags "${tags}" ./cmd/skaffold/skaffold.go
}

check() {
  cd "${pkgname}"
  # Skip integration tests, requires Java, a running Docker daemon and
  # connection to a Kubernetes cluster.
  local tests=$(
    go list ./... \
      | grep -v github.com/GoogleContainerTools/skaffold/v2/integration
  )
  export PATH="${PWD}:${PATH}"
  # shellcheck disable=SC2086
  go test -v -short -timeout=90s $tests
  ./hack/checks.sh
}

package() {
  cd "${pkgname}"
  install -vDm755 -t "${pkgdir}/usr/bin" skaffold
  ./skaffold completion bash \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/skaffold"
  ./skaffold completion zsh \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_skaffold"
  ./skaffold completion fish \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/skaffold.fish"
}
