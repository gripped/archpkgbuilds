# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: Corey Hinshaw <corey@electrickite.org>

pkgname=system76-firmware
pkgver=1.0.71
pkgrel=1
pkgdesc='System76 Firmware Tool and Daemon'
arch=(x86_64)
url=https://github.com/pop-os/system76-firmware
license=(GPL3)
depends=(
  ca-certificates
  dbus
  dfu-programmer
  gcc-libs
  glibc
  efibootmgr
  openssl
  xz
)
makedepends=(
  git
  rust
)
provides=(system76-firmware-daemon)
conflicts=(system76-firmware-daemon)
_tag=5a35c6c06602d402ecf367b6d7aa156eaa83bb02
source=(git+https://github.com/pop-os/system76-firmware.git#tag=${_tag})
b2sums=('53d3fe9708c94b007b189e93eb41b4ec500c7799c5db6a39f56cfec96ee4124c3eaafeb76a37e061f1d15458e1cf4ebbaa2731ddde996b53a0438189ce16f64f')

pkgver() {
  cd system76-firmware
  git describe --tags
}

build() {
  cd system76-firmware
  make
}

package() {
  cd system76-firmware
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/etc/systemd "${pkgdir}"/usr/lib/
}

# vim: ts=2 sw=2 et:
