# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: Corey Hinshaw <corey@electrickite.org>

pkgname=system76-firmware
pkgver=1.0.70
pkgrel=1
pkgdesc='System76 Firmware Tool and Daemon'
arch=(x86_64)
url=https://github.com/pop-os/system76-firmware
license=(GPL3)
depends=(
  ca-certificates
  dbus
  dfu-programmer
  gcc-libs
  glibc
  efibootmgr
  openssl
  xz
)
makedepends=(
  git
  rust
)
provides=(system76-firmware-daemon)
conflicts=(system76-firmware-daemon)
_tag=367fbbd82a5cf4e5ea3245b4aecf90f10d91c504
source=(git+https://github.com/pop-os/system76-firmware.git#tag=${_tag})
b2sums=('f0d2fb49b0e23f87f248b51c9968231451b5ec0d289e951a15fc6be66af460617946c56deed3f636ef00715c0207a049c2d6e5e604da29d99fd92e7bfb78d02b')

pkgver() {
  cd system76-firmware
  git describe --tags
}

build() {
  cd system76-firmware
  make
}

package() {
  cd system76-firmware
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/etc/systemd "${pkgdir}"/usr/lib/
}

# vim: ts=2 sw=2 et:
