# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: Corey Hinshaw <corey@electrickite.org>

pkgname=system76-firmware
pkgver=1.0.68
pkgrel=1
pkgdesc='System76 Firmware Tool and Daemon'
arch=(x86_64)
url=https://github.com/pop-os/system76-firmware
license=(GPL3)
depends=(
  ca-certificates
  dbus
  dfu-programmer
  gcc-libs
  glibc
  efibootmgr
  openssl
  xz
)
makedepends=(
  git
  rust
)
provides=(system76-firmware-daemon)
conflicts=(system76-firmware-daemon)
_tag=6e98acaa75c05344440d10f8fb924da0e2406238
source=(git+https://github.com/pop-os/system76-firmware.git#tag=${_tag})
b2sums=('92b90259e67c3e339ab9a93b5f850e5b0fdadbe72971c3b904218b3c60c978382adbc278c02eaf325a4c187cf4503c32b6bd909e8b099dd213774bc16b3e3923')

pkgver() {
  cd system76-firmware
  git describe --tags
}

build() {
  cd system76-firmware
  make
}

package() {
  cd system76-firmware
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/etc/systemd "${pkgdir}"/usr/lib/
}

# vim: ts=2 sw=2 et:
