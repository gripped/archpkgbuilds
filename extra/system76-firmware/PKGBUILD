# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: Corey Hinshaw <corey@electrickite.org>

pkgname=system76-firmware
pkgver=1.0.74
pkgrel=2
pkgdesc='System76 Firmware Tool and Daemon'
arch=(x86_64)
url=https://github.com/pop-os/system76-firmware
license=(GPL-3.0-only)
depends=(
  ca-certificates
  dbus
  dfu-programmer
  gcc-libs
  glibc
  efibootmgr
  openssl
  xz
)
makedepends=(
  git
  rust
)
provides=(system76-firmware-daemon)
conflicts=(system76-firmware-daemon)
_tag=c3e2bec4c00c90963a571cb4cda6a7462facfbe6
source=(git+https://github.com/pop-os/system76-firmware.git#tag=${_tag})
b2sums=('7504c90fb535896d1492724f37259462472d92874fa37fc4a9c0240f7bcfab425d3a575b1d2dfd9b6c22e179554eb41338d00af882fe6fcb09a702b40839b5ed')

pkgver() {
  cd system76-firmware
  git describe --tags
}

build() {
  cd system76-firmware
  make
}

package() {
  cd system76-firmware
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/etc/systemd "${pkgdir}"/usr/lib/
}

# vim: ts=2 sw=2 et:
