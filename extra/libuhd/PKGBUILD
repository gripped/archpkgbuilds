# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=libuhd
pkgver=4.7.0.0
pkgrel=3
pkgdesc="Universal Software Radio Peripheral (USRP) userspace driver"
arch=(x86_64)
url="https://github.com/EttusResearch/uhd"
license=(GPL-3.0-or-later)
depends=(
  boost-libs
  gcc-libs
  glibc
  libusb
  orc
)
makedepends=(
  boost
  cmake
  git
  python-mako
  python-numpy
  python-setuptools
)
checkdepends=(python-requests)
optdepends=(
  'python-mako: for imgbuilder'
  'python-numpy: for Python bindings'
  'python: for Python bindings and USRP utils'
)
provides=(libuhd.so)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('92d1957424aaddd476f22b2663bcf751793d68232292480593f8a57afbe12d5b')

prepare() {
  cd uhd
  # Boost 1.87 compatibility
  git cherry-pick -n 2dc7b3e572830c71d49ee0648eef445e7f3abfd6
  git cherry-pick -n adfe953d965e58b5931c1b1968899492c8087cf6
}

build() {
  cd uhd
  cmake -S host -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DENABLE_PYTHON_API=ON \
    -DENABLE_EXAMPLES=ON \
    -DENABLE_UTILS=ON \
    -DENABLE_TESTS=ON \
    -DENABLE_E300=ON
  cmake --build build
}

check() {
  cd uhd
  ctest --test-dir build --output-on-failure
}

package() {
  cd uhd
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 host/utils/uhd-usrp.rules \
    "$pkgdir/usr/lib/udev/rules.d/10-uhd-usrp.rules"
}
