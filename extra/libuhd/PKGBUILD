# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=libuhd
_pkgname=uhd
pkgver=4.7.0.0
pkgrel=2
pkgdesc="Universal Software Radio Peripheral (USRP) userspace driver"
arch=(x86_64)
url="https://files.ettus.com/manual/"
license=(GPL-3.0-or-later)
depends=(
  boost-libs
  gcc-libs
  glibc
  libusb
  orc
)
makedepends=(
  boost
  cmake
  python-mako
  python-numpy
  python-setuptools
)
checkdepends=(python-requests)
optdepends=(
  'python-mako: for imgbuilder'
  'python-numpy: for Python bindings'
  'python: for Python bindings and USRP utils'
)
provides=(libuhd.so)
source=("$pkgname-$pkgver.tar.gz::https://github.com/EttusResearch/uhd/archive/v$pkgver.tar.gz")
sha256sums=('afe56842587ce72d6a57535a2b15c061905f0a039abcc9d79f0106f072a00d10')

build() {
  cd $_pkgname-$pkgver
  cmake -S host -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DENABLE_PYTHON_API=ON \
    -DENABLE_EXAMPLES=ON \
    -DENABLE_UTILS=ON \
    -DENABLE_TESTS=ON \
    -DENABLE_E300=ON
  cmake --build build
}

check() {
  cd $_pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package() {
  cd $_pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 host/utils/uhd-usrp.rules \
    "$pkgdir/usr/lib/udev/rules.d/10-uhd-usrp.rules"
}
