# Maintainer:
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=libuhd
pkgver=4.6.0.0
pkgrel=4
pkgdesc="Universal Software Radio Peripheral (USRP) userspace driver"
arch=('x86_64')
url="https://files.ettus.com/manual/"
license=('GPL')
depends=('boost-libs' 'orc' 'libusb')
optdepends=('python: usrp utils'
            'python-numpy: python api')
makedepends=('git' 'cmake' 'boost' 'python-setuptools' 'python-mako' 'python-numpy')
options=(!lto)
# gpsd?  dpdk?

source=(git+https://github.com/EttusResearch/uhd#tag=v$pkgver)
sha256sums=('cbbb74818602b1c035db97530f2d541d90c17bc90f12addc64de01e9eba3115f')

prepare() {
  cd uhd
  git cherry-pick -n ea586168c596d13d05d145832519755794649ba0
  git cherry-pick -n c4863b9b9f8b639260f7797157e8ac4dd81fef93 # Fix build with boost 1.85
}

build() {
  cmake -B build -S uhd/host \
           -DCMAKE_INSTALL_PREFIX=/usr/ \
           -DPYTHON_EXECUTABLE=/usr/bin/python3 \
           -DENABLE_PYTHON_API=ON \
           -DENABLE_EXAMPLES=ON \
           -DENABLE_UTILS=ON \
           -DENABLE_TESTS=OFF \
           -DENABLE_E100=ON \
           -DENABLE_E300=ON
  cmake --build build
}

check() {
  cd build
  make test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 uhd/host/utils/uhd-usrp.rules "$pkgdir/usr/lib/udev/rules.d/10-uhd-usrp.rules"
} 
