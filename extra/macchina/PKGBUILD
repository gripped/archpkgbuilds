# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: grtcdr <ba.tahaaziz@gmail.com>

pkgname=macchina
pkgver=6.4.0
pkgrel=1
pkgdesc="A system information fetcher, with an (unhealthy) emphasis on performance."
arch=('x86_64')
url="https://github.com/Macchina-CLI/macchina"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cargo' 'libgit2' 'git')
optdepends=('wmctrl: window manager support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('edd7591565f199c1365420655a144507bcd2838aed09b79fefdc8b661180432f')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked --target-dir=target
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 "doc/${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 "doc/${pkgname}.7" -t "${pkgdir}/usr/share/man/man7"

  install -d "${pkgdir}/usr/share/${pkgname}/themes"
  install -m 644 contrib/themes/* "${pkgdir}/usr/share/${pkgname}/themes"
}

# vim: ts=2 sw=2 et:
