# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20251025
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('23f85c6dd119db313aa4109b9ce6de137bea1a883df47a17396c2247dd18055f'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '0b67291a225be317e9379594d5402d6b27db7befce9afeb387b2f7e2f44b4b8d'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '641496715eeadcf932bb99a1b2184f75cbdbc94c2a3bf2ca53c3e3fbba08d9b7'
            'eceed2ccdc12df2e0f342186a660b98cf304d5671b0c79ea6ce6a1db27e691b5'
            '7db580710cf416ef6fc482c728a67fa6f660d2074a9a9f5ddb3e129b5100e755'
            'b73badbf6879be306ecbee0e24b08d4bffb8aa8c3ce70d6548ec405128f3cf47'
            '93cafdf7fede36f175710b7125df5e1a8d3578dc196eee9f4ad914202533b618'
            '68bfe94bbf593c4c19cfe54f542ba840a54ba5aae85d308c5d59561498d57f27'
            '8f5089b89efdfe2e55e1c39544c6bae3b492c77efbc9ca39d90f0036b2d3dc09'
            '7ce9a6442c2796b4102b0cd741dfaa4ec3cd416b6964c6c43bf7a7fdd98666eb')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
