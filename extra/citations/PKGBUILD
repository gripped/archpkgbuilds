# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=citations
pkgver=0.9.0
pkgrel=1
pkgdesc='Manage your bibliographies using the BibTeX format'
arch=(x86_64)
url='https://apps.gnome.org/Citations/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  openssl
  poppler-glib
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/citations.git#tag=$pkgver")
b2sums=(2bafc52297bde73782fa67e2d5f037f568dfd6ff63bad3539f5b7faa0b8df2662a8d94bd0f753fb3735939b4e498d3f2b6134541fe1194b31734a264334d201d)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
