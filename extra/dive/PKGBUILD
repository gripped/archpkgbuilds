# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: mark.blakeney at bullet-systems dot net
# Contributor: David Birks <david@birks.dev>
# Contributor: David Stark <david@starkers.org>

pkgname=dive
pkgver=0.13.1
pkgrel=1
pkgdesc="A tool for exploring layers in a docker image"
arch=('x86_64')
url="https://github.com/wagoodman/dive"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
checkdepends=('python')
source=("git+$url.git#tag=v$pkgver")
sha256sums=('SKIP')

build() {
  cd $pkgname
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-X main.version=$pkgver -linkmode external -extldflags \"${LDFLAGS}\"" \
    -o bin/dive \
    .
}

check() {
  cd $pkgname
  make test
}

package() {
  cd $pkgname
  install -Dm 755 "bin/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
