# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: mark.blakeney at bullet-systems dot net
# Contributor: David Birks <david@birks.dev>
# Contributor: David Stark <david@starkers.org>

pkgname=dive
pkgver=0.13.0
pkgrel=1
pkgdesc="A tool for exploring layers in a docker image"
arch=('x86_64')
url="https://github.com/wagoodman/dive"
license=('MIT')
depends=('glibc')
makedepends=('go')
checkdepends=('python')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('91449d9777c3b52ddfd9cf922b8bb980cd9270eaf056ea70b319a8779f7e2c2b')

build() {
  cd $pkgname-$pkgver
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-X main.version=$pkgver -linkmode external -extldflags \"${LDFLAGS}\"" \
    -o bin/dive \
    .
}

check() {
  cd "$pkgname-$pkgver"
  make test
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "bin/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 LICENSE  -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
