# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-bug
pkgver=0.10.1
pkgrel=2
pkgdesc='Distributed, offline-first bug tracker embedded in git, with bridges '
arch=(x86_64)
url='https://github.com/MichaelMure/git-bug'
license=(GPL-3.0-only)
depends=(glibc git)
makedepends=(go)
options=(!lto)
source=("git+$url#tag=v$pkgver")
sha512sums=('80e3b3bc1d6ae8847fa7bcda418e29f42caf345876ccf3715ae53ef8edf88bd7ab28bc158660c16b7f4ba021c15c365062b82fc112ad922fba5ba2c1b7a9e6e0')
b2sums=('cbeac34dc63676079f8ff5ba02abdc90762e1f55ffbc5572d98e2a4a8764ffd6ddce4ec6bb97712517c8c26401a7ee78ab9611f97244b63544bd9963e9d53b1e')

# NOTE: webui assets seem to be pregenerated & committed into the repo
# so there is very little point in regenerating them

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  local GIT_COMMIT="$(git rev-list -1 HEAD)"
  local GIT_LAST_TAG="$(git describe --abbrev=0 --tags)"
  local GIT_EXACT_TAG="$(git name-rev --name-only --tags HEAD)"
  local COMMANDS_PATH="github.com/MichaelMure/git-bug/commands"

  go generate

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
        -X ${COMMANDS_PATH}.GitCommit=${GIT_COMMIT} \
        -X ${COMMANDS_PATH}.GitLastTag=${GIT_LAST_TAG} \
        -X ${COMMANDS_PATH}.GitExactTag=${GIT_EXACT_TAG}" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/git-bug

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" doc/man/*.1

  # shell completion
  install -vDm644 misc/completion/bash/git-bug "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 misc/completion/fish/git-bug "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -vDm644 misc/completion/zsh/git-bug "$pkgdir/usr/share/zsh/site-functions/_$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/md/" doc/md/*.md
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/assets/" doc/assets/*.png
}
