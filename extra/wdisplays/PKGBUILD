# Maintainer: Campbell Jones <serebit@archlinux.org>

pkgname=wdisplays
pkgver=1.1.3
pkgrel=2
pkgdesc="Graphical application for configuring Wayland displays"
arch=('x86_64')
url="https://github.com/artizirk/wdisplays"
license=('GPL-3.0-or-later')
depends=(
	'cairo'
	'glibc'
	'glib2'
	'gtk3'
	'hicolor-icon-theme'
	'libepoxy'
	'pango'
	'wayland'
)
makedepends=(
	'git'
	'meson'
	'wayland-protocols'
)
source=("git+https://github.com/artizirk/wdisplays.git#tag=$pkgver")
b2sums=('45b05ce292276c135ec3199d2abad355417c2caae21d658a82039d57e902fe44b1d94cf4546198aa19a615747f9c521adfd855b3eb6297c6734247cf3737b7ec')

build() {
	arch-meson "$pkgname" build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
