# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.12.19
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'dtkwidget'
         'qt5-base' 'qt5-x11extras' 'qt5-multimedia' 'deepin-turbo' 'ffmpeg4.4' 'libxtst' 'opencv'
         'dconf' 'libxext' 'libx11' 'libxfixes' 'portaudio' 'deepin-image-editor' 'gcc-libs')
makedepends=('git' 'deepin-tray-loader' 'qt5-tools')
optdepends=('deepin-shortcut-viewer: for shortcut display')
provides=('deepin-screenshot')
conflicts=('deepin-screenshot')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-screen-recorder.git#tag=$pkgver"
        "$pkgname-unqualified-id.patch")
sha512sums=('184a6d15f0b0f54b857f050aa1ec8cfae295730f140459e3f819656f20f75d1d1d56fc482720a851811316cca2cab53a9d8b6ecd742026d41c0cc80342e5a94c'
            '5624a25292b0d4f74aa73c1374c51d9098db8a6984e9586da1f88349c0315e2dde1cb6f3b26ac7db6fd04044b9283238ab93a607be6883f746b0e1a57e8c3ce3')

prepare() {
  cd deepin-screen-recorder
  patch -Np1 -i ../$pkgname-unqualified-id.patch

  # https://github.com/linuxdeepin/developer-center/issues/3035
  sed -i "s|/etc/os-version|/etc/uos-version|" src/src.pro

  # OpenCV 4 compatibility
  sed -i '/#include<opencv2/i #include <opencv2/imgproc/types_c.h>' src/utils/pixmergethread.h

  # OpenCV missing in pkg-config targets
  sed -i 's/dframeworkdbus/dframeworkdbus opencv4/' src/src.pro
}

build(){
  cd deepin-screen-recorder
  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  qmake-qt5 PREFIX=/usr
  CFLAGS+=" -Wno-implicit-function-declaration -I/usr/include/ffmpeg4.4/" \
  make
}

package() {
  cd deepin-screen-recorder
  make INSTALL_ROOT="$pkgdir" install
}
