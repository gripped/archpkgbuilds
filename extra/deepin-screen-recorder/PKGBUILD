# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=6.5.1
pkgrel=2
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL-3.0-or-later')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'deepin-tray-loader' 'dtkcore' 'dtkgui' 'dtkwidget'
         'qt5-base' 'qt5-x11extras' 'qt5-multimedia' 'deepin-turbo' 'ffmpeg4.4' 'libxtst' 'opencv'
         'dconf' 'libprocps' 'libxext' 'libx11' 'libxfixes' 'portaudio' 'deepin-image-editor' 'gcc-libs')
makedepends=('git' 'deepin-tray-loader' 'qt5-tools')
optdepends=('deepin-shortcut-viewer: for shortcut display')
provides=('deepin-screenshot')
conflicts=('deepin-screenshot')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-screen-recorder.git#tag=$pkgver"
         libv4l-1.30.patch)
sha512sums=('3416ce1f9cf2b48fd78623b6b109c8c21d2cbcd176e8da4c901df869b4ddeeb62691c1ecaf6313d3c324d9398074b5c7f755ccb535a079a7792c660438566292'
            'b8527bbf18b78b530507ed7725218db88f3ae4ed6abb700f6430903421e188456d7f0695f680461376d403c9227bc0234578d909f29399a4072a13a042d524cb')

prepare() {
  cd deepin-screen-recorder
  # Revert opencv -> opencv-mobile change, because opencv-mobile is not available in Arch
  sed -i 's|opencv_mobile|opencv4|;s|-lopencv_small|-lopencv_core -lopencv_imgproc|' src/src.pro

  # https://github.com/linuxdeepin/developer-center/issues/3035
  sed -i "s|/etc/os-version|/etc/uos-version|" src/src.pro

  # OpenCV 4 compatibility
  sed -i '/#include<opencv2/i #include <opencv2/imgproc/types_c.h>' src/utils/pixmergethread.h

  patch -p1 -i ../libv4l-1.30.patch
}

build(){
  cd deepin-screen-recorder
  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  qmake-qt5 PREFIX=/usr
  CFLAGS+=" -Wno-implicit-function-declaration -I/usr/include/ffmpeg4.4/" \
  make
}

package() {
  cd deepin-screen-recorder
  make INSTALL_ROOT="$pkgdir" install
}
