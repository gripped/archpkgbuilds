# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=superlu_dist
pkgver=9.1.0
pkgrel=3
pkgdesc='Distributed memory, MPI based SuperLU'
arch=(x86_64)
url='https://github.com/xiaoyeli/superlu_dist'
license=(BSD-3-Clause-LBNL)
depends=(
  blas
  glibc
  gcc-libs
  lapack
  openmpi
)
makedepends=(
  cmake
  gcc-fortran
  #ninja
  python
)
checkdepends=(
  python-mpi4py
  python-numpy
  python-scipy
)
optdepends=(
  'python-mpi4py: for Python interface'
  'python-numpy: for Python interface'
  'python-scipy: for Python interface'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
b2sums=('ffa4afe42725a1ae52803b799918ad6787807d743886c1de3a2e59103a15b2243067b42495f7ca2c064f8400d55db4906a3259e7079ae6da287e15bd82a2f581')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  # Remove non-prototype declarations of fopen
  # https://github.com/xiaoyeli/superlu_dist/pull/185
  sed -i -e 's|\*fp, \*fopen()|\*fp|g' -e 's|\*fopen(), ||g' $(find -name '*.c')

  # Fix finding the libsuperlu_dist_python.so library
  sed -i "s|os.getenv('SUPERLU_PYTHON_LIB_PATH')|os.getenv('SUPERLU_PYTHON_LIB_PATH', '/usr/lib')|" PYTHON/pdbridge.py

  # Fix importing pdbridge in the pddrive.py example (but keep the original for testing)
  cp PYTHON/pddrive.py PYTHON/test_pddrive.py
  sed -i 's|import pdbridge|import superlu_dist.pdbridge as pdbridge|' PYTHON/pddrive.py
}

build() {
  local cmake_options=(
    -B build
    -S $pkgname-$pkgver
    # FIXME: ninja does not work: ninja: build stopped: multiple rules generate FORTRAN/superlupara_mod.mod.
    #-G Ninja
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DBUILD_SHARED_LIBS=ON
    -Denable_doc=OFF
    -Denable_examples=OFF
    -Denable_python=ON
    -Denable_tests=ON
    -DTPL_ENABLE_LAPACKLIB=ON
    -DTPL_ENABLE_PARMETISLIB=OFF
    -DTPL_ENABLE_INTERNAL_BLASLIB=OFF
    -DCMAKE_INSTALL_INCLUDEDIR=include/superlu_dist/
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

check(){
  OMP_NUM_THREADS=4 \
  ctest --test-dir build --output-on-failure

  SUPERLU_PYTHON_LIB_PATH=build/PYTHON \
  python $pkgname-$pkgver/PYTHON/test_pddrive.py
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir"/usr/share/doc/$pkgname/
  install -vDm 644 $pkgname-$pkgver/DOC/ug.pdf -t "$pkgdir"/usr/share/doc/$pkgname/
  install -vDm 644 $pkgname-$pkgver/License.txt -t "$pkgdir"/usr/share/licenses/$pkgname/

  # remove useless files
  rm -rv "$pkgdir"/usr/include/$pkgname/FORTRAN

  # install Python example
  install -vdm 755 "$pkgdir"/usr/share/doc/$pkgname/examples/
  mv -v "$pkgdir"/usr/lib/PYTHON/pddrive.py "$pkgdir"/usr/share/doc/$pkgname/examples/

  # install Python utility functions
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -vdm 755 "$pkgdir$site_packages"
  mv -v "$pkgdir"/usr/lib/PYTHON/ "$pkgdir$site_packages"/$pkgname/
}
