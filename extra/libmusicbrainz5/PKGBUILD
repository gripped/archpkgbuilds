# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmusicbrainz5
pkgver=5.1.0
pkgrel=6
pkgdesc="Library That Provides Access to the MusicBrainz Server"
url='https://musicbrainz.org/doc/libmusicbrainz'
arch=(x86_64)
license=(LGPL-2.1-only)
depends=(neon libxml2)
makedepends=(cmake ninja)
provides=(libmusicbrainz5.so)
source=("https://github.com/metabrainz/libmusicbrainz/releases/download/release-${pkgver}/libmusicbrainz-$pkgver.tar.gz"
        16.patch
        19.patch)
sha256sums=('6749259e89bbb273f3f5ad7acdffb7c47a2cf8fcaeab4c4695484cef5f4c6b46'
            '5ad7e45b191cceca3bb71c92adb6c767ae8079bf2814354341fae829d4328ae9'
            'c8defc41681b1ba7c93af93f73207bf1666c78e98d22a1a5f16c9e20b947f787')

prepare() {
  cd libmusicbrainz-$pkgver

  # https://github.com/metabrainz/libmusicbrainz/pull/16
  patch -Np1 -i ../16.patch
  # https://github.com/metabrainz/libmusicbrainz/pull/19
  patch -Np1 -i ../19.patch
}

build() {
  cmake -S libmusicbrainz-$pkgver -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
