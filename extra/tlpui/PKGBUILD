# Maintainer: George Hu <integral@archlinux.org>
# Contributor: HurricanePootis <hurricanepootis@protonmail.com>
# Contributer: HelloImWar <helloimwar at proton dot me>
# Contributer: Yamada Hayao <hayao@fascode.net>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: slact

pkgname=tlpui
_srcname=TLPUI
pkgver=1.9.0
pkgrel=2
epoch=2
pkgdesc="A GTK user interface for TLP written in Python"
arch=('any')
url="https://github.com/d4nj1/${_srcname}"
license=('GPL-2.0-or-later')
depends=('tlp' 'python-gobject' 'hicolor-icon-theme' 'python-yaml' 'gtk3' 'gdk-pixbuf2' 'python' 'glib2')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-poetry')
checkdepends=('python-pytest')
source=("${url}/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('88c026e16bf968d020ac7034a4978ddb51efd2f70eaf0b9e03d319696145e06c')

build() {
	cd "${_srcname}-${pkgname}-${pkgver}/"
	python -m build --wheel --no-isolation
}

check() {
	cd "${_srcname}-${pkgname}-${pkgver}/"
	pytest
}

package() {
	cd "${_srcname}-${pkgname}-${pkgver}/"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	install -Dm644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"

	cd "${pkgname}/icons/themeable/hicolor/"
	for i in 16 32 48 64 96 128 256; do
		install -Dm644 "${i}x${i}/apps/${pkgname}.png" -t "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/"
	done

	install -Dm644 "scalable/apps/${pkgname}.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
}
