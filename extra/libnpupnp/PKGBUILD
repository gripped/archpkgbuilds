# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=libnpupnp
pkgver=6.2.2
pkgrel=1
pkgdesc="C++ base UPnP library, derived from Portable UPnP, a.k.a libupnp"
url="https://www.lesbonscomptes.com//upmpdcli/libupnpp-refdoc/libupnpp-ctl.html"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(glibc gcc-libs curl expat libmicrohttpd)
makedepends=('meson')
provides=(libnpupnp.so)
source=("https://www.lesbonscomptes.com/upmpdcli/downloads/libnpupnp-${pkgver}.tar.gz")
sha256sums=('621d50411788025afd0807b59f0702644d598182745a3787b6f8e659a5be6b38')

build(){
  arch-meson $pkgname-$pkgver build
    meson compile -C build
}

package(){
  meson install -C build --destdir "$pkgdir"
  install -Dm644 "$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
