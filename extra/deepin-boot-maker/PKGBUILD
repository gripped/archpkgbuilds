# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-boot-maker
pkgver=6.0.11
pkgrel=1
pkgdesc='Tool to create a bootable usb stick quick and easy'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-boot-maker"
license=('GPL-3.0-only')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'p7zip' 'mtools' 'polkit-qt6' 'syslinux')
makedepends=('git' 'qt6-tools' 'python' 'gtest')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-boot-maker.git#tag=$pkgver")
sha512sums=('2261c296664c4e594170fda44356594093bd140d1e3c281068e7698772763ccb230f7f500ba1519a947150c83470fc320e57fb05fad6f0929942bfcc9273a4c5')

prepare() {
  cd deepin-boot-maker
  sed -i '/resources_big/d' src/app/app.pro
}

build() {
  cd deepin-boot-maker
  qmake6 PREFIX=/usr
  make
}

package() {
  cd deepin-boot-maker
  make INSTALL_ROOT="$pkgdir" install
}
