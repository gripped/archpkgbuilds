# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-boot-maker
pkgver=6.0.7
pkgrel=1
pkgdesc='Tool to create a bootable usb stick quick and easy'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-boot-maker"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'p7zip' 'mtools' 'polkit-qt6' 'syslinux')
makedepends=('git' 'qt6-tools' 'python' 'gtest')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-boot-maker.git#tag=$pkgver")
sha512sums=('691d92ccfa4d5e1715a5fdb1a1d728785cba390cb7a9ff61bac5801a424a9c80a0f26ab5d9fdf9654d61c1be62793e61c79787e32f501c5a260643ee871c2265')

prepare() {
  cd deepin-boot-maker
  sed -i '/resources_big/d' src/app/app.pro
}

build() {
  cd deepin-boot-maker
  qmake6 PREFIX=/usr
  make
}

package() {
  cd deepin-boot-maker
  make INSTALL_ROOT="$pkgdir" install
}
