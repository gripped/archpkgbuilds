# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-boot-maker
pkgver=6.0.5
pkgrel=1
pkgdesc='Tool to create a bootable usb stick quick and easy'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-boot-maker"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'p7zip' 'mtools' 'polkit-qt6' 'syslinux')
makedepends=('git' 'qt6-tools' 'python' 'gtest')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-boot-maker.git#tag=$pkgver")
sha512sums=('22459c5c47dad387ead7d2b6d50f064e771a64742b76044c5802515f5178720479186ff5345f38f6babb71805464d90f788448a12850f9b464537437a79fbaf9')

prepare() {
  cd deepin-boot-maker
  sed -i '/resources_big/d' src/app/app.pro
}

build() {
  cd deepin-boot-maker
  qmake6 PREFIX=/usr
  make
}

package() {
  cd deepin-boot-maker
  make INSTALL_ROOT="$pkgdir" install
}
