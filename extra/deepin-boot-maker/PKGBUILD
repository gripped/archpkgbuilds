# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-boot-maker
pkgver=6.0.12
pkgrel=1
pkgdesc='Tool to create a bootable usb stick quick and easy'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-boot-maker"
license=('GPL-3.0-only')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'p7zip' 'mtools' 'polkit-qt6' 'syslinux')
makedepends=('git' 'qt6-tools' 'python' 'gtest')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-boot-maker.git#tag=$pkgver")
sha512sums=('c01ee5d536176d1f152501c1dc1e4756693b27575dfddd86a22e9f3c400c59a1360e3d9374a01305567635269431da1601aabbc18c21cbdbd4e5ad1a7ba91667')

prepare() {
  cd deepin-boot-maker
  sed -i '/resources_big/d' src/app/app.pro
}

build() {
  cd deepin-boot-maker
  qmake6 PREFIX=/usr
  make
}

package() {
  cd deepin-boot-maker
  make INSTALL_ROOT="$pkgdir" install
}
