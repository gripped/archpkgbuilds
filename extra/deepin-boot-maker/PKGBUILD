# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-boot-maker
pkgver=6.0.4
pkgrel=1
pkgdesc='Tool to create a bootable usb stick quick and easy'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-boot-maker"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'p7zip' 'mtools' 'polkit-qt6' 'syslinux')
makedepends=('git' 'qt6-tools' 'python' 'gtest')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-boot-maker.git#tag=$pkgver")
sha512sums=('8e52e99c701ce3bfee5ea81eb701c29e4141cb577d8394ba553672162ee8296eb684d55f340fa58e8d45b1de8bf8e2ade24dfeccb20363a52e044c21256f1007')

prepare() {
  cd deepin-boot-maker
  sed -i '/resources_big/d' src/app/app.pro
}

build() {
  cd deepin-boot-maker
  qmake6 PREFIX=/usr
  make
}

package() {
  cd deepin-boot-maker
  make INSTALL_ROOT="$pkgdir" install
}
