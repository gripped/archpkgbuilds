# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=1.3.1
epoch=1
pkgrel=1
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('x86_64')
url="https://widelands.org/"
license=('GPL')
depends=('sdl2_mixer' 'sdl2_image' 'sdl2_ttf' 'lua' 'glew' 'python' 'dbus' 'minizip')
makedepends=('cmake' 'mesa' 'ninja' 'git' 'asio')
replaces=('widelands-data')
source=("$pkgname-$pkgver.tar.gz::https://github.com/widelands/widelands/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('ecb369078c535d47e3402e397c101ee5dcebdea01b44092870b150753923d385f3509e3e5dbba3caf4d311baba662373d8ce34eb9062681b3d16feb15b55c56d')

build() {
  cd $pkgname-$pkgver

  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWL_INSTALL_BINDIR=bin \
      -DWL_INSTALL_BASEDIR=/usr \
      -DWL_INSTALL_DATADIR=/usr/share/widelands
  ninja -C build
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir" ninja -C build install

  # Fix wrong locations of some installed files (upstream bug)
  # See also FS#72240
  mv "$pkgdir"/usr/{VERSION,ChangeLog,CREDITS} "$pkgdir"/usr/share/widelands

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/COPYING "$pkgdir"/usr/share/licenses/$pkgname

}
