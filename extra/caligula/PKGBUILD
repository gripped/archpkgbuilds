# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=caligula
pkgver=0.4.8
pkgrel=2
pkgdesc="A user-friendly, lightweight TUI for disk imaging"
arch=('x86_64')
url="https://github.com/ifd3f/caligula"
license=('GPL-3.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a1c00b3a83b08b72cfe93abe79b697f4bc08b86e2fa1401b68dab085860dc67b6772f4c31a6facee6acce71ea490e35c6750478fb4376481f521325094f9c99e')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" --locked
}

build() {
  cd "$pkgname-$pkgver"
  RUSTFLAGS="--cfg tracing_unstable" cargo build --release # --frozen
}

check() {
  cd "$pkgname-$pkgver"
  RUSTFLAGS="--cfg tracing_unstable" cargo test # --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
