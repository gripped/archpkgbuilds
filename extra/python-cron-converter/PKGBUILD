# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=python-cron-converter
pkgver=1.3.0
pkgrel=1
pkgdesc="Cron string converter for Python"
arch=(any)
url="https://github.com/Sonic0/cron-converter"
license=(MIT)
depends=(
  python
  python-dateutil
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('1d81c035acd8d1613ce580ff8bf700fbf555eb0607f0d70fcbe16d54142bd049165c87fa4141ab678480220e5f662efd8a38f6982d7dc7e3ee7f1f5a56c569f0')
b2sums=('667e72beec213e4b694eb98bc769137e698c3f49a9cd2d2410cd20fbd7e0c5697ccbb8ee121aca582ca1ff6fe05d4064a5a45a9ab9d7a533c3567c2166161623')

prepare() {
  cd $pkgname
  git cherry-pick -n 78a4113d6dd27e69dfe76fe9d2cd718a4c44ffd1
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python -m unittest discover -v tests/unit
  python -m unittest discover -v tests/integration
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
