# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-cron-converter
_name=${pkgname#python-}
pkgver=1.2.2
pkgrel=1
pkgdesc="Cron string converter for Python"
arch=(any)
url="https://github.com/Sonic0/cron-converter"
license=(MIT)
depends=(
  python
  python-dateutil
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('1ba2eddb8e580ddcbb0daae3b41b68030aa5b0342484115f92f8f21c4a0194b63572ec8a4c5bf71c9c8527381055ef44e17c913fa91f10089264e0e6b0b19a39')
b2sums=('44914de7a48ecf822e7df0c0e305f00b3b871b592641fb38b9d67825503659514aa20f03ce18c91d258d79ba2f9b498db4743b502f14418089f9ea5912d84014')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m unittest discover -v tests/unit
  python -m unittest discover -v tests/integration
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
