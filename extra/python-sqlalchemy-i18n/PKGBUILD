# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=sqlalchemy-i18n
pkgname=python-sqlalchemy-i18n
pkgver=1.1.0
pkgrel=9
pkgdesc='Internationalization extension for SQLAlchemy models'
url='https://github.com/kvesteri/sqlalchemy-i18n'
arch=('any')
license=(BSD-3-Clause)
depends=('python' 'python-sqlalchemy' 'python-sqlalchemy-utils')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-psycopg2' 'python-flexmock' 'python-jinja' 'python-docutils')
source=(https://github.com/kvesteri/sqlalchemy-i18n/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        remove-python-six-leftovers.patch)
sha512sums=('3fd11815366c3e971e0531d684715c4c8998f7f20d69a8b596bd4ca147da6f85ddf4448c99631031ccf60b5f09de6d1daa1e08e94d5c580268e31bf322af97d5'
            'c4a706babae154fece98e9c477e7ffa0a70d04a7a9507a663d74d163c383f236af165d63a41d51b0bc1137e8f3818778c6584d595b6aa51626cd563b1d9a688a')
b2sums=('6554d731768d22389a926f6b33ef49844fc7fddb5cb1b844a9150f9c274ea7f3ac905e02ca5bbf9be99d4ace47ef3848336fbe5062d1bddcb2ee8f75507806fd'
        '8e6991ad2571ab93b7fb425c4e77acfccf593a0a30a0b06603ec00b4df2fa05f6dae6f9eca304262daff494e9b13d6f646e820ca22b12998be9e9e083922cdbf')

prepare() {
  cd ${_name}-${pkgver}
  # Upstream is dead, so we patch
  # https://github.com/kvesteri/sqlalchemy-i18n/pull/35
  patch -Np1 -i ${srcdir}/remove-python-six-leftovers.patch
}

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
