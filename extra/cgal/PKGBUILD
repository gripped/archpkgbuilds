# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Dmitriy Morozov <foxcub>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=cgal
pkgver=6.0.2
pkgrel=1
pkgdesc="Computational Geometry Algorithms Library"
arch=(any)
url="https://www.cgal.org"
license=(GPL-3.0-or-later LGPL-3.0-or-later)
depends=(boost eigen gmp mpfr)
makedepends=(cmake)
source=(https://github.com/CGAL/cgal/releases/download/v$pkgver/CGAL-$pkgver.tar.xz)
sha256sums=('f30c5be7c25a2a3ea24bccbaab5cf63fafda63c0279f34d7e7f8b3212821fed6')

build() {
  local cmake_options=(
    -B build
    -S CGAL-$pkgver
    -W no-dev
    -DCMAKE_INSTALL_PREFIX=/usr
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  mv "$pkgdir"/usr/share/doc/{CGAL,cgal}
}
