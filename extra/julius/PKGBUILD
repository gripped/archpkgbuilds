# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=julius
pkgver=1.7.0
pkgrel=2
pkgdesc='An enhanced re-implementation of Caesar III'
arch=('x86_64')
url='https://github.com/bvschaik/julius'
license=('AGPL-3.0-only')
depends=(
  'glibc'
  'hicolor-icon-theme'
  'sdl2'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('b6c9f1596d7a39b4a6ffb288a483468ee6ec9040e039526964abc33f6aec3e41cb7d5b6b4f0888c95b98928491e3e1fcc15f6460327eb1d7d9d2ec3a4a19bf4e')
b2sums=('4efefff3b34c8beff14b68f1cc123958ffa38dc690efafeaa56c8f09119416f5502f8c87331b6e86abe678302a1fbef41c6ba8a654c4df4983c7e09f72660127')

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -D CMAKE_INSTALL_PREFIX='/usr' \
    -D CMAKE_BUILD_TYPE='Release' \
    -D SYSTEM_LIBS=ON

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE.txt"
}
