# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=julius
pkgver=1.8.0
pkgrel=1
pkgdesc='An enhanced re-implementation of Caesar III'
arch=(x86_64)
url='https://github.com/bvschaik/julius'
license=(AGPL-3.0-only)
depends=(
  glibc
  hicolor-icon-theme
  libpng
  sdl2
  sdl2_mixer
)
makedepends=(git cmake)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('b85e47461ed4a88175dd1bb17bff0b627f341366fc79f41fdf87c5e15a7c1c4b4f233d251ba91ac5488f99e690a059c8243f84e066647591544cad365a9b2de7')
b2sums=('bf1e2ef415efec5110040669b1e5278492e400fc7586872bec2f401a694118faf143d0e28874b9d2079df310308b7abb539fe8d0244c590611e9ffccace993cd')

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -D CMAKE_INSTALL_PREFIX='/usr'
    -D CMAKE_BUILD_TYPE='Release'
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5
    -D SYSTEM_LIBS=ON
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE.txt"
}
