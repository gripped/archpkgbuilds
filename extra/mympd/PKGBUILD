# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=18.1.1
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('cda31a4149c999495410a0a6f0509867c52eab2f818c81049394d186928b8c620d8c324390083c6a2c640daf3ce89ea580bbe1b6b27b4e9240c517e0320d92dc')
b2sums=('99a196de8f0c95fc9082704664cd9bb060fd9260ea9953269549b86219b3c630433d0b705fb7b7f6b206ae05b619ba04afd6598bcfe74774beb67a79bb5a542e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -S $_name-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
