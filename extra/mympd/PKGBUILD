# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=16.1.0
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('cb58724595f38bdc84ee1acc821dc1e9724b6361b337f496e7b78a863c0a4950580d82a3e56ca9299e7c1c9864bcc5074574af73a71389c92e60dbde98f36355')
b2sums=('e4ee87c92262845f2cd6378b96a9b8ce8416f191a8226837e5df44e267150ea5ba8e6e89788f91e1fdfb439d41074229d38ce168ce1ee70b4bc8894530ccc0f6')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -S $_name-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
