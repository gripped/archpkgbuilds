# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=11.0.4
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
source=(https://github.com/jcorporation/$pkgname/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('7789d5e6dbc5eeba5704f772876f8a536b165aff0936b573307e55c3b6ac56ec6095bb4871f37cc96ada8fbf856fe5f28bff8f1bb09b095afc742fd6bb138b98')
b2sums=('0e710bf1493dbe54853e1e67347094a57c4af41d6fe565d300616cf3206b69daeb21ede55321f4ac37647a558adc19d629bc43d920dc0a1493ad5494ae534697')

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DENABLE_FLAC=ON
    -S $_name-$pkgver
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
