# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=21.0.1
pkgrel=2
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('3e6ba143b5e2a09dd4a0514904c1a78e05cf583ca23785ed32932ff2041cb495bd2438a9f635376b97fb80c4aea1b9e2c1b4f24f98e9e87c29a2f413048ac898')
b2sums=('4470f239933f3f540741439a1fc7e6f3de1c839371d77893160f19d0b80c793943b456a56f36df1c426a36c352b18a0bdd5d3073926644f12d8c2881af317b2d')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -D MYMPD_BUILD_TESTING=ON
    -S $_name-$pkgver
    -W no-dev
  )

  # CMAKE_BUILD_TYPE=None does not automatically disable compiling in debug mode, so we do this specifically
  CFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  touch -d "yesterday" "$pkgdir/usr/bin/$pkgname"
  ls -lahR "$pkgdir/usr/bin/"
}
