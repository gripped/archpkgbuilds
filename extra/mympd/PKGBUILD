# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=22.0.4
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('675e2ad39f4d369877815c522e6670a03412c9914b9e1d8f5c3a2cfd5d64284b3a2b05201492cf018369a62e21c553d7e4e97b86e93952f3dc8f47e9de957fe2')
b2sums=('d59eba43d083adb2bc58bf31ec03bea1105143d681e409c92e60303b9ede75285aec04d0f70a6dcc52ca62f3eb27a8287840d3fcc3ff9489a4dc9d53e61e9a89')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -D MYMPD_BUILD_TESTING=ON
    -S $_name-$pkgver
    -W no-dev
  )

  # CMAKE_BUILD_TYPE=None does not automatically disable compiling in debug mode, so we do this specifically
  CFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  touch -d "yesterday" "$pkgdir/usr/bin/$pkgname"
  ls -lahR "$pkgdir/usr/bin/"
}
