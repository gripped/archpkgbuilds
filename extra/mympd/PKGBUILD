# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=24.0.2
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  libutf8proc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('3e94d0456000054fe549f49f2531b4bd17b32d4d9ab5376718402b9f0b609b89d0429c1a747cfed3b8bae3567b4db0d32003c96ed1db49970bbcd82bdb9bf8f3')
b2sums=('34e65d11991cc98c2527ab0fa5d06451545fb96831a5599d821000aeab22ac37043aeee90986ba798de8de46463d5e10ec90f3c2747891ef4849b7c1b9d6b8cc')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -D MYMPD_BUILD_TESTING=ON
    -S $_name-$pkgver
    -W no-dev
  )

  # CMAKE_BUILD_TYPE=None does not automatically disable compiling in debug mode, so we do this specifically
  CFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  local ctest_options=(
    --exclude-regex "^test_utf8wrap$"
    --test-dir build
    --output-on-failure
  )

  ctest "${ctest_options[@]}"
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  touch -d "yesterday" "$pkgdir/usr/bin/$pkgname"
  ls -lahR "$pkgdir/usr/bin/"
}
