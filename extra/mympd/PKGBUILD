# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=20.1.3
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('a579ed7379c5ed4ceb11a4e35791556caf9f39c485170658d84c76bb128ce23d1aafdfbec4b87423cb4d60689f1ffe2fcdab001f9ad4387802b3a98a9fdd5552')
b2sums=('b923a29c7bf82e3619445549f89e7f1446e3e2f8f303df92c2bda37bf71f8ac1649d02bf0cf428633aff3e8f97dfdc04abca23dbdafa10e3d6dc21546264b2cb')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -S $_name-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
