# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=22.1.0
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('1ad13846131caf3bb694e9e9684fdca794393323d59dadccf6f87b8e2bea9115e4aa699787af7982929ba7b769ab1134770926ca9f75313161fa15ccd37e37f0')
b2sums=('46d0e726194411ca660af513a1950b5d011e4af0cc3f1ebb7ee0231f0e199fd57ff2ec44e3e874b321f3523e9842857a58a16d6a5123e06d8dccb85ce10cf876')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -D MYMPD_BUILD_TESTING=ON
    -S $_name-$pkgver
    -W no-dev
  )

  # CMAKE_BUILD_TYPE=None does not automatically disable compiling in debug mode, so we do this specifically
  CFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  touch -d "yesterday" "$pkgdir/usr/bin/$pkgname"
  ls -lahR "$pkgdir/usr/bin/"
}
