# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=22.0.2
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('60f54cc724429c1eec12ea4038e9c169f56b87ba859f9a59de37d73ee206e8f3f6ce78a53d6a1febd597f2117824a6d6736c0657e0c5b9579ea27e17aed855e6')
b2sums=('81358c1d683161121e23e81765f516a12052554f348c73a32ba253a812781ec85e04bbbeb2d30698a05b8cfc39332039ef30e5597609478caebd43e6d05d9f91')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -D MYMPD_BUILD_TESTING=ON
    -S $_name-$pkgver
    -W no-dev
  )

  # CMAKE_BUILD_TYPE=None does not automatically disable compiling in debug mode, so we do this specifically
  CFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  touch -d "yesterday" "$pkgdir/usr/bin/$pkgname"
  ls -lahR "$pkgdir/usr/bin/"
}
