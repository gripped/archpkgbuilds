# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=20.0.0
pkgrel=2
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL-3.0-or-later)
depends=(
  glibc
  lua
)
makedepends=(
  cmake
  flac
  jq
  libid3tag
  openssl
  pcre2
  perl
)
optdepends=(
  'libnewt: for mympd-config'
  'sh: for mympd-config'
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('de8175dbd4b8d7ebf389581acb6a650dae30eb7a9d49bfb7f74d9acbb32882531b90e7493fab63bc51855edc0be76bd3897e0ba5fa9c9f5bd8aff6624ce5b605')
b2sums=('a78a0b7fde44f99b48ac999fd4f21458bf434f79468e4dc1f4274ab526e3710596b42fb6a6793deff0b548ff9d99663ba0ba9dc24b91fc760341fa1895894b01')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MYMPD_ENABLE_FLAC=ON
    -S $_name-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
