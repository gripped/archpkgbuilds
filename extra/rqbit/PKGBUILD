# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgbase=rqbit
pkgname=(
  rqbit
  rqbit-desktop
)
pkgver=5.5.2
pkgrel=2
pkgdesc="A bittorrent client in Rust"
url='https://github.com/ikatson/rqbit'
arch=('x86_64')
license=('Apache-2.0')
makedepends=(
  'cairo'       'libcairo.so'
  'cargo'
  'cargo-tauri'
  'gdk-pixbuf2' 'libgdk_pixbuf-2.0.so'
  'glib2'       'libglib-2.0.so'
  'gtk3'        'libgtk-3.so'
  'libsoup'     'libsoup-2.4.so'
  'npm'
  'openssl'     'libssl.so'
  'pango'       'libpango-1.0.so'
  'typescript'
  'webkit2gtk'  'libwebkit2gtk-4.0.so'
)
source=(https://github.com/ikatson/${pkgbase}/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('7022bd1b85baae88cddeb1c4f7608ff2ea3c6b1ef7c3951f78b1a0fdaf356791')
b2sums=('d264d1806a3a7e75a3d02a822e95e13ecf3ce8f0f50eff57f949d711ae8030b850fca433839b78ba210e3fbd7c28c6041fbc12c2451daaf3ccd188c1a78db7ae')

prepare() {
  cd "${pkgbase}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  pushd desktop
  npm install
  popd

  pushd crates/librqbit/webui
  npm install
}

build() {
  cd ${pkgbase}-${pkgver}
  cargo build -p rqbit --frozen --release
  cd desktop
  cargo tauri build -b deb --ci
}

check() {
  cd ${pkgbase}-${pkgver}
  cargo test --frozen -- --skip tests::e2e
}

package_rqbit() {
  depends=(
    'gcc-libs'
    'glibc'
    'libssl.so'
  )

  cd ${pkgbase}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/rqbit

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  RUST_LOG=off "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  RUST_LOG=off "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  RUST_LOG=off "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

package_rqbit-desktop() {
  pkgdesc="A bittorrent client in Rust (desktop app)"
  depends=(
    'gcc-libs'
    'glibc'
    'hicolor-icon-theme'
    'libcairo.so'
    'libgdk_pixbuf-2.0.so'
    'libglib-2.0.so'
    'libgtk-3.so'
    'libpango-1.0.so'
    'libsoup-2.4.so'
    'libssl.so'
    'libwebkit2gtk-4.0.so'
    'rqbit'
  )

  mv "${pkgbase}-${pkgver}"/desktop/src-tauri/target/release/bundle/deb/rqbit-desktop_*/data/usr "${pkgdir}"
}

# vim: ts=2 sw=2 et:
