# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgbase=rqbit
pkgname=(
  rqbit
  rqbit-desktop
)
pkgver=8.1.1
pkgrel=2
pkgdesc="A bittorrent client in Rust"
url='https://github.com/ikatson/rqbit'
arch=('x86_64')
license=('Apache-2.0')
makedepends=(
  'cairo' 'libcairo.so'
  'cargo'
  'cargo-tauri'
  'gdk-pixbuf2' 'libgdk_pixbuf-2.0.so'
  'glib2' 'libglib-2.0.so'
  'gtk3' 'libgtk-3.so'
  'libsoup3' 'libsoup-3.0.so'
  'npm'
  'openssl' 'libssl.so'
  'typescript'
  'webkit2gtk-4.1' 'libwebkit2gtk-4.1.so'
)
source=(https://github.com/ikatson/${pkgbase}/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('452b8260fabba938567e1819a9edfcf6b69579ecd5f8b87fee4ca1666fa8fede')
b2sums=('e202a78eea56728d33b63d8956a838e3d7bf1eba87d8c7a293fc5d8515b11ef91303b6aacbd2532222b84d4fb434d7de36be3f862787d779fd5e0564f262b13c')

prepare() {
  cd "${pkgbase}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  pushd desktop
  npm install
  popd

  pushd crates/librqbit/webui
  npm install
}

build() {
  cd ${pkgbase}-${pkgver}
  cargo build -p rqbit --frozen --release
  cd desktop
  cargo tauri build -b deb --ci
}

check() {
  cd ${pkgbase}-${pkgver}
  cargo test --frozen -- --skip tests::e2e
}

package_rqbit() {
  depends=(
    'gcc-libs'
    'glibc'
    'libssl.so'
  )

  cd ${pkgbase}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/rqbit

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  RUST_LOG=off "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  RUST_LOG=off "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  RUST_LOG=off "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

package_rqbit-desktop() {
  pkgdesc="A bittorrent client in Rust (desktop app)"
  depends=(
    'gcc-libs'
    'glibc'
    'hicolor-icon-theme'
    'libcairo.so'
    'libgdk_pixbuf-2.0.so'
    'libglib-2.0.so'
    'libgtk-3.so'
    'libsoup-3.0.so'
    'libssl.so'
    'libwebkit2gtk-4.1.so'
    'rqbit'
  )

  mv "${pkgbase}-${pkgver}"/target/release/bundle/deb/rqbit-desktop_*/data/usr "${pkgdir}"
}

# vim: ts=2 sw=2 et:
