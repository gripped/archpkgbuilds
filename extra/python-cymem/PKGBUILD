# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cymem
pkgver=2.0.9
pkgrel=1
pkgdesc="Cython memory pool for RAII-style memory management"
url="https://github.com/explosion/cymem"
license=('MIT')
arch=('x86_64')
depends=('python')
makedepends=('git' 'cython' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/explosion/cymem.git#tag=release-v$pkgver")
sha512sums=('485cdbf5cdeabd72e06bab39befc354c74939400622c91da624c5dc8b58527d618d6f1d0e64e8a6623d826f7eef20c0b581555dacf405e71444901a067c2b8b2')

build() {
  cd cymem
  python -m build --no-isolation --wheel
}

check() {
  cd cymem

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer -d "tmp_install" dist/cymem-*.whl
  cd tmp_install/$site_packages
  pytest cymem
}

package() {
  cd cymem
  python -m installer -d "$pkgdir" dist/cymem-*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
