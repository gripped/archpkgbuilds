# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cymem
pkgver=2.0.10
pkgrel=1
pkgdesc="Cython memory pool for RAII-style memory management"
url="https://github.com/explosion/cymem"
license=('MIT')
arch=('x86_64')
depends=('python')
makedepends=('git' 'cython' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/explosion/cymem.git#tag=release-v$pkgver")
sha512sums=('28e8b53828a1f384bffa87542ce6d2d81fc3b92630b376e6e6dbf9787019be87f5089609e2e35255db798dba8a4671a4006b1daabe9716985f4a055611790b2e')

build() {
  cd cymem
  python -m build --no-isolation --wheel
}

check() {
  cd cymem

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer -d "tmp_install" dist/cymem-*.whl
  cd tmp_install/$site_packages
  pytest cymem
}

package() {
  cd cymem
  python -m installer -d "$pkgdir" dist/cymem-*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
