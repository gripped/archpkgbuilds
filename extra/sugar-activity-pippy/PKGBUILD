# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-pippy
_pkgname=Pippy
pkgver=76
pkgrel=1
pkgdesc='Sugar activity to learn and develop in Python programming language'
arch=(any)
url='https://github.com/sugarlabs/Pippy'
license=(GPL-2.0-or-later)
groups=(sugar-fructose)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  gtksourceview4
  pango
  python
  python-cairo
  python-dbus
  python-gobject
  python-pybox2d
  python-pygame
  sugar
  sugar-toolkit-gtk3
  telepathy-glib
  vte3
)
makedepends=(git)
source=("git+https://github.com/sugarlabs/$_pkgname.git#tag=v$pkgver")
b2sums=(7b86de0a43803a8ac9570992452065d716d4d8f7fe2f790156110ad9ebc757678b4bb869010005430ea3a30bb7e35bc444a84fd2dc86aaa1ae4329f24b37a3d2)

prepare() {
  cd $_pkgname

  # Port to GtkSource 4
  sed -i "s/'GtkSource', '3.0'/'GtkSource', '4'/" notebook.py

  # Use standard icon name
  sed -i "s/ToolButton('gtk-add')/ToolButton('list-add')/" notebook.py
}

package() {
  cd $_pkgname
  python setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
