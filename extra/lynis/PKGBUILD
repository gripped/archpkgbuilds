# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=3.1.4
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='https://cisofy.com/lynis/'
arch=(any)
license=(GPL-3.0-only)
backup=('etc/lynis/default.prf')
depends=(
  sh
  awk
)
optdepends=(
  'net-tools: networking tests'
  'bind-tools: nameserver tests'
  'iptables: firewall tests'
)
source=("https://downloads.cisofy.com/lynis/lynis-$pkgver.tar.gz"{,.asc})
sha512sums=('60e251e98d3859748f917e1d7c4516ad7f8d75d6fcffcc4128c522b8c94a20a4404e57c0ea574dfc8e4774875785cf8e612a61085fc22900dff973a480d2e906'
            'SKIP')
b2sums=('f889a780f8ff735818676c177119ca560ae739ccae09dc0446d55b6b68daa59cff46caad3a5b4bbffb0132940cac4be4bdb9380eeee0d9e04a706119c8fcf965'
        'SKIP')
validpgpkeys=('84FAA9983B24AEF24D6C87F1FEBB7D1812576482') # CISOfy (Software Signing Key) <software@cisofy.com>

prepare() {
  cd "$pkgname"
  sed -e 's|/path/to/lynis|/usr/bin/lynis|g' -i extras/systemd/lynis.service
  chmod +r include/consts
}

package() {
  cd "$pkgname"

  # application
  install -Dm 755 lynis -t "${pkgdir}/usr/bin"
  install -Dm 644 default.prf -t "${pkgdir}/etc/${pkgname}"
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -ra db include plugins "${pkgdir}/usr/share/${pkgname}"

  # doc files
  install -Dm 644 README INSTALL CHANGELOG.md FAQ \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.8" -t "${pkgdir}/usr/share/man/man8"

  # completion
  install -Dm 644 extras/bash_completion.d/${pkgname} \
    -t "${pkgdir}/usr/share/bash-completion/completions"

  # systemd
  install -Dm 644 extras/systemd/{lynis.service,lynis.timer} \
    -t "${pkgdir}/usr/lib/systemd/system"
}

# vim: ts=2 sw=2 et:
