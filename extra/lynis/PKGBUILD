# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=3.1.6
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='https://cisofy.com/lynis/'
arch=(any)
license=(GPL-3.0-only)
backup=('etc/lynis/default.prf')
depends=(
  sh
  awk
)
optdepends=(
  'net-tools: networking tests'
  'bind-tools: nameserver tests'
  'iptables: firewall tests'
)
source=("https://downloads.cisofy.com/lynis/lynis-$pkgver.tar.gz"{,.asc})
sha512sums=('a462be0e4177033b01db7573b1a30c46e1b263cda782963b6f2fdc1f79309362961cd1ae667d974e586500e21f903ebe36f208cc6b69a4017076f3586d7c4ba8'
            'SKIP')
b2sums=('45712d8d943ee8139755e56edd9c5afec333d171f5eb77017871b17e695c2ae61a1945ed92a09b36df5465071eebcffaa91ae042c4f5000556dc327fea97f509'
        'SKIP')
validpgpkeys=('84FAA9983B24AEF24D6C87F1FEBB7D1812576482') # CISOfy (Software Signing Key) <software@cisofy.com>

prepare() {
  cd "$pkgname"
  sed -e 's|/path/to/lynis|/usr/bin/lynis|g' -i extras/systemd/lynis.service
  chmod +r include/consts
}

package() {
  cd "$pkgname"

  # application
  install -Dm 755 lynis -t "${pkgdir}/usr/bin"
  install -Dm 644 default.prf -t "${pkgdir}/etc/${pkgname}"
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -ra db include plugins "${pkgdir}/usr/share/${pkgname}"

  # doc files
  install -Dm 644 README INSTALL CHANGELOG.md FAQ \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.8" -t "${pkgdir}/usr/share/man/man8"

  # completion
  install -Dm 644 extras/bash_completion.d/${pkgname} \
    -t "${pkgdir}/usr/share/bash-completion/completions"

  # systemd
  install -Dm 644 extras/systemd/{lynis.service,lynis.timer} \
    -t "${pkgdir}/usr/lib/systemd/system"
}

# vim: ts=2 sw=2 et:
