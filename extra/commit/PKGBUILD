# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=commit
pkgver=4.4
pkgrel=1
pkgdesc='Editor that helps you write better Git and Mercurial commit messages'
arch=(any)
url='https://apps.gnome.org/Commit/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gjs
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libspelling
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
optdepends=(
  'git: Git support'
  'mercurial: Mercurial support'
)
source=(
  "git+https://github.com/sonnyp/Commit.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
)
b2sums=(
  d99b4e99f2189bad5f561747f3ca356022be7d17fce8547dee2e560132527eb2e39bac14bfa397b6106a7321b8ef02b04a6a829467cdf43d3288d666b72e569d
  SKIP
)

prepare() {
  cd Commit

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson Commit build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s re.sonny.Commit "$pkgdir/usr/bin/commit"
}
