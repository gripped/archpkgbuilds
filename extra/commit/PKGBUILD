# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=commit
pkgver=4.3
pkgrel=1
pkgdesc='Editor that helps you write better Git and Mercurial commit messages'
arch=(any)
url='https://apps.gnome.org/Commit/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gjs
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libspelling
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
optdepends=(
  'git: Git support'
  'mercurial: Mercurial support'
)
source=(
  "git+https://github.com/sonnyp/Commit.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
)
b2sums=(
  1953b1fb06100efb37b91838cd15f8b175be946eba4fbb6971100ffddffe008140e816003d0a4866b7bbc9dbee34c113fb70a5255821315886d5b31bf4818a88
  SKIP
)

prepare() {
  cd Commit

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson Commit build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s re.sonny.Commit "$pkgdir/usr/bin/commit"
}
