# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=commit
pkgver=4.2
pkgrel=5
pkgdesc='Editor that helps you write better Git and Mercurial commit messages'
arch=(any)
url='https://apps.gnome.org/Commit/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gjs
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libspelling
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
optdepends=(
  'git: Git support'
  'mercurial: Mercurial support'
)
source=(
  "git+https://github.com/sonnyp/Commit.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
  commit-gtk-init.patch
  troll-gtk-init.patch
)
b2sums=(
  9e6f0d4dd86298241340176e4cb8053070a90d352e90d1198ae759bdc5fd9a577937f5191a2b7cf06109e049abfc83466657243886d11a1ca3ffcb2698d3e513
  SKIP
  1fa8ad0cf0637d213f1856c4187f9dbb28845681ca0586b18c5aa19efd3b89d7b6db46896281309accebf65a751bbb0c39f45864ca2f9ffcd0a13ed6ad703366
  7190a1851b490e1e429de5f571f6acdf7e2254c30d5368acbed270bac9d09e9e4d33ac5479d8f2b813a4178d2c3b3d82d49e82b76111530b05c36fe89d407338
)

prepare() {
  cd Commit

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update

  # Fix initialization with GTK 4.18
  git apply -3 ../commit-gtk-init.patch

  cd troll
  # Fix initialization with GTK 4.18
  git apply -3 ../../troll-gtk-init.patch
}

build() {
  arch-meson Commit build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s re.sonny.Commit "$pkgdir/usr/bin/commit"
}
