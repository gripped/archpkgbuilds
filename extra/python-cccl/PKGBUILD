# Maintainer: Jakub Klinkovsk√Ω <lahwaacz@archlinux.org>

_name=cccl
pkgname=python-$_name
pkgver=0.4.5
pkgrel=1
pkgdesc="CUDA Core Compute Libraries for Python"
arch=(x86_64)
url="https://github.com/NVIDIA/cccl"
license=("Apache-2.0 WITH LLVM-exception")
depends=(
  gcc-libs
  glibc
  python
  python-cuda-bindings
  python-cuda-core
  python-cuda-pathfinder
  python-llvmlite
  python-numba
  python-numba-cuda
  python-numpy
  python-typing_extensions
)
makedepends=(
  cython
  nlohmann-json
  python-build
  python-installer
  python-scikit-build-core
  python-setuptools-scm
  python-wheel
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/python-$pkgver.tar.gz)
b2sums=('aaaf028b9fcbcfcebe6afba68db6da3541977fcfe0d0b9520a869b98fcae8e9f1c67aac3a270b4a19f7ef5e680f1f80b371b3252ac22c55f06d56a71ea71c859')

prepare() {
  cd $_name-python-$pkgver
  # disable CPM dependency
  sed -i 's|CPMAddPackage("gh:nlohmann/json@3.12.0")|find_package(nlohmann_json REQUIRED)|' cmake/CCCLGetDependencies.cmake
}

build() {
  cd $_name-python-$pkgver/python/cuda_cccl
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver \
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-python-$pkgver/python/cuda_cccl
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 ../../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
