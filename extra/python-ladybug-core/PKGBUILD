# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.24
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('9879d8dd912b674c11bea5d5d96184911c82d7f210c4ee3e9643e6571658b02dd7c17e4406434237c0e7c906d3d2bbf3ff24f5702cb00334c9a945ac98eafc75')
b2sums=('8a4d04b101bbf1ddf41359a1db9781dd5c8015b4646e8a4ff8debf076127dddb26e90061a3f578783ca6163807bfaf8209211c33fd58268d6c8855d2510c7339')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
