# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.28
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('e4e5bc17ddf4049c28c52ccce1f94b4dae6e1bc1c3a7a26785e24808f72e5321a665423a4c5e4d7a238170612a745a3194e778f2903e31e3c713caaeea0c9706')
b2sums=('e1281d15c40350d68942a31a22d8ff68f4a28d51f904d1a82a1b70e3daed2dfc7fd21a48ff1de5a5004b5d2b7a9fa8f565c8fc5d6fc1d9d753d85d10afa67899')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
