# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.29
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('2b76826eb1e5ad35b4ced52f6a84aeb4481fdca2662892af71c0f0b4efce942265417f238c6826e9521e2de50555117de08c551eddf27298ca3cf6d7fcb92504')
b2sums=('ec67228b549fb10fe6193253e8804f2c15e6bb43a56fe2b537e0bc1cedd0c2a6cf0ddb69c4bb801121dce6174a6332e2d843f1315710846a79edc869a7634e5f')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
