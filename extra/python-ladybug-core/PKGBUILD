# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.25
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('dc9c62fd01857d87581505765db74e9886dfc3827ed8c0dc06a078321c1c0b95e4cd1b8fc0cd990d8f2bc76c0019964f16dd9ee43929cd35097314458c3f50bb')
b2sums=('649ad131c62eb546fd73fc5bec3fc3066b8b7af518c476d4a37b9eb047bf92713297cd84cd8323b01f94e655f19174d1d7e269eb6ccf475dcbbfc3b8b37d9a05')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
