# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.20
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('8618a3e63ad78a04aecbf4558ab5733eb08af68d73a86755f131cee0708691b640768d1ca1eb4c50833e1b0734aa023443214d29b826119a13caf1c018fd9f4e')
b2sums=('39f556298ab8103c65a5aa6b77d11c20656588f8cbbde9658439291d78c881a840801b23f8b2defc6f4597b751aae179328422583dc0059ab3c7009803997425')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
