# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.33
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('53563795902c49732e0871232456794f7aa4a956bbed357fbab1c655b3c53508692ce208c641e4124e7e5823eb376cb918e05e332f5212ef5f6a1bf6df483e5d')
b2sums=('73767d97eafef5ee1439327a3f2480fe6813e3a3467fc5d92da22ca393a467d326374d610f99edc6f2f5f8737032ca0d144fc7d3a572e5b05f7cbc61712a7b39')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
