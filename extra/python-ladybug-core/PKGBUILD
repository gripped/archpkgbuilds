# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.34
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('28cf19eb5224148c25d83742010add789caee57562a26da10815df18d642f5289d71d936bb09c3352fe459d66c83f9bd741a50320cd27078f4ab568f80e03578')
b2sums=('6f9053b73fbd8753d5f78a8716d2387d6a60525bafef8df7a52f9da63879672a5b2ecadd0f4ea20cd0453c8c0192e51980bb6aefc041e3213b6f0f0a2b902d6b')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
