# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.30
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('96a9a78c087bf22180a5bb3b7346cf797be377c21980111144a0884abb9087abc4f9e5805f5c5aea609d7009aaed18b59ff1d969c4a5cc6908fd1d6b783e7ff7')
b2sums=('7b790748dc6abed4b91560ec7924cea67700ab3a7911e82ab82c590d855f2dfa7b35f1b3de0e677072513389d0c5edf42663554185c6c7d0f90fa187204cb4ef')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
