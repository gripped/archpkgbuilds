# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.32
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('b16f7168a2744b29fc60111e7066ee78c744668124c861a6b6619c58d4337c34e7c77af2ab2a40ecd06969f291a9c10edbe2a840c8578eb8706b6e452af31f74')
b2sums=('1a5e956852c54905a30958d9e6f03213d72951a5319d796115ee1b349a06e50691a34afdd0c7dff82f72b56ac665945d54ae22efa2210f8d719b305d44a8d402')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
