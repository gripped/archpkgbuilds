# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.22
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('ae47b5a6b351b85dced8ff88d35d575990d9b72a66d2ce7a591c8e0a63134a4c95ac64e5b2fe0ca0edf8fb88cd3c9d7c4fac3f2ffc2c7c7e7740635f512a43e7')
b2sums=('72b6f19ded127f1c91a2205da5cbfdca46baa26deee42cfaed8ac20ef9675c1efecebe9b516b0fb3e64959a5c3548996df4a6008a3b6a9f42291f58475173276')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
