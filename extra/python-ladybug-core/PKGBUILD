# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-core
pkgver=0.44.27
pkgrel=1
pkgdesc='Python library to load, analyze and modify EnergyPlus Weather files (epw)'
arch=(any)
url='https://github.com/ladybug-tools/ladybug'
license=(AGPL-3.0-only)
depends=(
  python
  python-ladybug-geometry
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('4d63bdead9f4dcb15e204ba1bc89e56a8b1b909b4e2b7cdc59c02b41290e61830a6b902aea4ed6cb2c046c09b8c16ce961b326bf86ca7d1247a62ea6e9ee637e')
b2sums=('9596010ebfea7f1c85d6352dc8bd08ebbb93d179081b10ccb6c787a9bc5e963d4197e57b598a450dad940339cfeb9cf25301046b5db484ba5957f88e537aea4b')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
