# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>

pkgname=pychess
pkgver=1.0.5
pkgrel=5
pkgdesc="Chess client"
url="https://pychess.github.io"
arch=('any')
license=('GPL-3.0-only')
depends=('gst-plugins-base' 'gtksourceview3' 'python-cairo' 'python-gobject'
         'python-ptyprocess' 'python-psutil' 'python-sqlalchemy' 'python-pexpect'
         'python-websockets')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pychess/pychess/archive/refs/tags/${pkgver}.tar.gz"
        remove_telnetlib.patch)
sha256sums=('3dda31117e5a18b0e0357aaafea6d498f64e717ad8beab82adcde00711be5638'
            '196c875e40aa997d2f49fe138012734ef7337597137b22557ccdbbf93f60f7b1')

prepare() {
	cd "${pkgname}-${pkgver}"
	PYTHONPATH=lib python3 pgn2ecodb.py
	PYTHONPATH=lib python3 create_theme_preview.py

	# Temporary patch to remove telnetlib import
	# as it was removed in Python 3.13
	# See https://github.com/pychess/pychess/pull/2235
	patch -Np1 < "${srcdir}/remove_telnetlib.patch"
}

build() {
	cd "${pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	# FS#59882
	find "${pkgdir}" -wholename 'external/pexpect' -type d -delete
}
