# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.6.16
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
depends=('dtkcore' 'freeimage' 'libraw' 'libqtxdg' 'librsvg' 'qt5-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtkgui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d8ceae320ae5367ac8534beacb8e076265ec1d1e53d14f628c0e2795532219a8a30dbf4e51f55ff68d4f05bef3379e443886681a7c870fc0f4389617fe3b8a4e')

build() {
  cd dtkgui-$pkgver
  cmake . -GNinja \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtkgui-$pkgver
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkgui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
