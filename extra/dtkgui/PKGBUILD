# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.7.30
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL-3.0-or-later')
depends=('dtkcore' 'libqt5xdg' 'librsvg' 'qt5-base' 'qt5-wayland' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'ninja' 'qt5-tools' 'gtest' 'doxygen' 'treeland-protocols')
source=("git+https://github.com/linuxdeepin/dtkgui.git#tag=$pkgver")
sha512sums=('6107c0dd27bb961877974ee95ebe00d950dbf0ad90d2a75341115085791f327c69cd8f597df0debb00003cfd800c726a0b4fe3a472a4f816a1b612a81d34870d')

build() {
  cd dtkgui
  cmake . -GNinja \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DDTK_DISABLE_EX_IMAGE_FORMAT=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

check() {
  cd dtkgui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkgui
  DESTDIR="$pkgdir" ninja install
}
