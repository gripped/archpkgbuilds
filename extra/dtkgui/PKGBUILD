# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.7.11
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
depends=('dtkcore' 'libqt5xdg' 'librsvg' 'qt5-base' 'qt5-wayland' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'ninja' 'qt5-tools' 'gtest' 'doxygen' 'treeland-protocols')
source=("git+https://github.com/linuxdeepin/dtkgui.git#tag=$pkgver")
sha512sums=('5d7fa6a55e6b074fcc089ec681301e5e1d646fcd9327b9b7f3996a9b981ef0f124c4e64bd4125d6cae76c081fbd0f5eba4d3b5916cd503c119f0c24a2ac49c76')

build() {
  cd dtkgui
  cmake . -GNinja \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DDTK_DISABLE_EX_IMAGE_FORMAT=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtkgui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkgui
  DESTDIR="$pkgdir" ninja install
}
