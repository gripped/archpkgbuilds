# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.6.26
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
depends=('dtkcore' 'freeimage' 'libraw' 'libqtxdg' 'librsvg' 'qt5-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtkgui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('95e86ac252b9c6e20fe48a29fd07a6924eeed2f96400aa8f4632e4eddfc13c642c11fe47dcc7d88af06c44c7ea0394733c90b1385ca8408af740653e287371ba')

build() {
  cd dtkgui-$pkgver
  cmake . -GNinja \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtkgui-$pkgver
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkgui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
