# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.6.28
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
depends=('dtkcore' 'freeimage' 'libraw' 'libqt5xdg' 'librsvg' 'qt5-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtkgui.git#tag=$pkgver")
sha512sums=('25f4b148dcec91905410c9fb397fa664f8b8d6797ae33cabb035f2ed527072e2f3ec7430718eb295029f7e4fe51c5d677507aeb7ddb65251869e91abc9e89d2a')

build() {
  cd dtkgui
  cmake . -GNinja \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtkgui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkgui
  DESTDIR="$pkgdir" ninja install
}
