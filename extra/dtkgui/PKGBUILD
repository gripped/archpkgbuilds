# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.6.29
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
depends=('dtkcore' 'freeimage' 'libraw' 'libqt5xdg' 'librsvg' 'qt5-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtkgui.git#tag=$pkgver")
sha512sums=('6d724df229e9b564f79065e39a0731e766404280aa5a47ddc5c95ce6522564e7514c365b16c6c1942db45e8d65b3af03613d46bf98cddc0ed0be2b5fc05e19ef')

build() {
  cd dtkgui
  cmake . -GNinja \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DQCH_INSTALL_DESTINATION=share/doc/qt \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtkgui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkgui
  DESTDIR="$pkgdir" ninja install
}
