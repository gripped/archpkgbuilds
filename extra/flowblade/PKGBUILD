# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: sxe <sxxe@gmx.de>

pkgname=flowblade
pkgver=2.22
pkgrel=2
pkgdesc='Multitrack non-linear video editor'
arch=('any')
url='https://jliljebl.github.io/flowblade/'
license=('GPL-3.0-or-later')
depends=('dbus-python' 'ffmpeg' 'frei0r-plugins' 'gmic' 'gtk3' 'mlt' 'python-cairo'
         'python-gobject' 'python-numpy' 'python-pillow' 'sdl_image' 'swh-plugins' 'python-libusb1')
makedepends=('python-setuptools')
source=("https://github.com/jliljebl/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('7f9c30712320170bf7e4f8bcc7006f4421f4459a77ef25059412f562ae94823a')

prepare() {
  cd $pkgname-$pkgver/$pkgname-trunk
  _python_site_packages=`python -c 'import site; print(site.getsitepackages()[0])'`
  sed -i "s|/usr/share/pyshared/Flowblade|$_python_site_packages/Flowblade|" flowblade
}

build() {
  cd $pkgname-$pkgver/$pkgname-trunk
  python setup.py build
}

package() {
  cd $pkgname-$pkgver/$pkgname-trunk
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
