# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: nyanmisaka <nst799610810@gmail.com>

pkgname=jellyfin-ffmpeg
epoch=1
pkgver=7.1.3p3
pkgrel=1
pkgdesc='Jellyfin fork of ffmpeg'
arch=(x86_64)
url='https://github.com/jellyfin/jellyfin-ffmpeg'
license=(GPL-3.0-only)
depends=(
  glibc
  alsa-lib
  bzip2
  fontconfig
  fribidi
  gmp
  gnutls
  lame
  libass.so
  libbluray.so
  libchromaprint.so
  libdav1d.so
  libdrm
  libfreetype.so
  libopenmpt.so
  libplacebo.so
  libtheora
  libva.so
  libva-drm.so
  libvdpau
  libvorbisenc.so
  libvorbis.so
  libvpx.so
  libwebp
  libx11
  libx264.so
  libx265.so
  libxext
  libxml2
  libzimg.so
  ocl-icd
  onevpl
  opus
  shaderc
  sndio
  srt
  svt-av1
  vulkan-icd-loader
  xz
  zlib
  zvbi
)
makedepends=(
  git
  nasm
  clang
  ffnvcodec-headers
  amf-headers
  opencl-headers
  vulkan-headers
)
optdepends=(
  'intel-media-driver: for Intel VAAPI support (Broadwell and newer)'
  'intel-media-sdk: for Intel Quick Sync Video'
  'onevpl-intel-gpu: for Intel Quick Sync Video (Alder Lake and newer)'
  'intel-compute-runtime: for Intel OpenCL runtime based tonemapping'
  'libva-intel-driver: for Intel legacy VAAPI support (Comet Lake and older)'
  'libva-mesa-driver: for AMD VAAPI support'
  'nvidia-utils: for Nvidia NVDEC/NVENC support'
  'opencl-amd: for AMD OpenCL runtime based Tonemapping'
  'vulkan-radeon: for AMD RADV Vulkan support'
  'vulkan-intel: for Intel ANV Vulkan support'
)
options=(!lto)
source=("$pkgname::git+$url#tag=v${pkgver/p/-}")
sha512sums=('aa3f651f012de9c7f724498f3e2b120aa5f6741bbb0fc5f8d1dd701ecf0193fec714f73c52bee9f27f8980376dcd2b712128741eefaad5f5675c0c0deeb9f0aa')
b2sums=('784eecef0528c5e7f2a9dad5165bcc5d788d4b93b901c82e6b9a00183b1519b07f56083125692ee5f98da58ed219ab119314f7622c8d2308334e4f98bdd45e0b')

prepare() {
  cd "$pkgname"

  # apply jellyfin patches to ffmpeg
  for i in debian/patches/*.patch; do
    patch -p1 -i "$i"
  done
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix="/usr/lib/jellyfin-ffmpeg" \
    --target-os=linux \
    --extra-version=Jellyfin \
    --disable-doc \
    --disable-ffplay \
    --disable-ptx-compression \
    --disable-shared \
    --disable-libxcb \
    --disable-sdl2 \
    --disable-xlib \
    --enable-gpl \
    --enable-version3 \
    --enable-static \
    --enable-gmp \
    --enable-gnutls \
    --enable-chromaprint \
    --enable-libfontconfig \
    --enable-libass \
    --enable-libbluray \
    --enable-libdrm \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-libopenmpt \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libdav1d \
    --enable-libsvtav1 \
    --enable-libwebp \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libzvbi \
    --enable-libzimg \
    --enable-libshaderc \
    --enable-libplacebo \
    --enable-vulkan \
    --enable-opencl \
    --enable-vaapi \
    --enable-amf \
    --enable-libvpl \
    --enable-ffnvcodec \
    --enable-cuda \
    --enable-cuda-llvm \
    --enable-cuvid \
    --enable-nvdec \
    --enable-nvenc

  make
}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/lib/$pkgname" ff{mpeg,probe}
}
