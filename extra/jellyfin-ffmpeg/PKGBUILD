# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: nyanmisaka <nst799610810@gmail.com>

pkgname=jellyfin-ffmpeg
epoch=1
pkgver=7.0.2p9
pkgrel=2
pkgdesc='Jellyfin fork of ffmpeg'
arch=('x86_64')
url='https://github.com/jellyfin/jellyfin-ffmpeg'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'alsa-lib'
  'bzip2'
  'fontconfig'
  'fribidi'
  'gmp'
  'gnutls'
  'lame'
  'libass.so'
  'libbluray.so'
  'libchromaprint.so'
  'libdav1d.so'
  'libdrm'
  'libfreetype.so'
  'libopenmpt.so'
  'libplacebo.so'
  'libtheora'
  'libva.so'
  'libva-drm.so'
  'libvdpau'
  'libvorbisenc.so'
  'libvorbis.so'
  'libvpx.so'
  'libwebp'
  'libx11'
  'libx264.so'
  'libx265.so'
  'libxext'
  'libxml2'
  'libzimg.so'
  'ocl-icd'
  'onevpl'
  'opus'
  'shaderc'
  'srt'
  'svt-av1'
  'vulkan-icd-loader'
  'xz'
  'zlib'
  'zvbi'
)
makedepends=(
  'git'
  'nasm'
  'clang'
  'ffnvcodec-headers'
  'amf-headers'
  'opencl-headers'
  'vulkan-headers'
)
optdepends=(
  'intel-media-driver: for Intel VAAPI support (Broadwell and newer)'
  'intel-media-sdk: for Intel Quick Sync Video'
  'onevpl-intel-gpu: for Intel Quick Sync Video (Alder Lake and newer)'
  'intel-compute-runtime: for Intel OpenCL runtime based tonemapping'
  'libva-intel-driver: for Intel legacy VAAPI support (Comet Lake and older)'
  'libva-mesa-driver: for AMD VAAPI support'
  'nvidia-utils: for Nvidia NVDEC/NVENC support'
  'opencl-amd: for AMD OpenCL runtime based Tonemapping'
  'vulkan-radeon: for AMD RADV Vulkan support'
  'vulkan-intel: for Intel ANV Vulkan support'
)
options=('!lto')
source=("$pkgname::git+$url#tag=v${pkgver/p/-}"
        "fix_build_with_svt-av1-3.0.patch")
sha512sums=('aef16c9eb8455bb75fc2cab9f3a709055416a3b0bcae177fc78ef2601e5b7c67668bcc03129f17d862960c71aa26230c9328ac46b5d0a5cbe88b9514ce075a0b'
            'e11f1ce94faaffa0dde9580d4d9ae74b73330d2e5cefa81e4512230fa7d164572f98af7f6feb96c3e84e3b6896650cdd7c7b659e311a4adc4b31d7000a110e69')
b2sums=('f940f534cafa035c8af40e3050d65be2af27d38751fa86d333e8a23396529dcb8c8901766c97733b521460e8df25adbaa9032319e1e82fe9bec99172b8d14a64'
        'a71d0c0092ae64b9cef38efb4a585b415e74842a173796b63778cd2c570c3216179c5ac59346d51388cc874cc37a32adb93f038ab26747d8e3fc0c984ce116ff')

prepare() {
  cd "$pkgname"
  # Fix for svt-av1 3.0
  patch -Np1 < ../fix_build_with_svt-av1-3.0.patch
  # apply jellyfin patches to ffmpeg
  for i in debian/patches/*.patch; do
    patch -p1 -i "$i"
  done
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix="/usr/lib/jellyfin-ffmpeg" \
    --target-os=linux \
    --extra-version=Jellyfin \
    --disable-doc \
    --disable-ffplay \
    --disable-ptx-compression \
    --disable-shared \
    --disable-libxcb \
    --disable-sdl2 \
    --disable-xlib \
    --enable-gpl \
    --enable-version3 \
    --enable-static \
    --enable-gmp \
    --enable-gnutls \
    --enable-chromaprint \
    --enable-libfontconfig \
    --enable-libass \
    --enable-libbluray \
    --enable-libdrm \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-libopenmpt \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libdav1d \
    --enable-libsvtav1 \
    --enable-libwebp \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libzvbi \
    --enable-libzimg \
    --enable-libshaderc \
    --enable-libplacebo \
    --enable-vulkan \
    --enable-opencl \
    --enable-vaapi \
    --enable-amf \
    --enable-libvpl \
    --enable-ffnvcodec \
    --enable-cuda \
    --enable-cuda-llvm \
    --enable-cuvid \
    --enable-nvdec \
    --enable-nvenc

  make
}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/lib/$pkgname" ff{mpeg,probe}
}
