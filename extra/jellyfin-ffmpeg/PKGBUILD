# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: nyanmisaka <nst799610810@gmail.com>

pkgname=jellyfin-ffmpeg
epoch=1
pkgver=7.1.3p1
pkgrel=3
pkgdesc='Jellyfin fork of ffmpeg'
arch=(x86_64)
url='https://github.com/jellyfin/jellyfin-ffmpeg'
license=(GPL-3.0-only)
depends=(
  glibc
  alsa-lib
  bzip2
  fontconfig
  fribidi
  gmp
  gnutls
  lame
  libass.so
  libbluray.so
  libchromaprint.so
  libdav1d.so
  libdrm
  libfreetype.so
  libopenmpt.so
  libplacebo.so
  libtheora
  libva.so
  libva-drm.so
  libvdpau
  libvorbisenc.so
  libvorbis.so
  libvpx.so
  libwebp
  libx11
  libx264.so
  libx265.so
  libxext
  libxml2
  libzimg.so
  ocl-icd
  onevpl
  opus
  shaderc
  srt
  svt-av1
  vulkan-icd-loader
  xz
  zlib
  zvbi
)
makedepends=(
  git
  nasm
  clang
  ffnvcodec-headers
  amf-headers
  opencl-headers
  vulkan-headers
)
optdepends=(
  'intel-media-driver: for Intel VAAPI support (Broadwell and newer)'
  'intel-media-sdk: for Intel Quick Sync Video'
  'onevpl-intel-gpu: for Intel Quick Sync Video (Alder Lake and newer)'
  'intel-compute-runtime: for Intel OpenCL runtime based tonemapping'
  'libva-intel-driver: for Intel legacy VAAPI support (Comet Lake and older)'
  'libva-mesa-driver: for AMD VAAPI support'
  'nvidia-utils: for Nvidia NVDEC/NVENC support'
  'opencl-amd: for AMD OpenCL runtime based Tonemapping'
  'vulkan-radeon: for AMD RADV Vulkan support'
  'vulkan-intel: for Intel ANV Vulkan support'
)
options=(!lto)
source=("$pkgname::git+$url#tag=v${pkgver/p/-}"
        "0001-unbreak-svt-av1-4-build.patch")
sha512sums=('1b422ad95a9baef0847243321e978b5e67c6e804f9a17beefc232fbb77c521e7caba0f6172204b619eb47d2e4da203341cf1e396f8319ec2c69db67a4f2cb2b1'
            'd13251bd6e76b58c63650f07a60f34cb74da5e107396812166b587bb586fc4d39579591ac7c12cb96a704e5e23b073912436823480f9a85ae511534561600868')
b2sums=('9005d5d0a8a36573532db7b9332b9f50ded7692dc76d75c4ffb351d7ad4d1edd4d09ec4fc1b9685f778470af32d58700b253fe2596561599ddf67e3673bb29e7'
        'a004d7b7b021e82ae01e9fd171c4cd2901bb1348aafa8ec2256f38c967521cdb947c01b8b6872b6076fc0ee96bf296992abfecb5446a83eb8c052b976851948a')

prepare() {
  cd "$pkgname"

  # apply jellyfin patches to ffmpeg
  for i in debian/patches/*.patch; do
    patch -p1 -i "$i"
  done
  patch -Np1 < ../0001-unbreak-svt-av1-4-build.patch
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix="/usr/lib/jellyfin-ffmpeg" \
    --target-os=linux \
    --extra-version=Jellyfin \
    --disable-doc \
    --disable-ffplay \
    --disable-ptx-compression \
    --disable-shared \
    --disable-libxcb \
    --disable-sdl2 \
    --disable-xlib \
    --enable-gpl \
    --enable-version3 \
    --enable-static \
    --enable-gmp \
    --enable-gnutls \
    --enable-chromaprint \
    --enable-libfontconfig \
    --enable-libass \
    --enable-libbluray \
    --enable-libdrm \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-libopenmpt \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libdav1d \
    --enable-libsvtav1 \
    --enable-libwebp \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libzvbi \
    --enable-libzimg \
    --enable-libshaderc \
    --enable-libplacebo \
    --enable-vulkan \
    --enable-opencl \
    --enable-vaapi \
    --enable-amf \
    --enable-libvpl \
    --enable-ffnvcodec \
    --enable-cuda \
    --enable-cuda-llvm \
    --enable-cuvid \
    --enable-nvdec \
    --enable-nvenc

  make
}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/lib/$pkgname" ff{mpeg,probe}
}
