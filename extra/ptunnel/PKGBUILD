# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: George Hilliard <gh403@msstate.edu>

pkgname=ptunnel
pkgver=0.72
pkgrel=6
pkgdesc="A tool for reliably tunneling TCP connections over ICMP echo request and reply packets"
arch=('x86_64')
url="https://www.cs.uit.no/~daniels/PingTunnel"
license=('BSD')
depends=('libpcap')
source=("https://www.cs.uit.no/~daniels/PingTunnel/PingTunnel-$pkgver.tar.gz"
        "ptunnel.service")
sha512sums=('943fc571ca95bb6af59f2a74458470865a89a502145bb1e5cf9b86159d61d897418d10d100691337f299892af7aba65964624462068a77ef1645890a4abf5d17'
            '4adb89e26a1900d35178a1f372aaf96043856f4c334589949f758e3e47ffb15cffcf358c772ccda2530f5871240f2f2d9839c4ab896556e178f8bd8d6e670f29')

build() {
  cd "$srcdir/PingTunnel"
  make
}

package() {
  install -Dm755 PingTunnel/ptunnel "$pkgdir/usr/bin/ptunnel"
  install -Dm644 ptunnel.service "$pkgdir/usr/lib/systemd/system/ptunnel.service"
  install -Dm644 PingTunnel/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
