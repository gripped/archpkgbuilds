# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=kernel-hardening-checker
pkgver=0.6.10.2
pkgrel=1
pkgdesc='Tool for checking the security hardening options of the Linux kernel'
url="https://github.com/a13xp0p0v/kernel-hardening-checker"
arch=(any)
license=(GPL-3.0-only)
depends=(python)
makedepends=(git python-setuptools python-build python-installer python-wheel)
replaces=(kconfig-hardened-check)
conflicts=(kconfig-hardened-check)
source=("git+https://github.com/a13xp0p0v/kernel-hardening-checker.git#tag=v${pkgver}")
sha256sums=('f5361cc5ed70180a08be5da1b4646ef96b8f0e10990a44b68f08712bd8de54e9')
b2sums=('cc12e1b5b3b099be2ba1b73d82804650a2779799e955c16095d7aa784d0920e1cee80fd1fc6c8c4c30791e82037b4b74917e7edea28797223c5f696206a28a42')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
