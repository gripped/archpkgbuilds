# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=kernel-hardening-checker
pkgver=0.6.10.1
pkgrel=1
pkgdesc='Tool for checking the security hardening options of the Linux kernel'
url="https://github.com/a13xp0p0v/kernel-hardening-checker"
arch=(any)
license=(GPL-3.0-only)
depends=(python)
makedepends=(git python-setuptools python-build python-installer python-wheel)
replaces=(kconfig-hardened-check)
conflicts=(kconfig-hardened-check)
source=("git+https://github.com/a13xp0p0v/kernel-hardening-checker.git#tag=v${pkgver}")
sha256sums=('904a79d31491c9f9cdc11e1a8c3c8d4fe5962e1a008209ec127c84aa0fde4980')
b2sums=('d0131e84e738c20d97191f3c9ee66153d3985235a2f8ee428349545f2c0c64c41554f495e2c9d15d3134575e28f527685f6f91f699099d7aaa323f7358529449')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
