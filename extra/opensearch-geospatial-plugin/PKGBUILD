# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-geospatial-plugin
pkgver=3.1.0.0
pkgrel=1
_opensearchver=3.1.0
_jdkver=21
pkgdesc="OpenSearch Geospatial Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/dashboards/geojson-regionmaps/"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}" 'opensearch-job-scheduler-plugin')
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/geospatial/archive/${pkgver}.tar.gz"
)
sha256sums=('9246ba4d5ceb8e3371935d8448184ebb979d34334e07e1b4ce89b661333c062b')

build() {
  cd "geospatial-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew build \
    --exclude-task ":jacocoTestReport" \
    --exclude-task ":jacocoTestCoverageVerification"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-geospatial"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-geospatial"
  unzip "${srcdir}/geospatial-${pkgver}/build/distributions/opensearch-geospatial-${pkgver}.zip"

  install -Dm644 "${srcdir}/geospatial-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
