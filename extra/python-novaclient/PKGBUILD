# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-novaclient
pkgver=18.9.0
pkgrel=1
pkgdesc="Client library for OpenStack Compute API"
arch=('any')
url="https://docs.openstack.org/python-novaclient/latest/"
license=('Apache')
depends=('python' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
         'python-requests' 'python-iso8601' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization')
makedepends=('git' 'python-setuptools')
checkdepends=('python-oslotest' 'python-osprofiler' 'python-requests-mock' 'python-ddt' 'python-stestr' 'python-testscenarios')
source=("git+https://github.com/openstack/python-novaclient.git#tag=$pkgver")
sha512sums=('c3d42614a4019c1f474ffc414ad417c65396bbcc96b3540e263b9ad4880222a1c50a26bf52cb6fb527088499b7b4abe96090113a0d24620cd51bf5d6e5c486ab')

build() {
  cd python-novaclient
  python setup.py build
}

check() {
  cd python-novaclient
  stestr run
}

package() {
  cd python-novaclient
  python setup.py install --root="$pkgdir" --optimize=1
}
