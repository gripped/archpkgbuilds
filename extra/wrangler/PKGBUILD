# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=wrangler
pkgver=4.9.1
pkgrel=1
_esbuild_ver='0.24.2'
pkgdesc="The CLI for Cloudflare Workers"
url='https://github.com/cloudflare/workers-sdk'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'glibc'
  'nodejs'
  'worker-build'
  'workerd'
)
makedepends=(
  'go'
  'npm'
  'pnpm'
  'turbo'
  'typescript'
  'yq'
)
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/cloudflare/workers-sdk/archive/refs/tags/wrangler@${pkgver}.tar.gz
        "esbuild-${_esbuild_ver}.tar.gz::https://github.com/evanw/esbuild/archive/v${_esbuild_ver}.tar.gz")
sha256sums=('378a9a1e1d23536f87a44ab7f258b8656e2a6c000f7680715804800d683f8f3b'
            '171e1b0cd4c64222a1953203f6b3dab3c7a3f95b8939a72b4ebbd024302513b4')
b2sums=('1e98cd0590050a6a656c48739bac236a855f2da7de54a1d18adb7530443751f4d241ad3f3738e9ac7da753592c36eb6324a0af00c1e9e67372279591f052be62'
        '339cf510231570e4e4df44138e9f4315c2407ef9cba7b2c5e4e3700e3e7b8e8b6f7778a8b2f6ed8269b5fffb181aa6f3850684eb474b9b7e109226a76f097d95')

prepare() {
  cd "workers-sdk-wrangler-${pkgver}"

  # verify we're still using the correct esbuild version
  esbuild=$(yq -r .catalogs.default.esbuild.version pnpm-lock.yaml)
  [[ "$esbuild" == "$_esbuild_ver" ]]
}

build() {
  cd "workers-sdk-wrangler-${pkgver}"
  pnpm install -C packages/wrangler --frozen-lockfile
  turbo build
  pnpm pack -C packages/wrangler

  # build a copy of the specific esbuild version wrangler depends on
  # getting this wrong makes `wrangler dev` fail with no error
  cd "${srcdir}/esbuild-${_esbuild_ver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    ./cmd/esbuild
}

package() {
  npm install -g --prefix "${pkgdir}/usr" "workers-sdk-wrangler-${pkgver}/packages/wrangler/wrangler-${pkgver}.tgz"
  install -Dm644 "workers-sdk-wrangler-${pkgver}/LICENSE-MIT"  -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # This file is very big
  rm "${pkgdir}/usr/lib/node_modules/wrangler/wrangler-dist/cli.js.map"

  # Remove pre-compiled binaries
  rm -rv \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/esbuild" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/esbuild"

  # Add symlinks to workerd package
  ln -s "../../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/"
  ln -s "../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/"

  # Add esbuild binary
  install -Dm755 -t "${pkgdir}/usr/lib/wrangler/bin/" \
    "esbuild-${_esbuild_ver}/esbuild"
  ln -s "../../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/"
  ln -s "../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/"
}

# vim: ts=2 sw=2 et:
