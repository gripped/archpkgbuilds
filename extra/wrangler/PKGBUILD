# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=wrangler
pkgver=3.28.3
pkgrel=1
_esbuild_ver='0.17.19'
pkgdesc="The CLI for Cloudflare Workers"
url='https://github.com/cloudflare/workers-sdk'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'glibc'
  'nodejs'
  'worker-build'
  'workerd'
)
makedepends=(
  'go'
  'jq'
  'npm'
)
noextract=("${pkgname}-${pkgver}.tgz")
source=(https://registry.npmjs.org/${pkgname}/-/${pkgname}-${pkgver}.tgz
        https://github.com/cloudflare/workers-sdk/raw/wrangler@${pkgver}/LICENSE-MIT
        "esbuild-${_esbuild_ver}.tar.gz::https://github.com/evanw/esbuild/archive/v${_esbuild_ver}.tar.gz")
sha256sums=('f75fd06ec861c03a0b24c226821a9280e0b6ccc7eecec3669d7e320b76f5756b'
            '9bb3b077cc8628334bab25961223dd8207252c8a56aa054195be38f1c042aaf4'
            '481201f00848594f5850ac32ddf9d4d21047e5a4c306485fb7461dd4d14d6fa2')
b2sums=('a1706706d19d5b32735a6a90d38bb30fd0ef616d4f2f9681b851073ae3606ce4576667d79d7d8fec438e13674bf5f9a5495af4667f0c1f8fcb421cdef157faa8'
        'b7ab53a734a99d75008151c4606ddbfb851d6569047acdbeef3ec9be07bea3d4c59c1d570889d41613f61eb1849c4fb8de84f44dda093af7d091543310eee2dd'
        '4c26fdd7dd52706f98c6e9d60e6b3993168b2ffefd35b326e03be806d5083adcb50d9cb931f925f551e90aef5392647c99807b73af4075d6d5e2336c565c33ea')

prepare() {
  # verify we're still using the correct esbuild version
  esbuild=$(tar xOf "${pkgname}-${pkgver}.tgz" package/package.json | jq -r .dependencies.esbuild)
  [[ "$esbuild" == "$_esbuild_ver" ]]
}

build() {
  # build a copy of the specific esbuild version wrangler depends on
  # getting this wrong makes `wrangler dev` fail with no error
  cd esbuild-${_esbuild_ver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/esbuild
}

package() {
  npm install -g --prefix "${pkgdir}/usr" "${srcdir}/${pkgname}-${pkgver}.tgz"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"

  ## Remove pre-compiled binaries
  rm -rv \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/esbuild" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/esbuild"

  # Add symlinks to workerd package
  ln -s "../../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/"
  ln -s "../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/"

  # Add esbuild binary
  install -Dm755 -t "${pkgdir}/usr/lib/wrangler/bin/" \
    "esbuild-${_esbuild_ver}/esbuild"
  ln -s "../../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/"
  ln -s "../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/"
}

# vim: ts=2 sw=2 et:
