# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=wrangler
pkgver=4.14.1
pkgrel=1
_esbuild_ver='0.25.2'
pkgdesc="The CLI for Cloudflare Workers"
url='https://github.com/cloudflare/workers-sdk'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'glibc'
  'nodejs'
  'worker-build'
  'workerd'
)
makedepends=(
  'go'
  'npm'
  'pnpm'
  'turbo'
  'typescript'
  'yq'
)
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/cloudflare/workers-sdk/archive/refs/tags/wrangler@${pkgver}.tar.gz
        "esbuild-${_esbuild_ver}.tar.gz::https://github.com/evanw/esbuild/archive/v${_esbuild_ver}.tar.gz")
sha256sums=('2e05941ee6938a7aef4050390815cb4be109c5ef472f75b9fc287467fedfcac8'
            '01a6c0a5949e5c2d53e19be52aec152b3186f8bbcf98df6996a20a972a78c330')
b2sums=('18c3cca295de4fe314ae758af46013dc1973fbe83e901cb69fca9285b1ceec7942dd0b47e9b1b61e687bc2b01bf6d479818114467a4dd3842a4673fe5c6a13e2'
        '98ebf2dce4c5715eba2b749ea7f6f00d5fcf6daf8f22ded970ec41df9676d1ca40d6185dab03a9bf6d7931489d2e5aca0c510c991dfc80294826c57b216921da')

prepare() {
  cd "workers-sdk-wrangler-${pkgver}"

  # verify we're still using the correct esbuild version
  esbuild=$(yq -r .catalogs.default.esbuild.version pnpm-lock.yaml)
  [[ "$esbuild" == "$_esbuild_ver" ]]
}

build() {
  cd "workers-sdk-wrangler-${pkgver}"
  pnpm install -C packages/wrangler --frozen-lockfile
  turbo build
  pnpm pack -C packages/wrangler

  # build a copy of the specific esbuild version wrangler depends on
  # getting this wrong makes `wrangler dev` fail with no error
  cd "${srcdir}/esbuild-${_esbuild_ver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    ./cmd/esbuild
}

package() {
  npm install -g --prefix "${pkgdir}/usr" "workers-sdk-wrangler-${pkgver}/packages/wrangler/wrangler-${pkgver}.tgz"
  install -Dm644 "workers-sdk-wrangler-${pkgver}/LICENSE-MIT"  -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # This file is very big
  rm "${pkgdir}/usr/lib/node_modules/wrangler/wrangler-dist/cli.js.map"

  # Remove pre-compiled binaries
  rm -rv \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/esbuild" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/workerd" \
    "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/esbuild"

  # Add symlinks to workerd package
  ln -s "../../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@cloudflare/workerd-linux-64/bin/"
  ln -s "../../../../../../bin/workerd" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/workerd/bin/"

  # Add esbuild binary
  install -Dm755 -t "${pkgdir}/usr/lib/wrangler/bin/" \
    "esbuild-${_esbuild_ver}/esbuild"
  ln -s "../../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/@esbuild/linux-x64/bin/"
  ln -s "../../../../../wrangler/bin/esbuild" "${pkgdir}/usr/lib/node_modules/wrangler/node_modules/esbuild/bin/"
}

# vim: ts=2 sw=2 et:
