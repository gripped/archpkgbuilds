# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Amish <contact at via dot aur>
# Contributor: Justin Davis <jrcd83@gmail.com>
# Contributor: Tony Sokhon <tonyskn@gmail.com>

pkgname=perl-par-packer
pkgver=1.064
pkgrel=2
pkgdesc='PAR Packager'
arch=('x86_64')
url='https://metacpan.org/release/PAR-Packer'
license=('GPL')
depends=('perl-archive-zip' 'perl-getopt-argvfile' 'perl-ipc-run3'
         'perl-module-scandeps' 'perl-par' 'perl-par-dist')
makedepends=('perl')
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/R/RS/RSCHUPP/PAR-Packer-${pkgver}.tar.gz")
b2sums=('20b31df0752430fa23be6ed5cb90a726c75aca2330bc757fc5e81ff159079f5685a69a7ab2e4ce90c1a5b7a1f91f18396e6be275c600459306da48e39939464b')

build() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  if [[ "$BUILDTOOL" == devtools ]]; then
    local perl_dep="perl=$(perl -e 'print substr($^V, 1)')"
    # this is added to the PKGINFO data
    depends+=($perl_dep)
  else
    # this is adde to the SRCINFO data
    depends+=(perl)
  fi

  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  #remove conflicting and empty man page
  rm "${pkgdir}"/usr/share/man/man1/pp.1
}
