# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=hotdoc
pkgver=0.17
pkgrel=1
pkgdesc="The Tastiest Documentation Tool"
url="https://github.com/hotdoc/hotdoc"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  bash
  glib2
  glibc
  json-glib
  libxml2
  python
  python-appdirs
  python-dbus-deviation
  python-faust-cchardet
  python-feedgen
  python-lxml
  python-networkx
  python-pkgconfig
  python-schema
  python-setuptools
  python-toposort
  python-wheezy-template
  python-yaml
)
makedepends=(
  cmake
  git
  npm
  python-build
  python-installer
  python-wheel
)
optdepends=(
  'clang: for the C extension'
  'llvm: for the C extension'
)
source=(
  "git+https://github.com/hotdoc/hotdoc#tag=$pkgver"
  "mathieu-cmark::git+https://github.com/MathieuDuponchelle/cmark"
  "prismjs::git+https://github.com/PrismJS/prism"
  "git+https://github.com/hotdoc/hotdoc_bootstrap_theme"
  0001-Simplify-deps-for-Python-3.11.patch
)
b2sums=('3be5a4b0401914e1b4362d5edb4adcccb7bc2edc158ac273ca4b8e433ededf6a1b22814a6b219f3dc5f692f0b5cdbfbe908eadfff37d58f3f3c0b38d75ef5e83'
        'SKIP'
        'SKIP'
        'SKIP'
        'cc1ebc2f1d4842d6bd39f9d755a4abf909bc3db12b863bc40a8cffea5eab71eb88034884a1d7c18e1be9d9714c3a15698753393c5497bbc19449db069ae535d7')

prepare() {
  cd hotdoc

  # Avoid having to package https://pypi.org/project/backports.entry-points-selectable/
  git apply -3 ../0001-Simplify-deps-for-Python-3.11.patch

  git submodule init
  git submodule set-url cmark "$srcdir/mathieu-cmark"
  git submodule set-url hotdoc/extensions/syntax_highlighting/prism "$srcdir/prismjs"
  git submodule set-url hotdoc/hotdoc_bootstrap_theme "$srcdir/hotdoc_bootstrap_theme"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  cd hotdoc
  python -m build --wheel --no-isolation
}

check() {
  cd hotdoc/build/lib.linux-$CARCH-cpython-*
  python -m unittest
}

package() {
  cd hotdoc
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set sw=2 sts=-1 et:
