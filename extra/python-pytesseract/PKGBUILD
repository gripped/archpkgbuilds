# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Christoph Brill <egore911@gmail.com>

pkgname=python-pytesseract
_name=${pkgname#python-}
pkgver=0.3.13
pkgrel=3
pkgdesc='Python wrapper for Google Tesseract'
arch=('any')
url=https://github.com/madmaze/pytesseract
license=('Apache')
depends=('python-packaging' 'python-pillow' 'tesseract')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest' 'tesseract-data-eng' 'tesseract-data-fra')
source=("git+$url.git#tag=v$pkgver")
b2sums=('f85ddc0168e710237b7fd7b59127bad033d43d91637e7151ee8e4da40505095adb11067f6c56135205a895449e1cf831479ed33f082c0aba3ea9466f85456712')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # test_get_languages coredumps tessaract with invalid input
  #[builduser@arch-nspawn-433964 pytesseract]$ tesseract --list-langs --tessdata-dir wjfiw
  # terminate called after throwing an instance of 'std::filesystem::__cxx11::filesystem_error'
  # what():  filesystem error: recursive directory iterator cannot open directory: No such file or directory [wjfiw/]
  # Aborted (core dumped)
  test-env/bin/python -m pytest -k 'not test_get_languages'
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
