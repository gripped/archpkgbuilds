# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-db
_upstream=alpm
pkgver=0.2.1
pkgrel=1
pkgdesc="Command line interfaces for the handling of alpm-db structures"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-db/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('eba239ac04f88d21733f5f0682944f820a0828f502af72d1a0671f4f2cd2aca93fe8b2836d0eb92661a83adb26ad01d82875368ae6ea9329840eb58a5f52ec16')
b2sums=('fde37f4a41003d9d44fea5396272582dbd247bce75e40a9b8d01db4172d47df9013ce12a8e46229555f91abcd42f93e550e3290533382591edfc975694f44e80')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --features cli --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
  just generate specifications $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  conflicts=(
    alpm-db-desc
    alpm-files
  )
  replaces=(
    alpm-db-desc
    alpm-files
  )

  cd $_upstream
  install -vDm 755 target/release/$pkgname-{desc,files} -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
  install -vDm 644 output/shell_completions/$pkgname-desc.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname-desc"
  install -vDm 644 output/shell_completions/$pkgname-files.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname-files"
  install -vDm 644 output/shell_completions/$pkgname-{desc,files}.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname-{desc,files} -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
