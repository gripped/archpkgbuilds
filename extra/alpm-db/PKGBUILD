# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-db
_upstream=alpm
pkgver=0.2.0
pkgrel=2
pkgdesc="Command line interfaces for the handling of alpm-db structures"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-db/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('622ab288d9e7b35ea3b90ac7c5cea352c76987521d98d308ef4dc29bc93689630f06c89ae321d6a8b5f2e4c94348dec80855e03e31090b2597295a0c46492ca2')
b2sums=('6564dd0f786d63588d64aeb74aa72e56443bfe4bbfbf64534ecb237cfda8197e20c5d8deea16948d719be7c01e57aeed218a3459fc902c1643e2016370c5f3fb')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --features cli --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
  just generate specifications $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  conflicts=(
    alpm-db-desc
    alpm-files
  )
  replaces=(
    alpm-db-desc
    alpm-files
  )

  cd $_upstream
  install -vDm 755 target/release/$pkgname-{desc,files} -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
  install -vDm 644 output/shell_completions/$pkgname-desc.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname-desc"
  install -vDm 644 output/shell_completions/$pkgname-files.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname-files"
  install -vDm 644 output/shell_completions/$pkgname-{desc,files}.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname-{desc,files} -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
