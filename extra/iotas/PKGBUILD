# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iotas
pkgver=0.11.0
_katex_version=0.16.21
pkgrel=1
pkgdesc='Simple note taking with Markdown support and optional Nextcloud Notes sync'
arch=(any)
url='https://apps.gnome.org/Iotas/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libsecret
  pango
  python
  python-gobject
  python-gtkspellcheck
  python-linkify-it-py
  python-markdown-it-py
  python-mdit_py_plugins
  python-packaging
  python-pypandoc
  python-requests
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
source=(
  "git+https://gitlab.gnome.org/World/iotas.git#tag=$pkgver"
  "https://github.com/KaTeX/KaTeX/releases/download/v$_katex_version/katex.tar.gz"
  iotas-search-provider-fixes.patch
)
b2sums=('394edb381de1bb53a4c8b0dab33a964abf01a83fb417101ad572c26bcf1ed00df563e4d489799bbdf155882d669aaa91094258203b47f638cbc52a3313d18847'
        'bf574824d35e1c4cbacb0fbcc752934b4b99cec11921f84c3a2eb18941156ebcbc49e3f214c5f58af631cba74115d9c6df6bfc3086ecb4e382d167b637367e0d'
        'eeff948e1beaeac03141c6ed91e79daf841d94b8fd0618c5b2f42de019c0022a3ec6240487b9fc055a12e255dda4e0f01064dcfd9f0d5b07b7585d219e28d1f1')

prepare() {
  cd $pkgname

  # Better error handling in search provider
  # https://gitlab.gnome.org/World/iotas/-/merge_requests/284
  git apply -3 ../iotas-search-provider-fixes.patch

  # Don't hide end title buttons
  # https://gitlab.gnome.org/World/iotas/-/merge_requests/285
  sed -i '/show-end-title-buttons/d' data/ui/*.ui
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Bundle KaTeX
  cd katex
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/" katex.min.css
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/js/" katex.min.js
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/fonts/" fonts/*.woff2
}
