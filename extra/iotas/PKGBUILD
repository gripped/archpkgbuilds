# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iotas
pkgver=0.12.1
_katex_version=0.16.22
pkgrel=2
pkgdesc='Simple note taking with Markdown support and optional Nextcloud Notes sync'
arch=(any)
url='https://apps.gnome.org/Iotas/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libsecret
  pango
  python
  python-gobject
  python-gtkspellcheck
  python-linkify-it-py
  python-markdown-it-py
  python-mdit_py_plugins
  python-packaging
  python-requests
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
checkdepends=(
  python-pypandoc
  python-pytest
)
optdepends=('python-pypandoc: Export to ODT and custom formats')
source=(
  "git+https://gitlab.gnome.org/World/iotas.git#tag=$pkgver"
  "katex-v$_katex_version.tar.gz::https://github.com/KaTeX/KaTeX/releases/download/v$_katex_version/katex.tar.gz"
  iotas-optional-pandoc.patch
)
b2sums=(
  82d6e1eee72a625ee02d84543d8f139bf7290d835d5a64b300edfd1c9dc7cdbbe6724135cd68e0df51d564772a6dd264f8ee1a02ea2ff159971b5e1190db3947
  cf1f847bb4f6f64b048b04b52f9cf772dcabbe5dccbd802072978f133c7c9600b4404bcc2399ea35f71f77176774f6953f8f198e6b94b90a63b5124576c8bbd2
  c33be85c7523802cb0ec358779bd35aa7b446f811aad22c062d00079c3b9d2e0eb5f1e4d584ab77756d3c813d57a672435047a1ed312e7ec1d2e15947035509c
)

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/World/iotas/-/merge_requests/291
  git apply -3 ../iotas-optional-pandoc.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  PYTHONPATH="$PWD/iotas/third-party" \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Bundle KaTeX
  cd katex
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/" katex.min.css
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/js/" katex.min.js
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/fonts/" fonts/*.woff2
}
