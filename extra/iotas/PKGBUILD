# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iotas
pkgver=0.11.0
_katex_version=0.16.21
pkgrel=4
pkgdesc='Simple note taking with Markdown support and optional Nextcloud Notes sync'
arch=(any)
url='https://apps.gnome.org/Iotas/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libsecret
  pango
  python
  python-gobject
  python-gtkspellcheck
  python-linkify-it-py
  python-markdown-it-py
  python-mdit_py_plugins
  python-packaging
  python-requests
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
optdepends=('python-pypandoc: Export to ODT and custom formats')
source=(
  "git+https://gitlab.gnome.org/World/iotas.git#tag=$pkgver"
  "katex-v$_katex_version.tar.gz::https://github.com/KaTeX/KaTeX/releases/download/v$_katex_version/katex.tar.gz"
  iotas-dbus-activatable.patch
  iotas-actions.patch
  iotas-optional-pandoc.patch
)
b2sums=(
  394edb381de1bb53a4c8b0dab33a964abf01a83fb417101ad572c26bcf1ed00df563e4d489799bbdf155882d669aaa91094258203b47f638cbc52a3313d18847
  8427954e659536ac022eafd0b70a090f07485404708734ce5aa55a240eb609ea3c10431bd6811e7e03e14a76d61a05e61ae03182636a41e596b233bff4186eb9
  b4bf6b7ce8f06b317eff0d8ca429195711ebd8690a259c86cdae2714dd8201ff4536671ccdb2f37c69fef623e47e0e62a21a116506c82b37e0503dfd8d02dde9
  1f3b83f5358cc7c55a67355e27efa1a04c2cc41ab2d7ade8df7863ff716832bfd64a22cef522c6fb690cbf62ff9908ebf5265637ec531bbfbb252de54c3d3be9
  2ca2d821440f02b7b9476115bc16b2029debb399d2203d97536631c5c1d627a542c9d8cf354fefd7908d169045fce274539e4424e68f4b2678621f2b8f34f478
)

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/World/iotas/-/merge_requests/284
  git cherry-pick -n 5e60f5f67255259a681c6862a639bc2c14c14da9

  # https://gitlab.gnome.org/World/iotas/-/merge_requests/286
  git cherry-pick -n f235ac908a837fe548573f7a596d36d73d83f1b5

  # https://gitlab.gnome.org/World/iotas/-/merge_requests/287
  git cherry-pick -n 53b2f8a8e6480a5a087021f104a138364fd6c5cb

  # https://gitlab.gnome.org/World/iotas/-/merge_requests/288
  git cherry-pick -n f6e71780076f70ab40c4204bcd83e9726b64245a

  # https://gitlab.gnome.org/World/iotas/-/merge_requests/289
  git apply -3 ../iotas-dbus-activatable.patch

  # https://gitlab.gnome.org/World/iotas/-/merge_requests/290
  git apply -3 ../iotas-actions.patch

  # https://gitlab.gnome.org/World/iotas/-/merge_requests/291
  git apply -3 ../iotas-optional-pandoc.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Bundle KaTeX
  cd katex
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/" katex.min.css
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/js/" katex.min.js
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/fonts/" fonts/*.woff2
}
