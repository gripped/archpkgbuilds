# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iotas
pkgver=0.11.0
_katex_version=0.16.21
pkgrel=3
pkgdesc='Simple note taking with Markdown support and optional Nextcloud Notes sync'
arch=(any)
url='https://apps.gnome.org/Iotas/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libsecret
  pango
  python
  python-gobject
  python-gtkspellcheck
  python-linkify-it-py
  python-markdown-it-py
  python-mdit_py_plugins
  python-packaging
  python-pypandoc
  python-requests
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
source=(
  "git+https://gitlab.gnome.org/World/iotas.git#tag=$pkgver"
  "katex-v$_katex_version.tar.gz::https://github.com/KaTeX/KaTeX/releases/download/v$_katex_version/katex.tar.gz"
)
b2sums=(
  394edb381de1bb53a4c8b0dab33a964abf01a83fb417101ad572c26bcf1ed00df563e4d489799bbdf155882d669aaa91094258203b47f638cbc52a3313d18847
  8427954e659536ac022eafd0b70a090f07485404708734ce5aa55a240eb609ea3c10431bd6811e7e03e14a76d61a05e61ae03182636a41e596b233bff4186eb9
)

prepare() {
  cd $pkgname

  # Better error handling in search provider
  # https://gitlab.gnome.org/World/iotas/-/merge_requests/284
  git cherry-pick -n 5e60f5f67255259a681c6862a639bc2c14c14da9

  # Hide start title buttons
  # https://gitlab.gnome.org/World/iotas/-/merge_requests/286
  git cherry-pick -n f235ac908a837fe548573f7a596d36d73d83f1b5
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Bundle KaTeX
  cd katex
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/" katex.min.css
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/js/" katex.min.js
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/fonts/" fonts/*.woff2
}
