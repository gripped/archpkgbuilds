# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iotas
pkgver=0.9.5
_katex_version=0.16.11
pkgrel=5
pkgdesc='Simple note taking with Markdown support and optional Nextcloud Notes sync'
arch=(any)
url='https://gitlab.gnome.org/World/iotas'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libsecret
  pango
  python
  python-gobject
  python-gtkspellcheck
  python-linkify-it-py
  python-markdown-it-py
  python-mdit_py_plugins
  python-packaging
  python-pypandoc
  python-requests
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
source=(
  "git+https://gitlab.gnome.org/World/iotas.git#tag=$pkgver"
  "https://github.com/KaTeX/KaTeX/releases/download/v$_katex_version/katex.tar.gz"
  iotas-builtin-strenum.patch
)
b2sums=(
  1e421ff2dcf6c35d1f1b0a19043d4770c645d56b50ce11c6848dc338aade6f8d80c9b1457ecf83cab43e820d0e964bc4fa36ec2001d13938819a394f0ad4a67d
  bf574824d35e1c4cbacb0fbcc752934b4b99cec11921f84c3a2eb18941156ebcbc49e3f214c5f58af631cba74115d9c6df6bfc3086ecb4e382d167b637367e0d
  29475d433aa9811743ce1f8b808c0e22cba29f0c6d22670e9a98f3ca8afbd3fb2457ec19234cc7f129953817d72cf56b646acfdff319a2c0739866b4cbc06de9
)

prepare() {
  cd $pkgname

  # Use built-in StrEnum
  git apply -3 ../iotas-builtin-strenum.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Bundle KaTeX
  cd katex
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/" katex.min.css
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/js/" katex.min.js
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/fonts/" fonts/*.woff2
}
