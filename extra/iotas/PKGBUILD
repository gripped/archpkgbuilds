# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iotas
pkgver=0.10.3
_katex_version=0.16.11
pkgrel=1
pkgdesc='Simple note taking with Markdown support and optional Nextcloud Notes sync'
arch=(any)
url='https://apps.gnome.org/Iotas/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libsecret
  pango
  python
  python-gobject
  python-gtkspellcheck
  python-linkify-it-py
  python-markdown-it-py
  python-mdit_py_plugins
  python-packaging
  python-pypandoc
  python-requests
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
source=(
  "git+https://gitlab.gnome.org/World/iotas.git#tag=$pkgver"
  "https://github.com/KaTeX/KaTeX/releases/download/v$_katex_version/katex.tar.gz"
)
b2sums=('1cecaffcea754bf297e4c849097b59aec6e410ec2db1446cf9bcac7000b5f20b1d5fa5eaf0f2e08c5797209eee77eec4706db7da8b31b72b040d3923c01bc547'
        'bf574824d35e1c4cbacb0fbcc752934b4b99cec11921f84c3a2eb18941156ebcbc49e3f214c5f58af631cba74115d9c6df6bfc3086ecb4e382d167b637367e0d')

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Bundle KaTeX
  cd katex
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/" katex.min.css
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/js/" katex.min.js
  install -Dm644 -t "$pkgdir/usr/share/iotas/media/css/web/fonts/" fonts/*.woff2
}
