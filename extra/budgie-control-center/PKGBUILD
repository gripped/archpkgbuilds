# Maintainer: Campbell Jones <serebit@archlinux.org>

pkgname=budgie-control-center
pkgver=1.4.1
pkgrel=5
pkgdesc="Budgie's main interface to configure various aspects of the system"
arch=(x86_64)
url="https://github.com/BuddiesOfBudgie/budgie-control-center"
license=(GPL-2.0-or-later)
groups=(budgie)
depends=(
  accountsservice
  at-spi2-core
  cairo
  colord-gtk
  dconf
  fontconfig
  gcc-libs
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gnome-settings-daemon
  gsound
  gtk3
  hicolor-icon-theme
  krb5
  libcolord
  libcups
  libepoxy
  libgtop
  libgudev
  libhandy
  libibus
  libmm-glib
  libnm
  libnma
  libpulse
  libpwquality
  libsecret
  libwacom
  libx11
  libxi
  libxml2
  pango
  polkit
  smbclient
  udisks2
  upower
)
makedepends=(
  colord
  docbook-xsl
  git
  glib2-devel
  meson
  modemmanager
)
optdepends=(
  'gnome-user-share: WebDAV file sharing'
  'openssh: Remote login'
  'power-profiles-daemon: Power profiles support'
  'rygel: Media sharing'
  'system-config-printer: Printer settings'
)
source=("git+https://github.com/BuddiesOfBudgie/budgie-control-center.git#tag=v$pkgver")
b2sums=(df546ef6bddb9c120d7d31b4eb3bc339f096a42d555f10ab8bffc744b09adc4dbb1d169b9c4893aaa6790782474f52be44d99ec95ad0c08dbcda2b6726fe3508)
validpgpkeys=(
  1E1FB0017C998A8AE2C498A6C2EAA8A26ADC59EE # David Mohammed <fossfreedom at ubuntu dot com>
  0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B # Joshua Strobl <me at joshuastrobl dot com>
)

prepare() {
  cd $pkgname

  # Make building with bluetooth optional
  # https://github.com/BuddiesOfBudgie/budgie-control-center/pull/91
  git cherry-pick -n 810f8af92c94f77a045cbec0ed516d9b3bded2a6

  # Support dark mode
  # https://github.com/BuddiesOfBudgie/budgie-control-center/pull/98
  git cherry-pick -n 859fad15a43008b32020c8f445a7c2b490832fa1
}

build() {
  arch-meson $pkgname build \
    -D documentation=true \
    -D bluetooth=false \
    -D cheese=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
