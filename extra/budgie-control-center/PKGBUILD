# Maintainer: Campbell Jones <serebit@archlinux.org>

pkgname=budgie-control-center
pkgver=2.1.0
pkgrel=2
pkgdesc="Budgie's main interface to configure various aspects of the system"
arch=(x86_64)
url="https://github.com/BuddiesOfBudgie/budgie-control-center"
license=(GPL-2.0-or-later)
groups=(budgie)
depends=(
  accountsservice
  at-spi2-core
  cairo
  dconf
  fontconfig
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gnome-settings-daemon
  gsound
  gtk3
  hicolor-icon-theme
  krb5
  libcups
  libepoxy
  libgtop
  libgudev
  libhandy
  libibus
  libmm-glib
  libnm
  libnma
  libpulse
  libpwquality
  libsecret
  libwacom
  libx11
  libxi
  libxml2
  pango
  polkit
  smbclient
  udisks2
  upower
)
makedepends=(
  colord
  colord-gtk
  docbook-xsl
  git
  glib2-devel
  meson
  modemmanager
)
optdepends=(
  'gnome-user-share: WebDAV file sharing'
  'openssh: Remote login'
  'power-profiles-daemon: Power profiles support'
  'rygel: Media sharing'
  'system-config-printer: Printer settings'
)
source=("git+https://github.com/BuddiesOfBudgie/budgie-control-center.git#tag=v$pkgver")
b2sums=('b43f00cf91754e4c30ac8e59905448c3eceb7ad7fbadbed8801cfd36259cbca52591d7e00fb40eba7b2c04212d3a8ff496251841fe380683d4f0e8d3040a537e')
validpgpkeys=(
  1E1FB0017C998A8AE2C498A6C2EAA8A26ADC59EE # David Mohammed <fossfreedom at ubuntu dot com>
  0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B # Joshua Strobl <me at joshuastrobl dot com>
)

build() {
  arch-meson $pkgname build \
    -D documentation=true \
    -D bluetooth=false \
    -D cheese=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
