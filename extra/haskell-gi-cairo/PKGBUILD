# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=gi-cairo
pkgname=haskell-gi-cairo
pkgver=1.0.30
pkgrel=26
pkgdesc="Cairo bindings"
url="https://github.com/haskell-gi/haskell-gi"
license=('LGPL-2.1-only')
arch=('x86_64')
depends=('ghc-libs' 'cairo' 'haskell-gi' 'haskell-gi-base' 'haskell-gi-overloading')
makedepends=('ghc' 'gobject-introspection')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('6d2cc3d1c6f022fa2c6a4570db00141a8651c1a8586ecc1f19bb75e67088f6d7ea153f04e77508651c0aff28897aed90a5c0346e402b0686d0dba36446d2fc20')

build() {
  cd $_hkgname-$pkgver    

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
