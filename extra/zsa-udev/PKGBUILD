# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=zsa-udev
_commit=bceec979b283123da45502e236cf24afad753f2b
pkgver=2.1.3.r14.gbceec97
pkgrel=1
pkgdesc="Udev rules for ZSA keyboards"
arch=(any)
url="https://github.com/zsa/wally/"
license=(MIT)
makedepends=(git)
# NOTE: upstream never releases because reasons, so we need to use a commit to get to all current rules
# https://github.com/zsa/wally/tree/master/dist/linux64
source=("$pkgname::git+$url#commit=$_commit")
sha512sums=('b6409a120fc9dfc3b42f0b3e9b8254a63a98b2b3a04020cbcc12a44fc964471adc97f0ff337de4291d0b1092d6613820379252ac55901b3a63d30f636f8a1c56')
b2sums=('6ed49d945617e89213f071860344d89fcae87cc0479d7aaff887658f74729fbccc4015752dec2a0bd6fa773f30afc998b4cf7e864d0f023524becc12f3b6d37b')

pkgver() {
  cd $pkgname
  git describe --match '*-linux' --long --tags --abbrev=7 | sed 's/-linux//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  install -vDm 644 $pkgname/dist/linux64/*.rules -t "$pkgdir/usr/lib/udev/rules.d/"
  install -vDm 644 $pkgname/license.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
