# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Christopher Cooper <christopher@cg505.com>

pkgname=openai-codex
pkgver=0.79.0
pkgrel=1
pkgdesc='OpenAIs lightweight coding agent that runs in your terminal'
arch=(x86_64)
url='https://github.com/openai/codex'
license=(Apache-2.0)
depends=(
  dotslash
  openssl
  gcc-libs
  glibc
)
makedepends=(cargo)
checkdepends=(cargo-nextest git)
optdepends=(
  'git: allow for repository actions'
  'ripgrep: accelerated large-repo search'
)
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/rust-v$pkgver.tar.gz")
b2sums=('e910df872d8c1e7232daae883f7eef0ebf44140c9967ec32095ca90759811ec233898f839713b984a3b082d15e587af9961a46295860ce8b19170fd36a7830fe')

prepare() {
  cd codex-rust-v$pkgver/codex-rs

  cargo fetch --target "$(rustc --print host-tuple)"
}

build() {
  cd codex-rust-v$pkgver/codex-rs

  cargo build --frozen --release --bin codex --bin codex-responses-api-proxy
}

check() {
  cd codex-rust-v$pkgver/codex-rs

  cargo nextest run --all-features --no-fail-fast -- \
    --skip 'suite::user_agent::get_user_agent_returns_current_codex_user_agent' \
    --skip 'suite::send_message::test_send_message_raw_notifications_opt_in' \
    --skip 'suite::approvals::approval_matrix_covers_all_modes' \
    --skip 'suite::compact_resume_fork::compact_resume_and_fork_preserve_model_history_view' \
    --skip 'suite::unified_exec::unified_exec_streams_after_lagged_output' \
    --skip 'suite::sandbox::python_multiprocessing_lock_works_under_sandbox' \
    --skip 'suite::codex_tool::test_codex_tool_passes_base_instructions' \
    --skip 'suite::codex_tool::test_patch_approval_triggers_elicitation' \
    --skip 'suite::codex_tool::test_shell_command_approval_triggers_elicitation' \
    --skip 'suite::compact_resume_fork::compact_resume_after_second_compaction_preserves_history' \
    --skip 'chatwidget::tests::view_image_tool_call_adds_history_cell' \
    --skip 'diff_render::tests::ui_snapshot_apply_update_block_relativizes_path' \
    --skip 'status::tests::status_snapshot_includes_monthly_limit' \
    --skip 'status::tests::status_snapshot_shows_missing_limits_message' \
    --skip 'status::tests::status_snapshot_truncates_in_narrow_terminal' \
    --skip 'status::tests::status_snapshot_shows_empty_limits_message' \
    --skip 'status::tests::status_snapshot_shows_stale_limits_message' \
    --skip 'status::tests::status_snapshot_includes_reasoning_details' \
    --skip 'suite::compact::manual_compact_twice_preserves_latest_user_messages' \
    --skip 'tests::composer_input_renders_typed_characters' \
    --skip 'suite::view_image::user_turn_with_local_image_attaches_image' \
    --skip 'suite::view_image::view_image_tool_attaches_local_image' \
    --skip 'suite::user_notification::summarize_context_three_requests_and_instructions' \
    --skip 'suite::unified_exec::unified_exec_formats_large_output_summary' \
    --skip 'update_prompt::tests::update_prompt_snapshot' \
    --skip 'suite::tools::shell_timeout_handles_background_grandchild_stdout' \
    --skip 'status::tests::status_snapshot_cached_limits_hide_credits_without_flag' \
    --skip 'suite::compact::manual_compact_retries_after_context_window_error' \
    --skip 'suite::compact::multiple_auto_compact_per_task_runs_after_token_limit_hit' \
    --skip 'suite::compact::manual_compact_emits_estimated_token_usage_event' \
    --skip 'suite::truncation::tool_call_output_exceeds_limit_truncated_chars_limit' \
    --skip 'suite::truncation::tool_call_output_configured_limit_chars_type' \
    --skip 'suite::compact::auto_compact_triggers_after_function_call_over_95_percent_usage' \
    --skip 'suite::shell_serialization::shell_output_for_freeform_tool_records_duration::shellmodeloutput_shellcommand_expects' \
    --skip 'status::tests::status_snapshot_includes_credits_and_limits' \
    --skip 'suite::truncation::mcp_tool_call_output_exceeds_limit_truncated_for_model' \
    --skip 'suite::truncation::mcp_image_output_preserves_image_and_no_text_summary' \
    --skip 'suite::truncation::mcp_tool_call_output_not_truncated_with_custom_limit' \
    --skip 'suite::compact::manual_compact_uses_custom_prompt' \
    --skip 'suite::compact::auto_compact_runs_after_token_limit_hit'
}

package() {
  cd codex-rust-v$pkgver/codex-rs

  install -Dm755 -t "$pkgdir"/usr/bin \
    target/release/codex \
    target/release/codex-responses-api-proxy
}

# vim:set ts=2 sw=2 et:
