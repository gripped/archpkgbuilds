# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Sergey Kostyuchenko <derfenix@gmail.com>

pkgname=corectrl
pkgver=1.3.7
pkgrel=1
_tag_head_commit=dd0b8ce1e43c30e014cbaf0590422132acc0ce31
pkgdesc="Core control application"
url="https://gitlab.com/corectrl/corectrl"
license=('GPL3')
arch=('x86_64')
depends=(procps-ng hwdata quazip-qt5 qt5-charts qt5-quickcontrols2 qt5-base
         botan2 qt5-svg qt5-xmlpatterns qt5-base dbus polkit fmt pugixml hicolor-icon-theme)
makedepends=(cmake qt5-tools extra-cmake-modules ninja easyloggingpp)
checkdepends=(catch2 trompeloeil)
optdepends=(
	'vulkan-tools: For vulkaninfo'
	'mesa-utils: For glxinfo'
	'util-linux: For lscpu'
)
source=("https://gitlab.com/corectrl/corectrl/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2")
sha256sums=('18e883ddb394b97cc784069c78750ecffa96edb28d3814913db3e46e1863b162')
b2sums=('fe8231ba71948d17b1e5d598bf19cde0e51ec13a20849b7bb12fa4cc8401d671123a5de00705cddd141e1ed68c097a315d676df060e704c5f0c0cf18c3383963')

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "${pkgname}-v${pkgver}" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_INSTALL_LIBEXECDIR='lib' \
    -DBUILD_TESTING=ON \
    -Wno-dev
  cmake --build build --verbose
}

check () {
  CTEST_OUTPUT_ON_FAILURE=1 ninja -C build test
}

package() {
  depends+=(
    fmt libfmt.so
  )
  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ts=2 sw=2 et:
