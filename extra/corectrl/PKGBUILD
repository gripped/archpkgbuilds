# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Sergey Kostyuchenko <derfenix@gmail.com>

pkgname=corectrl
pkgver=1.5.1
pkgrel=3
pkgdesc="Core control application"
url="https://gitlab.com/corectrl/corectrl"
license=('GPL-3.0-or-later')
arch=('x86_64')
depends=(procps-ng hwdata quazip-qt6 qt6-charts qt6-base spdlog
         botan qt6-svg dbus polkit fmt pugixml hicolor-icon-theme)
makedepends=(cmake qt6-tools extra-cmake-modules ninja)
checkdepends=(catch2 trompeloeil)
optdepends=(
	'vulkan-tools: For vulkaninfo'
	'mesa-utils: For glxinfo'
	'util-linux: For lscpu'
)
source=("https://gitlab.com/corectrl/corectrl/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2")
sha256sums=('643f6c061ce7f73629229ec421a8958873b369bfa40ed83e52a260533c791ba8')
b2sums=('24669cbe159bcfa3ec8d6d8472c2e0f7144c4b26eae8262179e175d03ec5ab4ce5edc0cc9b602a0ac10ba105680831799452502d2d1c2a40c105ff45b92c00c1')

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "${pkgname}-v${pkgver}" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_INSTALL_LIBEXECDIR='lib' \
    -DBUILD_TESTING=ON \
    -Wno-dev
  cmake --build build --verbose
}

check () {
  CTEST_OUTPUT_ON_FAILURE=1 ninja -C build test
}

package() {
  depends+=(
    fmt libfmt.so
  )
  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ts=2 sw=2 et:
