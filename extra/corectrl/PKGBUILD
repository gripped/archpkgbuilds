# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Sergey Kostyuchenko <derfenix@gmail.com>

pkgname=corectrl
pkgver=1.3.10
pkgrel=1
_tag_head_commit=dd0b8ce1e43c30e014cbaf0590422132acc0ce31
pkgdesc="Core control application"
url="https://gitlab.com/corectrl/corectrl"
license=('GPL3')
arch=('x86_64')
depends=(procps-ng hwdata quazip-qt5 qt5-charts qt5-quickcontrols2 qt5-base
         botan2 qt5-svg qt5-xmlpatterns qt5-base dbus polkit fmt pugixml hicolor-icon-theme)
makedepends=(cmake qt5-tools extra-cmake-modules ninja easyloggingpp)
checkdepends=(catch2 trompeloeil)
optdepends=(
	'vulkan-tools: For vulkaninfo'
	'mesa-utils: For glxinfo'
	'util-linux: For lscpu'
)
source=("https://gitlab.com/corectrl/corectrl/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2"
        "0001-Migrate-to-catch2-v3.patch")
sha256sums=('cefa6c18f6f4267f8edbab56e669773c90968385f4afddd83f59479a3c0b7470'
            '66442a5870b65422232e4bcaca4de8212b1f53803b2da98e5737310865786e66')
b2sums=('0fb314a476e8a7cee672da031ea50ff7ed4e26dbdc0a579be2f39044b865a19a69e3b84e335cf432e85f640c58bc40c977f8fe2674be2349adde6d1a92c28beb'
        '56080519dbb141e71b8b91774be13573dfde8bbbf64dd7d630391e5adda566fd14596bbeaf62a29899b77212c06ec529a9183d82ef9c4e512e13e000915783d3')

prepare() {
  cd "${pkgname}-v${pkgver}"
  patch -Np1 < "${srcdir}/0001-Migrate-to-catch2-v3.patch" # https://gitlab.com/corectrl/corectrl/-/merge_requests/44
}

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "${pkgname}-v${pkgver}" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_INSTALL_LIBEXECDIR='lib' \
    -DBUILD_TESTING=ON \
    -Wno-dev
  cmake --build build --verbose
}

check () {
  CTEST_OUTPUT_ON_FAILURE=1 ninja -C build test
}

package() {
  depends+=(
    fmt libfmt.so
  )
  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ts=2 sw=2 et:
