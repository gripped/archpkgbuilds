# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iec16022
pkgver=0.3.1
pkgrel=3
pkgdesc='Produce 2D barcodes often also referenced as DataMatrix'
arch=(x86_64)
url='https://github.com/rdoeffinger/iec16022'
license=(GPL-2.0-or-later)
depends=(
  glibc
  popt
)
makedepends=(git)
source=("git+https://github.com/rdoeffinger/iec16022.git?signed#tag=v$pkgver")
b2sums=(043cc635226bf3ebef7732650beeec8d799e8b0ef28d9fcf37268c5d0bd7cc4591b457308bc771e894dfe0193da968d2abbd09bd3ab8c948c1d3200c7a65bbc0)
validpgpkeys=(C61D16E59E2CD10C895838A40899A2B906D4D9C7) # Reimar Döffinger

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
