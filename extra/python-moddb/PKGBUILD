# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@tutanota.com>

pkgname=python-moddb
pkgver=0.12.0
pkgrel=1
pkgdesc='A Python scraper to access ModDB mods, games and more as objects'
arch=(any)
url=https://github.com/ClementJ18/moddb
license=('MIT')
depends=(
  python-beautifulsoup4
  python-requests
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=40877f34959297f14060914ef4c0c325d3f45166
source=(git+https://github.com/ClementJ18/moddb.git#tag=${_tag})
b2sums=('73406a3df809195c984008bc7535de7bded66ff2ae7296c5f9682d5a5f72af21dfa2219105a2639d9ac5984b5be3a09526878e556268c95d7af0253d59ef3131')

pkgver() {
  cd moddb
  git describe --tags | sed 's/^v//'
}

build() {
  cd moddb
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" moddb/dist/*.whl
  install -Dm 644 moddb/LICENSE -t "${pkgdir}"/usr/share/licenses/python-moddb/
}

# vim: ts=2 sw=2 et:
