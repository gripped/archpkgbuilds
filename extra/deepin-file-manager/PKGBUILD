# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=6.0.29
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'deepin-anything'
         'deepin-util-dfm' 'disomaster' 'qt5-base' 'qt5-svg' 'qt5-x11extras' 'deepin-pdfium'
         'gio-qt' 'docparser' 'libsecret' 'zlib' 'dconf' 'openssl' 'glib2' 'udisks2-qt5' 'libxcb'
         'libqtxdg' 'libx11' 'qt5-multimedia' 'cryptsetup' 'libmediainfo' 'lucene++' 'avfs'
         'polkit-qt5' 'poppler' 'ffmpegthumbnailer' 'kcodecs' 'taglib' 'util-linux-libs' 'icu'
         'pcre' 'gcc-libs' 'deepin-appearance' 'cryfs' 'socat')
makedepends=('boost' 'cmake' 'ninja' 'qt5-tools' 'deepin-dock' 'deepin-movie' 'deepin-gettext-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'deepin-compressor: for compress/decompress')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7202d50d0a0e57b77b384ea8048257d84bab3218f6d229a58c698b7c66f3859f91834feb8e45df0221e5991d1fc46db1d30b2e9e287b4f714382e3c0cc81893b')

prepare() {
  cd dde-file-manager-$pkgver
  sed -i 's/-fPIE//' CMakeLists.txt
}

build() {
  # cmake confused with glob files, see https://github.com/linuxdeepin/developer-center/issues/5158
  cmake -GNinja -B build -S dde-file-manager-$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  cd build
  ninja
}

package() {
  cd build
  DESTDIR=$pkgdir ninja install
}
