# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=6.0.47
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
# deepin-appearance: org.deepin.dde.Appearance1
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'deepin-anything' 'deepin-util-dfm' 'qt5-base' 'qt5-svg'
         'qt5-x11extras' 'deepin-pdfium' 'docparser' 'libsecret' 'zlib' 'dconf' 'openssl' 'glib2'
         'libxcb' 'libx11' 'qt5-multimedia' 'cryptsetup' 'lucene++' 'avfs' 'gsettings-qt'
         'polkit-qt5' 'poppler' 'kcodecs5' 'taglib' 'util-linux-libs' 'icu' 'pcre' 'glibc' 'gcc-libs'
         'deepin-appearance' 'deepin-qt-dbus-factory' 'cryfs' 'socat')
makedepends=('git' 'boost' 'cmake' 'ninja' 'qt5-tools' 'deepin-tray-loader' 'deepin-movie'
             'deepin-gettext-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'deepin-compressor: for compress/decompress')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
# https://github.com/linuxdeepin/developer-center/issues/5725
options=('!lto')
source=("git+https://github.com/linuxdeepin/dde-file-manager.git#tag=$pkgver"
         taglib-2.patch)
sha512sums=('ba4db482461b5e6c6931e29bbc3426b91eaeb597566356a364efcd37e0f24b005b83391f9113d9815ea0e91a3f2a8e09acd4a5172df7ec21bd90251403966161'
            'd010d787b406fc66ccf8aa2f6544dcabf291e21969e94a2b77455084395b8fd1620d7c359bd915518be79fac685791b60d401b0e9e1ff6544a05cdb3e2791fd7')

prepare() {
  cd dde-file-manager
  # New dde-dock interface
  git cherry-pick -n b0125c3a11a7cd7a914473beb85c8e4e5c449906
  patch -p1 -i ../taglib-2.patch
}

build() {
  # cmake confused with glob files, see https://github.com/linuxdeepin/developer-center/issues/5158
  cmake -GNinja -B build -S dde-file-manager -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  cd build
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
