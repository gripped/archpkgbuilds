# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=6.0.55
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
# deepin-appearance: org.deepin.dde.Appearance1
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'deepin-anything' 'deepin-util-dfm' 'qt5-base' 'qt5-svg'
         'qt5-x11extras' 'deepin-pdfium' 'docparser' 'libsecret' 'zlib' 'dconf' 'openssl' 'glib2'
         'libxcb' 'libx11' 'qt5-multimedia' 'cryptsetup' 'lucene++' 'avfs' 'gsettings-qt'
         'polkit-qt5' 'poppler' 'kcodecs5' 'taglib' 'util-linux-libs' 'icu' 'pcre' 'glibc' 'gcc-libs'
         'deepin-appearance' 'deepin-qt-dbus-factory' 'cryfs' 'socat')
makedepends=('git' 'boost' 'cmake' 'ninja' 'qt5-tools' 'deepin-tray-loader' 'deepin-movie'
             'deepin-gettext-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'deepin-compressor: for compress/decompress')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
# https://github.com/linuxdeepin/developer-center/issues/5725
options=('!lto')
source=("git+https://github.com/linuxdeepin/dde-file-manager.git#tag=$pkgver"
         gcc-15.patch)
sha512sums=('6d2d53209298667a9db22025eeee7d056dc1a8e3f9afc578e7a4197519e3f9530440b3a96c94637056d3c6e1f82d777dbb3e83c966f95fe3c9937571958c67ce'
            'b2cda989438b90703a5718ac0daf5972f4d70e21ceef5ad9751445178c4299200f8ac3be112e79682bd37b751e6e3fbd3aaef4189adaaf9abe2506132c16ab41')

prepare() {
  cd dde-file-manager
  patch -p1 -i ../gcc-15.patch
}

build() {
  # cmake confused with glob files, see https://github.com/linuxdeepin/developer-center/issues/5158
  cmake -GNinja -B build -S dde-file-manager -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  cd build
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
