# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Gavin Lloyd <gavinhungry@gmail.com>
# Contributor: Timothy Rice <timothy.rice@unimelb.edu.au>

pkgname=datamash
pkgver=1.9
pkgrel=1
pkgdesc='Performs basic numeric, textual and statistical operations on input textual data files'
arch=('x86_64')
url='https://www.gnu.org/software/datamash'
license=('GPL-3.0-only')
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
sha512sums=('f8b081e6bc2579bdd753817aeaa09ea12e8c2bfa0a2b9a1071e10b402cf2a34f9cc76a7cadf5f1ecb9d88ea68e5aa99685496be8c4a41044e4e7e33a2105a57f'
            'SKIP')
b2sums=('a56f38b7006fe3c6fb2bda24ce3798d44c6d4cc41b6b4c5ca20c7e2fdfde046c60de9ee9a9ca559a733fe1364522a4cd260ceeb0f969de684bf267eaf3942865'
        'SKIP')
validpgpkeys=('33382C8D62017A1012A05B35BDB72EC3D3F87EE6') # Timothy Rice (Yubikey 5 Nano 13139911) <trice@posteo.net>

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr

  make
}

check() {
  cd "$pkgname-$pkgver"

  make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # ensure bash completions go to correct directory
  install -vd "$pkgdir/usr/share/bash-completion/completions"
  mv -v "$pkgdir/usr/share/datamash/bash-completion.d/$pkgname" \
    "$pkgdir/usr/share/bash-completion/completions"

  # move documentation to correct directory
  install -vd "$pkgdir/usr/share/doc"
  mv -v "$pkgdir/usr/share/$pkgname" "$pkgdir/usr/share/doc"

  # remove empty folder
  rm -vrf "$pkgdir/usr/share/datamash"
}
