# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=8.0.0
pkgrel=2
pkgdesc='Switchboard About Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  appstream
  fwupd
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libgtop
  libgudev
  libpackagekit-glib
  libswitchboard-3.so
  polkit
  switcheroo-control
  udisks2
)
makedepends=(
  git
  meson
  vala
)
_tag=2345b890c248794293a7609c72ae40ef97c104e1
source=(
  git+https://github.com/elementary/switchboard-plug-about.git#tag=${_tag}
  fwupd-2.0-compat.patch
)
b2sums=('19e2c8e71e5175049b7236191a2ffc67ab32349166881ed8e2447f7232aa8fd7d885d72172fcbffd424e545e15bb3249007063beb9a7e04fb2eae9a6122fd982'
        '5e61ccd97fd6fd5ead0afe1e1c528834e58c852d6f3bc4f74a2bd2d5e569c3e83d13b6ea7f1e1d36b995a233b5c26a7a86a30828e128ff92af9afd3783efefab')

pkgver() {
  cd switchboard-plug-about
  git describe --tags
}

prepare() {
  cd switchboard-plug-about
  patch -Np1 -i ../fwupd-2.0-compat.patch
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
