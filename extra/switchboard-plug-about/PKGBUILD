# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=8.2.1
pkgrel=1
pkgdesc='Switchboard About Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  appstream
  fwupd
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libgtop
  libgudev
  libpackagekit-glib
  libswitchboard-3.so
  polkit
  switcheroo-control
  udisks2
)
makedepends=(
  git
  meson
  vala
)
_tag=30ab29079f9cc9d6ee8b6cb47d3228424f784c6f
source=(
  git+https://github.com/elementary/switchboard-plug-about.git#tag=${_tag}
)
b2sums=('6fe368ee65c1ec5923aafc268aa98b7368213ec24f8ffe1da2a5ab1e059149f64940d6b3d073a837bd6f63eb692ba5da0e32e7243e270dcef25fc638cd5d540e')

pkgver() {
  cd switchboard-plug-about
  git describe --tags
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
