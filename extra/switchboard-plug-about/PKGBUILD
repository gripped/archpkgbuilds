# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=8.1.0
pkgrel=1
pkgdesc='Switchboard About Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  appstream
  fwupd
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libgtop
  libgudev
  libpackagekit-glib
  libswitchboard-3.so
  polkit
  switcheroo-control
  udisks2
)
makedepends=(
  git
  meson
  vala
)
_tag=f688445cf9ea4f08b061b0df9d5f8065239df050
source=(
  git+https://github.com/elementary/switchboard-plug-about.git#tag=${_tag}
)
b2sums=('9086d0f50515bca1e81714c1878620d009c7de88e2c10835ef39a523ecdb6bbfb934e46b165b97a43bb63fe9faee5b261270087560663588cb956f131ba8d350')

pkgver() {
  cd switchboard-plug-about
  git describe --tags
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
