# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=8.2.0
pkgrel=1
pkgdesc='Switchboard About Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  appstream
  fwupd
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libgtop
  libgudev
  libpackagekit-glib
  libswitchboard-3.so
  polkit
  switcheroo-control
  udisks2
)
makedepends=(
  git
  meson
  vala
)
_tag=ece172529e8c65b37a69ed0df03c1396e32ea113
source=(
  git+https://github.com/elementary/switchboard-plug-about.git#tag=${_tag}
)
b2sums=('a57c5fa9830496b67caef0e8d1b37b70433c7a05dbe8af91adcf276034953aebec3ed3fc674b225756e62727d6524842b82ad3436a3567b60ec3769d4155d365')

pkgver() {
  cd switchboard-plug-about
  git describe --tags
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
