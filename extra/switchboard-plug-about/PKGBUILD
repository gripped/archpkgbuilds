# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=8.2.2
pkgrel=1
pkgdesc='Switchboard About Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  appstream
  fwupd
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libgtop
  libgudev
  libpackagekit-glib
  libswitchboard-3.so
  polkit
  switcheroo-control
  udisks2
)
makedepends=(
  git
  meson
  vala
)
_tag=d969c3858b6b827a246bfa26687fc4c8f1e2a59f
source=(
  git+https://github.com/elementary/switchboard-plug-about.git#tag=${_tag}
)
b2sums=('8e609ae9248451a9e89a29b9240e89f23a4661c8b7ecbc884c6022c30731c28d3d4d0a97ae8848b07e13fd57b7e0571993cfe47c6904677d42961c6b17da647e')

pkgver() {
  cd switchboard-plug-about
  git describe --tags
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
