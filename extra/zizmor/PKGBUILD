# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=zizmor
pkgver=1.17.0
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/zizmorcore/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/zizmorcore/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('86dbd18ca6c5143b30ff7a50817a57aeb20c07835583bab74540541f5291160f')
b2sums=('21d53bb598bc32ee04aa4ee992d611d6319fe2db02670f406edce5b04fa5257647297f586cee4275a103b5b93a3e2bf7f40919d9f8e7658059ab8883c5909807')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help

  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/template-injection.yml | \
    grep -q 'code injection via template expansion'
  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/cache-poisoning.yml | \
    grep -q 'runtime artifacts potentially vulnerable to a cache poisoning attack'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
