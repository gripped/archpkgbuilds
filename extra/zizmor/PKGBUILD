# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=zizmor
pkgver=1.1.1
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/woodruffw/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/woodruffw/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('bc2a1b5e08a1b786029e8fab3591090b2b57a73070661d0749088840646ece24')
b2sums=('8fb988d9a02303568cd541de05160eb3b57c3a70dca46e8439ff0ffacf720ea53cc499f0741f3cd819888f079db3db86b58a8653737474cd87165d91fe36cd4b')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help
  cat > action.yaml <<YAML
on: push
jobs:
  vulnerable:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@v4
YAML
  target/release/zizmor --format plain ./action.yaml | grep -q 'does not set persist-credentials: false'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
