# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=zizmor
pkgver=1.3.0
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/woodruffw/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/woodruffw/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7fee5244723fd3da8b3211fc0edf1cdc269cfb7e36221c411e409a147b4da211')
b2sums=('d7b4f5b135ce61c6b888678473781eef68066d6457c17ccc4a07f3b632fe2ff777464bc405de0f283e0239a54a3bdfc2ce4fd44dfda4ff78810450372419b5e9')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help
  cat > action.yaml <<YAML
on: push
jobs:
  vulnerable:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@v4
YAML
  target/release/zizmor --format plain ./action.yaml | grep -q 'does not set persist-credentials: false'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
