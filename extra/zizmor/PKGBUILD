# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=zizmor
pkgver=1.3.1
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/woodruffw/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/woodruffw/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a8ed1b318de38ffa2e1847ae4b66cd44324030cd0fe7239e82430341b39c16ee')
b2sums=('f32f5676a9420b6ed7cba64d6e75f2c0eebb2d24830e18960d914800443db10fca5133c0b36f776363bb5fd2bca5192d99371809c9857d0a43eb53608189c1a7')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help
  cat > action.yaml <<YAML
on: push
jobs:
  vulnerable:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@v4
YAML
  target/release/zizmor --format plain ./action.yaml | grep -q 'does not set persist-credentials: false'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
