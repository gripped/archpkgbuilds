# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=zizmor
pkgver=1.16.3
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/zizmorcore/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/zizmorcore/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0953b0bd6016b929a743b18e693b9cf8e0f2766e53531a64134f6d762965e933')
b2sums=('47bd90e29b90335baf5d50bd182ecd05cfda0ced7299a9ada921e8289781fa29b6e46de1963a039c4a7fc1342ac4ef4563b38f23554ae4ac01a3d4c3c39b11c6')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help

  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/template-injection.yml | \
    grep -q 'code injection via template expansion'
  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/cache-poisoning.yml | \
    grep -q 'runtime artifacts potentially vulnerable to a cache poisoning attack'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
