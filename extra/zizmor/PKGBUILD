# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=zizmor
pkgver=1.5.2
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/woodruffw/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/woodruffw/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('9789dca47e36ac8c124be5856c38acfac6169839c7fb3a0ce492776e47f1d880')
b2sums=('25e3095f9d4ee2f94cf4c6a59568e00bf1114887d32ba630c535313428ecdb7ec8b5c02b3087e6ec12e2749be4820a763f27b56bdb00ddce9694a9f921a6ef5a')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help
  cat > action.yaml <<YAML
on: push
jobs:
  vulnerable:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@v4
YAML
  target/release/zizmor --format plain ./action.yaml | grep -q 'does not set persist-credentials: false'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
