# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=zizmor
pkgver=1.12.0
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/zizmorcore/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/zizmorcore/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ee296858017e88c142d46c7935c9911e9715fd8dd501583e823839ebb39bd711')
b2sums=('56dbed5dbeee0374608f64beb31606b5a155b640fa23f9151314e26c75f5c9c970b5a75b6433dba7fd97778892ffc9ec85f7ab323b378f7b78abfed2cde76912')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help

  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/template-injection.yml | \
    grep -q 'code injection via template expansion'
  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/cache-poisoning.yml | \
    grep -q 'runtime artifacts potentially vulnerable to a cache poisoning attack'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
