# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=zizmor
pkgver=1.18.0
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/zizmorcore/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/zizmorcore/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('17633af9cdf5ca6b5fd31468dfc3dce262b6ff85b244dfc397c484969bfba634')
b2sums=('487043fbb09aa587877b61a40240507e7ba21f837c823318205664c542adba65021451f4e2952a5f7b2183febe5f228413cb97e2bd689810118322d85f5012db')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help

  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/template-injection.yml | \
    grep -q 'code injection via template expansion'
  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/cache-poisoning.yml | \
    grep -q 'runtime artifacts potentially vulnerable to a cache poisoning attack'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
