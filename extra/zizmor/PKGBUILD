# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=zizmor
pkgver=1.8.0
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions"
url='https://github.com/zizmorcore/zizmor'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/zizmorcore/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6f5f4da30eb7e0fa4b7558a9418b58abd7c5ab467cb2dce330c8189a00668355')
b2sums=('ec4a7d1a85f9a437b4b41633d53fd067937edcb309f22153f52ade670bd0c87b2550dc3473ba42509ed0203d2af92c8dd45cf23132443234aa5f24676e436e57')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/zizmor --help

  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/template-injection.yml | \
    grep -q 'code injection via template expansion'
  target/release/zizmor --format plain crates/zizmor/tests/integration/test-data/cache-poisoning.yml | \
    grep -q 'runtime artifacts potentially vulnerable to a cache poisoning attack'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/zizmor

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
