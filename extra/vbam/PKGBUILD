# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgbase=vbam
pkgname=(
  vbam-sdl
  vbam-wx
)
pkgver=2.1.11
pkgrel=2
pkgdesc='Nintendo GameBoy Advance emulator'
arch=(x86_64)
url='https://visualboyadvance-m.org'
license=(GPL2)
depends=(
  glu
  libsfml-network.so
  libsfml-system.so
  libgl
  libpng
  sdl2
  sfml
  zlib
)
makedepends=(
  cmake
  ffmpeg
  git
  libglvnd
  ninja
  wxwidgets-gtk3
  zip
)
_pkgname=visualboyadvance-m
source=("git+https://github.com/${_pkgname}/${_pkgname}.git?signed#tag=v${pkgver}")
b2sums=('caa270b95f5fa1ea543d57d9ed4309b7bdb345b421d91c4fd1511b8918997339a3143333bad08ce754207ea38f4686d7f7b594b951ea1636a5e7a99a7db4790a')
validpgpkeys=('A0C0E526E36FD2138C149D4D08AB596679D86240' # Rafael Kitover <rkitover@gmail.com>
              '0662A706ABE516087A23D1B9F2E06F4EE438BE81' # Zach Bacon <zachbacon@vba-m.com>
)

prepare() {
  cd ${_pkgname}
}

build() {
  cmake -S ${_pkgname} -B build-sdl -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_SKIP_RPATH=TRUE \
    -DENABLE_SDL=TRUE \
    -DENABLE_WX=FALSE \
    -DENABLE_LINK=TRUE \
    -Wno-dev
  cmake --build build-sdl

  cmake -S ${_pkgname} -B build-wx -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_SKIP_RPATH=TRUE \
    -DENABLE_SDL=FALSE \
    -DENABLE_WX=TRUE \
    -DENABLE_FFMPEG=TRUE \
    -DENABLE_LINK=TRUE \
    -Wno-dev
  cmake --build build-wx
}

package_vbam-sdl() {
  backup=(etc/vbam.cfg)
  conflicts=(vbam-wx)

  DESTDIR="${pkgdir}" cmake --install build-sdl
}

package_vbam-wx() {
  depends+=(
    glib2
    gtk3
    libavcodec.so
    libavformat.so
    libavutil.so
    libswscale.so
    openal
    wxwidgets-gtk3
    zip
  )
  conflicts=(vbam-sdl)

  DESTDIR="${pkgdir}" cmake --install build-wx
}

# vim: ts=2 sw=2 et:
