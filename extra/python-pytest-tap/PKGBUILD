# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=python-pytest-tap
_pyname=${pkgname#python-}
pkgver=3.5
pkgrel=2
pkgdesc="Test Anything Protocol (TAP) reporting plugin for pytest."
arch=(any)
url="https://github.com/python-tap/pytest-tap"
license=('BSD')
depends=('python-pytest' 'python-tappy')
makedepends=('python-setuptools' 'python-build' 'python-hatchling' 'python-installer' 'python-wheel')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/python-tap/pytest-tap/archive/v${pkgver}.tar.gz)
sha256sums=('cdae1ed711a4287ab322a2664cedb00a11069553ea3c0a302129bedb0cde9706')

prepare() {
	cd "${srcdir}/${_pyname}-${pkgver}"

	# Let Hatch auto-discover packages.
	sed -i '/^packages =/d' pyproject.toml
}

build() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	python -m build --wheel --no-isolation
}

check() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
	python -m installer --destdir="${srcdir}/tmp_install" dist/*.whl
	PYTHONPATH="${srcdir}/tmp_install/usr/lib/python${python_version}/site-packages" pytest
}

package() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	python -m installer --destdir="$pkgdir" dist/*.whl
}
