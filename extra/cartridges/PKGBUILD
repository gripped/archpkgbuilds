# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=cartridges
pkgver=2.11
pkgrel=1
pkgdesc='Simple game launcher written in Python using GTK4 and Libadwaita'
arch=(any)
url='https://apps.gnome.org/Cartridges/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  python
  python-gobject
  python-pillow
  python-requests
  python-urllib3
  python-yaml
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/kra-mo/cartridges.git#tag=v$pkgver"
  cartridges-save-cover.patch
)
b2sums=(
  b52db871c404bda813798ce10af1c58263a11eb8eb866b2c0823601d8e11556f57d81f64cbcfa8230ce186c16c894bda86e7e909945cf1ee1c56e53da05fbc56
  d0f7ec28c561cc3d6b92943884bc6c467fac642d0f4950e0954061f027d4ecac691436dc8e4d9f863fa7318a9007eafda94cde30b72cf8e8871dfa154b97e299
)

prepare() {
  cd $pkgname

  # https://github.com/kra-mo/cartridges/pull/306
  git apply -3 ../cartridges-save-cover.patch
}

build() {
  arch-meson $pkgname build \
    -D tiff_compression=jpeg
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
