# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=cartridges
pkgver=2.10.1
pkgrel=2
pkgdesc='Simple game launcher written in Python using GTK4 and Libadwaita'
arch=(any)
url='https://apps.gnome.org/Cartridges/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  python
  python-gobject
  python-pillow
  python-yaml
  python-requests
  python-urllib3
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/kra-mo/cartridges.git#tag=v$pkgver"
  cartridges-save-cover.patch
  cartridges-get-blurred.patch
  cartridges-image-filter.patch
  cartridges-animation-draw.patch
)
b2sums=(
  4c205efcd99df0cc94e2b2f6937e72432d26d03c4bb509682288d7e82b34de68d2821376818b04aadbcc4eb000e4230881de29ef025ec952619c3c3a6e904274
  d0f7ec28c561cc3d6b92943884bc6c467fac642d0f4950e0954061f027d4ecac691436dc8e4d9f863fa7318a9007eafda94cde30b72cf8e8871dfa154b97e299
  f5e6158fed9d902cee5c4848b4d8f3b745ebacb9f269feda7a5d62d5aa6c14848971d6c6f576beabb538976dce4d2babf1d83640793b36e28e844a4bd358dadd
  f3a6b3fbedc240aafb559526da23ea70b4ca1586364c3918bdc39cacdc876285339e67c247b5ee37420a22058c372ff28c8a9e017a559a648b37cb20d49c2724
  13f91e71aed13eecc6a3b419ed072903597aa04e5611f32de0f7953063436833980090b57d858d105fef979a71d82009492c6fe9833db8a9da564832d3a7afd5
)

prepare() {
  cd $pkgname

  # https://github.com/kra-mo/cartridges/pull/306
  git apply -3 ../cartridges-save-cover.patch

  # https://github.com/kra-mo/cartridges/pull/307
  git apply -3 ../cartridges-get-blurred.patch

  # https://github.com/kra-mo/cartridges/pull/308
  git apply -3 ../cartridges-image-filter.patch

  # https://github.com/kra-mo/cartridges/pull/309
  git apply -3 ../cartridges-animation-draw.patch
}

build() {
  arch-meson $pkgname build \
    -D tiff_compression=jpeg
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
