# Maintainer: Maxime Gauduin <aiucryd@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: fanyujun <dlutfyj@outlook.com>

pkgname=python-lap
epoch=1
pkgver=0.5.13
pkgrel=1
pkgdesc='Linear Assignment Problem solver using Jonker-Volgenant algorithm (LAPJV/LAPMOD)'
arch=(x86_64)
url=https://github.com/gatagat/lap
license=(BSD-2-Clause)
depends=(
  glibc
  python
  python-numpy
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("$pkgname::git+https://github.com/gatagat/lap.git#tag=v$pkgver")
sha512sums=('811880be016335fcf82d45183561243960cf3457e3ed88f0dadd589d2d5af02091687fe1df1601692b2547c95a22cac3b424369bcb6595bc76b4bf802a74cf0e')
b2sums=('3a032257120cb8e6ae71de2f32f9057942b579f5993aebe7a457a929bb69cef5a32161414f19ee482419d964c04cb962ebc27c6afb71dcda1fcb7d9532ea7b7b')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim: ts=2 sw=2 et:
