# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=mailutils
pkgver=3.19
pkgrel=1
pkgdesc="MUA command line tool (mailx)"
arch=(x86_64)
url="https://www.gnu.org/software/mailutils/"
license=('GPL')
depends=('libldap' 'pam' 'gnutls' 'gsasl')
makedepends=('emacs')
options=(!emptydirs)
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732'
	      '4BE4E62655488EB92ABB468F79FFD94BFCE230B1')
source=(https://ftp.gnu.org/gnu/mailutils/mailutils-$pkgver.tar.bz2{,.sig}
        fix-build-mb-len-max.patch)
sha512sums=('dce9577a9de07bdef6e4285e230e04240bbd5c93d9c9c3be6b478aa9cc6d12c1977be4bddeb59eb13f0e394894e33f172486c9ff70fee2dc972beb8925980c5e'
            'SKIP'
            '48248bfae1cc3b80c3decfcb99f803b65fd0d068f6c09625eb5e3763b3184090d7bd97fa3bc916b3e2e8c737b2bd501aa25482a96aceb555c08307b54c2a24a8')

prepare() {
  cd "$srcdir/mailutils-$pkgver"
  # Include limits.h in frm/frm.h so it doesn't misdefine MB_LEN_MAX to 4
  # (patch from github.com/NixOS/nixpkgs)
#  patch -Np1 -i ../fix-build-mb-len-max.patch
}

build() {
  cd "$srcdir/mailutils-$pkgver"
  unset CFLAGS
  unset CXXFLAGS
  ./configure \
	--prefix=/usr \
	--sbindir=/usr/bin \
	--libexecdir=/usr/lib/$pkgname \
	--sysconfdir=/etc \
	--with-gdbm \
	--with-gnutls \
	--disable-python \
	--without-guile
#  make CFLAGS="$CFLAGS -O2 -Wno-error" || make CFLAGS="$CFLAGS -O2 -Wno-error" LDFLAGS="-L`pwd`/lib -lmutcpwrap -ltinfo"
  make
}

package() {
  cd "$srcdir/mailutils-$pkgver"
  make -j1 DESTDIR="$pkgdir" install
  rm -f "$pkgdir"/usr/share/info/dir
  mv "$pkgdir"/usr/bin/mail "$pkgdir"/usr/bin/gnu-mail
  mv "$pkgdir"/usr/share/man/man1/mail.1 "$pkgdir"/usr/share/man/man1/gnu-mail.1
  chown -R root:root "$pkgdir"
}
