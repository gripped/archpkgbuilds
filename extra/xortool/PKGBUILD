# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>

pkgname=xortool
pkgver=1.1.0
pkgrel=1
pkgdesc='Tool to analyze multi-byte XOR cipher'
url='https://github.com/hellman/xortool'
arch=(any)
license=(MIT)
depends=(
  python
  python-docopt
  python-importlib-metadata
)
makedepends=(
  python-build
  python-installer
  python-poetry-core
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('60f93b5ad7ba9416b179e45ce7b682cf3e2e8cef19d787c069a7c03d25e8afe4abb6846358b38ebedf263de92266cd34c6515412564b319d3d9486404803399f')
b2sums=('fe9d050200deb3fc0bfd76bc25c0b332fb3cf8a5cc951ca22697d892320c700810b2fd447c4a56a162df7ee6eed4214735637010361aad1244c430f76589480c')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}
  export LANG=en_US.UTF-8
  export LC_CTYPE=en_US.UTF-8
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" \
  PYTHONPATH="$PWD" \
  ./test/test.sh
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
