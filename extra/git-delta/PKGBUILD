# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: ccorn

pkgname=git-delta
_pkgname=delta
pkgver=0.18.1
pkgrel=1
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=(x86_64)
url='https://dandavison.github.io/delta/'
_url='https://github.com/dandavison/delta'
license=(MIT)
depends=(gcc-libs # libgcc_s.so
         glibc # libc.so libm.so
         libgit2
         oniguruma # libonig.so
         zlib)
makedepends=(cargo
             mdbook)
checkdepends=(git)
source=("$_url/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('545d753b5e9981d87f356e123d3e7f99340adbca5b963e67a2fa5291cc4e6a175462554c50733ad30931e39e7e615bcd0cfb1490c26968b12fe172edddf352e7')
b2sums=('1ee2f6215a2e7db7c0388fceff3e8eee6371a8946a8a100552ba3b0850fcdee2b45b6c1e12dd9902674c92816ca55247f4884ba849d96087214c03feb30f880f')

prepare() {
  cd "$_pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"

  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=' -ffat-lto-objects'

  export RUSTONIG_DYNAMIC_LIBONIG=1

  # binary
  cargo build --frozen --release --all-features

  # html manual
  cd manual
  mdbook build --dest-dir html
}

check() {
  cd "$_pkgname-$pkgver"
  # some tests hang for 60+ seconds, find out why
  # took my machine ~ 470 seconds
  cargo test --frozen --all-features
}

package() {
  depends+=(libgit2.so
            libz.so)
  cd "$_pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"
  install -vDm644 etc/completion/completion.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/$_pkgname.fish"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # html manual
  cp -r manual/html "$pkgdir/usr/share/doc/$pkgname"
}
