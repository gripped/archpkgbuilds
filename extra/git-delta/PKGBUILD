# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: ccorn

pkgname=git-delta
_pkgname=delta
pkgver=0.18.2
pkgrel=7
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=(x86_64)
url='https://dandavison.github.io/delta/'
license=(MIT)
depends=(glibc # libc.so libm.so
         libgcc libgcc_s.so
         libgit2
         oniguruma # libonig.so
         zlib)
makedepends=(cargo
             mdbook)
checkdepends=(git)
source=("https://github.com/dandavison/delta/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('c096e22e07945f004f4ef8e805d6dfe45ae53a2e9bcb46736f9764d76fcfa556ce1622157ab66021b8fa36a1e322f80fee217e7f3d9d3489ec4df1f8fc61ca67')
b2sums=('6a1dc650de50e495b0d1ced9369518a19d2b92270e58d9a1c93edd4b61cc5565bb23f5b6d69a250178e8f2ba13169dda700450539eafcc6f042ef35c03d19ed2')

prepare() {
  cd "$_pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$_pkgname-$pkgver"

  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=' -ffat-lto-objects'

  export RUSTONIG_DYNAMIC_LIBONIG=1

  # binary
  cargo build --frozen --release --all-features

  # html manual
  cd manual
  # mdbook upgrade broke doc generation here, this projects docs are full of errors
  # mdbook build --dest-dir html
}

check() {
  cd "$_pkgname-$pkgver"
  # some tests hang for 60+ seconds, find out why
  # took my machine ~ 470 seconds
  cargo test --frozen --all-features
}

package() {
  depends+=(libgit2.so
            libz.so)
  cd "$_pkgname-$pkgver"

  # binary
  local _bin=target/release/delta
  install -vDm755 -t "$pkgdir/usr/bin/" $_bin

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell completions
  install -vDm644 <($_bin --generate-completion=bash) \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 <($_bin --generate-completion=elvish) \
    "$pkgdir/usr/share/elvish/lib/$_pkgname.elv"
  install -vDm644 <($_bin --generate-completion=fish) \
    "$pkgdir/usr/share/fish/vendor_completions.d/$_pkgname.fish"
  install -vDm644 <($_bin --generate-completion=zsh) \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # html manual
  # cp -r manual/html "$pkgdir/usr/share/doc/$pkgname"
}
