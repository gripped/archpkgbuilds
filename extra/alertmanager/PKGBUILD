# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=alertmanager
pkgver=0.31.1
pkgrel=1
pkgdesc="Service which handles alerts sent by client applications such as the Prometheus server"
url="https://github.com/prometheus/alertmanager"
arch=(x86_64)
depends=(glibc)
license=('Apache-2.0')
makedepends=(go git)
options=(!lto)
backup=('etc/alertmanager/alertmanager.yml' 'etc/conf.d/alertmanager')
source=($pkgname-$pkgver.tar.gz::https://github.com/prometheus/alertmanager/archive/v${pkgver}.tar.gz
        alertmanager.service
        alertmanager.sysusers
        alertmanager.conf)
sha512sums=('f73a7bc70bf3eed50e727a11538a762c48fd4e1af444359f61a3b444532fd9b3107fa6b7b8abb8498adbc2b6368c39769091e6fe9196399967a24f9ef91c0b30'
            '15b2a91afe77d89accda5f5d42ef7a0fb07807a453c393003e346d9747a57d3d45e0f24a2ae1068e18b191be9e890bd6a8083d51d27cf10390a54173c05a9a08'
            '469f321f40b0dd6e1cc6d0791032c476449bb2ab2364d57b06d0e0309d09710be8751ded64d84e29dd6e28e96b71ef69e2bee6c71282500a9074a9d7ada8bdf0'
            '505c42b175be804961215cd5581e984464c21348c82f0ba6490fc58e21a6b8d8b3e1db29a95807316d7060132f3e130ae7d604a7b60fd6f15bad55bda6a45f2e')

build() {
  cd $pkgname-$pkgver
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-extldflags \"${LDFLAGS}\" \
      -X github.com/prometheus/common/version.Version=$pkgver \
      -X github.com/prometheus/common/version.Revision=$pkgver \
      -X github.com/prometheus/common/version.Branch=tarball \
      -X github.com/prometheus/common/version.BuildUser=someone@builder \
      -X github.com/prometheus/common/version.BuildDate=$(date -u '+%Y%m%d-%H:%M:%S' --date=@${SOURCE_DATE_EPOCH})" \
    ./cmd/alertmanager

  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-extldflags \"${LDFLAGS}\" \
      -X github.com/prometheus/common/version.Version=$pkgver \
      -X github.com/prometheus/common/version.Revision=$pkgver \
      -X github.com/prometheus/common/version.Branch=tarball \
      -X github.com/prometheus/common/version.BuildUser=someone@builder \
      -X github.com/prometheus/common/version.BuildDate=$(date -u '+%Y%m%d-%H:%M:%S' --date=@${SOURCE_DATE_EPOCH})" \
    ./cmd/amtool
}

check() {
  cd $pkgname-$pkgver
  go test ./...
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm755 amtool "$pkgdir/usr/bin/amtool"

  install -Dm644 examples/ha/${pkgname}.yml "${pkgdir}/etc/${pkgname}/${pkgname}.yml"
  install -d -m755 "${pkgdir}/etc/${pkgname}/template/"

  install -d -m750 -o 211 -g 211 "${pkgdir}/var/lib/${pkgname}"

  install -Dm0644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "$srcdir/alertmanager.sysusers" "$pkgdir/usr/lib/sysusers.d/alertmanager.conf"
  install -Dm644 "$srcdir/alertmanager.conf" "$pkgdir/etc/conf.d/alertmanager"
}
