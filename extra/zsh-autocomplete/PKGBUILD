# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=zsh-autocomplete
pkgver=25.03.19
pkgrel=1
pkgdesc='Real-time type-ahead completion for Zsh'
arch=(any)
url=https://github.com/marlonrichert/zsh-autocomplete
license=(MIT)
depends=(zsh)
makedepends=(git)
checkdepends=(clitest)
source=("git+$url.git#tag=$pkgver")
b2sums=('8df7e2ace958637ef73198214c3197589f8ddb2e735986fec6954a3224f5644a56eb2ab750c29e971d35cb6e632d07ea2210df27f3acd9a3c3aaa2711f5c0f17')

check() {
  cd $pkgname
  ./run-tests.zsh
}

package() {
  cd $pkgname
  install -d "$pkgdir"/usr/share/zsh/plugins/$pkgname
  cp -r Completions Functions zsh-autocomplete.plugin.zsh \
    "$pkgdir"/usr/share/zsh/plugins/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
