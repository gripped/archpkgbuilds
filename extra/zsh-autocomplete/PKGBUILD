# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=zsh-autocomplete
pkgver=24.09.04
pkgrel=2
pkgdesc='Real-time type-ahead completion for Zsh'
arch=(any)
url=https://github.com/marlonrichert/zsh-autocomplete
license=(MIT)
depends=(zsh)
makedepends=(git)
checkdepends=(clitest)
source=("git+$url.git#tag=$pkgver")
b2sums=('2600b999789abe876cfc0cfdb8a9e513e19400300dea084cd6c86a02b5994dd645e73949bd5bd8020648f5212a3923fe640ef6d1c253da0e145644f974e4e5a3')

check() {
  cd $pkgname
  ./run-tests.zsh
}

package() {
  cd $pkgname
  install -d "$pkgdir"/usr/share/zsh/plugins/$pkgname
  cp -r Completions Functions zsh-autocomplete.plugin.zsh \
    "$pkgdir"/usr/share/zsh/plugins/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
