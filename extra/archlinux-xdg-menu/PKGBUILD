# Maintainer: Sergej Pupykin <sergej@aur.archlinux.org>
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>

pkgname=archlinux-xdg-menu
pkgver=0.7.6.8
pkgrel=1
pkgdesc="automatic generate WM menu from xdg files"
arch=('any')
url="https://wiki.archlinux.org/index.php/XdgMenu"
license=("GPL")
depends=('perl' 'perl-xml-parser')
#optdepends=('sux: for xdg_menu_su')
backup=("etc/update-menus.conf"
	"etc/xdg/menus/arch-applications.menu")
source=("https://arch.p5n.pp.ru/~sergej/dl/2025/arch-xdg-menu-$pkgver.tar.gz")
sha256sums=('f86486c14a0bc1bb988b0cb837685f3683dc34cbb246525576bd5a9e357ee177')

package() {
  cd "$srcdir"
  install -D -m 0755 xdg_menu "$pkgdir"/usr/bin/xdg_menu
  install -D -m 0755 xdg_menu_su "$pkgdir"/usr/bin/xdg_menu_su
  install -D -m 0755 update-menus "$pkgdir"/usr/bin/update-menus
  install -D -m 0644 update-menus.conf "$pkgdir"/etc/update-menus.conf
  mkdir -p "$pkgdir"/usr/share/desktop-directories/
  cp arch-desktop-directories/* "$pkgdir"/usr/share/desktop-directories/
  mkdir -p "$pkgdir"/etc/xdg/menus/
  cp arch-xdg-menu/* "$pkgdir"/etc/xdg/menus/
  mkdir -p "$pkgdir"//var/cache/xdg-menu
}
