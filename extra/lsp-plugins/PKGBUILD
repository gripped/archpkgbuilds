# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=lsp-plugins
pkgname=(lsp-plugins{,-{clap,docs,gst,ladspa,lv2,standalone,vst,vst3}} liblsp-r3d-glx-lib)
pkgver=1.2.20
pkgrel=1
pkgdesc="Collection of open-source plugins"
arch=(x86_64)
url="https://lsp-plug.in"
license=(LGPL-3.0-or-later)
makedepends=(
  cairo
  gcc-libs
  glibc
  gstreamer
  gst-plugins-base
  hicolor-icon-theme
  libglvnd
  libx11
  libxrandr
  jack
  ladspa
  libsndfile
  lv2
  php
)
checkdepends=(
  jq
  lv2lint
)
source=(https://github.com/sadko4u/$pkgname/releases/download/$pkgver/$pkgname-src-$pkgver.tar.gz
        https://github.com/lsp-plugins/lsp-dsp-lib/commit/bf4a8bed.patch)
sha512sums=('da9ca4f28863dd7c51d345a8ad6db3e5de7b9ab367b07f4d022c82a8a4a08101ce709ee5f8ae49cb5315c6c07726d644a2cbf394b860ad9b04bb9b9463c99039'
            '6b5c6006bc728dfea866d877ab56fb701becb9835aaeffad09f6177285c3605468c9e119951a5efa08562aa82ad68e41dd9b33f24da5368fadc57dc0fe71dd78')
b2sums=('500f3a9cda9bef95823a7eaf892dcada0ee36db148e6de3eabc34265116a1bc8592c5547ef46e214458665acdef291cb018f5ba90cd1ea1e26f9ea97631d22fb'
        '081ca3e9bd980dd6b1d83557ebbebbf1e137b9000dc15efc090360d38e7a55ff5b71af2dd16138db527051f882f30fd37815c72c02330dcceef4c0343bb74289')
_dir=$pkgbase

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  patch -d lsp-plugins/modules/lsp-dsp-lib -p1 < bf4a8bed.patch # Fix build
}

build() {
  local config_options=(
    config
    VERBOSE=1
    ARTIFACT_EXPORT_HEADERS=1
    FEATURES='clap doc gst jack ladspa lv2 ui vst2 vst3 xdg'
    PREFIX=/usr
    -C $_dir
  )

  make "${config_options[@]}"
  make VERBOSE=1 -C $_dir
}

check() {
  local _plugin
  local _lv2_uri="http://lsp-plug.in/plugins/lv2/"

  # install to temporary location
  make FEATURES=lv2 DESTDIR="$PWD/test" install -C $_dir

  for _plugin in $(jq -r '.plugins[].id' $_dir/.build/target/lsp-plugin-fw/plugins.json); do
    printf "Testing plugin %s\n" "$_plugin"
    lv2lint -Mpack -I "$PWD/test/usr/lib/lv2/$pkgname.lv2/" $_lv2_uri$_plugin
  done
}

package_lsp-plugins() {
  depends=($pkgbase-{clap,gst,ladspa,lv2,standalone,vst,vst3})
  optdepends=(
    'lsp-plugins-docs: for documentation'
  )

  make PREFIX=/usr DESTDIR="$pkgdir" install -C $_dir
  printf "successfully installed to pkgdir\n"

  (
    cd "$pkgdir"
    _pick liblsp-r3d-glx-lib usr/include/*
    _pick liblsp-r3d-glx-lib usr/lib/liblsp*
    _pick liblsp-r3d-glx-lib usr/lib/pkgconfig/*
    _pick $pkgbase-clap usr/lib/clap/*
    _pick $pkgbase-docs usr/share/doc/*
    _pick $pkgbase-gst usr/lib/gstreamer-1.0/*
    _pick $pkgbase-gst usr/lib/lsp-plugins/liblsp-plugins-gstreamer-*.so
    _pick $pkgbase-ladspa usr/lib/ladspa/*
    _pick $pkgbase-lv2 usr/lib/lv2/*
    _pick $pkgbase-standalone etc/*
    _pick $pkgbase-standalone usr/bin/*
    _pick $pkgbase-standalone usr/lib/$pkgbase/*
    _pick $pkgbase-standalone usr/share/applications/*
    _pick $pkgbase-standalone usr/share/desktop-directories/*
    _pick $pkgbase-standalone usr/share/icons/*
    _pick $pkgbase-vst usr/lib/vst/*
    _pick $pkgbase-vst3 usr/lib/vst3/*
  )
}

package_liblsp-r3d-glx-lib() {
  pkgdesc="GLX-based 3D backend library for rendering scenes"
  depends=(
    gcc-libs
    glibc
    libglvnd libGL.so
    libx11
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-clap() {
  pkgdesc+=" - CLAP"
  groups=(clap-plugins)
  depends=(
    cairo
    clap-host
    freetype2
    gcc-libs
    glibc
    liblsp-r3d-glx-lib=$pkgver
    libsndfile libsndfile.so
    libx11
    libxrandr
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-docs() {
  pkgdesc+=" (documentation)"

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-gst() {
  pkgdesc+=" - GStreamer"
  groups=(gstreamer-plugins)
  depends=(
    gcc-libs
    glib2 libglib-2.0.so libgobject-2.0.so
    glibc
    gstreamer
    gst-plugins-base-libs
    liblsp-r3d-glx-lib=$pkgver
    libsndfile libsndfile.so
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-ladspa() {
  pkgdesc+=" - LADSPA"
  groups=(ladspa-plugins)
  depends=(
    gcc-libs
    glibc
    ladspa-host
    liblsp-r3d-glx-lib=$pkgver
    libsndfile libsndfile.so
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-lv2() {
  pkgdesc+=" - LV2"
  groups=(lv2-plugins)
  depends=(
    cairo
    freetype2
    gcc-libs
    glibc
    lv2-host
    liblsp-r3d-glx-lib=$pkgver
    libsndfile libsndfile.so
    libx11
    libxrandr
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-standalone() {
  pkgdesc+=" - JACK standalone"
  groups=(pro-audio)
  depends=(
    cairo
    freetype2
    gcc-libs
    glibc
    hicolor-icon-theme
    jack libjack.so
    liblsp-r3d-glx-lib=$pkgver
    libsndfile libsndfile.so
    libx11
    libxrandr
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-vst() {
  pkgdesc+=" - VST2"
  groups=(vst-plugins)
  depends=(
    cairo
    freetype2
    gcc-libs
    glibc
    liblsp-r3d-glx-lib=$pkgver
    libsndfile libsndfile.so
    libx11
    libxrandr
    vst-host
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-vst3() {
  pkgdesc+=" - VST3"
  groups=(vst3-plugins)
  depends=(
    cairo
    freetype2
    gcc-libs
    glibc
    liblsp-r3d-glx-lib=$pkgver
    libsndfile libsndfile.so
    libx11
    libxrandr
    vst3-host
  )

  mv -v $pkgname/* "$pkgdir"
}
