# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=lsp-plugins
pkgname=(lsp-plugins{,-{clap,docs,gst,ladspa,lv2,standalone,vst,vst3}} liblsp-r3d-glx-lib)
pkgver=1.2.26
pkgrel=1
pkgdesc="Collection of open-source plugins"
arch=(x86_64)
url="https://lsp-plug.in"
license=(LGPL-3.0-or-later)
makedepends=(
  cairo
  gcc-libs
  glibc
  gstreamer
  gst-plugins-base
  hicolor-icon-theme
  libglvnd
  libx11
  libxrandr
  jack
  ladspa
  libsndfile
  lv2
  php
)
checkdepends=(
  jq
  lv2lint
)
source=(https://github.com/sadko4u/$pkgname/releases/download/$pkgver/$pkgname-src-$pkgver.tar.gz)
sha512sums=('098bab0fa246805d307f4fade5b48b311e0d85b0dc553133e4e3979c555c912f0ea80b8464dbfde095d976c531355f8dc64b28bf900a14a09f61cb1012cf02f7')
b2sums=('dbb57d60ecc407dad6fe230fe5cd2f423f61e84c2217b6f553767b69a5b6f938ccc645fa774d35a05f500d54f667190108398ab3befa122597abfbc7e649bbc6')
_dir=$pkgbase

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local config_options=(
    config
    VERBOSE=1
    ARTIFACT_EXPORT_HEADERS=1
    FEATURES='clap doc gst jack ladspa lv2 ui vst2 vst3 xdg'
    PREFIX=/usr
    -C $_dir
  )

  make "${config_options[@]}"
  make VERBOSE=1 -C $_dir
}

check() {
  local _plugin
  local _lv2_uri="http://lsp-plug.in/plugins/lv2/"

  # install to temporary location
  make FEATURES=lv2 DESTDIR="$PWD/test" install -C $_dir

  for _plugin in $(jq -r '.plugins[].id' $_dir/.build/target/lsp-plugin-fw/plugins.json); do
    printf "Testing plugin %s\n" "$_plugin"
    lv2lint -Mpack -I "$PWD/test/usr/lib/lv2/$pkgname.lv2/" $_lv2_uri$_plugin
  done
}

package_lsp-plugins() {
  depends=($pkgbase-{clap,gst,ladspa,lv2,standalone,vst,vst3})
  optdepends=(
    'lsp-plugins-docs: for documentation'
  )

  make PREFIX=/usr DESTDIR="$pkgdir" install -C $_dir
  printf "successfully installed to pkgdir\n"

  (
    cd "$pkgdir"
    _pick liblsp-r3d-glx-lib usr/include/*
    _pick liblsp-r3d-glx-lib usr/lib/liblsp*
    _pick liblsp-r3d-glx-lib usr/lib/pkgconfig/*
    _pick $pkgbase-clap usr/lib/clap/*
    _pick $pkgbase-docs usr/share/doc/*
    _pick $pkgbase-gst usr/lib/gstreamer-1.0/*
    _pick $pkgbase-gst usr/lib/lsp-plugins/liblsp-plugins-gstreamer-*.so
    _pick $pkgbase-ladspa usr/lib/ladspa/*
    _pick $pkgbase-lv2 usr/lib/lv2/*
    _pick $pkgbase-standalone etc/*
    _pick $pkgbase-standalone usr/bin/*
    _pick $pkgbase-standalone usr/lib/$pkgbase/*
    _pick $pkgbase-standalone usr/share/applications/*
    _pick $pkgbase-standalone usr/share/desktop-directories/*
    _pick $pkgbase-standalone usr/share/icons/*
    _pick $pkgbase-vst usr/lib/vst/*
    _pick $pkgbase-vst3 usr/lib/vst3/*
  )
}

package_liblsp-r3d-glx-lib() {
  pkgdesc="GLX-based 3D backend library for rendering scenes"
  depends=(
    glibc
    libglvnd libGL.so
    libx11
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-clap() {
  pkgdesc+=" - CLAP"
  groups=(clap-plugins)
  depends=(
    cairo
    clap-host
    fontconfig
    freetype2
    gcc-libs
    glibc
    libglvnd libGL.so
    libsndfile libsndfile.so
    libx11
    libxrandr
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-docs() {
  pkgdesc+=" (documentation)"

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-gst() {
  pkgdesc+=" - GStreamer"
  groups=(gstreamer-plugins)
  depends=(
    gcc-libs
    glib2 libglib-2.0.so libgobject-2.0.so
    glibc
    gstreamer
    gst-plugins-base-libs
    libsndfile libsndfile.so
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-ladspa() {
  pkgdesc+=" - LADSPA"
  groups=(ladspa-plugins)
  depends=(
    gcc-libs
    glibc
    ladspa-host
    libsndfile libsndfile.so
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-lv2() {
  pkgdesc+=" - LV2"
  groups=(lv2-plugins)
  depends=(
    cairo
    fontconfig
    freetype2
    gcc-libs
    glibc
    libglvnd libGL.so
    libsndfile libsndfile.so
    libx11
    libxrandr
    lv2-host
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-standalone() {
  pkgdesc+=" - JACK standalone"
  groups=(pro-audio)
  depends=(
    cairo
    fontconfig
    freetype2
    gcc-libs
    glibc
    hicolor-icon-theme
    jack libjack.so
    libglvnd libGL.so
    libsndfile libsndfile.so
    libx11
    libxrandr
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-vst() {
  pkgdesc+=" - VST2"
  groups=(vst-plugins)
  depends=(
    cairo
    fontconfig
    freetype2
    gcc-libs
    glibc
    libglvnd libGL.so
    libsndfile libsndfile.so
    libx11
    libxrandr
    vst-host
  )

  mv -v $pkgname/* "$pkgdir"
}

package_lsp-plugins-vst3() {
  pkgdesc+=" - VST3"
  groups=(vst3-plugins)
  depends=(
    cairo
    fontconfig
    freetype2
    gcc-libs
    glibc
    libglvnd libGL.so
    libsndfile libsndfile.so
    libx11
    libxrandr
    vst3-host
  )

  mv -v $pkgname/* "$pkgdir"
}
