# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=stunnel
pkgver=5.77
pkgrel=1
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL"
arch=('x86_64')
url="https://www.stunnel.org/"
license=('GPL-2.0-or-later')
depends=('openssl' 'perl' 'libnsl' 'systemd-libs')
makedepends=('systemd')
source=(
  "https://www.stunnel.org/downloads/archive/5.x/$pkgname-$pkgver.tar.gz"{,.asc}
  'Makefile.patch'
  'stunnel.sysusers'
  'stunnel.tmpfiles'
)
sha512sums=('271c7d60678fe6f21209454e693636058578698b7c2c3b6b820a0ac32ce5cb4fa42935f28c3a000baad616a678995a5b6863504c2118e0749f1d44434daf5e5b'
            'SKIP'
            '9c669b6a8ebfb2ed26ba90578a12a5de4c4667100ffaa02725650f451ed02b2eab4a77f00f20cc3c7cab761312b658059fcf259ef58e273541ca202c36a18095'
            'fa15a45a55b19cc7b8321e915819c8847054c1149c6e2ee9da14e5c599a6cf77d5f6e25ff8de31a149e24849f6a220c279d60ca896fe5eb5060d1635d7f0fbaf'
            '8e06ec39547ce5991217e551e563f5d212742afda94da801ec266c632375aae489594d14815fe596319d9881eb5a58e32bf05baa69f3a9d1bae554419f5ac1bb')
b2sums=('a060ff965872852c362a85231044c1850bb2ababa21d187013a2dd3c79fc4c09188573fa3ce3d13270ca7534e582fee540c7ee2e3d46fb72f957722ffefd44e3'
        'SKIP'
        'a1eacbfc7815a0e2eb6cda434639e315a94c27bdc59ad93090a00c8fbcdd760f9db1f22dbeaacc99615a44001444c295136c2ed92e57901d141478b661078ee3'
        '8826a496dba02b6a80b00c50cf0052bafadec0c9ba5d906ac52657b06edd9e3e95f71c67decabcb859530392a57fea167a75d5b108f9ab48641ab11600336bba'
        'cedd3eccd1964dcc86255fca0e47fe692a2619fbcbc576480adf193192a2218972d07961a3ab4d30f095c308844f25fe71cdf285bde201050be24d4ce0ca80d3')
validpgpkeys=('AC915EA30645D9D3D4DAE4FEB1048932DD3AAAA3') # Micha≈Ç Trojnara <Michal.Trojnara@stunnel.org>

prepare() {
  cd "$pkgname-$pkgver"

  # skip generation of $pkgdir/var
  patch -p0 <"$srcdir/Makefile.patch"

  # ensure user/group is set to stunnel
  sed \
    -e "s:\(nobody\|@DEFAULT_GROUP@\):stunnel:g" \
    -i tools/stunnel.conf-sample.in
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-ipv6 \
    --disable-libwrap

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # systemd integration
  install -vDm644 tools/stunnel.service "$pkgdir/usr/lib/systemd/system/stunnel.service"
  install -vDm644 "$srcdir/stunnel.sysusers" "$pkgdir/usr/lib/sysusers.d/stunnel.conf"
  install -vDm644 "$srcdir/stunnel.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/stunnel.conf"

  # bash completion missing from 5.65 onwards
  install -vDm644 tools/stunnel.bash -t "$pkgdir/usr/share/bash-completion/completions"
}
