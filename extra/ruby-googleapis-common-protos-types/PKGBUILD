# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='googleapis-common-protos-types'
pkgname="ruby-${_gemname}"
pkgver=1.20.0
pkgrel=1
pkgdesc='Common protocol buffer types used by Google APIs'
arch=('any')
url="https://github.com/googleapis/common-protos-ruby"
license=('Apache-2.0')
depends=(
  ruby
  ruby-google-protobuf
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("${url}/archive/${_gemname}%2Fv${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('331798bbac9f713238b18ad626ce0e40ff5afe3d669d9af2194e56f41b32a408bd443147ff3c0cc7651fc94594daa197a4ea9627b8df78fbc4aa65cf41d51ffb')
b2sums=('753766fb9265be1605d6eee62bf7cc398bf13ca88c36f8c070f63006b1ef7e26732e659ae2d0f4d389e7830a7b7e93700086209b096977cf413c0615f32b91eb')

prepare() {
  cd "common-protos-ruby-${_gemname}-v${pkgver}/${_gemname}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "common-protos-ruby-${_gemname}-v${pkgver}/${_gemname}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "common-protos-ruby-${_gemname}-v${pkgver}/${_gemname}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 ../LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
