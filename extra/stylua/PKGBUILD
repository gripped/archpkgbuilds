# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=StyLua
pkgname=stylua
pkgver=2.0.1
pkgrel=1
pkgdesc='Deterministic code formatter for Lua'
arch=('x86_64')
url=https://github.com/JohnnyMorganz/StyLua
license=('MPL-2.0')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'rust')
replaces=(stylua-bin)
source=("git+$url.git#tag=v$pkgver")
b2sums=('9a14a17a162ebf44bfee705b8f46efcb5fef3ae698ef75c40fd6df3904f6b92e14e4463e25f8ae22b233926b959654c3b5a24da45730237ac4d9e406a2e187d9')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  cargo build --release --frozen --all-features
}

check() {
  cd $_name
  cargo test --frozen --all-features
}

package() {
  cd $_name
  install -Dm0755 -t "$pkgdir"/usr/bin target/release/$pkgname
}
