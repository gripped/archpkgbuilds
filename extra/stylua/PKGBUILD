# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=StyLua
pkgname=stylua
pkgver=2.1.0
pkgrel=1
pkgdesc='Deterministic code formatter for Lua'
arch=('x86_64')
url=https://github.com/JohnnyMorganz/StyLua
license=('MPL-2.0')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'rust')
replaces=(stylua-bin)
source=("git+$url.git#tag=v$pkgver")
b2sums=('fa3ce80ed2364b3baad4e4237229dc96da0d54af0909bb8788dad2748fc94e04c3c2efa50bcb8a5b6efdb837392a2fdca086c81a752df0cb335bf42f25d1f581')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  cargo build --release --frozen --all-features
}

check() {
  cd $_name
  cargo test --frozen --all-features
}

package() {
  cd $_name
  install -Dm0755 -t "$pkgdir"/usr/bin target/release/$pkgname
}
