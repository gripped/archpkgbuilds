# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=StyLua
pkgname=stylua
pkgver=0.19.0
pkgrel=2
pkgdesc='Code formatter for Lua'
arch=('x86_64')
url=https://github.com/JohnnyMorganz/StyLua
license=('MPL2')
depends=('gcc-libs')
makedepends=('git' 'rust')
source=("git+$url.git#tag=v$pkgver")
b2sums=('SKIP')

prepare() {
  cd $_name
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_name
  cargo build --release --frozen --all-features
}

check() {
  cd $_name
  cargo test --release --frozen
  cargo test --release --frozen --features luau
  cargo test --release --frozen --features lua52
  cargo test --release --frozen --all-features # must me last because overwrites binary from build
}

package() {
  cd $_name
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}
