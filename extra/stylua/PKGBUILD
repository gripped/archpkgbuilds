# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=StyLua
pkgname=stylua
pkgver=2.2.0
pkgrel=1
pkgdesc='Deterministic code formatter for Lua'
arch=(x86_64)
url=https://github.com/JohnnyMorganz/StyLua
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('53e7794f8cbd9b87c990f9e551a825ab1afd3ccd5121167b3598c21b098b4c5f0014c9a72883514378e8f6a90a2b2e540b880056066f353f1c169bd144ef6134')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  cargo build --release --frozen --all-features
}

check() {
  cd $_name
  cargo test --frozen --all-features
}

package() {
  cd $_name
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}
