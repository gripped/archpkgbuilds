# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=StyLua
pkgname=stylua
pkgver=2.3.0
pkgrel=1
pkgdesc='Deterministic code formatter for Lua'
arch=(x86_64)
url=https://github.com/JohnnyMorganz/StyLua
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('e89fe204edb233e7bb593385d84019a0e84dee04a1e25442d503fbed7482361c04dffde8667ddc0944725102bee8ac553940cc7ad86e46b698f1339826632daf')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  cargo build --release --frozen --all-features
}

check() {
  cd $_name
  cargo test --frozen --all-features
}

package() {
  cd $_name
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}
