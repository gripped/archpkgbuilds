# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=StyLua
pkgname=stylua
pkgver=2.0.0
pkgrel=1
pkgdesc='Deterministic code formatter for Lua'
arch=('x86_64')
url=https://github.com/JohnnyMorganz/StyLua
license=('MPL-2.0')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'rust')
replaces=(stylua-bin)
source=("git+$url.git#tag=v$pkgver")
b2sums=('ebbd7fcc82a79762253c52296f17be979a520fbc80a89177916900598f5c1903ca41ec70e3590114042bed555735baeeb2929f5860ef24d6f4ba3a82fa03c90d')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  cargo build --release --frozen --all-features
}

check() {
  cd $_name
  cargo test --frozen --all-features
}

package() {
  cd $_name
  install -Dm0755 -t "$pkgdir"/usr/bin target/release/$pkgname
}
