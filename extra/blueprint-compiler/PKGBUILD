# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=blueprint-compiler
pkgver=0.14.0
pkgrel=1
pkgdesc='A markup language for GTK user interfaces'
arch=(any)
url='https://jwestman.pages.gitlab.gnome.org/blueprint-compiler/'
license=(LGPL-3.0-or-later)
depends=(
  libgirepository
  python
  python-gobject
)
makedepends=(
  git
  meson
  python-sphinx-furo
)
checkdepends=(
  libadwaita
  xorg-server-xvfb
)
source=("git+https://gitlab.gnome.org/jwestman/$pkgname.git#tag=v$pkgver")
b2sums=('33240d9c2c8e347860edc73efd29c7de090c6ee9af57313370e4841471728c809cf0d57f52fbb07239dd99966ae21b00ee4b375890e94c9848307ffce7582bb1')

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m sphinx -b html $pkgname/docs "$pkgdir/usr/share/doc/$pkgname/html"
  rm -r "$pkgdir/usr/share/doc/$pkgname/html/.doctrees"
}
