# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=bugstalker
_pkgname=BugStalker
pkgver=0.3.1
pkgrel=1
pkgdesc="Rust debugger for Linux x86-64"
arch=('x86_64')
url="https://github.com/godzie44/BugStalker"
license=('MIT')
depends=('gcc-libs' 'libunwind' 'oniguruma')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a6f237c546436fa14b918320de6bea585318f83364e8af3d2b2c92e01d3445dd21106a00757d264ebd722561376ab7358a0ef4a58361e4a3a9113d524996b786')
options=('!lto')

prepare() {
  mv "$_pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTONIG_DYNAMIC_LIBONIG=1
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/bs" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
