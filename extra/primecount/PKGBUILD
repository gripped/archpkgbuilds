# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primecount
pkgver=7.18
pkgrel=1
pkgdesc='Fast C++ prime counting function implementation'
arch=(x86_64)
url='https://github.com/kimwalisch/primecount'
license=(BSD-2-Clause)
depends=(gcc-libs
         glibc
         primesieve)
makedepends=(cmake
             git)
source=(git+https://github.com/kimwalisch/primecount#tag=v$pkgver)
sha256sums=('a94a3ee0864811b2e91b626295c8c9e5e34fdf351a481f2af8a416e4de1ceda2')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBPRIMESIEVE=OFF \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_FLOAT128=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
