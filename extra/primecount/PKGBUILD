# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primecount
pkgver=7.17
pkgrel=1
pkgdesc='Fast C++ prime counting function implementation'
arch=(x86_64)
url='https://github.com/kimwalisch/primecount'
license=(BSD-2-Clause)
depends=(gcc-libs
         glibc
         primesieve)
makedepends=(cmake
             git)
source=(git+https://github.com/kimwalisch/primecount#tag=v$pkgver)
sha256sums=('553818ecfea82e101b5568dbe2e2767a1f720b6b855c6d72fbfd835620159535')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBPRIMESIEVE=OFF \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_FLOAT128=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
