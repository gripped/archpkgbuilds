# Maintainer: christian rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=git-credential-gopass
pkgver=1.15.17
pkgrel=1
pkgdesc="git-credentials helper for gopass"
arch=('x86_64')
url="https://github.com/gopasspw/git-credential-gopass"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('38a3b46e13805338b9157af1e835a35f3296d9b9e39764f0e5f319ac5cafe6dab9cde1a2296fce69941e8ec7397ff5ad18e49f1be2760d3cf44c2a976bf33ab1')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build(){
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags="
    -compressdwarf=false
    -linkmode=external
    -X main.version=$pkgver
  "
  go build -v -ldflags="$ld_flags" -o git-credential-gopass
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" git-credential-gopass
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
