# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.4.0
pkgrel=1
pkgdesc="Free, open source software for digital DJing"
arch=(x86_64)
url="https://www.mixxx.org"
_url="https://github.com/mixxxdj/mixxx"
license=(GPL-2.0-or-later)
groups=(pro-audio)
# TODO: package libshout-idjc
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  hidapi
  lame
  libebur128
  libkeyfinder
  libmad
  libmodplug
  libx11
  openssl
  opus
  opusfile
  qt5-base
  qt5-declarative
  qt5-script
  qt5-svg
  qt5-x11extras
  qtkeychain
  soundtouch
  speex
  sqlite
  taglib1
  upower
  wavpack
)
makedepends=(
  benchmark
  chromaprint
  cmake
  faad2
  ffmpeg
  flac
  glib2
  glu
  gperftools
  gtest
  libid3tag
  libogg
  libsndfile
  libusb
  libvorbis
  lilv
  lv2
  microsoft-gsl
  qt5-tools
  portaudio
  portmidi
  protobuf
  rubberband
  vamp-plugin-sdk
)
checkdepends=(xorg-server-xvfb)
source=($_url/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('cde73d5824b3c700b3b2baf853a8cc4f338431363a05e0052197d06c3a97d09fc27b1748d9ffc8448c9a8715dc969d9af797b6021a2c5ddbc8d3418fa0eaf486')
b2sums=('0fbf0df61f888f33880b9e952d5afd94464d8199e18a8cda16d9156da1a674835850fe8c865e678f3416d7314aed62a388880e13f2688781515fda89a4d378ea')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  export PKG_CONFIG_PATH=/usr/lib/taglib1/pkgconfig
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  xvfb-run ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    chromaprint libchromaprint.so
    flac libFLAC.so
    ffmpeg libavcodec.so libavformat.so libavutil.so
    glib2 libgobject-2.0.so
    libglvnd libOpenGL.so
    libid3tag libid3tag.so
    libogg libogg.so
    libsndfile libsndfile.so
    libusb libusb-1.0.so
    libvorbis libvorbis.so libvorbisenc.so libvorbisfile.so
    lilv liblilv-0.so
    portaudio libportaudio.so
    portmidi libportmidi.so
    protobuf libprotobuf-lite.so
    rubberband librubberband.so
    zlib libz.so
  )

  DESTDIR="$pkgdir" cmake --install build
}
