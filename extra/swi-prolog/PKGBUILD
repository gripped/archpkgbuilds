# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=9.2.2 # using the stable version
pkgrel=1
pkgdesc='Prolog environment'
arch=(x86_64)
url='https://www.swi-prolog.org/'
license=(GPL LGPL)
depends=(db5.3 gperftools libarchive libxcrypt libyaml qt5-base util-linux-libs)
makedepends=(cmake jdk-openjdk junit libjpeg libxft libxinerama libxpm ninja python-protobuf unixodbc)
optdepends=('java-runtime: for using Prolog from Java'
            'jdk-openjdk: for using Prolog from Java'
            'libjpeg: for using the pl2xpce library'
            'libxft: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'python: for using Prolog from Python'
            'unixodbc: for using the odbc4pl library')
source=("$pkgname-$pkgver.tar.gz::https://www.swi-prolog.org/download/stable/src/swipl-$pkgver.tar.gz"
        bdb_5.3.patch)
b2sums=('be4653e322f613e79fdf1ebbb422b02687d9bd901f9e0f83f82b3204a3db1dafe997dc38a3002c7d5168cfde9cdbea03946432aa47795a27ee69632960123e7a'
        '37e14768ec59f9aee4363c5ffd63be8c50c04eecf269e52e8aadd5cadbc99f6286aa676b1145b1391490ab49072039d896eed588e934ba769d09c6bdb2d2222d')

prepare() {
  cd swipl-$pkgver
  patch -Np1 -i ../bdb_5.3.patch
}

build() {
  cmake \
    -B build \
    -D BDB_INCLUDE_DIR=/usr/include/db5.3 \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -ffile-prefix-map=$PWD= -w" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D LIBEDIT_INCLUDE_DIR=/usr/include \
    -D LIBEDIT_LIBRARIES=/usr/lib/libedit.so.0 \
    -G Ninja \
    -S swipl-$pkgver
  ninja -C build
}

check() {
  ctest --test-dir build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
