# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=9.2.9 # using the latest stable version, see: https://www.swi-prolog.org/download/stable
pkgrel=1
pkgdesc='Prolog environment'
arch=(x86_64)
url='https://www.swi-prolog.org'
license=(BSD-2-Clause)
depends=(db5.3 gperftools libarchive libxcrypt libyaml qt5-base util-linux-libs)
makedepends=(cmake jdk-openjdk libjpeg-turbo libxft libxinerama libxpm python-protobuf unixodbc)
checkdepends=(java-hamcrest junit)
optdepends=('java-runtime: for using Prolog from Java'
            'jdk-openjdk: for using Prolog from Java'
            'libjpeg: for using the pl2xpce library'
            'libxft: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'python: for using Prolog from Python'
            'unixodbc: for using the odbc4pl library')
source=("$pkgname-$pkgver.tar.gz::$url/download/stable/src/swipl-$pkgver.tar.gz"
        bdb_5.3.patch)
b2sums=('6d058f0ce366e8220a77a9a0e6df3e732a891b0c214ca01c08fd58df356e5c3868ecd9b9ce69b8357d2ed65b2b7195c3e1a14c1f1553d6a35f06e0b1dc38fdaf'
        '37e14768ec59f9aee4363c5ffd63be8c50c04eecf269e52e8aadd5cadbc99f6286aa676b1145b1391490ab49072039d896eed588e934ba769d09c6bdb2d2222d')

prepare() {
  cd swipl-$pkgver
  patch -Np1 -i ../bdb_5.3.patch
}

build() {
  cmake \
    -B build \
    -D BDB_INCLUDE_DIR=/usr/include/db5.3 \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -ffile-prefix-map=$PWD= -w" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D LIBEDIT_INCLUDE_DIR=/usr/include \
    -D LIBEDIT_LIBRARIES=/usr/lib/libedit.so.0 \
    -S swipl-$pkgver
  make -C build
}

check() {
  # skip the jpl:prolog_in_java test, for now (has been reported upstream)
  ctest -E "jpl:prolog_in_java" --test-dir build || cat build/Testing/Temporary/LastTest.log
}

package() {
  DESTDIR="$pkgdir" make -C build install
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" swipl-$pkgver/LICENSE
}
