# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ananta
pkgver=1.2.2
pkgrel=3
pkgdesc="A command-line tool to execute commands on multiple remote hosts"
arch=('any')
url="https://sr.ht/~cwt/ananta/"
license=('MIT')
depends=('python-asyncssh')
optdepends=('python-uvloop: for improved performance')
makedepends=('mercurial' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("hg+https://hg.sr.ht/~cwt/ananta#tag=v$pkgver")
sha512sums=('09f58c15c8ea8bfc7ce80d0a459fe4ec13d67751981af0a8ac37bcd209490e17c5a7644616710e2a1a38f1a7f827b3203870a4aa62970bf320b3ee047e7c218c')

build() {
  cd ananta
  python -m build --wheel --no-isolation
}

check() {
  cd ananta
  python -m venv --system-site-packages venv
  venv/bin/python -m installer dist/*.whl
  venv/bin/python -m pytest
}

package() {
  cd ananta
  python -m installer --destdir="$pkgdir" dist/*.whl
}
