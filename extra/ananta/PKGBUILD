# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ananta
pkgver=1.3.2
pkgrel=1
pkgdesc="A command-line tool to execute commands on multiple remote hosts"
arch=('any')
url="https://sr.ht/~cwt/ananta/"
license=('MIT')
depends=('python-asyncssh' 'python-urwid')
optdepends=('python-uvloop: for improved performance')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("git+https://github.com/cwt/ananta.git#tag=v$pkgver")
sha512sums=('bdcf421cfb93b25bc2436272c52473ef41fd53940aa4b201d1fd3e998b781aa9388633a8110b036af0529dd0cbca9912bf7de6b0db3b5f57855ec7c1a1314522')

build() {
  cd ananta
  python -m build --wheel --no-isolation
}

check() {
  cd ananta
  python -m venv --system-site-packages venv
  venv/bin/python -m installer dist/*.whl
  venv/bin/python -m pytest
}

package() {
  cd ananta
  python -m installer --destdir="$pkgdir" dist/*.whl
}
