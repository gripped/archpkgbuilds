# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ananta
pkgver=1.1.7
pkgrel=1
pkgdesc="A command-line tool to execute commands on multiple remote hosts"
arch=('any')
url="https://sr.ht/~cwt/ananta/"
license=('MIT')
depends=('python-asyncssh')
optdepends=('python-uvloop: for improved performance')
makedepends=('mercurial' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("hg+https://hg.sr.ht/~cwt/ananta#tag=v$pkgver")
sha512sums=('9455a4dc977c622bbd501a3f3689d583c87d1ef1df1421409c5b86144906218c123e50b2d9bf36ad19c48903cc143e23303f422dfa11095b894057269d6bf552')

build() {
  cd ananta
  python -m build --wheel --no-isolation
}

check() {
  cd ananta
  python -m venv --system-site-packages venv
  venv/bin/python -m installer dist/*.whl
  venv/bin/python -m pytest
}

package() {
  cd ananta
  python -m installer --destdir="$pkgdir" dist/*.whl
}
