# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ananta
pkgver=1.3.1
pkgrel=1
pkgdesc="A command-line tool to execute commands on multiple remote hosts"
arch=('any')
url="https://sr.ht/~cwt/ananta/"
license=('MIT')
depends=('python-asyncssh' 'python-urwid')
optdepends=('python-uvloop: for improved performance')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("git+https://github.com/cwt/ananta.git#tag=v$pkgver")
sha512sums=('7e13e995ec361604318072c1432ac6f244c4450f89b5df62772bbd6cb536469982208ca5092afca85b1dbdc9daea5d3493c677beaa9d8a26f27e8198c4ad0b0f')

build() {
  cd ananta
  python -m build --wheel --no-isolation
}

check() {
  cd ananta
  python -m venv --system-site-packages venv
  venv/bin/python -m installer dist/*.whl
  venv/bin/python -m pytest
}

package() {
  cd ananta
  python -m installer --destdir="$pkgdir" dist/*.whl
}
