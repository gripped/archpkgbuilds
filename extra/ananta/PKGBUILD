# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ananta
pkgver=1.1.4
pkgrel=1
pkgdesc="A command-line tool to execute commands on multiple remote hosts"
arch=('any')
url="https://sr.ht/~cwt/ananta/"
license=('MIT')
depends=('python-asyncssh')
optdepends=('python-uvloop: for improved performance')
makedepends=('mercurial' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("hg+https://hg.sr.ht/~cwt/ananta#tag=v$pkgver")
sha512sums=('77a3a174724b2b59fe41c889398ff029675cef5dcaed270db80fc1c468f7f655e4e3dfa9a1992d72eecad8e9f40f3dd1e1f59d593840c0c8e49fac391f23a713')

build() {
  cd ananta
  python -m build --wheel --no-isolation
}

check() {
  cd ananta
  python -m venv --system-site-packages venv
  venv/bin/python -m installer dist/*.whl
  venv/bin/python -m pytest
}

package() {
  cd ananta
  python -m installer --destdir="$pkgdir" dist/*.whl
}
