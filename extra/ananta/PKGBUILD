# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ananta
pkgver=1.1.8
pkgrel=1
pkgdesc="A command-line tool to execute commands on multiple remote hosts"
arch=('any')
url="https://sr.ht/~cwt/ananta/"
license=('MIT')
depends=('python-asyncssh')
optdepends=('python-uvloop: for improved performance')
makedepends=('mercurial' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("hg+https://hg.sr.ht/~cwt/ananta#tag=v$pkgver")
sha512sums=('b0ae833a4cc4246a9f2b05db2dc02c6b8b7b2ae3023bc2305781ad77442382695b3734a9a9d9ae76981bab85183ce003dbe478ad70d113f2a83003a3960757f1')

build() {
  cd ananta
  python -m build --wheel --no-isolation
}

check() {
  cd ananta
  python -m venv --system-site-packages venv
  venv/bin/python -m installer dist/*.whl
  venv/bin/python -m pytest
}

package() {
  cd ananta
  python -m installer --destdir="$pkgdir" dist/*.whl
}
