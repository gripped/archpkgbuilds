# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ananta
pkgver=1.3.0
pkgrel=1
pkgdesc="A command-line tool to execute commands on multiple remote hosts"
arch=('any')
url="https://sr.ht/~cwt/ananta/"
license=('MIT')
depends=('python-asyncssh' 'python-urwid')
optdepends=('python-uvloop: for improved performance')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("git+https://github.com/cwt/ananta.git#tag=v$pkgver")
sha512sums=('49b46363d5e2d08036144b8c25b56e70b02dc23baad8101a7334c415596e49c47ed9c0bb61d1fc4ce16c07059b5ac6bf8227b3f06df9bdd6c1759139fec29558')

build() {
  cd ananta
  python -m build --wheel --no-isolation
}

check() {
  cd ananta
  python -m venv --system-site-packages venv
  venv/bin/python -m installer dist/*.whl
  venv/bin/python -m pytest
}

package() {
  cd ananta
  python -m installer --destdir="$pkgdir" dist/*.whl
}
