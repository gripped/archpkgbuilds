# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=signstar-sign
_name=signstar
pkgver=0.1.0
pkgrel=1
pkgdesc="A command line tool for creating signatures for signing requests"
arch=(x86_64)
url="https://signstar.archlinux.page/signstar-sign"
_url="https://gitlab.archlinux.org/archlinux/signstar"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  jq
  just
  mold
  rust
  rust-script
)
source=($_name::git+$_url.git?signed#tag=$pkgname/$pkgver)
sha512sums=('18c45adfbce43c6cbfb92c7377b1a422628ed2a9fcad8a77935c15d3c4360c442bf695ca9606003d6685f79c4d63e8971ca26548151adec2dbe3f6618c8078b5')
b2sums=('88c763ed758499fd41e3a4092cbf1eabd2239d3d8095118d8943203407ba1cf23091f0cd40bebf9d6caef8bb7678f0e3197b9428e05b76ac261d5ce4a748862f')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release --package $pkgname
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/$pkgname/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $_name/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
