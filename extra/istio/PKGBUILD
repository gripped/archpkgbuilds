# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Arturo Penen <apenen@gmail.com>
# Contributor: jtagcat <arch-3ff61201@jtag.cat>

pkgname=istio
pkgver=1.23.1
pkgrel=1
pkgdesc="Istio configuration command line utility for service operators to debug and diagnose their Istio mesh"
arch=(x86_64)
url="https://github.com/istio/istio"
license=(Apache-2.0)
depends=(glibc)
makedepends=(go)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('015b6a1af42d2dcee189229e5635da32d9e835d7fbdd77ca4d49351bb5a7768a081e11d9934c3a1ea40d6e335f3f9cda1740afed50540b7092d2fa86740531b8')
b2sums=('22c13fc94e4799be53588dbb7946233d1cef87ab5b81cb205bec48136f60aa91820b6cfcf068262f54bf76662bdd73ca6679fb35a164cfbe375a8066407c5de6')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -compressdwarf=false  \
    -linkmode=external  \
    -X istio.io/istio/pkg/version.buildVersion=$pkgver \
  "
  go build -v -buildvcs=false -ldflags="$ld_flags" -o build/istioctl \
    ./istioctl/cmd/istioctl
}

check() {
  cd $pkgname-$pkgver
  # Test that the exectuble reports the correct version.
  test "$(./build/istioctl version --remote=false)" = "client version: $pkgver"

  # Disable unit tests failing in a chroot - not sure why.
  local unit_tests=$(
    go list ./istioctl/... \
      | grep -v istio.io/istio/istioctl/pkg/multicluster \
      | grep -v istio.io/istio/istioctl/pkg/tag
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" build/istioctl

  ./build/istioctl collateral --bash
  ./build/istioctl collateral --zsh
  install -vDm644 istioctl.bash "$pkgdir/usr/share/bash-completion/completions/istioctl"
  install -vDm644 _istioctl "$pkgdir/usr/share/zsh/site-functions/_istioctl"
}
