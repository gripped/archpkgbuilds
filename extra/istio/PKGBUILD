# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Arturo Penen <apenen@gmail.com>
# Contributor: jtagcat <arch-3ff61201@jtag.cat>

pkgname=istio
pkgver=1.22.1
pkgrel=2
pkgdesc='Istio configuration command line utility for service operators to debug and diagnose their Istio mesh'
arch=('x86_64')
url='https://github.com/istio/istio'
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('ee5401adc72f3db95acbd5e4b923cac03ad88d91f2b30cc1a21a3e165be6d4ebd99135e5e9a6e0737c8c44d4279da6392d7a0cee902cb2947ab1f2e7c52d4900')
b2sums=('78bf54ed45a0bc8d88e8a5d75d81dc92b1f1e595e4fe535f3b69b6e608478f16dd32bcdb4099f2258649ab837272e7b85bf9021b655f4362e252d08b31f587f8')

prepare() {
  cd $pkgname-$pkgver

  go mod download -x
}

build() {
  cd $pkgname-$pkgver

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  # shellcheck disable=SC2153
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
  export GOPATH="$srcdir"

  mkdir -p build
  local ldflags="-compressdwarf=false -linkmode=external -X istio.io/istio/pkg/version.buildVersion=$pkgver"
  go build -v -buildvcs=false -o build -ldflags="$ldflags" \
    ./istioctl/cmd/istioctl
}

check() {
  cd $pkgname-$pkgver

  # Test that the exectuble reports the correct version.
  test "$(./build/istioctl version --remote=false)" = "$pkgver"

  # Disable unit tests failing in a chroot - not sure why.
  local unit_tests=$(
    go list ./istioctl/... \
      | grep -v istio.io/istio/istioctl/pkg/multicluster \
      | grep -v istio.io/istio/istioctl/pkg/tag
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  install -Dm 755 "$srcdir/$pkgname-$pkgver/build/istioctl" "$pkgdir/usr/bin/istioctl"

  # Populate bash and zsh completions.
  "$pkgdir/usr/bin/istioctl" collateral --bash
  "$pkgdir/usr/bin/istioctl" collateral --zsh
  install -Dm 644 istioctl.bash "${pkgdir}/usr/share/bash-completion/completions/istioctl"
  install -Dm 644 _istioctl "${pkgdir}/usr/share/zsh/site-functions/_istioctl"
}
