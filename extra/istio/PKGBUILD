# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Arturo Penen <apenen@gmail.com>
# Contributor: jtagcat <arch-3ff61201@jtag.cat>

pkgname=istio
pkgver=1.22.2
pkgrel=1
pkgdesc='Istio configuration command line utility for service operators to debug and diagnose their Istio mesh'
arch=('x86_64')
url='https://github.com/istio/istio'
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('07c1994fe96d90b199543c69fdb70d49f77b136516ae5277b2971a71bcf08929f48ae60662db8783f15bac21654ca795182363a30e5ab4adcef277b4238eb8a6')
b2sums=('f0d20af60e3d4c85a60a9a5baa8328bcefde0fbe42a779fbfa66f10c5d98b5551d2614e58f0b5cefc97c48e3c3a04f7bbcb05e2c63bbaa911478d1d4dd477bb4')

prepare() {
  cd $pkgname-$pkgver

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  # shellcheck disable=SC2153
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  mkdir -p build
  local ldflags=" \
    -compressdwarf=false  \
    -linkmode=external  \
    -X istio.io/istio/pkg/version.buildVersion=$pkgver \
  "
  go build -v -buildvcs=false -o build -ldflags="$ldflags" \
    ./istioctl/cmd/istioctl
}

check() {
  cd $pkgname-$pkgver

  # Test that the exectuble reports the correct version.
  test "$(./build/istioctl version --remote=false)" = "$pkgver"

  # Disable unit tests failing in a chroot - not sure why.
  local unit_tests=$(
    go list ./istioctl/... \
      | grep -v istio.io/istio/istioctl/pkg/multicluster \
      | grep -v istio.io/istio/istioctl/pkg/tag
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  install -Dm 755 "$srcdir/$pkgname-$pkgver/build/istioctl" "$pkgdir/usr/bin/istioctl"

  # Populate bash and zsh completions.
  "$pkgdir/usr/bin/istioctl" collateral --bash
  "$pkgdir/usr/bin/istioctl" collateral --zsh
  install -Dm 644 istioctl.bash "${pkgdir}/usr/share/bash-completion/completions/istioctl"
  install -Dm 644 _istioctl "${pkgdir}/usr/share/zsh/site-functions/_istioctl"
}
