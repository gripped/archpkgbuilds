# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Arturo Penen <apenen@gmail.com>
# Contributor: jtagcat <arch-3ff61201@jtag.cat>

pkgname=istio
pkgver=1.29.0
pkgrel=1
pkgdesc="Istio configuration command line utility for service operators to debug and diagnose their Istio mesh"
arch=(x86_64)
url="https://github.com/istio/istio"
license=(Apache-2.0)
depends=(glibc)
makedepends=(go)
options=(!lto)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('773d895bf69c0d6ee8a0271d54639a969f440de7f8fdad282c4887a42833dad73c9e0a976760d88906683c21d5ca7d45e637a70c7b982e4b7baa86d63792ed64')
b2sums=('b1f7045640eda4d86aec786eab26cbd6fcd5473e32b71dbd4ecc782eb05c4d39a22c85bb4b56d597097b707a4332bfdddd080db6f35ea7898e02810fd178bb9b')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -compressdwarf=false  \
    -linkmode=external  \
    -X istio.io/istio/pkg/version.buildVersion=$pkgver \
    -X istio.io/istio/pkg/version.buildTag=$pkgver \
    -X istio.io/istio/pkg/version.buildHub=docker.io/istio \
  "
  go build -v -buildvcs=false -ldflags="$ld_flags" -o build/istioctl \
    ./istioctl/cmd/istioctl
}

check() {
  cd $pkgname-$pkgver
  # Test that the exectuble reports the correct version.
  test "$(./build/istioctl version --remote=false)" = "client version: $pkgver"

  go test -v ./istioctl/...
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" build/istioctl

  ./build/istioctl collateral --bash
  ./build/istioctl collateral --zsh
  install -vDm644 istioctl.bash "$pkgdir/usr/share/bash-completion/completions/istioctl"
  install -vDm644 _istioctl "$pkgdir/usr/share/zsh/site-functions/_istioctl"
}
