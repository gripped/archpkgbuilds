# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Arturo Penen <apenen@gmail.com>
# Contributor: jtagcat <arch-3ff61201@jtag.cat>

pkgname=istio
pkgver=1.27.2
pkgrel=1
pkgdesc="Istio configuration command line utility for service operators to debug and diagnose their Istio mesh"
arch=(x86_64)
url="https://github.com/istio/istio"
license=(Apache-2.0)
depends=(glibc)
makedepends=(go)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('2b63263533487ea978af1d662290afb45b9aac4269549c3761108494e2db3a5d5e112a6375638aaaf5fc3a5c7aefddf6e5a40099305cda4491125b651879d41a')
b2sums=('13edf0cff93ef2197901b95531a724231de8fbcceffc225097198778aae4256e24921a080939362ce4b7e99093dbdf2e5621e4c1ce0968c4fc24ce06f8ecce1e')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -compressdwarf=false  \
    -linkmode=external  \
    -X istio.io/istio/pkg/version.buildVersion=$pkgver \
    -X istio.io/istio/pkg/version.buildTag=$pkgver \
    -X istio.io/istio/pkg/version.buildHub=docker.io/istio \
  "
  go build -v -buildvcs=false -ldflags="$ld_flags" -o build/istioctl \
    ./istioctl/cmd/istioctl
}

check() {
  cd $pkgname-$pkgver
  # Test that the exectuble reports the correct version.
  test "$(./build/istioctl version --remote=false)" = "client version: $pkgver"

  go test -v ./istioctl/...
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" build/istioctl

  ./build/istioctl collateral --bash
  ./build/istioctl collateral --zsh
  install -vDm644 istioctl.bash "$pkgdir/usr/share/bash-completion/completions/istioctl"
  install -vDm644 _istioctl "$pkgdir/usr/share/zsh/site-functions/_istioctl"
}
