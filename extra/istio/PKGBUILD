# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Arturo Penen <apenen@gmail.com>
# Contributor: jtagcat <arch-3ff61201@jtag.cat>

pkgname=istio
pkgver=1.28.1
pkgrel=1
pkgdesc="Istio configuration command line utility for service operators to debug and diagnose their Istio mesh"
arch=(x86_64)
url="https://github.com/istio/istio"
license=(Apache-2.0)
depends=(glibc)
makedepends=(go)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('29884983a45812da287228278da309244ee70d89db36ceb2a17fb572087853b2a1e69089939cbf09344c131134c471d87eacd158f63a3cbbeca4d610b27d9b12')
b2sums=('12fde9cd05891f5c2f9ddbd1fed32afb7ea80be38bb625bdb2bf20866d8f9e426cef980e8e106956be10c54c7753131ac69aa26acb3e90bd923c9b733b9be487')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -compressdwarf=false  \
    -linkmode=external  \
    -X istio.io/istio/pkg/version.buildVersion=$pkgver \
    -X istio.io/istio/pkg/version.buildTag=$pkgver \
    -X istio.io/istio/pkg/version.buildHub=docker.io/istio \
  "
  go build -v -buildvcs=false -ldflags="$ld_flags" -o build/istioctl \
    ./istioctl/cmd/istioctl
}

check() {
  cd $pkgname-$pkgver
  # Test that the exectuble reports the correct version.
  test "$(./build/istioctl version --remote=false)" = "client version: $pkgver"

  go test -v ./istioctl/...
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" build/istioctl

  ./build/istioctl collateral --bash
  ./build/istioctl collateral --zsh
  install -vDm644 istioctl.bash "$pkgdir/usr/share/bash-completion/completions/istioctl"
  install -vDm644 _istioctl "$pkgdir/usr/share/zsh/site-functions/_istioctl"
}
