# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
pkgname=mkinitcpio-tinyssh
pkgver=0.0.5
pkgrel=1
pkgdesc="Archlinux mkinitcpio hook to install and enable the tinyssh daemon in early userspace"
arch=('any')
url="https://github.com/grazzolini/mkinitcpio-tinyssh"
license=('BSD')
depends=('mkinitcpio-busybox' 'psmisc' 'tinyssh')
optdepends=('mkinitcpio-netconf: Network interface configuration'
            'tinyssh-convert: For converting OpenSSH keys')
conflicts=('mkinitcpio-dropbear')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::$url/releases/download/v$pkgver/v$pkgver.tar.gz.sig")
changelog='ChangeLog'
sha512sums=('96415eb02bb91ca0e7b4ef0d35c9b46d99ef96b61e673626bb6f244653ef0710cf5fedaa5a191957169318906bcdee08f04a67a1679f017461bf93ddc79c3167'
            'SKIP')
validpgpkeys=('ED2A707C15D4A9E57535C1E7354AAB6377B981BC') # Giancarlo Razzolini <grazzolini@gmail.com>

package() {
  install -Dm644 "$srcdir/$pkgname-$pkgver/tinyssh_hook"      "$pkgdir/usr/lib/initcpio/hooks/tinyssh"
  install -Dm644 "$srcdir/$pkgname-$pkgver/tinyssh_install"   "$pkgdir/usr/lib/initcpio/install/tinyssh"
  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
