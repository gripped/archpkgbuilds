# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=cava
pkgver=0.10.7
pkgrel=1
pkgdesc='Console-based Audio Visualizer with support for multiple backends'
arch=('x86_64')
url='https://github.com/karlstav/cava'
license=('MIT')
# Compile with support for (and depend on) all input/output backends for
# simplicity's sake. One could possibly split this into one package per
# backend, but let's start with this.
depends=(
  'fftw'
  'glibc'
  'iniparser'
  # Input backends
  'alsa-lib'
  'jack'
  'libpipewire'
  'libpulse'
  'portaudio'
  'sndio'
  # Output backends
  'ncurses'
  'sdl2'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6db21291cf06049b6349ac2ffbc6d2803481e514374dcb21d57d9d23cfe886a8bdfc8a247c51db1fb5feb57a855953f2d468db7762ac119dbddd7f37d1f4b597')

prepare() {
  cd $pkgname-$pkgver
  echo "$pkgver" > version
  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
