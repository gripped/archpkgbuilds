# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=trippy
pkgver=0.13.0
pkgrel=1
pkgdesc='A network diagnostic tool'
arch=('x86_64')
url='https://trippy.cli.rs'
license=('Apache-2.0')
depends=('gcc-libs' 'libcap')
makedepends=('cargo')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/fujiapple852/trippy/archive/$pkgver.tar.gz")
b2sums=('cab7d88b0c1c83139a80ed8b8f6ebfff3f0ee686740e0176d4607bb488ff94e67d8e5fee4a2e3079044d3401cc54be194054e8e603f3fc76dd279ddd8268995b')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  mkdir completions
  mkdir manuals
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --frozen
  local compgen="target/release/trip --generate"
  local mangen="target/release/trip --generate-man"
  $compgen bash >"completions/$pkgname"
  $compgen fish >"completions/$pkgname.fish"
  $compgen zsh >"completions/_$pkgname"
  $mangen >"manuals/trip.1"
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 -t "$pkgdir"/usr/bin target/release/trip
  install -Dm 644 -t "$pkgdir"/usr/share/doc/"$pkgname" README.md
  install -Dm 644 "manuals/trip.1" -t "$pkgdir/usr/share/man/man1/"
  install -Dm 644 "completions/$pkgname" -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm 644 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions/"
}
