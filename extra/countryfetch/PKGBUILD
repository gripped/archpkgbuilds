# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: bemxio <bemxiov at protonmail dot com>

pkgname=countryfetch
pkgver=0.1.9
pkgrel=3
pkgdesc="A neofetch-like tool for fetching information about your country"
arch=('x86_64')
url="https://github.com/nik-rev/countryfetch"
license=("MIT")
depends=('openssl' 'gcc-libs')
makedepends=('cargo-nightly') # https://github.com/nik-rev/countryfetch/issues/12
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f68a0110f13cb7140017c62633525bb92ae34e65cfec4b4a8072a20216f24c4611c53dbc8479665479aae5efe0591bd2b2aa1c700cb7e2bd0bcf6c1720f12552')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use the system-provided openssl
  sed -i 's/openssl = { version = "0.10", features = \["vendored"\] }//' countryfetch/Cargo.toml
  CARGO_TOOLCHAIN=nightly cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  CARGO_TOOLCHAIN=nightly cargo build --release
}

check() {
  cd "${pkgname}-${pkgver}"
  CARGO_TOOLCHAIN=nightly cargo test
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/$pkgname/README.md"
}

# vim: ts=2 sw=2 et:
