# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=apk-tools
pkgver=3.0.5
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'openssl'
  'zlib'
)
makedepends=(
  'meson'
  'lua53'
  'lua53-zlib'
  'scdoc'
)
checkdepends=(
  'cmocka'
)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('e9d7398d5f70badea596903599bfa1e05b64d67d13c929da9f0eee85828260c6')
b2sums=('89e06cbf16665a3c6b7cb0f18109b158db3fcf0adc0475c198bbc2d569596abc072ef24bd6332aeb7b75ec641302275d27e8d6499d6cfce071d5a79510741858')

build() {
  arch-meson "${pkgname}-v${pkgver}" build \
    --sbindir=/usr/bin \
    --libdir=/usr/lib
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
