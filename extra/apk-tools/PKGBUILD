# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=apk-tools
pkgver=3.0.4
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'openssl'
  'zlib'
)
makedepends=(
  'meson'
  'lua53'
  'lua53-zlib'
  'scdoc'
)
checkdepends=(
  'cmocka'
)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('e64bf295c8d24bd437831f74633cb9c162bd149d41cd9fbb9cf3bb2b1c56213c')
b2sums=('399b7a4e1718f993ee19faebbadb110f5fc32555f7a647ab8af993937cb7fbb07486d0b33567b43bdcba887beed60571d2b78a692146d0676ae4f955b95382a6')

build() {
  arch-meson "${pkgname}-v${pkgver}" build \
    --sbindir=/usr/bin \
    --libdir=/usr/lib
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
