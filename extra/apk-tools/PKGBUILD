# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=apk-tools
pkgver=3.0.0
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'openssl'
  'zlib'
)
makedepends=(
  'meson'
  'lua53'
  'lua53-zlib'
  'scdoc'
)
checkdepends=(
  'cmocka'
)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('1b1ced0a1a6ffd9323c21d58521bfa2151424ee9daded4cde209007efe9e5794')
b2sums=('e417ed8156b429be2b4c15ed7cb8ff3fcb8eef6b3e1d0a9bf5932c301112de7ef5599be38293cd908b9ce4bc47a50955eef8e48bae6d4b4de3ba87722a0ea24e')

build() {
  arch-meson "${pkgname}-v${pkgver}" build \
    --sbindir=/usr/bin \
    --libdir=/usr/lib
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
