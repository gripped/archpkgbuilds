# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=apk-tools
pkgver=2.14.5
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'openssl'
  'zlib'
)
makedepends=(
  'lua53'
  'lua53-zlib'
  'scdoc'
)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('f61b45fc514b7b1e15817bdbb982a08ea235407f87acbb426529265fc0d02b5c')
b2sums=('4c13c96c8c6ffcf40e1ad2e8d19a9b8617ac0ced1f93cd019250453a147ccc4794e3fb6ca0da1c98e21d47e71fe7be4b725c9ab2ed08d9f0e6345ae80d6febf7')

prepare() {
  cd "${pkgname}-v${pkgver}"
  sed -i -e 's:-Werror::' Make.rules
}

build() {
  cd "${pkgname}-v${pkgver}"
  make SBINDIR=/usr/bin LIBDIR=/usr/lib
}

check() {
  cd "${pkgname}-v${pkgver}"
  make test
}

package() {
  cd "${pkgname}-v${pkgver}"
  make install DESTDIR="${pkgdir}" SBINDIR=/usr/bin LIBDIR=/usr/lib
}

# vim: ts=2 sw=2 et:
