# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=apk-tools
pkgver=3.0.3
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'openssl'
  'zlib'
)
makedepends=(
  'meson'
  'lua53'
  'lua53-zlib'
  'scdoc'
)
checkdepends=(
  'cmocka'
)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('a45214cb2135fbb7cddbdb7a7daab6179300b42eb9040f6a6c6e06061c5dffca')
b2sums=('2f146671a9ceca03cc8de87b2c5538368ec4a56168e967fe8094b5c82cb0b84771b696c44257d16dfab446cfd9d456833649cbc9ae1bcd0418cebd0495fdb1e0')

build() {
  arch-meson "${pkgname}-v${pkgver}" build \
    --sbindir=/usr/bin \
    --libdir=/usr/lib
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
