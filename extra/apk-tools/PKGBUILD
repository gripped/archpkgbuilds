# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=apk-tools
pkgver=3.0.2
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'openssl'
  'zlib'
)
makedepends=(
  'meson'
  'lua53'
  'lua53-zlib'
  'scdoc'
)
checkdepends=(
  'cmocka'
)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('987cef95e3b1cb3a365e016c9ab2566aaf440bccdc15a1650cdd0cd5ea3209e4')
b2sums=('e23243de41b5aefb0a21cfea82d6c630ffdee05999eff4d00f723aac56299e81c6a6223587ae723804874c148087d7ad05522e65c2dd41c8e0a27a4bea9d082b')

build() {
  arch-meson "${pkgname}-v${pkgver}" build \
    --sbindir=/usr/bin \
    --libdir=/usr/lib
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
