# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apk-tools
pkgver=2.14.3
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'openssl'
  'zlib'
)
makedepends=(
  'lua53'
  'lua53-zlib'
  'scdoc'
)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('5391ad16a7941c6550f098d282c6c9bbb8afdb5edd3e971d679a2e3a2fb5eb86')
b2sums=('f2be886fee1651a7aba643f7cfc60b467a3d16c96a606262453c727ad7bac61fbf67b83d36c4aa7bb85be817cfa285afa77b36dd784ee407384708ecfad3a4cc')

prepare() {
  cd "${pkgname}-v${pkgver}"
  sed -i -e 's:-Werror::' Make.rules
}

build() {
  cd "${pkgname}-v${pkgver}"
  make SBINDIR=/usr/bin LIBDIR=/usr/lib
}

check() {
  cd "${pkgname}-v${pkgver}"
  make test
}

package() {
  cd "${pkgname}-v${pkgver}"
  make install DESTDIR="${pkgdir}" SBINDIR=/usr/bin LIBDIR=/usr/lib
}

# vim: ts=2 sw=2 et:
