# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apk-tools
pkgver=2.14.1
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'openssl'
  'zlib'
)
makedepends=(
  'lua53'
  'lua53-zlib'
  'scdoc'
)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('c06487563cae9e92161dfe1a81e714f700229cc0ad075b6c26ac3f157892e732')
b2sums=('28ce9106972864e369cee98d82631ebd51424d425a23d858d7be374b45b493c4989277592b8437eb5265fa71d0b5a528c7a8aa9f899740279e45cce7bd6ab3b0')

prepare() {
  cd "${pkgname}-v${pkgver}"
  sed -i -e 's:-Werror::' Make.rules
}

build() {
  cd "${pkgname}-v${pkgver}"
  make SBINDIR=/usr/bin LIBDIR=/usr/lib
}

check() {
  cd "${pkgname}-v${pkgver}"
  make test
}

package() {
  cd "${pkgname}-v${pkgver}"
  make install DESTDIR="${pkgdir}" SBINDIR=/usr/bin LIBDIR=/usr/lib
}

# vim: ts=2 sw=2 et:
