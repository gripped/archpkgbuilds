# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Norbert Preining <norbert@preining.info>

pkgname=hdr10plus-tool
pkgver=1.7.0
pkgrel=1
pkgdesc="CLI utility to work with HDR10+ in HEVC files"
arch=(x86_64)
url=https://github.com/quietvoid/hdr10plus_tool
license=(MIT)
depends=(fontconfig)
makedepends=(
  cargo
  git
  gnu-free-fonts
)
_tag=a0725cb9419478dc0fc750f88e4e007a88c460ce
source=(git+https://github.com/quietvoid/hdr10plus_tool.git#tag=${_tag})
b2sums=('2853b84ede385fd7d14edf336c3a1e8b57e3e905fb109e27c4d856553b8b171a85c7cc4c9292af5d59b69cf4499499020c24a06c568c1ee5fc146db9c1a7bcd0')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path hdr10plus_tool/Cargo.toml
}

pkgver() {
  cd hdr10plus_tool
  git describe --tags
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path hdr10plus_tool/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path hdr10plus_tool/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path hdr10plus_tool \
    --root "${pkgdir}"/usr
  install -Dm 644 hdr10plus_tool/LICENSE -t "${pkgdir}"/usr/share/licenses/hdr10plus-tool/
}

# vim: ts=2 sw=2 et:
