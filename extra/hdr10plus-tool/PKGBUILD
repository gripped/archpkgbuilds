# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Norbert Preining <norbert@preining.info>

pkgname=hdr10plus-tool
pkgver=1.6.1
pkgrel=1
pkgdesc="CLI utility to work with HDR10+ in HEVC files"
arch=(x86_64)
url=https://github.com/quietvoid/hdr10plus_tool
license=(MIT)
depends=(fontconfig)
makedepends=(
  cargo
  git
  gnu-free-fonts
)
_tag=5a5b835a7269d84cd33473fd888cb9c09260a4de
source=(git+https://github.com/quietvoid/hdr10plus_tool.git#tag=${_tag})
b2sums=('9f8087186c7569645f0df05e4c91bf349be45b3c232e41dc821c4aa6c7836363f3721a87b6378bb3821d55b461ecabe6d74e23f6bbfdae4081310c9b1c5ca704')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path hdr10plus_tool/Cargo.toml
}

pkgver() {
  cd hdr10plus_tool
  git describe --tags
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path hdr10plus_tool/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path hdr10plus_tool/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path hdr10plus_tool \
    --root "${pkgdir}"/usr
  install -Dm 644 hdr10plus_tool/LICENSE -t "${pkgdir}"/usr/share/licenses/hdr10plus-tool/
}

# vim: ts=2 sw=2 et:
