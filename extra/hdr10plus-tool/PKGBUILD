# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Norbert Preining <norbert@preining.info>

pkgname=hdr10plus-tool
pkgver=1.7.1
pkgrel=1
pkgdesc="CLI utility to work with HDR10+ in HEVC files"
arch=(x86_64)
url=https://github.com/quietvoid/hdr10plus_tool
license=(MIT)
depends=(fontconfig)
makedepends=(
  cargo
  git
  gnu-free-fonts
)
_tag=8592f0f26ff64bfcc80d820c3cc2e080982eb98a
source=(git+https://github.com/quietvoid/hdr10plus_tool.git#tag=${_tag})
b2sums=('97d2d6240f41928a6fd1231ba275dfec5929f42cff69b2d4d8f6a4429cd71f6b93f39b8cbb7cf21f922d9b11ec532f2defdf885d280fcf48208a7aba546d15bd')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path hdr10plus_tool/Cargo.toml
}

pkgver() {
  cd hdr10plus_tool
  git describe --tags
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path hdr10plus_tool/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path hdr10plus_tool/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path hdr10plus_tool \
    --root "${pkgdir}"/usr
  install -Dm 644 hdr10plus_tool/LICENSE -t "${pkgdir}"/usr/share/licenses/hdr10plus-tool/
}

# vim: ts=2 sw=2 et:
