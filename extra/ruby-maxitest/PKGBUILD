# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-maxitest
pkgver=5.6.0
_commit=d2fb9270761e1f14467f345dde0c10e8eb970488
pkgrel=2
pkgdesc='Minitest + all the features you always wanted'
arch=(any)
url='https://github.com/grosser/maxitest'
license=(MIT)
depends=(
  ruby
  ruby-minitest
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  procps-ng
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=("git+$url.git?signed#commit=$_commit")
sha256sums=('70eb0df5a26f6b7cd89b17ea680a1e2acb7d93abf8ec005cd065facfe2adc9c0')
# curl https://github.com/grosser.gpg | gpg --import
validpgpkeys=('72170F9E193D0A5A31DB742FB19DB1587D698ED7') # Michael Grosser <michael@grosser.it>

prepare() {
  cd maxitest
  git rm Gemfile.lock

  # Remove dependency on bump and debug
  sed -r -e '/bump|debug/d' -i Gemfile
}

build() {
  local gemdir="$(gem env gemdir)"
  cd maxitest
  gem build maxitest.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$gemdir" \
    --bindir "tmp_install/usr/bin" \
    maxitest-$pkgver.gem
  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r "tmp_install/$gemdir/cache"
}

check() {
  local gemdir="$(gem env gemdir)"
  cd maxitest
  PATH="tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$gemdir" rake
}

package() {
  cd maxitest
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" MIT-LICENSE
}
