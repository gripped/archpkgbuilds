# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-maxitest
_pkgname=${pkgname#ruby-}
pkgver=6.0.1
pkgrel=2
pkgdesc='Minitest + all the features you always wanted'
arch=(any)
url='https://github.com/grosser/maxitest'
license=(MIT)
depends=(
  ruby
  ruby-minitest
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  procps-ng
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=("git+$url.git#tag=v$pkgver")
sha512sums=('ae0e246489a3c536f2adc81fdd1a9214a7dbee37dea191f8a21f41354e568f57440c46520560333f7ab5d9020642d8d1181a451c887ec56cae97a4e3a60fced2')

prepare() {
  cd "$_pkgname"
  git rm Gemfile.lock

  # Remove dependency on bump and debug
  sed -r -e '/bump|debug/d' -i Gemfile

  # Remove ruby-minitest version constraints
  sed -i 's/s.add_runtime_dependency "minitest".*/s.add_runtime_dependency "minitest"/' \
    maxitest.gemspec
}

build() {
  cd "$_pkgname"
  local gemdir="$(gem env gemdir)"

  gem build "$_pkgname.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "$_pkgname-$pkgver.gem"

  rm --force --recursive --verbose \
    "tmp_install/$gemdir/cache/" \
    "tmp_install/$gemdir/gems/$_pkgname-$pkgver/vendor/" \
    "tmp_install/$gemdir/doc/$_pkgname-$pkgver/ri/ext/"

  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
    -iname "*.o" -o \
    -iname "*.c" -o \
    -iname "*.so" -o \
    -iname "*.time" -o \
    -iname "gem.build_complete" -o \
    -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/$gemdir/extensions/" \
    -type f \
    \( \
    -iname "mkmf.log" -o \
    -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "$_pkgname"
  PATH="tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$(gem env gemdir)" rake
}

package() {
  cd "$_pkgname"
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" MIT-LICENSE
}
