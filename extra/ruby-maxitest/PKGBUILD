# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-maxitest
_pkgname=${pkgname#ruby-}
pkgver=6.0.0
pkgrel=2
pkgdesc='Minitest + all the features you always wanted'
arch=(any)
url='https://github.com/grosser/maxitest'
license=(MIT)
depends=(
  ruby
  ruby-minitest
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  procps-ng
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=("git+$url.git#tag=v$pkgver")
sha512sums=('280ee71fe849b016b97bf93ce545ac5259fdf379882cd6b356dd22e60eb21f2fa7e69265bfcdf74d594c2318d26b3fa5dc541fa7f83d1a7701315c0af637b6bb')
b2sums=('8e2115b482d3789a731aed99f961f41ab9910b8953f9d78dc54af4afc83d93c246bfe6bac0a4b125f7e05b325bf9044505cea35657a2058025dd0d9c0265a5e0')

prepare() {
  cd "$_pkgname"
  git rm Gemfile.lock

  # Remove dependency on bump and debug
  sed -r -e '/bump|debug/d' -i Gemfile
}

build() {
  cd "$_pkgname"
  local gemdir="$(gem env gemdir)"

  gem build "$_pkgname.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "$_pkgname-$pkgver.gem"

  rm --force --recursive --verbose \
    "tmp_install/$gemdir/cache/" \
    "tmp_install/$gemdir/gems/$_pkgname-$pkgver/vendor/" \
    "tmp_install/$gemdir/doc/$_pkgname-$pkgver/ri/ext/"

  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
    -iname "*.o" -o \
    -iname "*.c" -o \
    -iname "*.so" -o \
    -iname "*.time" -o \
    -iname "gem.build_complete" -o \
    -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/$gemdir/extensions/" \
    -type f \
    \( \
    -iname "mkmf.log" -o \
    -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "$_pkgname"
  PATH="tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$(gem env gemdir)" rake
}

package() {
  cd "$_pkgname"
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" MIT-LICENSE
}
