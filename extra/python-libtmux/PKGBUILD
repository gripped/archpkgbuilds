# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.23.0post0
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(
  python
  python-typing-extensions
  tmux
)
makedepends=(
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-pytest-rerunfailures
  procps-ng
)
optdepends=('python-pytest: for pytest plugin')
source=($_name-$pkgver.tar.gz::https://github.com/tmux-python/libtmux/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('8aca932ccb2026f554d4ba5eada22c1c3b755a500dea5c9a8836d98f6850335bd30520906b8ba1af1a3045e5c8514b44a6a2372454332f2c1fa99eaa5a507d46')
b2sums=('efaa3398f20521cb1118d7df560980e6d70972252f892c01a42bf7496a038da98fdbc32b0cd74b23e52d75d050328ba17d4ad06db10ef1ec05ef7d4ee3946b16')

prepare() {
  cd $_name-$pkgver
  # remove unknown pytest option: https://github.com/tmux-python/libtmux/issues/496
  sed 's/doctest-docutils-modules/doctest-modules/' -i setup.cfg
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages"
  pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
