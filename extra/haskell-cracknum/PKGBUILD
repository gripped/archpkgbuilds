# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=crackNum
pkgname=haskell-cracknum
pkgver=3.5
pkgrel=19
pkgdesc="Crack various integer and floating-point data formats"
url="https://github.com/LeventErkok/crackNum"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-libbf' 'haskell-sbv' 'haskell-tasty' 'haskell-tasty-golden')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4c68489ee421ac1255d58f0d0da127ba316981f4546c52dd6a9da15f2e68f7c9ddca52474d3728a6841303601ef5dafa842d97b0f0a2dd23395e1229b767132f')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
}

package() {
    cd $_hkgname-$pkgver

    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
