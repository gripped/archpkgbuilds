# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=hiprand
pkgver=7.1.0
pkgrel=1
pkgdesc='rocRAND marshalling library'
arch=('x86_64')
url='https://rocm.docs.amd.com/projects/hipRAND/en/latest/'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'hip-runtime-amd'
  'rocm-core'
  'rocrand'
)
makedepends=(
  'cmake'
  'gcc-fortran'
  'git'
  'rocm-cmake'
)
optdepends=('gcc-fortran: Use Fortran wrapper')
_git='https://github.com/ROCm/rocm-libraries'
source=("rocm-libraries::git+$_git.git#tag=rocm-$pkgver"
        "$pkgname-rocm-bin-path.patch")
sha256sums=('aaadb942b979b814dc436de772afab3120382aa0e4662e3c58d9a2ddfbfe790c'
            '144fb5162222d81559e847d31226f7b56215fa558a549c8be534cfba3fbca241')
options=(!lto)
_dirname="rocm-libraries/projects/$pkgname"

prepare() {
  cd "$_dirname"
  patch -Np1 -i "$srcdir/$pkgname-rocm-bin-path.patch"
}

build() {
  # -fcf-protection is not supported by HIP, see
  # https://rocm.docs.amd.com/projects/llvm-project/en/latest/reference/rocmcc.html#support-status-of-other-clang-options
  local cmake_args=(
    -Wno-dev
    -S "$_dirname"
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_TOOLCHAIN_FILE=toolchain-linux.cmake
    -D CMAKE_CXX_COMPILER=/opt/rocm/bin/hipcc
    -D CMAKE_CXX_FLAGS="${CXXFLAGS} -fcf-protection=none"
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
    -D BUILD_FORTRAN_WRAPPER=ON
  )
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$_dirname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
