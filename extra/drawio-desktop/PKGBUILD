# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Dmitry Valter <`echo ZHZhbHRlciA8YXQ+IHByb3Rvbm1haWwgPGRvdD4gY29tCg== | base64 -d`>

pkgname=drawio-desktop
pkgver=29.3.0
pkgrel=1
pkgdesc="Official Electron build of draw.io"
arch=(any)
url="https://www.drawio.com"
license=(Apache-2.0)
_electronver=38
_nodever=20
depends=(
  "electron$_electronver"
  hicolor-icon-theme
  sh
)
makedepends=(
  "nodejs>=$_nodever"
  git
  npm
  yarn
)
source=(
  "git+https://github.com/jgraph/drawio-desktop.git#tag=v$pkgver"
  "git+https://github.com/jgraph/drawio.git#tag=v$pkgver"
  "drawio.desktop"
  "drawio.sh"
  "drawio.xml"
)
sha512sums=('175a3a59baa4c7a446c925d475e4d435157f99c3afcd4cef229f7e4c93bcd4faad3c7813a384900a5fb79266a5a9a4f6a79c9d9809c28fb13336d7f0eddc128e'
            'dcdb9ebe6796f5dc04f58d112e7546db841436fbc09865d1807b8a05906eb0b6d221ba2cb1cf253a7b4c80de101554ec728c8e04d43690d35e8c1314f60ecde6'
            '01b1641e547fa66fc084927b9dab67d8fd789fbe07ba08f6ef9231f0acf862de137813d442a5e6da2def95c78b8c3e3bd2b72bbeec1eb1c356a16b2e405a62ca'
            'bb4f6383fbb6e9cfdf95528a8382b71778579eb051a54af7ef561bb8e12fc7259fc44b6718a5d481128b8fbb2c080f93a5876e95b0f172f68b3cd913ad80165e'
            'dc00aafb2c8aaeead2e80e60afa05687be8ce5406349a95f6618bf036f22b00b6a7ffa2831a097ba3b863d06168e4ece3b82d62eb1995ae945efef87d7d5314c')

prepare() {
  cd $pkgname
  sed -i "s/@_electronver@/$_electronver/" "$srcdir/drawio.sh"

  git submodule init
  git config submodule.drawio.url "$srcdir/drawio"
  git -c protocol.file.allow=always submodule update

  grep -q "\"version\": \"$pkgver\"" package.json \
    || ( echo "Version mismatch in package.json"; exit 1 )
  grep -qE '"electron": "\^?'$_electronver package.json \
    || ( echo "Electron version mismatch in package.json"; exit 1 )
  grep -q "\"node\": \">=$_nodever\"" package.json \
    || ( echo "Node version mismatch in package.json"; exit 1 )

  # Disable auto-updates
  sed -i 's/return false;/return true;/' src/main/disableUpdate.js

  # Remove unnecessary files from asar
  local to_remove=(
    drawio/docs
    drawio/etc
    drawio/src/main/java
    drawio/src/main/webapp/connect
    drawio/src/main/webapp/js/cryptojs
    drawio/src/main/webapp/js/deflate
    drawio/src/main/webapp/js/dropbox
    drawio/src/main/webapp/js/embed*
    drawio/src/main/webapp/js/freehand
    drawio/src/main/webapp/js/integrate.min.js
    drawio/src/main/webapp/js/jquery
    drawio/src/main/webapp/js/jszip
    drawio/src/main/webapp/js/mermaid
    drawio/src/main/webapp/js/onedrive
    drawio/src/main/webapp/js/orgchart
    drawio/src/main/webapp/js/rough
    drawio/src/main/webapp/js/sanitizer
    drawio/src/main/webapp/js/shapes.min.js
    drawio/src/main/webapp/js/simplepeer
    drawio/src/main/webapp/js/spin
    drawio/src/main/webapp/js/viewer-static.min.js
    drawio/src/main/webapp/js/viewer.min.js
    drawio/src/main/webapp/service-worker*
    drawio/src/main/webapp/workbox-*
  )
  rm -r "${to_remove[@]}"

  yarn install --frozen-lockfile
}

build() {
  cd $pkgname
  yarn run electron-builder --linux dir \
    -c.electronDist=/usr/lib/electron$_electronver \
    -c.electronVersion=$_electronver
}

package() {
  cd $pkgname
  install -vDm644 -t "$pkgdir/usr/lib/$pkgname" dist/linux-unpacked/resources/app.asar
  install -vDm755 "$srcdir/drawio.sh" "$pkgdir/usr/bin/drawio"

  install -vDm644 "$srcdir/drawio.xml" "$pkgdir/usr/share/mime/packages/drawio.xml"
  install -vDm644 -t "$pkgdir/usr/share/applications" "$srcdir/drawio.desktop"

  for icon in $(find build -regex '.*/[0-9]+x[0-9]+\.png' | sort -n); do
    size=$(basename -s .png "$icon")
    install -vDm644 "build/$size.png" "$pkgdir/usr/share/icons/hicolor/$size/apps/drawio.png"
  done
}
