# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=acme.sh
pkgver=3.0.8
pkgrel=1
pkgdesc='An ACME Shell script, an acme client alternative to certbot'
arch=('any')
url='https://github.com/acmesh-official/acme.sh'
license=('GPL3')
depends=('bash')
makedepends=('git')
optdepends=('socat: for standalone mode'
            'oath-toolkit: Used to compute the OTP for some DNS providers')
source=("git+$url.git#tag=$pkgver")
sha512sums=('bafc40c3459dad5fa7b7a93af5808d66db1a54a2501e66beb089ac53b250d5cde2a9b03c787cd7698f2030db7e77fe304e0bb3a0152baa83fa12ba8fb09b6105')

package() {
  cd $pkgname

  install -dm755 "$pkgdir"/usr/share/acme.sh/deploy
  install -dm755 "$pkgdir"/usr/share/acme.sh/dnsapi
  install -dm755 "$pkgdir"/usr/share/acme.sh/notify
  install -dm755 "$pkgdir"/usr/bin

  install -m755 acme.sh "$pkgdir"/usr/share/acme.sh/
  install -m755 deploy/*.sh "$pkgdir"/usr/share/acme.sh/deploy/
  install -m755 dnsapi/*.sh "$pkgdir"/usr/share/acme.sh/dnsapi/
  install -m755 notify/*.sh "$pkgdir"/usr/share/acme.sh/notify/

  ln -s /usr/share/acme.sh/acme.sh "$pkgdir"/usr/bin/acme.sh
}
