# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6widget
pkgver=6.0.43
pkgrel=2
pkgdesc='Deepin base graphical widgets library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6widget"
license=('LGPL-3.0-or-later')
# qt6-imageformats is needed for webp (dci) icons
depends=('dtk6core' 'dtk6gui' 'gcc-libs' 'glibc' 'libx11' 'libxcb' 'libxext' 'libxi'
         'qt6-base' 'qt6-imageformats' 'qt6-svg' 'startup-notification' 'xcb-util')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6widget.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('0cc90953f0e1b5a402768d6f74d183acc4d55d0cc31f2f546ffd35989f10c0cb612fff4ec60e4c2888ff1ee43ba22a998f17f4779dd1ca77ec05ba9a92c23d23'
            '981904e5c6a21aac54243e1c2a602dad02cb559dee16ebe03cf331fe0d6518d54d69760555738a9990ca6ab2e3438c0dcbad23f098815ea9164750a219eb5bab')

prepare() {
  cd dtk6widget
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd dtk6widget
  # Disable plugins because they are not going to be installed
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_PLUGINS=OFF \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=None
  ninja
}

check() {
  cd dtk6widget
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6widget
  DESTDIR="$pkgdir" ninja install
}
