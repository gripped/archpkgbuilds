# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6widget
pkgver=6.0.21
pkgrel=1
pkgdesc='Deepin base graphical widgets library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6widget"
license=('LGPL3')
# qt6-imageformats is needed for webp (dci) icons
depends=('dtk6core' 'dtk6gui' 'gcc-libs' 'glibc' 'libx11' 'libxcb' 'libxext' 'libxi'
         'qt6-base' 'qt6-imageformats' 'qt6-svg' 'startup-notification' 'xcb-util')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6widget.git#tag=$pkgver")
sha512sums=('c1ca8895facbd3b9d961ffa80f503fb0819368754f2677d6e5061b5f7457e6ce5f00497054131f22205e2dae6614752fcc6b58cbca7cb9cbc1d796f482dcc258')

build() {
  cd dtk6widget
  # Disable plugins because they are not going to be installed
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_PLUGINS=OFF \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver \
      -DCMAKE_BUILD_TYPE=None
  ninja
}

check() {
  cd dtk6widget
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6widget
  DESTDIR="$pkgdir" ninja install
}
