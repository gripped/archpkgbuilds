# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6widget
pkgver=6.0.18
pkgrel=2
pkgdesc='Deepin base graphical widgets library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6widget"
license=('LGPL3')
# qt6-imageformats is needed for webp (dci) icons
depends=('dtk6core' 'dtk6gui' 'gcc-libs' 'glibc' 'libx11' 'libxcb' 'libxext' 'libxi'
         'qt6-base' 'qt6-imageformats' 'qt6-svg' 'startup-notification' 'xcb-util')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6widget.git#tag=$pkgver"
         qt-6.8.patch)
sha512sums=('59337df062bd7583ad141fb8130e9863980da4fef1ce20874df9b13c535a1b2ab4dc67864cd1a296fcc677cae2b206c0f577a8ffd194d594ba515368f479d1a2'
            'c20a6c0ff1e9780527aac32b1219f89a463da1f002be56b291e84d1b532a52e71c822475164454c2a45714d1a997ba96c104d2155805e41381c93161cddbaf41')

prepare() {
  cd dtk6widget
  patch -p1 -i ../qt-6.8.patch
}

build() {
  cd dtk6widget
  # Disable plugins because they are not going to be installed
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_PLUGINS=OFF \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver \
      -DCMAKE_BUILD_TYPE=None
  ninja
}

check() {
  cd dtk6widget
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6widget
  DESTDIR="$pkgdir" ninja install
}
