# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemadapter
pkgver=0.13.0
pkgrel=3
pkgdesc="Common interface for data container classes"
url="https://github.com/scrapy/itemadapter"
license=('BSD-3-Clause')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling' 'python-wheel')
checkdepends=('python-attrs' 'python-pydantic' 'python-pytest' 'scrapy')
source=("git+https://github.com/scrapy/itemadapter.git#tag=v$pkgver")
sha512sums=('4a28ce1dca6c5cfa03804ba31b551c69bc5a95d170463cb456042865280bdbb8d8c08a7261f461b638e956283581c7472ad7e4d79fe71946a369633db7c5a4d9')

build() {
  cd itemadapter
  python -m build --wheel --no-isolation
}

check() {
  cd itemadapter
  pytest
}

package() {
  cd itemadapter
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
