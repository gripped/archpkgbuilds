# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemadapter
pkgver=0.12.2
pkgrel=1
pkgdesc="Common interface for data container classes"
url="https://github.com/scrapy/itemadapter"
license=('BSD-3-Clause')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling' 'python-wheel')
checkdepends=('python-attrs' 'python-pydantic' 'python-pytest' 'scrapy')
source=("git+https://github.com/scrapy/itemadapter.git#tag=v$pkgver")
sha512sums=('a5b31961001491888586d371bf6694783039526fad0bf5aedc99accba4f94092256e89f7db44e26ea5da87a433cfa5e31fc7bcac4f7ecc546e4904537325afa5')

build() {
  cd itemadapter
  python -m build --wheel --no-isolation
}

check() {
  cd itemadapter
  pytest
}

package() {
  cd itemadapter
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
