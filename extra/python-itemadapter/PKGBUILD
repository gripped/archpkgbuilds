# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemadapter
pkgver=0.12.1
pkgrel=1
pkgdesc="Common interface for data container classes"
url="https://github.com/scrapy/itemadapter"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-hatchling' 'python-wheel')
checkdepends=('python-attrs' 'python-pydantic' 'python-pytest' 'scrapy')
source=("https://github.com/scrapy/itemadapter/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('477bdf001cbdf830675f4f70004970999bd42acac3ca690931e54f22f93b5ae44de8d3fd3d770f5b1e5cd29367b5197b9b4cb95de0305ba8bddd83eb182ac21a')

prepare() {
  cd itemadapter-$pkgver
}

build() {
  cd itemadapter-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd itemadapter-$pkgver
  pytest
}

package() {
  cd itemadapter-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
