# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Adam Griffiths <adam_griffithsAATTdart.net.au>

pkgname=ode
pkgver=0.16.6
pkgrel=1
pkgdesc='High performance library for simulating rigid body dynamics'
arch=('x86_64')
url='https://bitbucket.org/odedevs/ode/'
license=('LGPL-2.1-or-later OR BSD-3-Clause')
depends=(
  'bash'
  'gcc-libs'
  'glibc'
)
source=("$url/downloads/$pkgname-$pkgver.tar.gz")
sha512sums=('57b4caca0bdd9a577239ba8cdfaeddd3316523a0f0835470caccc31793fb2231f62e3a16e3b8693c8b8a42c336b1c782f6427e60f949a6fba2bfd216b107a946')

prepare() {
  cd $pkgname-$pkgver
  ./bootstrap
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-double-precision \
    --enable-libccd \
    --enable-ou \
    --enable-silent-rules
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" make install
  install -vDm644 ou/LICENSE-BSD.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-BSD.TXT"
  install -vDm644 GIMPACT/GIMPACT-LICENSE-BSD.TXT "$pkgdir/usr/share/licenses/$pkgname/GIMPACT-LICENSE-BSD.TXT"
  install -vDm644 libccd/BSD-LICENSE "$pkgdir/usr/share/licenses/$pkgname/BSD-LICENSE"
}
