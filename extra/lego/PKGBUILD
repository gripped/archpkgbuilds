# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.28.0
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
arch=(x86_64)
url='https://go-acme.github.io/lego'
license=(MIT)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+https://github.com/go-acme/lego.git#tag=v$pkgver")
sha512sums=('7f2e8688adcdf8bdd26d61a32696ce59d194db338b713a0d859b107ece6dc000196cf92b9b25264e89c8c809349916ea3121abad38bf98998de89336ba708d6b')
b2sums=('675ed73db4815203aef09825e929cb23bf9bef19c40e497a5e96c27d86d7c304cbdabfae1e3fd57218d328b33bcac7d366b7c17e27310268825c1cb605ee0a9a')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
