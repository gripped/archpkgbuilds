# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.31.0
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
arch=(x86_64)
url='https://go-acme.github.io/lego'
license=(MIT)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+https://github.com/go-acme/lego.git#tag=v$pkgver")
sha512sums=('a8f7ce403529b52dc768a058db0cb1254a76947473e533ee9ad1be8b236ea2e1b7476960761dcadaaff9e9b2e6db2f25211a9114f15d40610425500b4a9d9fd4')
b2sums=('b9a5a2c6507bcd0c9623a1d60034642627a3415c26a50945266b18c97d55ff31077f239797bc406ef4242e9d6e5a2ab437acec15ad494472c4f1b087184288fa')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
