# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.25.1
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
url='https://go-acme.github.io/lego'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/go-acme/lego.git#tag=v$pkgver")
sha512sums=('062af7e6520bf833509642eca6a6cb03531aef9badde9b573617b5a2cf931850dc9033e250b4a72d127653cf4ac92747d7e533ea363e5d7211ce9198c1c22240')
b2sums=('d601e0490ffb29d9fd1428a64555d805e652c86d1c8bc34d2e5f53bff370354c7f060d64a6c959f05974c78ee688ddd2704a1e3713540a828aad79025bab949c')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
