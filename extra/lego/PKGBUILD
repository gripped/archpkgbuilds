# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.23.1
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
url='https://go-acme.github.io/lego'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/go-acme/lego.git#tag=v$pkgver")
sha512sums=('a58df951acf3ff60c21fb146dfcc230512e104c95945376f0d7cefdfdac6c86c4b6fe011361965b28a699c95468c531e563c9942017aaa4d6e5649ca968f027d')
b2sums=('6561d2f262e0198515ee26c77b1291abba4076993d07fc4941f56d744007ef40355a4562d2e05cdf65f1ad858c283639383a3eb5c4a7356b1ccc49569f5b2bc4')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
