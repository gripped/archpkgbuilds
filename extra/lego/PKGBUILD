# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.26.0
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
url='https://go-acme.github.io/lego'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/go-acme/lego.git#tag=v$pkgver")
sha512sums=('67fbecc4a97aa555afe4cac49d23263a61a6c28a9ea25ef780a789e5fbc50c3227d42006ad4d0b170b1965c2a291a9c219fd2185232b74ee0954296faa71e331')
b2sums=('7cb39288219d6dcc50d97d23aa2c2d164d8b40e39fe4e2966bc965a2567827017184149db09f5ee0dfaca6041cba6a8f0a0e8e6f4d4874bd01c79b0d91d19432')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
