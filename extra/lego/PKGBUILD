# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.28.1
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
arch=(x86_64)
url='https://go-acme.github.io/lego'
license=(MIT)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+https://github.com/go-acme/lego.git#tag=v$pkgver")
sha512sums=('a14443f010adfe1522bb02966fa837ca3c30ca1180c66285832b4b147f31d20542ebdd816c6e700c762b22af05043a776016f4ed02e629fc5b0af1ebdd163c48')
b2sums=('657895b7356e217d09de97033d259307e71f2fa01ebe73964444651e0436b02d2ac4b4f5144c0410c7ef9a1500b67436b8f941fffa6803b68576a45092c29bf9')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
