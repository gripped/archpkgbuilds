# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.29.0
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
arch=(x86_64)
url='https://go-acme.github.io/lego'
license=(MIT)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+https://github.com/go-acme/lego.git#tag=v$pkgver")
sha512sums=('ef81d923d2e50f76ce81b1e2ac87bc89baef3d859ff038f1e99e47ecd80d4da377954679cf0e26ccf2c5c55f1a15acfbe49f9580e007ef0fdcbca7d9e33fb873')
b2sums=('3e2462e4b1b58b7b6e0d8c3d4460092f20ff64940d81e11105e798c47acc7bdfec1754e84292851efd0201dfa1dafcde88a244ef5d5ca0fd71f661f38ce09127')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
