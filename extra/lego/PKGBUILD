# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.32.0
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
arch=(x86_64)
url='https://go-acme.github.io/lego'
license=(MIT)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+https://github.com/go-acme/lego.git#tag=v$pkgver")
sha512sums=('a059ca9b9a903b81b6db17ab936e086743366940563204b74d7583b2f6b696abb4f75b5c7ab78f39cac2e68778184f331c92512d547c23f28d7d34819c3b55ae')
b2sums=('f4fb9d633268c29385946d46003cddccb0cee85b997f481138d0db7f320ad26259a23bd561d3b530344d973b96f87928f6707e65cae582537aa25406a8b59090')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
