# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-client
pkgver=3.3.0
pkgrel=1
pkgdesc="Simple command-line client for controlling cdemu-daemon"
url="https://cdemu.sourceforge.io/"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  cdemu-daemon
  glib2
  python
  python-gobject
)
makedepends=(
  bash-completion
  cmake
  git
  intltool
  ninja
)
conflicts=(cdemu)
source=("cdemu-code::git+https://git.code.sf.net/p/cdemu/code#tag=$pkgname-$pkgver")
b2sums=('2efad5bc2d914ed92953a131d66f5ef8c0afe664a8679c9ff6658d9f0f2c9f0b7bdecbbf348a99035cfbda7ce734686d3875e273901cb38e95f4fd46206423d2')

prepare() {
  cd cdemu-code/$pkgname
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
  )

  cmake -S cdemu-code/$pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 cdemu-code/$pkgname/README
}

# vim:set sw=2 sts=-1 et:
