# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=nota
pkgname=maui-$_pkgname
pkgver=4.0.1
pkgrel=1
pkgdesc='Multi-platform text editor'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         mauikit
         mauikit-filebrowsing
         mauikit-terminal
         mauikit-texteditor
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.xz{,.sig}
        fix-build.patch)
sha256sums=('b673c33f06653c97406c2118655a54c8ce4655eb889729a86672baec74931457'
            'SKIP'
            '2454ffc26c87da5855c0f6b3ff88635ec79c82f998f1257ed4a5d2b86389ea21')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

prepare() {
  patch -d $_pkgname-$pkgver -p1 < fix-build.patch
}

build() {
  cmake -B build -S $_pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
