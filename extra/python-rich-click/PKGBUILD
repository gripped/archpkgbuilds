# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=python-rich-click
_name=${pkgname#python-}
pkgver=1.8.9
pkgrel=1
pkgdesc='Format click help output nicely with rich.'
arch=(any)
url='https://github.com/ewels/rich-click'
depends=(python python-click python-typing_extensions python-rich python-importlib-metadata)
makedepends=(python-build python-installer python-wheel python-setuptools git)
checkdepends=(python-pytest)
license=(MIT)
source=(git+$url.git#tag=v$pkgver)
b2sums=('7e363dcf4becd5d0c3794aa55ddd47bdb738e861e138928e09634073f519a3b4cb6fe30070ee295224a3bbbd39b463c857d3cf6bef8fda6e4aa69857af16c7c7')

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  cd $_name
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -P -m pytest
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
