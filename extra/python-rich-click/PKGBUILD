# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=python-rich-click
_name=${pkgname#python-}
pkgver=1.9.7
pkgrel=1
pkgdesc='Format click help output nicely with rich.'
arch=(any)
url='https://github.com/ewels/rich-click'
depends=(python python-click python-typing_extensions python-rich python-importlib-metadata)
makedepends=(python-build python-installer python-wheel python-setuptools git)
checkdepends=(python-pytest python-typer python-inline-snapshot ruff)
license=(MIT)
source=(git+$url.git#tag=v$pkgver
  coverage.patch)
b2sums=('22bd625411379fd1ee34f19097cc4b2d2cc652aabcf9d8d9b468902827392664f7bb1d52d0daf3403e3a14a2511b61721c31a7b1527f907e8d54b997baf19275'
        'ce76531f596c44b9da8b5ae0b462a9a41e1003f46a2b8b7141f75ae127e0c285dcf0ae4cc0655381f839c196f8097789e83c6f236c5661d8cf30b20c455329d8')

prepare() {
  cd $_name
  # patch pytest command line args for a newer version
  patch -p1 <../coverage.patch
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  cd $_name
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -P -m pytest -k "not test_markdown_help_rich_12 and not test_markdown_help_text_markup_field_rich_12"
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
