# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Christian Visintin <christian dot visintin at gmail dot com>
# Contributor: Luis Martinez <luis dot martinez at tuta dot io>

pkgname=termscp
pkgver=0.19.0
pkgrel=1
pkgdesc="A feature rich terminal UI file transfer and explorer"
arch=('x86_64')
url="https://github.com/veeso/termscp"
license=('MIT')
depends=('gcc-libs' 'zlib' 'openssl' 'dbus' 'smbclient')
makedepends=('cargo' 'git')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('16da20a8e252a18c6be54058b02e518f6e47a46e9353675b1fece1aeae877518ea2ffdcede4ed031fdd6f398170a55d1eb15cccffda73f04218eb77ec030b35a')
options=('!lto')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  # `termscp -v` will show "makepkg" branch without this
  export VERGEN_GIT_BRANCH=main
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen -- \
    --skip system::environment::tests::test_system_environment_get_config_dir_err \
    --skip system::keys::keyringstorage::tests::test_system_keys_keyringstorage \
    --skip system::environment::tests::test_system_environment_get_config_dir
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
