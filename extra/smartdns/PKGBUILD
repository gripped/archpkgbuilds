# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=46.1
pkgrel=1
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('gcc-libs' 'openssl')
makedepends=('git' 'systemd')  # systemd.pc
checkdepends=('gtest' 'bind')
source=("git+https://github.com/pymumu/smartdns.git#tag=Release$pkgver")
sha512sums=('08fbd0583e03ba40f5f1b72e22b86cb6da5bc7b438f05d66603c503cdf7db5038520e0b357497bdb8d7ce010be7ab75456773d4258bde2f01d5aa23f0700b201')

prepare() {
  # Unfortunately upstream does not provide a way to reuse .o files
  cp -a smartdns smartdns-test
}

build() {
  cd smartdns
  make RUNSTATEDIR=/run SBINDIR=/usr/bin
}

check() {
  cd smartdns-test
  make -C test test
}

package() {
  cd smartdns
  make RUNSTATEDIR=/run SBINDIR=/usr/bin DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/etc/init.d
}
