# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Peter Jung <ptr1337@archlinux.org>

pkgbase=ghostty
pkgname=(ghostty ghostty-shell-integration ghostty-terminfo)
pkgver=1.1.2
pkgrel=2
pkgdesc='Fast, native, feature-rich terminal emulator pushing modern features'
arch=(x86_64 aarch64 i686)
url="https://github.com/ghostty-org/$pkgbase"
license=(MIT)
depends=(bzip2
         fontconfig libfontconfig.so
         freetype2 libfreetype.so
         gcc-libs # ld-linux-x86-64.so
         glibc # libc.so libm.so
         glib2 libglib-2.0.so libgio-2.0.so libgobject-2.0.so
         gtk4 libgtk-4.so
         libx11 # libX11.so
         harfbuzz libharfbuzz.so
         libadwaita libadwaita-1.so
         libpng
         oniguruma # libonig.so
         pixman
         zlib)
makedepends=(pandoc-cli
             zig)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz"
        0001-Use-epoll.patch)
sha256sums=('54d74a49df9f2e4b9a8b7c88372bdb78f4e3c4f072f6cee197c873e90ba27d19'
            'c2de4d291fa600c0a6173c6ed0a5c9312e09df89b9ffec64f271420345aa534c')

prepare() {
	cd "$_archive"

	# Use epoll instead of io_uring
	# https://gitlab.archlinux.org/archlinux/packaging/packages/ghostty/-/issues/2
	patch -Np1 -i ../0001-Use-epoll.patch

	export ZIG_GLOBAL_CACHE_DIR="$srcdir/zig-global-cache/"
	zig build --fetch
}

build() {
	cd "$_archive"
	DESTDIR=build zig build \
		--summary all \
		--prefix "/usr" \
		--system "$srcdir/zig-global-cache/p" \
		-Doptimize=ReleaseFast \
		-Dgtk-x11=true \
		-Dcpu=baseline \
		-Dpie=true \
		-Demit-docs \
		-Dversion-string="$pkgver-arch$pkgrel"
}

package_ghostty() {
	depends+=(ghostty-shell-integration
	          ghostty-terminfo)
	cd "$_archive"
	cp -a build/* "$pkgdir/"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	rm -r "$pkgdir"/usr/share/terminfo
	rm -r "$pkgdir"/usr/share/ghostty/shell-integration
}

package_ghostty-shell-integration() {
	pkgdesc='Shell integration scripts for ghostty'
	depends=()
	cd "$_archive"
	mkdir -p "$pkgdir/usr/share/ghostty/shell-integration"
	cp -r build/usr/share/ghostty/shell-integration/* "$pkgdir/usr/share/ghostty/shell-integration/"
}

package_ghostty-terminfo() {
	pkgdesc='Terminfo for ghostty'
	depends=()
	cd "$_archive"
	mkdir -p "$pkgdir/usr/share/terminfo"
	cp -r build/usr/share/terminfo/* "$pkgdir/usr/share/terminfo/"
}
