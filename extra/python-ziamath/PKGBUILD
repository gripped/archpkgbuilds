# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziamath
pkgver=0.12
pkgrel=2
pkgdesc="Render MathML and LaTeX Math to SVG without Latex installation"
url="https://ziamath.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python-ziafont' 'python-latex2mathml')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("git+https://github.com/cdelker/ziamath.git#tag=$pkgver")
sha512sums=('951f10a058f702fb02a252dab80de1c3039f458d75e7b1639ff37433444de91305ac9df9e5f3bae67e97b129d9a50ce4b2c31471b8bf70f66de29ae800ce0c57')

build() {
  cd ziamath
  python -m build -nw
}

package() {
  cd ziamath
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
