# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziamath
pkgver=0.10
pkgrel=2
pkgdesc="Render MathML and LaTeX Math to SVG without Latex installation"
url="https://ziamath.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python-ziafont' 'python-latex2mathml')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("git+https://github.com/cdelker/ziamath.git#tag=$pkgver")
sha512sums=('85505a9c910856764b783354f81e2bf3c2d3cf9e5b64e0bb844208b2552a310e230229cc1d3e63aeea284b3498c593d53ef3be7bbff0ff321cd9319f93af9b9f')

build() {
  cd ziamath
  python -m build -nw
}

package() {
  cd ziamath
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
