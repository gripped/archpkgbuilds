# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=linuxsampler
pkgver=2.4.2
pkgrel=2
pkgdesc="Professional-grade audio sampler alternative to Gigasampler"
arch=(x86_64)
url="https://www.linuxsampler.org/"
license=(LicenseRef-LinuxSampler-Non-Commercial)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  sqlite
)
makedepends=(
  alsa-lib
  dssi
  jack
  ladspa
  libgig
  libsndfile
  lv2
  perl-xml-parser
)
optdepends=(
  'dssi-host: for DSSI plugin'
  'lv2-host: for LV2 plugin'
)
provides=(liblinuxsampler.so)
source=(
  https://download.linuxsampler.org/packages/$pkgname-$pkgver.tar.bz2
)
sha512sums=('9bbb8d85fcf10d7720f9b30872d1222f64c59bb16ea726379585864e20e888ff191dbdc77e87ef7e522d29b37ff129ef8abd0a643a749ba52ad5ee12ad615b77')
b2sums=('39bbf327ad81638ca60085b9016855b175987eb5ccd3754d4c56345e4c36566cfe0b91be563bcd69aeb2035ef2cdceb6c81dd91dfcdb9f7959c2fda3466f9d16')

prepare(){
  # extract custom license exception
  sed -n '11,15p' $pkgname-$pkgver/README > LinuxSampler-Non-Commercial.txt

  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  # Prevent code generation from creating unreproducible results.
  make -j1
}

package() {
  depends+=(
    alsa-lib libasound.so
    jack libjack.so
    libgig libgig.so
    libsndfile libsndfile.so
  )

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # NOTE: Due to autotools obfuscating how to properly configure the installation location of the shared libraries in /usr/lib/, we have to move files manually... :(
  #       In the past we have used patches to the Makefile.in files, but with running `autoreconf`, this no longer works.
  mv -v "$pkgdir/usr/lib/$pkgname/lib$pkgname"* "$pkgdir/usr/lib/"

  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ../LinuxSampler-Non-Commercial.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  # lscp files conflict with nilfs-utils:
  # https://bugs.archlinux.org/task/45827
  mv -v "$pkgdir/usr/bin/lscp" "${pkgdir}/usr/bin/lscp-$pkgname"
  mv -v "$pkgdir/usr/share/man/man1/lscp.1" "$pkgdir/usr/share/man/man1/lscp-$pkgname.1"
}
# vim:set ts=2 sw=2 et:
