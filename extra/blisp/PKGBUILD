# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=blisp
pkgver=0.0.5
pkgrel=1
pkgdesc='ISP tool & library for Bouffalo Labs RISC-V Microcontrollers and SoCs'
arch=(x86_64)
url=https://github.com/pine64/blisp
license=(MIT)
depends=(glibc)
makedepends=(
  cmake
  git
  ninja
)
_tag=caccc7818a31d5ba33d313fda7d7610d565a5fea
options=(!debug)
source=(
  git+https://github.com/pine64/blisp.git#tag=${_tag}
  git+https://github.com/argtable/argtable3.git
  git+https://github.com/sigrokproject/libserialport.git
)
b2sums=('2a50ccb55702a490bdcfe729373c88eb97b518763f0bcd710933dec8d5a21ce3644986320c4c9cd30b8dfbc482f5ab0f344665260e985d5442e82c3c5185a8c6'
        'SKIP'
        'SKIP')

prepare() {
  cd blisp
  for submodule in vendor/{argtable3,libserialport}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

pkgver() {
  cd blisp
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S blisp -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DBLISP_BUILD_CLI=ON \
    -Wno-dev
  cmake --build build
}

package() {
  install -Dm 755 build/tools/blisp/blisp -t "${pkgdir}"/usr/bin/
  install -Dm 644 blisp/LICENSE -t "${pkgdir}"/usr/share/licenses/blisp/
}

# vim: ts=2 sw=2 et:
