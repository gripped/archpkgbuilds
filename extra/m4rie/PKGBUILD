# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=m4rie
pkgver=20250102
pkgrel=2
pkgdesc='Algorithms for linear algebra over F_2^e'
arch=(x86_64)
url='https://bitbucket.org/malb/m4rie'
license=(GPL-2.0-or-later)
depends=(glibc
         m4ri)
makedepends=(git)
source=(git+https://bitbucket.org/malb/m4rie#tag=release-$pkgver)
sha256sums=('9d6c4e1df85d8e2701a379e9ad12f9bb7a5815e33d74b6872ba2a341339c9197')

prepare() {
  cd $pkgname
  autoreconf -vi
  sed -e 's|const mzd_t|mzd_t|g' -i src/m4ri_functions.h
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-openmp
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
