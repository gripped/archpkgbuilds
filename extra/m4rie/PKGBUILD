# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=m4rie
pkgver=20200125
pkgrel=6
pkgdesc='Algorithms for linear algebra over F_2^e'
arch=(x86_64)
url='https://bitbucket.org/malb/m4rie'
license=(GPL-2.0-or-later)
depends=(glibc
         m4ri)
makedepends=(git)
source=(git+https://bitbucket.org/malb/m4rie#tag=release-$pkgver)
sha256sums=('74af1853527a12cd7fd7ff09bc9971f057dfd20c8675c307919b30e1e2fbcd57')

prepare() {
  cd $pkgname
  autoreconf -vi
# Fix build with m4ri 20240729
  git cherry-pick -n 54d6f7231db01d9902588f7b3eb8a6f8acd133f4 \
                     9a05f209bbd3789ad94de11c06793e3ba5a55ff7 \
                     1fa0c6a7f312049e9519477348dff3fd22dd6ed0 \
                     7de883ba16d20175211656c7849ae6476d155fdd \
                     e40cf0db2d4b0fd59c6ee79d2292a552161ff2af \
                     571ff9e367032d3c2479ab0bb708aed1904d433a
  sed -e 's|const mzd_t|mzd_t|g' -i src/m4ri_functions.h
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-openmp
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
