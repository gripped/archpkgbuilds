# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-desktop-theme
pkgver=1.1.2
pkgrel=1
pkgdesc='Theme resources for Deepin desktop environment'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-theme"
license=('CCPL:by-4.0')
makedepends=('git' 'cmake' 'ninja')
optdepends=('adobe-source-han-sans-otc-fonts: theme standard font')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-desktop-theme.git#tag=$pkgver")
sha512sums=('71560180cdf499380d331a0dcb5a6106eb444789777a273582a3c7cc86ba1e4ba54261784af3974d2fe59593b51ff6ff5d547cc9e38a8642e685aa273c9e2e66')

build() {
  cd deepin-desktop-theme
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-desktop-theme
  DESTDIR="$pkgdir" ninja install
}
