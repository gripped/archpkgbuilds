# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-desktop-theme
pkgver=1.1.13
pkgrel=1
pkgdesc='Theme resources for Deepin desktop environment'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-theme"
license=('CC-BY-4.0')
makedepends=('git' 'cmake' 'ninja')
optdepends=('adobe-source-han-sans-otc-fonts: theme standard font')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-desktop-theme.git#tag=$pkgver")
sha512sums=('078690635c30d2b921f9f973be5e958417c57ae5260316aa2480376fb162ad795d4ea4e38536792f641c39090524e8ec7232cec21f3000d6dd51f702fd21f7dd')

prepare() {
  cd deepin-desktop-theme
  # remove the tool, suggested by upstream
  sed -i '/xdgicon2dci/d' tools/CMakeLists.txt
}

build() {
  cd deepin-desktop-theme
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-desktop-theme
  DESTDIR="$pkgdir" ninja install
}
