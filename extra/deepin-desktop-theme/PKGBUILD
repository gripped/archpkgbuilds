# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-desktop-theme
pkgver=1.1.8
pkgrel=1
pkgdesc='Theme resources for Deepin desktop environment'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-theme"
license=('CC-BY-4.0')
makedepends=('git' 'cmake' 'ninja')
optdepends=('adobe-source-han-sans-otc-fonts: theme standard font')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-desktop-theme.git#tag=$pkgver")
sha512sums=('9e15dafb6982a1adf9e7c0f0f75f6bd67e6b490fce001517c775b1c176ccbf7b50b0317e26c0a6dff43df17b6d3392a4c86dce93bd81ed932e69bd472a3a0759')

build() {
  cd deepin-desktop-theme
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-desktop-theme
  DESTDIR="$pkgdir" ninja install
}
