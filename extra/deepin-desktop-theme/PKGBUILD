# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-desktop-theme
pkgver=1.1.10
pkgrel=1
pkgdesc='Theme resources for Deepin desktop environment'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-theme"
license=('CC-BY-4.0')
makedepends=('git' 'cmake' 'ninja')
optdepends=('adobe-source-han-sans-otc-fonts: theme standard font')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-desktop-theme.git#tag=$pkgver")
sha512sums=('de9fe3ffd45ffcb208dee46774a60e07d35b8c8c8b806dea0587ddbe020f696b686f872c6cd1dbfc4d281e77652259ac69c944068d1f2db87506041f6a5ecb5f')

prepare() {
  cd deepin-desktop-theme
  # remove the tool, suggested by upstream
  sed -i '/xdgicon2dci/d' tools/CMakeLists.txt
}

build() {
  cd deepin-desktop-theme
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-desktop-theme
  DESTDIR="$pkgdir" ninja install
}
