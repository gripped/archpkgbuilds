# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rouge
pkgname=ruby-rouge
pkgver=4.5.1
pkgrel=3
pkgdesc='Pure-ruby code highlighter that is compatible with pygments'
url='https://rouge.jneen.net/'
arch=('any')
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-memory_profiler
  ruby-minitest
  ruby-minitest-power_assert
  ruby-pry
  ruby-puma
  ruby-rake
  ruby-redcarpet
  ruby-sinatra
  ruby-yard
)
options=('!emptydirs')
source=("https://github.com/jneen/rouge/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f0659597cb04d04c48d1d20722be870f04fec5cf92f5b47f9ba6dde8e653a359c2a6798a1bb9f8785682e218390f16ebf5cba5c4582604a0a3a7a3596e0139b1')
b2sums=('b74cd8aeec73b35fd2670b611ae1ab3220a0d98ee85cd0abe2d008fa1a34aa827f1e66aab770ec867d4b59b9491e61339be0bb5e896dfeb4b93d77759ba256ff')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place --regexp-extended \
    --expression '/changelog/d' \
    --expression '/git/d' \
    --expression '/rubocop/d' \
    --expression '/shotgun/d' \
    Gemfile \
    Rakefile

  rm --verbose \
    tasks/check/style.rake \
    tasks/update/changelog.rake
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake check:specs check:warnings
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
