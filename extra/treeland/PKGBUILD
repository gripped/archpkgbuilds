# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=treeland
pkgver=0.7.3
pkgrel=3
pkgdesc='New wayland compositer for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/treeland"
license=('Apache-2.0' 'LGPL-3.0-only' 'GPL-2.0-only' 'GPL-3.0-only')
depends=('ddm' 'dtk6core' 'dtk6declarative' 'dtk6systemsettings' 'gcc-libs' 'glibc' 'jemalloc'
         'libdrm' 'libinput' 'pam' 'pixman' 'qt6-base' 'qt6-declarative'
         'systemd-libs' 'wayland' 'wlroots0.19')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols' 'wlr-protocols'
             'treeland-protocols' 'vulkan-headers')
replaces=('waylib' 'qwlroots')
conflicts=('waylib' 'qwlroots')
source=("git+https://github.com/linuxdeepin/treeland.git#tag=$pkgver"
         qt-6.10.patch)
sha256sums=('add1dcd2588275fbf85d292fe2dd416ef31789ee21aa61b2cf548962770a509e'
            '9563ff0e432de3160965627cfae4537d448112aa355e310c40a01149ce76dedd')

prepare() {
    cd treeland
    sed -i '/Q_MOC_INCLUDE(<QDBusObjectPath>)/aQ_MOC_INCLUDE("treelandconfig.hpp")' src/seat/helper.h
    patch -p1 -i ../qt-6.10.patch
}

build() {
	cd treeland
    # TODO: Fix RUNPATH issue
	cmake . -GNinja \
		-DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc \
		-DCMAKE_INSTALL_PREFIX=/usr
        # -DCMAKE_SKIP_RPATH=ON
	cmake --build .
}

package() {
	cd treeland
	DESTDIR="$pkgdir" ninja install
}
