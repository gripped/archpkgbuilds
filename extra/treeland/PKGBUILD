# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=treeland
pkgver=0.8.2
pkgrel=1
pkgdesc='New wayland compositer for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/treeland"
license=('Apache-2.0' 'LGPL-3.0-only' 'GPL-2.0-only' 'GPL-3.0-only')
# xwayland is needed for XWayland support (not optional)
depends=('ddm' 'dtk6core' 'dtk6declarative' 'dtk6systemsettings' 'gcc-libs' 'glibc'
         'libdrm' 'libinput' 'mpv' 'pam' 'pixman' 'qt6-base' 'qt6-declarative'
         'systemd-libs' 'wayland' 'wlroots0.19' 'xorg-xwayland' 'deepin-session')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols' 'wlr-protocols'
             'treeland-protocols' 'vulkan-headers')
groups=('deepin')
replaces=('waylib' 'qwlroots')
conflicts=('waylib' 'qwlroots')
source=("git+https://github.com/linuxdeepin/treeland.git#tag=$pkgver"
         qt-6.10.2.patch)
sha256sums=('4625eec4879fda4bebf715a79f005ae2792c6486e97aed7811d50d267275ca15'
            '47090d70005a8913effc9a2cef59c5bc3a88cba6caec6b0e9b11148e110cef43')

prepare() {
  patch -d treeland -p1 < qt-6.10.2.patch
}

build() {
	cd treeland
    # TODO: Fix RUNPATH issue
	cmake . -Bbuild -GNinja \
		-DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc \
		-DCMAKE_INSTALL_PREFIX=/usr
        # -DCMAKE_SKIP_RPATH=ON
	cmake --build build
}

package() {
	cd treeland
	DESTDIR="$pkgdir" ninja -C build install
}
