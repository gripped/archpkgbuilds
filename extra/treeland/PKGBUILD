# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=treeland
pkgver=0.7.8
pkgrel=4
pkgdesc='New wayland compositer for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/treeland"
license=('Apache-2.0' 'LGPL-3.0-only' 'GPL-2.0-only' 'GPL-3.0-only')
# xhost & xwayland are needed for XWayland support (not optional)
depends=('ddm' 'dtk6core' 'dtk6declarative' 'dtk6systemsettings' 'gcc-libs' 'glibc'
         'libdrm' 'libinput' 'pam' 'pixman' 'qt6-base' 'qt6-declarative'
         'systemd-libs' 'wayland' 'wlroots0.19' 'xorg-xhost' 'xorg-xwayland' 'deepin-session')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols' 'wlr-protocols'
             'treeland-protocols' 'vulkan-headers')
groups=('deepin')
replaces=('waylib' 'qwlroots')
conflicts=('waylib' 'qwlroots')
source=("git+https://github.com/linuxdeepin/treeland.git#tag=$pkgver")
sha256sums=('f5a2ae830da33b12e37e08e54cc3937264c21a07aa9a1771ad079f022dfe0ec7')

build() {
	cd treeland
    # TODO: Fix RUNPATH issue
	cmake . -Bbuild -GNinja \
		-DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc \
		-DCMAKE_INSTALL_PREFIX=/usr
        # -DCMAKE_SKIP_RPATH=ON
	cmake --build build
}

package() {
	cd treeland
	DESTDIR="$pkgdir" ninja -C build install
}
