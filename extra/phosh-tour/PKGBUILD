# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=phosh-tour
pkgver=0.52.0
pkgrel=1
pkgdesc='Simple tour through Phosh'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/phosh-tour'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libgmobile
)
makedepends=(
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/World/Phosh/phosh-tour.git?signed#tag=v$pkgver")
b2sums=(b4e091f87b1a0b5c15776ef084246cc5d8e6e17dab1888e8354acbf20f89179e345edf3b87746cc90924f7440834c1a672f95c126100bc93ae5fbf62c57ab258)
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido Günther <guido@xcpu.ch>

prepare() {
  cd $pkgname
}

build() {
  _PRETTY_NAME=`sed -n 's/^PRETTY_NAME="\([^"]*\)"/\1/p' /etc/os-release`
  arch-meson $pkgname build \
    -D vendor="$_PRETTY_NAME"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Enable first login user service
  install -dm755 "$pkgdir/usr/lib/systemd/user/mobi.phosh.Shell.target.wants"
  ln -s ../mobi.phosh.PhoshTour-first-login.service \
    "$pkgdir/usr/lib/systemd/user/mobi.phosh.Shell.target.wants/mobi.phosh.PhoshTour-first-login.service"
}
