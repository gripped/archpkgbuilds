# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ivan Souza <ivanttt54@gmail.com>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>
# Contributor: Payn <ivanttt54@gmail.com>

pkgname=odin
pkgver=dev_2024_11
pkgrel=2
pkgdesc='Data-oriented programming language'
arch=(x86_64)
url='https://odin-lang.org/'
license=(BSD-3-Clause)
depends=(clang llvm-libs python)
makedepends=(git llvm)
options=(staticlibs)
source=("git+https://github.com/odin-lang/Odin#commit=76bf28ef2e3440bc31200b1d80c9b327ac208912") # 2024-11-01
b2sums=('1d3144e633d69ba30231049f03187560bd3c1eac682a6c01f209030e5751e6dc675a52a2ab1b8d6c627225b4c12790212fe4df16940de4116e8b9dd6b54e5a7a')

build() {
  cd Odin

  export CXX=/usr/bin/clang++
  export LLVM_CONFIG=llvm-config
  make release

  # TODO: unbundle third-party dependencies
  make -C vendor/stb/src
  make -C vendor/cgltf/src
  make -C vendor/miniaudio/src
}

check() {
  cd Odin

  ./odin check examples/all -strict-style

  python tests/core/download_assets.py tests/core/assets

  #./odin test tests/core/normal.odin -file -all-packages
  ./odin test tests/core/speed.odin -o:speed -file -all-packages
  ./odin test tests/vendor -all-packages
  ./odin test tests/internal -all-packages
}

package() {
  cd Odin

  install -d "$pkgdir/usr/"{bin,lib/$pkgname}
  cp odin "$pkgdir/usr/lib/$pkgname/odin"
  cp -r base core shared vendor "$pkgdir/usr/lib/$pkgname/"
  ln -s "/usr/lib/$pkgname/odin" "$pkgdir/usr/bin/odin"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 PROPOSAL-PROCESS.md "$pkgdir/usr/share/doc/$pkgname/PROPOSAL-PROCESS.md"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
