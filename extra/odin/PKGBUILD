# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ivan Souza <ivanttt54@gmail.com>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>
# Contributor: Payn <ivanttt54@gmail.com>

pkgname=odin
pkgver=dev_2025_07 # using dev tags until the next release is tagged, to make it build
pkgrel=1
pkgdesc='Data-oriented programming language'
arch=(x86_64)
url='https://odin-lang.org/'
license=(BSD-3-Clause)
depends=(clang llvm-libs python)
makedepends=(git llvm stb)
options=(staticlibs)
source=("git+https://github.com/odin-lang/Odin#tag=${pkgver//_/-}")
b2sums=('a656d8b265e49719e48521b57db88f8be21696d8eae1f0a00018971d04b4ffbe22d940407d89030aba71e9d30e89822bee4bb2da94e42048161d8d1992667ff3')

build() {
  cd Odin

  export CXX=clang++
  export LLVM_CONFIG=llvm-config
  make release

  # TODO: unbundle third-party dependencies
  make -C vendor/cgltf/src
  make -C vendor/miniaudio/src
  make -C vendor/stb/src
}

check() {
  cd Odin

  ./odin check examples/all -strict-style

  python tests/core/download_assets.py tests/core/assets

  #./odin test tests/core/normal.odin -file -all-packages
  ./odin test tests/core/speed.odin -o:speed -file -all-packages
  ./odin test tests/vendor -all-packages
  ./odin test tests/internal -all-packages
}

package() {
  cd Odin

  install -d "$pkgdir/usr/"{bin,lib/$pkgname}
  cp odin "$pkgdir/usr/lib/$pkgname/odin"
  cp -r base core shared vendor "$pkgdir/usr/lib/$pkgname/"
  ln -s "/usr/lib/$pkgname/odin" "$pkgdir/usr/bin/odin"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 PROPOSAL-PROCESS.md "$pkgdir/usr/share/doc/$pkgname/PROPOSAL-PROCESS.md"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
