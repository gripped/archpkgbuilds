# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Ivan Souza <ivanttt54@gmail.com>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>
# Contributor: Payn <ivanttt54@gmail.com>
# Contributor: Johannes Müller <mail@jotrorox.com>

pkgname=odin
# using monthly tags until a release is tagged
pkgver=dev_2025_11
pkgrel=1
pkgdesc='Data-oriented programming language'
arch=(x86_64)
url='https://odin-lang.org/'
license=(BSD-3-Clause)
depends=(clang llvm-libs python)
makedepends=(git llvm stb)
options=(staticlibs)
source=("git+https://github.com/odin-lang/Odin#tag=${pkgver//_/-}")
b2sums=('b810f934911ff63440b442cfb4698d7a675bb4e68afa4ef8a64ec3045b671ee697a4715f52f015a7a1467e5f0023f8c74cd601dc1a8b80a2bea9b8e832e14983')

build() {
  cd Odin

  export CXX=clang++
  export LLVM_CONFIG=llvm-config
  make release

  # TODO: unbundle third-party dependencies
  make -C vendor/cgltf/src
  make -C vendor/miniaudio/src
  make -C vendor/stb/src
}

check() {
  cd Odin

  ./odin check examples/all -strict-style

  python tests/core/download_assets.py tests/core/assets

  #./odin test tests/core/normal.odin -file -all-packages
  ./odin test tests/core/speed.odin -o:speed -file -all-packages
  ./odin test tests/vendor -all-packages
  ./odin test tests/internal -all-packages
}

package() {
  cd Odin

  install -d "$pkgdir/usr/"{bin,lib/$pkgname}
  cp odin "$pkgdir/usr/lib/$pkgname/odin"
  cp -r base core shared vendor "$pkgdir/usr/lib/$pkgname/"
  ln -s "/usr/lib/$pkgname/odin" "$pkgdir/usr/bin/odin"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 PROPOSAL-PROCESS.md "$pkgdir/usr/share/doc/$pkgname/PROPOSAL-PROCESS.md"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
