# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=polyphone
pkgver=2.5.1
pkgrel=1
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=(x86_64)
url="https://polyphone-soundfonts.com/en/"
_url=https://github.com/davy7125/polyphone
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  alsa-lib
  gcc-libs
  glibc
  hicolor-icon-theme
  openssl
  qt5-base
  qt5-svg
  zlib
)
makedepends=(
  jack
  libogg
  libvorbis
  libsndfile
  rtaudio
  rtmidi
  qt5-tools
  stk
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/davy7125/$pkgname/archive/$pkgver.tar.gz
)
sha512sums=('745709b625e7f013fd185168deda274a0bca3236fc08e921689c830c0186fa99bae7c8db02be48425aea2f9b0330b2da8b55e2af8a430d1aac02118fb83511e4')
b2sums=('7a962e25040caf3902257a4167565ac79302da9b69d60ca998e88bd8fee24b20e4ab5eb206cb27b93b7a8beb2a0a6b890e9038186cb106322fd3834b2883e927')

build() {
  cd $pkgname-$pkgver/sources
  qmake-qt5 $pkgname.pro PREFIX=/usr
  make
}

package() {
  depends+=(
    jack libjack.so
    libogg libogg.so
    libsndfile libsndfile.so
    libvorbis libvorbis.so libvorbisenc.so libvorbisfile.so
    stk libstk-5.0.0.so
  )

  cd $pkgname-$pkgver/sources
  # doesn't have an install target: https://github.com/davy7125/polyphone/issues/62
  install -vDm 755 bin/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 resources/$pkgname.png -t "$pkgdir/usr/share/icons/hicolor/512x512/apps"
  install -vDm 644 contrib/man/fr/man1/$pkgname.*1 -t "$pkgdir/usr/share/man/fr/man1/"
  install -vDm 644 contrib/man/man1/$pkgname.*1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 contrib/*.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 contrib/*.xml -t "$pkgdir/usr/share/metainfo/"
  install -vDm 644 contrib/$pkgname.xml -t "$pkgdir/usr/share/mime/packages/"
  install -vDm 644 {../README.md,changelog} -t "$pkgdir/usr/share/doc/$pkgname/"
}
