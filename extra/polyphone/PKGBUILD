# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=polyphone
pkgver=2.4.0
pkgrel=1
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=(x86_64)
url="https://polyphone-soundfonts.com/en/"
_url=https://github.com/davy7125/polyphone
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  alsa-lib
  gcc-libs
  glibc
  hicolor-icon-theme
  openssl
  qt5-base
  qt5-svg
  zlib
)
makedepends=(
  flac
  jack
  libogg
  libvorbis
  rtaudio
  rtmidi
  qt5-tools
  stk
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/davy7125/$pkgname/archive/$pkgver.tar.gz
  $pkgname-2.4.0-qt5_build.patch::https://github.com/davy7125/polyphone/commit/a41645001daee6bbbe844dc824ad72052cb0a5b9.patch
  $pkgname-2.4.0-qmath_include.patch::https://github.com/davy7125/polyphone/commit/2b03ac9efefa49e2a863bf003333e10127fab590.patch
)
sha512sums=('5d7be409491e290c48547cef00358faf8f0b9281708b968daa9a4692abbcf2901c9cf1e948d1e4deba6cb6c4f7e02c11465c8ae36c53d6c58e2d960e7e12c588'
            '56d968a80a3bb212352691360b0dc364e5b71b64d3e8658a7ef02d042d812290fc1930bd83630d8fc5a2a33d809bf50b45e8562b35fafc3fbb7482a29ef2157b'
            '5ecc486b0b7908ce5879521f8b302cae4cb5d631a9c1b80a6a2a441636a7860160064a16d2b768657dae74bfc4aab85fed2af8e7bf6a4a2790b128baee1f21f3')
b2sums=('bf5f7ce6ac3ceb0c4912469dca241b6ed7abb9fed57d9af9624be1fbd5889afb963001d0f2109492133d2cb04d48d13b4474a0de6dc853e12b36e01ca6f190ba'
        'f7012468c43fc67b23cd499fe6699048019db4c6909ffe4ea331ad5766f095ed03dba5574e9a4945be327a069d5b593d3c37dadf39f2bb7096545df8d1e74f29'
        'a6e0b54a8a12de5bdd167808accef9865542eac0a3f44e73a6a4b0df37687a2f4df1e25253204f9c6f06aed2916dc6ac92fccf265dec1f22f3520c7af93fd39a')

prepare() {
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.4.0-qt5_build.patch
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.4.0-qmath_include.patch
}

build() {
  cd $pkgname-$pkgver/sources
  qmake-qt5 $pkgname.pro PREFIX=/usr
  make
}

package() {
  depends+=(
    flac libFLAC.so
    jack libjack.so
    libogg libogg.so
    libvorbis libvorbis.so libvorbisenc.so libvorbisfile.so
    stk libstk-5.0.0.so
  )

  cd $pkgname-$pkgver/sources
  # doesn't have an install target: https://github.com/davy7125/polyphone/issues/62
  install -vDm 755 bin/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 resources/$pkgname.png -t "$pkgdir/usr/share/icons/hicolor/512x512/apps"
  install -vDm 644 contrib/man/fr/man1/$pkgname.*1 -t "$pkgdir/usr/share/man/fr/man1/"
  install -vDm 644 contrib/man/man1/$pkgname.*1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 contrib/man/ru/man1/$pkgname.*1 -t "$pkgdir/usr/share/man/ru/man1/"
  install -vDm 644 contrib/*.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 contrib/*.xml -t "$pkgdir/usr/share/metainfo/"
  install -vDm 644 contrib/$pkgname.xml -t "$pkgdir/usr/share/mime/packages/"
  install -vDm 644 {../README.md,changelog} -t "$pkgdir/usr/share/doc/$pkgname/"
}
