# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=polyphone
pkgver=2.4.1
pkgrel=1
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=(x86_64)
url="https://polyphone-soundfonts.com/en/"
_url=https://github.com/davy7125/polyphone
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  alsa-lib
  gcc-libs
  glibc
  hicolor-icon-theme
  openssl
  qt5-base
  qt5-svg
  zlib
)
makedepends=(
  flac
  jack
  libogg
  libvorbis
  rtaudio
  rtmidi
  qt5-tools
  stk
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/davy7125/$pkgname/archive/$pkgver.tar.gz
)
sha512sums=('e706c6c39ab619a520721f052557c497fef086af0fb4e4e0e906678b9c8d120c0ec267e7f0e6b44001c2e7123b2869a4bd7975b92003c009fb0d8784f0a0d101')
b2sums=('50376bd4e852fb6ca009c7d090548d80be48a79ffc6dc4f3a93a458a843fe870bbc88ec8c0c0c5d891eb6d225a4b04a8238d4fba09be1b9b12ba0562d6933d46')

build() {
  cd $pkgname-$pkgver/sources
  qmake-qt5 $pkgname.pro PREFIX=/usr
  make
}

package() {
  depends+=(
    flac libFLAC.so
    jack libjack.so
    libogg libogg.so
    libvorbis libvorbis.so libvorbisenc.so libvorbisfile.so
    stk libstk-5.0.0.so
  )

  cd $pkgname-$pkgver/sources
  # doesn't have an install target: https://github.com/davy7125/polyphone/issues/62
  install -vDm 755 bin/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 resources/$pkgname.png -t "$pkgdir/usr/share/icons/hicolor/512x512/apps"
  install -vDm 644 contrib/man/fr/man1/$pkgname.*1 -t "$pkgdir/usr/share/man/fr/man1/"
  install -vDm 644 contrib/man/man1/$pkgname.*1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 contrib/man/ru/man1/$pkgname.*1 -t "$pkgdir/usr/share/man/ru/man1/"
  install -vDm 644 contrib/*.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 contrib/*.xml -t "$pkgdir/usr/share/metainfo/"
  install -vDm 644 contrib/$pkgname.xml -t "$pkgdir/usr/share/mime/packages/"
  install -vDm 644 {../README.md,changelog} -t "$pkgdir/usr/share/doc/$pkgname/"
}
