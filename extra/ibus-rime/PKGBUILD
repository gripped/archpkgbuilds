# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: GONG Chen <chen dot sst at gmail dot com>
# Contributor: 網軍總司令

pkgname=ibus-rime
pkgver=1.6.0
pkgrel=1
pkgdesc="Rime input method engine for ibus"
arch=('x86_64')
url="https://rime.im"
license=('GPL-3.0-or-later')
depends=('ibus' 'libnotify' 'librime')
makedepends=('git' 'cmake')
install='ibus-rime.install'
source=("git+https://github.com/lotem/ibus-rime.git#tag=$pkgver")
sha512sums=('924ad8b9c1f9adeb12e7772fdb9c41e3970d2f2e5503b89bd515e871a213d49eee2bf09dd78a633dcd52ec3e454c1a28888f8caf58e1d28dee4536d90cd92721')

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
