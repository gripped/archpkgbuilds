# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=selene
pkgver=0.28.0
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=('x86_64')
url=https://github.com/Kampfkarren/selene
license=('MPL-2.0')
depends=('gcc-libs' 'glibc' 'openssl')
makedepends=('cargo' 'git')
conflicts=("$pkgname-linter")
replaces=("$pkgname-linter")
options=('!lto')
source=("git+$url.git#tag=$pkgver")
b2sums=('4c1b7623fafeb6d5394a72eece74f8a9817f796a833cd320dc453ae7a1078926b798f8093cbe98b5740c9d41e1f76a3465b1c81a192e83bfb7e90110d1a5518c')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

check() {
  cd $pkgname
  cargo test --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}

# vim:set ts=2 sw=2 et:
