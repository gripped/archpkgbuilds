# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=selene
pkgver=0.29.0
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=(x86_64)
url=https://github.com/Kampfkarren/selene
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(
  cargo
  git
)
options=(!lto)
source=("git+$url.git#tag=$pkgver")
b2sums=('02e60b094cd942d5b010573c5f5e1086d1ba911501f3b5c2bfbabd639504b05ffb3c56fb64bc66a362856ab16014225158be635f97f160b0b07d839b61dafaf3')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

check() {
  cd $pkgname
  cargo test --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}

# vim:set ts=2 sw=2 et:
