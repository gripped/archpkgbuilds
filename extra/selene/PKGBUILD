# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=selene
pkgver=0.30.0
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=(x86_64)
url=https://github.com/Kampfkarren/selene
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(
  cargo
  git
)
options=(!lto)
source=("git+$url.git#tag=$pkgver")
b2sums=('08139162434fa13c77cfffabf785f7026449c87997d42a4bdf637f2745ce7921c457a1e175f344822c68e932f95940aef3e26c05bfe474670f958bbefd9aa4a3')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

check() {
  cd $pkgname
  cargo test --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}

# vim:set ts=2 sw=2 et:
