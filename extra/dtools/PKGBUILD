# Maintainer: Dan Printzell <wild@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Mihails Strasunse <public@dicebot.lv>

pkgname=dtools
pkgver=2.112.0
pkgrel=1
pkgdesc='Ancilliary tools for the D programming language'
license=('BSL-1.0')
groups=('dlang')
arch=('x86_64')
url='https://www.dlang.org'
makedepends=('dmd' 'libphobos')
provides=('ddemangle' 'dustmite' 'rdmd')
conflicts=('ddemangle' 'dustmite' 'rdmd')
depends=('curl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dlang/tools/archive/v$pkgver.tar.gz")
sha512sums=('3dfc0f901677d280e94a0e3769be17be45c40884c084c407a72209b18d06d0af6dc472ba92eb9823afb10329ce4500191d857e6950ac264509edde15aff2df6f')

build() {
    DMD=dmd

    # Add $LDFLAGS to the compilation, and remove -flto
    _linkflags=$(echo -ne $LDFLAGS | cut -d\" -f2 | tail -c+4 | sed -e "s/-Wl,/-L=/g" -e "s/,/ -L=/g" -e "s/-flto=auto//")

    cd tools-$pkgver
    # rdmd is used to build others
    $DMD -od. -ofdtools-rdmd -L--build-id $_linkflags rdmd.d

    for tool in DustMite/dustmite ddemangle catdoc detab tolf dget; do
        ./dtools-rdmd --compiler=$DMD --build-only -od. -ofdtools-`basename $tool` $_linkflags $tool.d
    done
}

package() {
    cd tools-$pkgver

    mkdir -p "$pkgdir/usr/bin"
    for tool in rdmd ddemangle catdoc detab tolf dget dustmite; do
        install -m755 -t "$pkgdir/usr/bin" $pkgname-$tool
    done

    cd "$pkgdir"
    ln -s dtools-rdmd usr/bin/rdmd
    ln -s dtools-dustmite usr/bin/dustmite
    ln -s dtools-ddemangle usr/bin/ddemangle
}
