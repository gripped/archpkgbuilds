# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=quickshell
pkgver=0.2.1
pkgrel=5
pkgdesc='Flexible toolkit for making desktop shells with QtQuick'
arch=(x86_64 aarch64)
url="https://git.outfoxxed.me/$pkgname/$pkgname"
license=(LGPL-3.0-only)
depends=(gcc-libs #libgcc_s.so libstdc++.so
         glibc # libc.so libm.so
         hicolor-icon-theme
         jemalloc libjemalloc.so
         libdrm # libdrm.so
         libglvnd libEGL.so libOpenGL.so
         libpipewire libpipewire-0.3.so
         libxcb # libxcb.so
         mesa # libgbm.so
         pam libpam.so
         qt6-base # libQt6Core.so libQt6DBus.so libQt6Gui.so libQt6Network.so libQt6Widgets.so
         qt6-declarative # libQt6Qml.so libQt6Quick.so
         qt6-svg
         qt6-wayland # libQt6WaylandClient.so
         wayland libwayland-client.so)
makedepends=(cli11
             cmake
             ninja
             qt6-shadertools
             spirv-tools
             wayland-protocols)
_archive="$pkgname-$pkgver"
source=("$_archive.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('d815c5f99f4a0a28545ffaa90464420c773b7c0ab62f713d9d8735a8e7282ca7')

build() {
	cd "$pkgname"
	local cmake_options=(
		-D CMAKE_BUILD_TYPE=RelWithDebInfo
		-D DISTRIBUTOR='Arch Linux'
		-D DISTRIBUTOR_DEBUGINFO_AVAILABLE=Yes
		-D CRASH_REPORTER=Off
		-D CMAKE_INSTALL_PREFIX=/usr
		-D INSTALL_QML_PREFIX=lib/qt6/qml
	)
	cmake -G Ninja -B build -W no-dev "${cmake_options[@]}"
	cmake --build build
}

package() {
	cd "$pkgname"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
