# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.com>
# Contributor: libertylocked <libertylocked@disroot.org>

pkgname=bitwarden-cli
pkgver=2024.9.0
pkgrel=1
pkgdesc="The command line vault"
arch=('x86_64')
url="https://github.com/bitwarden/clients"
license=('GPL3')
depends=('nodejs-lts-iron')
makedepends=('git' 'python' 'python-setuptools' 'npm' 'modclean')
options=(!emptydirs)
source=(bitwarden::git+https://github.com/bitwarden/clients.git#tag=cli-v${pkgver})
sha512sums=('2a4ca84a5bff9c5735f6afacaffa769317d80a096b4856e623fc8f91cae0c970d2271ee2e1b27128f05c63cdefe7f8107d00a3334b2843aacacf4a369bf400d6')

prepare() {
	cd bitwarden

	export npm_config_build_from_source=true
	export npm_config_cache="$srcdir/npm_cache"

	npm ci
}

build() {
	cd bitwarden/apps/cli
	export npm_config_cache="$srcdir/npm_cache"
	npm run build:bit
	npm run clean
	node ./build/bw.js completion --shell zsh > _bw
}

package() {
	cd bitwarden/apps/cli
    export CXXFLAGS="$CXXFLAGS -Wno-error"
	npm install --production -g --prefix "$pkgdir"/usr $(npm pack . | tail -1)

	# Non-deterministic race in npm gives 777 permissions to random directories.
	# See https://github.com/npm/npm/issues/9359 for details.
	chmod -R u=rwX,go=rX "$pkgdir"
	# npm gives ownership of ALL FILES to build user
	# https://bugs.archlinux.org/task/63396
	chown -R root:root "${pkgdir}"

	# package zsh completions
	install -vDm644 _bw -t "${pkgdir}/usr/share/zsh/site-functions"

	# cleanup
	sed -e "s|${srcdir}|/|" -i "$pkgdir"/usr/lib/node_modules/@bitwarden/cli/package.json
	find "$pkgdir"/usr/lib/node_modules -name 'package.json' -exec sed -e "s|${srcdir}||" -i {} \;
	modclean --path "$pkgdir"/usr/lib -r -a "*.ts,.bin,.github,.vscode,bin.js" --ignore='license'
}
