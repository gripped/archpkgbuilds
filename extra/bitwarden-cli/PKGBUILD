# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.com>
# Contributor: libertylocked <libertylocked@disroot.org>

pkgname=bitwarden-cli
pkgver=2025.3.0
pkgrel=1
pkgdesc="The command line vault"
arch=('x86_64')
url="https://github.com/bitwarden/clients"
license=('GPL3')
depends=('nodejs-lts-iron' 'argon2')
makedepends=('git' 'python' 'python-setuptools' 'npm' 'modclean' 'fdupes')
options=(!emptydirs)
source=(bitwarden::git+https://github.com/bitwarden/clients.git#tag=cli-v${pkgver}
        system-libargon2.patch)
sha512sums=('7c261b12dbbef5b2d071e20c570e9aaa3ba199a8739ff0fd41e35ed016d2dbb0645dd766c06599d48e471bcb81fb299788d0e0f90c4501e6dfd122d713feacbf'
            '6970261c24d8056b001f4ff308af7b9be67ef638be28f54f7c08ee4bed9ad895145a6cedf9a3cbda61b023104c50a6758a8bfcc8bc40097a298b98b60b1ffee7')

prepare() {
	cd bitwarden

	export npm_config_build_from_source=true
	export npm_config_cache="$srcdir/npm_cache"

	npm ci
	patch -p1 -i "$srcdir/system-libargon2.patch"
}

build() {
	cd bitwarden/apps/cli
	export npm_config_build_from_source=true
	export npm_config_cache="$srcdir/npm_cache"
	export CXXFLAGS="$CXXFLAGS -Wno-error"
	npm run build:bit
	npm run clean
	node ./build/bw.js completion --shell zsh > _bw
}

package() {
	cd bitwarden/apps/cli
	export npm_config_build_from_source=true
	export npm_config_cache="$srcdir/npm_cache"
	export CXXFLAGS="$CXXFLAGS -Wno-error"
	npm install --production -g --prefix "$pkgdir"/usr $(npm pack . | tail -1)

	# Non-deterministic race in npm gives 777 permissions to random directories.
	# See https://github.com/npm/npm/issues/9359 for details.
	chmod -R u=rwX,go=rX "$pkgdir"
	# npm gives ownership of ALL FILES to build user
	# https://bugs.archlinux.org/task/63396
	chown -R root:root "${pkgdir}"

	# package zsh completions
	install -vDm644 _bw -t "${pkgdir}/usr/share/zsh/site-functions"

	# cleanup
	rm -vr "$pkgdir"/usr/lib/node_modules/@bitwarden/cli/node_modules/argon2/prebuilds/
	sed -e "s|${srcdir}|/|" -i "$pkgdir"/usr/lib/node_modules/@bitwarden/cli/package.json
	find "$pkgdir"/usr/lib/node_modules -name 'package.json' -exec sed -e "s|${srcdir}||" -i {} \;
	modclean --path "$pkgdir"/usr/lib -r -a "*.ts,.bin,.github,.vscode,bin.js" --ignore='license'

	fdupes "$pkgdir"/usr/lib
	pushd "$pkgdir"/usr/lib
	#JS debug symbols (unusable)
	find -name '*.map' -type f -print -delete
	#Source code
	find -name '*.c' -type f -print -delete
	find -name '*.cpp' -type f -print -delete
	find -name '*.h' -type f -print -delete
	find -name '*.gyp' -type f -print -delete
	find -name '*.gypi' -type f -print -delete
	find -name '*.ts' -type f -print -delete
	find -name '*.cts' -type f -print -delete
	find -name build-tmp-napi-v3  -print0 |xargs -r0 -- rm -rvf --
	find -name Makefile -type f -print -delete
	find -name 'Pipfile*' -type f -print -delete
	find -name '*.patch' -type f -print -delete
	#Temporary build files
	find -name '.deps' -print0 |xargs -r0 -- rm -rvf --
	find -name 'obj.target' -print0 |xargs -r0 -- rm -rvf --
	find -name '*package-lock.json' -type f -print -delete
	find -name '*.mk' -type f -print -delete
	find -name '*.Makefile' -type f -print -delete

	#Documentation
	find -name '*.md' -type f -print -delete
	find -name doc -print0 |xargs -r0 -- rm -rvf --
	find -name test -print0 |xargs -r0 -- rm -rvf --
	#Compile-time-only dependencies
	find -name nan -print0 |xargs -r0 -- rm -rvf --
	find -name node-addon-api -print0 |xargs -r0 -- rm -rvf --
	#Other trash
	find -name '*.yml' -type f -print -delete
	find -name '.npmignore' -type f -print -delete
	find -name '.gitignore' -type f -print -delete

	#Fix file mode
	find . -type f -exec chmod 644 {} \;
	find . -name '*.node' -exec chmod 755 {} \;

	# Remove empty directories
	find . -type d -empty -print -delete
	popd
	chmod 755 "$pkgdir"/usr/lib/node_modules/@bitwarden/cli/build/bw.js
}
