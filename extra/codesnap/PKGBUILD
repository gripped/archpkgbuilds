# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Adam Perkowski <adas1per@protonmail.com>

pkgname=codesnap
pkgver=0.12.2
pkgrel=1
pkgdesc="Pure Rust tool for generating beautiful code snapshots"
arch=('x86_64')
url="https://github.com/codesnap-rs/codesnap"
license=('MIT')
depends=('oniguruma' 'gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('d6acfee415e5f6019f49dbe655921ee5307791255a94635f5139c328c2b4409e')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" --locked
}

build() {
  export RUSTFLAGS="-C link-arg=/usr/lib/libonig.so"
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

# check() {
#   cd "$pkgname-$pkgver"
#   cargo test --frozen
# }

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
