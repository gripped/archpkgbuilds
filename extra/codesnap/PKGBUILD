# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Adam Perkowski <adas1per@protonmail.com>

pkgname=codesnap
pkgver=0.13.1
pkgrel=2
pkgdesc="Pure Rust tool for generating beautiful code snapshots"
arch=('x86_64')
url="https://github.com/codesnap-rs/codesnap"
license=('MIT')
depends=('oniguruma' 'gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('e1636f08781fdb6e380428bd54f458f59b7764702271a7f2f407ad4432753c33')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc --print host-tuple)" --locked
}

build() {
  export RUSTFLAGS="-C link-arg=/usr/lib/libonig.so"
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

# check() {
#   cd "$pkgname-$pkgver"
#   cargo test --frozen
# }

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
