# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Thang Pham <phamducthang1234 at gmail dot com>

pkgname=spotify-player
pkgver=0.21.2
pkgrel=1
pkgdesc="A command driven spotify player"
arch=('x86_64')
url="https://github.com/aome510/spotify-player"
license=('MIT')
depends=('openssl' 'dbus' 'libpulse' 'libsixel')
optdepends=('libnotify: Notification support' 'fzf: Fuzzy search support')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/aome510/spotify-player/archive/v${pkgver}.tar.gz")
sha512sums=('c630a59e94083b9aabf2cc064477c8f7a86395f3d7742f5ede7300ec01d86da2e6ac9df0d87b342dfc32424bb37bc860781f0b10ed2ff5da28356633c09ff18e')
options=('!lto')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  local _features=('pulseaudio-backend' 'media-control' 'notify' 'daemon' 'image' 'sixel' 'fzf')
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release --bin spotify_player --no-default-features --features "${_features[*]}"
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm755 target/release/spotify_player -t "${pkgdir}/usr/bin"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

# vim:set ts=2 sw=2 et:
