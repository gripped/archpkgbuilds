# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Thang Pham <phamducthang1234 at gmail dot com>

pkgname=spotify-player
pkgver=0.20.5
pkgrel=1
pkgdesc="A command driven spotify player"
arch=('x86_64')
url="https://github.com/aome510/spotify-player"
license=('MIT')
depends=('alsa-lib' 'openssl' 'dbus')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/aome510/spotify-player/archive/v${pkgver}.tar.gz")
sha512sums=('f54d15194414e878801668b1dd3d882b5746d9e4c6d7edbd0bbac8f5fe19676fab9ae87827063a9788a7e97e0b6b6f1d60387778efcb7f71f4d2870d963553ae')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}"
	cargo build --frozen --release --bin spotify_player --no-default-features
}

check() {
	cd "${pkgname}-${pkgver}"
	cargo test --frozen
}

package() {
	cd "${pkgname}-${pkgver}"

	install -Dm755 target/release/spotify_player -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

# vim:set ts=2 sw=2 et:
