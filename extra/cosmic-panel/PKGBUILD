# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-panel
pkgver=1.0.0.alpha.3
pkgrel=1
pkgdesc='XDG Shell Wrapper Panel for Cosmic'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-panel
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=041a44eb6f825b7e0b3d45ccf745252b136d5da9
source=(git+https://github.com/pop-os/cosmic-panel.git#tag=${_tag})
b2sums=('6f7335cd1eef7c6468b0d02c03ba3bb45297aa9c3bef84fc520306bade54b6fbf9097181efa3915fa08bec4c138fe67997329667f6cf32bdccf6bd1d9f11828e')

prepare() {
  cd cosmic-panel
  cargo fetch --locked
}

pkgver() {
  cd cosmic-panel
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-panel
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-panel
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
