# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-panel
pkgver=1.0.0.alpha.6
pkgrel=1
pkgdesc='XDG Shell Wrapper Panel for Cosmic'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-panel
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=dc429bc571393624a72ce91c6dd864849c3812ae
source=(git+https://github.com/pop-os/cosmic-panel.git#tag=${_tag})
b2sums=('54e866413eb16a771c905ff088c84a63c272cc52a767fb6ad405204e4c2970c3aecf117f5a6b251327c4b1e936d1dd885e715708240e4b1c97a83bbe3adc0153')

prepare() {
  cd cosmic-panel
  cargo fetch --locked
}

pkgver() {
  cd cosmic-panel
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-panel
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-panel
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
