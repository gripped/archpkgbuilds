# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-panel
pkgver=1.0.0.alpha.4
pkgrel=1
pkgdesc='XDG Shell Wrapper Panel for Cosmic'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-panel
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=734e3fafe2eafea1cb3cece7d0b4ddf72a2c4323
source=(git+https://github.com/pop-os/cosmic-panel.git#tag=${_tag})
b2sums=('f1c194c546aaf41d068c58e79b5229789f03a658c746656243a93ffc253b49c62c20db6669fc196ffe0c483fc412e74885c5fd7bead06ec6632f2c54f620ea7f')

prepare() {
  cd cosmic-panel
  cargo fetch --locked
}

pkgver() {
  cd cosmic-panel
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-panel
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-panel
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
