# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-panel
pkgver=1.0.0.beta.1.1
pkgrel=1
pkgdesc='XDG Shell Wrapper Panel for Cosmic'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-panel
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=1cbfafc68b68ef822f346b2c3c3a04dc04ab0e9a
source=(git+https://github.com/pop-os/cosmic-panel.git#tag=${_tag})
b2sums=('e25bc9d530f5b3a2c9ea8cbb0831da9e54a9fe467f7da84a329363f1ae7a302827e2248f94283bc26b2f1b3560900ac251d981cad995c83d8d25a4733c4efe47')

prepare() {
  cd cosmic-panel
  cargo fetch --locked
}

pkgver() {
  cd cosmic-panel
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-panel
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-panel
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
