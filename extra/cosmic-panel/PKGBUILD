# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-panel
pkgver=1.0.0.beta.4
pkgrel=1
pkgdesc='XDG Shell Wrapper Panel for Cosmic'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-panel
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=f1a947605243a79c4a4a0813fa234fa21440c859
source=(git+https://github.com/pop-os/cosmic-panel.git#tag=${_tag})
b2sums=('6207336790bd20195247aca9ef79a8a8dfdb4ced544c371162e63b472f5f976eac632c8ce4604716e1a5fb2652ec52fd12cefe757533312bf1558e1652500d37')

prepare() {
  cd cosmic-panel
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-panel
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-panel
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-panel
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
