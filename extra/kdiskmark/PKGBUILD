# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Jiachen YANG <farseerfc@gmail.com>
# Contributor: Gabriel Rauter <rauter.gabriel@gmail.com>

arch=('x86_64')
_pkgname=KDiskMark
pkgname=kdiskmark
pkgver=3.2.0
pkgrel=2
pkgdesc="HDD and SSD benchmark tool with a very friendly graphical user interface"
url="https://github.com/JonMagon/${_pkgname}"
license=(GPL-3.0-or-later)
depends=(qt6-base fio libaio hicolor-icon-theme glibc libgcc libstdc++ polkit-qt6)
makedepends=(git cmake extra-cmake-modules qt6-tools qt6-declarative clang)
source=("git+https://github.com/JonMagon/${_pkgname}#tag=$pkgver"
        "$pkgname-SingleApplication::git+https://github.com/itay-grudev/SingleApplication.git")
sha256sums=('c4182293e1612fe42bb2cb58d6b3d0fdc7302388820ae420543c8b90df177c8e'
            'SKIP')

prepare() {
  cd "${_pkgname}"

  git submodule init

  git config submodule."src/singleapplication".url "${srcdir}/$pkgname-SingleApplication"

  git -c protocol.file.allow=always submodule update
}

build() {
  cmake -B build -S "${_pkgname}" \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBEXECDIR='lib' \
    -Wno-dev
  make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install
}
