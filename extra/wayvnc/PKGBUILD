# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=wayvnc
pkgver=0.9.1
pkgrel=1
pkgdesc='VNC server for wlroots-based Wayland compositors'
arch=(x86_64)
url=https://github.com/any1/wayvnc
license=(ISC)
depends=(
  glibc
  jansson
  libaml.so
  libdrm
  libneatvnc.so
  libpixman-1.so
  libxkbcommon.so
  wayland
)
makedepends=(
  git
  meson
  ninja
  pam
  scdoc
)
optdepends=('pam: PAM authentication')
_tag=e4ec935d29c0aa2de38d9c12dd30d682f83b91c2
source=(
  git+https://github.com/any1/wayvnc.git#tag=${_tag}
  wayvnc.service
)
b2sums=('a4a123ce471c8d1e47f0d8fd8e0fbc01f7c27e93084129e20c8bfb915a291fbc504f795f497d183481abefeec82fcb1d877537be3c699397a8460b7b939d0ee7'
        '083d7ddbf353d06126b76152e51807ed7572684e0c995b71802fcf36ca133c48401126907fcac657271bf9590b4a304a103f7bf9104b73e0699af98f7388f65c')

pkgver() {
  cd wayvnc
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson wayvnc build \
    -Dman-pages=enabled \
    -Dpam=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 wayvnc/wayvnc.pam "${pkgdir}"/etc/pam.d/wayvnc
  install -Dm 644 wayvnc.service -t "${pkgdir}"/usr/lib/systemd/user
  install -Dm 644 wayvnc/COPYING -t "${pkgdir}"/usr/share/licenses/wayvnc
}

# vim: ts=2 sw=2 et:
