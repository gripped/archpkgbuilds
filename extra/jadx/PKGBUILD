# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Nicolas Hureau <archlinux@kalenz.net>

pkgname=jadx
pkgver=1.5.5
pkgrel=1
pkgdesc='Command line and GUI tools to produce Java source code from Android Dex and APK files'
url='https://github.com/skylot/jadx'
arch=(any)
license=(Apache-2.0)
depends=(
  bash
)
makedepends=(
  'java-environment=21'
  gradle
)
source=(https://github.com/skylot/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('8b25c874fcf9c49b0b5e043f009ecd18d2373b8c336c8b300c8a2b5fb60745eedccde78bbb9fbb9917ddd86f896b2f332e3d4f9764f43322461130c7cd083be9')
b2sums=('d0b327b8e5c9f47b26d86075e3537b8107471aedf3fed2ed2b3622f4ebc6330ffb4d01eb162095e2bf597adb9f6abab5d59f4b75e38c1d5fd0a4d266dae85744')

build() {
  cd ${pkgname}-${pkgver}
  # https://github.com/skylot/jadx/blob/v1.5.4/build.gradle.kts#L14
  JADX_VERSION=${pkgver} gradle dist
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  depends+=('java-runtime>=11')
  cd ${pkgname}-${pkgver}

  pushd build/jadx
  install -Dm 755 bin/{jadx,jadx-gui} -t "${pkgdir}/usr/share/java/${pkgname}/bin"
  install -Dm 644 lib/* -t "${pkgdir}/usr/share/java/${pkgname}/lib"
  popd

  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/java/${pkgname}/bin/jadx "${pkgdir}/usr/bin/jadx"
  ln -s /usr/share/java/${pkgname}/bin/jadx-gui "${pkgdir}/usr/bin/jadx-gui"
  install -Dm 644 contrib/jadx-gui.desktop -t "${pkgdir}/usr/share/applications"

  install -Dm 644 LICENSE NOTICE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
