# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Nicolas Hureau <archlinux@kalenz.net>

pkgname=jadx
pkgver=1.5.4
pkgrel=1
pkgdesc='Command line and GUI tools to produce Java source code from Android Dex and APK files'
url='https://github.com/skylot/jadx'
arch=(any)
license=(Apache-2.0)
depends=(
  bash
)
makedepends=(
  'java-environment=21'
  gradle
)
source=(https://github.com/skylot/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c1a7d24a59cd51840a8539abff1de6cfbc740915101c6a31d04b4048600e8e7f868da61b532f65066589518c72adb04d9ebc535467a175b6ee9d9f952842f779')
b2sums=('10d12bfce1b8afd42769bb76dd79255370ef91b3c29dee079c8e471fe513bd389ad3ac1ea6acf2fe70132348f1ec6cf44a2c60bebbd36c000a9f0726c91eb69d')

build() {
  cd ${pkgname}-${pkgver}
  gradle dist
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  depends+=('java-runtime>=11')
  cd ${pkgname}-${pkgver}

  pushd build/jadx
  install -Dm 755 bin/{jadx,jadx-gui} -t "${pkgdir}/usr/share/java/${pkgname}/bin"
  install -Dm 644 lib/* -t "${pkgdir}/usr/share/java/${pkgname}/lib"
  popd

  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/java/${pkgname}/bin/jadx "${pkgdir}/usr/bin/jadx"
  ln -s /usr/share/java/${pkgname}/bin/jadx-gui "${pkgdir}/usr/bin/jadx-gui"
  install -Dm 644 contrib/jadx-gui.desktop -t "${pkgdir}/usr/share/applications"

  install -Dm 644 LICENSE NOTICE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
