# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Nicolas Hureau <archlinux@kalenz.net>

pkgname=jadx
pkgver=1.5.3
pkgrel=1
pkgdesc='Command line and GUI tools to produce Java source code from Android Dex and APK files'
url='https://github.com/skylot/jadx'
arch=(any)
license=(Apache-2.0)
depends=(
  bash
)
makedepends=(
  'java-environment=21'
  gradle
)
source=(https://github.com/skylot/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('438e62b3794f4dc153bcbc08cd549dcdb9a19e22cc2112522886556ee68c34d8aaea4d7ab748bc37a5195b790099fc44851bf2df7d5e20955ddc46a445eea8d7')
b2sums=('24414e79c0964dfa26c3fecd93dbee088538de8a995ad454d89353c0d055f2fd635088d419099c41401533e1f0ec63ce4a7f77ac64656f187a86db906faba099')

build() {
  cd ${pkgname}-${pkgver}
  gradle dist
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  depends+=('java-runtime>=11')
  cd ${pkgname}-${pkgver}

  pushd build/jadx
  install -Dm 755 bin/{jadx,jadx-gui} -t "${pkgdir}/usr/share/java/${pkgname}/bin"
  install -Dm 644 lib/* -t "${pkgdir}/usr/share/java/${pkgname}/lib"
  popd

  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/java/${pkgname}/bin/jadx "${pkgdir}/usr/bin/jadx"
  ln -s /usr/share/java/${pkgname}/bin/jadx-gui "${pkgdir}/usr/bin/jadx-gui"
  install -Dm 644 contrib/jadx-gui.desktop -t "${pkgdir}/usr/share/applications"

  install -Dm 644 LICENSE NOTICE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
