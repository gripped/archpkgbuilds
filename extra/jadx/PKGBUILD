# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Nicolas Hureau <archlinux@kalenz.net>

pkgname=jadx
pkgver=1.5.1
pkgrel=1
pkgdesc='Command line and GUI tools to produce Java source code from Android Dex and APK files'
url='https://github.com/skylot/jadx'
arch=(any)
license=(Apache-2.0)
depends=(
  bash
)
makedepends=(
  'java-environment=21'
  gradle
)
source=(https://github.com/skylot/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('2f160e14544e8c2cb78e62e41190f3adc0e46417b4a50bff9075965893e7e24d1bb904ef398c7e7f47c67e3e86129e964d002b3fed0f3bbe1e8db12778c252ae')
b2sums=('dd62b6899f781ee0c831703d93db1a7893fc9edb42fdf876f00382bf455db48de1005ab556b4489887f6630f331d2b761008a7f91844f6fec8595802c5e67b75')

build() {
  cd ${pkgname}-${pkgver}
  gradle dist
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  depends+=('java-runtime>=11')
  cd ${pkgname}-${pkgver}

  pushd build/jadx
  install -Dm 755 bin/{jadx,jadx-gui} -t "${pkgdir}/usr/share/java/${pkgname}/bin"
  install -Dm 644 lib/* -t "${pkgdir}/usr/share/java/${pkgname}/lib"
  popd

  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/java/${pkgname}/bin/jadx "${pkgdir}/usr/bin/jadx"
  ln -s /usr/share/java/${pkgname}/bin/jadx-gui "${pkgdir}/usr/bin/jadx-gui"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 NOTICE README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
