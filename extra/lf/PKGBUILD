# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Andrew Rabert <ar@nullsum.net>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: Ian Beringer <ian@ianberinger.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=lf
pkgver=35
pkgrel=1
pkgdesc='A terminal file manager inspired by ranger'
arch=('x86_64')
url='https://github.com/gokcehan/lf'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=r$pkgver")
sha512sums=('6c32789b0516c430a61a3567a2ea86eabab250104b10a7ee01e228ae99ecdbf748bf3fa787144f8645b087ca0c81fed23ee63eaedeee25dd7adc16f17c621e4e')
b2sums=('02999399a46106fee921dfb923428d563de5381db1b06d475e09909dc3f41da19ac2378e70d829ea5ae8cfcafe67984fd838813e511289bacd7509656615d336')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.gVersion=$pkgver" \
    -o build \
    .
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lf

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" lf.1

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md etc/lfrc.example

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" lf.desktop

  # vim integration
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/syntax" etc/lf.vim
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/ftdetect" etc/lf.vim

  # shell integrations
  install -vDm644 -t "$pkgdir/etc/profile.d" etc/*.{sh,csh}
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_functions.d" etc/lfcd.fish

  # shell completions
  install -vDm644 etc/lf.bash "$pkgdir/usr/share/bash-completion/completions/lf"
  install -vDm644 etc/lf.fish "$pkgdir/usr/share/fish/vendor_completions.d/lf.fish"
  install -vDm644 etc/lf.zsh "$pkgdir/usr/share/zsh/site-functions/_lf"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
