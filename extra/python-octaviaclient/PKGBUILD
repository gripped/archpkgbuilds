# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-octaviaclient
pkgver=3.11.1
pkgrel=1
pkgdesc="Octavia client for OpenStack Load Balancing"
arch=('any')
url="https://docs.openstack.org/python-octaviaclient/latest/"
license=('Apache-2.0')
depends=('python-cliff' 'python-keystoneauth1' 'python-neutronclient' 'python-openstackclient'
         'python-osc-lib' 'python-oslo-serialization' 'python-oslo-utils' 'python-pbr'
         'python-requests')
makedepends=('git')
checkdepends=('python-hacking' 'python-requests-mock' 'python-pygments' 'python-subunit'
              'python-oslotest' 'python-stestr' 'python-testscenarios')
source=("git+https://github.com/openstack/python-octaviaclient.git#tag=$pkgver")
sha512sums=('a6cd83e957ac5ccc764edf0b3f77d41b34d6a9f0361cf55959e197fb698a6b57c4929a9b5c17a6c4cbc981952c56773cef817afc8b993677d482358c92dd195e')

build() {
  cd python-octaviaclient
  python setup.py build
}

check() {
  cd python-octaviaclient
  stestr run
}

package() {
  cd python-octaviaclient
  python setup.py install --root="$pkgdir" --optimize=1
}
