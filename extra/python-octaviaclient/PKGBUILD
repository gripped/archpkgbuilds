# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-octaviaclient
pkgver=3.8.0
pkgrel=2
pkgdesc="Octavia client for OpenStack Load Balancing"
arch=('any')
url="https://docs.openstack.org/python-octaviaclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-keystoneauth1' 'python-neutronclient' 'python-openstackclient'
         'python-osc-lib' 'python-oslo-serialization' 'python-oslo-utils' 'python-pbr'
         'python-requests')
makedepends=('git')
checkdepends=('python-hacking' 'python-requests-mock' 'python-pygments' 'python-subunit'
              'python-oslotest' 'python-stestr' 'python-testscenarios')
source=("git+https://github.com/openstack/python-octaviaclient.git#tag=$pkgver")
sha512sums=('676323b40e382e380467f4eecd611d4b195f47c1307ba1ae55ef4ab9340d8edf482fd14cb9bae5eb47e77a38e7762465fc54b3e17a0beba2950a1486684a5425')

build() {
  cd python-octaviaclient
  python setup.py build
}

check() {
  cd python-octaviaclient
  stestr run
}

package() {
  cd python-octaviaclient
  python setup.py install --root="$pkgdir" --optimize=1
}
