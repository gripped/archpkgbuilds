# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Simon Legner <Simon.Legner@gmail.com>

pkgname=python-osmium
pkgver=4.0.2
pkgrel=2
pkgdesc="Python bindings for libosmium, the data processing library for OSM data"
url="https://osmcode.org/pyosmium"
depends=('python' 'libosmium' 'pybind11' 'python-requests' 'python-urllib3' 'bzip2' 'zlib' 'lz4' 'expat' 'glibc' 'gcc-libs')
makedepends=('python-build' 'python-wheel' 'python-installer' 'python-setuptools' 'cmake' 'protozero')
checkdepends=('python-pytest' 'python-pytest-httpserver' 'python-shapely' 'python-numpy')
replaces=('pyosmium')
provides=('python-pyosmium' 'pyosmium')
license=('BSD-2-Clause')
arch=('x86_64')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/osmcode/pyosmium/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('23c64f0217fe0c69fa51bd039f3b3a963a9c9fd254df1bdd3a766b2f51ec8de6')

build() {
    cd "pyosmium-${pkgver}"

    python -m build --wheel --no-isolation
}

check() {
    cd "pyosmium-${pkgver}"

    python -m pytest test
}

package() {
    cd "pyosmium-${pkgver}"

    python -m installer --destdir="${pkgdir}" dist/*.whl

    install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}
