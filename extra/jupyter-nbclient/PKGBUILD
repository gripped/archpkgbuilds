# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbclient
pkgver=0.10.3
pkgrel=1
pkgdesc='A tool for running Jupyter Notebooks in different execution contexts'
arch=(any)
url='https://github.com/jupyter/nbclient'
license=(BSD-3-Clause)
depends=(jupyter-nbformat
         python
         python-jupyter-client
         python-jupyter-core
         python-traitlets)
makedepends=(git
             python-build
             python-hatchling
             python-installer)
checkdepends=(jupyter-nbconvert
              python-flaky
              python-ipywidgets
              python-pytest-asyncio
              python-testpath
              python-xmltodict)
source=(git+https://github.com/jupyter/nbclient#tag=v$pkgver)
sha256sums=('9e53516890d71777ab43ded2b3adde58b64c632f5413c1a1ce4c354a430a10e8')

build() {
  cd nbclient
  python -m build --wheel --no-isolation
}

check() {
  cd nbclient
  pytest -v -k 'not test_cli_simple' # fails on build server
}

package() {
  cd nbclient
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
