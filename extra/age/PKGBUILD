# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Tharre <tharre3@gmail.com>

pkgname=age
pkgver=1.3.1
pkgrel=1
pkgdesc='A simple, modern and secure file encryption tool'
arch=('x86_64')
url="https://github.com/FiloSottile/$pkgname"
license=('BSD-3-Clause')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('95bb8049de36630f0b128c9626427646e5d4d76e35b6bd6790e29cf3f69fb2a0266ac7786616f56fe9bf378a81c1f3edc17fd9eba197286041b970944898504c')

build() {
    cd "$pkgname-$pkgver"
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-X main.Version=$pkgver -linkmode external -extldflags \"$LDFLAGS\"" \
      -o . ./...
}

check() {
    cd "$pkgname-$pkgver"
    go test ./...
}

package() {
    cd "$pkgname-$pkgver"
    for b in age age-keygen age-plugin-batchpass age-inspect; do
        install -Dm755 -t "$pkgdir/usr/bin/" "$b"
        install -Dm644 -t "$pkgdir/usr/share/man/man1/" "doc/$b.1"
    done

    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
