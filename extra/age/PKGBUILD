# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Tharre <tharre3@gmail.com>

pkgname=age
pkgver=1.3.0
pkgrel=1
pkgdesc='A simple, modern and secure file encryption tool'
arch=('x86_64')
url="https://github.com/FiloSottile/$pkgname"
license=('BSD-3-Clause')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a246e1b51bd9910f04f4709ca2a23bfeb44bb16c4f007945202af25eff72ccdd5374d33cf6d49459dbb214107d1201a517f86c33b3cc60c802b7139c02b33e6d')

build() {
    cd "$pkgname-$pkgver"
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-X main.Version=$pkgver -linkmode external -extldflags \"$LDFLAGS\"" \
      -o . ./...
}

check() {
    cd "$pkgname-$pkgver"
    go test ./...
}

package() {
    cd "$pkgname-$pkgver"
    for b in age age-keygen age-plugin-batchpass age-inspect; do
        install -Dm755 -t "$pkgdir/usr/bin/" "$b"
        install -Dm644 -t "$pkgdir/usr/share/man/man1/" "doc/$b.1"
    done

    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
