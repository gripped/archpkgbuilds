# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pstoedit
pkgver=4.00
pkgrel=1
pkgdesc="Translates PostScript and PDF graphics into other vector formats"
arch=('x86_64')
url="http://www.pstoedit.net/"
license=('GPL')
depends=('gcc-libs' 'plotutils' 'gd' 'imagemagick')
makedepends=('ghostscript')
source=("https://downloads.sourceforge.net/sourceforge/pstoedit/pstoedit-${pkgver}.tar.gz")
sha512sums=('82a8afd56bdefdc78fe7c39367480943a1639412e271f9469e3f9b77e3652cd6a291a828eeaba28ff0aeba58cb1bb052624650cdc4c8658b4e7e27f6d4a386f4')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -e 's|ImageMagick++|Magick++|' -i configure.ac
  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
