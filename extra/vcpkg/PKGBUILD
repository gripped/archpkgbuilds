# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=vcpkg
pkgver=2025.11.13
_pkgver="${pkgver//./-}"
pkgrel=1
pkgdesc='C++ library manager'
arch=('x86_64')
url='https://vcpkg.io'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'fmt'
  'unzip'
  'zip'
  'cmake'
  'ninja'
  'git'
)
install='vcpkg.install'
source=(
  "$pkgname::git+https://github.com/microsoft/vcpkg-tool#tag=$_pkgver"
  'vcpkg.sh'
   fmt-12.patch
)
sha512sums=('d753403973e3768631a2b137a07ef77920b8e667da457fae8963f51fd84edd13a24cf7000a8ac395fef1148aafff74353d7bf4a04fc92da21143644833d06e2a'
            'a71f4de5044197871b01d66db0453c8df228d690b259972fd4cf242806f3eac65d80e3c794ce511ce03b69f95d6c40ab20989e6fd9907efc962337d5dfbdf4f6'
            '79cb073b763f9e65aebebd7d09c7b1c11d1cbe439e0b18fc7968686aa29056674f0b8de552b0d755a2ba47016d9a0cfbca0ad7f7b346c7254f671486303f1ec8')
b2sums=('974b3af568b1a78a97ecf635e6605a998e65aec5f6e5f495165f5b8d061eabe33ba8bd6ba1b7cda6e5221b0e506946d9ccab973840f659ba5da68b808f04d69a'
        'c67bb718f04146ae72ba5d7a290d08079e965cc330db11c98b94de355cd4a6ff489dbd4c363c940f4a96a1fd7ff8a754ff0efb62156e81bcccfd80376b4d0e39'
        'eb4f0be5aa3d5a8c5da68fa4820804ea5ccb231b37e8b10b4e5257a767b91cc60ba587840651734d2ab574b87801d669995576e3f313e96014baac0da1d5b262')

prepare() {
  cd $pkgname
  patch -p1 -i ../fmt-12.patch
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -G Ninja \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX='/usr' \
    -D CMAKE_SKIP_INSTALL_RPATH=ON \
    -D VCPKG_DEVELOPMENT_WARNINGS=OFF \
    -D VCPKG_DEPENDENCY_EXTERNAL_FMT=ON \
    -D VCPKG_BASE_VERSION="$_pkgver" \
    -D VCPKG_VERSION=$(git -C $pkgname rev-parse HEAD) \
    -D BUILD_TESTING=OFF \
    -D VCPKG_WARNINGS_AS_ERRORS=OFF \
    -D VCPKG_BUILD_TLS12_DOWNLOADER=OFF \
    -D VCPKG_BUILD_FUZZING=OFF \
    -D VCPKG_EMBED_GIT_SHA=OFF \
    -D VCPKG_BUILD_BENCHMARKING=OFF \
    -D VCPKG_ADD_SOURCELINK=OFF

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # override environment variables
  install -vDm644 -t "$pkgdir/etc/profile.d" vcpkg.sh

  # license
  cd "$pkgname"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt NOTICE.txt
}
