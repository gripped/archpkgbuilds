# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=vcpkg
pkgver=2025.06.20
_pkgver="${pkgver//./-}"
pkgrel=1
pkgdesc='C++ library manager'
arch=('x86_64')
url='https://vcpkg.io'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'fmt'
  'unzip'
  'zip'
  'cmake'
  'ninja'
  'git'
)
install='vcpkg.install'
source=(
  "$pkgname::git+https://github.com/microsoft/vcpkg-tool#tag=$_pkgver"
  'vcpkg.sh'
)
sha512sums=('addd5d381ba2c9d29da312bae389d5a78fbe5462bd6ad93daf4d3a8d80bffdc548867740145c4bb150ab525004ec580f85668bc9c1324f824110da8bd164773d'
            'a71f4de5044197871b01d66db0453c8df228d690b259972fd4cf242806f3eac65d80e3c794ce511ce03b69f95d6c40ab20989e6fd9907efc962337d5dfbdf4f6')
b2sums=('01fa840ca5887fd8976b6f3a613f219f89dfedff59f5eb12edfc733f740d7bdfe90e9552f446b463c4ddc76d655ac51896e49320231a9e3be11fffe06afc674c'
        'c67bb718f04146ae72ba5d7a290d08079e965cc330db11c98b94de355cd4a6ff489dbd4c363c940f4a96a1fd7ff8a754ff0efb62156e81bcccfd80376b4d0e39')

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -G Ninja \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX='/usr' \
    -D CMAKE_SKIP_INSTALL_RPATH=ON \
    -D VCPKG_DEVELOPMENT_WARNINGS=OFF \
    -D VCPKG_DEPENDENCY_EXTERNAL_FMT=ON \
    -D VCPKG_BASE_VERSION="$_pkgver" \
    -D VCPKG_VERSION=$(git -C $pkgname rev-parse HEAD) \
    -D BUILD_TESTING=OFF \
    -D VCPKG_WARNINGS_AS_ERRORS=OFF \
    -D VCPKG_BUILD_TLS12_DOWNLOADER=OFF \
    -D VCPKG_BUILD_FUZZING=OFF \
    -D VCPKG_EMBED_GIT_SHA=OFF \
    -D VCPKG_BUILD_BENCHMARKING=OFF \
    -D VCPKG_ADD_SOURCELINK=OFF

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # override environment variables
  install -vDm644 -t "$pkgdir/etc/profile.d" vcpkg.sh

  # license
  cd "$pkgname"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt NOTICE.txt
}
