# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=vcpkg
pkgver=2024.11.12
_pkgver="${pkgver//./-}"
pkgrel=1
pkgdesc='C++ library manager'
arch=('x86_64')
url='https://vcpkg.io'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'fmt'
  'unzip'
  'zip'
  'cmake'
  'ninja'
  'git'
)
install='vcpkg.install'
source=(
  "$pkgname::git+https://github.com/microsoft/vcpkg-tool#tag=$_pkgver"
  'vcpkg.sh'
)
sha512sums=('ab13247d1da07e860f746de5a7586eafa57bbc432b3b361e41e619c357c48dfefe82bbfc0cccf629ccf249b0b1c802507565428baf255c28f1185c7ae54981cf'
            'a71f4de5044197871b01d66db0453c8df228d690b259972fd4cf242806f3eac65d80e3c794ce511ce03b69f95d6c40ab20989e6fd9907efc962337d5dfbdf4f6')
b2sums=('5b6f6e939cea2d7061c489d159a0509d6271468dc0bac292ef24a6b63609c2a1a19f56b8abd63fd6a091f1f97ab7be3f68d76075a898f81f348dd140bd3d216d'
        'c67bb718f04146ae72ba5d7a290d08079e965cc330db11c98b94de355cd4a6ff489dbd4c363c940f4a96a1fd7ff8a754ff0efb62156e81bcccfd80376b4d0e39')

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -G Ninja \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX='/usr' \
    -D CMAKE_SKIP_INSTALL_RPATH=ON \
    -D VCPKG_DEVELOPMENT_WARNINGS=OFF \
    -D VCPKG_DEPENDENCY_EXTERNAL_FMT=ON \
    -D VCPKG_BASE_VERSION="$_pkgver" \
    -D VCPKG_VERSION=$(git -C $pkgname rev-parse HEAD) \
    -D BUILD_TESTING=OFF \
    -D VCPKG_WARNINGS_AS_ERRORS=OFF \
    -D VCPKG_BUILD_TLS12_DOWNLOADER=OFF \
    -D VCPKG_BUILD_FUZZING=OFF \
    -D VCPKG_EMBED_GIT_SHA=OFF \
    -D VCPKG_BUILD_BENCHMARKING=OFF \
    -D VCPKG_ADD_SOURCELINK=OFF

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # override environment variables
  install -vDm644 -t "$pkgdir/etc/profile.d" vcpkg.sh

  # license
  cd "$pkgname"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt NOTICE.txt
}
