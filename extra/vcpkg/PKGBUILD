# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=vcpkg
pkgver=2024.10.18
_pkgver="${pkgver//./-}"
pkgrel=1
pkgdesc='C++ library manager'
arch=('x86_64')
url='https://vcpkg.io'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'fmt'
  'unzip'
  'zip'
  'cmake'
  'ninja'
  'git'
)
install='vcpkg.install'
source=(
  "$pkgname::git+https://github.com/microsoft/vcpkg-tool#tag=$_pkgver"
  'vcpkg.sh'
)
sha512sums=('30b9f25cc251964f76d55e50dd843bf7445b2957b04ca672f9fe2fddc403fa0cad71502943936ec5a4a803845698f4fced84b9a4a9170adc1644a490c806c2c5'
            'a71f4de5044197871b01d66db0453c8df228d690b259972fd4cf242806f3eac65d80e3c794ce511ce03b69f95d6c40ab20989e6fd9907efc962337d5dfbdf4f6')
b2sums=('65bdde28f615852b6db9a763307ea8ce05cb584e58774f6274ae836c1b29d4c44c2ec390e715625d0c346de0deb2d42ce88c03924ee3b8a0237314a5b067ef6b'
        'c67bb718f04146ae72ba5d7a290d08079e965cc330db11c98b94de355cd4a6ff489dbd4c363c940f4a96a1fd7ff8a754ff0efb62156e81bcccfd80376b4d0e39')

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -G Ninja \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX='/usr' \
    -D CMAKE_SKIP_INSTALL_RPATH=ON \
    -D VCPKG_DEVELOPMENT_WARNINGS=OFF \
    -D VCPKG_DEPENDENCY_EXTERNAL_FMT=ON \
    -D VCPKG_BASE_VERSION="$_pkgver" \
    -D VCPKG_VERSION=$(git -C $pkgname rev-parse HEAD) \
    -D BUILD_TESTING=OFF \
    -D VCPKG_WARNINGS_AS_ERRORS=OFF \
    -D VCPKG_BUILD_TLS12_DOWNLOADER=OFF \
    -D VCPKG_BUILD_FUZZING=OFF \
    -D VCPKG_EMBED_GIT_SHA=OFF \
    -D VCPKG_BUILD_BENCHMARKING=OFF \
    -D VCPKG_ADD_SOURCELINK=OFF

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # override environment variables
  install -vDm644 -t "$pkgdir/etc/profile.d" vcpkg.sh

  # license
  cd "$pkgname"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt NOTICE.txt
}
