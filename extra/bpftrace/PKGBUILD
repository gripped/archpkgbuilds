# Maintainer: Anatol Pomozov
# Contributor: Tommaso Sardelli <lacapannadelloziotom at gmail dot com>

pkgname=bpftrace
pkgver=0.21.0
pkgrel=2
pkgdesc='High-level tracing language for Linux eBPF'
arch=('x86_64')
url='https://github.com/iovisor/bpftrace'
license=('Apache-2.0')
depends=('glibc' 'gcc-libs' 'libelf' 'zlib' 'llvm-libs' 'clang' 'bcc' 'libbpf' 'libpcap' 'zstd')
makedepends=('binutils' 'cmake' 'llvm' 'git' 'linux-headers' 'ninja' 'gtest' 'cereal'
             'asciidoctor' 'xxd')
options=('!strip' '!debug')
source=("https://github.com/iovisor/bpftrace/archive/v$pkgver/bpftrace-$pkgver.tar.gz")
sha512sums=('15ecce2a17f704a771b8fbcda77fc7e19da2066c2635e5f6829358b3fe08aeff58cc78f98d90087afe0b3effbcd2c061397b64aeb79511062cd5767519e35ad6')

build() {
  cmake -S bpftrace-$pkgver -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SYSTEM_BPF_BCC=ON
  cmake --build build
}

package() {
  depends+=(
    'binutils' 'libsframe.so'
  )

  DESTDIR="$pkgdir" cmake --install build
}
