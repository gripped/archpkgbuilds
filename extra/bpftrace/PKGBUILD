# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Tommaso Sardelli <lacapannadelloziotom at gmail dot com>

pkgname=bpftrace
pkgver=0.24.2
pkgrel=4
pkgdesc='High-level tracing language for Linux eBPF'
arch=('x86_64')
url='https://github.com/iovisor/bpftrace'
license=('Apache-2.0')
depends=(
  'bcc'
  'clang'
  'gcc-libs'
  'glibc'
  'libbpf'
  'libelf'
  'libpcap'
  'llvm-libs'
  'systemd-libs'
  'zlib'
  'zstd'
)
makedepends=(
  'asciidoctor'
  'binutils'
  'bpf'
  'cereal'
  'cmake'
  'git'
  'gtest'
  'linux-headers'
  'llvm'
  'xxd'
)
options=(
  '!debug'
  '!strip'
)
source=("$url/archive/v$pkgver/bpftrace-$pkgver.tar.gz")
sha512sums=('d9ff773ce39b578877d5e73a48434d3654b46985bb4b9ad458fbf622718049da5075b3a2d2e6d1ae0a9effbdb74658822b4c4535bca384fbd6e539fdec393f4b')

build() {
  cmake -S $pkgname-$pkgver -B build \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -W no-dev \
    -D ENABLE_SYSTEMD=ON
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    'binutils' 'libsframe.so'
  )

  DESTDIR="$pkgdir" cmake --install build
}
