# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=lemurs
pkgver=0.3.2
pkgrel=2
pkgdesc="A customizable TUI display/login manager written in Rust"
arch=('x86_64')
url="https://github.com/coastalwhite/lemurs"
license=('Apache-2.0 OR MIT')
depends=('systemd' 'pam')
makedepends=('cargo')
backup=('etc/lemurs/config.toml')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('a366595602c75ed723e7b527ce1b8be33bdb998f5faa824605b97fb4b9e810f4')

prepare() {
	cd "${pkgname}-${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 extra/config.toml "${pkgdir}/etc/lemurs/config.toml"
	install -Dm 755 extra/xsetup.sh "${pkgdir}/etc/lemurs/xsetup.sh"
	install -Dm 644 extra/lemurs.pam "${pkgdir}/etc/pam.d/lemurs"
	install -Dm 644 extra/lemurs.service "${pkgdir}/usr/lib/systemd/system/lemurs.service"
	install -Dm 644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/MIT"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
