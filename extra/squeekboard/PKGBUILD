# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Ghassan Alduraibi <git@ghassan.dev>
# Contributor: Philip Goto <philip.goto@gmail.com>

pkgname=squeekboard
pkgver=1.43.1
pkgrel=2
pkgdesc='An on-screen-keyboard input method for Wayland'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/squeekboard'
license=(GPL-3.0-or-later)
depends=(feedbackd gnome-desktop)
makedepends=(meson libbsd rust wayland-protocols glib2-devel python-gobject)
optdepends=('python-gobject: squeekboard-entry command')
source=("https://gitlab.gnome.org/World/Phosh/squeekboard/-/archive/v${pkgver}/squeekboard-v${pkgver}.tar.gz")
sha256sums=('64c73636f6d8a6ffe9f1094c4084b184db3f60ac4e02bf8bff860060308c61ab')

build() {
  arch-meson "squeekboard-v${pkgver}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
