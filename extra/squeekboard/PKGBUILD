# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Ghassan Alduraibi <git@ghassan.dev>
# Contributor: Philip Goto <philip.goto@gmail.com>

pkgname=squeekboard
pkgver=1.43.1
pkgrel=4
pkgdesc='An on-screen-keyboard input method for Wayland'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/squeekboard'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  feedbackd
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gtk3
  libxkbcommon
  pango
  wayland
)
makedepends=(
  git
  glib2-devel
  libbsd
  meson
  python-gobject
  rust
  wayland-protocols
)
optdepends=('python-gobject: squeekboard-entry command')
conflicts=(stevia)
provides=(phosh-osk-provider)
source=("git+https://gitlab.gnome.org/World/Phosh/squeekboard.git#tag=v$pkgver")
b2sums=(2af3a0d790f71b04b682152fadc8fc6ffe8798da951936d84fa40362d3ccb38213239228b778428f5e01fd26cb9bde20ff8cc27550a2bfc565cd8657d58f489e)

prepare() {
  cd $pkgname

  # Add systemd unit file for compatibility with GNOME 49
  # https://gitlab.gnome.org/World/Phosh/squeekboard/-/merge_requests/712
  git cherry-pick -n 65a2e657ce7340744d81fc1ca15519031c205d9d
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
