# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: pallxk <aur@pallxk.com>
# https://docs.microsoft.com/en-us/cli/azure/install-azure-cli-linux
pkgname=azure-cli
pkgver=2.76.0
pkgrel=1
pkgdesc="Command-line tools for Microsoft Azure"
arch=('any')
url="https://github.com/Azure/azure-cli"
license=('MIT')
depends=(
  "gcc-libs"
  "glibc"
  "libsecret"
  "python"
)
makedepends=("git")
options=('!debug' '!strip')
source=("git+https://github.com/Azure/azure-cli.git#tag=azure-cli-${pkgver}"
        "az")
sha256sums=('9a7dffb6b7dd7523909b0188702156bda2317d3fe1342c15db8dc1f1f8c43567'
            '990f70efec828c1e1f3f57748fc6e40f9705f2223dc96683e8e29d118daac116')

package() {
  cd azure-cli
  mkdir -v "$pkgdir/opt"

  # We're going to just slap all of this junk into /opt because it needs so much stuff
  # with very specific versions that it's just not worth the hassle to try to fight it.
  # Sometimes you just have to be like water, my friend.
  python -m venv "$pkgdir/opt/azure-cli"
  # shellcheck disable=SC1091
  source "$pkgdir/opt/azure-cli/bin/activate"

  scripts/install_full.sh

  install -vDm755 "$srcdir/az" "$pkgdir/usr/bin/az"
  install -vDm644 az.completion "$pkgdir/usr/share/bash-completion/completions/az"

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
