# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: pallxk <aur@pallxk.com>
# https://docs.microsoft.com/en-us/cli/azure/install-azure-cli-linux
pkgname=azure-cli
pkgver=2.63.0
pkgrel=1
pkgdesc="Command-line tools for Microsoft Azure"
arch=('any')
url="https://github.com/Azure/azure-cli"
license=('MIT')
depends=("python")
makedepends=("git")
options=("!debug")
source=("git+https://github.com/Azure/azure-cli.git#tag=azure-cli-${pkgver}"
        "${pkgname}-27196.patch::https://github.com/Azure/azure-cli/compare/azure-cli-${pkgver}...4801ee240ce11b9b37c80a600302a9164db5bbca.diff"
        "az")
sha256sums=('2175e23a8700a9c5b377faebbf83a19a841753c53f86285e71f09c5e6074c742'
            '55f3e047db43e822e8819d082c284b5280a2fa32a28f3fcf074bbdcf3667a37a'
            '990f70efec828c1e1f3f57748fc6e40f9705f2223dc96683e8e29d118daac116')
options=('!strip')

prepare() {
  cd azure-cli
  patch -Np1 -i "${srcdir}"/${pkgname}-27196.patch
}

package() {
  cd azure-cli

  mkdir "$pkgdir/opt"

  # We're going to just slap all of this junk into /opt because it needs so much stuff
  # with very specific versions that it's just not worth the hassle to try to fight it.
  # Sometimes you just have to be like water, my friend.
  python -m venv "$pkgdir/opt/azure-cli"
  source "$pkgdir/opt/azure-cli/bin/activate"

  scripts/install_full.sh

  install -Dm755 "$srcdir/az" "$pkgdir/usr/bin/az"
  install -Dm644 az.completion "$pkgdir/usr/share/bash-completion/completions/az"
}
