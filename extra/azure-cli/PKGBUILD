# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: pallxk <aur@pallxk.com>
# https://docs.microsoft.com/en-us/cli/azure/install-azure-cli-linux

pkgname=azure-cli
pkgver=2.78.0
pkgrel=1
pkgdesc="Command-line tools for Microsoft Azure"
arch=('any')
url="https://github.com/Azure/azure-cli"
license=('MIT')
depends=(
  "gcc-libs"
  "glibc"
  "libsecret"
  "python"
)
makedepends=("git")
options=(
  '!debug'
  '!strip'
)
source=("git+https://github.com/Azure/azure-cli.git#tag=azure-cli-${pkgver}"
        "az")
sha256sums=('5b89c2b0d305df7c862520cd16f4a2225851b09a9d60ef55700e2d46c81a2a08'
            '990f70efec828c1e1f3f57748fc6e40f9705f2223dc96683e8e29d118daac116')

package() {
  cd $pkgname
  mkdir -v "$pkgdir/opt"

  # We're going to just slap all of this junk into /opt because it needs so much stuff
  # with very specific versions that it's just not worth the hassle to try to fight it.
  # Sometimes you just have to be like water, my friend.
  python -m venv "$pkgdir/opt/azure-cli"
  # shellcheck disable=SC1091
  source "$pkgdir/opt/azure-cli/bin/activate"

  scripts/install_full.sh

  install -vDm755 "$srcdir/az" "$pkgdir/usr/bin/az"
  install -vDm644 az.completion "$pkgdir/usr/share/bash-completion/completions/az"

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
