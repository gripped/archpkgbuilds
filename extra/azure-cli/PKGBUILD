# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: pallxk <aur@pallxk.com>
# https://docs.microsoft.com/en-us/cli/azure/install-azure-cli-linux

pkgname=azure-cli
pkgver=2.80.0
pkgrel=1
pkgdesc="Command-line tools for Microsoft Azure"
arch=('any')
url="https://github.com/Azure/azure-cli"
license=('MIT')
depends=(
  "gcc-libs"
  "glibc"
  "libsecret"
  "python"
)
makedepends=("git")
options=(
  '!debug'
  '!strip'
)
source=("git+https://github.com/Azure/azure-cli.git#tag=azure-cli-${pkgver}"
        "az")
sha512sums=('608eca7dd240af8735eb5726e04356c744355a689e52a9cb76c636ef76dc945c5817de0a832043ca3aec375308e5c11a563a57ff1c02ff59a8b0ce1f2b5011e0'
            '94a9d3daeced17fccae9cb94638a82db0515e4c7fd6b8bbe68f9b428dc84c19a6c956a809c160083e040180914665a3980c257e9f210c431c8d46d574bf113ba')

package() {
  cd $pkgname
  mkdir -v "$pkgdir/opt"

  # We're going to just slap all of this junk into /opt because it needs so much stuff
  # with very specific versions that it's just not worth the hassle to try to fight it.
  # Sometimes you just have to be like water, my friend.
  python -m venv "$pkgdir/opt/azure-cli"
  # shellcheck disable=SC1091
  source "$pkgdir/opt/azure-cli/bin/activate"

  scripts/install_full.sh

  install -vDm755 "$srcdir/az" "$pkgdir/usr/bin/az"
  install -vDm644 az.completion "$pkgdir/usr/share/bash-completion/completions/az"

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
