# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: pallxk <aur@pallxk.com>
# https://docs.microsoft.com/en-us/cli/azure/install-azure-cli-linux
pkgname=azure-cli
pkgver=2.65.0
pkgrel=1
pkgdesc="Command-line tools for Microsoft Azure"
arch=('any')
url="https://github.com/Azure/azure-cli"
license=('MIT')
depends=("python")
makedepends=("git")
options=('!debug' '!strip')
source=("git+https://github.com/Azure/azure-cli.git#tag=azure-cli-${pkgver}"
        "az")
sha256sums=('d19e3b921efb076af4f29e6b4998579405f8b32651465f9417952ba584e7a1ca'
            '990f70efec828c1e1f3f57748fc6e40f9705f2223dc96683e8e29d118daac116')

package() {
  cd azure-cli

  mkdir "$pkgdir/opt"

  # We're going to just slap all of this junk into /opt because it needs so much stuff
  # with very specific versions that it's just not worth the hassle to try to fight it.
  # Sometimes you just have to be like water, my friend.
  python -m venv "$pkgdir/opt/azure-cli"
  source "$pkgdir/opt/azure-cli/bin/activate"

  scripts/install_full.sh

  install -Dm755 "$srcdir/az" "$pkgdir/usr/bin/az"
  install -Dm644 az.completion "$pkgdir/usr/share/bash-completion/completions/az"
}
