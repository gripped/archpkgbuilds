# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown
pkgver=4.1.15
pkgrel=1
pkgdesc="FeelUOwn Music Player"
arch=('any')
url="https://github.com/feeluown/FeelUOwn"
license=('GPL-3.0-or-later')
depends=('python-qasync' 'python-pyqt5' 'mpv' 'python-opengl' 'python-janus' 'python-requests'
         'python-tomlkit' 'python-packaging' 'python-pydantic' 'python-mutagen' 'python-openai'
         'qt5-svg' 'xdg-user-dirs')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-mock' 'python-pytest-qt'
              'python-json-rpc' 'xorg-server-xvfb' 'yt-dlp')
optdepends=('yt-dlp: browser cookie extraction support'
            'dbus-python: mpris2 server support'
            'python-sanic: webserver support'
            'python-json-rpc: webserver support'
            'python-websockets: webserver support'
            'feeluown-netease' 'feeluown-qqmusic' 'feeluown-ytmusic' 'feeluown-bilibili')
conflicts=('feeluown-local')
provides=('feeluown-local')
replaces=('feeluown-local')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/FeelUOwn.git#tag=v$pkgver"
        "feeluown.desktop"
	"pytest-skip-benchmark-cov-plugin.patch")
sha512sums=('4d9be5929f116c08dc78e5caf77546e8323385cc16f036f534d7751b72c0039454b8ace2d78b4beef9b4ea82724c5991a4842b6460d76b64503f78178e3ff618'
            '48882f7469c22e5db332663bc1aa8b398b0a10a0c929d4d7e3d7b8b91205d7d3070c5fa295cb9a14f8c352bff57978bfaad167343e0ddc51c92417eda07c8087'
            '97e1d3133c921b1071ad1f6966b5175755cde22660bc76d2f107ddea9d3cde6b105636ed39838d4e81bef168b40ade130c09e8f9752bf1ac77d67cff2a42bae1')

prepare() {
  cd FeelUOwn
  # qasync 0.28+ compatibility
  git cherry-pick -n 0100e01a6c2c1ab71138b39ddb08ae526a61a5b6
  patch -Np1 -i ../pytest-skip-benchmark-cov-plugin.patch
  # TODO: figure out how to use virtual audio output
  sed -i 's/cannot_play_audio = is_travis_env/cannot_play_audio = os.environ.get("CANNOT_PLAY_AUDIO") == "1"/' tests/helpers.py
}

build() {
  cd FeelUOwn
  python setup.py build
}

check() {
  cd FeelUOwn
  CANNOT_PLAY_AUDIO=1 xvfb-run pytest --deselect=tests/test_bench.py
}

package() {
  cd FeelUOwn
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 feeluown/gui/assets/icons/feeluown.png -t "$pkgdir"/usr/share/icons/hicolor/512x512/apps/
  install -Dm644 "$srcdir"/feeluown.desktop -t "$pkgdir"/usr/share/applications/
}
