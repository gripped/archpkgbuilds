# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown
pkgver=4.1.1
pkgrel=1
pkgdesc="FeelUOwn Music Player"
arch=('any')
url="https://github.com/feeluown/FeelUOwn"
license=('GPL3')
depends=('python-qasync' 'python-pyqt5' 'mpv' 'python-opengl' 'python-janus' 'python-requests'
         'python-tomlkit' 'python-packaging' 'python-pydantic' 'python-mutagen' 'qt5-svg'
         'xdg-user-dirs')
makedepends=('python-setuptools')
checkdepends=('python-pytest-benchmark' 'python-pytest-cov' 'python-pycryptodome')
optdepends=('python-secretstorage: browser cookie extraction support'
            'python-pycryptodome: browser cookie extraction support'
            'dbus-python: mpris2 server support'
            'python-sanic: webserver support'
            'python-json-rpc: webserver support'
            'python-websockets: webserver support'
            'feeluown-netease' 'feeluown-qqmusic' 'feeluown-ytmusic' 'feeluown-bilibili')
conflicts=('feeluown-local')
provides=('feeluown-local')
replaces=('feeluown-local')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/feeluown/feeluown-$pkgver.tar.gz"
        "feeluown.desktop")
sha512sums=('fc8a0b4ec966eb1b8d93323e2c5e2e51b2622e7b41815cbebe4270ae16d91d7c164c05245e0061c1c3fb70259526b14267e6ee95a002244792306c40e94aa44a'
            '48882f7469c22e5db332663bc1aa8b398b0a10a0c929d4d7e3d7b8b91205d7d3070c5fa295cb9a14f8c352bff57978bfaad167343e0ddc51c92417eda07c8087')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  pytest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 feeluown/gui/assets/icons/feeluown.png -t "$pkgdir"/usr/share/icons/hicolor/512x512/apps/
  install -Dm644 "$srcdir"/feeluown.desktop -t "$pkgdir"/usr/share/applications/
}
