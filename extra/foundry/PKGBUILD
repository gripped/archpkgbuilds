# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=foundry
pkgver=1.0.0
pkgrel=1
pkgdesc='Aims to extract much of what makes GNOME Builder an IDE into a library and companion command-line tool'
arch=(x86_64)
url='https://gitlab.gnome.org/GNOME/foundry'
license=(LGPL-2.1-or-later)
depends=(
  cmark
  dconf
  editorconfig-core-c
  flatpak
  gcc-libs
  glib2
  glibc
  gom
  gtk4
  gtksourceview5
  json-glib
  libdex
  libgit2
  libpeas-2
  libsoup3
  libspelling
  libssh2
  libsysprof-capture
  libxml2
  libyaml
  pango
  template-glib
  vte4
  webkitgtk-6.0
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/foundry.git#tag=${pkgver/[a-z]/.&}")
b2sums=(e54513c35c8e7b22fd375ba22b0c9fe49016370686844e8fa3f51b8bf64d81bde38b6003602ce4a8a20191265be37c784bd61e6df6279b9fd1f453eca0710212)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
