# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=foundry
pkgver=1.0.1
pkgrel=2
pkgdesc='Aims to extract much of what makes GNOME Builder an IDE into a library and companion command-line tool'
arch=(x86_64)
url='https://gitlab.gnome.org/GNOME/foundry'
license=(LGPL-2.1-or-later)
depends=(
  cmark
  dconf
  editorconfig-core-c
  flatpak
  gcc-libs
  glib2
  glibc
  gom
  gtk4
  gtksourceview5
  json-glib
  libdex
  libgit2
  libpeas-2
  libsoup3
  libspelling
  libssh2
  libsysprof-capture
  libxml2
  libyaml
  pango
  template-glib
  vte4
  webkitgtk-6.0
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/foundry.git#tag=${pkgver/[a-z]/.&}")
b2sums=(52cc15ba1e9878f2b9097c5652343caa91632afc99af29b2dbdaf0b1174cbd886dffeaae3a3917c3ce00778466feef50dc249842d1d638b3544a981c6c325cc5)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
