# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=gnuradio-iqbal
_pkgname=gr-iqbal
pkgver=0.38.3
pkgrel=2
pkgdesc="Gnuradio I/Q balancing"
arch=(x86_64)
url="https://cgit.osmocom.org/gr-iqbal/"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  python
  python-pygccxml
)
makedepends=(
  boost
  cmake
  fftw
  git
  gmp
  gnuradio
  libvolk
  pybind11
)
provides=(libgnuradio-iqbalance.so)
replaces=(gr-iqbal)
source=(
  "git+https://gitea.osmocom.org/sdr/gr-iqbal.git#tag=v$pkgver"
  "git+https://gitea.osmocom.org/sdr/libosmo-dsp.git"  # TODO: package this instead
   boost-1.89.patch
)
sha256sums=('cd30127783b952545f3ab1f2675fd59ee52c1264769b49838da0f09b4bfb4abe'
            'SKIP'
            '23594ed764caf13f104a06245f61a2c99232afa8d3635a3461ad88296b314805')

prepare() {
  cd $_pkgname
  git submodule init
  git submodule set-url libosmo-dsp "$srcdir/libosmo-dsp"
  git -c protocol.file.allow=always submodule update

  patch -p1 -i ../boost-1.89.patch
}

build() {
  cd $_pkgname
  local cmake_options=(
    -S . -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -W no-dev
    -D CMAKE_LIBRARY_PATH=/usr/lib
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    fftw libfftw3f.so
    gnuradio libgnuradio-pmt.so libgnuradio-runtime.so
  )

  cd $_pkgname
  DESTDIR="$pkgdir" cmake --install build
}
