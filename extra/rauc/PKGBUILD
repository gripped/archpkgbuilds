# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rauc
pkgver=1.15.1
pkgrel=1
pkgdesc="Safe and secure software updates for embedded Linux"
arch=(x86_64)
url="https://rauc.io"
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
  libnl
  openssl
  sh
)
makedepends=(
  curl
  dbus
  glib2
  glib2-devel
  json-glib
  meson
  python
  python-sphinx
  python-sphinx_rtd_theme
  systemd
  util-linux
)
checkdepends=(squashfs-tools)
optdepends=(
  'casync: for casync conversion support'
  'cryptsetup: for disk encryption support'
  'dosfstools: for creating and checking FAT filesystems'
  'e2fsprogs: for creating and checking ext2/3/4 filesystems'
  'efibootmgr: for interacting with EFI boot manager'
  'grub: for GRUB support'
  'mtd-utils: for interacting with MTD devices'
  'squashfs-tools: for creating and inspecting bundles'
  'uboot-tools: for u-boot support'
)
source=(
  https://github.com/rauc/rauc/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz{,.asc}
  $pkgname.tmpfiles
)
sha512sums=('8fc9f70f5e2f75f1e29a99880ff127280abf3086e1bbae17a0fbd6b0bce8176a33f78431ccb7b2ffb5e7c16fccff7be8c6297d9f6befcdf09a4526845f5d53af'
            'SKIP'
            '35173525a8529c4d5a227186db353107193bf8cdd3e8ab0cd94c418b34187be50fe379bcabf623bfddbf1916e8e7e965800a269e823f7ee9c26f44ac1f689c94')
b2sums=('d8f6ad754636f3b93fe900f2ed5a9f90c8733879d43daa135101c6fd38913a954bc26f7be3a6d85a86c58ddeeeb7d9a2cdad4abd83965edfa2f03aa5a99e61c7'
        'SKIP'
        'e970b292f89cf4663b4ea318965444dc9bb47ef5f937143f180a372c5906f534e7c2f234dfa824b70434011069ea7758e4d6c8afd73dd5c4d92337da7e281eca')
validpgpkeys=("977843FD5C3EBF76BAD4008EA58CC53DBF05E090")  # Jan LÃ¼bbe <jlu@pengutronix.de>

build() {
  arch-meson -D manpages=true $pkgname-$pkgver build
  meson compile -C build

}

check() {
  local meson_tests=($(meson test -C build --list))
  # disable broken tests
  local disabled_tests=()
  for target in "${disabled_tests[@]}"; do
    for i in "${!meson_tests[@]}"; do
      if [[ ${meson_tests[i]} = $target ]]; then
        unset 'meson_tests[i]'
      fi
    done
  done

  meson test -C build "${meson_tests[@]}"
}

package() {
  depends+=(
    curl libcurl.so
    glib2 lib{gobject,gio,glib}-2.0.so
    json-glib libjson-glib-1.0.so
    util-linux-libs libfdisk.so
  )

  meson install -C build --destdir "$pkgdir"
  install -vdm 755 "$pkgdir/etc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/contrib/{grub.conf,uboot.sh} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
