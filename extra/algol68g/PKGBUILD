# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Karolina Lindqvist <karolina.lindqvist@kramnet.se>

pkgbase=algol68g
pkgname=(algol68g algol68g-doc)
pkgver=3.8.5
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(x86_64)
_host='https://jmvdveer.home.xs4all.nl'
url="$_host/en.algol-68-genie.html"
license=(GPL-3.0-or-later)
depends=(gsl mpfr plotutils)
optdepends=('postgresql-libs: for PostgreSQL support')
source=("$_host/$pkgbase-$pkgver.tar.gz"
        "$_host/learning-algol-68-genie.pdf")
sha256sums=('cce7ea293d5e4c1ecc15bbe299aedb6ee9100de66546f7142fd520d10147b36f'
            '827cca186efed0a09cfa1abe1a32f7074064d6729ad2ed4552e121f72c1394cb')
b2sums=('9d52e69e368eba407739b9d180f9cc785c3a5c9be99b24259f9a123467d915f6c9b86f78b4ce2d82472ef8175b677b32c8ba37160c3894df0542c02fb5f5bf05'
        '913d7788bf894fb11c026257594879638e59e1d08eea011b25bf5dc7566ec01d174ae0d0dd364679f5af1b21f10bbf8b4a6c3cfe4e529d977d8f829016fcbddd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 learning-algol-68-genie.pdf "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
