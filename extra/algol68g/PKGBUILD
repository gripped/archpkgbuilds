# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Karolina Lindqvist <karolina.lindqvist@kramnet.se>

pkgbase=algol68g
pkgname=(algol68g algol68g-doc)
pkgver=3.5.14
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(x86_64)
_host='https://jmvdveer.home.xs4all.nl'
url="$_host/en.algol-68-genie.html"
license=(GPL-3.0-or-later)
depends=(gsl mpfr plotutils)
optdepends=('postgresql-libs: for PostgreSQL support')
source=("$_host/$pkgbase-$pkgver.tar.gz"
        "$_host/learning-algol-68-genie.pdf")
sha256sums=('b88cbcac88548e8862409fcae44a6bb082125cc031d70dbb237706a3ff47f569'
            '34a3c9d6257381fc28d63866c57e18a832a792a732678860be4c420e4dd1af2c')
b2sums=('95806e4c5ba2f2d74b4ea40abe56f07a6dcfff5c545489183a24c554b7d1281b058ca8adac1e4ad734b8a88c2ae7468aa1d445ef7e1a787f6d923a38a6f1c4f1'
        '28315b7bfe8cc20df0189546f35cebea7009f96d4179bd5b926184eba7154c0b96219b6ea489773e3356052f5322c41242ca81b32f320817108a707acca384ac')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 learning-algol-68-genie.pdf "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
