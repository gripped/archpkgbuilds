# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Karolina Lindqvist <karolina.lindqvist@kramnet.se>

pkgbase=algol68g
pkgname=(algol68g algol68g-doc)
pkgver=3.5.12
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(x86_64)
_host='https://jmvdveer.home.xs4all.nl'
url="$_host/en.algol-68-genie.html"
license=(GPL-3.0-or-later)
depends=(gsl mpfr plotutils)
optdepends=('postgresql-libs: for PostgreSQL support')
source=("$_host/$pkgbase-$pkgver.tar.gz"
        "$_host/learning-algol-68-genie.pdf")
sha256sums=('720ab4411d589180a85ba1d4c63c90b5a77e9be1b345e27c25ceb743d88cd71f'
            '0df14ff9496b422a7f553621b7776ba52e88addbe78eeb6c4fbc6f71cb6ca7b2')
b2sums=('52d855b4dce6ddefcba5013a0c66a84c4c86109b5cf63cb806f26fef1a165f4383bfd29bfed37460e332874eb1754900a3b6ad8b816970410911b67a5573145c'
        'fa53026a6d12d85b008660801d632547447b0bea46258a39ec8d9801665dbf1bc5be5e1530c468665b6acda1760da6209f2837da0074b7078d5b83b77436402c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 learning-algol-68-genie.pdf "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
