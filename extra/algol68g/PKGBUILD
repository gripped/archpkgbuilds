# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Karolina Lindqvist <karolina.lindqvist@kramnet.se>

pkgname=(algol68g algol68g-doc)
pkgver=3.5.3
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(x86_64)
_host='https://jmvdveer.home.xs4all.nl'
url="$_host/en.algol-68-genie.html"
license=(GPL)
depends=(gsl mpfr plotutils)
optdepends=('postgresql-libs: for postgresql support')
source=("$_host/algol68g-$pkgver.tar.gz"
        "$_host/learning-algol-68-genie.pdf")
sha256sums=('7df60b13778233bd925e3e447bc6eb0abf8f12c01664bb8118707ea31b938c33'
            '1ccfed1601c006b435048a552cc0bc3ebf116fb2ea11b25c6a19e30623d0a24c')
b2sums=('d7052104650fdbbed6014f7a1b01d7bc037d44061ebbf91a46abc6d7bab5865c94a254881bb3915e59676b0fee98dd378a1d90f5cd39ea2369433ad7c425ebb8'
        '417827a210ebe6c556e460fc81187ca5541efb464836f247f0dc280e37a22f334d8f7699e3141a839f7e7b095146443bc79951191770ee68a3468cababcd3732')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" \
    docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 learning-algol-68-genie.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
