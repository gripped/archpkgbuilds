# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-deblock
pkgver=7.1
pkgrel=1
pkgdesc='Deblock plugin for VapourSynth'
arch=(x86_64)
url=https://github.com/HomeOfVapourSynthEvolution/VapourSynth-Deblock
license=(GPL2)
depends=(
  gcc-libs
  glibc
  vapoursynth
)
makedepends=(
  git
  meson
)
_tag=7688426eaaaa87fba4a9ab3c62e50eebad4f4e5c
source=(git+https://github.com/HomeOfVapourSynthEvolution/VapourSynth-Deblock.git#tag=${_tag})
b2sums=('6333fd58504b07101a37ed6867d4f09aec70275c2191a7241dce7ff34255c7223e0d4eaf292934e7dbca53da6e097581b70f1027c44deb974947070f61bd650b')

pkgver() {
  cd VapourSynth-Deblock

  git describe --tags | sed 's/^r//'
}

build() {
  arch-meson VapourSynth-Deblock build
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
