# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=schemacrawler
pkgver=17.5.0
pkgrel=1
pkgdesc="Free database schema discovery and comprehension tool"
arch=('any')
url="https://www.schemacrawler.com"
license=('GPL-3.0-only')
depends=('java-runtime' 'graphviz')
makedepends=('maven' 'java-environment=17' 'unzip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/schemacrawler/SchemaCrawler/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('5727b2c5b81624aef28d6af5e54b1cacea4eaafb2793e83f9524937bbdb684244a1a254124df3e5de9b96abd1ac3fe4ce7ec6a44dae55c34b5433917b73b3293')

build() {
  cd SchemaCrawler-$pkgver

  export JAVA_HOME="/usr/lib/jvm/java-17-openjdk"
  mvn -Ddistrib -DskipTests package
}

package() {
  cd SchemaCrawler-$pkgver/schemacrawler-distrib/target
  unzip schemacrawler-$pkgver-distribution.zip

  # Delete the cassandra drivers.  For some reason these end up being pulled in
  # and causes the application to fail due to missing dependencies.  Upstream
  # is confused said to simply delete them.
  # https://github.com/schemacrawler/SchemaCrawler/issues/1560
  find schemacrawler-$pkgver-distribution/_schemacrawler/lib/ -name 'cassandra-*.jar' -delete

  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r schemacrawler-$pkgver-distribution "$pkgdir"/opt/schemacrawler
  echo -e '#!/bin/bash\ncd /opt/schemacrawler/_schemacrawler/bin\n./schemacrawler.sh $@' > "$pkgdir"/usr/bin/schemacrawler
  chmod -R +r "$pkgdir"/opt/schemacrawler
  chmod +x "$pkgdir"/usr/bin/schemacrawler
}

# vim: ts=2 sw=2 et:
