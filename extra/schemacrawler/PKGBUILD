# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=schemacrawler
pkgver=16.27.1
pkgrel=1
pkgdesc="Free database schema discovery and comprehension tool"
arch=('any')
url="https://www.schemacrawler.com"
license=('GPL-3.0-only')
depends=('java-runtime' 'graphviz')
makedepends=('maven' 'java-environment=17' 'unzip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/schemacrawler/SchemaCrawler/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('9144031b0ed9a9f80bc4f9536ab3a62e17f8ca8c063ab632728e219120f321bbaa5d153727d2695df71eb655058e64bf6af2434a9f789dbd6f2554f1beca3325')

build() {
  cd SchemaCrawler-$pkgver

  export JAVA_HOME="/usr/lib/jvm/java-17-openjdk"
  mvn -Ddistrib -DskipTests package
}

package() {
  cd SchemaCrawler-$pkgver/schemacrawler-distrib/target
  unzip schemacrawler-$pkgver-distribution.zip

  # Delete the cassandra drivers.  For some reason these end up being pulled in
  # and causes the application to fail due to missing dependencies.  Upstream
  # is confused said to simply delete them.
  # https://github.com/schemacrawler/SchemaCrawler/issues/1560
  find schemacrawler-$pkgver-distribution/_schemacrawler/lib/ -name 'cassandra-*.jar' -delete

  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r schemacrawler-$pkgver-distribution "$pkgdir"/opt/schemacrawler
  echo -e '#!/bin/bash\ncd /opt/schemacrawler/_schemacrawler/bin\n./schemacrawler.sh $@' > "$pkgdir"/usr/bin/schemacrawler
  chmod -R +r "$pkgdir"/opt/schemacrawler
  chmod +x "$pkgdir"/usr/bin/schemacrawler
}

# vim: ts=2 sw=2 et:
