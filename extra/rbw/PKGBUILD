# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jesse Luehrs <archlinux@tozt.net>

pkgname=rbw
pkgver=1.8.1
pkgrel=1
pkgdesc='Unofficial Bitwarden CLI'
arch=('x86_64')
url='https://git.tozt.net/rbw'
license=('MIT')
depends=('glibc' 'gcc-libs' 'pinentry')
makedepends=('rust')
options=('!lto')
source=("$url/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('399db19b4e6955b0df8fb61de5d50738b7e3abebc3a55e6e32cda05d59315724a106564269221ef30b262921900a7dcce0c79a954463ddd246febe0a62862779')
b2sums=('84bd0206d9ee654a2b555707fea92813497475ef1ed33765db3673e9ac32ae90ca2c333d07705930f1bbee2af62d0a22dc0328f8b6b8f8ff15865ed5ee0d66cd')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features

  # generate shell auto-completions
  for completion in bash fish zsh; do
    cargo run --frozen --release --bin rbw -- \
      gen-completions "$completion" > "$completion-completions"
  done
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rbw{,-agent}

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md

  # shell auto-completions
  install -vDm644 bash-completions "$pkgdir/usr/share/bash-completion/completions/rbw"
  install -vDm644 fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/rbw.fish"
  install -vDm644 zsh-completions "$pkgdir/usr/share/zsh/site-functions/_rbw"
}
