# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jesse Luehrs <archlinux@tozt.net>

pkgname=rbw
pkgver=1.11.0
pkgrel=1
pkgdesc='Unofficial Bitwarden CLI'
arch=('x86_64')
url='https://git.tozt.net/rbw'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'pinentry'
)
makedepends=(
  'git'
  'rust'
)
options=('!lto')
source=("git+https://github.com/doy/rbw.git#tag=$pkgver")
sha512sums=('c3b3f42a9717d1154aacb8485af48c68c46cb8be4476ef9d299f328d132fcc1bdc702e39b9b69e4d074d4f1560a1614ec461d1f8273277ad2e5da32625bc50e8')
b2sums=('34e8cefb6c29b748046ba3aed65231ea2a7b7bf709df07f33989ad9189fb51652414d30eefe1909659872ac8f2252541a05f1df53d1ddddb4fa2b57347dd0ad0')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release --all-features

  # generate shell auto-completions
  for completion in bash fish zsh; do
    cargo run --frozen --release --bin rbw -- \
      gen-completions "$completion" > "$completion-completions"
  done
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rbw{,-agent}

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md

  # shell auto-completions
  install -vDm644 bash-completions "$pkgdir/usr/share/bash-completion/completions/rbw"
  install -vDm644 fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/rbw.fish"
  install -vDm644 zsh-completions "$pkgdir/usr/share/zsh/site-functions/_rbw"
}
