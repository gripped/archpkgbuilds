# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jesse Luehrs <archlinux@tozt.net>

pkgname=rbw
pkgver=1.13.2
pkgrel=1
pkgdesc='Unofficial Bitwarden CLI'
arch=('x86_64')
url='https://git.tozt.net/rbw'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'pinentry'
)
makedepends=(
  'git'
  'rust'
)
options=('!lto')
source=("git+https://github.com/doy/rbw.git#tag=$pkgver")
sha512sums=('ab0bb3e4db9d48724588eee630acc2c314bd97675eade8573a109e179d6ba7dde707e21e8b932ab2f497a6161027625507a5c44b567c98b1312e3ca1a36ee2c4')
b2sums=('36ab5d82fa7810272a18045b04a9655414e123773484336e71592bc4bfb4bb5999158e379314278774d9453d4165b211a5198fc0c3e71df92cf561e3648eb7e7')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release --all-features

  # generate shell auto-completions
  for completion in bash fish zsh; do
    cargo run --frozen --release --bin rbw -- \
      gen-completions "$completion" > "$completion-completions"
  done
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rbw{,-agent}

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md

  # shell auto-completions
  install -vDm644 bash-completions "$pkgdir/usr/share/bash-completion/completions/rbw"
  install -vDm644 fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/rbw.fish"
  install -vDm644 zsh-completions "$pkgdir/usr/share/zsh/site-functions/_rbw"
}
