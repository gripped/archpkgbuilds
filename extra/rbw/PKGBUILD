# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jesse Luehrs <archlinux@tozt.net>

pkgname=rbw
pkgver=1.14.0
pkgrel=1
pkgdesc='Unofficial Bitwarden CLI'
arch=('x86_64')
url='https://git.tozt.net/rbw'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'pinentry'
)
makedepends=(
  'git'
  'rust'
)
options=('!lto')
source=("git+https://github.com/doy/rbw.git#tag=$pkgver")
sha512sums=('45765f6730c8f4def4873b0c9fbfe38a68d7dd1fe295bbc4968e7b709c7bfdd8b9a44a072dd612ac92024c19a6cc2506a0fe36591771fd1e06aa828ad88930b0')
b2sums=('01239638fb0eb61159af14f0bba6c532e5de641d716c8b3d621de1e93b2758721f5cd54499e76752810c2933aa9e2265083cb6af983802220dda8a5f30e2a4ff')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release --all-features

  # generate shell auto-completions
  for completion in bash elvish fish zsh; do
    cargo run --frozen --release --bin rbw -- \
      gen-completions "$completion" > "$completion-completions"
  done
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rbw{,-agent}

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md

  # shell auto-completions
  install -vDm644 bash-completions "$pkgdir/usr/share/bash-completion/completions/rbw"
  install -vDm644 elvish-completions "$pkgdir/usr/share/elvish/lib/rbw.elv"
  install -vDm644 fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/rbw.fish"
  install -vDm644 zsh-completions "$pkgdir/usr/share/zsh/site-functions/_rbw"
}
