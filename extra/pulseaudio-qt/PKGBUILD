# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=pulseaudio-qt
pkgver=1.3
pkgrel=2
pkgdesc='Qt bindings for libpulse'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(gcc-libs
         glibc
         libpulse
         qt5-base)
makedepends=(doxygen
             extra-cmake-modules
             qt5-doc
             qt5-tools)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/libraries/pulseaudio-qt/-/commit/f8ef220e.patch)
sha256sums=('bceb07630d946fa0ce928b177687f618186cdef2309828188211d61a36589ec4'
            'SKIP'
            'f501e14a6361dfc8a55ac61503cb0a0032fa5b097fe1d0f781f94e3dab94d8cf')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E  # Jonathan Riddell <jr@jriddell.org>
              F53223F3337665C2CCEA122E873AC3459BBCF5C0) # Nicolas Fella <nicolas.fella@kdab.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < f8ef220e.patch # Fix crash when removing profiles
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_QCH=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
