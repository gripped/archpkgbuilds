# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: David Manouchehri <manouchehri@riseup.net>
# Contributor: Sven Lechner <SirWindfield@users.noreply.github.com>

pkgname=pam-u2f
_name="${pkgname/-/_}"
pkgver=1.4.0
pkgrel=2
pkgdesc="Universal 2nd Factor (U2F) PAM authentication module from Yubico"
url="https://developers.yubico.com/pam-u2f"
arch=('x86_64')
license=('BSD-2-Clause')
depends=('libfido2' 'openssl' 'pam')
makedepends=('asciidoc' 'cmake' 'git')
replaces=("${_name}")
source=("git+https://github.com/Yubico/pam-u2f.git#tag=${_name}-${pkgver}?signed"
        'pam-u2f.conf')
b2sums=('f6111e61a59c6ed656a77281a4d6dbe3b07e8018ab433e41a3e1427a83cb3a6e28221b517eea36b1807ced7179932fcdeac381adf8361220ee4bdc988f47cfe5'
        'dc7bcc00304f500f808d9d69f154b4d1e2e9abc9110f681e3be4072d3983313db02fd5df599bf1daa3b3cd7449be2f469cc8e7f38d48bd71197b0b8569c49f03')
# Yubico code signing keys are listed at <https://developers.yubico.com/Software_Projects/Software_Signing.html>
# This is how upstream is establishing trust paths
validpgpkeys=('B70D62AA6A31AD6B9E4F9F4BDC8888925D25CA7A'  # Alessio Di Mauro <alessio@yubico.com>
              '78D997D53E9C0A2A205392ED14A19784723C9988'  # Ludvig Michaelsson <ludvig.michaelsson@yubico.com>
              'F475928225229F58460640EAD91777C175533EE5') # Giovanni Simoni <giovanni.simoni@yubico.com>

build() {
	cmake -B build -S "${pkgname}" \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-DCMAKE_BUILD_TYPE='None' \
		-DPAM_DIR='/usr/lib/security' \
		-Wno-dev
	cmake --build build
}

check() {
	ctest --test-dir build --output-on-failure
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 "${pkgname}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

	# Relax polkit-agent sandbox permissions for accessing hardware keys
	# See https://gitlab.archlinux.org/archlinux/packaging/packages/pam-u2f/-/merge_requests/1
	install -Dm 644 "${pkgname}.conf" "${pkgdir}/usr/lib/systemd/system/polkit-agent-helper@.service.d/${pkgname}.conf"
}
