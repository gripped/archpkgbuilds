# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=2.5.2
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=(
  'libusb'
  'python'
  'python-adb-shell'
  'python-appdirs'
  'python-click'
  'python-cryptography'
  'python-iosbackup'
  'python-libusb1'
  'python-packaging'
  'python-pyahocorasick'
  'python-requests'
  'python-rich'
  'python-simplejson'
  'python-tld'
  'python-tqdm'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('8c9e4f4b2b041c9ce892bc64c094d41847a5f9361a7a6535438986d0042906b9b82d4e4b4f40e7a16d59c6001ef9cca1d5545e5f41d52a5437077167f0799642')
b2sums=('2fae8d69d830c5082c60d40372353bf2139f742546ccc40823f40e3003f5a607655af494ee3ff1960d9f023223bf5beb65c3cee25b8310ef1ceb0de4e16393aa')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

check() {
  cd "mvt-${pkgver}"
  dev/mvt-android --help
  dev/mvt-ios --help
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
