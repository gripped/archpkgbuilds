# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=2.4.4
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=(
  'libusb'
  'python'
  'python-adb-shell'
  'python-appdirs'
  'python-click'
  'python-cryptography'
  'python-iosbackup'
  'python-libusb1'
  'python-packaging'
  'python-pyahocorasick'
  'python-requests'
  'python-rich'
  'python-simplejson'
  'python-tld'
  'python-tqdm'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('7e925443ab4481ec02568910b6cc60af0da656c66190a588657c14b4a701aba983c2af8e9c6bec9614c9dd03871e19dbd6dc04ac634433c426015a7079b4e71b')
b2sums=('4cd2943d30ac72b00c820048c96f4634f334689b3bd67f3b8701ea8d78a08f3e9ec8cc4b9c74fbe73503b46940a5d481964c2e93ec57257812f121143b5f12ff')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

check() {
  cd "mvt-${pkgver}"
  dev/mvt-android --help
  dev/mvt-ios --help
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
