# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=2.5.1
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=(
  'libusb'
  'python'
  'python-adb-shell'
  'python-appdirs'
  'python-click'
  'python-cryptography'
  'python-iosbackup'
  'python-libusb1'
  'python-packaging'
  'python-pyahocorasick'
  'python-requests'
  'python-rich'
  'python-simplejson'
  'python-tld'
  'python-tqdm'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('f22738f15ff5c3d0ce8bebe192bf7e69812928e80f2a9ddef547a6ea43117e0c6645a67485f9ff47c4dd7dc905e05022a381225e6df90cb6c6650b03fb36f206')
b2sums=('1a6611d2a7bb003485c414ee048a796696d14b73559e95b86b6b76fc2bd53e95691f4cc6e0c26390280ee7d86ee7559ec010973c5031748f5aaf031534bf42df')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

check() {
  cd "mvt-${pkgver}"
  dev/mvt-android --help
  dev/mvt-ios --help
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
