# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pydle
pkgver=1.1.0
pkgrel=2
pkgdesc="A compact, flexible and standards-abiding IRC library"
url="https://codeberg.org/shiz/pydle"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
optdepends=('python-pure-sasl: for SASL support')
checkdepends=('python-pytest-asyncio' 'python-pure-sasl')
source=("${pkgname}-${pkgver}.tar.gz::https://codeberg.org/shiz/pydle/archive/v${pkgver}.tar.gz")
sha512sums=('e095f2e4c47377db1cda968af8b46b052bb195ae0e4e0bbfe274ad9cd16041757fe25e01adbba9932af1b58c47308c47893386dd245b1398b3e2c3409475c824')

prepare() {
  cd pydle
}

build() {
  cd pydle
  python -m build -nw
}

check() {
  cd pydle

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd pydle
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
