# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-repo-db
_upstream=alpm
pkgver=0.1.1
pkgrel=1
pkgdesc="Command line interfaces for the handling of alpm-repo-db files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-repo-db/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('ef10652356d956ed2412a33d74dcd480d001336ae5414624e1abb6017b333801962daa63b0a7b7b290360aae4101d5a2ebac4d4aed7f2982a2a55a05233399f7')
b2sums=('ac24d6e7eea98d0d1f7b6c658a47ce25e34d155b988c2c075406a2942c214accfe9e429e2ea588393c303b97e14fe98f550eeb83d15bf720325dc646a3e52740')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --features cli --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
  just generate specifications $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  conflicts=(alpm-files)
  replaces=(alpm-files)

  local bins=(
    alpm-repo-desc
    alpm-repo-files
  )

  cd $_upstream
  for _bin in "${bins[@]}"; do
    install -vDm 755 target/release/$_bin -t "$pkgdir/usr/bin/"
    install -vDm 644 output/shell_completions/$_bin.bash "$pkgdir/usr/share/bash-completions/completions/$_bin"
    install -vDm 644 output/shell_completions/$_bin.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
    install -vDm 644 output/shell_completions/_$_bin -t "$pkgdir/usr/share/zsh/site-functions/"
  done

  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
