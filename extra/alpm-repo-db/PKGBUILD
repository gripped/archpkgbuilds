# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-repo-db
_upstream=alpm
pkgver=0.1.0
pkgrel=1
pkgdesc="Command line interfaces for the handling of alpm-repo-db files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-repo-db/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('42a1dfc294495234663430b0c48b275edc3acd584bb0294b3cf0cdc954f9009d4a076c7739f9897d0ee2cf44bd06258d10d5924b72a35825d398c9b22947875e')
b2sums=('3a67161a72115b5b300c5e4b67bfbfd66cd43d2a772e54e9c6ae0aa5f8cdd55c4a44278448ef777b381761dfd33b4017957891f4097774549ba4608c8ec85b71')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --features cli --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
  just generate specifications $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  conflicts=(alpm-files)
  replaces=(alpm-files)

  local bins=(
    alpm-repo-desc
    alpm-repo-files
  )

  cd $_upstream
  for _bin in "${bins[@]}"; do
    install -vDm 755 target/release/$_bin -t "$pkgdir/usr/bin/"
    install -vDm 644 output/shell_completions/$_bin.bash "$pkgdir/usr/share/bash-completions/completions/$_bin"
    install -vDm 644 output/shell_completions/$_bin.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
    install -vDm 644 output/shell_completions/_$_bin -t "$pkgdir/usr/share/zsh/site-functions/"
  done

  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
