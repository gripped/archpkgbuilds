# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=(
  dark-reader
  firefox-dark-reader
  thunderbird-dark-reader
)
pkgbase=dark-reader
_name=${pkgbase/-}
pkgver=4.9.109
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
arch=(any)
url=https://darkreader.org/
license=(MIT)
makedepends=(
  git
  npm
)
source=("git+https://github.com/$_name/$_name.git#tag=v$pkgver")
b2sums=('8a6c810715a5d8252159a70e55519712bed93a0b7d90c1743fd0330924d67de342659bc429e190748a30adb33f18a76e1d22f1004c938babc5da85da0f812dc3')

prepare() {
  cd "$_name"
  npm ci
}

build() {
  cd "$_name"
  npm run build
}

check() {
  cd "$_name"
  npm test
}

package_dark-reader() {
  pkgdesc+=' (unpacked webextension)'

  cd "$_name"/build/release/chrome-mv3
  install -d "$pkgdir"/usr/lib/$pkgbase
  cp -r -- * "$pkgdir"/usr/lib/$pkgbase
}

package_firefox-dark-reader() {
  groups=('firefox-addons')

  cd "$_name"/build/release
  install -Dm644 "$_name"-firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/addon@"$_name".org.xpi
}

package_thunderbird-dark-reader() {
  groups=('thunderbird-addons')

  cd "$_name"/build/release
  install -Dm644 "$_name"-thunderbird.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@"$_name".org.xpi
}
