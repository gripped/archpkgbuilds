# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=(
  dark-reader
  firefox-dark-reader
  thunderbird-dark-reader
)
pkgbase=dark-reader
_name=${pkgbase/-}
pkgver=4.9.118
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
arch=(any)
url=https://darkreader.org/
license=(MIT)
makedepends=(
  git
  npm
)
source=("git+https://github.com/$_name/$_name.git#tag=v$pkgver")
b2sums=('f5352569500155f0583205d7782731b0d56c1c7cc12859499bff14305846ddf28143d9195e2f91f469f07a74efb8ed399c1d5a6a096dc61ea92fdef034f1fd67')

prepare() {
  cd "$_name"
  npm i
}

build() {
  cd "$_name"
  npm run build
}

check() {
  cd "$_name"
  npm test
}

package_dark-reader() {
  pkgdesc+=' (unpacked webextension)'

  cd "$_name"/build/release/chrome-mv3
  install -d "$pkgdir"/usr/lib/$pkgbase
  cp -r -- * "$pkgdir"/usr/lib/$pkgbase
}

package_firefox-dark-reader() {
  groups=('firefox-addons')

  cd "$_name"/build/release
  install -Dm644 "$_name"-firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/addon@"$_name".org.xpi
}

package_thunderbird-dark-reader() {
  groups=('thunderbird-addons')

  cd "$_name"/build/release
  install -Dm644 "$_name"-thunderbird.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@"$_name".org.xpi
}
