# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=(
  dark-reader
  firefox-dark-reader
  thunderbird-dark-reader
)
pkgbase=dark-reader
_name=${pkgbase/-}
pkgver=4.9.102
pkgrel=2
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
arch=(any)
url=https://darkreader.org/
license=(MIT)
makedepends=(
  git
  npm
)
source=("git+https://github.com/$_name/$_name.git#tag=v$pkgver")
b2sums=('4c677ad5de3b1550f69a8a7f0ee9599beef5212659d5f3f3fdef613e4fea7aa9377b8fac8e78aa22115a715c6fc73b40e53cd257815e3818a930f7df861f5187')

prepare() {
  cd "$_name"
  npm ci
}

build() {
  cd "$_name"
  npm run build
}

check() {
  cd "$_name"
  npm test
}

package_dark-reader() {
  pkgdesc+=' (unpacked webextension)'

  cd "$_name"/build/release/chrome-mv3
  install -d "$pkgdir"/usr/lib/$pkgbase
  cp -r -- * "$pkgdir"/usr/lib/$pkgbase
}

package_firefox-dark-reader() {
  groups=('firefox-addons')

  cd "$_name"/build/release
  install -Dm644 "$_name"-firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/addon@"$_name".org.xpi
}

package_thunderbird-dark-reader() {
  groups=('thunderbird-addons')

  cd "$_name"/build/release
  install -Dm644 "$_name"-thunderbird.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@"$_name".org.xpi
}
