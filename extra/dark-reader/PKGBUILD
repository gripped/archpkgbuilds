# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=('dark-reader' 'firefox-dark-reader' 'thunderbird-dark-reader')
pkgbase=dark-reader
_name=${pkgbase/-}
pkgver=4.9.83
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
arch=('any')
url=https://darkreader.org/
license=('MIT')
makedepends=('git' 'npm')
source=("git+https://github.com/$_name/$_name.git#tag=v$pkgver")
b2sums=('4b107cc38d08a8584e0c84606910f73f9d223c2c2b21817376d0192ba6805a7d5401d8d4224cf2b7c8ce6ed928aebb03777028291b658cb8dba938812a4dba4b')

prepare() {
  cd "$_name"
  npm ci
}

build() {
  cd "$_name"
  npm run build
}

check() {
  cd "$_name"
  npm test
}

package_dark-reader() {
  pkgdesc+=' (unpacked webextension)'
  cd "$_name"/build/release/chrome
  install -d "$pkgdir"/usr/lib/$pkgbase
  cp -r -- * "$pkgdir"/usr/lib/$pkgbase
}

package_firefox-dark-reader() {
  groups=('firefox-addons')
  cd "$_name"/build/release
  install -Dm644 "$_name"-firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/addon@"$_name".org.xpi
}

package_thunderbird-dark-reader() {
  groups=('thunderbird-addons')
  cd "$_name"/build/release
  install -Dm644 "$_name"-thunderbird.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@"$_name".org.xpi
}
