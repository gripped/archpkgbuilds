# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pystemmer
pkgver=3.0.0
pkgrel=2
pkgdesc="Snowball stemming algorithms, for information retrieval"
arch=('x86_64')
license=('BSD' 'MIT')
url="http://snowball.tartarus.org"
depends=('python' 'libstemmer')
makedepends=('cython' 'python-setuptools')
source=("$pkgname-$pkgver.tar.gz"::https://github.com/snowballstem/pystemmer/archive/refs/tags/v${pkgver}.tar.gz
	0001-Update-algorithm-list-for-snowball-3.0.0.patch)
sha512sums=('8bcb839df2c3964d410bddaa56599e5498152a81ff121b56490e5b4e42a4e52c74c9fc822a30283325e73f19219ac117ae3ac1a8e738b5a9c72ff0342b4991cf'
            '61e12f623f02c0d5831144d5cd2649ab417cef4c09d206841f1cfee82ee7815334398718791788b47e1d4045e4ce89e85b249687dd1ccce7aef4452c36b83aef')

export PYSTEMMER_SYSTEM_LIBSTEMMER=1

prepare() {
  cd pystemmer-$pkgver
  patch -Np1 -i ${srcdir}/0001-Update-algorithm-list-for-snowball-3.0.0.patch
}

build() {
  cd pystemmer-$pkgver
  python setup.py build
}

check() {
  cd pystemmer-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" python runtests.py
}

package() {
  cd pystemmer-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
