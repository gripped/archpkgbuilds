# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-session
pkgver=2.3.1
pkgrel=4
pkgdesc='Session indicator for Wingpanel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-session
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libgee
  libgranite.so
  libhandy-1.so
  libwingpanel.so
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_tag=ade43580559fb7dff8c2ae725677c700482d661c
source=(git+https://github.com/elementary/wingpanel-indicator-session.git#tag=${_tag})
b2sums=('aeb4caf0d8beebdc3509f828b2c747c575715aa2d459e7c68ceac44e286bd1e07270822d84ee59d35af026724d0fc0f391b5dd58527d60ededf3148a32a5899a')

pkgver() {
  cd wingpanel-indicator-session
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-session build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
