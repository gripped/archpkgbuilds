# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Quentin Stievenart <quentin.stievenart@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mk-configure
pkgver=0.40.0
pkgrel=1
pkgdesc="Lightweight replacement for GNU autotools"
arch=('any')
makedepends=('bmake' 'bmkdep')
url="https://github.com/cheusov/mk-configure"
license=('BSD')
depends=('bash' 'gawk' 'bmake')
source=("https://github.com/cheusov/$pkgname/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('7daaa79467389cd3bb7875ad24d0e56ba460b8f6165e138eb222620caf67f903')

build() {
  cd $pkgname-$pkgname-$pkgver
  export USE_AWK=/usr/bin/gawk
  export USE_INSTALL=/usr/bin/install
  export PREFIX=/usr SYSCONFDIR=/etc MANDIR=/usr/share/man LIBEXECDIR=/usr/lib
  bmake CUSTOM.prog_nbmkdep=/usr/bin/bmkdep all
}

package() {
  cd $pkgname-$pkgname-$pkgver
  export PREFIX=/usr SYSCONFDIR=/etc MANDIR=/usr/share/man LIBEXECDIR=/usr/lib
  bmake CUSTOM.prog_nbmkdep=/usr/bin/bmkdep DESTDIR="$pkgdir" install
  install -Dm644 doc/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
