# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: jtheoof <contact@jtheoof.me>

pkgname=swappy
pkgver=1.7.1
pkgrel=1
pkgdesc="A Wayland native snapshot editing tool"
url="https://github.com/jtheoof/swappy"
arch=('x86_64')
license=('MIT')
depends=('cairo' 'gtk3' 'pango')
optdepends=('wl-clipboard: copy to clipboard support'
            'otf-font-awesome: to draw the paint icons properly')
makedepends=('meson' 'scdoc' 'git')
source=("git+${url}.git#tag=v${pkgver}?signed")
b2sums=('d3a3303a0c1d878c05b1d2790703d3a9ac83e48ea4c56e605f1d7c4e41a3b94b904d2766f35cf4d3d9e24fda5f56bacb67e7ed719ef2e60e04a081be87a838c1')
validpgpkeys=('F44D05A50F6C9EB5C81BCF966A6B35DBE9442683')

build() {
	cd "${pkgname}"
	arch-meson build
	ninja -C build
}

package() {
	cd "${pkgname}"
	DESTDIR="${pkgdir}" ninja -C build install
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
