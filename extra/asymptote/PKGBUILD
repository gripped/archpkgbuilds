# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=asymptote
pkgver=3.05
pkgrel=2
pkgdesc='A vector graphics language (like metapost)'
arch=(x86_64)
url='http://asymptote.sourceforge.net/'
license=(GPL-3.0-only)
depends=(boost-libs
         curl
         fftw
         freeglut
         gc
         gcc-libs
         glibc
         gsl
         libsigsegv
         libtirpc
         ncurses
         readline
         texlive-basic
         zlib)
makedepends=(boost
             cmake
             doxygen
             ghostscript
             git
             glm
             glu
             imagemagick
             python-click
             python-pyqt5
             texlive-binextra
             texlive-fontutils
             texlive-latexextra
             texlive-plaingeneric)
optdepends=('python-pyqt5: for the xasy GUI'
            'python-cson: for the xasy GUI'
            'ghostscript: PDF output')
source=(git+https://github.com/vectorgraphics/asymptote#tag=$pkgver
        boost-1.88.patch
        boost-1.89.patch)
sha256sums=('501c2d4d0f8a90f2e4ec8f79b00e70974d2fe2a23496550cfa88d45a36579eb0'
            '07529adb880c42f474f2cd371e557f079717a5023883c4fbefd4ec824caa6dd1'
            'fd48e245671d3466d56f243d10fd317ab5aac3868c86eb54d196e59a7f00e883')

prepare() {
  cd $pkgname
  patch -d LspCpp -p1 < ../boost-1.88.patch
  patch -p1 < ../boost-1.89.patch
  ./autogen.sh
}

build() {
  cd $pkgname
  CXXFLAGS+=" -I$srcdir/$pkgname" \
  ./configure --enable-gc=/usr \
              --prefix=/usr \
              --with-latex=/usr/share/texmf/tex/latex \
              --with-context=/usr/share/texmf/tex/context
  make asy asy-keywords.el base/webgl/asygl.js
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install-asy
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf "$pkgdir"/usr/share/info/asymptote

  # copy some data files to their correct location
  mkdir -p "$pkgdir"/usr/share/emacs/site-lisp
  cp "$pkgdir"/usr/share/asymptote/*.el "$pkgdir"/usr/share/emacs/site-lisp
  mkdir -p "$pkgdir"/usr/share/vim/vimfiles/syntax
  cp "$pkgdir"/usr/share/asymptote/*.vim "$pkgdir"/usr/share/vim/vimfiles/syntax
  mkdir -p "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax
  cd "$pkgdir"/usr/share/asymptote/
  sh asy-kate.sh
  mv asymptote.xml "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax
}
