# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=rebels-in-the-sky
pkgver=1.5.1
pkgrel=1
pkgdesc="P2P terminal game about spacepirates playing basketball across the galaxy"
url="https://github.com/ricott1/rebels-in-the-sky"
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo' 'cmake')
arch=('x86_64')
license=('GPL-3.0')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c8791d5f09d8643bb52beccfd6741e9416f669cc6280de4a7ffb8d903ca93a787bb4c4212e66a67899bf71db6881895f90a5dcd12b5da0fd536c246e90e289b0')
options=('!lto')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --target "$(rustc --print host-tuple)" --locked
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --frozen
}

# check() {
#   cd "${pkgname}-${pkgver}"
#   cargo test --frozen -- --skip "test_game"
# }

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/rebels" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
