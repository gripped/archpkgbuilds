# Maintainer: Morgan Adamiec <morganamilo@archlinux.org>
# Contributor: icxes <dev.null@need.moe>

pkgname=copyparty
pkgver=1.20.10
pkgrel=1
pkgdesc='File server with accelerated resumable uploads, dedup, WebDAV, FTP, TFTP, zeroconf, media indexer, thumbnails++'
arch=(any)
url='https://github.com/9001/copyparty'
license=(MIT)
depends=(
  bash
  python
  lsof
  python-jinja
  python-importlib_resources
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
  pigz
)
optdepends=(
  'ffmpeg: thumbnails for videos, images (slower) and audio, music tags'
  'cfssl: generate TLS certificates on startup'
  'python-mutagen: music tags (alternative)'
  'python-pillow: thumbnails for images'
  'python-pyvips: thumbnails for images (higher quality, faster, uses more ram)'
  'libkeyfinder: detection of musical keys'
  'python-pyopenssl: ftps functionality'
  'python-pyzmq: send zeromq messages from event-hooks'
  'python-argon2-cffi: hashed passwords in config'
)
backup=(etc/copyparty/copyparty.conf)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/9001/copyparty/releases/download/v$pkgver/copyparty-$pkgver.tar.gz"
  remove-pkg_resources.patch
)
sha512sums=('c7eeff99722daf3098a04caea59458f08ee3e65a59f97cc8dff3ca7358b4258349551035574b5ef5cd7d2e6c059f248c466a2c6b31f179e8dcc26b37f0d8e21b'
            '25b9e6c79f68c1dbef047a4019a1b40a9e0dc71f4839cd9fb18b0ed4fc9b509aef92a575de58bbf748523b4f8522800c01608b59da09f6f5b19ec28f380f2b33')
b2sums=('46079103bc4efed5b3c2a94c4f2ed0ebe3744592fd563ab936c67cc19712022deb8b7fe7fb3214b9845babcf18fe6414ac0dabe8471c30a86702e7de94b1ee37'
        '9ebfab21c3a7a800bdaa653bebad81d5aa7126519fd2c9323ce09cea81a387f9cef241b38a7245fde19e3bdc1eccf0a41dbf9e316aa4f02a09f3eec13a0c89e4')

prepare() {
  cd "$pkgname-$pkgver"

  # pkg_resources is deprecated
  patch -p1 -i "$srcdir/remove-pkg_resources.patch"

  sed \
    -e 's/python3/python/g' \
    -i bin/prisonparty.sh \
    -i contrib/systemd/*.service
}
build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # python pkg
  python -m installer --destdir="$pkgdir" dist/*.whl

  # prisonparty
  install -vDm755 bin/prisonparty.sh "$pkgdir/usr/bin/prisonparty"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  cd contrib/systemd

  # global configuration
  install -vDm644 -t "$pkgdir/etc/copyparty" copyparty.conf

  # systemd integration
  install -vDm644 \
    -t "$pkgdir/usr/lib/systemd/system" \
    {copy,prison}party@.service
  install -vDm644 \
    copyparty-user.service \
    "$pkgdir/usr/lib/systemd/user/copyparty.service"
  install -vDm644 index.md "$pkgdir/var/lib/copyparty-jail/README.md"
}
