# Maintainer: Morgan Adamiec <morganamilo@archlinux.org>
# Contributor: icxes <dev.null@need.moe>

pkgname=copyparty
pkgver=1.20.7
pkgrel=1
pkgdesc='File server with accelerated resumable uploads, dedup, WebDAV, FTP, TFTP, zeroconf, media indexer, thumbnails++'
arch=(any)
url='https://github.com/9001/copyparty'
license=(MIT)
depends=(
  bash
  python
  lsof
  python-jinja
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
  pigz
)
optdepends=(
  'ffmpeg: thumbnails for videos, images (slower) and audio, music tags'
  'cfssl: generate TLS certificates on startup'
  'python-mutagen: music tags (alternative)'
  'python-pillow: thumbnails for images'
  'python-pyvips: thumbnails for images (higher quality, faster, uses more ram)'
  'libkeyfinder: detection of musical keys'
  'python-pyopenssl: ftps functionality'
  'python-pyzmq: send zeromq messages from event-hooks'
  'python-argon2-cffi: hashed passwords in config'
)
backup=(etc/copyparty/copyparty.conf)
source=("$pkgname-$pkgver.tar.gz::https://github.com/9001/copyparty/releases/download/v$pkgver/copyparty-$pkgver.tar.gz")
sha512sums=('a73f422634ecad394d6b6c48a2003973e6aedcf3d5f408bbc45e1a492a7274d74af75c6b2e74903f883249c26554f0af011733a2ea47a92969eb730f778388f7')
b2sums=('aebdc34b65a36aaf52b49794da45740f3655bc1f379f2f1d01132eac5958acc53330b8741f230cd46cd9f43d6235b09deb6a4b775f259b63a926f954d6108d6b')

prepare() {
  cd "$pkgname-$pkgver"

  sed \
    -e 's/python3/python/g' \
    -i bin/prisonparty.sh \
    -i contrib/systemd/*.service
}
build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # python pkg
  python -m installer --destdir="$pkgdir" dist/*.whl

  # prisonparty
  install -vDm755 bin/prisonparty.sh "$pkgdir/usr/bin/prisonparty"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  cd contrib/systemd

  # global configuration
  install -vDm644 -t "$pkgdir/etc/copyparty" copyparty.conf

  # systemd integration
  install -vDm644 \
    -t "$pkgdir/usr/lib/systemd/system" \
    {copy,prison}party@.service
  install -vDm644 \
    copyparty-user.service \
    "$pkgdir/usr/lib/systemd/user/copyparty.service"
  install -vDm644 index.md "$pkgdir/var/lib/copyparty-jail/README.md"
}
