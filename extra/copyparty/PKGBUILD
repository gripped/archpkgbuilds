# Maintainer: Morgan Adamiec <morganamilo@archlinux.org>
# Contributor: icxes <dev.null@need.moe>

pkgname=copyparty
pkgver=1.20.8
pkgrel=1
pkgdesc='File server with accelerated resumable uploads, dedup, WebDAV, FTP, TFTP, zeroconf, media indexer, thumbnails++'
arch=(any)
url='https://github.com/9001/copyparty'
license=(MIT)
depends=(
  bash
  python
  lsof
  python-jinja
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
  pigz
)
optdepends=(
  'ffmpeg: thumbnails for videos, images (slower) and audio, music tags'
  'cfssl: generate TLS certificates on startup'
  'python-mutagen: music tags (alternative)'
  'python-pillow: thumbnails for images'
  'python-pyvips: thumbnails for images (higher quality, faster, uses more ram)'
  'libkeyfinder: detection of musical keys'
  'python-pyopenssl: ftps functionality'
  'python-pyzmq: send zeromq messages from event-hooks'
  'python-argon2-cffi: hashed passwords in config'
)
backup=(etc/copyparty/copyparty.conf)
source=("$pkgname-$pkgver.tar.gz::https://github.com/9001/copyparty/releases/download/v$pkgver/copyparty-$pkgver.tar.gz")
sha512sums=('aea6a3591e8f53d44c647a542d275f16e2ac8cb5785c038c97e6415a885fb8c0b0ccc7c3759733909cc959c4238a364fa05ed395e132cbd1ab3351f727cd6f0d')
b2sums=('33bdb3857ec1a5540644494d736ffab4e0b2bd4924553c60d9cc0f8c6d11e04cc70055920d3c52fb778b3d0821f288d549db67e760dd28d25f143ee524f2c334')

prepare() {
  cd "$pkgname-$pkgver"

  sed \
    -e 's/python3/python/g' \
    -i bin/prisonparty.sh \
    -i contrib/systemd/*.service
}
build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # python pkg
  python -m installer --destdir="$pkgdir" dist/*.whl

  # prisonparty
  install -vDm755 bin/prisonparty.sh "$pkgdir/usr/bin/prisonparty"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  cd contrib/systemd

  # global configuration
  install -vDm644 -t "$pkgdir/etc/copyparty" copyparty.conf

  # systemd integration
  install -vDm644 \
    -t "$pkgdir/usr/lib/systemd/system" \
    {copy,prison}party@.service
  install -vDm644 \
    copyparty-user.service \
    "$pkgdir/usr/lib/systemd/user/copyparty.service"
  install -vDm644 index.md "$pkgdir/var/lib/copyparty-jail/README.md"
}
