# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testtools
pkgver=2.7.0
_commit=87ab35910abe305c8918e686823418ec7e8b8675
pkgrel=1
pkgdesc="Extensions to the Python standard library unit testing framework"
arch=('any')
license=('MIT')
url="https://github.com/testing-cabal/testtools"
depends=('python-setuptools')
makedepends=('git' 'python-setuptools-scm')
checkdepends=('python-testscenarios' 'python-testresources' 'python-twisted')
source=("git+https://github.com/testing-cabal/testtools.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd testtools
  python setup.py build
}

check() {
  cd testtools
  python -m testtools.run testtools.tests.test_suite
}

package() {
  cd testtools
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
