# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=talosctl
pkgver=1.5.5
pkgrel=2
pkgdesc='CLI for Talos - A modern OS for Kubernetes'
arch=('x86_64')
url='https://github.com/siderolabs/talos'
license=('MPL2')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/siderolabs/talos.git#tag=v${pkgver}?signed")
b2sums=('SKIP')
validpgpkeys=('DB997306E3102F11C4E8F5527B26396447AB6DFD'  # "Andrey Smirnov <andrey.smirnov@talos-systems.com>"
              'CC51116A94490FA6FB3C18EB2401FCAE863A06CA'  # frezbo (Noel Georgi) <git@frezbo.dev>
              '15D5721F5F5BAF121495363EFE042E3D4085A811') # Andrey Smirnov <andrey.smirnov@siderolabs.com>

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  export _PKGS=$(grep '^PKGS ?=.*' Makefile | cut -d' ' -f3)
  export _EXTRA=$(grep '^EXTRAS ?=.*' Makefile | cut -d' ' -f3)

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags ${LDFLAGS} \
    -X github.com/talos-systems/talos/pkg/version.Name=Talos \
    -X github.com/talos-systems/talos/pkg/version.SHA=$(git rev-parse HEAD) \
    -X github.com/talos-systems/talos/pkg/version.Tag=v${pkgver} \
    -X github.com/talos-systems/talos/pkg/version.PkgsVersion=${_PKGS:?_PKGS unset} \
    -X github.com/talos-systems/talos/pkg/version.ExtrasVersion=${_EXTRA:?_EXTRA unset} \
    -X github.com/talos-systems/talos/pkg/images.Username=talos-systems \
    -X github.com/talos-systems/talos/pkg/images.Registry=ghcr.io \
    -X ggithub.com/talos-systems/talos/cmd/talosctl/pkg/mgmt/helpers.ArtifactsPath=_out" \
    -o build/talosctl \
    ./cmd/talosctl

  # generate shell completions
  for shell in bash fish zsh; do
    ./build/talosctl completion "$shell" > "build/$shell.completion"
  done
}

package() {
  cd "$pkgname/build"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" talosctl

  # shell completions
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/talosctl"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/completions/talosctl.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_talosctl"
}
