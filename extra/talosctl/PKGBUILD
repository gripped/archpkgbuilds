# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=talosctl
pkgver=1.7.3
pkgrel=1
pkgdesc='CLI for Talos - A modern OS for Kubernetes'
arch=('x86_64')
url='https://github.com/siderolabs/talos'
license=('MPL-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/siderolabs/talos.git#tag=v${pkgver}?signed")
sha512sums=('09758de9a97f8f983c6c305556db5212e6afcebeb44f8605f0b0e80ba3c1e7fcb04face73b5f73b66356614f9699c333ba8ccc3bdb30d9a75fd49b8e8de044a8')
b2sums=('ef2f865ac2af82f031d73954f7f54d00f2c9190466e84cfc90070bbeaa5ec407e2e902d43ea6f5d79b9f367d5252a7e9ac022c9ddcf6ac46dc7a87075c262074')
validpgpkeys=('DB997306E3102F11C4E8F5527B26396447AB6DFD'  # "Andrey Smirnov <andrey.smirnov@talos-systems.com>"
              'CC51116A94490FA6FB3C18EB2401FCAE863A06CA'  # frezbo (Noel Georgi) <git@frezbo.dev>
              '15D5721F5F5BAF121495363EFE042E3D4085A811') # Andrey Smirnov <andrey.smirnov@siderolabs.com>

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # https://github.com/siderolabs/talos/blob/d8a435f0e4e093570325b737f89ff3b1205de48e/Dockerfile#L252
  # upstream switched from using LDFLAGS to go:embed some time ago,
  # this is how sha/pkgver/etc are added to the binary
  git rev-parse HEAD > pkg/machinery/gendata/data/sha
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build/talosctl \
    ./cmd/talosctl

  # generate shell completions
  for shell in bash fish zsh; do
    ./build/talosctl completion "$shell" > "build/$shell.completion"
  done
}

package() {
  cd "$pkgname/build"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" talosctl

  # shell completions
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/talosctl"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/completions/talosctl.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_talosctl"
}
