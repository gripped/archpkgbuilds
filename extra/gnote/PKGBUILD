# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=48beta
pkgrel=1
pkgdesc="A note taking application"
url="https://wiki.gnome.org/Apps/Gnote"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairomm-1.16
  dconf
  gcc-libs
  glib2
  glibc
  glibmm-2.68
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libsecret
  libsigc++-3.0
  libxml2
  libxslt
  pango
  pangomm-2.48
  util-linux-libs
)
makedepends=(
  git
  meson
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#tag=${pkgver/[a-z]/.&}")
b2sums=('e4ae24a413a252f8a3f755c6461c5baa25cb5d2baf06d07041b598b99990c1c39f5bdeba007baadb9c1a7412dc7e69ca2f9b1190d6e8b46e02e5878f208f4ce9')

prepare() {
  cd gnote
}

build() {
  arch-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
