# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=49.3
pkgrel=1
pkgdesc="A note taking application"
url="https://gitlab.gnome.org/GNOME/gnote/-/wikis/Gnote"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairomm-1.16
  dconf
  glib2
  glibc
  glibmm-2.68
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libgcc
  libsecret
  libsigc++-3.0
  libstdc++
  libxml2
  libxslt
  pango
  pangomm-2.48
  util-linux-libs
)
makedepends=(
  git
  meson
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#tag=${pkgver/[a-z]/.&}")
b2sums=('79f117841901d76795de406277c8c982404f39db03d68367e4e6471294eb872b24cbde76720abd2e71d32a6cfc0811d3ea7ff86f6dd21d2f5a49fb627884ce6e')

build() {
  arch-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
