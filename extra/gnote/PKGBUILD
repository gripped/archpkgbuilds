# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=49rc
pkgrel=1
pkgdesc="A note taking application"
url="https://gitlab.gnome.org/GNOME/gnote/-/blob/49.rc/wiki/main.md"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairomm-1.16
  dconf
  gcc-libs
  glib2
  glibc
  glibmm-2.68
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libsecret
  libsigc++-3.0
  libxml2
  libxslt
  pango
  pangomm-2.48
  util-linux-libs
)
makedepends=(
  git
  meson
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#tag=${pkgver/[a-z]/.&}")
b2sums=('21d344c2e197f14b598041903bf0957cb08c1880cf51e6fac8e43f0eaba4bc4aadfe40762ece360e754c4bd12a2a5b817b41d7baa369421eb9e428a4a4f8c7d2')

build() {
  arch-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
