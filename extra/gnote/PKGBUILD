# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=48.1
pkgrel=1
pkgdesc="A note taking application"
url="https://wiki.gnome.org/Apps/Gnote"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairomm-1.16
  dconf
  gcc-libs
  glib2
  glibc
  glibmm-2.68
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libsecret
  libsigc++-3.0
  libxml2
  libxslt
  pango
  pangomm-2.48
  util-linux-libs
)
makedepends=(
  git
  meson
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#tag=${pkgver/[a-z]/.&}")
b2sums=('80c08f51fa8cd92fc9f3013f7ae920c3805dfede0b302dc29a7f8f04c7d8e098eed77075c5ca962613c26cc34fccccf16a0119eef352237dcc77ff56a5d5192e')

prepare() {
  cd gnote

  # Add "new-window" command-line option
  # https://gitlab.gnome.org/GNOME/gnote/-/merge_requests/37
  git cherry-pick -n 37f1c993236ba07128b1a34a6e419f6e6d5ef551
  git cherry-pick -n a5c7ce44cc56ea5a12153a0cd51dfddf22472413
}

build() {
  arch-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
