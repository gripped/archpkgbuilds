# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thayne <astrothayne at gmail dot com>
# Contributor: Yiqiao Wang <yiqiao.wang96 at gmail dot com>
# Contributor: wenLiangcan <boxeed at gmail dot com>

pkgname=ammonite
pkgver=3.0.8
_scalaversion=2.13.11
pkgrel=1
pkgdesc="A cleanroom re-implementation of the Scala REPL from first principles. "
arch=('any')
url="https://github.com/lihaoyi/Ammonite"
license=('MIT')
depends=('bash' 'java-environment')
makedepends=('git')
source=("git+https://github.com/lihaoyi/Ammonite.git#tag=$pkgver")
b2sums=('59bc9a60c3313762d4ce983820c372b6733817f164a6c1b33caaa263513caa5e9826a3391933fbccb914e59fac8316341177cd881c3a83ea490dc2a26d436e0a')

build() {
  cd Ammonite
  ./mill -i amm[$_scalaversion].assembly
}

package() {
  cd Ammonite
  install -dm755 "$pkgdir"/usr/bin
  (echo "#!/bin/sh"; cat out/amm/$_scalaversion/assembly.dest/out.jar) > "$pkgdir"/usr/bin/amm
  chmod +x "$pkgdir"/usr/bin/amm
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
