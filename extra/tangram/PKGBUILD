# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=tangram
pkgver=3.4
pkgrel=1
pkgdesc='Browser for your pinned tabs'
arch=(any)
url='https://apps.gnome.org/Tangram/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  dconf
  gdk-pixbuf2
  gjs
  glib2
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/sonnyp/Tangram.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
)
b2sums=(
  7f9dfc7b10cad67b0af4280e72b796a7e5293213a4486d92aa473fd92f0386f05d8245ac8a6f066c009a518d410e377cf23f3dfb9f55630cb57ac72e3353fa9e
  SKIP
)

prepare() {
  cd Tangram

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson Tangram build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s re.sonny.Tangram "$pkgdir/usr/bin/tangram"
}
