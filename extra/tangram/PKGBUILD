# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=tangram
pkgver=3.3
pkgrel=1
pkgdesc='Browser for your pinned tabs'
arch=(any)
url='https://apps.gnome.org/Tangram/'
license=(GPL-3.0-only)
depends=(
  dconf
  gdk-pixbuf2
  gjs
  glib2
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/sonnyp/Tangram.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
  tangram-live-thumbnail.patch
)
b2sums=('b991837ce3aee766d56e4fa0a2d3eac58effb87184749118c5fa16d5bfb034f91519f8262bac4071893c10f0fdd1ae3988f0a1e9a0d7c53d61b1f70125babeaa'
        'SKIP'
        '8caf4d3b9d9ba6ae3ef08a0234014c0601c1049bffed3257c342c887856d8bcb7f71ba93c9c1dd33e8006ab78bbc5de8a5534a69bc28314c890acf2197490ea4')

prepare() {
  cd Tangram

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update

  # https://github.com/sonnyp/Tangram/pull/311
  git apply -3 ../tangram-live-thumbnail.patch
}

build() {
  arch-meson Tangram build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s re.sonny.Tangram "$pkgdir/usr/bin/tangram"
}
