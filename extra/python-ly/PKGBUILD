# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

pkgname=python-ly
pkgver=0.9.9
pkgrel=2
pkgdesc='Parse, manipulate or create documents in LilyPond format'
arch=(any)
url="https://github.com/frescobaldi/$pkgname"
license=(GPL-2.0-or-later)
depends=(python)
makedepends=(python-{build,installer,wheel}
             python-hatchling
             python-sphinx)
checkdepends=(python-lxml
              python-pytest)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
# PyPi releases are behind, see https://github.com/frescobaldi/python-ly/issues/163
# source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$_archive.tar.gz")
sha256sums=('b64983ab44ec7e580451b8fb929da5d981c3d4a633612239e86ce515c1dd7899')

build() {
	cd "$_archive"
	python -m build -wn
	pushd doc
	make man text
}

check() {
	cd "$_archive"
	# Upstream tests are borked, see https://github.com/frescobaldi/python-ly/issues/153
	# PYTHONPATH=. pytest
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" CHANGELOG.md README.rst
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" doc/build/text/*
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" doc/build/man/*
}
