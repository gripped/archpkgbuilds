# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

pkgname=python-ly
pkgver=0.9.7
pkgrel=6
pkgdesc='Parse, manipulate or create documents in LilyPond format'
arch=(any)
url="https://github.com/frescobaldi/$pkgname"
license=(GPL-2.0-or-later)
depends=(python)
makedepends=(python-{build,installer,wheel}
             python-setuptools
             python-sphinx)
_archive="$pkgname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$_archive.tar.gz")
sha256sums=('d4d2b68eb0ef8073200154247cc9bd91ed7fb2671ac966ef3d2853281c15d7a8')

build() {
	cd "$_archive"
	python -m build -wn
	pushd doc
	make man text
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" ChangeLog README.rst
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" doc/build/text/*
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" doc/build/man/*
}
