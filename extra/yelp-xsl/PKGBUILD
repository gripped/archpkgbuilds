# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=yelp-xsl
pkgver=42.3
pkgrel=1
pkgdesc="Document transformations from Yelp"
url="https://gitlab.gnome.org/GNOME/yelp-xsl"
arch=(any)
license=(
  BSD-3-Clause
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
makedepends=(
  git
  itstool
  libxslt
  meson
)
source=(
  "git+$url.git#tag=$pkgver"
  0001-build-Add-missing-css-subdir.patch
)
b2sums=('73c61f90a1c99a00e0f03a82f5c38f7a1ced4594af49a0ccbf39175bb860fae63339f9b6cbc04eabaefd173b97b815f00f0ef6a438d5bee4f0c3bba14947ec64'
        '43555dd6758f34c2268192ab7f6d06114e7c29210389e113bd3e57eda99e82803b8ccf4bc7c0aa09af0423204bb860ab1c1f90feadded18e1e92901695d475a4')

prepare() {
  cd yelp-xsl
  git apply -3 ../0001-build-Add-missing-css-subdir.patch
}

build() {
  local meson_options=(
    -D dita=true
  )

  arch-meson yelp-xsl build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 yelp-xsl/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
