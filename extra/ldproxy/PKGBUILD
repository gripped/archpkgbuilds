# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=ldproxy
pkgver=0.3.4
pkgrel=1
pkgdesc="A linker proxy tool"
url='https://github.com/esp-rs/embuild'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/esp-rs/embuild/archive/${pkgname}-v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('858ea83abf9bb009c970555650d388ef5a0e75496ae0d01d6be46da69fa61e86'
            '4abcd9c29d6303c9e3ee59990aee522c45bc2237e5fc079c2bade789eacc482c')
b2sums=('33f99229617943884d4ac05e44a36f5b2a3ee059c5f84e52c0101ae7cc7fd55b923057b536d066383017c90fee68a55da2c1deba632a57c1d169f2c6f1e30400'
        'f27bb6464a6b818c0732467ebfaf47f2d60817cc3766f142a376b020b7b5110c61ba62edad7e22d5e3c354454aca490d7ea46bb0d4fd6024627b5e4b6ef47097')

prepare() {
  cd "embuild-${pkgname}-v${pkgver}/${pkgname}"
  cp ../../Cargo.lock ..
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "embuild-${pkgname}-v${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "embuild-${pkgname}-v${pkgver}/${pkgname}"
  cargo build --frozen --release
}

check() {
  cd "embuild-${pkgname}-v${pkgver}/${pkgname}"
  cargo test --frozen
}

package() {
  cd "embuild-${pkgname}-v${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}

  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
