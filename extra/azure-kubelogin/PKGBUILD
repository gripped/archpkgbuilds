# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: William Aass Dahlen <cznk@protonmail.com>

pkgname=azure-kubelogin
pkgver=0.2.14
pkgrel=1
pkgdesc="A Kubernetes credential (exec) plugin implementing azure authentication"
arch=(x86_64)
url="https://github.com/Azure/kubelogin"
license=(MIT)
depends=(glibc)
makedepends=(
  git
  go
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
b2sums=('5d0d15455f6d8cf7f1b028a5d15249c1c17c09996cc38e16da412f7490e1426c8c097db4a05d8e81531aa27e3879284cdc1f8965e19b47e295d6924630a63862')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external  \
    -X main.gitTag=v$pkgver \
  "
  go build -v -ldflags "$ld_flags" .
}

check() {
  cd $pkgname
  go test -v ./...
}

package() {
  cd $pkgname
  # Completions
  ./kubelogin completion bash \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/kubelogin"
  ./kubelogin completion fish \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/kubelogin.fish"
  ./kubelogin completion zsh \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_kubelogin"

  install -vDm755 -t "$pkgdir/usr/bin" kubelogin
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
