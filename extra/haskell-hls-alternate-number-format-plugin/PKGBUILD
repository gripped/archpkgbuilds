# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-alternate-number-format-plugin
pkgname=haskell-hls-alternate-number-format-plugin
pkgver=2.2.0.0
pkgrel=5
pkgdesc="Provide Alternate Number Formats plugin for Haskell Language Server"
url="https://github.com/haskell/haskell-language-server"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-extra' 'haskell-ghcide' 'haskell-hie-compat'
         'haskell-hls-graph' 'haskell-hls-plugin-api' 'haskell-lens' 'haskell-lsp'
         'haskell-regex-tdfa' 'haskell-syb' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-hls-test-utils' 'haskell-tasty-quickcheck')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('6b06a290b171abaaf84421aad121e005b0100a13a47af0d4021fdc4a9c3d5f16')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --datasubdir=haskell-hls-alternate-number-format-plugin --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
