# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=palette
pkgver=3.0.0
pkgrel=1
pkgdesc='Tool for viewing the GNOME color palette as defined by the design guidelines'
arch=(x86_64)
url='https://gitlab.gnome.org/World/design/palette'
license=(GPL-3.0-only)
depends=(
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  vala
)
source=(
  "git+https://gitlab.gnome.org/World/design/palette.git#tag=$pkgver"
  git+https://gitlab.gnome.org/Teams/Design/HIG-app-icons.git
)
b2sums=(
  fb3369865ccd80b896d7ab9a48e905c74fa6f2f96b73c6edb8f2a5e3c5b15ef7a497e320011554f6acea3856a594632530c43fc0af24f24680e852f7a727109b
  SKIP
)

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url src/hig "$srcdir/HIG-app-icons"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s org.gnome.design.Palette "$pkgdir/usr/bin/palette"
}
