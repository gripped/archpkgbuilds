# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.18.0
pkgrel=2
pkgdesc='Consumer grade GIS software'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack/wiki'
license=('GPL3')
depends=('alglib' 'gdal' 'proj' 'qt6-webengine' 'qt6-tools' 'qt6-declarative' 'qt6-5compat' 'quazip-qt6' 'routino>=3.1')
makedepends=('cmake' 'clang')
source=($pkgname-$pkgver.tar.gz::https://github.com/Maproom/qmapshack/archive/refs/tags/V_$pkgver.tar.gz)
sha256sums=('e094123f7a0e68c466cbf39473402a6a06343c7fa1e54450eb8fdcc9c95625ca')

build() {
  cmake -B build -S $pkgname-V_$pkgver \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
