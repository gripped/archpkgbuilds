# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.19.0
pkgrel=2
pkgdesc='Consumer grade GIS software'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack'
license=('GPL-3.0-only')
depends=(
  'alglib'
  'gdal'
  'glibc'
  'hicolor-icon-theme'
  'libgcc'
  'libjpeg-turbo'
  'libstdc++'
  'proj'
  'qt6-5compat'
  'qt6-base'
  'qt6-declarative'
  'qt6-tools'
  'qt6-webengine'
  'quazip-qt6'
  'routino'
)
makedepends=(
  'clang'
  'cmake'
)
source=("$url/archive/V_$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('d6a18cf17f65e2992c071d65e69433edc6838014cc0669c0a9c169abccb52b93759c8c74f76d292123fdf22e7f27d610fe4e1b64fd0fe47ea168bbd851bae2ec')

build() {
  cmake -B build -S $pkgname-V_$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
