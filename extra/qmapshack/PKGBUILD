# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.19.0
pkgrel=1
pkgdesc='Consumer grade GIS software'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack'
license=('GPL-3.0-only')
depends=(
  'alglib'
  'gcc-libs'
  'gdal'
  'glibc'
  'hicolor-icon-theme'
  'libjpeg-turbo'
  'proj'
  'qt6-5compat'
  'qt6-base'
  'qt6-declarative'
  'qt6-tools'
  'qt6-webengine'
  'quazip-qt6'
  'routino'
)
makedepends=(
  'clang'
  'cmake'
)
source=("$url/archive/V_$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2686bad86dafb582e1dfb20a40972bd7986906de47f8620f0646cb4ca672523c7fd27658810b73245c3c41a613da9f4f7fe41a5abb42554f3838f04b6b54ad74')

build() {
  cmake -B build -S $pkgname-V_$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
