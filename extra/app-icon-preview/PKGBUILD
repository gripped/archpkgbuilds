# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=app-icon-preview
pkgver=3.5.0
pkgrel=1
pkgdesc='Tool for designing applications icons'
arch=(x86_64)
url='https://gitlab.gnome.org/World/design/app-icon-preview'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libxml2
  pango
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/design/app-icon-preview.git#tag=$pkgver")
b2sums=(261d4180d2bed244088da98c8a9cc30a4b6f39c9ee450e6f49a210181e025a2c5206e27bb577df5247778f617ba5a94a6dbd918f71a35dcbdf93ba08f945c6a9)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
