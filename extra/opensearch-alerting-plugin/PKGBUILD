# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-alerting-plugin
pkgver=3.3.0.0
pkgrel=1
_opensearchver=3.3.0
_jdkver=21
pkgdesc="OpenSearch Alerting Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/observing-your-data/alerting/index/"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}" "opensearch-notifications-plugin")
makedepends=("java-environment-openjdk=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/alerting/archive/${pkgver}.tar.gz"
  "add-licence-header.patch"
)
sha256sums=('0e48017040ece05de2d9a93740a0174b48e2fafb377084df9a8ee331dad8a07e'
            '446d9a0e034142042cb013e192361ed3be33c088531faeb29d44623efcc2c039')

prepare(){
  cd "alerting-${pkgver}"
  patch -p1 -i "$srcdir/add-licence-header.patch"
}

build() {
  cd "alerting-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # alertingBwcCluster and integTest (Reaper) requires JDK 14
  ./gradlew assemble
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-alerting"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-alerting"
  unzip "${srcdir}/alerting-${pkgver}/alerting/build/distributions/opensearch-alerting-${pkgver}.zip"

  install -Dm644 "${srcdir}/alerting-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
