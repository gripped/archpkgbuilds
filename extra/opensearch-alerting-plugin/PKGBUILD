# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-alerting-plugin
pkgver=2.19.2.0
pkgrel=1
_opensearchver=2.19.2
_jdkver=11
pkgdesc="OpenSearch Alerting Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/observing-your-data/alerting/index/"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}" "opensearch-notifications-plugin")
makedepends=("java-environment-openjdk=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/alerting/archive/${pkgver}.tar.gz"
  "remove-empty-file-without-headers.patch"
)
sha256sums=('efa6f8d8fb12cf8e14a4fe376422facfb29b6d44d2d22e3e02a9533cf7c3870c'
            '5bebae69c5b4074b6bf985546dec6fc418e1e7936051ac848d1e5bf47256de0e')

prepare() {
  cd "alerting-${pkgver}"
  patch -Np1 < "$srcdir/remove-empty-file-without-headers.patch"
}

build() {
  cd "alerting-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # alertingBwcCluster and integTest (Reaper) requires JDK 14
  ./gradlew build \
    --exclude-task ":alerting:forbiddenApisTest" \
    --exclude-task ":alerting:alertingBwcCluster#fullRestartClusterTask" \
    --exclude-task ":alerting:alertingBwcCluster#mixedClusterTask" \
    --exclude-task ":alerting:alertingBwcCluster#oldVersionClusterTask0" \
    --exclude-task ":alerting:alertingBwcCluster#oldVersionClusterTask1" \
    --exclude-task ":alerting:alertingBwcCluster#rollingUpgradeClusterTask" \
    --exclude-task ":alerting:alertingBwcCluster#twoThirdsUpgradedClusterTask" \
    --exclude-task ":alerting:integTest" \
    --exclude-task ":alerting:jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-alerting"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-alerting"
  unzip "${srcdir}/alerting-${pkgver}/alerting/build/distributions/opensearch-alerting-${pkgver}.zip"

  install -Dm644 "${srcdir}/alerting-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
