# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=paperwork
pkgver=2.2.5
pkgrel=4
pkgdesc='Personal document manager to manage scanned documents and PDFs'
arch=(any)
url='https://openpaper.work/'
license=(GPL-3.0-or-later)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  hicolor-icon-theme
  libhandy
  libinsane
  libnotify
  pango
  poppler-glib
  python
  python-cairo
  python-distro
  python-fabulous
  python-gobject
  python-numpy
  python-pillow
  python-pillowfight
  python-psutil
  python-pycountry
  python-pydbus
  python-pyocr
  python-pyxdg
  python-rich
  python-scikit-learn
  python-scipy
  python-setuptools
  python-whoosh
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
source=(
  "git+https://gitlab.gnome.org/World/OpenPaperwork/paperwork.git#tag=$pkgver"
  paperwork-l10n-compile.patch
  paperwork-check-path.patch
)
b2sums=(
  f2a01e06382aee7d9dd4f1b2d53e4b5ad0ec5e29d074078c84a7fdbcccf8c33d8b4e51caea2cfea8b222fd93bb709b77ed495307c51c182c59bea73f94974e90
  2489e31ea97f971dd89218f0b2496d3ea1ea9ac6114532e30022cbdcc194fad2091047731538352f53f4a81680e561e91b74953381ad2e0904434dcb071e9c5d
  91256d006e850cb4405d3a6934177242f708c491200bcec69fa91387deb581a4913ed84e1bdb5545a9ccba915a185ec6a8c25bbfe068bfc6e144e2f0bffd54ad
)

prepare() {
  cd $pkgname
  curl -s "https://download.openpaper.work/data/paperwork/master_$(git rev-parse --short HEAD)/data.tar.gz" | tar -xvz

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/-/merge_requests/862
  git apply -3 ../paperwork-l10n-compile.patch

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/-/merge_requests/867
  git apply -3 ../paperwork-check-path.patch
}

build() {
  cd $pkgname
  make l10n_compile

  for dir in openpaperwork-{core,gtk} paperwork-{backend,shell,gtk}; do
    pushd "$dir"
    python -m build --wheel --no-isolation
    popd
  done
}

package() {
  cd $pkgname
  for dir in openpaperwork-{core,gtk} paperwork-{backend,shell,gtk}; do
    pushd "$dir"
    python -m installer --destdir="$pkgdir" dist/*.whl
    popd
  done

  PYTHONPATH="$pkgdir"$(python -c 'import site; print(site.getsitepackages()[0])') \
    "$pkgdir"/usr/bin/paperwork-gtk install \
      --icon_base_dir="$pkgdir/usr/share/icons" \
      --data_base_dir="$pkgdir/usr/share"
}
