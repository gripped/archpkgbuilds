# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-security-privacy
pkgver=8.0.2
pkgrel=1
pkgdesc='Switchboard Security & Privacy Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-security-privacy
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  bash
  dconf
  glib2
  glibc
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
  light-locker
  polkit
  ufw
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=3c697f501177f129e2c30744b8287e30e8524166
source=(git+https://github.com/elementary/switchboard-plug-security-privacy.git#tag=${_tag})
b2sums=('89a45b8997a3301ac33e1aef7097662a0aa17f2547d61287f306160e094e57fc7e85e6d1f2934926471427f84635ec2d198373ec3e5d05bb54eaad8b4b2b3737')

pkgver() {
  cd switchboard-plug-security-privacy
  git describe --tags
}

build() {
  arch-meson switchboard-plug-security-privacy build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
