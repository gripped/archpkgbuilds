# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-security-privacy
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Security & Privacy Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-security-privacy
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  bash
  dconf
  glib2
  glibc
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
  light-locker
  polkit
  ufw
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=8be67e413efad35552f949f6873e3865b4a55d06
source=(git+https://github.com/elementary/switchboard-plug-security-privacy.git#tag=${_tag})
b2sums=('640950804f40164e8674aa541cc571aaca1e959e38f7d04c501b46b062f1380f6e427f20a4713282af8699d20fe66926eafc427ff8501759fc0acaacdc749b75')

pkgver() {
  cd switchboard-plug-security-privacy
  git describe --tags
}

build() {
  arch-meson switchboard-plug-security-privacy build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
