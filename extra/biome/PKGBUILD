# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=biome
pkgver=2.3.12
pkgrel=1
pkgdesc='Formatter, linter, and more for Javascript, Typescript, JSON, and CSS'
arch=(x86_64)
url=https://biomejs.dev
_url="https://github.com/biomejs/$pkgname"
license=(MIT)
depends=(gcc-libs
         glibc)
makedepends=(cargo)
replaces=(rome
          "$pkgname-bin")
_archive="$pkgname--biomejs-biome-$pkgver"
source=("$_url/archive/@biomejs/biome@$pkgver/$_archive.tar.gz")
sha256sums=('f86ee07054d71681b081994a42ca094592bd8afacf3e72c32a31f53e77dc690f')

prepare() {
	cd "$_archive/crates/${pkgname}_cli"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
}

_srcenv() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
}

build() {
	_srcenv
	export BIOME_VERSION="$pkgver"
	cargo build -p "${pkgname}_cli"  --frozen --release --all-features
}

check() {
	_srcenv
	export BIOME_VERSION="0.0.0" # upstream test expectations changed in 2.3.8
	local skipped=(
		cases::diagnostics::max_diagnostics_verbose
		commands::explain::explain_help
		commands::lint::lint_help
		commands::lsp_proxy::lsp_proxy_help
	)
	cargo test -p "${pkgname}_cli" --frozen --all-features -- \
		${skipped[@]/#/--skip }
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-*
}
