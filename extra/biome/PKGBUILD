# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=biome
pkgver=2.3.4
pkgrel=1
pkgdesc='Formatter, linter, and more for Javascript, Typescript, JSON, and CSS'
arch=(x86_64)
url=https://biomejs.dev
_url="https://github.com/biomejs/$pkgname"
license=(MIT)
depends=(gcc-libs
         glibc)
makedepends=(cargo)
replaces=(rome
          "$pkgname-bin")
_archive="$pkgname--biomejs-biome-$pkgver"
source=("$_url/archive/@biomejs/biome@$pkgver/$_archive.tar.gz")
sha256sums=('33380b1dc507b3b4366a26b8c75f9a4d51a5c761975698e34c2378103e981be2')

prepare() {
	cd "$_archive/crates/${pkgname}_cli"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_srcenv() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
}

build() {
	_srcenv
	BIOME_VERSION=$pkgver cargo build -p "${pkgname}_cli"  --frozen --release --all-features
}

check() {
	_srcenv
	local skipped=(
		cases::diagnostics::max_diagnostics_verbose
		commands::explain::explain_help
		commands::lint::lint_help
		commands::lsp_proxy::lsp_proxy_help
	)
	cargo test -p "${pkgname}_cli" --frozen --all-features -- \
		${skipped[@]/#/--skip }
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-*
}
