# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-datetime
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Date & Time Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-datetime
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=e6b327586e73def9b6d8d651600aa5bbf5da32de
source=(git+https://github.com/elementary/switchboard-plug-datetime.git#tag=${_tag})
b2sums=('eadccb6125b0c1b4f5798afe8168679c92103f70eda6cb6313a5e1c87654c18e79c1f8c655c99d97988283286481d09f4f243d78221c102e1303b3c2b83cd653')

pkgver() {
  cd switchboard-plug-datetime
  git describe --tags
}

build() {
  arch-meson switchboard-plug-datetime build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
