# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-datetime
pkgver=8.1.0
pkgrel=1
pkgdesc='Switchboard Date & Time Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-datetime
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libical
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=763dc18bba6008722b6ee4106d7c98bf17ea458d
source=(git+https://github.com/elementary/switchboard-plug-datetime.git#tag=${_tag})
b2sums=('acaa7376adb905830ad00b32c4e9433f7b80e95f3773d1890646409dd67d8bd119b668195b2d3616d2abdfab82f6bf83f93546fd198b3536f3dbb69da05f3ca2')

pkgver() {
  cd switchboard-plug-datetime
  git describe --tags
}

build() {
  arch-meson switchboard-plug-datetime build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
