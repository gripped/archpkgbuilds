# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Hao Long <aur@esd.cc>
# Contributor: Aaron Blair <aaron@aaronpb.me>

pkgname=gobuster
pkgver=3.6.0
pkgrel=2
pkgdesc="Directory/File, DNS and VHost busting tool written in Go"
arch=('x86_64')
url="https://github.com/OJ/gobuster"
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'go'
)
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/OJ/gobuster/archive/v${pkgver}.tar.gz")
sha256sums=('509b16ca713de02f8fcdc7b33d97e7e18b687bb99634dde076be38297d4b401b')
b2sums=('404f506898272cc3c20f849ddfcd5b1f8be2e7ca6f8d1a8beb067dbe7aee2a85b59a84fd34699482715cc83639d589703bb9588301a970b5edaf87bbed1627b4')

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false -linkmode=external" \
    .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
  ./gobuster --help
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    gobuster
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
