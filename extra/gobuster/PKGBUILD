# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Hao Long <aur@esd.cc>
# Contributor: Aaron Blair <aaron@aaronpb.me>

pkgname=gobuster
pkgver=3.7.0
pkgrel=2
pkgdesc="Directory/File, DNS and VHost busting tool written in Go"
arch=('x86_64')
url="https://github.com/OJ/gobuster"
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'go'
)
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/OJ/gobuster/archive/v${pkgver}.tar.gz")
sha256sums=('893f1979b453d655880c19552e1f894110a661a4057a58e95a4d7505bf6d7fa8')
b2sums=('47601f1a31af29cfe206936144a19c88352fa914b906afa85b16288ef05fd0b33c92d5231b8641b8f1dad7ddae3728fe7e785fc3c296ced172702a43e027d9e8')

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false -linkmode=external" \
    .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
  ./gobuster --help
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    gobuster
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
