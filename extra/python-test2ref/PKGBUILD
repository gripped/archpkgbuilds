# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=python-test2ref
pkgver=0.8.1
pkgrel=1
pkgdesc="Testing Against Learned Reference Data"
arch=('any')
url="https://test2ref.readthedocs.io/"
license=('MIT')
depends=('python' 'python-binaryornot')
makedepends=('python-build' 'python-installer' 'git' 'python-pdm-backend')
checkdepends=('python-pytest' 'python-pytest-cov')
source=(
  "$pkgname::git+https://github.com/nbiotcloud/test2ref#tag=v$pkgver"
)
b2sums=('82222b7b682f681e377534c595b6cbe33e38ec9c6628a91cd190cf512daf83bc8ff3a91d2a01cfe9cfa53d1c323592e669658e71ed6df902353337b87f9e645c')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"
  python -m pytest
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
