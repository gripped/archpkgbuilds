# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=kompose
pkgver=1.35.0
pkgrel=1
pkgdesc="Docker compose to Kubernetes transformation tool"
arch=('x86_64')
url="https://github.com/kubernetes/kompose"
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'git' # git binary is needed for test scripts
  'go'
  'jq'
)
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('023e4e5b328e69efee5ac5de4dde44652f57f5a17cf6158283dda218a5e8ed43368cb7fb08cea75655f2a04e6be0cfcc017ad8293c9eee2ad38721d2985893f2')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"
  local ld_flags="-compressdwarf=false -linkmode=external"
  go build -v -o build -ldflags="$ld_flags" .
}

check() {
  cd $pkgname-$pkgver
  go test -v ./...
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" ./build/kompose
  build/kompose completion bash \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/kompose"
  build/kompose completion zsh \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_kompose"
  build/kompose completion fish \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/kompose.fish"
}
