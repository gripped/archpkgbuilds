# Maintainer: Peter Jung <ptr1337@archlinux.org>

pkgname=amd-debug-tools
pkgver=0.2.1
pkgrel=2
pkgdesc="A collection of Python-based tools for debugging AMD platforms, particularly Zen-based systems."
arch=('any')
url="https://git.kernel.org/pub/scm/linux/kernel/git/superm1/amd-debug-tools.git"
license=('MIT')
depends=(
  python
  python-cysystemd
  python-jinja
  python-matplotlib
  python-packaging
  python-pandas
  python-pyudev
  python-seaborn
  python-setuptools
  python-tabulate
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-git-versioning
  python-wheel
)
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/superm1/amd-debug-tools.git#tag=${pkgver}")
sha256sums=('37d791d085e0dfc127676feb7c73f8ab83a09f0743b5b000b9bc3401d1db34bf')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir/" dist/*.whl
  # Copy systemd sleep hook
  mkdir -p "$pkgdir/usr/lib/systemd/system-sleep/"
  cp "$pkgdir/usr/lib/python3.13/site-packages/amd_debug/s2idle-hook" "$pkgdir/usr/lib/systemd/system-sleep/s2idle-hook"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"  
}
