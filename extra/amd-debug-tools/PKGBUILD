# Maintainer: Peter Jung <ptr1337@archlinux.org>

pkgname=amd-debug-tools
pkgver=0.2.2
pkgrel=1
pkgdesc="A collection of Python-based tools for debugging AMD platforms, particularly Zen-based systems."
arch=('any')
url="https://git.kernel.org/pub/scm/linux/kernel/git/superm1/amd-debug-tools.git"
license=('MIT')
depends=(
  python
  python-cysystemd
  python-jinja
  python-matplotlib
  python-packaging
  python-pandas
  python-pyudev
  python-seaborn
  python-setuptools
  python-tabulate
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-git-versioning
  python-wheel
)
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/superm1/amd-debug-tools.git#tag=${pkgver}")
sha256sums=('bca20db4c90fd9b32713818d7c59d9c731fc43206ee22f5dd345b0916feb20cd')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir/" dist/*.whl
  # Copy systemd sleep hook
  mkdir -p "$pkgdir/usr/lib/systemd/system-sleep/"
  cp "$pkgdir/usr/lib/python3.13/site-packages/amd_debug/s2idle-hook" "$pkgdir/usr/lib/systemd/system-sleep/s2idle-hook"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"  
}
