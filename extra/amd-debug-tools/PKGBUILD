# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: HurricanePootis <hurricanepootis@protonmail.com>

pkgname=amd-debug-tools
pkgver=0.2.9
pkgrel=1
pkgdesc="A collection of Python-based tools for debugging AMD platforms, particularly Zen-based systems."
arch=('any')
url="https://git.kernel.org/pub/scm/linux/kernel/git/superm1/amd-debug-tools.git"
license=('MIT')
depends=(
  acpica
  fwupd
  python
  python-cysystemd
  python-dbus
  python-gobject
  python-jinja
  python-matplotlib
  python-packaging
  python-pandas
  python-pyudev
  python-seaborn
  python-setuptools
  python-tabulate
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-git-versioning
  python-wheel
)
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/superm1/amd-debug-tools.git#tag=${pkgver}")
sha256sums=('b09d8a539a42569834c76aa00a5e9edfec9af9ee2170529a1072f99e765a3c27')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir/" dist/*.whl
  install -Dm755 "$pkgdir/usr/lib/python3.13/site-packages/amd_debug/s2idle-hook" "$pkgdir/usr/lib/systemd/system-sleep/s2idle-hook"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"  
}
