# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=2.9.4
pkgrel=1
pkgdesc='Input method server supporting various input methods'
url='https://hyperrate.com/dir.php?eid=67'
# "gcin source 授權為 LGPL2.1" from https://hyperrate.com/m/dir.php?eid=67, and I didn't find a statement with "or later"
license=('LGPL-2.1-only')
arch=('x86_64')
depends=('glibc' 'glib2' 'gdk-pixbuf2' 'cairo' 'pango' 'gtk3' 'libx11' 'libxtst'
         # Loaded via dlopen()
         'libappindicator-gtk3')
makedepends=('qt5-base' 'gtk2' 'anthy' 'curl')
optdepends=('qt5-base: support for qt5 input method'
            'gcc-libs: support for qt5 input method'
            'gtk2: support for gtk2 input method'
            'anthy: support for anthy input method'
            'curl: for downloading and uploading vocabulary databases via ts-edit'
            'bash: for various scripts')
source=("https://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz"
        fix-gtk-immodules-path.patch
        gcin-2.9.0-gcc15.patch)
sha512sums=('8ce5783b76240d6a675679ce8861424603a3db2f5f79abb874ac5b5e7a2fba0cb3f9e4465cce63bd3f54f4f8611e3abb2029d43807e823b57f6acb98bd925db5'
            '54e90b04de45acf9f8821c92234b9708281907cbdf24ebb29f9820945615669d82a8443e49fd49d6c08be4a7b9f2857c8e763f170ef879c8fab1f232bb67e85d'
            'eeb12b212fadda5ae709c0153c0e0458f56d4e835958fe32563696cb165ca43ef418c643a4fd30998decb1f96df65aa0c35c4da8a00b18934cd5375b80334162')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    sed -e 's/lib64/lib/g' \
        -e 's/^OPTFLAGS=.*/OPTFLAGS="$CFLAGS $CPPFLAGS"/' \
        -e 's/echo "LDFLAGS=/echo "LDFLAGS=$LDFLAGS /' \
        -e 's/echo "SO_FLAGS=/echo "SO_FLAGS=$LDFLAGS /' \
        -i configure
    sed -e 's/QT=qt5/QT=qt/' \
        -e 's/^LDFLAGS=/LDFLAGS+=/' \
        -i qt5-im/Makefile59
    # Don't strip programs - makepkg will handle it
    sed -e 's/install -s/install/' -i Makefile
    patch -Np1 -i ../fix-gtk-immodules-path.patch

    # Patch from OpenSUSE https://src.opensuse.org/pool/gcin/src/branch/factory/gcin-2.9.0-gcc15.patch
    patch -Np1 -i ../gcin-2.9.0-gcc15.patch

    # Several source files are 0600, breaking reading from sources in gcin-debug package
    find . -type f -exec chmod og+r {} \;
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Workarounds for GCC 14, see https://hyperrate.com/thread.php?tid=35294
    CFLAGS="$CFLAGS -Wno-error=implicit-function-declaration -Wno-error=incompatible-pointer-types -Wno-error=int-conversion" \
    ./configure --prefix=/usr --use_i18n=Y
    make -j1
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
