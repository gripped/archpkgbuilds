# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=3.12.3+r36+g5cdefa8c
pkgrel=1
pkgdesc="CD/DVD mastering tool"
url="https://wiki.gnome.org/Apps/Brasero"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gst-plugins-good totem-plparser cdrtools cdrdao shared-mime-info
         libcanberra dvd+rw-tools dconf libsm libnotify gvfs tracker3)
makedepends=(gtk-doc yelp-tools gnome-common intltool libburn libisofs
             gobject-introspection itstool docbook-xsl git glib2-devel)
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'vcdimager: video project')
_commit=5cdefa8c76ddb797bce8b67a3f5767678bd36a5a  # master
source=("git+https://gitlab.gnome.org/GNOME/brasero.git#commit=$_commit"
        brasero-appdata.patch
        brasero-better-error.patch
        brasero-iso-mime.patch
        brasero-signal-name.patch
        brasero-cell-renderer.patch
        brasero-multidrag-treeview.patch
        brasero-version-compare.patch
        brasero-spin-button.patch)
b2sums=('1455be80170e8c05823aaafcfb4d2064e28db9a8fdd0c71310bd372a24a5ace4f3f85157a91e7ec05b9dbf76b0ef594f7bb7c97ed303f3d7055c812714b28faa'
        '7c197c6c51fe5d8ae3e761badc56152260621b6e666a47af4b0691f51def7bc8b3e8d53a5cf86d5f45f14a3cbabad13604e62bd1f015c560e35ea81ce24aa655'
        'e250c13beafeb62aec9daccd0cb98276d35f3db66244edec3be4cc430b9ccaee40f0b6bfc46e0f418bb9d13c6339c6dfaab6150d3372cdf1855bc98de7dcda9e'
        'ec5abafe7a7b2873053488b8cbfaa2abb63c377a13246de11c651bf7006035ea967eaf095315721a0183731b3cc3fafc9e2e16bfcad734b12e6918f3635f7eb0'
        '95606ef57d0b57c5f472dd6dd112a8654b781595635132fa98fa47bd11816e3248dfb93ef2804887039c9ec2c32dc60bd2a2707ecd241a843ffdf4f652d7c20c'
        '9ef20ebe27cfe68452fc6588df1e77875c17de13598b94d41a05c669d926ed87c700fc9da16bb5263923935ec3f27b93187a973045eb8840b2c0ed949582976b'
        'af0a907a788e2ddd682854028a8f66764e2202edf65dee6aa50082d4d97fdc93f43087ca8b354d0c9373c827ce2f55fa9d9db571894f0d151afa24d41a63d50f'
        'edb3cc891094b3ad9a5032528f45014dcda8754ce59461a140a3dec38c3f907e37cfe5635d966e9ec48c83cfc338ebbdff1192d10d4117d1ae1aaa8022cf2003'
        '8bbffd0a8a3bfde14562fa7f379deb9876b6dde5cd1ce6baab7b3dbe0cdb12a92a09926c7727b2887ba3e49d520f6b91b134cb6b80827d83f20776bb315c460f')

pkgver() {
  cd brasero
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd brasero

  # https://gitlab.gnome.org/GNOME/brasero/-/merge_requests/11
  git apply -3 ../brasero-appdata.patch

  # https://gitlab.gnome.org/GNOME/brasero/-/merge_requests/22
  git apply -3 ../brasero-better-error.patch

  # https://gitlab.gnome.org/GNOME/brasero/-/merge_requests/26
  git apply -3 ../brasero-iso-mime.patch

  # https://gitlab.gnome.org/GNOME/brasero/-/merge_requests/27
  git apply -3 ../brasero-signal-name.patch

  # https://gitlab.gnome.org/GNOME/brasero/-/merge_requests/28
  git apply -3 ../brasero-cell-renderer.patch

  # https://gitlab.gnome.org/GNOME/brasero/-/merge_requests/29
  git apply -3 ../brasero-multidrag-treeview.patch

  # https://gitlab.gnome.org/GNOME/brasero/-/merge_requests/31
  git apply -3 ../brasero-version-compare.patch

  # https://gitlab.gnome.org/GNOME/brasero/-/merge_requests/32
  git apply -3 ../brasero-spin-button.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd brasero
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-caches \
    --disable-search \
    --disable-schemas-compile \
    --enable-compile-warnings=minimum \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd brasero
  make DESTDIR="$pkgdir" install
}
