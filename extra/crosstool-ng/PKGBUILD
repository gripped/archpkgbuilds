# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Christian Pfeiffer <cpfeiffer at live dot de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jwwolf <jwwolf+arch@gmail.com>
# Contributor: Bernhard Walle <bernhard@bwalle.de>

pkgname=crosstool-ng
pkgver=1.28.0
pkgrel=1
pkgdesc='A versatile (cross-)toolchain generator'
arch=(x86_64)
url=https://crosstool-ng.github.io/
license=(
  CC-BY-SA-2.5
  GPL-2.0-only
  LGPL-2.1-only
)
depends=(
  git
  gperf
  help2man
  lzip
  meson
  ncurses
  python
  rsync
  unzip
)
_tag=d427a7ca41690f9d50c04049df6b9b1fe35810d7
source=(git+https://github.com/crosstool-ng/crosstool-ng.git#tag=${_tag})
b2sums=('8b7ea9c5694dea147352467d71f20f76f571eb92fb182c2ffa6350b83f11a951c69fcb1a5349f0437f9b4613726c5ea90d637772adbb9cc1381ad8ce19e9b338')

prepare() {
  cd crosstool-ng
  ./bootstrap
}

pkgver() {
  cd crosstool-ng
  git describe --tags | sed 's/^crosstool-ng-//'
}

build() {
  cd crosstool-ng
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --with-bash-completion \
    --with-ncurses
  make
}

package() {
  cd crosstool-ng
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
