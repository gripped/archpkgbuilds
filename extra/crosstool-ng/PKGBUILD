# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Christian Pfeiffer <cpfeiffer at live dot de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jwwolf <jwwolf+arch@gmail.com>
# Contributor: Bernhard Walle <bernhard@bwalle.de>

pkgname=crosstool-ng
pkgver=1.26.0
pkgrel=2
pkgdesc='A versatile (cross-)toolchain generator'
arch=(x86_64)
url=https://crosstool-ng.github.io/
license=(GPL2)
depends=(
  git
  gperf
  help2man
  lzip
  meson
  ncurses
  python
  rsync
  unzip
)
_tag=334f6d6479096b20e80fd39e35f404319bc251b5
source=(git+https://github.com/crosstool-ng/crosstool-ng.git#tag=${_tag}
        strip_gzip_timestamps.patch)
b2sums=('7b78366d820e82dc8732317636261b865662e4e4b021b151d879cbc7b768ec64fc1719b92450b07de8baf3dcdf832c4d9893c65a2bcc2e1a7a483bd9c87394e7'
        '5e95e51c8ed5a85fad8d7346601c583659b807f08a87863656940f59750f5934aa44ddce2a6bc0439555699299ffe31e5a8b41b7aec06927e022ba4a7bcc5331')

prepare() {
  cd crosstool-ng

  # Patch to ignore timestamps recording in gzip metadata
  # This is required for reproducible builds
  # See https://github.com/crosstool-ng/crosstool-ng/pull/2137
  patch -Np1 -i "${srcdir}/strip_gzip_timestamps.patch"

  ./bootstrap
}

pkgver() {
  cd crosstool-ng
  git describe --tags | sed 's/^crosstool-ng-//'
}

build() {
  cd crosstool-ng
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --with-bash-completion \
    --with-ncurses
  make
}

package() {
  cd crosstool-ng
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
