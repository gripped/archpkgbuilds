# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=osqp
pkgname=python-$_pyname
pkgver=0.6.7.post0
pkgrel=1
pkgdesc='Python wrapper for OSQP: the Operator Splitting QP Solver'
url='https://osqp.org/'
license=(Apache-2.0)
arch=(x86_64)
depends=(glibc
         python
         python-numpy
         python-qdldl
         python-scipy)
makedepends=(cmake
             git
             python-build
             python-installer
             python-setuptools
             python-setuptools-scm
             python-wheel)
checkdepends=(intel-oneapi-mkl
              python-pytest)
source=(git+https://github.com/osqp/osqp-python#tag=v$pkgver
        git+https://github.com/osqp/osqp
        osqp-qdldl::git+https://github.com/osqp/qdldl)
sha256sums=('8ad12be9faee54479a85e9241043ed9918f1b2ceab0583366563514c23a3505e'
            'SKIP'
            'SKIP')

prepare() {
  cd osqp-python
  git submodule init
  git submodule set-url osqp_sources "$srcdir"/osqp
  git -c protocol.file.allow=always submodule update

  cd osqp_sources
  git submodule init
  git submodule set-url lin_sys/direct/qdldl/qdldl_sources "$srcdir"/osqp-qdldl
  git -c protocol.file.allow=always submodule update
}

build() {
  cd osqp-python
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver \
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd osqp-python
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd osqp-python
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
