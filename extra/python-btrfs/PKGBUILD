# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# AUR Maintainer: Timofey Titovets <nefelim4ag@gmail.com>

pkgname=python-btrfs
pkgver=14.1
pkgrel=2
pkgdesc="Python Btrfs module, with useful scripts to inspect btrfs internals"
arch=(any)
url="https://github.com/knorrie/python-btrfs"
license=('LGPL-3.0-or-later')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$pkgname::git+$url.git#tag=v${pkgver}")
sha256sums=('696f0496e507af773b7f1958b8a4015039d221855f5d827ab1d687c54284feb0')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
