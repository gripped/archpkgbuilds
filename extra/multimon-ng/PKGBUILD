# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=multimon-ng
pkgver=1.4.1
pkgrel=1
pkgdesc="An SDR decoder that supports POCSAG, UFSK, CLIPFSK, AFSK, HAPN, FSK, DTMF, ZVEI"
url="https://github.com/EliasOenal/multimon-ng"
arch=('x86_64')
license=("GPL-2.0-only")
depends=(
  'glibc'
  'libpulse'
)
makedepends=('cmake')
optdepends=('sox: wav conversion')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('348bb43b6fbb641d04243b8910a89713681afe20d224071ddc909b4bfdcd2c9ab1d2350a44607cad9a450b626b2ddf609b2f26890b727795221c4c6738ff4042')

build() {
  export CMAKE_POLICY_VERSION_MINIMUM=3.5
  cmake -S $pkgname-$pkgver -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
