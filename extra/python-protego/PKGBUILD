# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-protego
pkgver=0.2.1
pkgrel=3
pkgdesc="A pure-Python robots.txt parser with support for modern conventions"
url="https://github.com/scrapy/protego"
license=('BSD-3-Clause')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/protego/archive/$pkgver.tar.gz")
sha512sums=('ef8a8196d8ece577eec90cc2e2cca2899625bd777c18f01c8e72a6130d4c2c13f71fa6e32a3e8e43b749f7556f4e3dbb1662e5e94ae18e6c8a7eef950779be1b')

build() {
  cd protego-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd protego-$pkgver
  PYTHONPATH="$PWD"/src pytest
}

package() {
  cd protego-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
