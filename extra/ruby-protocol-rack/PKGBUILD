# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-rack
pkgver=0.21.1
pkgrel=1
pkgdesc='An implementation of the Rack protocol/specification'
arch=(any)
url='https://github.com/socketry/protocol-rack'
license=(MIT)
depends=(
  ruby
  ruby-protocol-http
  ruby-rack
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-async-http
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async-http
  ruby-sus-fixtures-console
)
options=(!emptydirs)
source=(https://github.com/socketry/protocol-rack/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('d541e629ef2624b313924c957b244c4631e9911c1db2ab841b1a6d810000ebabadcbe45ac0b6e613655322ba38c668521ba0ae4c3011d153a1e82bdb195db5a6')
b2sums=('e9d8c6d88dc2e53d371f03d1d0fc16806c9c26afd846b38e61ea9d201c299af0440192ea000aa4bfdc282475e38490f78a286ac304d0cf7ea2efda6668bcb2e7')

prepare() {
  cd protocol-rack-$pkgver

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i protocol-rack.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-rack-$pkgver
  gem build protocol-rack.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-rack-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-rack-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-rack-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
