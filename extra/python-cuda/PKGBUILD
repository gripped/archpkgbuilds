# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>

pkgbase=python-cuda
pkgname=(python-cuda python-cuda-core python-cuda-bindings python-cuda-pathfinder)
pkgver=13.0.1
pkgrel=1
pkgdesc="Python interface for CUDA provided by NVIDIA"
arch=(x86_64)
url="https://nvidia.github.io/cuda-python"
license=(Apache-2.0 LicenseRef-NVIDIA)
makedepends=(
  cuda
  cython
  git
  python-wheel
  python-build
  python-installer
  python-setuptools-scm
  python-versioneer
  python-pyclibrary
)
checkdepends=(
  python-pytest
  python-pytest-benchmark
  python-numpy
  python-scipy
)
options=(!emptydirs)
source=(
  $pkgbase::git+https://github.com/NVIDIA/cuda-python#tag=v$pkgver
  fix-cudart.patch
)
b2sums=('34aed910c8d8178cbef60130aa156ef8490303962e58e83385a4745fde92de0d7a58c8403246a1e0e98a4b8a21f66024569701cc98fc51aeeefb0a9f51312336'
        'db50fcdae97ddda91c400c6ad41d1d7e25ac406f8b57ccef8db9ed97ed83a1104ea4942a0f853b0b8fd02c8dbf6d5672b8181eb616515063604b345702792db7')

prepare() {
  cd $pkgbase
  # fix linking to cudart https://github.com/NVIDIA/cuda-python/issues/608
  patch -p1 -i ../fix-cudart.patch

  # release cython constraints
  sed -i 's|"cython>=3.0,<3.1.0"|"cython>=3.0"|' cuda_bindings/pyproject.toml
}

build() {
  cd $pkgbase

  # enable parallel build
  export CUDA_PYTHON_PARALLEL_LEVEL=$(nproc)

  pushd cuda_bindings
  python -m build --wheel --no-isolation
  popd

  pushd cuda_core
  python -m build --wheel --no-isolation
  popd

  pushd cuda_pathfinder
  python -m build --wheel --no-isolation
  popd

  pushd cuda_python
  python -m build --wheel --no-isolation
  popd
}

check() {
  cd $pkgbase
  # cannot run tests in chroot
  #python -m venv --system-site-packages test-env
  #test-env/bin/python -m installer cuda_bindings/dist/*.whl
  #test-env/bin/python -m installer cuda_core/dist/*.whl
  #test-env/bin/python -m installer cuda_python/dist/*.whl
  #test-env/bin/python -m pytest */tests
}

package_python-cuda-bindings() {
  license=(LicenseRef-NVIDIA)
  depends=(
    cuda
    gcc-libs
    glibc
    python
    python-cuda-pathfinder
  )

  cd $pkgbase/cuda_bindings

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_python-cuda-core() {
  license=(Apache-2.0)
  depends=(
    gcc-libs
    glibc
    python
    python-cuda-bindings
    python-numpy
  )

  cd $pkgbase/cuda_core

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_python-cuda-pathfinder() {
  license=(Apache-2.0)
  depends=(
    python
  )

  cd $pkgbase/cuda_pathfinder

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_python-cuda() {
  license=(LicenseRef-NVIDIA)
  depends=(
    python-cuda-bindings
    python-cuda-core
    python-cuda-pathfinder
  )

  cd $pkgbase/cuda_python

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
