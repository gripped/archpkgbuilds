# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>

pkgbase=python-cuda
# NOTE: python-cuda-pathfinder and python-cuda-core are packaged separately
pkgname=(python-cuda python-cuda-bindings)
pkgver=13.1.1
pkgrel=3
pkgdesc="Python interface for CUDA provided by NVIDIA"
arch=(x86_64)
url="https://nvidia.github.io/cuda-python"
license=(LicenseRef-NVIDIA)
makedepends=(
  cuda
  cython
  git
  python-wheel
  python-build
  python-installer
  python-setuptools-scm
  python-versioneer
  python-pyclibrary
  python-cuda-pathfinder
)
checkdepends=(
  python-pytest
  python-pytest-benchmark
  python-numpy
  python-scipy
)
options=(!emptydirs)
source=(
  $pkgbase::git+https://github.com/NVIDIA/cuda-python#tag=v$pkgver
)
b2sums=('aeabb096e55a8f71d713b2990789c425d9597e8f2a744e2fa280b173003f5beb76def191f9f151806f5ae867ab6811461885321e01ab0afc6c8fb54389dd69cf')

prepare() {
  cd $pkgbase
  # release cython constraints
  sed -i 's|"cython>=3.0,<3.1.0"|"cython>=3.0"|' cuda_bindings/pyproject.toml
}

build() {
  cd $pkgbase

  # enable parallel build
  export CUDA_PYTHON_PARALLEL_LEVEL=$(nproc)

  pushd cuda_bindings
  python -m build --wheel --no-isolation
  popd

  pushd cuda_python
  python -m build --wheel --no-isolation
  popd
}

check() {
  cd $pkgbase
  # cannot run tests in chroot
  #python -m venv --system-site-packages test-env
  #test-env/bin/python -m installer cuda_bindings/dist/*.whl
  #test-env/bin/python -m installer cuda_python/dist/*.whl
  #test-env/bin/python -m pytest */tests
}

package_python-cuda-bindings() {
  depends=(
    cuda
    gcc-libs
    glibc
    python
    python-cuda-pathfinder
  )

  cd $pkgbase/cuda_bindings

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_python-cuda() {
  depends=(
    python-cuda-bindings
    python-cuda-core
    python-cuda-pathfinder
  )
  optdepends=(
    'python-cccl: cuda.coop and cuda.compute modules'
    'python-numba-cuda: a DSL for CUDA SIMT programming model'
    'python-cuda-tile: a DSL for CUDA Tile programming model'
    'python-nvmath: bindings for math libraries'
    'python-nvshmem: bindings for NVSHMEM'
  )

  cd $pkgbase/cuda_python

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
