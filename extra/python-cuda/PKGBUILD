# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname=cuda
pkgbase="python-${_pkgname}"
pkgname=("python-${_pkgname}" "python-${_pkgname}-docs")
pkgver=12.4.0
pkgrel=3
pkgdesc="Python interface for CUDA provided by NVIDIA"
arch=(x86_64)
url="https://nvidia.github.io/cuda-python"
license=(LicenseRef-NVIDIA)
makedepends=(
  cuda
  cython
  git
  python-wheel
  python-build
  python-installer
  python-setuptools-scm
  python-versioneer
  python-pyclibrary
)
checkdepends=(
  python-pytest
  python-pytest-benchmark
  python-numpy
  python-scipy
)
options=(!emptydirs)
source=("$pkgbase::git+https://github.com/NVIDIA/cuda-python#tag=v$pkgver")
sha256sums=('1e30ea4dc7c047f48e3a12a74f3b1ce4a6d5ca089652fbd9c0ce63c4ae53f0c5')

build() {
  cd $pkgbase
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgbase
  # cannot run tests in chroot
  #python -m venv --system-site-packages test-env
  #test-env/bin/python -m installer dist/*.whl
  #test-env/bin/python -m pytest
}

package_python-cuda() {
  depends=(
    cuda
    cython
    python-pyclibrary
    NVIDIA-MODULE
  )

  cd $pkgbase

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}

package_python-cuda-docs() {
  pkgdesc+=" - documentation"

  cd $pkgbase

  install -vdm 755 "${pkgdir}/usr/share/doc/$pkgname"
  cp -va docs/* "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
