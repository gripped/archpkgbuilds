# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>

pkgbase=python-cuda
pkgname=(python-cuda python-cuda-core python-cuda-bindings)
pkgver=12.9.0
pkgrel=2
pkgdesc="Python interface for CUDA provided by NVIDIA"
arch=(x86_64)
url="https://nvidia.github.io/cuda-python"
license=(Apache-2.0 LicenseRef-NVIDIA)
makedepends=(
  cuda
  cython
  git
  python-wheel
  python-build
  python-installer
  python-setuptools-scm
  python-versioneer
  python-pyclibrary
)
checkdepends=(
  python-pytest
  python-pytest-benchmark
  python-numpy
  python-scipy
)
options=(!emptydirs)
source=(
  $pkgbase::git+https://github.com/NVIDIA/cuda-python#tag=v$pkgver
  fix-cudart.patch
)
b2sums=('ae3073a0bcf8d9617859b2bff77ba2950b576811eafb2629345f6d99a2eeb65ee4c9c6ada5a981af0b93327d782fbebf09141664d881e2802439d9f6cbd850f0'
        'db50fcdae97ddda91c400c6ad41d1d7e25ac406f8b57ccef8db9ed97ed83a1104ea4942a0f853b0b8fd02c8dbf6d5672b8181eb616515063604b345702792db7')

prepare() {
  cd $pkgbase
  # fix linking to cudart https://github.com/NVIDIA/cuda-python/issues/608
  patch -p1 -i ../fix-cudart.patch
}

build() {
  cd $pkgbase

  # enable parallel build
  export CUDA_PYTHON_PARALLEL_LEVEL=$(nproc)

  pushd cuda_bindings
  python -m build --wheel --no-isolation
  popd

  pushd cuda_core
  python -m build --wheel --no-isolation
  popd

  pushd cuda_python
  python -m build --wheel --no-isolation
  popd
}

check() {
  cd $pkgbase
  # cannot run tests in chroot
  #python -m venv --system-site-packages test-env
  #test-env/bin/python -m installer cuda_bindings/dist/*.whl
  #test-env/bin/python -m installer cuda_core/dist/*.whl
  #test-env/bin/python -m installer cuda_python/dist/*.whl
  #test-env/bin/python -m pytest */tests
}

package_python-cuda-bindings() {
  license=(LicenseRef-NVIDIA)
  depends=(
    cuda
    gcc-libs
    glibc
    python
  )

  cd $pkgbase/cuda_bindings

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_python-cuda-core() {
  license=(Apache-2.0)
  depends=(
    gcc-libs
    glibc
    python
    python-cuda-bindings
    python-numpy
  )

  cd $pkgbase/cuda_core

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_python-cuda() {
  license=(LicenseRef-NVIDIA)
  depends=(
    python-cuda-bindings
    python-cuda-core
  )

  cd $pkgbase/cuda_python

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 ../README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
