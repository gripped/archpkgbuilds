# Maintainer: David Runge <dvzrv@archlinux.org>

_name=atpublic
pkgname=python-atpublic
pkgver=3.1.2
pkgrel=1
pkgdesc="@public decorator for Python"
arch=(any)
url="https://gitlab.com/warsaw/public"
license=(Apache)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-pdm-backend
)
checkdepends=(
  python-pytest
  python-sybil
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('0b779e56c248b8147dc8b84c07748adf8685359d8e2de3040c375f8a2825d880376c4b0a191382b9e294a2f001244c68230a10d2d796144c1b5acc2648bca604')
b2sums=('f9a8ca6504b8fcf2910aab42899730afdc28551738321ad8d4bb722ca76c1a45a7f61310c18852b583bb4eec5571c823dcf47846dec73190370df7043ccd6e1b')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
