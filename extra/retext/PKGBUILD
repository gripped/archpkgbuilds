# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# AUR Maintainer: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Bartosz Chmura <chmurli@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: alfplayer
# Contributor: menta

_pkgname=ReText
pkgname=retext
pkgver=8.1.0
pkgrel=2
pkgdesc="A simple editor for Markdown and ReStructuredText markup languages"
arch=('any')
url="https://github.com/retext-project/retext"
license=('GPL-2.0-or-later')
depends=(
  'hicolor-icon-theme'
  'python'
  'python-chardet'
  'python-docutils'
  'python-markdown'
  'python-markups>=2.0.0'
  'python-pygments'
  'python-pyqt6'
  'xdg-utils'
)
makedepends=(
  'git'
  'imagemagick'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  'qt6-tools'
)
checkdepends=(
  'python-chardet'
  'python-docutils'
  'python-pymdown-extensions'
  'xorg-server-xvfb'
)
optdepends=(
  'python-pyqt6-webengine: for WebEngine preview'
  'python-pyenchant: for spell checking support'
)
source=(
  "git+$url#tag=$pkgver?signed"
  "x-retext-rst.xml"
)
sha256sums=('f7f2e53a17d43966924a889e4f70cfebfe0d25b1c447aad54b5eb7f7eb52a4bd'
            '6fef80cccb14813d9cc74810c397a6cd7831d1ca243536759a47c6e8b6cc977a')
validpgpkeys=('F24299FF1BBC9018B906A4CB6026936D2F1C8AE0') # Dmitry Shachnev <mitya57@debian.org>

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  xvfb-run python -m unittest -v
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # create /usr/share/* dirs
  local sharedir="$pkgdir/usr/share"
  install -vdm 755 "$sharedir"/{applications,mime/packages}

  # install icons
  local iconsdir="$sharedir/icons/hicolor"
  for size in 16 22 24 32 48 128; do
    install -vdm 755 "$iconsdir/${size}x${size}/apps"
    convert -resize $size +set date:create +set date:modify ReText/icons/$pkgname.png "$iconsdir/${size}x${size}/apps/$pkgname.png"
  done
  install -vdm 755 "$iconsdir/scalable/apps"
  install -vDm 644 ReText/icons/$pkgname.svg "$iconsdir/scalable/apps/$pkgname.svg"

  # install mime files
  install -vDm 644 "$srcdir/x-retext-rst.xml" "$sharedir/mime/packages/"

  # install desktop file
  install -vDm 644 data/me.mitya57.ReText.desktop "$sharedir/applications/"
}
