# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.56.0
pkgrel=1
pkgdesc='GNOME Flashback session'
arch=(x86_64)
url='https://wiki.gnome.org/Projects/GnomeFlashback'
license=(GPL-3.0-or-later)
depends=(
  adwaita-icon-theme
  adwaita-icon-theme-legacy
  alsa-lib
  at-spi2-core
  bash
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-bluetooth-3.0
  gnome-desktop
  gnome-panel
  gnome-session
  gnome-settings-daemon
  gsettings-desktop-schemas
  gtk3
  libcanberra
  libgdm
  libibus
  libpulse
  libx11
  libxcb
  libxext
  libxfixes
  libxi
  libxkbfile
  libxrandr
  libxxf86vm
  metacity
  nautilus
  pam
  pango
  polkit
  systemd-libs
)
makedepends=(
  autoconf-archive
  git
  glib2-devel
)
optdepends=(
  'gnome-backgrounds: Default background'
  'gnome-control-center: System settings'
  'network-manager-applet: Network management'
)
source=(
  "git+https://gitlab.gnome.org/GNOME/$pkgname.git?signed#tag=$pkgver"
  git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git
  gnome-flashback.pam
  gnome-flashback.theme
  gnome-flashback-portals.conf
)
b2sums=(
  b4c1eb2cf7637fd98efb43d48411e8034616a7d9277bd5958659d0fffc8244e2ab04e91ed24ae9fa6f5febee9296e05c219f62754214ca3751753e04e55d87aa
  SKIP
  214a01d01d3bee526bf3d856758f56fc51e77dacc8feaefee27221924abacdb3828d4269e5fa6773abec728a563478588d705996da6afbd0693d456631f01eb5
  f6d9e6933c79605c2f0fb30b5a9b69be4ffccfce2304d539019034b429117339e1136f1254ee987c763a5162b6944b350bd3eea36ee3dcb048c21a46747e4709
  ab0f388ee846b7a60655bc2d0a2103d58c6b468c5919fc652fd489282ca8700b89f0afd8a116c830b7b5793396b067a8b30c6fe4998140b17f467948ee979d63
)
validpgpkeys=(7B44FD78E49334EC10B3B288A3D013EC303E1894) # Alberts Muktupāvels <alberts.muktupavels@gmail.com>

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url gvc/gvc "$srcdir/libgnome-volume-control"
  git -c protocol.file.allow=always submodule update

  # Use the desktop background for the lock screen
  sed -i 's/org.gnome.desktop.screensaver/org.gnome.desktop.background/' gnome-flashback/libscreensaver/gf-manager.c

  # Customize style
  sed -i '/gf-unlock-dialog frame border {/{n;s/border-radius: [0-9]\+px;/border-radius: 30px;/}
          /gf-popup-window {/{n;s/border-radius: [0-9]\+px;/border-radius: 15px;/}' data/theme/common.css

  # Set default icon theme to GnomeFlashback
  sed -i '1i [org.gnome.desktop.interface:GNOME-Flashback]\nicon-theme='\''GnomeFlashback'\''\n' data/schemas/00_gnome-flashback.gschema.override

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --disable-debug \
    --enable-compile-warnings
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # Install PAM file for the screensaver
  install -Dm644 ../gnome-flashback.pam "$pkgdir/etc/pam.d/gnome-flashback"

  # Install custom icon theme to workaround the broken theme inheritance in GTK
  install -Dm644 ../gnome-flashback.theme "$pkgdir/usr/share/icons/GnomeFlashback/index.theme"

  # Add portal configuration
  install -Dm644 ../gnome-flashback-portals.conf "$pkgdir/usr/share/xdg-desktop-portal/gnome-flashback-portals.conf"
}
