# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mikael Blomstrand <mbloms ÅT kth DÖT se>

pkgname=vtable-dumper
pkgver=1.2
pkgrel=2
pkgdesc="Lists content of virtual tables in a C++ shared library"
url="https://github.com/lvc/vtable-dumper"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
  libelf
)
makedepends=(git)
source=("git+$url#tag=$pkgver")
b2sums=('3a4fe1a399edfacc3c6597d399137c6b0a5ab61c97eb8c10e0ccc364f583cc8f3dc819784c7a776547db05b173d60a86d1649155e57ce21f7ba823e5010f89be')

prepare() {
  cd $pkgname

  # Fix license
  git cherry-pick -n 11f9e0e96b0db0a25f85c37fcb9cb26fdd8824af
}

build() {
  make -C $pkgname
}

package() {
  make -C $pkgname install DESTDIR="$pkgdir"
}

# vim:set sw=2 sts=-1 et:
