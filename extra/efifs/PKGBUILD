# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=efifs
pkgver=1.11
pkgrel=1
_commit=cc679906228b76b78d0457cea54f058eb7396ffc
pkgdesc='Standalone EFI file system drivers'
arch=('any')
url='https://efi.akeo.ie/'
license=('GPL-3.0-or-later')
makedepends=('git' 'mingw-w64-gcc' 'arm-none-eabi-gcc' 'arm-none-eabi-newlib'
             'aarch64-linux-gnu-gcc' 'riscv64-linux-gnu-gcc')
source=("git+https://github.com/pbatard/EfiFs.git#commit=${_commit}?signed"
        'git+https://git.savannah.gnu.org/git/grub.git'
        'git+https://github.com/ncroxon/gnu-efi.git'
        '010-efifs-grub-fix-conflicting-parameter-types.patch'
        '020-efifs-grub-fix-incompatible-pointer-types.patch'
        '030-efifs-gnu-efi-remove-werror.patch')
sha256sums=('51a6af9eb2f9760365dc564c703715b262aaa694d1249706d84dbe2e138bde59'
            'SKIP'
            'SKIP'
            'f87972064fbde9215ee2c9c9f7085dfc738f79cecd8b5b735cd4ce2916b543f8'
            '5f207623fd37dfc6be89e600e5d54c118d93537c94fb8ec19f9ae5e1473cca6b'
            'acf342ebe5f88198caefaf50661280c1f873a477f8d8be4bafc8224e303eafd0')
validpgpkeys=('E7CDF2A016DDDD53F0B7943838E0CF5E69EDD671') # Pete Batard <pete@akeo.ie>

prepare() {
    git -C EfiFs submodule init
    git -C EfiFs config --local submodule.grub.url "${srcdir}/grub"
    git -C EfiFs config --local submodule.gnu-efi.url "${srcdir}/gnu-efi"
    git -C EfiFs -c protocol.file.allow='always' submodule update
    
    patch -d EfiFs/grub -Np1 -i "${srcdir}/EfiFs/0001-GRUB-fixes.patch"
    
    # https://github.com/pbatard/EfiFs/issues/47
    patch -d EfiFs/grub -Np1 -i "${srcdir}/010-efifs-grub-fix-conflicting-parameter-types.patch"
    patch -d EfiFs/grub -Np1 -i "${srcdir}/020-efifs-grub-fix-incompatible-pointer-types.patch"
    patch -d EfiFs/gnu-efi -Np1 -i "${srcdir}/030-efifs-gnu-efi-remove-werror.patch"
    
    cp -af EfiFs{,-ia32}
    cp -af EfiFs{,-arm}
    cp -af EfiFs{,-aa64}
    cp -af EfiFs{,-riscv64}
}

build() {
    unset -v CFLAGS
    unset -v MAKEFLAGS
    
    printf '%s\n' '  -> building for x64...'
    make -C EfiFs ARCH='x64'
    
    printf '%s\n' '  -> building for ia32...'
    make -C EfiFs-ia32 ARCH='ia32'
    
    printf '%s\n' '  -> building for arm...'
    make -C EfiFs-arm ARCH='arm' CROSS_COMPILE='arm-none-eabi-'
    
    printf '%s\n' '  -> building for aa64...'
    make -C EfiFs-aa64 ARCH='aa64' CROSS_COMPILE='aarch64-linux-gnu-'
    
    printf '%s\n' '  -> building for riscv64...'
    make -C EfiFs-riscv64 ARCH='riscv64' CROSS_COMPILE='riscv64-linux-gnu-'
}

package() {
    install -D -m644 EfiFs/src/*.efi -t "${pkgdir}/usr/lib/efifs-x64"
    install -D -m644 EfiFs-ia32/src/*.efi -t "${pkgdir}/usr/lib/efifs-ia32"
    install -D -m644 EfiFs-arm/src/*.efi -t "${pkgdir}/usr/lib/efifs-arm"
    install -D -m644 EfiFs-aa64/src/*.efi -t "${pkgdir}/usr/lib/efifs-aa64"
    install -D -m644 EfiFs-riscv64/src/*.efi -t "${pkgdir}/usr/lib/efifs-riscv64"
}
