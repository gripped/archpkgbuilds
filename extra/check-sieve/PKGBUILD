# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Geoffrey Frogeye <geoffrey+aur@frogeye.fr>

pkgname=check-sieve
pkgver=0.10
pkgrel=1
pkgdesc='Syntax checker for Sieve scripts'
arch=('x86_64')
url='https://github.com/dburkart/check-sieve'
license=('MIT')
depends=('gcc-libs')
makedepends=('git')
checkdepends=('python')
options=('!buildflags')
source=("$pkgname::git+$url#tag=check-sieve-$pkgver")
sha512sums=('06892661fe24b8746b52be4df51c35f970de3599332e4556c0ae6dc15cfcc7150bb422b32f73a96e2a51e10f67313bcdd005ca693238f21cf48e34d55d9b4ba9')
b2sums=('a70ab4f5195b80fe3b820ed64e24423cf48ba718eaaf9c65d335ef55753cd6800fa28f62e7839cb7ca3cf340128a71f69b6481590b76a895e1c2c4903893123c')

prepare() {
  cd "$pkgname"

  # ensure LDFLAGS are passed to compiler
  sed \
    -i Makefile \
    -e '/\t$(CXX).*/s/$/ $(LDFLAGS)/'
}


build() {
  cd "$pkgname"

  export CFLAGS+=" -ffat-lto-objects"

  make
}

# https://github.com/dburkart/check-sieve/issues/66
#check() {
#  cd "$pkgname"
#
#  make test
#}

package() {
  cd "$pkgname"

  make INSTALL_PREFIX="$pkgdir/usr" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # fix incorrect permissions on library
  chmod 644 "$pkgdir/usr/lib/libchecksieve.a"
}

