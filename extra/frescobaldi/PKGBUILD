# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=frescobaldi
pkgver=4.0.0
pkgrel=3
pkgdesc="A LilyPond sheet music text editor"
arch=(any)
url="https://www.frescobaldi.org/"
_url="https://github.com/frescobaldi/frescobaldi"
license=(GPL-2.0-only)
depends=(poppler
         python
         python-ly
         python-pygame
         python-pyqt6
         python-pyqt6-sip
         python-pyqt6-webengine
         python-qpageview
         qt6-base
         qt6-svg)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
optdepends=('lilypond: music engraving'
            'python-pygame: MIDI input and playback'
            'python-pycups: printing to a CUPS server'
            'hyphen-de: hyphenation patterns for German'
            'hyphen-en: hyphenation patterns for English'
            'hyphen-es: hyphenation patterns for Spanish'
            'hyphen-fr: hyphenation patterns for French'
            'hyphen-hu: hyphenation patterns for Hungarian'
            'hyphen-it: hyphenation patterns for Italian'
            'hyphen-nl: hyphenation patterns for Dutch')
_archive="$pkgname-$pkgver"
source=("$_url/archive/v$pkgver/$_archive.tar.gz"
        "$_url/pull/1901.patch"
        )
sha256sums=('dce8aca1b45090526d5bd8182519f5399c7956b90402207bb7aa4f338edfe98e'
            '1e13c7168e4176d2546dcc53b523b33e47817283d39d295f327ab84697167872')

prepare() {
	cd "$_archive"
	patch -p1 -i ../1901.patch
	# Provided by hyphen-*
	rm -f frescobaldi/hyphdicts/hyph_*.dic
	rm -f frescobaldi/hyphdicts/README*
}

build() {
	cd "$_archive"
	python -m build -wn
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
