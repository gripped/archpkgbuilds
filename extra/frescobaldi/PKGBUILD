# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=frescobaldi
pkgver=4.0.2
pkgrel=4
pkgdesc="A LilyPond sheet music text editor"
arch=(any)
url="https://www.frescobaldi.org/"
_url="https://github.com/frescobaldi/frescobaldi"
license=(GPL-2.0-only)
depends=(python
         python-ly
         python-pygame
         python-pyqt6
         python-pyqt6-sip
         python-pyqt6-webengine
         python-qpageview
         qt6-base
         qt6-svg)
makedepends=(python-{build,installer,wheel}
             python-hatchling)
optdepends=('lilypond: music engraving'
            'python-pygame: MIDI input and playback'
            'python-pycups: printing to a CUPS server'
            'hyphen-de: hyphenation patterns for German'
            'hyphen-en: hyphenation patterns for English'
            'hyphen-es: hyphenation patterns for Spanish'
            'hyphen-fr: hyphenation patterns for French'
            'hyphen-hu: hyphenation patterns for Hungarian'
            'hyphen-it: hyphenation patterns for Italian'
            'hyphen-nl: hyphenation patterns for Dutch')
_archive="$pkgname-$pkgver"
source=("$_url/releases/download/v$pkgver/$_archive.tar.gz")
sha256sums=('3533f5fc610f509ecac38f22c5a301057360239406839a84a553ccefbc68abf9')

prepare() {
	cd "$_archive"
	# Provided by hyphen-*
	rm -f frescobaldi/hyphdicts/hyph_*.dic
	rm -f frescobaldi/hyphdicts/README*
}

build() {
	cd "$_archive"
	python -m build -wn
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm644 -t "$pkgdir/usr/share/applications/" linux/org.frescobaldi.Frescobaldi.desktop
	install -Dm644 -t "$pkgdir/usr/share/metainfo/" linux/org.frescobaldi.Frescobaldi.metainfo.xml
}
