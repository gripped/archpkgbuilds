# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

shopt -s extglob

pkgbase=ghc
pkgname=(ghc-libs ghc ghc-static)
pkgver=9.4.8
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('x86_64')
url='https://www.haskell.org/ghc/'
license=('custom')
makedepends=('ghc-static' 'perl' 'libxslt' 'docbook-xsl' 'python-sphinx' 'haskell-hadrian'
             'haskell-hscolour' 'texlive-fontsrecommended' 'texlive-latexextra' 'texlive-xetex'
             'ttf-dejavu' 'alex' 'happy' 'time' 'numactl' 'python-sphinx_rtd_theme')
source=("https://downloads.haskell.org/~ghc/$pkgver/$pkgbase-${pkgver}-src.tar.xz"
        ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook)
sha512sums=('e5cfb30adc73dc0054f5db2921e5f255c8a980e005798882a2e9daa8df2409ddbe1ec6403e1f6862efc9e4700db4b68d5cae36999d77c7d3fd2fe0ab51cb9923'
            'd69e5222d1169c4224a2b69a13e57fdd574cb1b5932b15f4bc6c7d269a9658dd87acb1be81f52fbcf3cb64f96978b9943d10cee2c21bff0565aaa93a5d35fcae'
            '5f659651d8e562a4dcaae0f821d272d6e9c648b645b1d6ab1af61e4dd690dc5a4b9c6846753b7f935963f001bb1ae1f40cd77731b71ef5a8dbc079a360aa3f8f'
            '3bdbd05c4a2c4fce4adf6802ff99b1088bdfad63da9ebfc470af9e271c3dd796f86fba1cf319d8f4078054d85c6d9e6a01f79994559f24cc77ee1a25724af2e6')

prepare() {
  cd ghc-$pkgver

  # devendor rtd-theme for sphinx compatibility
  rm -r docs/users_guide/rtd-theme
  sed -i 's/rtd-theme/sphinx_rtd_theme/' docs/users_guide/conf.py

  # detects GCC correctly
  sed -i 's/grep -q gcc/grep -qi gcc/' m4/fp_gcc_version.m4
}

build() {
  cd ghc-$pkgver

  LDFLAGS="${LDFLAGS/-Wl,-z,pack-relative-relocs/}" ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/ghc \
    --with-system-libffi
  hadrian -j --flavour=perf
}

check() {
  cd ghc-$pkgver
  # hadrian test --flavour=perf
}

package_ghc-static() {
  pkgdesc='The Glasgow Haskell Compiler - Static Libraries and Documentation'
  depends=('ghc')

  cd ghc-$pkgver

  DESTDIR="$pkgdir" LDFLAGS="${LDFLAGS/-Wl,-z,pack-relative-relocs/}" hadrian install --flavour=perf --prefix=/usr

  mv "$pkgdir"/usr/lib/ghc-$pkgver/lib/package.conf.d "$srcdir"/static-package.conf.d
  find "$pkgdir"/usr/lib ! \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -type f -delete
  find "$pkgdir"/usr/lib -type d -empty -delete
  mv "$srcdir"/static-package.conf.d "$pkgdir"/usr/lib/ghc-$pkgver/lib/
  rm "$pkgdir"/usr/lib/ghc-$pkgver/lib/static-package.conf.d/*.copy
  rm -r "$pkgdir"/usr/bin "$pkgdir"/usr/share/man "$pkgdir"/usr/lib/ghc-$pkgver/bin
  mv "$pkgdir"/usr/share/doc/ghc{-$pkgver,}

  install -Dm755 libraries/{gen_contents_index,prologue.txt} -t "$pkgdir"/usr/share/doc/ghc-$pkgver/html/libraries/
  install -Dm644 "$srcdir"/ghc-rebuild-doc-index.hook -t "$pkgdir"/usr/share/libalpm/hooks/
}

package_ghc() {
  pkgdesc='The Glasgow Haskell Compiler'
  depends=('gcc' 'ghc-libs')
  provides=('haskell-haddock=2.27.0'
            'haskell-hp2ps=0.1'
            'haskell-hpc-bin=0.68'
            'haskell-hsc2hs=0.68.8')
  replaces=('haskell-haddock'
            'haskell-hp2ps'
            'haskell-hpc-bin'
            'haskell-hsc2hs')

  provides+=("haskell-ghc=$pkgver")
  replaces+=("haskell-ghc")

  cd ghc-$pkgver
  DESTDIR="$pkgdir" LDFLAGS="${LDFLAGS/-Wl,-z,pack-relative-relocs/}" hadrian install --flavour=perf --prefix=/usr

  # Remove static libs
  find "$pkgdir"/usr/lib \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -delete

  # ghc-pkg is in ghc-libs
  rm "$pkgdir"/usr/lib/ghc-$pkgver/bin/ghc-pkg*
  rm "$pkgdir"/usr/bin/ghc-pkg*
  (cd "$pkgdir"/usr/lib/ghc-$pkgver/lib; rm -r package.conf.d)
  (cd "$pkgdir"/usr/lib/ghc-$pkgver/lib/$CARCH-linux-ghc-$pkgver; rm -r !(ghc-$pkgver|libHSghc-$pkgver-ghc$pkgver.so))

  # docs moved to ghc-static
  rm -r "$pkgdir"/usr/share/doc

  install -Dm644 utils/completion/ghc.bash \
    "$pkgdir/usr/share/bash-completion/completions/ghc"
}

package_ghc-libs() {
  pkgdesc='The Glasgow Haskell Compiler - Dynamic Libraries'
  install='ghc.install'
  depends=('gmp' 'libffi' 'numactl' 'perl')
  provides=('haskell-array=0.5.4.0'
            'haskell-base=4.17.2.1'
            'haskell-binary=0.8.9.1'
            'haskell-bytestring=0.11.5.3'
            'haskell-cabal=3.8.1.0'
            'haskell-cabal-syntax=3.8.1.0'
            'haskell-containers=0.6.7'
            'haskell-deepseq=1.4.8.0'
            'haskell-directory=1.3.7.1'
            'haskell-exceptions=0.10.5'
            'haskell-filepath=1.4.2.2'
            'haskell-ghc-bignum=1.3'
            'haskell-ghc-boot=9.4.8'
            'haskell-ghc-boot-th=9.4.8'
            'haskell-ghc-compact=0.1.0.0'
            'haskell-ghc-heap=9.4.8'
            'haskell-ghc-prim=0.9.1'
            'haskell-haskeline=0.8.2'
            'haskell-hpc=0.6.1.0'
            'haskell-integer-gmp=1.1'
            'haskell-libiserv=9.4.8'
            'haskell-mtl=2.2.2'
            'haskell-parsec=3.1.16.1'
            'haskell-pretty=1.1.3.6'
            'haskell-process=1.6.18.0'
            'haskell-stm=2.5.1.0'
            'haskell-template-haskell=2.19.0.0'
            'haskell-terminfo=0.4.1.5'
            'haskell-text=2.0.2'
            'haskell-time=1.12.2'
            'haskell-transformers=0.5.6.2'
            'haskell-unix=2.7.3'
            'haskell-xhtml=3000.2.2.1'
            'haskell-ghc-pkg=9.4.8')
  replaces=('haskell-array'
            'haskell-base'
            'haskell-binary'
            'haskell-bytestring'
            'haskell-cabal'
            'haskell-cabal-syntax'
            'haskell-containers'
            'haskell-deepseq'
            'haskell-directory'
            'haskell-exceptions'
            'haskell-filepath'
            'haskell-ghc-bignum'
            'haskell-ghc-boot'
            'haskell-ghc-boot-th'
            'haskell-ghc-compact'
            'haskell-ghc-heap'
            'haskell-ghci'
            'haskell-ghc-prim'
            'haskell-haskeline'
            'haskell-hpc'
            'haskell-integer-gmp'
            'haskell-libiserv'
            'haskell-mtl'
            'haskell-parsec'
            'haskell-pretty'
            'haskell-process'
            'haskell-stm'
            'haskell-template-haskell'
            'haskell-terminfo'
            'haskell-text'
            'haskell-time'
            'haskell-transformers'
            'haskell-unix'
            'haskell-xhtml'
            'haskell-ghc-pkg')

  provides+=("haskell-ghci=$pkgver")
  conflicts+=('haskell-ghci')

  cd ghc-$pkgver

  DESTDIR="$pkgdir" LDFLAGS="${LDFLAGS/-Wl,-z,pack-relative-relocs/}" hadrian install --flavour=perf --prefix=/usr

  # Remove static libs
  find "$pkgdir"/usr/lib \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -delete

  # ghc library, headers, and other exes are in the ghc package
  rm -r "$pkgdir"/usr/lib/ghc-$pkgver/lib/$CARCH-linux-ghc-$pkgver/{ghc-$pkgver,libHSghc-$pkgver-ghc$pkgver.so}
  (cd "$pkgdir"/usr/lib/ghc-$pkgver/bin; rm !(ghc-pkg*))
  (cd "$pkgdir"/usr/lib/ghc-$pkgver/lib; rm -r !(package.conf.d|$CARCH-linux-ghc-$pkgver))
  rm "$pkgdir"/usr/lib/ghc-$pkgver/lib/package.conf.d/*.copy
  (cd "$pkgdir"/usr/bin; rm !(ghc-pkg*))

  # docs moved to ghc-static, man pages moved to ghc
  rm -r "$pkgdir"/usr/share/{man,doc}

  install -Dm644 "$srcdir"/ghc-register.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-register.hook
  install -Dm644 "$srcdir"/ghc-unregister.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-unregister.hook

  install -dm755 "$pkgdir"/usr/share/haskell/{register,unregister}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
