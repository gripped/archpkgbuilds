# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=eog-plugins
pkgver=44.0
pkgrel=3
pkgdesc="Plugins for Eye of Gnome"
url="https://wiki.gnome.org/Apps/EyeOfGnome/Plugins"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  eog
  libchamplain
  python-gobject
)
makedepends=(
  git
  gobject-introspection
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/eog-plugins.git#tag=${pkgver/[a-z]/.&}")
b2sums=('13e362677b7078b53be0c7b615e9b2808823aa6f01a4c24f1f4d2b3873097efcba2afdbaa62932c84e45bc086c8e7135caaabf89525520b478268c9e33d2cd62')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D plugin_postasa=false
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
