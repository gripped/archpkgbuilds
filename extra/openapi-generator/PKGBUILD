# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=7.0.0
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=(any)
url="https://github.com/openapitools/openapi-generator/"
license=(Apache)
depends=(
  bash
  java-runtime=11
)
makedepends=(
  maven
  java-environment=11
  strip-nondeterminism
)
source=(
  $url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('05f1cb00c41eb6a7727ec69a0007160f13bca86933937b9f31164c0aba37775eb1c2eafaa60f168ac76bc9368fe8cc7748c1de9e7e11159870ac06523a7981a5'
            '0b81955e2da12182b70366c4d8ff48d970a0edee6242a13dc657879b2e1c15205a2bfa01cbef51dd86d90e1176569a211b704d30d19b529e15ead825a89a4e9c')
b2sums=('ee749c1efa7159a7057b25efec8ba288e3e5aa90ef825751592490e7022d8025654bb642b4ff1431e9c31e91b6af37b2ae5d5d94d4deec2ea2eef402aec557c2'
        'd2f38df8fd23a32e95e9e09624283ca4927dce8cf6dbdbfc11547396ae2bd9c6c1ff65082447f1e30caba3654fd6e50558272d524242ca6727df7024729a1123')

build() {
  cd $pkgname-$pkgver
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  install -vDm 644 $pkgname-$pkgver/modules/$pkgname-cli/target/openapi-generator-cli.jar -t "$pkgdir/usr/share/java/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/scripts/$pkgname-cli-completion.bash "$pkgdir/usr/share/bash/bash-completion/completions/openapi-generator"
  install -vDm 755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  ln -svf "$pkgname" "$pkgdir/usr/bin/$pkgname-cli"
}
