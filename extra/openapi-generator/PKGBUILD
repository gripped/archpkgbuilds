# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=7.4.0
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=(any)
url="https://github.com/openapitools/openapi-generator/"
license=(Apache-2.0)
_java_version=11
depends=(
  bash
  java-runtime=$_java_version
)
makedepends=(
  maven
  java-environment=$_java_version
  strip-nondeterminism
)
source=(
  $url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('7d652d74a68bb6056e528de67eca07a3e1ed7af3c64fedf6e156f6369d2d5c16b49be4db0a7b4463204538c2fece4e4f69a5e5b0d3bf0b6bd3be0c02a2fc4c7c'
            '82a21b46b5340108a6cf54350c25e7d9e9fbb97a0837ff68dc74f7cf2b105c12c33a4cc20aa641e8a9e4034de3e62a3f9ca03f6f7e5dc4f37b3827dff41eac46')
b2sums=('66c208e98b27b87f348a4a1f529bb84d7f6c37df9f76b67715a02ef2a4665c608cb0dda36619a28526eee42405144164a31417cbc8906e7f04d1d94cf6cc4798'
        'c7bf34d415aeceba2816edd6010706a7b9b547c9240344e5431f3280a552c2b578de6fc3fc90b0640d4f91d655cc419aabe615b8946d6c82d71061afa65cc302')

prepare() {
  # set java version for wrapper script
  sed "s/JAVA_VERSION/$_java_version/" $pkgname.sh > $pkgname
}

build() {
  cd $pkgname-$pkgver
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  install -vDm 644 $pkgname-$pkgver/modules/$pkgname-cli/target/openapi-generator-cli.jar -t "$pkgdir/usr/share/java/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/scripts/$pkgname-cli-completion.bash "$pkgdir/usr/share/bash/bash-completion/completions/openapi-generator"
  install -vDm 755 $pkgname -t "$pkgdir/usr/bin/"
  ln -svf "$pkgname" "$pkgdir/usr/bin/$pkgname-cli"
}
