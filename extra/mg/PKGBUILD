# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hannes Rist <hrist@phreeknet.org>

pkgname=mg
pkgver=20260222
pkgrel=1
pkgdesc="Micro GNU/emacs"
arch=('x86_64')
url="https://github.com/hboetes/mg"
makedepends=('git')
depends=('ncurses' 'libbsd')
license=('custom')
source=("git+https://github.com/hboetes/mg.git#tag=${pkgver}")
sha256sums=('1512c141c30239358487d156d414a190e9a86bd4832ad64d578ab8cbbbbc0750')

build() {
  cd mg
  make prefix=/usr mandir=/usr/share/man
}

package() {
  cd mg

  make install INSTALL=install prefix=/usr mandir=/usr/share/man DESTDIR="$pkgdir" BINDIR=/usr/bin

  install -Dm644 README -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 tutorial -t "$pkgdir/usr/share/doc/$pkgname"
}
