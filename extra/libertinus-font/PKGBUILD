# Maintainer: Ivy Foster <iff@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=libertinus-font
_fname="${pkgbase%-font}"
pkgname=(otf-$_fname)
pkgver=7.050
pkgrel=1
pkgdesc='Fonts based on Linux Libertine/Biolinum, with extended math support'
arch=(any)
url="https://github.com/alerque/$_fname"
license=(OFL-1.1-RFN)
_archive="${_fname^}-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.zst")
sha256sums=('cbb54c4c482376eb17bb6397494489baacff0755d3864f9b5c772e2f3d43d429')

package_otf-libertinus() {
	cd "$_archive"
	provides=("$pkgbase=$pkgver")
	replaces=("$pkgbase")
	install -Dm0644 -t "$pkgdir/usr/share/fonts/${_fname}/" static/OTF/*.otf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" OFL.txt AUTHORS.txt
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md preview.svg FONTLOG.txt documentation/*.pdf
}
