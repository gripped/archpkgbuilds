# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=proton-vpn-gtk-app
pkgver=4.14.1
pkgrel=2
pkgdesc="ProtonVPN GTK app, Maintained by Community"
arch=(any)
url="https://github.com/ProtonVPN/proton-vpn-gtk-app"
license=(GPL-3.0-or-later)
depends=(
  dbus-python
  glib2
  pango
  gtk4
  libnotify
  python
  python-cairo
  python-distro
  python-gobject
  python-packaging
  proton-vpn-daemon
  python-proton-core
  python-proton-keyring-linux
  python-proton-vpn-api-core
  python-proton-vpn-local-agent
  python-proton-vpn-network-manager
  python-requests
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
optdepends=(
  libappindicator-gtk3
)
source=("git+https://github.com/ProtonVPN/proton-vpn-gtk-app.git#tag=v${pkgver}")
sha256sums=('65daf7a10be49306ff39a8f8b7fb6286e6dbc4792a9039b261e9f1519136c5a0')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -d -m755 "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

  cp ./rpmbuild/SOURCES/proton.vpn.app.gtk.desktop "${pkgdir}/usr/share/applications/proton.vpn.app.gtk.desktop"
  cp ./rpmbuild/SOURCES/proton-vpn-logo.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
  cp ./proton/vpn/app/gtk/assets/icons/proton-vpn-sign.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
}
