# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=proton-vpn-gtk-app
pkgver=4.13.1
pkgrel=2
pkgdesc="ProtonVPN GTK app, Maintained by Community"
arch=(any)
url="https://github.com/ProtonVPN/proton-vpn-gtk-app"
license=(GPL-3.0-or-later)
depends=(
  dbus-python
  glib2
  pango
  gtk3
  libnotify
  python
  python-cairo
  python-distro
  python-gobject
  python-packaging
  proton-vpn-daemon
  python-proton-core
  python-proton-keyring-linux
  python-proton-vpn-api-core
  python-proton-vpn-local-agent
  python-proton-vpn-network-manager
  python-requests
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
optdepends=(
  libappindicator-gtk3
)
source=("git+https://github.com/ProtonVPN/proton-vpn-gtk-app.git#tag=v${pkgver}")
sha256sums=('25c02d41b771d5050b2f4b56683ebaea46d3fe552dbee361aa4d3ac272393055')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -d -m755 "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

  cp ./rpmbuild/SOURCES/proton.vpn.app.gtk.desktop "${pkgdir}/usr/share/applications/protonvpn.desktop"
  cp ./rpmbuild/SOURCES/proton-vpn-logo.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
}
