# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=proton-vpn-gtk-app
pkgver=4.9.6
pkgrel=1
pkgdesc="ProtonVPN GTK app, Maintained by Community"
arch=(any)
url="https://github.com/ProtonVPN/proton-vpn-gtk-app"
license=(GPL-3.0-or-later)
depends=(
  dbus-python
  gtk3
  libnotify
  python-cairo
  python-distro
  python-gobject
  python-packaging
  python-proton-core
  python-proton-keyring-linux
  python-proton-vpn-api-core
  python-proton-vpn-local-agent
  python-proton-vpn-network-manager
  python-requests
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
optdepends=(
  libappindicator-gtk3
)
source=("git+https://github.com/ProtonVPN/proton-vpn-gtk-app.git#tag=${pkgver}")
sha256sums=('345543cef1492716811e874b3dbe98d6c80f228db3754a644459ef0c32d12f22')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -d -m755 "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

  cp ./rpmbuild/SOURCES/protonvpn-app.desktop "${pkgdir}/usr/share/applications/protonvpn.desktop"
  cp ./rpmbuild/SOURCES/proton-vpn-logo.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
}
