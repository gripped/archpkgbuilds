# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=proton-vpn-gtk-app
pkgver=4.8.1
pkgrel=3
pkgdesc="ProtonVPN GTK app, Maintained by Community"
arch=(any)
url="https://github.com/ProtonVPN/proton-vpn-gtk-app"
license=(GPL-3.0-or-later)
depends=(
  dbus-python
  gtk3
  python-aiohttp
  python-bcrypt
  python-cairo
  python-distro
  python-gnupg
  python-gobject
  python-jinja
  python-packaging
  python-proton-core
  python-proton-keyring-linux
  python-proton-vpn-api-core
  python-proton-vpn-local-agent
  python-proton-vpn-network-manager
  python-pynacl
  python-pyopenssl
  python-requests
  python-sentry_sdk
  webkit2gtk
)
makedepends=(
  git
  python-setuptools
)
optdepends=(
  libappindicator-gtk3
)
source=("git+https://github.com/ProtonVPN/proton-vpn-gtk-app.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
  cd "$pkgname"
  python setup.py build
}

package() {
  cd "$pkgname"
  python setup.py install --root="$pkgdir" --optimize=1
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -d -m755 "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

  cp ./rpmbuild/SOURCES/protonvpn-app.desktop "${pkgdir}/usr/share/applications/protonvpn.desktop"
  cp ./rpmbuild/SOURCES/proton-vpn-logo.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
}
