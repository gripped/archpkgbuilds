# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=packer
pkgver=1.13.1
pkgrel=1
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('LicenseRef-BSL-1.1')
makedepends=('git' 'go' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('89d85094fe390abc26a52228a28bdbb95c136b1164890dbd85e7597ae01030b5b61a2d00a6bc37ed5370ad06f41061074bee616561e64ec62acbf3794e89c653')

prepare() {
	export GOPATH="${srcdir}"
	export PATH="$PATH:$GOPATH/bin"
	mkdir -p "src/github.com/hashicorp"
	mv "${pkgname}-${pkgver}" "src/github.com/hashicorp/${pkgname}"
	sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${pkgname}/Makefile"
}

build() {
  cd "src/github.com/hashicorp/${pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
	go build -o packer-binary
}

# Currently broken
# check() {
#   cd "src/github.com/hashicorp/${pkgname}"
#   go test ./...
# }

package() {
	cd "src/github.com/hashicorp/${pkgname}"
	install -Dm755 packer-binary "${pkgdir}/usr/bin/packer"
	install -Dm644 contrib/zsh-completion/_packer "${pkgdir}"/usr/share/zsh/site-functions/_packer
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set et sw=2 ts=2 tw=79:
