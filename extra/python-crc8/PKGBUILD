# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-crc8
pkgver=0.2.0
pkgrel=5
pkgdesc="A module with the CRC8 algorithm for Python"
url="https://github.com/niccokunzmann/crc8"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("git+https://github.com/niccokunzmann/crc8.git#tag=v$pkgver")
sha512sums=('1677c179a42c3772c4640eab579ba914f8a29a98179e091b22ce2f37faa251b15df53875e6f30e474526bb8492ed776742ad9329842c148a971ad3c10222b0f1')

build() {
  cd crc8
  python -m build --wheel --no-isolation
}

check() {
  cd crc8
  python test_crc8.py
}

package() {
  cd crc8
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
