# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>
# Contributor: Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>

pkgname=cgit-aurweb
pkgver=1.3
pkgrel=1
pkgdesc='A web interface for git written in plain C (aurweb branch)'
arch=('x86_64')
url='https://git.zx2c4.com/cgit/'
license=('GPL-2.0-only')
depends=('glibc'
         'luajit' #'libluajit-5.1.so'
         'zlib-ng' 'libz-ng.so')
makedepends=('git' 'asciidoc')
optdepends=('groff: about page using man page syntax'
            'python-pygments: syntax highlighting support'
            'python-docutils: about page formatted with reStructuredText'
            'python-markdown: about page formatted with markdown'
            'lua51-luaossl: for lua filters'
            'gzip: gzip compressed snapshots'
            'bzip2: bzip2 compressed snapshots'
            'lzip: lzip compressed snapshots'
            'xz: xz compressed snapshots'
            'zstd: zstd compressed snapshots'
            'mime-types: serve file with correct content-type header')
install=cgit.install
validpgpkeys=('AB9942E6D4A4CFC3412620A749FC7012A5DE03AE')
source=("git+https://git.zx2c4.com/cgit.git#tag=v${pkgver}"
        'git+https://github.com/git/git.git'
        '1001-aurweb.patch'
        'cgit.tmpfiles'
        'apache.example.conf')
sha256sums=('a285188cb3b9cda138e418ff07dddecfc0302d13418a9193c6d64548f981d3be'
            'SKIP'
            '5b0f85f9c7d6004565c2e3b75200380037c5dbeb60e29d6161a3b82cff9b1c97'
            'c1d03b2abdd4091f10e777644242762d2e34f903b4587e1683a0560bebc45eea'
            'fde213191b38137c1e29b123eb777004dc84cc6ca9d97d5f78ad717689def5f1')

prepare() {
  cd cgit/

  # branch 'aurweb' from https://gitlab.archlinux.org/archlinux/cgit.git ...
  # Please do *NOT* merge/cherry-pick, as commit hashes will change on rebase!
  git apply --verbose --index < ../1001-aurweb.patch

  git config --file=.gitmodules submodule.git.url ../git/
  git submodule init
  git -c protocol.file.allow=always submodule update
}

build() {
  cd cgit/

  ZLIB_NG=1
  export ZLIB_NG

  make \
    all doc-man
}

# The customized output for AUR makes time tests fail, so
# skip completly. We should be find if cgit package succeeded.

package() {
  cd cgit/

  make \
    CGIT_SCRIPT_PATH="/usr/share/webapps/${pkgname}" \
    DESTDIR="${pkgdir}" \
    prefix='/usr' \
    filterdir="/usr/lib/${pkgname}/filters" \
    install install-man

  install -vDm0644 "${srcdir}/cgit.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -vDm0644 "${srcdir}/apache.example.conf" "${pkgdir}/etc/webapps/${pkgname}/apache.example.conf"
  install -d "${pkgdir}/usr/lib/${pkgname}"
  mv "${pkgdir}/usr/share/webapps/${pkgname}/cgit.cgi" "${pkgdir}/usr/lib/${pkgname}"
  ln -sf ../../../lib/${pkgname}/cgit.cgi "${pkgdir}/usr/share/webapps/${pkgname}/cgit.cgi"
}
