# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gtk-theme-elementary
pkgver=8.0.0
pkgrel=1
pkgdesc='elementary GTK theme'
arch=(any)
url=https://github.com/elementary/stylesheet
license=(GPL3)
groups=(pantheon)
makedepends=(
  git
  meson
  sassc
)
optdepends=('gtk-engine-murrine: GTK2 theme')
_tag=9c713d0f4c35f80a783e5fa6845a39d977c7140c
source=(gtk-theme-elementary::git+https://github.com/elementary/stylesheet.git#tag=${_tag})
sha256sums=('98b6ae60b2279ff6af268d8d6d4301627643bb91066ffeab6170825ac1dee039')

pkgver() {
  cd gtk-theme-elementary
  git describe --tags
}

build() {
  arch-meson gtk-theme-elementary build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
