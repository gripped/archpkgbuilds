# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gtk-theme-elementary
pkgver=8.2.1
pkgrel=1
pkgdesc='elementary GTK theme'
arch=(any)
url=https://github.com/elementary/stylesheet
license=(GPL3)
groups=(pantheon)
makedepends=(
  git
  meson
  sassc
)
optdepends=('gtk-engine-murrine: GTK2 theme')
_tag=5bd252f584929be05e3a0cef23aba4d006c3c133
source=(gtk-theme-elementary::git+https://github.com/elementary/stylesheet.git#tag=${_tag})
sha256sums=('490e79eb9f2cd45d09bb1a06ed4fc6ac5b140f55b6ad2503512c76b8c422f789')

pkgver() {
  cd gtk-theme-elementary
  git describe --tags
}

build() {
  arch-meson gtk-theme-elementary build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
