# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=python-kubernetes-validate
_name="${pkgname#python-}"
pkgver=1.31.0
pkgrel=2
license=(Apache-2.0)
pkgdesc="Validate Kubernetes resource definitions against the declared Kubernetes schemas"
arch=(any)
url="https://github.com/willthames/kubernetes-validate"
depends=(
  python
  python-importlib-resources
  python-jsonschema
  python-packaging
  python-referencing
  python-typing_extensions
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(
  python-pytest
)
source=("$_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('64a450e7c54b4a6c1a5cfba9db0dd3cd95a3f335959ffa3e2425fb74c0b8e1045ed07716e9f25a130a8a6664099125c4c939042e48b89f4d6ab104d3f66e58af')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
