# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=python-kubernetes-validate
_name="${pkgname#python-}"
pkgver=1.34.1
pkgrel=2
license=(Apache-2.0)
pkgdesc="Validate Kubernetes resource definitions against the declared Kubernetes schemas"
arch=(any)
url="https://github.com/willthames/kubernetes-validate"
depends=(
  python
  python-importlib-resources
  python-jsonschema
  python-packaging
  python-referencing
  python-typing_extensions
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(
  python-pytest
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('5c3d9a56c52374acc65b7318e9c88673683ad96d9a3b7796a82567297c0c1feb646cd54ff965d2ded9dc54a9c5e8134bd01317cbb0a238518f39fbf5a3581992')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
