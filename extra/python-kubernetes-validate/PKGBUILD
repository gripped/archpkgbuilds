# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=python-kubernetes-validate
_name="${pkgname#python-}"
pkgver=1.33.0
pkgrel=1
license=(Apache-2.0)
pkgdesc="Validate Kubernetes resource definitions against the declared Kubernetes schemas"
arch=(any)
url="https://github.com/willthames/kubernetes-validate"
depends=(
  python
  python-importlib-resources
  python-jsonschema
  python-packaging
  python-referencing
  python-typing_extensions
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(
  python-pytest
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('d1cff3743c4a029a46d2449d3d83c32554642e16a626fd18301b89295def8ef8fa12fdadeb794eb339818b761c7ef62140e1452d97d40581faaacc4743dd917f')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
