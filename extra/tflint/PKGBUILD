# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Gabriel M. Dutra <nulldutra@proton.me>

pkgname=tflint
pkgver=0.57.0
pkgrel=1
pkgdesc="A linter for Terraform code"
arch=('x86_64')
url="https://github.com/terraform-linters/tflint"
license=('MPL-2.0')
depends=(
  glibc
)
makedepends=(
  git
  go
)
options=(!lto)
source=(
  "${pkgname}-${pkgver}::git+${url}#tag=v${pkgver}"
  "git+https://github.com/hashicorp/terraform-aws-consul.git"
  "git+https://github.com/terraform-aws-modules/terraform-aws-ec2-instance.git"
)
sha512sums=('fdadac0709857e2895cacd2ece349e6416941a93b2d4c462692ee6a80d622997e3a7d1a03b4391741fc4e3c503b2a6d90be90b15b36cbf0c57c1481c90c4ce94'
            'SKIP'
            'SKIP')
b2sums=('808d8c4204b4551ae4961a7547a2e9faa4fb57b0c997a3df18bdd28c6b09036056058d35f7e268e1d191a0e2e697e3484bd8a51d5cbdcf4410a4f8bfff87900d'
        'SKIP'
        'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"

  git submodule init
  git config submodule.terraform/test-fixtures/v0.15.0_module/.terraform/modules/consul.url "${srcdir}/terraform-aws-consul"
  git config submodule.integrationtest/inspection/with_module_init/.terraform/modules/remote.url "${srcdir}/terraform-aws-ec2-instance"
  git -c protocol.file.allow=always submodule update

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
  "

  go build -ldflags "$ld_flags" -v -o "./out/${pkgname}"
}

check() {
  cd "${pkgname}-${pkgver}"

  export PATH="$(pwd)/out:$PATH"

  go run ./plugin/stub-generator
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install --verbose -D --mode=0755 "out/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
