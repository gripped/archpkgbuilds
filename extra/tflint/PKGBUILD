# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Gabriel M. Dutra <nulldutra@proton.me>

pkgname=tflint
pkgver=0.58.1
pkgrel=1
pkgdesc="A linter for Terraform code"
arch=('x86_64')
url="https://github.com/terraform-linters/tflint"
license=('MPL-2.0')
depends=(
  glibc
)
makedepends=(
  git
  go
)
options=(!lto)
source=(
  "${pkgname}-${pkgver}::git+${url}#tag=v${pkgver}"
  "git+https://github.com/hashicorp/terraform-aws-consul.git"
  "git+https://github.com/terraform-aws-modules/terraform-aws-ec2-instance.git"
)
sha512sums=('90d29de529cb16d357cce2abf6fc75ba431cf3cca716d1c1bcc70e5893cfad99e5bff5251b4b7721563b316c427aefc88d62bc287130e6981f87c41d3325df1a'
            'SKIP'
            'SKIP')
b2sums=('db8ce3befbac8e3ffadcbc66d68fa5499002d59f16085013f75df198c5767803129c880e3045aeb62c7383772053f655d8f6d74a515cf7b29bb20a0bef5c6442'
        'SKIP'
        'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"

  git submodule init
  git config submodule.terraform/test-fixtures/v0.15.0_module/.terraform/modules/consul.url "${srcdir}/terraform-aws-consul"
  git config submodule.integrationtest/inspection/with_module_init/.terraform/modules/remote.url "${srcdir}/terraform-aws-ec2-instance"
  git -c protocol.file.allow=always submodule update

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
  "

  go build -ldflags "$ld_flags" -v -o "./out/${pkgname}"
}

check() {
  cd "${pkgname}-${pkgver}"

  export PATH="$(pwd)/out:$PATH"

  go run ./plugin/stub-generator
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install --verbose -D --mode=0755 "out/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
