# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Gabriel M. Dutra <nulldutra@proton.me>

pkgname=tflint
pkgver=0.55.1
pkgrel=1
pkgdesc="A linter for Terraform code"
arch=('x86_64')
url="https://github.com/terraform-linters/tflint"
license=('MPL-2.0')
depends=(
  glibc
)
makedepends=(
  git
  go
)
options=(!lto)
source=(
  "${pkgname}-${pkgver}::git+${url}#tag=v${pkgver}"
  "git+https://github.com/hashicorp/terraform-aws-consul.git"
  "git+https://github.com/terraform-aws-modules/terraform-aws-ec2-instance.git"
)
sha512sums=('342a7fa5f2d01ad7b1fc4645d41c2bdcfc348012f0f2cda0a76b9add1cab825d79fe34c507c97602114276e6e3130a0d875340a1673d767bc9faf38eda0353e6'
            'SKIP'
            'SKIP')
b2sums=('bd50a2db9f89cc8789567f6eb49cc4321a3432ad0f3fd57ea2771decf5dcb81a891fc08d615145fd640eeefee9c273e23c4b4cdca588303a488bee9ee4c66fdf'
        'SKIP'
        'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"

  git submodule init
  git config submodule.terraform/test-fixtures/v0.15.0_module/.terraform/modules/consul.url "${srcdir}/terraform-aws-consul"
  git config submodule.integrationtest/inspection/with_module_init/.terraform/modules/remote.url "${srcdir}/terraform-aws-ec2-instance"
  git -c protocol.file.allow=always submodule update

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
  "

  go build -ldflags "$ld_flags" -v -o "./out/${pkgname}"
}

check() {
  cd "${pkgname}-${pkgver}"

  export PATH="$(pwd)/out:$PATH"

  go run ./plugin/stub-generator
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install --verbose -D --mode=0755 "out/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
