# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-scummvm
pkgver=161052
pkgrel=1
pkgdesc='ScummVM core'
arch=(x86_64)
url=https://github.com/libretro/scummvm
license=(GPL)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=8e9d265d81661dcffe0bc326e07e50af5d1d224a
source=(libretro-scummvm::git+https://github.com/libretro/scummvm.git#commit=${_commit})
sha256sums=('e71fda6563195864c5413140501c3ccfb8a96418edb4b80e48f5eaa335d6efc0')

pkgver() {
  cd libretro-scummvm

  git rev-list --count HEAD
}

build() {
  make -C libretro-scummvm/backends/platform/libretro
}

package() {
  install -Dm 644 libretro-scummvm/backends/platform/libretro/scummvm_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
