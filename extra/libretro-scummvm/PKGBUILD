# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-scummvm
pkgver=156045
pkgrel=1
pkgdesc='ScummVM core'
arch=(x86_64)
url=https://github.com/libretro/scummvm
license=(GPL)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=6363f1d3d35e0364f0e041cba8f413b5a3c7e47f
source=(libretro-scummvm::git+https://github.com/libretro/scummvm.git#commit=${_commit})
sha256sums=('7efa6d730517ca10d2ae4b7eff00e9a79a447c7f6f942ee8a4ed76b837f9d569')

pkgver() {
  cd libretro-scummvm

  git rev-list --count HEAD
}

build() {
  make -C libretro-scummvm/backends/platform/libretro
}

package() {
  install -Dm 644 libretro-scummvm/backends/platform/libretro/scummvm_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
