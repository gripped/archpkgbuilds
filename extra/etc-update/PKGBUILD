# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Kewl <xrjy@nygb.rh.bet(rot13)>
# Contributor: Adam Nielsen <malvineous@shikadi.net>

pkgname='etc-update'
pkgdesc="CLI to interactively merge .pacnew configuration files"
pkgver=3.0.67
pkgrel=1
arch=('any')
url="https://wiki.gentoo.org/wiki/Handbook:X86/Portage/Tools#etc-update"
license=('GPL')
depends=('bash')
makedepends=('git')
optdepends=('dialog: for the menu mode')
backup=("etc/etc-update.conf")
source=("https://github.com/gentoo/portage/archive/portage-${pkgver}.tar.gz")
sha512sums=('7b75286826e0b13c02d7bdac6e00a904b2127f0406d3bc6c4a4ee97668a13d44315f471606bbfbf6dd2704b161bf346105bb0cc3614dd346a6c7673ede38477e')
b2sums=('6d67f2a465649bd114af06b65e9f343a79d2a6bb71b6bbe46b594a82d84af2a5594cca1d4287ea56889f7fa114879dadb39396d2baa1e345fc973c9facf63aa6')

package() {
	cd portage-portage-${pkgver}
	install -vDm755 bin/${pkgname} -t "${pkgdir}/usr/bin"
	install -vDm644 cnf/${pkgname}.conf -t "${pkgdir}/etc"
	install -vDm644 man/${pkgname}.1 -t "${pkgdir}/usr/share/man/man1"
}
