# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=repro-env
pkgver=0.4.2
pkgrel=1
pkgdesc="Dependency lockfiles for reproducible build environments"
url='https://github.com/kpcyrd/repro-env'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'catatonit'
  'gcc-libs'
  'glibc'
  'podman'
)
makedepends=(
  'cargo'
  'scdoc'
)
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")
sha256sums=('a16a5ae1a01659c77251d5b67f46b5897269e86dcbf39762fd89570c517f831c'
            'SKIP')
b2sums=('f926ed4aed9d984a0782295ae1216bdacb7ffc5912299856a08eb8b0f30b199d3a06b099feb39ced35a86d04a9c9e9190fb999d10362647e0b9a9e68cc162f05'
        'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
  make docs
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/repro-env --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/repro-env

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/repro-env.1

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
