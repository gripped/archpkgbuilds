# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=repro-env
pkgver=0.4.3
pkgrel=2
pkgdesc="Dependency lockfiles for reproducible build environments"
url='https://github.com/kpcyrd/repro-env'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'catatonit'
  'gcc-libs'
  'glibc'
  'podman'
)
makedepends=(
  'cargo'
  'scdoc'
)
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")
sha256sums=('4ad24bccc0d042fe3a543a0099881b7edfb85efd4c7ae55a572c1286b3584f3d'
            'SKIP')
b2sums=('ecfbff737512c6e84c1d34376ddb19b13ca7d152f2462eb287431c13e87878b28e9ffac72749ecc31f90854d0f1b47375f0108ca58f6bfba8fcd73a30a31d8cc'
        'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
  make docs
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/repro-env --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/repro-env

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/repro-env.1

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
