# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=repro-env
pkgver=0.2.0
pkgrel=1
pkgdesc="Dependency lockfiles for a reproducible build environment"
url='https://github.com/kpcyrd/repro-env'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('podman' 'shared-mime-info')
makedepends=('cargo')
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")
sha256sums=('ca70ebbbd830c451656824843a023ce16743364422a5a9dc6140cb8d25570b72'
            'SKIP')
b2sums=('134b86172ac8799ec3707cb9f4f68acbb13335fbba17118b499ebd8e3cb64e4cbcc030c459b3ba7dd5a6d57a9748e8505713944655020420c9bb8e06843090db'
        'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/repro-env

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
