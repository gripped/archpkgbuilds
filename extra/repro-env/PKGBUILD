# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=repro-env
pkgver=0.3.3
pkgrel=1
pkgdesc="Dependency lockfiles for reproducible build environments"
url='https://github.com/kpcyrd/repro-env'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('podman' 'catatonit')
makedepends=('cargo' 'scdoc')
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")
sha256sums=('43e7b4bd711152610a068c7399e993a9fc6d706130044cf1745d90aaacdbb960'
            'SKIP')
b2sums=('2e7371fb54b75b1718bdc8452bafbf795ab1b7712d55a566d82f4de9ef1e6ab4790bc3006b54ef37eaf911ea5b79f24b2635091f2717937f56970a63462480d0'
        'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
  make docs
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/repro-env --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/repro-env

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/repro-env.1

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
