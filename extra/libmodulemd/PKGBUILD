# Contributor: larchunix
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=libmodulemd
pkgver=2.15.2
pkgrel=2
pkgdesc="C Library for manipulating module metadata files"
arch=('x86_64')
url="https://github.com/fedora-modularity/$pkgname"
license=('MIT')
depends=('file' 'glib2' 'libyaml' 'rpm-tools')
makedepends=('glib2-devel' 'gobject-introspection' 'help2man' 'meson' 'python-gobject')
optdepends=('python-gobject: for python bindings')
source=("$url/releases/download/$pkgver/${pkgname#lib}-$pkgver.tar.xz")
sha256sums=('6fb926e270ba44d1981d1abadaa6728c5e357636eee3b3bb533e95b92d104970')

build() {
  cd ${pkgname#lib}-$pkgver
  arch-meson build -Dwith_docs=False
  ninja -C build
}

check() {
  cd ${pkgname#lib}-$pkgver
  ninja -C build test
}

package() {
  cd ${pkgname#lib}-$pkgver
  DESTDIR="$pkgdir/" ninja -C build install

  install -Dp -m644 COPYING   "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dp -m644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
