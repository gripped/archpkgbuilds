# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2024.06.21
pkgrel=2
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
makedepends=('git')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-icon-theme.git#tag=$pkgver")
sha512sums=('8d991bd8b4c88c21a150534d48d337d1b46b1c4be26d7af8ecb4d85e6f4c000b3c40f4ce89145c9c4f10a06430748b9db620df6d1416a1d961584e5cfdac1a8e')

build() {
  cd $pkgname
  make hicolor-links
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/

  # https://gitlab.archlinux.org/archlinux/packaging/packages/deepin-icon-theme/-/issues/1
  rm "$pkgdir"/usr/share/icons/bloom/icon-theme.cache
}
