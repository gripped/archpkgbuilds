# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2024.07.10
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
makedepends=('git')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-icon-theme.git#tag=$pkgver")
sha512sums=('a0bc08dfe86db9f11e457edf8fff179c3fb9c8ea67ff9db2c0cb418787526ce114a928268fbd38b4474db8d3b2c6038e0c81599b563e077f436ab733d5cb3ab5')

build() {
  cd $pkgname
  make hicolor-links
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/

  # https://gitlab.archlinux.org/archlinux/packaging/packages/deepin-icon-theme/-/issues/1
  rm "$pkgdir"/usr/share/icons/bloom/icon-theme.cache
}
