# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2023.12.13
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('cef8335e25e7def3e409d493d0596e0cfbc6ee4beb13a023107ed9916dd54f8100d900cb3496dc0be2521efea21f0513c5a7e282d7f338f94c414d90030d94d9')

build() {
  cd $pkgname-$pkgver
  make hicolor-links
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
}
