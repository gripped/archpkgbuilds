# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2024.07.02
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
makedepends=('git')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-icon-theme.git#tag=$pkgver")
sha512sums=('0cc274d3edef4af857548f5178b00e368d1399ac1e684c04542eaba414f4f406889f328516a7af06e3ce0729d53d9f040df40b3bea02c274064793c029c1f0f3')

build() {
  cd $pkgname
  make hicolor-links
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/

  # https://gitlab.archlinux.org/archlinux/packaging/packages/deepin-icon-theme/-/issues/1
  rm "$pkgdir"/usr/share/icons/bloom/icon-theme.cache
}
