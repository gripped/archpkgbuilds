# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-graph
pkgver=0.7.0
pkgrel=1
pkgdesc='show clear git graphs arranged following different branching model'
arch=(x86_64)
url="https://github.com/mlange-42/$pkgname"
license=(MIT)
depends=(gcc-libs # libgcc_s.so
         glibc # libc.so
         libgit2 libgit2.so
         zlib libz.so)
makedepends=(cargo)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('105980d19b93324e27ee714abde35cb05b29e21d3c42cd972afe4d4500af05ee')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
	export LIBGIT2_NO_VENDOR=1
	cargo build --frozen --release
}

check() {
	cd "$_archive"
	cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
