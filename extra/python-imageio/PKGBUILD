# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=imageio
pkgname=python-$_name
pkgver=2.37.0
pkgrel=3
# test_images repo has no releases, the commit may need to be updated on pkgver bumps
_test_images_commit=f676c96b1af7e04bb1eed1e4551e058eb2f14acd
pkgdesc="Python library for reading and writing image data"
arch=(any)
url="https://github.com/imageio/imageio"
license=(BSD-2-Clause)
depends=(
  python
  python-numpy
  python-pillow
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
)
checkdepends=(
  freeimage
  python-fsspec
  python-pytest
)
optdepends=(
  'python-av: for the PyAV plugin'
  'python-opencv: for the OpenCV plugin'
  'python-tifffile: for the TIFF plugin'
  'python-rawpy: for the RawPy plugin'
  # the following are marked as legacy
  'freeimage: for the FreeImage plugin (legacy)'
  'python-astropy: for the FITS plugin (legacy)'
  'python-imageio-ffmpeg: for the FFMPEG plugin (legacy)'
  'python-gdal: for the GDAL plugin (legacy)'
  'python-simpleitk: for the ITK plugin (legacy)'
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
  $pkgname-test-images::git+https://github.com/imageio/test_images.git#commit=$_test_images_commit
)
b2sums=('6d66b9896cceca2279cec10968f1f71e0a9b387f0a3c43dcdf69a02cb037e3df2ea4e07c13d4f89dc5e61177d7a57fbed30c1cc8423977a1518c8b056ed8e87c'
        'b48440fbd50d12e0138a6c19676b57029b48905cd6e24ea310ce0ad309275eec6bbf8c58604d5854989d795aa9d4a4c420f586d56f9b313ffdef6dcfb5482aa6')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    # configure path for test images
    --test-images="$srcdir"/$pkgname-test-images
    # deselect tests that require sending requests to github
    --deselect tests/test_freeimage.py::test_get_fi_lib
    --deselect tests/test_freeimage.py::test_freeimage_format
    --deselect tests/test_freeimage.py::test_freeimage_lib
    --deselect tests/test_freeimage.py::test_png
    --deselect tests/test_freeimage.py::test_png_dtypes
    --deselect tests/test_freeimage.py::test_jpg
    --deselect tests/test_freeimage.py::test_jpg_more
    --deselect tests/test_freeimage.py::test_bmp
    --deselect tests/test_freeimage.py::test_gif
    --deselect tests/test_freeimage.py::test_animated_gif
    --deselect tests/test_freeimage.py::test_ico
    --deselect tests/test_freeimage.py::test_multi_icon_ico
    --deselect tests/test_freeimage.py::test_pnm
    --deselect tests/test_freeimage.py::test_gamma_correction
    --deselect tests/test_freeimage.py::test_improps
    --deselect tests/test_freeimage.py::test_exr_write
    # deselect tests for a legacy plugin which require the python-imageio-ffmpeg package
    --deselect tests/test_format.py::test_missing_format
    --deselect tests/test_format.py::test_preferring_arbitrary
    # ValueError: operands could not be broadcast together with shapes (256,256,4) (256,256)
    --deselect tests/test_pillow.py::test_boolean_reading
    # TypeError: numpy boolean subtract, the `-` operator, is not supported, use the bitwise_xor, the `^` operator, or the logical_xor function instead.
    --deselect tests/test_pillow.py::test_boolean_writing
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  # remove utilities for downloading binary dependencies
  rm "$pkgdir"/usr/bin/{imageio_download_bin,imageio_remove_bin}
  rmdir --ignore-fail-on-non-empty "$pkgdir"/usr/bin
}
