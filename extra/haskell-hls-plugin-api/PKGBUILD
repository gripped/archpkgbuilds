# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-plugin-api
pkgname=haskell-hls-plugin-api
pkgver=2.0.0.1
pkgrel=10
pkgdesc="Haskell Language Server API for plugin communication"
url="https://github.com/haskell/haskell-language-server/hls-plugin-api"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-aeson' 'haskell-data-default' 'haskell-dependent-map'
         'haskell-dependent-sum' 'haskell-dlist' 'haskell-extra' 'haskell-ghc' 'haskell-hashable'
         'haskell-hls-graph' 'haskell-hw-fingertree' 'haskell-lens' 'haskell-lens-aeson'
         'haskell-lsp' 'haskell-megaparsec' 'haskell-optparse-applicative' 'haskell-opentelemetry'
         'haskell-regex-tdfa' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-lsp-types' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-tasty-rerun')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('67ea73aa766b8e05988a21b7398be096ef9e4c4dc15b884fcfa3b2b74518963b921d73791c0f7345d1514f7f4416cb375c85d17f93495fbf49f004ad98a93b29')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
