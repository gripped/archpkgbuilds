# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-plugin-api
pkgname=haskell-hls-plugin-api
pkgver=2.1.0.0
pkgrel=42
pkgdesc="Haskell Language Server API for plugin communication"
url="https://github.com/haskell/haskell-language-server/hls-plugin-api"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-aeson' 'haskell-co-log-core' 'haskell-data-default'
         'haskell-dependent-map' 'haskell-dependent-sum' 'haskell-dlist' 'haskell-extra'
         'haskell-ghc' 'haskell-hashable' 'haskell-hls-graph' 'haskell-hw-fingertree' 'haskell-lens'
         'haskell-lens-aeson' 'haskell-lsp' 'haskell-megaparsec' 'haskell-optparse-applicative'
         'haskell-opentelemetry' 'haskell-prettyprinter' 'haskell-regex-tdfa' 'haskell-row-types'
         'haskell-unliftio' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-lsp-types' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-tasty-rerun')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('a00a440dabb05b8399100ffa75382aa8385a5e5395c599379f147168ad73902dcf350afc24f414285be5708d3fd61855b33e9e8034465327b4c94c91d9978f4b')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u Diff
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
