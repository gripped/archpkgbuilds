# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=ddm
pkgver=0.3.0
pkgrel=1
pkgdesc='New Display Manager for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/ddm"
license=('GPL-2.0-or-later')
groups=('deepin')
depends=(bash
         gcc-libs
         glibc
         libxau
         pam
         qt6-base
         qt6-declarative
         systemd-libs
         ttf-font
         xorg-server
         xorg-xauth)
makedepends=(git
             extra-cmake-modules
             python-docutils
             qt6-tools
             treeland-protocols
             ninja)
source=("git+https://github.com/linuxdeepin/ddm.git#tag=$pkgver")
sha512sums=('163c167276cbc64cd893534948890fb3e9311fc551ca44f3b13a12509337dadcd8942fdfff5468ca5dd06c027a0d1a498fb7fff263ecf0942767326b7f2efff5')

build() {
  cd ddm
  cmake . -GNinja \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build .
}

package() {
  cd ddm
  DESTDIR="$pkgdir" ninja install
}
