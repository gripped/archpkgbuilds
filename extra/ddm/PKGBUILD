# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=ddm
pkgver=0.2.2
pkgrel=1
pkgdesc='New Display Manager for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/ddm"
license=('GPL-2.0-or-later')
depends=(bash
         gcc-libs
         glibc
         libxau
         pam
         qt6-base
         qt6-declarative
         systemd-libs
         ttf-font
         xorg-server
         xorg-xauth)
makedepends=(git
             extra-cmake-modules
             python-docutils
             qt6-tools
             treeland-protocols
             ninja)
source=("git+https://github.com/linuxdeepin/ddm.git#tag=$pkgver")
sha512sums=('947ec7adf37f742a405b94d556dd326c8eb175e94e6359db72ef4e983e8e9d4d460b7e431ec26257b6ec42467e75f05ecba5b38f9f82df5ace62a5cbe709f9aa')

build() {
  cd ddm
  cmake . -GNinja \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build .
}

package() {
  cd ddm
  DESTDIR="$pkgdir" ninja install
}
