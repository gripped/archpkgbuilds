# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=ddm
pkgver=0.2.0
pkgrel=1
pkgdesc='New Display Manager for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/ddm"
license=('LGPL3')
depends=(bash
         gcc-libs
         glibc
         libxau
         pam
         qt6-base
         qt6-declarative
         systemd-libs
         ttf-font
         xorg-server
         xorg-xauth)
makedepends=(git
             extra-cmake-modules
             python-docutils
             qt6-tools
             treeland-protocols
             ninja)
source=("git+https://github.com/linuxdeepin/ddm.git#tag=$pkgver")
sha512sums=('174b0a83c01752d082b2e3850f6d3e6c5dbf2b97932976a17fab2c7f7da78f6504f56a586548bce69500da6024b71b9e4d6da615517f7254e2c7476cb9040701')

build() {
  cd ddm
  cmake . -GNinja \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build .
}

package() {
  cd ddm
  DESTDIR="$pkgdir" ninja install
}
