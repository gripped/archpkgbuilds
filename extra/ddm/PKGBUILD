# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=ddm
pkgver=0.2.3
pkgrel=2
pkgdesc='New Display Manager for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/ddm"
license=('GPL-2.0-or-later')
depends=(bash
         gcc-libs
         glibc
         libxau
         pam
         qt6-base
         qt6-declarative
         systemd-libs
         ttf-font
         xorg-server
         xorg-xauth)
makedepends=(git
             extra-cmake-modules
             python-docutils
             qt6-tools
             treeland-protocols
             ninja)
source=("git+https://github.com/linuxdeepin/ddm.git#tag=$pkgver")
sha512sums=('3bc4948f751136a85752ffe39de6ce1d00a04a7408d01f28cfe8bb7c93e2f3e680876b2e137c6e81d33047a6a2b3c589988104b9549024aa9efb893308c3f99f')

prepare() {
  cd ddm
  # fix seat errors on startup
  git cherry-pick -n c5d26af8f9bab50958a2725d22fb82c76992fcae
}

build() {
  cd ddm
  cmake . -GNinja \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build .
}

package() {
  cd ddm
  DESTDIR="$pkgdir" ninja install
}
