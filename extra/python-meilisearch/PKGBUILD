# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-meilisearch
pkgver=0.40.0
epoch=1
pkgrel=1
pkgdesc='Python client for MeiliSearch API'
arch=(any)
license=('MIT')
url='https://pypi.org/project/meilisearch/'
depends=(python python-requests python-camel-converter python-pydantic)
makedepends=(python-setuptools python-build python-installer python-wheel)
checkdepends=(python-pytest python-pytest-ordering)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/meilisearch/meilisearch-python/archive/v${pkgver}.tar.gz)
sha512sums=('3578cde3a583f3098b57d944b53afd7e9fa129dee1943afff1df9f17094d5f344cbcbf2c2374c7afb30916ea703b0593616704460fb13a39194a59c9641aae22')

build() {
  cd "meilisearch-python-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  # requires running meilisearch https://github.com/meilisearch/meilisearch-python/blob/master/.github/workflows/test.yml#L20
  cd "meilisearch-python-${pkgver}"
  #pytest .
}

package() {
  cd "meilisearch-python-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
