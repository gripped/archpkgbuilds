# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-meilisearch
pkgver=0.38.0
epoch=1
pkgrel=1
pkgdesc='Python client for MeiliSearch API'
arch=(any)
license=('MIT')
url='https://pypi.org/project/meilisearch/'
depends=(python python-requests python-camel-converter python-pydantic)
makedepends=(python-setuptools python-build python-installer python-wheel)
checkdepends=(python-pytest python-pytest-ordering)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/meilisearch/meilisearch-python/archive/v${pkgver}.tar.gz)
sha512sums=('dfd7906bc93e9ade767b7460537cd9927659935d4ef22cb23a3fd70513bdd8d3cad32652f992c58e60bf7ae77cd40146e9c8f823f5ec2e8c82dc72e14243c445')

build() {
  cd "meilisearch-python-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  # requires running meilisearch https://github.com/meilisearch/meilisearch-python/blob/master/.github/workflows/test.yml#L20
  cd "meilisearch-python-${pkgver}"
  #pytest .
}

package() {
  cd "meilisearch-python-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
