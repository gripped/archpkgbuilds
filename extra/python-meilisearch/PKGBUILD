# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-meilisearch
pkgver=0.31.5
epoch=1
pkgrel=2
pkgdesc='Python client for MeiliSearch API'
arch=(any)
license=('MIT')
url='https://pypi.org/project/meilisearch/'
depends=(python python-requests python-camel-converter python-pydantic)
makedepends=(python-setuptools python-build python-installer python-wheel)
checkdepends=(python-pytest python-pytest-ordering)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/meilisearch/meilisearch-python/archive/v${pkgver}.tar.gz)
sha512sums=('9f0ef6b52daebfeec359ee41030d23b7032b6e26913dfba26a14714e8b1223aa67e222b42b9ea67380ff7f98618a6efe393282ff8820531c891989306df35812')

build() {
  cd "meilisearch-python-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  # requires running meilisearch https://github.com/meilisearch/meilisearch-python/blob/master/.github/workflows/test.yml#L20
  cd "meilisearch-python-${pkgver}"
  #pytest .
}

package() {
  cd "meilisearch-python-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
