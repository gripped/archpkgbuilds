# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=cups-browsed
pkgver=2.0.1
pkgrel=2
pkgdesc="a helper daemon to browse the network for remote CUPS queues and IPP network printers"
arch=('x86_64')
url="https://wiki.linuxfoundation.org/openprinting/cups-filters"
license=('Apache-2.0 WITH LLVM-exception')
depends=('libcups' 'libcupsfilters' 'libppd'
          'avahi' 'glib2' 'glibc' 'libldap')
makedepends=('glib2-devel')
backup=(etc/cups/cups-browsed.conf)
source=(https://github.com/OpenPrinting/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz
        CVE-2024-47176.patch)
sha256sums=('ebed1b49dea593912b15fe55f88b691c1a0ac7d6c42178e9526ea949d00ef01a'
            'cef22036f4512dc813e6ea2536558c2111919134b93b23b6d3f239d3fad1fa4a')

prepare() {
  cd "$pkgname"-$pkgver
  # default to dnssd only browsing
  patch -Np1 -i ../CVE-2024-47176.patch
  autoreconf -vfi
}

build() {
  cd "$pkgname"-$pkgver
  ./configure --prefix=/usr  \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --with-rcdir=no \
    --enable-auto-setup-driverless-only
  make
}

check() {
  cd "$pkgname"-$pkgver
# "make check" hangs
#  make test - does nothing
}

package() {
  cd "$pkgname"-$pkgver
  make DESTDIR="$pkgdir/" install
  
  # add upstream systemd support file
  install -Dm644 daemon/cups-browsed.service "${pkgdir}"/usr/lib/systemd/system/cups-browsed.service
  sed -i "s|/usr/sbin/cups-browsed|/usr/bin/cups-browsed|" "${pkgdir}"/usr/lib/systemd/system/cups-browsed.service
  
  # use cups group from cups pkg FS#56818
  chgrp -R 209 "${pkgdir}"/etc/cups

  # license
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/{COPYING,NOTICE} "${pkgdir}"/usr/share/licenses/${pkgname}/
}
