# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=gi-pango
pkgname=haskell-gi-pango
pkgver=1.0.30
pkgrel=10
pkgdesc="Pango bindings"
url="https://github.com/haskell-gi/haskell-gi"
license=('LGPL-2.1-only')
arch=('x86_64')
depends=('ghc-libs' 'haskell-gi-gio' 'haskell-gi-glib' 'haskell-gi-gobject' 'haskell-gi-harfbuzz'
         'haskell-gi' 'haskell-gi-base' 'haskell-gi-overloading' 'pango')
makedepends=('ghc' 'gobject-introspection')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('37ed9bcbf3429714e7172b654443fcf81c7bbd42a4c8d155542ccc28ad95e3ce')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
