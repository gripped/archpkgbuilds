# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='jwt'
pkgname="ruby-${_gemname}"
pkgver=2.8.1
pkgrel=1
pkgdesc='A pure ruby implementation of the RFC 7519 OAuth JSON Web Token (JWT) standard'
arch=('any')
url="https://github.com/jwt/${pkgname}"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rspec' 'ruby-bundler')
checkdepends=('ruby-rubocop' 'ruby-simplecov')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz" 'drop-ci-dependencies.patch')
sha512sums=('df4ad49f223bba0ace69a37d229868c2deabed5c09fd84e5e4ce4021242cef9e30c07a592b5bf54af6bdcfec552487d808620cf020d242757fc4310d201469c2'
            'd161e49fd6d8c94e871039b6fa63dae35f828b1a04fb66fa4013f17f990307647b0f48b796f5c4d442d56d76bb91947d460036b7b981614a9c45924bc8f9ea45')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rm Gemfile
  patch -p1 -N -i "${srcdir}/drop-ci-dependencies.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gem build "${pkgname}.gemspec"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rake test --trace
}

package() {
cd "${srcdir}/${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md AUTHORS CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
