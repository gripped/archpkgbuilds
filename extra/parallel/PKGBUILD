# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20250722
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=('any')
url='https://www.gnu.org/software/parallel/'
license=('GPL-3.0-only')
makedepends=(
  'git'
  'perl'
  'libreoffice-fresh'
  'graphviz'
  'jdk-openjdk'
  'perl-pod2pdf'
  'perl-pod-pom-view-restructured'
  # pod2texi provided by texinfo in base-devel
)
source=(
  "$pkgname::git+https://git.savannah.gnu.org/git/parallel.git?signed#tag=${pkgver}"
  '0001-Remove-citation-things.patch'
)
sha512sums=('c4b7cb0f38936465a9b5a8b09e2c2e6bab5a7252b6896eeedb3fb61902c41fa4776fa9eddcef165997eebb6ecd028ca17438c5c247b0e73cba0d72aad138c24e'
            '4c88983ea0c9cc968c66e59e753c4b12f872a353e0b62f6355e867f0316ab0e88f4f77323cddd4a8076e4439c0f1127508c6f658606cf0f54037f25748f74021')
b2sums=('0b8ef51ce6728e110b89ed925d1aa90c1efdd6de8408aa211b82fb5ea01c91aa12392693c4ab125fe19a259bfeab4f8339eaf4f2e0e91f8671d392454621d049'
        '15ce72777128b438b8acd19e5094f9c2ea8525d4f3df588ae5b92d59f493248fb0895392dee6f863d19d39b51a2d81aad2f03a6a83418bf11c0f47975c815ac7')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"

  autoreconf -fiv
}

build() {
  cd "$pkgbase"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}
# vim:set ts=2 sw=2 et:
