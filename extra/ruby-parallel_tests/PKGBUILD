# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='parallel_tests'
pkgname="ruby-${_gemname}"
pkgver=5.6.0
pkgrel=1
pkgdesc='Run Test::Unit / RSpec / Cucumber / Spinach in parallel'
arch=('any')
url="https://github.com/grosser/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-parallel
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('92c1cd0bf8e603d4bf2e002a34448b8c1cf63940541052e7e427827dfcaf570e36c3f663a23ff3d9556a9568e30c22b5a57aee39f12ec379bef64ee604ae3960')
b2sums=('4431ebbeca076c643357d5b49683c12bc415480ee5e2c29d84e9769bc86134a3d2c93a07b07c06a64f0db5e4c607f3c790049d7c94b54dd2b58f2f91cb6518e8')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

# https://github.com/grosser/spinach is not packaged yet
# check() {
#   cd "${_gemname}-${pkgver}"
#
#   local _gemdir="$(gem env gemdir)"
#
#   GEM_HOME="tmp_install${_gemdir}" rake test
# }

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 Readme.md --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
