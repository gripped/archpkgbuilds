# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='parallel_tests'
pkgname="ruby-${_gemname}"
pkgver=5.5.0
pkgrel=1
pkgdesc='Run Test::Unit / RSpec / Cucumber / Spinach in parallel'
arch=('any')
url="https://github.com/grosser/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-parallel
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('88931ef3fb0151b15a6b3625ebc7189eb3c40767676a635be3e1f803dd35a3359ace332c2735e70d3ea1444612414561f4adc6d78576892fcaf95054ee5ad692')
b2sums=('4c56efb1ff9affcc17a90f7d10b3e56e828b11f679f99dec3213dac6d398cf25bfc62602110a6c120376e5842ddc6b27592934718d4a2fd3ec820296147e31be')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

# https://github.com/grosser/spinach is not packaged yet
# check() {
#   cd "${_gemname}-${pkgver}"
#
#   local _gemdir="$(gem env gemdir)"
#
#   GEM_HOME="tmp_install${_gemdir}" rake test
# }

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 Readme.md --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
