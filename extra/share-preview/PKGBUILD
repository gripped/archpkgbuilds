# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=share-preview
pkgver=0.5.0
pkgrel=2
pkgdesc='Preview and debug websites metadata tags for social media share'
arch=(x86_64)
url='https://apps.gnome.org/SharePreview/'
license=(GPL-3.0-or-later)
depends=(
  curl
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://github.com/rafaelmardojai/share-preview#tag=$pkgver")
b2sums=(1da089664a3716622531fb8e1847f3f025f7a698c9980ecbb5ad8fcae6e54d4ef487d59c0cb424c16c301904167bcb2dc9968aa40009edf032a7a2054723c882)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
