# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rstcheck
pkgver=6.2.4
pkgrel=1
pkgdesc='Checks syntax of reStructuredText and code blocks nested within it'
arch=(any)
url=https://github.com/rstcheck/rstcheck
license=(MIT)
depends=(
  python-rstcheck-core
  python-typer
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-sphinx
  python-pytest
)
optdepends=('python-sphinx: for Sphinx support')
source=("git+$url.git#tag=v$pkgver")
b2sums=('f42683cb93b9ec5034eabd0a9c832aa3cd7fc2244107db6cbc2a3273a375276d5ea8fa5ce6dc471d299a0aaa0f9632556c2eaeb572eb26429c417a7b74987563')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
