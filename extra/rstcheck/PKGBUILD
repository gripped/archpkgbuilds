# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rstcheck
pkgver=6.2.5
pkgrel=2
pkgdesc='Checks syntax of reStructuredText and code blocks nested within it'
arch=(any)
url=https://github.com/rstcheck/rstcheck
license=(MIT)
depends=(
  python-rstcheck-core
  python-typer
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-sphinx
  python-pytest
)
optdepends=('python-sphinx: for Sphinx support')
source=("git+$url.git#tag=v$pkgver")
b2sums=('59cd16429002021b9f711d314b86734b00a636aa012456cbe956ddbe88666e3997e3ffe8a097448c1a6416144485c791b0677e325c4ca60a6dc1c6227d3d051a')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/licenses/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
