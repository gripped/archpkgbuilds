# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: John K. Luebs <jkluebs@luebsphoto.com>

pkgname=xiccd
pkgver=0.3.0
pkgrel=2
pkgdesc="X color profile daemon"
url="https://github.com/agalakhov/xiccd"
arch=('x86_64')
license=('GPL-3.0-only')
depends=('colord' 'libxrandr')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('94dbe352ad3043079fa5edd8150318ec88f1dc87b75f69b1fced8ce2981c36a9')

prepare() {
	cd "${pkgname}-${pkgver}"
	aclocal
	autoconf
	automake --add-missing --foreign --force --copy
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
