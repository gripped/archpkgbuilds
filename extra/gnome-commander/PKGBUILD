# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=gnome-commander
pkgver=1.18.1
pkgrel=3
pkgdesc='Graphical two-pane filemanager for GNOME'
arch=(x86_64)
url='https://gcmd.github.io/'
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  exiv2
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libgsf
  pango
  poppler-glib
  taglib
)
makedepends=(
  appstream
  git
  glib2-devel
  gtest
  itstool
  meson
  smbclient
)
checkdepends=(xorg-server-xvfb)
source=("git+https://gitlab.gnome.org/GNOME/gnome-commander.git?signed#tag=$pkgver")
b2sums=(573070af651d6acb9b5d7a12987228222f6ec36f1a11e6a755f69fd1c5a30cc5dc15f6c91093d4368515060cc13ef4c2a5e65424ba8372e608b405836539875e)
validpgpkeys=(7627E6FCECF4FC066A03B673F50A89A3ADE4C6E8) # Uwe Scholz <u.scholz83@gmx.de>

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
