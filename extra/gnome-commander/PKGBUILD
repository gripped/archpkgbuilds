# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=gnome-commander
pkgver=1.18.3
pkgrel=1
pkgdesc='Graphical two-pane filemanager for GNOME'
arch=(x86_64)
url='https://gcmd.github.io/'
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  exiv2
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libgsf
  pango
  poppler-glib
  taglib
)
makedepends=(
  appstream
  git
  glib2-devel
  gtest
  itstool
  meson
  smbclient
)
source=("git+https://gitlab.gnome.org/GNOME/gnome-commander.git?signed#tag=$pkgver")
b2sums=(665c5faef5d0cdd2b73952dcf5e45179a22316ad5cb5f14ee9c6fe72b011d04da265f63896731ed36e275df1cddca242e0da2fcc747aad37250a51d31df590a1)
validpgpkeys=(7627E6FCECF4FC066A03B673F50A89A3ADE4C6E8) # Uwe Scholz <u.scholz83@gmx.de>

prepare() {
  cd $pkgname

  # Don't check for pixbufloader-xpm; we build gdk-pixbuf2 with xpm support
  git revert -n 9077d4423f0ca0b02fc6000b8ff95d7fac1d4b74
}

build() {
  arch-meson $pkgname build \
    -D tests=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
