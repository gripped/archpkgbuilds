# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-pydyf
pkgver=0.12.0
pkgrel=1
pkgdesc='A low-level PDF generator'
arch=('any')
url='https://www.courtbouillon.org/pydyf'
license=('BSD')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-flit-core'
  'python-installer'
)
checkdepends=(
  'python-pytest'
  'python-pillow'
  'ghostscript'
)
source=("$pkgname::git+https://github.com/CourtBouillon/pydyf.git#tag=v$pkgver")
sha256sums=('6c63fcad7bac94ea554eeb9e069f728674e6d0b32916ca433781fb749ec36077')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
