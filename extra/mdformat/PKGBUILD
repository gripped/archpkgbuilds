# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>

pkgname=mdformat
pkgver=0.7.22
pkgrel=1
pkgdesc='CommonMark compliant Markdown formatter'
arch=(any)
url=https://github.com/hukkin/mdformat
license=(MIT)
depends=(python-markdown-it-py)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("git+$url.git#tag=$pkgver")
b2sums=('1aae1bfd5cffe6246be9e26a24e6419c3ed88d27647143540b0f0c312829a0b6e1450cb5c0d2df577d7bb9ab5c686a9a126485cd11747c470cb4888eac722d3c')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  PYTHONPATH=build/lib pytest
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
