# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>

pkgname=mdformat
pkgver=0.7.18
pkgrel=1
pkgdesc='CommonMark compliant Markdown formatter'
arch=(any)
url=https://github.com/executablebooks/mdformat
license=(MIT)
depends=(python-markdown-it-py)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("git+$url.git#tag=$pkgver")
b2sums=('2f597ec2c1eca54cf43379928de8c0deef88ad7c7154841dfb9ca34869b7fe23676d5c0478194df92a4c969ac5f1c7006346606b441437c023a8ce2676597605')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  PYTHONPATH=build/lib pytest
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
