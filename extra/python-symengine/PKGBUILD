# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Spies <lukas (at) photoqt _dot_ org>

pkgname=python-symengine
_pkgname=symengine.py
pkgver=0.11.0
pkgrel=3
arch=(x86_64)
pkgdesc='Python wrappers for SymEngine'
url='https://github.com/symengine/symengine.py'
depends=(gcc-libs
         glibc
         gmp
         gperftools
         libmpc
         mpfr
         python
         symengine)
makedepends=(boost
             cereal
             cmake
             cython
             git
             llvm
             python-setuptools)
license=(MIT)
source=(git+https://github.com/symengine/symengine.py#tag=v$pkgver)
sha256sums=('d834bba84f330e7982ea30893acf95a9c5bab7e10084964740e48b1bc2207532')

prepare() {
  cd $_pkgname
# Fix build with symengine 0.13
  git cherry-pick -n dd3dc6efc228c9193088e3b284fa88faf920b393
}

build() {
  cmake -B build -S $_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $_pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
