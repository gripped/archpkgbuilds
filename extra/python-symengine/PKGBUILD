# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Spies <lukas (at) photoqt _dot_ org>

pkgname=python-symengine
_pkgname=symengine.py
pkgver=0.13.0
pkgrel=4
arch=(x86_64)
pkgdesc='Python wrappers for SymEngine'
url='https://github.com/symengine/symengine.py'
depends=(gcc-libs
         glibc
         gmp
         gperftools
         libmpc
         mpfr
         python
         symengine)
makedepends=(boost
             cereal
             cmake
             cython
             git
             llvm
             python-build
             python-installer
             python-setuptools)
license=(MIT)
source=(git+https://github.com/symengine/symengine.py#tag=v$pkgver
        no-tcmalloc.patch)
sha256sums=('56c6c675d85c2b8448b4eb1e21c46b7dbc5d2fa3dcd26921a37a5ba64d95b821'
            '4f733b108ec21897b55b69476961d178f3bb02cc54c8b86e440b16ef7370927a')

prepare() {
  cd $_pkgname
# https://github.com/symengine/symengine.py/issues/475
  patch -p1 -i ../no-tcmalloc.patch
}

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
