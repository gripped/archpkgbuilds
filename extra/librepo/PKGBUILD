# Contributor: larchunix
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=librepo
pkgver=1.20.0
pkgrel=1
pkgdesc="Repodata downloading library"
arch=('x86_64')
url="https://github.com/rpm-software-management/$pkgname"
license=('GPL')
depends=('curl' 'glib2' 'gpgme' 'libxml2' 'openssl')
makedepends=('cmake' 'python')
checkdepends=('check' 'python-gpgme' 'python-pyxattr' 'python-requests')
optdepends=('python: for python bindings')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c21dd3caefe97ea58bc865f92095a9d2db2ec8aab49d0c714d4742094db930b6')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"

  cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_C_FLAGS_RELEASE='-DNDEBUG' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DENABLE_DOCS=OFF \
        -DENABLE_PYTHON=ON \
        -DWITH_ZCHUNK=OFF \
	-DENABLE_SELINUX=off

  make -C build
}

check() {
  cd "$pkgname-$pkgver"

  make -C build ARGS="-V" test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install

  install -Dp -m644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
