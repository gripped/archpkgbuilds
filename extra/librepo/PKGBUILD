# Contributor: larchunix
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=librepo
pkgver=1.19.0
pkgrel=3
pkgdesc="Repodata downloading library"
arch=('x86_64')
url="https://github.com/rpm-software-management/$pkgname"
license=('GPL')
depends=('curl' 'glib2' 'gpgme' 'libxml2' 'openssl')
makedepends=('cmake' 'python')
checkdepends=('check' 'python-gpgme' 'python-pyxattr' 'python-requests')
optdepends=('python: for python bindings')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz" "gpgme-config.patch")
sha256sums=('7395b34af0d7a3fa13d2db1ad0ca0d8cbcae6695c2ecd62c6204606184bfd92b'
            'c0d08e34a0a02613036d1113fd0257a97e1896284afde7f8417f49abb34b2278')

prepare() {
  cd "$pkgname-$pkgver"
  # https://github.com/rpm-software-management/librepo/issues/269
  patch -Np1 -i ${srcdir}/gpgme-config.patch
}

build() {
  cd "$pkgname-$pkgver"

  cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_C_FLAGS_RELEASE='-DNDEBUG' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DENABLE_DOCS=OFF \
        -DENABLE_PYTHON=ON \
        -DWITH_ZCHUNK=OFF \
	-DENABLE_SELINUX=off

  make -C build
}

check() {
  cd "$pkgname-$pkgver"

  make -C build ARGS="-V" test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install

  install -Dp -m644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
