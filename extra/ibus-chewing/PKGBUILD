# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
_pkgver=2.1.6
pkgver=${_pkgver/-rc./rc}
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('x86_64')
# "GPLv2+" in https://github.com/chewing/ibus-chewing/blob/1.6.2/CMakeLists.txt#L90
license=('GPL-2.0-or-later')
url='https://github.com/chewing/ibus-chewing'
depends=('ibus' 'libchewing>=0.9.0' 'gtk4' 'libadwaita')
makedepends=('cmake' 'minisign' 'glib2-devel')
checkdepends=('dbus' 'xorg-server-xvfb')
source=("https://github.com/chewing/ibus-chewing/releases/download/v$_pkgver/$pkgname-$_pkgver-Source.tar.xz"{,.minisig})
sha512sums=('b8820588309401832ab90a53a8bda2921fc55faa8e99848103b95f4c8f9a7c21a5ee7c506b14a3e3d77317029aae9610b12a63801c3dc844f1345188fbd18272'
            '3d8bbc246d4fe88d76c39b5d4d8b701288eaa02f944188178f3f37e259a4d7c8606e2a9208f20c49a4143f2f0aa6f0cc37e6065189dadcfa8742b7114ea69ebc')
# The key is mentioned on https://github.com/chewing/ibus-chewing
_validminisignkey='RWRzJFnXiLZleAyCIv1talBjyRewelcy9gzYQq9pd3SKSFBPoy57sf5s'

# XXX: move to verify() when devtools supports it
# https://gitlab.archlinux.org/archlinux/devtools/-/issues/224
prepare() {
  minisign -Vm $pkgname-$_pkgver-Source.tar.xz -P $_validminisignkey
}

build() {
  cd $pkgname-$_pkgver-Source

  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/ibus

  make -C build
}

check() {
  cd $pkgname-$_pkgver-Source/build

  # glib-compile-schemas is needed for tests
  # https://github.com/chewing/ibus-chewing/blob/v2.0.0/.github/workflows/ci.yml#L33
  glib-compile-schemas ../src/setup --targetdir "$srcdir/$pkgname-$_pkgver-Source/build/bin"
  GSETTINGS_SCHEMA_DIR="$srcdir/$_pkgname/build/bin/" xvfb-run --auto-display dbus-run-session make test
}

package() {
  cd $pkgname-$_pkgver-Source/build
  make DESTDIR="$pkgdir" install
}
