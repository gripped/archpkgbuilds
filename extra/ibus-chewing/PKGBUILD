# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
_pkgver=2.1.7
pkgver=${_pkgver/-rc./rc}
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('x86_64')
# "GPLv2+" in https://github.com/chewing/ibus-chewing/blob/1.6.2/CMakeLists.txt#L90
license=('GPL-2.0-or-later')
url='https://github.com/chewing/ibus-chewing'
depends=('ibus' 'libchewing>=0.9.0' 'gtk4' 'libadwaita')
makedepends=('cmake' 'minisign' 'glib2-devel')
checkdepends=('dbus' 'xorg-server-xvfb')
source=("https://github.com/chewing/ibus-chewing/releases/download/v$_pkgver/$pkgname-$_pkgver-Source.tar.xz"{,.minisig})
sha512sums=('7a654b6a46e0d0cb5b35900a1cce535e52292b345854afbd20e4de5f8778fb7eeabb66b4786251a705c90d8ad69258af87dc9075505a9d901363317d2467b250'
            '834c15e86ae52c46dd0d2bd4c4382265f782f27fde07c3dcbbbf76689df556259a5aa747e9730da5b5fa73427d015901e73fdf8d640be37699308355cb991aa3')
# The key is mentioned on https://github.com/chewing/ibus-chewing
_validminisignkey='RWRzJFnXiLZleAyCIv1talBjyRewelcy9gzYQq9pd3SKSFBPoy57sf5s'

# XXX: move to verify() when devtools supports it
# https://gitlab.archlinux.org/archlinux/devtools/-/issues/224
prepare() {
  minisign -Vm $pkgname-$_pkgver-Source.tar.xz -P $_validminisignkey
}

build() {
  cd $pkgname-$_pkgver-Source

  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/ibus

  make -C build
}

check() {
  cd $pkgname-$_pkgver-Source/build

  # glib-compile-schemas is needed for tests
  # https://github.com/chewing/ibus-chewing/blob/v2.0.0/.github/workflows/ci.yml#L33
  export GSETTINGS_SCHEMA_DIR="$srcdir/$pkgname-$_pkgver-Source/build/bin"
  glib-compile-schemas ../src/setup --targetdir "$GSETTINGS_SCHEMA_DIR"
  xvfb-run --auto-display dbus-run-session make test
}

package() {
  cd $pkgname-$_pkgver-Source/build
  make DESTDIR="$pkgdir" install
}
