# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
_pkgver=2.1.2
pkgver=${_pkgver/-rc./rc}
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('x86_64')
# "GPLv2+" in https://github.com/chewing/ibus-chewing/blob/1.6.2/CMakeLists.txt#L90
license=('GPL-2.0-or-later')
url='https://github.com/chewing/ibus-chewing'
depends=('ibus' 'libchewing>=0.9.0' 'gtk4' 'libadwaita')
makedepends=('cmake' 'minisign' 'glib2-devel')
checkdepends=('dbus' 'xorg-server-xvfb')
source=("https://github.com/chewing/ibus-chewing/releases/download/v$_pkgver/$pkgname-$_pkgver-Source.tar.xz"{,.minisig})
sha512sums=('872de320664aed2158449e526c5c2d09e2d620035c98d3686fe6b5d249979dbd9d78b67afb05ebbd04a867b031d11c5bf9065a7558b51e0f11808b0b2bf409ae'
            '7ce17af635a88d488a61bf28f6fdba5e168f4283e76e7f46d551111c60397afe8dadf8864ec8b25bef48a6f9429664e0fc1f035a50adee6375ec82a62dbe810e')
# The key is mentioned on https://github.com/chewing/ibus-chewing
_validminisignkey='RWRzJFnXiLZleAyCIv1talBjyRewelcy9gzYQq9pd3SKSFBPoy57sf5s'

# XXX: move to verify() when pacman 7.0 is out
# https://gitlab.archlinux.org/pacman/pacman/-/merge_requests/37
prepare() {
  minisign -Vm $pkgname-$_pkgver-Source.tar.xz -P $_validminisignkey
}

build() {
  cd $pkgname-$_pkgver-Source

  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/ibus

  make -C build
}

check() {
  cd $pkgname-$_pkgver-Source/build

  # glib-compile-schemas is needed for tests
  # https://github.com/chewing/ibus-chewing/blob/v2.0.0/.github/workflows/ci.yml#L33
  glib-compile-schemas ../src/setup --targetdir "$srcdir/$pkgname-$_pkgver-Source/build/bin"
  GSETTINGS_SCHEMA_DIR="$srcdir/$_pkgname/build/bin/" xvfb-run --auto-display dbus-run-session make test
}

package() {
  cd $pkgname-$_pkgver-Source/build
  make DESTDIR="$pkgdir" install
}
