# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
_pkgver=2.1.4
pkgver=${_pkgver/-rc./rc}
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('x86_64')
# "GPLv2+" in https://github.com/chewing/ibus-chewing/blob/1.6.2/CMakeLists.txt#L90
license=('GPL-2.0-or-later')
url='https://github.com/chewing/ibus-chewing'
depends=('ibus' 'libchewing>=0.9.0' 'gtk4' 'libadwaita')
makedepends=('cmake' 'minisign' 'glib2-devel')
checkdepends=('dbus' 'xorg-server-xvfb')
source=("https://github.com/chewing/ibus-chewing/releases/download/v$_pkgver/$pkgname-$_pkgver-Source.tar.xz"{,.minisig})
sha512sums=('c0528a3337164e8d7f4c5faf564f8c9b252cdb426e37c1f5b7d669dfacb03a9795143622119f9c95002d49f58912ce70335267ee96562696ff473cdb1c4d889a'
            '213f65c2463c907277a58236f41c2a34c63d33cdc01b9ef3b80de1d0854bcf636893de7443d33dd143ab18d5dd2858573ba1d5e7c5ba8f7a93b37277e6ef8ab3')
# The key is mentioned on https://github.com/chewing/ibus-chewing
_validminisignkey='RWRzJFnXiLZleAyCIv1talBjyRewelcy9gzYQq9pd3SKSFBPoy57sf5s'

# XXX: move to verify() when devtools supports it
# https://gitlab.archlinux.org/archlinux/devtools/-/issues/224
prepare() {
  minisign -Vm $pkgname-$_pkgver-Source.tar.xz -P $_validminisignkey
}

build() {
  cd $pkgname-$_pkgver-Source

  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/ibus

  make -C build
}

check() {
  cd $pkgname-$_pkgver-Source/build

  # glib-compile-schemas is needed for tests
  # https://github.com/chewing/ibus-chewing/blob/v2.0.0/.github/workflows/ci.yml#L33
  glib-compile-schemas ../src/setup --targetdir "$srcdir/$pkgname-$_pkgver-Source/build/bin"
  GSETTINGS_SCHEMA_DIR="$srcdir/$_pkgname/build/bin/" xvfb-run --auto-display dbus-run-session make test
}

package() {
  cd $pkgname-$_pkgver-Source/build
  make DESTDIR="$pkgdir" install
}
