# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=archlinux-contrib
pkgver=20240617
pkgrel=1
pkgdesc='Collection of contrib scripts used in Arch Linux'
arch=('any')
license=('GPL-2.0-or-later')
url='https://github.com/archlinux/contrib'
depends=('python' 'bash' 'pyalpm')
optdepends=('python-requests: for security tracker'
            'jq: for rebuild-todo'
            'pacman-contrib: to enable pacdiff usage in the checkservices script')
source=("https://github.com/archlinux/contrib/releases/download/${pkgver}/contrib-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('C100346676634E80C940FB9E9C02FF419FECBE16'  # Morten Linderud
              '86CFFCA918CF3AF47147588051E8B148A9999C34'  # Evangelos Foutras
              'F00B96D15228013FFC9C9D0393B11DAA4C197E3D') # Christian Heusel
sha256sums=('a40673802995ae8c3090ec266b105e33907bc329247ce7ddeb7cebe7d5095c31'
            'SKIP')

package() {
  cd contrib-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
