# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=ffms2
pkgver=5.0RC4
pkgrel=1
pkgdesc='A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access'
arch=(x86_64)
url=https://github.com/FFMS/ffms2
license=(GPL)
depends=(ffmpeg)
makedepends=(
  avisynthplus
  git
)
optdepends=(
  'avisynthplus: AviSynth plugin'
  'vapoursynth: VapourSynth plugin'
)
provides=(
  avisynth-plugin-ffms2
  libffms2.so
  vapoursynth-plugin-ffms2
)
_tag=a3c6e25a103157ba2170fc4eafc2e444cc789b5b
source=(git+https://github.com/FFMS/ffms2.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd ffms2
  mkdir -p src/config
  autoreconf -fiv
}

pkgver() {
  cd ffms2
  git describe --tags | sed 's/-//'
}

build() {
  cd ffms2
  export CXXFLAGS="$CXXFLAGS $(pkg-config --cflags avisynth) -fpermissive"
  ./configure \
    --prefix=/usr \
    --enable-avisynth \
    --enable-shared \
    --disable-static
  make
}

package() {
  make DESTDIR="${pkgdir}" -C ffms2 install
  for s in {avi,vapour}synth; do
    install -dm 755 "${pkgdir}"/usr/lib/$s
    ln -s ../libffms2.so "${pkgdir}"/usr/lib/$s/
  done
}

# vim: ts=2 sw=2 et:
