# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>

pkgname=arch-install-scripts
pkgver=31
pkgrel=1
pkgdesc="Scripts to aid in installing Arch Linux"
arch=('any')
url="https://gitlab.archlinux.org/archlinux/arch-install-scripts"
license=('GPL-2.0-only')
depends=('awk' 'bash' 'coreutils' 'grep' 'pacman' 'util-linux')
makedepends=('asciidoc' 'git')
source=("git+$url.git#tag=v${pkgver}?signed")
validpgpkeys=(
  'C100346676634E80C940FB9E9C02FF419FECBE16'  # Morten Linderud
)
sha256sums=('77b4f4eb96ecb6031e3ed0ff4c68de1fbeb28deae9615cfe91e2632c1fe629e2')

build() {
  make -C "$pkgname"
}

check() {
  make -C "$pkgname" check
}

package() {
  make -C "$pkgname" PREFIX=/usr DESTDIR="$pkgdir" install
}
