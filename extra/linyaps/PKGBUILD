# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: taotieren <admin@taotieren.com>

pkgname=linyaps
pkgver=1.9.1
pkgrel=1
pkgdesc='Next-Gen Universal Package Manager for Linux (linglong)'
arch=(x86_64)
url='https://github.com/OpenAtom-Linyaps/linyaps'
license=('LGPL-3.0-or-later')
provides=(
  linglong
)
conflicts=(
  linglong
)
depends=(
  bash
  curl
  gcc-libs
  glib2
  glibc
  libelf
  libseccomp
  linyaps-box
  qt6-base
  systemd-libs
  ostree
  yaml-cpp
)
makedepends=(
  cli11
  cmake
  git
  gtest
  qt6-tools
  ninja
  nlohmann-json
  openssl
  tl-expected
  vulkan-headers
)
source=(
  git+https://github.com/OpenAtom-Linyaps/linyaps.git#tag=$pkgver
)
sha256sums=('9038396d9baf2fb804334e90a4612d1a5b426086320aa128965bf1fd97837815')

prepare() {
  cd linyaps
  # fix libexecdir
  git cherry-pick -n 64ad68580db39f782e512b67eeb550a7d886c21a
}

build() {
  cd linyaps
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCPM_LOCAL_PACKAGES_ONLY=ON \
    -DLINGLONG_VERSION="$pkgver" \
    -Wno-dev \
    -B build \
    -G Ninja

  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C linyaps/build install
}
