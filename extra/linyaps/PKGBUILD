# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: taotieren <admin@taotieren.com>

pkgname=linyaps
pkgver=1.9.12
pkgrel=1
pkgdesc='Next-Gen Universal Package Manager for Linux (linglong)'
arch=(x86_64)
url='https://github.com/OpenAtom-Linyaps/linyaps'
license=('LGPL-3.0-or-later')
provides=(
  linglong
)
conflicts=(
  linglong
)
depends=(
  bash
  curl
  fmt
  gcc-libs
  glib2
  glibc
  libelf
  libseccomp
  linyaps-box
  qt6-base
  systemd-libs
  ostree
  yaml-cpp
)
makedepends=(
  cli11
  cmake
  git
  gtest
  qt6-tools
  ninja
  nlohmann-json
  openssl
  tl-expected
  vulkan-headers
)
source=(
  git+https://github.com/OpenAtom-Linyaps/linyaps.git#tag=$pkgver
)
sha256sums=('8908a53e098856b4f8f5d1554aff81810dd216ce6a91a38a3a4b2d7757459a19')

prepare() {
  cd linyaps
  sed -i 's/Qt${QT_VERSION_MAJOR}::DBusPrivate//' libs/linglong/tests/ll-tests/CMakeLists.txt
}

build() {
  cd linyaps
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCPM_LOCAL_PACKAGES_ONLY=ON \
    -DLINGLONG_VERSION="$pkgver" \
    -Wno-dev \
    -B build \
    -G Ninja

  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C linyaps/build install
}
