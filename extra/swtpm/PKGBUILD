# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=swtpm
pkgver=0.9.0
pkgrel=1
pkgdesc='Libtpms-based TPM emulator with socket, character device, and Linux CUSE interface'
arch=('x86_64')
url='https://github.com/stefanberger/swtpm'
license=('BSD-3-Clause')
depends=(
  'fuse2'
  'gcc-libs'
  'glib2'
  'glibc'
  'gmp'
  'gnutls'
  'json-glib'
  'libseccomp'
  'libseccomp.so'
  'libtasn1'
  'libtpms'
  'openssl'
)
makedepends=(
  'expect'
  'git'
  'iproute2'
  'python'
  'socat'
)
checkdepends=('softhsm')
source=("git+$url.git?signed#tag=v$pkgver")
sha512sums=('0d56ef4c297dc5bf7914dc5bd4cfa2827880abd2b8f915b6d9e8221a11958ca5ded3bbcf088fcb1f40c2b05d9012e51f6e65ca2b5754c44b90e5322fc0837bb8')
validpgpkeys=('B818B9CADF9089C2D5CEC66B75AD65802A0B4211') # Stefan Berger <stefanb@linux.vnet.ibm.com>

prepare() {
  cd "$pkgname"
  # Remove usage of /usr/bin/env to avoid PATH manipulation attacks
  sed --in-place 's/env //' \
    samples/swtpm-create-tpmca \
    samples/swtpm-create-user-config-files.in

  autoreconf --install --force
}

build() {
  cd "$pkgname"
  ./configure \
    --prefix=/usr \
    --with-cuse \
    --with-gnutls \
    --with-seccomp
  make
}

check() {
  cd "$pkgname"
  make check
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  echo 'u tss - "tss user for tpm2"' \
    | install -vDm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'z /var/lib/swtpm-localca 0750 tss root' \
    | install -vDm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  chmod -v 750 "$pkgdir/var/lib/swtpm-localca"
}
