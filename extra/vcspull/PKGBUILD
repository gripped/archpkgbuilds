# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='vcspull'
pkgver=1.35.0
pkgrel=1
pkgdesc='Synchronize projects via yaml/json manifest. Built using `libvcs`'
arch=('any')
url='https://vcspull.git-pull.com/'
license=('MIT')
depends=(
  python
  python-colorama
  python-libvcs
  python-shtab
  python-yaml
)
makedepends=(
  python-build
  python-hatchling
  python-installer
  python-poetry-core
)
checkdepends=(
  git
  python-pytest
  python-pytest-rerunfailures
)
optdepends=(
  'bash-completion: for command line completion in bash'
)
source=("https://github.com/vcs-python/vcspull/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e93de603d3a95eb9e8b1ceeb56d57463f49f63a7e4f76de7f8b12b7278833e1ec6ae51a25a2f1715df7286bd4f6a17fffd8773583cf5e74ec2fa781e9d53cabb')
b2sums=('04061ed9594c26ce76d7083ae5b489f12c81f859d836f38d6f9c13318aca996bbbd574c2408baf8e96283e72a44c95f7d3ef6caf8540bc3dc4ce2f7ff43de72a')

build() {
  cd  "${pkgname}-${pkgver}"

  local _site_packages="$(python -c "import site; print(site.getsitepackages()[0])")"

  python -m build --wheel --skip-dependency-check --no-isolation
  python -m installer --destdir=tmp_install dist/*.whl

  export PYTHONPATH="tmp_install${_site_packages}:${PYTHONPATH}"
}

check() {
  cd  "${pkgname}-${pkgver}"

  pytest -vv tests
}

package() {
  cd  "${pkgname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose --directory --mode=0755 \
      "${pkgdir}/usr/share/bash-completion/completions" \
      "${pkgdir}/usr/share/zsh/site-functions" \
      "${pkgdir}/etc/profile.d"

  shtab --shell=bash --error-unimportable vcspull.cli.create_parser > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  shtab --shell=zsh --error-unimportable vcspull.cli.create_parser > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  shtab --shell=tcsh --error-unimportable vcspull.cli.create_parser > "${pkgdir}/etc/profile.d/${pkgname}.completion.csh"

  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md CHANGES MIGRATION --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 examples/* --target-directory "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
