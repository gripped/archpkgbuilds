# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='vcspull'
pkgver=1.23.0
pkgrel=1
pkgdesc='Synchronize projects via yaml/json manifest. Built using `libvcs`'
arch=('any')
url='https://vcspull.git-pull.com/'
license=('MIT')
depends=('python' 'python-colorama' 'python-libvcs' 'python-shtab' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-poetry-core' 'python-typing_extensions')
checkdepends=('git' 'python-pytest' 'python-pytest-rerunfailures')
optdepends=(
  'bash-completion: for command line completion in bash'
)
source=("https://github.com/vcs-python/vcspull/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e192d957416231e3ae0737b90cf6baea3bb740b27e5bc444715ceb314a16d9f34663668fcf6a2f7685b29f66879d4c58c24f088517fbb139d11a50e8a81d4426')
b2sums=('dc48bea2ef81c3218bdf7f66e226e5a63a866799b82f215050eaa259a886e4f19a8d2a7bd6669850bd6ac38aacf232f9fd9bf8338836043a3c600df0cfba6d5b')

build() {
  cd  "${pkgname}-${pkgver}"

  local _site_packages="$(python -c "import site; print(site.getsitepackages()[0])")"

  python -m build --wheel --skip-dependency-check --no-isolation
  python -m installer --destdir=tmp_install dist/*.whl

  export PYTHONPATH="tmp_install${_site_packages}:${PYTHONPATH}"
}

check() {
  cd  "${pkgname}-${pkgver}"

  pytest -vv tests
}

package() {
  cd  "${pkgname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose --directory --mode=0755 \
      "${pkgdir}/usr/share/bash-completion/completions" \
      "${pkgdir}/usr/share/zsh/site-functions" \
      "${pkgdir}/etc/profile.d"

  shtab --shell=bash --error-unimportable vcspull.cli.create_parser > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  shtab --shell=zsh --error-unimportable vcspull.cli.create_parser > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  shtab --shell=tcsh --error-unimportable vcspull.cli.create_parser > "${pkgdir}/etc/profile.d/${pkgname}.completion.csh"

  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md CHANGES MIGRATION --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 examples/* --target-directory "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
