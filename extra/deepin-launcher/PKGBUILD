# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=6.0.15
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'dtkcore' 'dtkgui' 'dtkwidget' 'dconf' 'glib2'
         'qt5-base' 'libxcb' 'xcb-util-wm' 'gcc-libs' 'xdg-user-dirs' 'deepin-application-manager'
         'deepin-appearance')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-launcher/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('47c4a94d7440ea98ad6e4d5722e42d76c4866a366584f3936d49207d530d0873387722b95cf19c32b0e3d5320ffe44f0673a7ecc5b1ddf1fdba26744fca0c211')

prepare() {
  cd dde-launcher-$pkgver

  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=
  ninja
}

check() {
  cd dde-launcher-$pkgver
  # ninja test
}

package() {
  cd dde-launcher-$pkgver
  DESTDIR="$pkgdir" ninja install
}
