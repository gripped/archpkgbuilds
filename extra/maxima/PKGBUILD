# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgbase=maxima
pkgname=($pkgbase{,-sbcl,-ecl,-fas})
pkgver=5.49.0
_sbclver=2.6.0
_eclver=24.5.10
pkgrel=3
pkgdesc='A sophisticated computer algebra system'
arch=(x86_64)
license=(GPL-2.0-only)
url='http://maxima.sourceforge.net'
makedepends=(ecl
             emacs
             git
             python
             sbcl)
# needs rebuild when bash changes version
# needs a rebuild when ecl or sbcl changes version
options=(!zipman) # don't zip info pages or they won't work inside maxima
source=(maxima::git://git.code.sf.net/p/maxima/code#tag=$pkgver
        matrixexp.patch
        maxima-sbcl-gmp.patch
        0001-ECL-Fix-autoconf-options-on-whitespace.patch)
sha256sums=('0bbc6258d1982b60be2e6c6ed7868f687dee5af00c2d0468e19b36ad90188117'
            'ef1bc6a15fc982ff8c6aa1800bbbd3284d9e060ca27abf9d8c1266632c0c2619'
            '7e85da0b6672b096d7aefac861573a57323dfe805e08d033781448f7ca6ed856'
            '05f6f82550e03af3927379d71a4e2a762ea523fa0c0ea1021741c7dd51414c32')


prepare() {
  patch -d $pkgname -p1 < matrixexp.patch # fix matrix exponentiation
  patch -d $pkgname -p1 < maxima-sbcl-gmp.patch # Use GMP arithmetic with sbcl (Void Linux)
  patch -d $pkgname -p1 < 0001-ECL-Fix-autoconf-options-on-whitespace.patch # Handle whitespace in LDFLAGS correctly
  cd $pkgbase
  rm -fr .git # prevent a dirty version number
  autoreconf -fiv
}

build() {
  cd $pkgbase
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --libexecdir=/usr/lib \
    --enable-sbcl \
    --enable-ecl \
    --with-default-lisp=sbcl

  # help avoid (re)running makeinfo/tex
  touch doc/info/maxima.info
  make
}

#check() {  # test 23&129 fail from rtest_taylor
#  cd $pkgbase
#  make check
#}

package_maxima() {
  depends=(maxima-backend
           texinfo)
  replaces=('maxima-ecl<5.45.1-9')
  optdepends=('gnuplot: plotting capabilities'
              'rlwrap: readline support via /usr/bin/rmaxima'
              'tk: graphical xmaxima interface')

  cd $pkgbase
  make DESTDIR="$pkgdir" emacsdir=/usr/share/emacs/site-lisp/maxima install

# Remove backends
  rm -r "$pkgdir"/usr/lib/maxima/$pkgver/binary-*
}

package_maxima-sbcl() {
  pkgdesc='SBCL backend for Maxima'
  depends=(sbcl=$_sbclver)
  provides=(maxima-backend)

  install -Dm755 $pkgbase/src/binary-sbcl/maxima.core -t "$pkgdir"/usr/lib/maxima/$pkgver/binary-sbcl
}

package_maxima-ecl() {
  pkgdesc='ECL backend for Maxima'
  depends=(ecl=$_eclver
           glibc)
  provides=(maxima-backend)

  install -Dm755 $pkgbase/src/binary-ecl/maxima -t "$pkgdir"/usr/lib/maxima/$pkgver/binary-ecl
}

package_maxima-fas() {
  pkgdesc='Maxima FAS module for ECL'
  depends+=(ecl=$_eclver
            maxima)

  install -Dm644 $pkgbase/src/binary-ecl/maxima.fas -t "$pkgdir"/usr/lib/ecl-$_eclver
}
