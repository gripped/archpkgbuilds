# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Iyán Méndez Veiga <me (at) iyanmv (dot) com>
# Contributor: Thomas Wucher <arch (at) thomaswucher (dot) de>

pkgname=stress-ng
pkgver=0.18.12
pkgrel=1
pkgdesc="Software to stress test a computer system in various selectable ways"
url="https://github.com/ColinIanKing/stress-ng"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('apparmor' 'gmp' 'judy' 'kmod' 'libbsd' 'libglvnd' 'libjpeg-turbo' 'lksctp-tools' 'mpfr' 'xxhash')
optdepends=('intel-ipsec-mb: support for IPsec tests'
	    'linux-headers: include some headers from the Arch kernel'
	    'linux-lts-headers: include some headers from the LTS kernel'
	    'linux-zen-headers: include some headers from the ZEN kernel'
	    'linux-hardened-headers: include some headers from the HARDENED kernel')
makedepends=('attr' 'eigen' 'keyutils' 'libaio' 'libcap' 'libgcrypt' 'libglvnd' 'libmd')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/V${pkgver}.tar.gz")
b2sums=('6b25fa6b9734cb2920094ca9ce49c39ec0997e467a8aa66cdafc535d963627a66309745eb42c0549cdf26a27ed533be974285b82b139aee1a118c93b61249e41')

prepare() {
	cd "${pkgname}-${pkgver}"
	make clean
}

build() {
	cd "${pkgname}-${pkgver}"
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
