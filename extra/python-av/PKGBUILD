# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=PyAV
pkgname=python-av
pkgver=16.1.0
pkgrel=1
pkgdesc="Pythonic bindings for FFmpeg"
arch=(x86_64)
url="https://github.com/PyAV-Org/PyAV"
license=(BSD-3-Clause)
depends=(
  cython
  glibc
  ffmpeg
  python
  python-numpy
  python-pillow
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('5525169e4834e31457aa6603c2cb066d68190f078747834ade32c342c80ad771112384e61aa04abe4300a46e62a00caa32b845e00e1c0252e4c38fab4b32e158')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    # use importlib mode to avoid adding the project's root directory to sys.path
    # https://docs.pytest.org/en/stable/explanation/pythonpath.html
    --import-mode importlib
  )

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer $_name-$pkgver/dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" $_name-$pkgver/tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
