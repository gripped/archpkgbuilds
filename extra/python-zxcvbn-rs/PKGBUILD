# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=python-zxcvbn-rs
pkgver=0.2.0
pkgrel=1
pkgdesc="Python bindings for zxcvbn-rs, the Rust implementation of zxcvbn"
url="https://github.com/fief-dev/zxcvbn-rs-py"
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  git
  maturin
  python-installer
  rust
)
source=(
  "git+$url#tag=v$pkgver"
)
b2sums=('cad84718b1c9aff9326c7c6368ff515f668b44eba217ad0eb76b4468e2c1a2651372d74f97a595c62f938319ff9070e4e21326c9590d0a77d6697f445c63a71e')

prepare() {
  cd zxcvbn-rs-py

  cargo fetch --locked
}

build() {
  cd zxcvbn-rs-py

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  maturin build --release --frozen
}

package() {
  cd zxcvbn-rs-py
  python -m installer -d "$pkgdir" target/wheels/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
