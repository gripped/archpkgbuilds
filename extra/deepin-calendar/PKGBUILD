# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=6.5.3
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'glibc' 'gcc-libs' 'libical' 'qt6-base' 'qt6-svg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-declarative')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-calendar.git#tag=$pkgver")
sha512sums=('f3703bfcc0e19c9f1bfc91917a7fdb946afbd947d9c069032329d68fa9dfafbd1c99e684a3226891210d3440eb1675e5fac9a10f44d2619296b37bde970eb275')

build() {
  cd dde-calendar
  cmake -GNinja . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DVERSION=$pkgver
  ninja
}

package() {
  cd dde-calendar
  DESTDIR="$pkgdir" ninja install
}
