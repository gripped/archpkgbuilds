# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=6.5.4
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'glibc' 'gcc-libs' 'libical' 'qt6-base' 'qt6-svg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-declarative')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-calendar.git#tag=$pkgver")
sha512sums=('5e65b84d4886e318f06d0f243c1307fbb8043cb500086f488b2e2c13d775ed907a2b7e5ccc797312f603c1817d4c29004aae1a2d082bea73f7c176c16671aa7f')

build() {
  cd dde-calendar
  cmake -GNinja . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DVERSION=$pkgver
  ninja
}

package() {
  cd dde-calendar
  DESTDIR="$pkgdir" ninja install
}
