# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=6.5.2
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'glibc' 'gcc-libs' 'libical' 'qt6-base' 'qt6-svg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-declarative')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-calendar.git#tag=$pkgver")
sha512sums=('6b5e2022ab7b2cb4d0681de9c692fe5e72299614fd477cd4240c3b54ff5cb81c4a51e0621a9f18a7f64930d52661542802158d698e3900981408264fb47bd767')

build() {
  cd dde-calendar
  cmake -GNinja . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DVERSION=$pkgver
  ninja
}

package() {
  cd dde-calendar
  DESTDIR="$pkgdir" ninja install
}
