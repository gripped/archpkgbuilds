# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.13.1
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'libical' 'qt5-svg' 'qt5-base')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-calendar.git#tag=$pkgver")
sha512sums=('d9026d56636f668b35516e4049337a27dee010351f557a4b59efc54a582b7b25236182f36a8927336cb6fc102e6d676eb6a4f98d8e2908c69b3460d8d6a5ac02')

build() {
  cd dde-calendar
  cmake -GNinja . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DVERSION=$pkgver
  ninja
}

package() {
  cd dde-calendar
  DESTDIR="$pkgdir" ninja install
}
