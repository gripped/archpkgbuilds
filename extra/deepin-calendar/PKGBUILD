# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.14.11
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'libical' 'qt5-svg' 'qt5-base')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-calendar.git#tag=$pkgver")
sha512sums=('c94c77628913810da1a85dbd540e4b28b7e25398a25da53b8bcda9f9aa82a4998a1314d9a797b0f834578b5b6eab447ccf635eee72d255c2b6e57056bddf091b')

build() {
  cd dde-calendar
  cmake -GNinja . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DVERSION=$pkgver
  ninja
}

package() {
  cd dde-calendar
  DESTDIR="$pkgdir" ninja install
}
