# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-genesis-plus-gx
pkgver=2263
pkgrel=1
pkgdesc='Sega MS/GG/MD/CD core'
arch=(x86_64)
url=https://github.com/libretro/Genesis-Plus-GX
license=(BSD LGPL2.1)
groups=(libretro)
depends=(
  libretro-core-info
  libvorbis
  zlib
)
makedepends=(
  clang
  git
)
_commit=a69c81e44a3a25e5a9254a18652eae2dad875003
source=(libretro-genesis-plus-gx::git+https://github.com/libretro/Genesis-Plus-GX.git#commit=${_commit})
b2sums=('def617f17d694ef254f226345714f33278e367723c7d5ecde425c68703cfc4648c503ee2af47b22a79704f2e02c3d3fdbfd3d9a55e470d3125d9137d6fe640ef')

pkgver() {
  cd libretro-genesis-plus-gx
  git rev-list --count HEAD
}

build() {
  export CC=clang # FS#71188 - [libretro-genesis-plus-gx] segfaults when compiled with -O2
  make \
    SHARED_LIBVORBIS=1 \
    SYSTEM_ZLIB=1 \
    -C libretro-genesis-plus-gx \
    -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-genesis-plus-gx/genesis_plus_gx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-genesis-plus-gx/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libretro-genesis-plus-gx/
}

# vim: ts=2 sw=2 et:
