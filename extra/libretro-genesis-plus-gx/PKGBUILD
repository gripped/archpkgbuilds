# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-genesis-plus-gx
pkgver=2199
pkgrel=1
pkgdesc='Sega MS/GG/MD/CD core'
arch=(x86_64)
url=https://github.com/libretro/Genesis-Plus-GX
license=(BSD LGPL2.1)
groups=(libretro)
depends=(
  libretro-core-info
  libvorbis
  zlib
)
makedepends=(
  clang
  git
)
_commit=7de0f0b6cde9bda1235b448aa607044b3f80ab3c
source=(libretro-genesis-plus-gx::git+https://github.com/libretro/Genesis-Plus-GX.git#commit=${_commit})
b2sums=('e7ac474e55c32fbae29717bb5033397a77252d0c8f5c1c49aa86b41dc7c79e99692fb8b007da8a8265ca5bb91a36b1145d10571560540b9c599a72c188aae8f8')

pkgver() {
  cd libretro-genesis-plus-gx
  git rev-list --count HEAD
}

build() {
  export CC=clang # FS#71188 - [libretro-genesis-plus-gx] segfaults when compiled with -O2
  make \
    SHARED_LIBVORBIS=1 \
    SYSTEM_ZLIB=1 \
    -C libretro-genesis-plus-gx \
    -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-genesis-plus-gx/genesis_plus_gx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-genesis-plus-gx/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libretro-genesis-plus-gx/
}

# vim: ts=2 sw=2 et:
