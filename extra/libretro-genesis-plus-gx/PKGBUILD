# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-genesis-plus-gx
pkgver=2208
pkgrel=1
pkgdesc='Sega MS/GG/MD/CD core'
arch=(x86_64)
url=https://github.com/libretro/Genesis-Plus-GX
license=(BSD LGPL2.1)
groups=(libretro)
depends=(
  libretro-core-info
  libvorbis
  zlib
)
makedepends=(
  clang
  git
)
_commit=8ea39eefc76bd97331c0da9efbac6ff343704a95
source=(libretro-genesis-plus-gx::git+https://github.com/libretro/Genesis-Plus-GX.git#commit=${_commit})
b2sums=('fa125ea8863e3a8b4d6ebf9669b61f72df03e6ea8285ab996239461bb9f666f9b3e40ade70db8433740c51dd824b3f1359df20dd0a15cb709c0ae86c3fd6ef2a')

pkgver() {
  cd libretro-genesis-plus-gx
  git rev-list --count HEAD
}

build() {
  export CC=clang # FS#71188 - [libretro-genesis-plus-gx] segfaults when compiled with -O2
  make \
    SHARED_LIBVORBIS=1 \
    SYSTEM_ZLIB=1 \
    -C libretro-genesis-plus-gx \
    -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-genesis-plus-gx/genesis_plus_gx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-genesis-plus-gx/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libretro-genesis-plus-gx/
}

# vim: ts=2 sw=2 et:
