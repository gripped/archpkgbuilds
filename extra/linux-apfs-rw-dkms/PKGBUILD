# Maintainer: George Hu <integral@archlinux.org>

pkgname=linux-apfs-rw-dkms
_pkgname=${pkgname%-dkms}
epoch=1
pkgver=0.3.18
pkgrel=1
pkgdesc="Experimental APFS kernel module with Write support (DKMS)"
arch=('any')
url="https://github.com/linux-apfs/${_pkgname}"
license=(
	'GPL-2.0-only'
	'BSD-3-Clause'
)
depends=('dkms')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('aa3cffc300d8766f6b238da5152d8c5a8633f923ba07001ea9230e47d70c796d')

prepare() {
	# Extract BSD-3-Clause terms from sources
	sed -n '2,21p' "${_pkgname}-${pkgver}/lzfse/lzfse.h" > BSD-3-Clause.txt
}

package() {
	local _dkms_dir="${pkgdir}/usr/src/${_pkgname}-${pkgver}/"
	install -d "${_dkms_dir}"
	cp -r ${_pkgname}-${pkgver}/* "${_dkms_dir}"

	install -vDm 644 BSD-3-Clause.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
