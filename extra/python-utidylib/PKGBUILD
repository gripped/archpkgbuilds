# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-utidylib
pkgver=1.0.0
pkgrel=1
pkgdesc='Python wrapper for the HTML cleaning library named TidyLib'
arch=(any)
url='https://github.com/nijel/utidylib'
license=(MIT)
depends=(
  python
  tidy
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
  python-sphinx
  python-sphinx-furo
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('2da8154428b12f1bd89c45e2eb2904f300a9143ed5c6f74ec613d41e3168f5744c4e8b717802c1fff637eece30c72bb1bd2afe55c5eca2b63bb2d2f91791a07f')
b2sums=('e9bc3be6d8cac39a9426dea2c1ff14d63bfa4237aba333b90a3b3a6140431674fd5f43249649ef8d9c7be797c37b61f0a6a4826f93a122b5ae10d01beb5f80af')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation

  make -C docs man html

  # remove unreproducible files from html documentation
  rm -vf docs/_build/html/.buildinfo
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" docs/_build/man/utidylib.1

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst
  cp -vr docs/_build/html "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
