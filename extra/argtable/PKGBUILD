# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=argtable
pkgver=2.13
pkgrel=6
pkgdesc="An ANSI C library for parsing GNU style command line options with a minimum of fuss"
arch=(x86_64)
url="http://argtable.sourceforge.net/"
license=(LGPL-2.0-or-later)
depends=(glibc)
replaces=(argtable-docs)
source=(
  https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgname-$pkgver/$pkgname${pkgver/./-}.tar.gz
  $pkgname-2.13-add-missing-includes.patch
)
sha512sums=('3d8303f3ba529e3241d918c0127a16402ece951efb964d14a06a3a7d29a252812ad3c44e96da28798871e9923e73a2cfe7ebc84139c1397817d632cae25c4585'
            '157193f8b3d283df9aac1675187bd61a91a46f621c06939b9391fb3a972e65392f3f3c06d991719c0cd37de8ad05fb00b291300f78dee5b2edaf144bf6e280df')
_dir=$pkgname${pkgver/./-}

prepare() {
  # add missing includes
  patch -Np1 -d $_dir -i ../$pkgname-2.13-add-missing-includes.patch
  cd $_dir
  autoreconf -fiv
}

build() {
  cd $_dir
  ./configure --prefix=/usr
  make
}

check() {
  cd $_dir
  make -k check
}

package() {
  make DESTDIR="$pkgdir/" install -C $_dir
  # moving examples
  mv -v "$pkgdir/usr/share/doc/${pkgname}2/" "$pkgdir/usr/share/doc/$pkgname/"
  # removing unneeded pdf/ps: https://bugs.archlinux.org/task/59732
  rm -v "$pkgdir/usr/share/doc/$pkgname/"*.{ps,pdf}
  # docs
  install -vDm 644 $_dir/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
