# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Dennis Herbrich (Gyroplast) <dennis.herbrich@veloxis.de>
# Contributor: Yamada Hayao <hayao@fascode.net>
# Contributor: Bernhard Landauer <oberon@manjaro.org>

pkgname=xfce4-calculator-plugin
pkgver=0.7.3
pkgrel=1
pkgdesc="Simple command line based calculator for the XFCE panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-calculator-plugin"
license=('GPL-2.0-or-later')
depends=('xfce4-panel')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-calculator-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('27156c24221b9c25d6a1efacfbbe83d700654afda7fbb9b2505404f310843c62')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

check() {
    cd $pkgname
    make check
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
