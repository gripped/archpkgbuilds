# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Dennis Herbrich (Gyroplast) <dennis.herbrich@veloxis.de>
# Contributor: Yamada Hayao <hayao@fascode.net>
# Contributor: Bernhard Landauer <oberon@manjaro.org>

pkgname=xfce4-calculator-plugin
pkgver=0.7.2
pkgrel=2
pkgdesc="Simple command line based calculator for the XFCE panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-calculator-plugin"
license=('GPL-2.0-or-later')
depends=('xfce4-panel')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-calculator-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('4c074c43f509a553f3dfb9c15b3a3719d3e13c279caedacb053a93ed17ac737f')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

check() {
    cd $pkgname
    make check
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
