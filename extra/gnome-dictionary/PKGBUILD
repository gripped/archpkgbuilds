# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-dictionary
pkgver=40.0+r89+g0f14a2e
pkgrel=1
pkgdesc="Look up words in dictionary sources"
url="https://wiki.gnome.org/Apps/Dictionary"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  pango
)
makedepends=(
  git
  glib2-devel
  meson
  yelp-tools
)
_commit=0f14a2e4ed92a9ab59ab5b613ca60093803e9a36  # master
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-dictionary.git#commit=$_commit"
  0001-metainfo-Fix-screenshots.patch
  0002-Fix-crash-in-strv_concat.patch
  0003-Add-new-window-command-line-option.patch
  0004-Don-t-hide-in-KDE.patch
)
b2sums=('6c72ae4a58672e64ce97afdd0b7240a27e9aaae5492ab9c819d736d9619a2091024361503d05f6bfe7b095ff8fd43fd3b1b5c2194c92b82d6c911ab5451321e5'
        '203e364ce642ecae4b269c126bbcc83b90181dd667489ef2227829bf3652e287cb6c81cb884a56d6e915de00cc63bf6031c90f2723042d756476662aca1e7d0b'
        '70be598af0e0e771c7631ea887658f12cbe8955327b6aefb01871e5d871ee48f98ac054bd44d56c792f8f254c346bbc2de80f20b03a27c7b8598ebcf3eeb64c1'
        '4b5abc25dd6b65f2155b7f7054bbb1414e2b9865b5f4546478a99d4e734d94c1287ee5175d37f0176c1f012d3850aedbf5a3abe4d62827c87921f69ee9b16a0d'
        '91baab07532f5a281d8a65bf751df1d37a3022e4d9b74575161fd3fc91db2c2ee0fbd9e45e6880c5d810a663338484c91ef9333a1898a3337bd43726b6393c7c')
validpgpkeys=(53EF3DC3B63E2899271BD26322E8091EEA11BBB7) # Emmanuele Bassi (GNOME) <ebassi@gnome.org>

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix screenshot URLs
  git apply -3 ../0001-metainfo-Fix-screenshots.patch

  # Fix crash in strv_concat
  git apply -3 ../0002-Fix-crash-in-strv_concat.patch

  # Add "new-window" command-line option
  git apply -3 ../0003-Add-new-window-command-line-option.patch

  # Don't hide in KDE
  git apply -3 ../0004-Don-t-hide-in-KDE.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
