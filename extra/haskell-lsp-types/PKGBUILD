# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lsp-types
pkgname=haskell-lsp-types
pkgver=2.0.2.0
pkgrel=3
pkgdesc="Haskell library for the Microsoft Language Server Protocol, data types"
url="https://github.com/haskell/lsp"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-data-default' 'haskell-diff' 'haskell-dlist'
         'haskell-file-embed' 'haskell-hashable' 'haskell-indexed-traversable'
         'haskell-indexed-traversable-instances' 'haskell-lens' 'haskell-lens-aeson'
         'haskell-mod' 'haskell-network-uri' 'haskell-prettyprinter' 'haskell-regex'
         'haskell-row-types' 'haskell-safe' 'haskell-some' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-quickcheck-instances')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        "$pkgname-aeson-2.2.patch")
sha512sums=('f6c14efddd2d514edc52c033182a8d659f197002538189936050d42d7dde62f5351efe315d939443e45a061296af6c46cae550038e537fda41019903755fdd4f'
            'bdac47ee85e55836fa436698b59f2248b3470189aa38ac09a5ef61d039d80725ab813cce83b83a2a73eda18ae56e85d46bc2f5e2135af5552206a71e9bd3e21b')

prepare() {
  cd $_hkgname-$pkgver
  patch -p2 -i ../$pkgname-aeson-2.2.patch
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
