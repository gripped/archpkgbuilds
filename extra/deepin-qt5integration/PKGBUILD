# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.6.29
pkgrel=1
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'libqt5xdg' 'qt5-x11extras' 'qt5-base' 'libx11' 'glibc'
         'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt5integration.git#tag=$pkgver")
sha512sums=('52804638eda7f42b98c303ddc05f4cfeec5139fdf5006127a7507f268cf44c0329e25068e537a802aa2345c634a45350e74c4beda5217083bcecfe424f7e9523')

build() {
  cd qt5integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPLUGIN_INSTALL_BASE_DIR=lib/qt/plugins
  ninja
}

package() {
  cd qt5integration
  DESTDIR="$pkgdir" ninja install
}
