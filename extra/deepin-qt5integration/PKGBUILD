# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.7.16
pkgrel=1
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('LGPL-3.0-or-later')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'libqt5xdg' 'qt5-x11extras' 'qt5-base' 'libx11' 'glibc'
         'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt5integration.git#tag=$pkgver")
sha512sums=('3ca4e2ee0dd51a42dfd173234e39bc38ba71979d4b91c6d3eb93589549c2d3fc005f7ea07950e3cb606f3b43b0971e4394cc48f577a88be55f98cee541c09e23')

build() {
  cd qt5integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPLUGIN_INSTALL_BASE_DIR=lib/qt/plugins
  ninja
}

package() {
  cd qt5integration
  DESTDIR="$pkgdir" ninja install
}
