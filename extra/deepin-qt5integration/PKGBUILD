# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.7.24
pkgrel=2
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('LGPL-3.0-or-later')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'libqt5xdg' 'qt5-x11extras' 'qt5-base' 'libx11' 'glibc'
         'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt5integration.git#tag=$pkgver")
sha512sums=('044e9db744f381495d4708473cf22a225309d703344e555841e9191d1edeeabd5404bbe289d0fb01d6787ab37781b38a866044f9bfe4435bd8a0b6204b663f15')

build() {
  cd qt5integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPLUGIN_INSTALL_BASE_DIR=lib/qt/plugins
  ninja
}

package() {
  cd qt5integration
  DESTDIR="$pkgdir" ninja install
}
