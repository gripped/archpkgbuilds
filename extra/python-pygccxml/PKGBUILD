# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org>
# Contributor: Yunhui Fu <yhfudev at gmail dot com>
# Contributor: Benjamin Chretien <chretien at lirmm dot fr>
# Contributor: zarra <zarraxx@gmail.com>

pkgname=python-pygccxml
pkgver=3.0.0
pkgrel=1
pkgdesc="Simple framework to navigate C++ declarations, using Python classes"
arch=(any)
url="https://github.com/gccxml/pygccxml"
license=(BSL-1.0)
depends=(
  castxml
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('fead5bec1a26a1a709e971124be19721240f3f3774f989c3971babe38207b350')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer --destdir=tmp_install dist/*.whl
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
