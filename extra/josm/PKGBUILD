# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Muflone http://www.muflone.com/contacts/english/
# Contributor: Michele Sch√§uble <mschaeuble@swissonline.ch>

pkgname=josm
pkgver=19253
pkgrel=1
pkgdesc="An editor for OpenStreetMap written in Java"
arch=('any')
url="https://josm.openstreetmap.de/"
license=('GPL-2.0-or-later')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'java-runtime>=11' 'libxtst' 'ttf-font')
makedepends=('subversion')
changelog=$pkgname.changelog
backup=('etc/conf.d/josm')
source=(https://josm.openstreetmap.de/download/$pkgname-snapshot-$pkgver.jar
        $pkgname.conf.d
        $pkgname::svn+https://josm.openstreetmap.de/svn/trunk#revision=$pkgver
        remove_javafx.patch)
noextract=($pkgname-snapshot-$pkgver.jar)
sha512sums=('36d40422080cf6f14db53623796ce73d6c61f956916493021f2002146f8f254781caa3fd48c905f1f78b0baff63b51c6566620c2e5627f505e15b1a794dfc1f7'
            'd69e681a679378915f980ebafe204c548e0d76778962e4aa33afe8f83a1e6fe4438c7729a92634e02b195349a52a388f8933959c6b81c01ecc602d061ff7c8df'
            'SKIP'
            '18e675fe998d01181c606eb414a72e55a4ded6ac3e4d89058ed654690e5b8821654ccaae2a6987003601ec918e62e91584e89b3b8a429ab0dfd92ab19fd9c4b4')

package() {
  cd "${srcdir}"

  install -Dm644 $pkgname-snapshot-$pkgver.jar "${pkgdir}"/usr/share/java/$pkgname/$pkgname.jar

  install -Dm644 $pkgname/native/linux/tested/usr/share/applications/org.openstreetmap.josm.desktop \
        "${pkgdir}"/usr/share/applications/org.openstreetmap.josm.desktop
  install -Dm644 $pkgname/native/linux/tested/usr/share/man/man1/josm.1 \
        "${pkgdir}"/usr/share/man/man1/josm.1
  install -Dm644 $pkgname/native/linux/tested/usr/share/metainfo/org.openstreetmap.josm.appdata.xml \
        "${pkgdir}"/usr/share/metainfo/org.openstreetmap.josm.appdata.xml
  install -Dm644 $pkgname/native/linux/tested/usr/share/icons/hicolor/scalable/apps/org.openstreetmap.josm.svg \
        "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/org.openstreetmap.josm.svg

  for _icon in 8 16 22 24 32 36 40 42 48 64 72 80 96 128 192 256 512; do
      install -Dm644 $pkgname/native/linux/tested/usr/share/icons/hicolor/${_icon}x${_icon}/apps/org.openstreetmap.josm.png \
        "${pkgdir}"/usr/share/icons/hicolor/${_icon}x${_icon}/apps/org.openstreetmap.josm.png
  done

  install -Dm644 "${srcdir}"/$pkgname.conf.d "${pkgdir}"/etc/conf.d/$pkgname
  install -Dm755 "${pkgname}/native/linux/tested/usr/bin/josm" "${pkgdir}/usr/bin/josm"
  install -Dm644 "${pkgname}/native/linux/tested/etc/default/josm" "${pkgdir}/etc/default/josm"

  # Load our custom josm config at /etc/conf.d/josm (will append this to line 4 of the start script)
  sed -i '4 a[ -f /etc/conf.d/josm ] && . /etc/conf.d/josm' "${pkgdir}/usr/bin/josm"

  # Remove JavaFX modules references not provided anymore by modern JVMs
  patch -N -p1 --no-backup-if-mismatch "${pkgdir}/usr/bin/josm" "${srcdir}/remove_javafx.patch"

  # Add arch env vars to top of josm script
  # We do this to have a sane default in case the config file gets deleted.
  sed -i '4 aJOSM_PATH="/usr/share/java/josm/josm.jar"' "${pkgdir}/usr/bin/josm"
}
