# Maintainer: Torsten Keßler <tpkessler at archlinux dot org>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Eric Förster <eric DOT foerster AT outlook DOT com>
# Contributor: Patrick Förster <patrick DOT foerster AT outlook DOT de>

pkgname=texlab
pkgver=5.23.1
pkgrel=1
pkgdesc="A cross-platform implementation of the Language Server Protocol for LaTeX."
arch=('x86_64')
url="https://github.com/latex-lsp/texlab/"
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('cargo' 'npm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/latex-lsp/texlab/archive/v${pkgver}.tar.gz")
b2sums=('04d4816d949a4720dd0b1488957f593d0393e256ac6c540c9cf45dbf20dd9601198878f0f24f2be1dcbd9b2fe4a4239aa0e6d816b23637e64a48bc34c4177c5c')
options=('!lto')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
