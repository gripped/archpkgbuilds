# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-application-manager
pkgver=1.2.22
pkgrel=1
pkgdesc='App manager of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-application-manager"
license=('GPL3')
# startdde: org.deepin.dde.XSettings1
depends=('dtk6core' 'qt6-base' 'glibc' 'gcc-libs' 'systemd-libs' 'startdde')
makedepends=('git' 'cmake' 'ninja' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-application-manager.git#tag=$pkgver")
sha512sums=('9aa3c3d3d28c2e187f9f65cee0dd3710a72b894f605da20f46e9fe784575f4492a1784fc5a09e454c49e2223120ab31f3de5dbd214d6630f4a3cbb7322ebf2fb')

build() {
  cd dde-application-manager
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-application-manager
  DESTDIR="$pkgdir" ninja install
  rm -r "$pkgdir"/etc/dpkg
  rmdir "$pkgdir"/etc
}
