# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-application-manager
pkgver=1.2.41
pkgrel=1
pkgdesc='App manager of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-application-manager"
license=('GPL-3.0-or-later')
# deepin-services: org.deepin.dde.XSettings1
depends=('dtk6core' 'qt6-base' 'qt6-wayland' 'glibc' 'gcc-libs' 'systemd-libs' 'deepin-daemon'
         'libxkbcommon')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'treeland-protocols')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-application-manager.git#tag=$pkgver")
sha512sums=('5b68448c38d82a1a9a0a82e6411f406b4462d8580f23cc37d46a2cfa78ca4e5cb9baf526a0a0c7a8b8e183b293b97cda08155a6c55da2465c3df004858b7f9e4')

prepare() {
  cd dde-application-manager
  sed -i 's/WaylandClient/WaylandClient WaylandClientPrivate/' CMakeLists.txt
}

build() {
  cd dde-application-manager
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-application-manager
  DESTDIR="$pkgdir" ninja install
  rm -r "$pkgdir"/etc/dpkg
  rmdir "$pkgdir"/etc
}
