# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-application-manager
pkgver=1.2.29
pkgrel=1
pkgdesc='App manager of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-application-manager"
license=('GPL3')
# startdde: org.deepin.dde.XSettings1
depends=('dtk6core' 'qt6-base' 'glibc' 'gcc-libs' 'systemd-libs' 'startdde')
makedepends=('git' 'cmake' 'ninja' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-application-manager.git#tag=$pkgver")
sha512sums=('a6739e5da02fcac2b939c944c5eb90d9cd45154e31bc98c03e9e36991b38567c7bf01c70f1b382d562fe179b7d10fcda45c9e6f1c0c4ebc90cba865071dfd968')

build() {
  cd dde-application-manager
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-application-manager
  DESTDIR="$pkgdir" ninja install
  rm -r "$pkgdir"/etc/dpkg
  rmdir "$pkgdir"/etc
}
