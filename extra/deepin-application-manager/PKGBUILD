# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-application-manager
pkgver=1.1.6
pkgrel=1
pkgdesc='App manager of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-application-manager"
license=('GPL3')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
# startdde: org.deepin.dde.XSettings1
depends=('qt6-base' 'glibc' 'gcc-libs' 'systemd-libs' 'deepin-wloutput-daemon' 'startdde')
makedepends=('cmake' 'ninja' 'gtest')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-application-manager/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ce1ec9319757ccc90a80dd2ecb5e55989dcf8759cf07e77cbf53ec347b3c35e2f2bc6ccab92b84ef7354ca6d2cf16bd0b7bc2f23de6b6e29296e9d4dd774ffcf')

build() {
  cd dde-application-manager-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-application-manager-$pkgver
  DESTDIR="$pkgdir" ninja install
  rm -r "$pkgdir"/etc/dpkg
  rmdir "$pkgdir"/etc
}
