# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-application-manager
pkgver=1.2.34
pkgrel=1
pkgdesc='App manager of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-application-manager"
license=('GPL3')
# startdde: org.deepin.dde.XSettings1
depends=('dtk6core' 'qt6-base' 'glibc' 'gcc-libs' 'systemd-libs' 'startdde')
makedepends=('git' 'cmake' 'ninja' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-application-manager.git#tag=$pkgver")
sha512sums=('e7b03103d0bc3af863d4db51c5ed04cb815bc62c4623abe4041b8c23156befa236f43643d9ebf80a923eeb882869d1e1f4a59545166cc81d3fbc69056211d669')

build() {
  cd dde-application-manager
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-application-manager
  DESTDIR="$pkgdir" ninja install
  rm -r "$pkgdir"/etc/dpkg
  rmdir "$pkgdir"/etc
}
