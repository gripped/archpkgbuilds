# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-application-manager
pkgver=1.2.40
pkgrel=1
pkgdesc='App manager of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-application-manager"
license=('GPL-3.0-or-later')
# deepin-services: org.deepin.dde.XSettings1
depends=('dtk6core' 'qt6-base' 'qt6-wayland' 'glibc' 'gcc-libs' 'systemd-libs' 'deepin-daemon'
         'libxkbcommon')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'treeland-protocols')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-application-manager.git#tag=$pkgver")
sha512sums=('b367b68d65dc27b051d4499622742a79f771915c6e798ca794784df8cf1a81a7bd082e267779d82cc63d443ed1f4f8b4dc3f23b5f17cadb1310dbd4a2125079c')

prepare() {
  cd dde-application-manager
  sed -i 's/WaylandClient/WaylandClient WaylandClientPrivate/' CMakeLists.txt
}

build() {
  cd dde-application-manager
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-application-manager
  DESTDIR="$pkgdir" ninja install
  rm -r "$pkgdir"/etc/dpkg
  rmdir "$pkgdir"/etc
}
