# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-application-manager
pkgver=1.1.9
pkgrel=1
pkgdesc='App manager of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-application-manager"
license=('GPL3')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
# startdde: org.deepin.dde.XSettings1
depends=('qt6-base' 'glibc' 'gcc-libs' 'systemd-libs' 'deepin-wloutput-daemon' 'startdde')
makedepends=('cmake' 'ninja' 'gtest')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-application-manager/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('325297b09122a6b5c6e0b0adc2b0e97de7bff6fabceb94214450eea8fb6392cb47918c181893d72f288c85a1da1f33c995936b6345b5b99033938e1665727f7d')

build() {
  cd dde-application-manager-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-application-manager-$pkgver
  DESTDIR="$pkgdir" ninja install
  rm -r "$pkgdir"/etc/dpkg
  rmdir "$pkgdir"/etc
}
