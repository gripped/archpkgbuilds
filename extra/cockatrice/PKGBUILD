# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=cockatrice
_pkgname=Cockatrice
pkgver=2.9.0
_date=2023-09-14
_tag=$_date-Release-$pkgver
pkgrel=19
pkgdesc='A cross-platform virtual tabletop for multiplayer card games'
arch=('x86_64')
url='https://cockatrice.github.io'
license=('GPL2')
depends=('qt6-base' 'protobuf' 'qt6-tools' 'qt6-svg' 'qt6-multimedia' 'qt6-websockets' 'qt6-imageformats' 'qt6-translations')
makedepends=('cmake' 'git' 'zlib' 'mariadb-libs')
checkdepends=('gtest' 'valgrind')
optdepends=('zlib: Load zipped files in Oracle'
	    'mariadb-libs: MySQL/MariaDB connection')
provides=('cockatrice-client' 'cockatrice-server')
conflicts=('cockatrice-client' 'cockatrice-server')
source=("git+https://github.com/Cockatrice/Cockatrice.git#tag=$_tag"
         protobuf-23.patch
         'fix-deleting-homedir.patch::https://github.com/Cockatrice/Cockatrice/pull/5135.patch')
sha512sums=('2ff9cf9ebf78d2e7c13cb3204e7ceb7dc3ae2adc1962eb0de12ba64f168495c188961a947895548eaf288ebadebcc63a6d436bd7a3a76e680039136f98f97d8e'
            '42eb9d1dbceee5f4e39a9ed6fa7b7b3b72901b001975b81b629fe0d2d51aef0d1cd4e5d1519d240479679b2532cf0aae73340d5ac2afe3294177288c405c8acd'
            '87183ac53f401644274af6c3bb07d7eafe521b886c88efd9d7d05df8950633cf6f691a5a412de9450272efbd288dca43fea779428a51915ccb67ed6e3c46b7e8')

prepare() {
  cd $_pkgname

  patch -p1 -i ../protobuf-23.patch  # Fix build with protobuf 23
  patch -p1 -i ../fix-deleting-homedir.patch  # Fix bug where the user home directory could be deleted (see https://github.com/Cockatrice/Cockatrice/issues/5134)
}

build() {
  mkdir -p $_pkgname/build
  cd $_pkgname/build

  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_CXX_STANDARD=17 \
  	-DWITH_ORACLE=ON \
  	-DWITH_CLIENT=ON \
  	-DWITH_SERVER=ON \
  	-DTEST=ON

  make
}

check() {
  cd $_pkgname/build

  make test
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
