# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=cockatrice
_pkgname=Cockatrice
pkgver=2.10.2
_date=2025-04-03
_tag=$_date-Release-$pkgver
pkgrel=3
pkgdesc='A cross-platform virtual tabletop for multiplayer card games'
arch=('x86_64')
url='https://cockatrice.github.io'
license=('GPL2')
depends=('qt6-base' 'protobuf' 'qt6-tools' 'qt6-svg' 'qt6-multimedia' 'qt6-websockets' 'qt6-imageformats' 'qt6-translations')
makedepends=('cmake' 'git' 'zlib' 'mariadb-libs')
checkdepends=('gtest' 'valgrind')
optdepends=('zlib: Load zipped files in Oracle'
	    'mariadb-libs: MySQL/MariaDB connection')
provides=('cockatrice-client' 'cockatrice-server')
conflicts=('cockatrice-client' 'cockatrice-server')
source=("git+https://github.com/Cockatrice/Cockatrice.git#tag=$_tag")
sha512sums=('e0f921bf29b8e71e1b1f1884d19b1c2b7a53833045c5e8176322f80113bd6bbabde7cdf90b4ca1e7b7af15d117b5993069c4dc45c89152b866e107e9cf531937')

build() {
  mkdir -p $_pkgname/build
  cd $_pkgname/build

  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_CXX_STANDARD=17 \
  	-DWITH_ORACLE=ON \
  	-DWITH_CLIENT=ON \
  	-DWITH_SERVER=ON \
  	-DTEST=ON

  make
}

check() {
  cd $_pkgname/build

  make test
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
