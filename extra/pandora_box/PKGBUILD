# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=pandora_box
_pkgname=pandora
pkgver=0.17.0
pkgrel=1
pkgdesc="Syd's log inspector & profile writer"
url="https://gitlab.exherbo.org/sydbox/sydbox/-/tree/main/pandora"
arch=('x86_64')
license=('GPL-3.0-only')
depends=('gcc-libs' 'glibc' 'syd')
makedepends=('cargo' 'git')
provides=("${_pkgname}")
source=("git+https://gitlab.exherbo.org/sydbox/sydbox.git#tag=${_pkgname}-${pkgver}?signed")
sha256sums=('b15489d233546c51f2df4c6d32145e287ed6d862277d57cee76891200043f5c1')
validpgpkeys=('5DF763560390A149AC6C14C7D076A377FB27DE70')

prepare() {
	cd "sydbox/${_pkgname}"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
	cd "sydbox/${_pkgname}"
	cargo build --frozen --release --all-features
}

package() {
	cd "sydbox/${_pkgname}"
	install -Dm 755 "target/release/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
