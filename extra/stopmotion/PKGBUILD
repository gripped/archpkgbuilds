# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=stopmotion
pkgver=0.9.0
pkgrel=2
pkgdesc='Program to create stop motion animations'
arch=(x86_64)
url='https://invent.kde.org/multimedia/stopmotion'
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libarchive
  libvorbis
  libxml2
  qt6-base
  qt6-multimedia
)
makedepends=(
  cmake
  git
  ninja
  qt6-tools
)
source=(
  "git+https://invent.kde.org/multimedia/stopmotion.git#tag=$pkgver"
  stopmotion-path-settings.patch
  stopmotion-install-icons.patch
)
b2sums=(
  f9875213269777030672ab88639a9e653a357605577ca63f62613206c6d25352440dd9691b9dadbbef36667996fd0fcaa74cc531a9c3e623a536194ab1c7ca19
  9548b1a9e15ef5c67f8d0bf2797eb418a846163f57764fca9f08a7b06411126411f109f2f374a4ff44e18c2109dbc4af9a656831816259002f10b74e0230773a
  3052918d2d20bd521cfe9d9f458118a12f613105aa8cc4e914d2b69d4e5004ab6b9ab8c68c04ef794f65c520f6772422aaf1fdf477ff1c9599f58df09e5d06b8
)

prepare() {
  cd $pkgname

  # Fix setting path for translation and HTML documentation
  # https://invent.kde.org/multimedia/stopmotion/-/merge_requests/33
  git apply -3 ../stopmotion-path-settings.patch

  # Install application icon into hicolor directory
  # https://invent.kde.org/multimedia/stopmotion/-/merge_requests/34
  git apply -3 ../stopmotion-install-icons.patch

  # Unset colors in manuals
  # https://invent.kde.org/multimedia/stopmotion/-/merge_requests/35
  sed -i -e '/BGCOLOR=/d' -e '/TEXT=/d' -e '/LINK=/d' manual/*.html
}

build() {
  cmake -S $pkgname -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D HTML_PATH=/usr/share/doc/stopmotion/manual \
    -D TRANSLATIONS_PATH=/usr/share/stopmotion/translations
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
