# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidinet
pkgver=1.0.1
pkgrel=2
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=(x86_64)
url="https://qmidinet.sourceforge.io"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  jack
  qt6-tools
)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('9b72ea9b88e5b3da63ca63b72098996a3b04e7426231008927d90627a838efe02d155c778362a385cbb4dbc227e1d3a86adc52e001603002b8b0a2a7ca8def01')
b2sums=('21502e469bf088b0513621fb4d7021324d6bc0815f3af919cdea64023424cf78b8e89be8e3a29622508fb2026598cd85fc676668cb4dea6381d897441a1a143b')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
    jack libjack.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
