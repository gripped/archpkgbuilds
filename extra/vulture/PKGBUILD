# Maintainer: Leonidas Spyropoulos <artafinde at archlinux dot org>

pkgname=vulture
pkgver=2.14
pkgrel=1
pkgdesc="Finds dead code in Python projects"
arch=('any')
url="https://github.com/jendrikseipp/vulture"
license=('MIT')
depends=(python-setuptools python-toml)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pytest python-pint)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('eac2cd25a3d38d7fac82c44827bf314e07d8f5426621aa16e5750972debaaf07')
b2sums=('056068d7532bedf45d1594ffac48e6fe47b64c0a7483b3a1101f8081f7153aa43ed9a666a3676f62d99ab30b44cf04e99291b468d10a278259fc35dff023635b')

build() {
  cd "${pkgname}"-"${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname}"-"${pkgver}"
  pytest
}

package() {
  cd "${pkgname}"-"${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
