# Maintainer: Leonidas Spyropoulos <artafinde at archlinux dot org>

pkgname=vulture
pkgver=2.12
pkgrel=1
pkgdesc="Finds dead code in Python projects"
arch=('any')
url="https://github.com/jendrikseipp/vulture"
license=('MIT')
depends=(python-setuptools python-toml)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pytest python-pint)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('840236cd766cf6389fe61a0c1b77bccdc2243373b264b8acb81ce70c090fe4d9')
b2sums=('cad2f32fc848d740dc4dcef3db5a8137cb976e43d176cc42ebf12ff30f66da2f147c1c650b338b6bcd5911b80b07b8a1f81e129972b498be9bb760b0af9d1435')

build() {
  cd "${pkgname}"-"${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname}"-"${pkgver}"
  pytest
}

package() {
  cd "${pkgname}"-"${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
