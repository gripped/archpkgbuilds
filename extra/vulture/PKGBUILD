# Maintainer: Leonidas Spyropoulos <artafinde at archlinux dot org>

pkgname=vulture
pkgver=2.13
pkgrel=2
pkgdesc="Finds dead code in Python projects"
arch=('any')
url="https://github.com/jendrikseipp/vulture"
license=('MIT')
depends=(python-setuptools python-toml)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pytest python-pint)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('569476889ce8812fbd3a3cdc0726be4e4e19d770231cc8c952f3dd6c4c306e31')
b2sums=('f1d478a04155e6eecd0b954ef2cf19aa59ee172e6b14855d9a99decf0cc221723c0d5982e161fe20cd714148271ada9cfb9e954aa48f1905eeebf1454fcdc9ff')

build() {
  cd "${pkgname}"-"${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname}"-"${pkgver}"
  pytest
}

package() {
  cd "${pkgname}"-"${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
