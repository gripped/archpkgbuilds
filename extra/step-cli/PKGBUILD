# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-cli
pkgver=0.27.4
pkgrel=1
pkgdesc="A zero trust swiss army knife for working with X509, OAuth, JWT, OATH OTP, etc."
url="https://github.com/smallstep/cli"
arch=(x86_64)
license=(Apache-2.0)
depends=(glibc)
makedepends=(go)
optdepends=("step-ca: setup CA/SSO/ACME server")
source=($pkgname-$pkgver.tar.gz::https://github.com/smallstep/cli/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('80945ee6a159a9d838d2ce1040f7132a51a9bb3c1c72868a24864d2526f33eec2c2ef24119a3b0e778e1316eb3346884af542320d068ed2e4abcaea2f794cfe6')

prepare() {
  cd cli-${pkgver}
  sed -i "s/step/${pkgname}/g" "autocomplete/zsh_autocomplete"
  sed -i "s/step/${pkgname}/g" "autocomplete/bash_autocomplete"
}

build() {
  export BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%d)"

  cd cli-${pkgver}
  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o step-cli cmd/step/main.go
}

check() {
  cd cli-${pkgver}
  go test ./...
}

package() {
  cd cli-${pkgver}
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 "autocomplete/bash_autocomplete" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "autocomplete/zsh_autocomplete" "$pkgdir/usr/share/zsh/site-functions/_${pkgname}"
}
