# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-cli
pkgver=0.27.5
pkgrel=1
pkgdesc="A zero trust swiss army knife for working with X509, OAuth, JWT, OATH OTP, etc."
url="https://github.com/smallstep/cli"
arch=(x86_64)
license=(Apache-2.0)
depends=(glibc)
makedepends=(go)
optdepends=("step-ca: setup CA/SSO/ACME server")
source=($pkgname-$pkgver.tar.gz::https://github.com/smallstep/cli/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('549d59f048800b398068e7230c051faec80ac59d13a12dcbfb4f6557e15302f3c5f26dcdf454adb5dd239c020f1841003369bfd3c0ec70597e470f594067bbb4')

prepare() {
  cd cli-${pkgver}
  sed -i "s/step/${pkgname}/g" "autocomplete/zsh_autocomplete"
  sed -i "s/step/${pkgname}/g" "autocomplete/bash_autocomplete"
}

build() {
  export BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%d)"

  cd cli-${pkgver}
  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o step-cli cmd/step/main.go
}

check() {
  cd cli-${pkgver}
  go test ./...
}

package() {
  cd cli-${pkgver}
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 "autocomplete/bash_autocomplete" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "autocomplete/zsh_autocomplete" "$pkgdir/usr/share/zsh/site-functions/_${pkgname}"
}
