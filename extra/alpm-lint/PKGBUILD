# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-lint
_upstream=alpm
pkgver=0.2.0
pkgrel=1
pkgdesc="Linting of ALPM packaging related files and projects"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-lint/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  alpm-pkgbuild-bridge
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('1bec1f77956cae42fa1b501bcbc16626404f9b001ce78cf95345909f654ef2185ae9b1ab9c4ceb732a3a1efe9c3490cb9bb0119587b7e9463285433b2fe40b04')
b2sums=('53d056e74d5e492f657e163c51a2ca6a1eb01a1aa382b918ee6fefb157c2142c5f3ea0645c8398e706952a79c3e1b85e3c372f67a9d9ddb24be576252e788db7')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
