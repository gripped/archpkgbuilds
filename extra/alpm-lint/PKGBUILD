# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-lint
_upstream=alpm
pkgver=0.3.0
pkgrel=1
pkgdesc="Linting of ALPM packaging related files and projects"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-lint/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  alpm-pkgbuild-bridge
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('0daef3fc9c68dffa92c05c09447a1671cf2640bd9011e77c004639b660025e608c8f244b70c322877d82257c94153bccf33659d50eff0b49316d2591fe82d325')
b2sums=('11a9cfd2be65cd34c36444074628a6a0a1df50a19f05b424b258694cea2c27c8e7ee81f785aa7e691749f56efb3199926bd548e4801bb5eeabc3d8e7ad1673a3')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
