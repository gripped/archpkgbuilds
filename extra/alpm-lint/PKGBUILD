# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-lint
_upstream=alpm
pkgver=0.1.0
pkgrel=1
pkgdesc="Linting of ALPM packaging related files and projects"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-lint/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  alpm-pkgbuild-bridge
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('9e8c06e84b3f334c0d73b3c5b5c88297f1379b3529c261351fcb99b45cd51d30d8fe503c1cc070bd8c56355ec2625e814ee11d548deb429ff5c49dc9a7dcffb6')
b2sums=('7771743d2b926af9cae567caf1d43e763fb9c3c3005f97981be381101adaff8d7f3eec8cd0303245c646772eb6c2e973f0998d079b3ca183bc1824ec1c5ac5d4')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --features cli --package $pkgname

  # shell completion generation is currently broken: https://gitlab.archlinux.org/archlinux/alpm/alpm/-/issues/230
  # just generate shell_completions $pkgname
  # man pages are currently not correct: https://gitlab.archlinux.org/archlinux/alpm/alpm/-/issues/231
  # just generate manpages $pkgname
}

check() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  # install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  # install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  # install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  # install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
