# Maintainer: Martin Rys <https://rys.rs/contact>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Sibren Vasse <arch@sibrenvasse.nl>
pkgname=python-casttube
_pkgname=casttube
pkgver=0.2.1
pkgrel=7
pkgdesc="YouTube Chromecast API"
arch=('any')
url="http://github.com/ur1katz/casttube"
license=('MIT')
depends=('python-requests')
makedepends=(
	'python-build'
	'python-installer'
	'python-setuptools'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ur1katz/casttube/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('351819818a10a107641675cab71c4154afb490762410b3138c18ef410cbf5c33')

build() {
	cd "${_pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	mv "${pkgdir}/usr/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}"
}
