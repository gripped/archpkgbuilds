# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=freeciv
pkgver=3.2.1
pkgrel=4
pkgdesc="A multiuser clone of the famous Microprose game of Civilization"
arch=('x86_64')
url="http://www.freeciv.org/"
license=('GPL-2.0-or-later')
depends=(
  'bzip2'
  'cairo'
  'curl'
  'gcc-libs'
  'gdk-pixbuf2'
  'glib2'
  'glibc'
  'gtk4'
  'hicolor-icon-theme'
  'icu'
  'lua'
  'pango'
  'qt6-base'
  'readline'
  'sdl2'
  'sdl2_mixer'
  'sqlite'
  'xz'
  'zlib'
  'zstd'
)
makedepends=(
  'meson'
  'python'
)
backup=('etc/freeciv/database.lua')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz")
sha512sums=('8667d756b67b0e1a63ad3015f6e33e0dacbda5bbbf85dae92238c88f4d660536623f4950d4461ab35a01764c767add138bc1c4f794a842cd47be9d62d1f2199b')

build() {
  arch-meson $pkgname-$pkgver build \
    -Dsyslua=true
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
