# Maintainer: Laurent Carlier <lordheavym@archlinux.org>
# Contributor: Martin Tuma <tumic@cbox.cz>

pkgbase=qtpbfimageplugin
pkgname=(qtpbfimageplugin-qt5 qtpbfimageplugin-qt6)
pkgver=3.2
pkgrel=4
pkgdesc='Qt image plugin for displaying Mapbox vector tiles'
arch=(x86_64)
url='https://github.com/tumic0/QtPBFImagePlugin'
license=('LGPL3')
depends=('protobuf' 'zlib')
makedepends=('qt5-base' 'qt6-base')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/tumic0/QtPBFImagePlugin/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('03d7aa364047106f5ebd1bb5bd58a5bcea7b77062a578c761b0805561fdfd955')

prepare() {
  mkdir build5
  mkdir build6
}

build() {
  cd build5

  qmake ../QtPBFImagePlugin-${pkgver}
  make

  cd  ../build6
  qmake6 ../QtPBFImagePlugin-${pkgver}
  make
}

package_qtpbfimageplugin-qt5() {
  depends+=(qt5-base)
  cd build5

  make install INSTALL_ROOT="$pkgdir"
  install -d 755 ${pkgdir}/usr/share/mime/packages
  install -m 644 ../QtPBFImagePlugin-${pkgver}/pkg/pbfplugin.xml ${pkgdir}/usr/share/mime/packages/qtpbfimageplugin-qt5.xml
}

package_qtpbfimageplugin-qt6() {
  depends+=(qt6-base)
  cd build6

  make install INSTALL_ROOT="$pkgdir"
  install -d 755 ${pkgdir}/usr/share/mime/packages
  install -m 644 ../QtPBFImagePlugin-${pkgver}/pkg/pbfplugin.xml ${pkgdir}/usr/share/mime/packages/qtpbfimageplugin-qt6.xml
}

