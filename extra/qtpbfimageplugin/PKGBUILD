# Maintainer: Laurent Carlier <lordheavym@archlinux.org>
# Contributor: Martin Tuma <tumic@cbox.cz>

pkgbase=qtpbfimageplugin
pkgname=(qtpbfimageplugin-qt5 qtpbfimageplugin-qt6)
pkgver=4.0
pkgrel=2
pkgdesc='Qt image plugin for displaying Mapbox vector tiles'
arch=(x86_64)
url='https://github.com/tumic0/QtPBFImagePlugin'
license=('LGPL-3.0-only')
makedepends=('qt5-base' 'qt6-base')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/tumic0/QtPBFImagePlugin/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('f2d5c0e74f99a4e5a103dcede8c7fb129afe366bc8a4d055044770458d4280d9')

prepare() {
  mkdir build5
  mkdir build6
}

build() {
  cd build5

  qmake ../QtPBFImagePlugin-${pkgver}
  make

  cd  ../build6
  qmake6 ../QtPBFImagePlugin-${pkgver}
  make
}

package_qtpbfimageplugin-qt5() {
  depends=(qt5-base)
  cd build5

  make install INSTALL_ROOT="$pkgdir"
  install -d 755 ${pkgdir}/usr/share/mime/packages
  install -m 644 ../QtPBFImagePlugin-${pkgver}/pkg/pbfplugin.xml ${pkgdir}/usr/share/mime/packages/qtpbfimageplugin-qt5.xml
}

package_qtpbfimageplugin-qt6() {
  depends=(qt6-base)
  cd build6

  make install INSTALL_ROOT="$pkgdir"
  install -d 755 ${pkgdir}/usr/share/mime/packages
  install -m 644 ../QtPBFImagePlugin-${pkgver}/pkg/pbfplugin.xml ${pkgdir}/usr/share/mime/packages/qtpbfimageplugin-qt6.xml
}

