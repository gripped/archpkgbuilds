# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libspecbleach
pkgver=0.2.0
pkgrel=1
pkgdesc="C library for audio noise reduction"
arch=(x86_64)
url="https://github.com/lucianodato/libspecbleach"
license=(LGPL-2.1-or-later)
depends=(
  glibc
)
makedepends=(
  fftw
  meson
)
provides=(libspecbleach.so)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('52c03116dd5559e74913df101d6ff491042a55d09bf49e988341ace3b50ce61de6c1e396234965d57dc11c5c0a1b47b46661c08aa285da7b268aceb69bbed189')
b2sums=('6f79fd30f639c766e865bd6709e10eb1d54be0efd63b7b0299aa69186e632e1dcca25408f45dfb1593a6b2599fb9eb6a49b1a229cfc9ff4a7251f7765844fe7c')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  depends+=(
    fftw libfftw3f.so
  )

  meson install -C build --destdir="$pkgdir"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/examples/* -t "$pkgdir/usr/share/doc/$pkgname/example/"
}
