# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Olaf Leidinger <oleid@mescharet.de>
# Contributor: fabien Cellier <fabien.cellier@gmail.com>

pkgname=pocl
pkgver=6.0.r843.g3b6e5c4
pkgrel=3
pkgdesc="Portable OpenCL is an open-source implementation of OpenCL which can be easily adapted for new targets"
arch=('x86_64')
url="http://portablecl.org/"
license=('MIT')
source=("git+https://github.com/pocl/pocl#commit=3b6e5c4b47187d8a954194d673a47e6721e45719"
        "pocl-link-cudart.patch::https://github.com/pocl/pocl/pull/1804.patch")
depends=('clang' 'hwloc' 'opencl-icd-loader')
makedepends=('llvm' 'cmake' 'opencl-headers' 'ocl-icd' 'ninja' 'python' 'git' 'nvidia-utils' 'cuda' 'cudnn')
optdepends=(
  'cuda: CUDA driver'
  'cudnn: CUDA driver'
)
sha512sums=('a05426f0e89f424ffa1b403eaf658af29302314c6f20de26316f854b6d8a03dc2823999ea2974358bc54a760073d169beefeb833b72715797682b08960c2a1a7'
            '33ea8a57617674466f6a6f0e5d1b5d902b818450ed8fd471043b7bab27c13fc8757e7d188ef7e695ce47f256e33ffd7d422bf52e4bb0fe65682fbee54b25bb06')

pkgver() {
  cd "$pkgname"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/RC[0-9]*//;s/\([^-]*-g\)/r\1/;s/-/./g;s/\.\././g'
}

prepare() {
  cd "$pkgname"
  # https://github.com/pocl/pocl/pull/1804
  patch -Np1 -i ../pocl-link-cudart.patch
}

build() {
  cd "$pkgname"
  local cmake_options=(
      -GNinja
      -Bbuild
      -DCMAKE_INSTALL_PREFIX=/usr
      -DCMAKE_INSTALL_LIBDIR=lib
      -DCMAKE_BUILD_TYPE=Release
      -DKERNELLIB_HOST_CPU_VARIANTS=distro
      # Should be provided by opencl-headers, avoid conflict
      -DINSTALL_OPENCL_HEADERS=OFF
      -DENABLE_CUDA=ON
      -DENABLE_CUDNN=ON
      -DCUDA_TOOLKIT_ROOT_DIR=/opt/cuda
  )
  cmake "${cmake_options[@]}"
  ninja -C build
}

check() {
  cd "$pkgname"
  ninja -C build check
}

package() {
  cd "$pkgname"
  DESTDIR="$pkgdir"/ ninja -C build install
  install -v -Dm 644 -t "$pkgdir"/usr/share/licenses/"$pkgname" LICENSE
}
