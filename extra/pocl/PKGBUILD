# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Olaf Leidinger <oleid@mescharet.de>
# Contributor: fabien Cellier <fabien.cellier@gmail.com>

pkgname=pocl
pkgver=6.0.r322.gf0e12ad
pkgrel=1
pkgdesc="Portable OpenCL is an open-source implementation of OpenCL which can be easily adapted for new targets"
arch=('x86_64')
url="http://portablecl.org/"
license=('MIT')
source=("git+https://github.com/pocl/pocl#commit=f0e12ada3320885cdba6e181d324a9e9f547b9fc")
depends=('clang' 'hwloc' 'opencl-icd-loader')
makedepends=('llvm' 'cmake' 'opencl-headers' 'ocl-icd' 'ninja' 'python' 'git')
sha512sums=('4b069296d0d83630dc364c12bc729e4599a50c78b38f718786a7fea87b8e034ebba1c4ca441f7d11465e4dd5b962c77ee524f433b8ffba67684eb3c9d0637674')

pkgver() {
  cd "$pkgname"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/RC[0-9]*//;s/\([^-]*-g\)/r\1/;s/-/./g;s/\.\././g'
}

build() {
  cd "$pkgname"
  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DKERNELLIB_HOST_CPU_VARIANTS=distro
  ninja -C build
}

check() {
  cd "$pkgname"
  ninja -C build check
}

package() {
  cd "$pkgname"
  DESTDIR="$pkgdir"/ ninja -C build install
  install -v -Dm 644 -t "$pkgdir"/usr/share/licenses/"$pkgname" LICENSE
}
