# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.31
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# libqtxdg: doesn't compile yet
depends=('glibc' 'dtk6core' 'librsvg' 'qt6-base' 'qt6-wayland' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen' 'treeland-protocols')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver"
         qt-6.9.patch)
sha512sums=('198a979b8ef47903ae4e3e80fc0b2c04108881916531e94c7a4bdfcf2c127a05a76d580287cc7e2ef1e3a7fa42e064bc2f87e7c5d779f2178b10ddb5f25348bc'
            'df65da2c85a4ffb1c7ba251aab2da8792bde7a64ec05160b19d835b0a85a89230241c5e3daa8ddc3fedf868cf37351c4661e7446479d0769357fa8d49f633d81')

prepare() {
  patch -d $pkgname -p1 < qt-6.9.patch
}

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DDTK_DISABLE_EX_IMAGE_FORMAT=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
