# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.26
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# libqtxdg: doesn't compile yet
depends=('glibc' 'dtk6core' 'librsvg' 'qt6-base' 'qt6-wayland' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen' 'treeland-protocols')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver")
sha512sums=('e569a517aff8e78848e91f916a0fb862d8514680d2671f03edb58e7c2043ebaf015c06bd11d08e14138fee24d586050fb5e53c4f5b7937668f37298a3c515eac')

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DDTK_DISABLE_EX_IMAGE_FORMAT=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
