# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.33
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# libqtxdg: doesn't compile yet
depends=('glibc' 'dtk6core' 'librsvg' 'qt6-base' 'qt6-wayland' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen' 'treeland-protocols')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver"
         qt-6.9.patch
         pwd-include.patch)
sha512sums=('537ae9ca0d1f78698f480c4737b952afac0e23c6717efd9429ec68c72e9bf0a654dcf9c25554d03c58cb4b2f28e700d819a2767752ae42661b645d17bc184c6d'
            'df65da2c85a4ffb1c7ba251aab2da8792bde7a64ec05160b19d835b0a85a89230241c5e3daa8ddc3fedf868cf37351c4661e7446479d0769357fa8d49f633d81'
            '4c4118e7f5657a9fcee40c30bc47b4d943f4fc7ad1a967bca5709bd75c3f1231f24a7f36f8f77caed630fd6ee46a104d4abbb0419d5d26ecc4ab474357988fd1')

prepare() {
  patch -d $pkgname -p1 < qt-6.9.patch
  patch -d $pkgname -p1 < pwd-include.patch
}

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DDTK_DISABLE_EX_IMAGE_FORMAT=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
