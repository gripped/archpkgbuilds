# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.19
pkgrel=2
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# 'libqtxdg' disabled until it supports Qt6
depends=('glibc' 'dtk6core' 'freeimage' 'libraw' 'librsvg' 'qt6-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver"
         qt-6.8.patch)
sha512sums=('7a7c49c193529f62a9e2ccb0dc06b27c8e9afa9b701398efe1fe41d5b1daa22946a42bb5ebe32b61b4b7470c7e5d63df5281762c48a1fd3cabadf9b67a196648'
            'f180187d8e53e31f16bbb9270f8203e8dfd7ecf1853268011c18c08107a3777c6d68c1fa921a500dc293512cd9e0aaf701656032613dca2408a307fc64e46265')

prepare() {
  cd dtk6gui
  patch -p1 -i ../qt-6.8.patch
}

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
