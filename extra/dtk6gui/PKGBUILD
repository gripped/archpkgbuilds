# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.10
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# 'libqtxdg' disabled until it supports Qt6
depends=('glibc' 'dtk6core' 'freeimage' 'libraw' 'librsvg' 'qt6-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver")
sha512sums=('822118ec8ad89ca1721a46e94de59cbaac62ccfc88fc4fa8f8c9ce04dddebeb482189c841799270cc8d9ea205549101aa1eb6b0beabfa6be8cc67608a9a5cd16')

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
