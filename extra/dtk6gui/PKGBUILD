# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.43
pkgrel=3
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL-3.0-or-later')
# libqtxdg: doesn't compile yet
depends=('glibc' 'dtk6core' 'librsvg' 'qt6-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen' 'treeland-protocols')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('84ff1b003216454c5a1b23a41f8c263a4f3bbdfb03b90822f0cbc27eec36be1d2d748866abca5e042dcc97ad8a635bed0dc0bca9a5c321fc69b5e968f55f93e6'
            'b83167acbd7a851d61ec35d3f28d221e0d4b89e2559f4d29e721627a211a6db0b357387c62634583b9b2153649d35ab03c3c77c02b85a9092c4893a0083bcce9')

prepare() {
  cd dtk6gui
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DDTK_DISABLE_EX_IMAGE_FORMAT=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
