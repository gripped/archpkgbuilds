# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.36
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# libqtxdg: doesn't compile yet
depends=('glibc' 'dtk6core' 'librsvg' 'qt6-base' 'qt6-wayland' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen' 'treeland-protocols')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver"
         qt-6.9.patch
         pwd-include.patch)
sha512sums=('fe6a6679e9de9472dd352884cfd5183a95396153e5d92f01918c92d711e26b3715944707210bf3146b05bf23880c68af77379f4071ad43450b3b721718d544c4'
            'f5a778971f2387d2ba9abc7f6a7a0f887adcc7b605eb9d73daea40d4d98a68122db1cdea068625f48f4c28ccb91752ba64a9c3c7943a07a9b058901c8ceb2b47'
            '4c4118e7f5657a9fcee40c30bc47b4d943f4fc7ad1a967bca5709bd75c3f1231f24a7f36f8f77caed630fd6ee46a104d4abbb0419d5d26ecc4ab474357988fd1')

prepare() {
  patch -d $pkgname -p1 < qt-6.9.patch
  patch -d $pkgname -p1 < pwd-include.patch
}

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DDTK_DISABLE_EX_IMAGE_FORMAT=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
