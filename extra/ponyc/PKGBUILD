# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=ponyc
pkgver=0.59.0
pkgrel=1
pkgdesc='An actor model, capabilities, high performance programming language'
arch=('x86_64')
url='https://ponylang.org/'
license=('BSD-2-Clause')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cmake'
  'git'
  'python'
)
options=('!lto')
source=(
  "git+https://github.com/ponylang/ponyc.git#tag=$pkgver"
  "git+https://github.com/llvm/llvm-project.git"
  "ponyc-exclude-failing-test.patch"
  "ponyc-remove-usr-local-refs.patch"
)
sha512sums=('e0ce37c87102d7a952436a708cb31e374ab3041ed27bcd835f30c2691fd3ea5c76dbc43b20c8a70f1da91e114e6a97f055bd6d02360db9362615d9febdd7d8e1'
            'SKIP'
            'dc4a160216727d5b946d8a359866b38ffc7c8d00c98ea1e1a105c191781265a2ecfa33afc283ecc23652012a811037aaa5dbe99e75566f26ff9eeecafa7934ab'
            'abbc3feb2802d7151f546616e1d9d353526e2ececfb7733fa810a59d63066c59ba0321321970fc82a1eceb71ff5a760e5bc77acc7af08bb72805635a7ae62ea8')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.lib/llvm/src.url "$srcdir/llvm-project"
  git -c protocol.file.allow=always submodule update \
    lib/llvm/src

  patch -Np1 < ../ponyc-exclude-failing-test.patch
  patch -Np1 < ../ponyc-remove-usr-local-refs.patch
}

build() {
  cd $pkgname
  make prefix=/usr build_flags="$MAKEFLAGS" libs
  make prefix=/usr build_flags="$MAKEFLAGS" arch=x86-64 configure
  make prefix=/usr build_flags="$MAKEFLAGS" build

  mkdir docs
  build/release/ponyc packages/stdlib -rexpr -g -o docs
}

check() {
  cd $pkgname
  make prefix=/usr build_flags="$MAKEFLAGS" test
}

package() {
  cd $pkgname
  make build_flags="$MAKEFLAGS" DESTDIR="$pkgdir/usr/lib/pony" install

  install -vdm755 "$pkgdir/usr/bin"
  ln -vs -t "$pkgdir/usr/bin" /usr/lib/pony/bin/ponyc

  install -vdm755 "$pkgdir/usr/lib"
  ln -vs -t "$pkgdir/usr/lib" /usr/lib/pony/lib/native/libponyc.a
  ln -vs -t "$pkgdir/usr/lib" /usr/lib/pony/lib/native/libponyrt-pic.a
  ln -vs -t "$pkgdir/usr/lib" /usr/lib/pony/lib/native/libponyrt.a

  install -vdm755 "$pkgdir/usr/include/pony/detail"
  ln -vs -t "$pkgdir/usr/include" /usr/lib/pony/include/pony.h
  ln -vs -t "$pkgdir/usr/include/pony/detail" /usr/lib/pony/include/pony/detail/atomics.h

  install -vdm755 "$pkgdir/usr/share/doc/$pkgname"
  cp -va -t "$pkgdir/usr/share/doc/$pkgname" docs/*

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
