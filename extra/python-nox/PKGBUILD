# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nox
pkgname=python-$_pkgname
pkgver=2025.11.12
pkgrel=2
pkgdesc='Flexible test automation for Python'
arch=(any)
url='https://github.com/wntrblm/nox'
license=(Apache-2.0)
depends=(
  python
  python-argcomplete
  python-attrs
  python-colorlog
  python-dependency-groups
  python-humanize
  python-packaging
  python-virtualenv
)
makedepends=(
  python-build
  python-installer
  python-hatchling
)
checkdepends=(
  python-pytest
  python-jinja
  python-tox
  uv
)
optdepends=(
  'python-jinja: tox_to_nox'
  'python-tox: tox_to_nox'
  'uv: uv backend'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('fe660f99263ef950e79ae70bd4beeee88046bf2f838e36e33ac74fba5bdd3d36f898a2d7194716f832222a13061f52ad6c03da7f4e3fc5c296ca6db04a48e13f')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  local pytest_options=(
    -vv
    # subprocess.CalledProcessError: Command '['uv', 'pip', 'install', 'nox @ git+https://github.com/wntrblm/nox.git@2024.10.09']' returned non-zero exit status 1.
    --deselect tests/test_main.py::test_noxfile_script_mode_url_req
  )

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}"
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
