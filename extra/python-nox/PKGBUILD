# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nox
pkgname=python-$_pkgname
pkgver=2024.10.09
pkgrel=1
pkgdesc='Flexible test automation for Python'
arch=('any')
url='https://github.com/wntrblm/nox'
license=('Apache')
depends=('python' 'python-argcomplete' 'python-colorlog' 'python-virtualenv' 'python-packaging')
optdepends=('python-jinja: tox_to_nox'
            'python-tox: tox_to_nox')
makedepends=('python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest' 'python-jinja' 'python-tox')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
         28bbaa5a.patch)
sha512sums=('51d1f70ba94d01abd10ba3a12896facd13a7a2881496221b6fa9d0a43ff947303b0cc975a29169a3d9e0174277e23e0bc4b1137b2774c1abffa23a48c9d69a11'
            '932a70916b6216b59d7352d5d1cdbcd9906f07b3c86611c15ba230e85c7b35b685f1273f26c447a7ac9a7205dcc30cc8c648694bf1faf0a21d6d4cc48d28cea8')

prepare() {
  cd $_pkgname-$pkgver
  patch -p1 -i ../28bbaa5a.patch # Fix tests
}

build() {
  cd $_pkgname-$pkgver

  python -m build -nw
}

check() {
  cd $_pkgname-$pkgver

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
# tox 4 isn't supported https://github.com/wntrblm/nox/issues/673
  test-env/bin/python -m pytest -k 'not test_tox_to_nox'
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
