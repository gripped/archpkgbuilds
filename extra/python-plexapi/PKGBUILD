# Maintainer: Martin Rys <https://rys.rs/contact>
# Contributor: mutantmonkey <aur@mutantmonkey.mx>
# Contributor: Kyle Sferrazza <kyle.sferrazza@gmail.com>
# Contributor: Dimitrios Vogiatzis <me@dimtree.net>

pkgname=python-plexapi
_pkgname=plexapi
pkgver=4.17.2
pkgrel=4
pkgdesc="Python bindings for the Plex API"
arch=('any')
url="https://github.com/pkkid/python-plexapi"
license=('BSD-3-Clause')
depends=(
	'python-requests'
	'python-tqdm'
	'python-websocket-client'
)
makedepends=(
	'python-build'
	'python-installer'
	'python-setuptools'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pkkid/python-plexapi/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('4aeca2241987368ab8e17b6b345af3929a00bb7b107456bc5690ec505b2a4346')

build() {
	cd "${pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl
	install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
