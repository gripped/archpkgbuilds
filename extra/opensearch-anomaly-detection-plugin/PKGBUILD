# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-anomaly-detection-plugin
pkgver=2.18.0.0
pkgrel=1
_opensearchver=2.18.0
_jdkver=11
pkgdesc="OpenSearch Anomaly Detection Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/monitoring-plugins/ad"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/anomaly-detection/archive/${pkgver}.tar.gz"
)
sha256sums=('7d8e45e2bb359e76119f1d9730100cc1f32e4ea92926c5512b61dc1403a9f90b')

build() {
  cd "anomaly-detection-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-anomaly-detection"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-anomaly-detection"
  unzip "${srcdir}/anomaly-detection-${pkgver}/build/distributions/opensearch-anomaly-detection-${pkgver}.zip"

  install -Dm644 "${srcdir}/anomaly-detection-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
