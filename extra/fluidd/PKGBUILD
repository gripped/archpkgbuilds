# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=fluidd

pkgver=1.35.1
pkgrel=1
pkgdesc='A free and open-source Klipper web interface for managing your 3d printer'
arch=(any)
url=https://docs.fluidd.xyz/
license=(GPL-3.0-only)
makedepends=(
  git
  nodejs-lts-iron
  npm
)
optdepends=('ustreamer: webcam streaming support')
_tag=238dba1c0d9f783e53b461709eca6436f67e6f95
source=(git+https://github.com/fluidd-core/fluidd.git#tag=${_tag})
validpgpkeys=(4C717BDC0E452F5B13490409AB4C64C58BA9C3ED) # Pedro Lamas <pedrolamas@gmail.com>
b2sums=('cecf2de94984e148778186dbed2029176491cfeccc6281be66c985b39e4f1564e48c3809335abf68b995d1706c69644738359c986fe1549e7f9247ff3f54103b')

pkgver() {
  cd fluidd
  git describe --tags | sed 's/^v//'
}

build() {
  cd fluidd
  npm install --frozen-lockfile
  npm run build
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/webapps
  cp -dr --no-preserve=ownership fluidd/dist "${pkgdir}"/usr/share/webapps/fluidd
}

# vim: ts=2 sw=2 et:
