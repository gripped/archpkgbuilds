# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=fluidd

pkgver=1.31.2
pkgrel=1
pkgdesc='A free and open-source Klipper web interface for managing your 3d printer'
arch=(any)
url=https://docs.fluidd.xyz/
license=(GPL-3.0-only)
makedepends=(
  git
  nodejs-lts-iron
  npm
)
optdepends=('ustreamer: webcam streaming support')
_tag=b743dbeebea9bdcbb61285e6196e3d3aacc99463
source=(git+https://github.com/fluidd-core/fluidd.git#tag=${_tag})
validpgpkeys=(4C717BDC0E452F5B13490409AB4C64C58BA9C3ED) # Pedro Lamas <pedrolamas@gmail.com>
b2sums=('3d805470db595958b47149942d017d6a3f07d46d6858e8356844f669b3a23aa0f20549cddbb283bfd1c0e489ebde5f0ba597711ea0c32b331eb9c7b452d531b0')

pkgver() {
  cd fluidd
  git describe --tags | sed 's/^v//'
}

build() {
  cd fluidd
  npm install --frozen-lockfile
  npm run build
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/webapps
  cp -dr --no-preserve=ownership fluidd/dist "${pkgdir}"/usr/share/webapps/fluidd
}

# vim: ts=2 sw=2 et:
