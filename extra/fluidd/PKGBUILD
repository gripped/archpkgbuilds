# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=fluidd

pkgver=1.34.1
pkgrel=1
pkgdesc='A free and open-source Klipper web interface for managing your 3d printer'
arch=(any)
url=https://docs.fluidd.xyz/
license=(GPL-3.0-only)
makedepends=(
  git
  nodejs-lts-iron
  npm
)
optdepends=('ustreamer: webcam streaming support')
_tag=22b0a27d23d2bbcbebbe3412a97aaeed97c9d995
source=(git+https://github.com/fluidd-core/fluidd.git#tag=${_tag})
validpgpkeys=(4C717BDC0E452F5B13490409AB4C64C58BA9C3ED) # Pedro Lamas <pedrolamas@gmail.com>
b2sums=('ac27493bf20f44e9613bdabab7e6045b7eb7842e134fadc81f5b139088b1158a7c7e7900793f5155921c9191a44c4d446daafe4f2736a7a41a60593303a60d73')

pkgver() {
  cd fluidd
  git describe --tags | sed 's/^v//'
}

build() {
  cd fluidd
  npm install --frozen-lockfile
  npm run build
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/webapps
  cp -dr --no-preserve=ownership fluidd/dist "${pkgdir}"/usr/share/webapps/fluidd
}

# vim: ts=2 sw=2 et:
