# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=fluidd

pkgver=1.34.3
pkgrel=1
pkgdesc='A free and open-source Klipper web interface for managing your 3d printer'
arch=(any)
url=https://docs.fluidd.xyz/
license=(GPL-3.0-only)
makedepends=(
  git
  nodejs-lts-iron
  npm
)
optdepends=('ustreamer: webcam streaming support')
_tag=dc5415e82fb66a8e43f8f4417ebda6aafe39b5dd
source=(git+https://github.com/fluidd-core/fluidd.git#tag=${_tag})
validpgpkeys=(4C717BDC0E452F5B13490409AB4C64C58BA9C3ED) # Pedro Lamas <pedrolamas@gmail.com>
b2sums=('4cd828b191764784697d3201c06b1e43380a9cf94a10e7685693ff86f6547f846318c5ca13a1af1f856ca515171ed8c062b45c7bded0ea60b921e9fab2338eb2')

pkgver() {
  cd fluidd
  git describe --tags | sed 's/^v//'
}

build() {
  cd fluidd
  npm install --frozen-lockfile
  npm run build
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/webapps
  cp -dr --no-preserve=ownership fluidd/dist "${pkgdir}"/usr/share/webapps/fluidd
}

# vim: ts=2 sw=2 et:
