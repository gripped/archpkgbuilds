# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: HurricanePootis <hurricanepootis@protonmail.com>
# Contributor: Spike29 <leguen.yannick@gmail.com>
# Contributor: Samir Faci <csgeek@archlinux.us>
# Contributor: TimothÃ©e Ravier <tim@siosm.fr>

pkgbase=qxmpp
pkgname=(qxmpp-qt5
         qxmpp-qt6)
pkgver=1.5.6
pkgrel=1
pkgdesc='Cross-platform C++ XMPP client and server library'
arch=(x86_64)
url='https://github.com/qxmpp-project/qxmpp'
license=(LGPL-2.1-or-later)
depends=(gcc-libs
         glib2
         glibc
         gstreamer)
makedepends=(cmake
             libomemo-c
             qca-qt5
             qca-qt6)
optdepends=('libomemo-c: OMEMO support')
source=(https://github.com/qxmpp-project/qxmpp/archive/v$pkgver/$pkgbase-$pkgver.tar.gz)
sha256sums=('5af37cfbe878284e12babf4cd5f07109198e7e3dd3f1f78dc3eef351b6614a48')

build() {
  cmake -B build5 -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TESTS=OFF \
    -DWITH_GSTREAMER=ON \
    -DBUILD_OMEMO=ON \
    -DQT_VERSION_MAJOR=5
  cmake --build build5
 
  cmake -B build6 -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TESTS=OFF \
    -DWITH_GSTREAMER=ON \
    -DBUILD_OMEMO=ON \
    -DQT_VERSION_MAJOR=6
  cmake --build build6
}

package_qxmpp-qt5() {
  depends+=(qt5-base
            qca-qt5)
  DESTDIR="$pkgdir" cmake --install build5
}

package_qxmpp-qt6() {
  depends+=(qt6-base
            qca-qt6)
  DESTDIR="$pkgdir" cmake --install build6
}
