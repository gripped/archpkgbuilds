# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: HurricanePootis <hurricanepootis@protonmail.com>
# Contributor: Spike29 <leguen.yannick@gmail.com>
# Contributor: Samir Faci <csgeek@archlinux.us>
# Contributor: TimothÃ©e Ravier <tim@siosm.fr>

pkgname=qxmpp
pkgver=1.10.1
pkgrel=1
pkgdesc='Cross-platform C++ XMPP client and server library'
arch=(x86_64)
url='https://github.com/qxmpp-project/qxmpp'
license=(LGPL-2.1-or-later)
depends=(gcc-libs
         glib2
         glibc
         gstreamer
         qca-qt6
         qt6-base)
makedepends=(cmake
             git
             libomemo-c)
optdepends=('libomemo-c: OMEMO support')
conflicts=(qxmpp-qt6)
provides=(qxmpp-qt6)
replaces=(qxmpp-qt6)
source=(git+https://github.com/qxmpp-project/qxmpp#tag=v$pkgver)
sha256sums=('55ae193fa1bb080e4208cc7d03dd188089223d4d3503cf7860929d53daf75914')

build() {
  cmake -B build -S $pkgbase \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TESTS=OFF \
    -DWITH_GSTREAMER=ON \
    -DBUILD_OMEMO=ON \
    -DQT_VERSION_MAJOR=6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
