# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=dgop
pkgver=0.1.13
pkgrel=1
pkgdesc='System monitoring CLI + REST API'
arch=(x86_64 aarch64 armv7h)
url="https://github.com/AvengeMedia/$pkgname"
license=(MIT)
depends=(glibc)
makedepends=(go)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('4271aba324d9d92547897990ad6325e996f7a28f25f26cfbd516fa9e7deec2ba')

prepare() {
	cd "$_archive"
	export CGO_CPPFLAGS="$CPPFLAGS"
	export CGO_CFLAGS="$CFLAGS"
	export CGO_CXXFLAGS="$CXXFLAGS"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o "$pkgname" ./cmd/cli
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
