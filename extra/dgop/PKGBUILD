# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=dgop
pkgver=0.2.2
pkgrel=1
pkgdesc='System monitoring CLI + REST API'
arch=(x86_64 aarch64 armv7h)
url="https://github.com/AvengeMedia/$pkgname"
license=(MIT)
depends=(glibc)
makedepends=(go)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('2e45d94d2227bb596844681e66f948aeea4a09be3f7ee88dcbbf38938cabec32')

prepare() {
	cd "$_archive"
	export CGO_CPPFLAGS="$CPPFLAGS"
	export CGO_CFLAGS="$CFLAGS"
	export CGO_CXXFLAGS="$CXXFLAGS"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build ./cmd/dgop
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
