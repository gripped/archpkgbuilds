# Maintainer: christian rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=gopass-summon-provider
pkgver=1.16.1
pkgrel=1
pkgdesc="summon provider for gopass"
arch=('x86_64')
url="https://github.com/gopasspw/gopass-summon-provider"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b5a85136f54ef07b1eb80944cdb1bd2cb101deea8c22eb51c1194613cce853d40f382c184af6a4b381fef740546b231417e84fe0aa6e0d64154e00c8a1b1621f')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags="
    -compressdwarf=false
    -linkmode=external
    -X main.version=$pkgver
  "
  go build -v -ldflags="$ld_flags" -o $pkgname
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" $pkgname
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
