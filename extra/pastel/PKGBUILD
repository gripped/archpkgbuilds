# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgbase=pastel
pkgname=(pastel pastel-docs)
pkgver=0.12.0
pkgrel=1
pkgdesc='A command-line tool to generate, analyze, convert and manipulate colors'
arch=(x86_64)
url='https://github.com/sharkdp/pastel'
license=(MIT Apache-2.0)
makedepends=(git rust)
optdepends=(
  'gpick: external color picker'
  'xcolor: external color picker'
  'zenity: external color picker'
  'yad: external color picker'
  'kcolorchooser: external color picker'
)
source=("$pkgbase::git+$url.git#tag=v$pkgver")
sha512sums=('db2baef228457c594d90e2a63ffbe12ea8838033ffa79967616aae56779aedd5a13bde7f27c909d4f2e5b5185844e5de4e3e63e2abc4a82e29c1d3c568f8f042')
b2sums=('ef2698cf81152e1135cec776dc794de3ac5b8f6e702d93708f121beba58255d1fe4f3c5708d7941706988c9d4a992fe74a0588472c1a8083c3a9772bb37843f6')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgbase"
  SHELL_COMPLETIONS_DIR="$PWD/completions" cargo build --release --frozen
}

check() {
  cd "$pkgbase"
  cargo test --frozen
}

package_pastel() {
  depends=(
    glibc
    libgcc
  )
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # shell completions
  install -vDm644 completions/_pastel \
    "$pkgdir/usr/share/zsh/site-functions/_pastel"
  install -vDm644 completions/pastel.bash \
    "$pkgdir/usr/share/bash-completion/completions/pastel"
  install -vDm644 completions/pastel.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/pastel.fish"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}

package_pastel-docs() {
  pkgdesc+=' (documentation)'
  depends=(bash)

  cd "$pkgbase"
  
  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc "$pkgdir/usr/share/doc/$pkgname"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
