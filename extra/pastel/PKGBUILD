# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgbase=pastel
pkgname=(pastel pastel-docs)
pkgver=0.11.0
pkgrel=1
pkgdesc='A command-line tool to generate, analyze, convert and manipulate colors'
arch=(x86_64)
url='https://github.com/sharkdp/pastel'
license=(MIT Apache-2.0)
makedepends=(git rust)
optdepends=(
  'gpick: external color picker'
  'xcolor: external color picker'
  'zenity: external color picker'
  'yad: external color picker'
  'kcolorchooser: external color picker'
)
source=("$pkgbase::git+$url.git#tag=v$pkgver")
sha512sums=('2ecdb1cd610ee7a221d4eba3c2ee00f8f964f2bf5efff5d81e9ec28a4b2058242c3030c863bcfc0c04dc495f923751a8cd96ec42781563cfe89818d26ee8c906')
b2sums=('8f6852e4b2e45b3a167578ed0050be7ff40d892cc6b7d3f59736dd37fa0cef19ed1722855794a0f515bfa484b539427170f7025a5b0158c72d0c7077244a7b20')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgbase"
  SHELL_COMPLETIONS_DIR="$PWD/completions" cargo build --release --frozen
}

check() {
  cd "$pkgbase"
  cargo test --frozen
}

package_pastel() {
  depends=('gcc-libs')
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # shell completions
  install -vDm644 completions/_pastel \
    "$pkgdir/usr/share/zsh/site-functions/_pastel"
  install -vDm644 completions/pastel.bash \
    "$pkgdir/usr/share/bash-completion/completions/pastel"
  install -vDm644 completions/pastel.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/pastel.fish"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}

package_pastel-docs() {
  pkgdesc+=' (documentation)'
  depends=('bash')

  cd "$pkgbase"
  
  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc "$pkgdir/usr/share/doc/$pkgname"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
