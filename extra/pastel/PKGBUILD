# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgbase=pastel
pkgname=('pastel' 'pastel-docs')
pkgver=0.10.0
pkgrel=1
pkgdesc='A command-line tool to generate, analyze, convert and manipulate colors'
arch=('x86_64')
url='https://github.com/sharkdp/pastel'
license=('MIT' 'Apache-2.0')
makedepends=('git' 'rust')
optdepends=(
  'gpick: external color picker'
  'xcolor: external color picker'
  'zenity: external color picker'
  'yad: external color picker'
  'kcolorchooser: external color picker'
)
source=("$pkgbase::git+$url.git#tag=v$pkgver")
sha512sums=('06a5c880a95ed85534835eff91c64363af36b37e43d097dffb5d187992434efefccdb2eee74f300416ac082917473f7144bae9abeae37d143ecad13bae6e67a1')
b2sums=('2f413811271c7f6736f47afc16ea4bdb5168883de4ba09df09d1a90bc5055ef1eb07a1c25f8565acc07d36ecbc55a8b5e76f8fb10e72dc815e60d7aaeabcfbdb')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgbase"
  SHELL_COMPLETIONS_DIR="$PWD/completions" cargo build --release --frozen
}

check() {
  cd "$pkgbase"
  cargo test --frozen
}

package_pastel() {
  depends=('gcc-libs')
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # shell completions
  install -vDm644 completions/_pastel \
    "$pkgdir/usr/share/zsh/site-functions/_pastel"
  install -vDm644 completions/pastel.bash \
    "$pkgdir/usr/share/bash-completion/completions/pastel"
  install -vDm644 completions/pastel.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/pastel.fish"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}

package_pastel-docs() {
  pkgdesc+=' (documentation)'
  depends=('bash')

  cd "$pkgbase"
  
  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc "$pkgdir/usr/share/doc/$pkgname"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
