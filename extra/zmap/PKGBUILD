# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jens Adam <jra@byte.cx>
# Contributor: Arch3y <arch3y[at]archstrike[dot]org>

pkgname=zmap
pkgver=4.3.1
pkgrel=1
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
arch=(x86_64)
license=(Apache-2.0)
depends=(
  glibc
  gmp
  json-c
  judy
  libpcap
  libunistring
)
makedepends=(
  byacc
  cmake
  gengetopt
  ninja
  pkg-config
)
backup=(
  etc/zmap/blocklist.conf
  etc/zmap/zmap.conf
)
source=(https://github.com/zmap/zmap/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('940f15b485df50304eb7eaac582cd2cb60e072eb4b84438cb39cdf54fd7c6e06d224a3831e37b6e77b030e83da3c70b352cdf24e81eeb9a5bfb3beb78760b627')
b2sums=('c7c4c009807df68102d4d38681141c7a274d82588d42613643824b06fe61d55a9bae01ef5fd7d19e6d11d4f0b30945f7073e977e89cade574e85fc64b872f996')

prepare () {
  cd ${pkgname}-${pkgver}
  sed 's|RUNTIME DESTINATION sbin|RUNTIME DESTINATION bin|' -i src/CMakeLists.txt
}

build() {
  cd ${pkgname}-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 AUTHORS CHANGELOG.md INSTALL.md README.md \
    -t "${pkgdir}/usr/share/doc/zmap"
  cp -a examples "${pkgdir}/usr/share/doc/zmap"
}

# vim: ts=2 sw=2 et:
