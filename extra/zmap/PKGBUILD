# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jens Adam <jra@byte.cx>
# Contributor: Arch3y <arch3y[at]archstrike[dot]org>

pkgname=zmap
pkgver=4.2.0
pkgrel=1
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
arch=(x86_64)
license=(Apache-2.0)
depends=(
  glibc
  gmp
  json-c
  judy
  libpcap
  libunistring
)
makedepends=(
  byacc
  cmake
  gengetopt
  ninja
  pkg-config
)
backup=(
  etc/zmap/blocklist.conf
  etc/zmap/zmap.conf
)
source=(https://github.com/zmap/zmap/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('97995626a0c7b0f0138095fea0f4d484d361a40db1e71bfa853062d4320bc83049d4fb30f87e648de2dfcc6c6ac6e0da28214507718584c25f1a6063715aa23c')
b2sums=('763808c039ca323a7a98de9f5a306900e4fca7e953836f9560038a0ed04663a01dda96321b7241cbb68d6c7d75d587761836191a3b7e5fd093abcc540dad10af')

prepare () {
  cd ${pkgname}-${pkgver}
  sed 's|RUNTIME DESTINATION sbin|RUNTIME DESTINATION bin|' -i src/CMakeLists.txt
}

build() {
  cd ${pkgname}-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 AUTHORS CHANGELOG.md INSTALL.md README.md \
    -t "${pkgdir}/usr/share/doc/zmap"
  cp -a examples "${pkgdir}/usr/share/doc/zmap"
}

# vim: ts=2 sw=2 et:
