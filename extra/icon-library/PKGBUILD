# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=icon-library
pkgver=0.0.20
pkgrel=2
pkgdesc='Find the right icon to use on your GNOME application'
arch=(x86_64)
url='https://gitlab.gnome.org/World/design/icon-library'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/design/icon-library.git#tag=$pkgver")
b2sums=(0ac0c54f825d70ab21caac43093ec394d4c1f57300e91f5f2e75c01c52b2a74acf8e832804fa93913d540abf710e07e9436f027c2c361d36879acb9a69a90d5c)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/design/icon-library/-/merge_requests/100
  git cherry-pick -n 3e59835ac30e0af92945fa8b1b5c5df212b6bd82
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
