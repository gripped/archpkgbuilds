# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=icon-library
pkgver=0.0.22
pkgrel=1
pkgdesc='Find the right icon to use on your GNOME application'
arch=(x86_64)
url='https://gitlab.gnome.org/World/design/icon-library'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/design/icon-library.git#tag=$pkgver")
b2sums=(6b65e0a33f29639cdff495ab900ace8c6a7aa85b7c8501b47fe0abfefbf40c1748f31ee841c64fa636a4120285b2732ba387dc273a2b51bb1dda4090978769be)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
