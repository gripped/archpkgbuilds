# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=icon-library
pkgver=0.0.19
pkgrel=2
pkgdesc='Find the right icon to use on your GNOME application'
arch=(x86_64)
url='https://gitlab.gnome.org/World/design/icon-library'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/design/icon-library.git#tag=$pkgver")
b2sums=(19437f93810c67d642c57199f0cfb556a81143a6efee4e0a0403f046c2a4d3d22796ed628f0dc50be51d242f3f60355418da76d9241d37c4d193f70c38dd21f6)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
