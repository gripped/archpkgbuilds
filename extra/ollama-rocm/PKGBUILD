# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Matt Harrison <matt@harrison.us.com>
# Contributor: Kainoa Kanter <kainoa@t1c.dev>
# Contributor: Wilken Gottwalt <wilken.gottwalt@posteo.net>

pkgname=ollama-rocm
pkgver=0.4.0
pkgrel=1
pkgdesc='Create, run and share large language models (LLMs) with ROCm'
arch=(x86_64)
url='https://github.com/ollama/ollama'
license=(MIT)
provides=(ollama)
conflicts=(ollama)
depends=(hipblas)
optdepends=('rocm-smi-lib: monitor GPU usage with rocm-smi')
makedepends=(clblast cmake git go rocm-hip-sdk rocm-opencl-sdk)
source=(git+$url#tag=v$pkgver
        ollama.service
        sysusers.conf
        tmpfiles.d)
b2sums=('f32a0be8c057ec9f574ac8eb685bec96421512b4146593373d2ff47088264f3d8295424065606d5106b1fad2e6c0dcda45684fb1c1038791b26f78042b26fd88'
        '031e0809a7f564de87017401c83956d43ac29bd0e988b250585af728b952a27d139b3cad0ab1e43750e2cd3b617287d3b81efc4a70ddd61709127f68bd15eabd'
        '3aabf135c4f18e1ad745ae8800db782b25b15305dfeaaa031b4501408ab7e7d01f66e8ebb5be59fc813cfbff6788d08d2e48dcf24ecc480a40ec9db8dbce9fec'
        'e8f2b19e2474f30a4f984b45787950012668bf0acb5ad1ebb25cd9776925ab4a6aa927f8131ed53e35b1c71b32c504c700fe5b5145ecd25c7a8284373bb951ed')

build() {
  export CC=/opt/rocm/llvm/bin/clang CXX=/opt/rocm/llvm/bin/clang++
  export CFLAGS+=' -fcf-protection=none -w' CXXFLAGS+=' -fcf-protection=none -w'
  export CGO_CFLAGS="$CFLAGS" CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS" CGO_LDFLAGS="$LDFLAGS"
  export CMAKE_DEFS='-DBUILD_SHARED_LIBS=off -DCMAKE_BUILD_TYPE=MinSizeRel'
  export LDFLAGS+=' -L/opt/cuda/targets/x86_64-linux/lib/stubs/'
  export MDGPU_TARGETS="gfx1030,gfx1100"
  export OLLAMA_CPU_TARGET="cpu_avx2"
  export OLLAMA_CUSTOM_CPU_DEFS='-DGGML_LTO=on'
  export OLLAMA_SKIP_CUDA_GENERATE=on
  export OLLAMA_SKIP_ONEAPI_GENERATE=on
  export ROCM_PATH=/opt/rocm

  local goflags='-buildmode=pie -trimpath -mod=readonly -modcacherw'
  local ldflags="-linkmode=external -buildid= -X github.com/ollama/ollama/version.Version=$pkgver -X github.com/ollama/ollama/server.mode=release"

  cd ollama
  go generate ./...
  go build $goflags -ldflags="$ldflags" -tags rocm
}

check() {
  ollama/ollama --version > /dev/null
  cd ollama
  go test .
}

package() {
  install -Dm755 ollama/ollama "$pkgdir/usr/bin/ollama"
  install -dm755 "$pkgdir/var/lib/ollama"
  install -Dm644 ollama.service "$pkgdir/usr/lib/systemd/system/ollama.service"
  install -Dm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/ollama.conf"
  install -Dm644 tmpfiles.d "$pkgdir/usr/lib/tmpfiles.d/ollama.conf"
  install -Dm644 ollama/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  ln -s /var/lib/ollama "$pkgdir/usr/share/ollama"
}
