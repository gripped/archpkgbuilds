# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-knn-plugin
pkgver=2.16.0.0
pkgrel=1
_opensearchver=2.16.0
_jdkver=11
pkgdesc="OpenSearch Index Management Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/search-plugins/knn"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/k-NN/archive/${pkgver}.tar.gz"
)
sha256sums=('67b7b40b99bbdc690ab8db5b801233ab446f7b8d55245a1ac61cbab1ca3d267f')

build() {
  cd "k-NN-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-knn"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-knn"
  unzip "${srcdir}/k-NN-${pkgver}/build/distributions/opensearch-knn-${pkgver}.zip"

  install -Dm644 "${srcdir}/k-NN-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
