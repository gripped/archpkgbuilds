# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Guillaume Alaux <guillaume@archlinux.org>
# Contributor: Vitaliy Berdinskikh <ur6lad@i.ua>

pkgname=tomcat-native
pkgver=2.0.9
pkgrel=1
pkgdesc='Optional component for Tomcat to use certain native resources for performance, compatibility'
arch=(x86_64)
url=https://tomcat.apache.org/native-doc/
license=(APACHE)
depends=(
  apr
  openssl
)
makedepends=(
  git
  java-environment
  python
)
options=(!emptydirs)
_tag=540d06a0741444d002e2caff8b244daf4a2963b8
source=(
  git+https://github.com/apache/tomcat-native.git#tag=${_tag}
  tomcat-native-buildconf.patch
)
b2sums=('ee7b983dcef7eb7908e3a68230da0ed49caf993e725d14b91240e977241ffd4bdbd633ba5e0663f26c58fb71ee69f052ef0a634e251761b625e91706eb2b9410'
        'd3603079626b908644331c193452088ef3ce89e92c775c60b232d16d04b8c49d40106d55c76f81c87daea93d335868140df0046c7102796d92b08431ac8749a4')

pkgver() {
  cd tomcat-native
  git describe --tags
}

prepare() {
  cd tomcat-native/native
  patch -Np2 -i ../../tomcat-native-buildconf.patch
  automake --add-missing --copy --force-missing || true
  ./buildconf \
    --with-apr=/usr/share/apr-1
}

build() {
  cd tomcat-native/native
  ./configure \
    --prefix=/usr \
    --with-apr=/usr/bin/apr-1-config \
    --with-java-home=/usr/lib/jvm/default \
    --with-ssl=/usr
  make
}

package() {
  make DESTDIR="${pkgdir}" -C tomcat-native/native install
}

# vim: ts=2 sw=2 et:
