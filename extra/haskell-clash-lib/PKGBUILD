# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=clash-lib
pkgname=haskell-clash-lib
pkgver=1.8.2
pkgrel=40
pkgdesc="Clash: a functional hardware description language - As a library"
url="https://clash-lang.org/"
license=('BSD-2-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec-aeson' 'haskell-aeson-pretty'
         'haskell-ansi-terminal' 'haskell-async' 'haskell-attoparsec' 'haskell-base16-bytestring'
         'haskell-clash-prelude' 'haskell-cryptohash-sha256' 'haskell-data-binary-ieee754'
         'haskell-data-default' 'haskell-dlist' 'haskell-extra' 'haskell-ghc' 'haskell-glob'
         'haskell-hashable' 'haskell-src-meta' 'haskell-hint' 'haskell-infinite-list' 'haskell-lens'
         'haskell-ordered-containers' 'haskell-pretty-show' 'haskell-prettyprinter'
         'haskell-prettyprinter-interp' 'haskell-primitive' 'haskell-string-interpolate'
         'haskell-temporary' 'haskell-terminal-size' 'haskell-trifecta'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-vector-binary-instances'
         'haskell-yaml')
makedepends=('ghc' 'expac' 'haskell-doctest-parallel' 'haskell-ghc-typelits-knownnat'
             'haskell-quickcheck-text' 'haskell-src-exts' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-tasty-th')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('78af3f3058c2bb178dd9da2264aa37dbc59eb9c906823995a3fd5e33a6c4a475a3102c46c14b24088bb2deda16a8af2547c1cb06798aaa304498a14ae1335f0e')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # == An ugly hack to generate .ghc.environment without cabal-install ==
  # doctest-parallel relies on this cabal-install feature to configure GHCi.
  # https://github.com/martijnbastiaan/doctest-parallel/issues/22
  ghc_version=$(expac %v ghc | cut -d - -f 1)
  _ghc_env_filename=.ghc.environment.$CARCH-linux-$ghc_version
  echo -e "package-db dist/package.conf.inplace" > $_ghc_env_filename
  ls dist/package.conf.inplace/*.conf | sed 's|.*/\(.*\).conf$|package-id \1|' >> $_ghc_env_filename
  ls /usr/lib/ghc-$ghc_version/lib/package.conf.d/*.conf | sed 's|.*/\(.*\).conf$|package-id \1|' >> $_ghc_env_filename

  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
