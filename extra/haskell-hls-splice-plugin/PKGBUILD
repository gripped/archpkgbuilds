# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-splice-plugin
pkgname=haskell-hls-splice-plugin
pkgver=2.1.0.0
pkgrel=46
pkgdesc="HLS Plugin to expand TemplateHaskell Splices and QuasiQuotes"
url="https://hackage.haskell.org/package/hls-splice-plugin"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-dlist' 'haskell-extra' 'haskell-foldl' 'haskell-ghc'
         'haskell-ghc-exactprint' 'haskell-ghcide' 'haskell-hls-plugin-api'
         'haskell-hls-refactor-plugin' 'haskell-lens' 'haskell-lsp' 'haskell-retrie' 'haskell-syb'
         'haskell-unliftio-core' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils' 'haskell-row-types')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('76ed7cf86a826baf20850fce31e574e7e33668a2a3b5865b0f0a55d4fe02c5cf714d0407ca18e865cde7145922982d49f30c9bb37a047c9682a13264519affd8')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
