# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Pranav Sharma <pranav.sharma.ama@gmail.com>
# Contributor: Chris Down <chris@chrisdown.name>
# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Jochen Schalanda <jochen+aur (-AT-) schalanda.name>
# Contributor: Ichimonji10 <Ichimonji10@gmail.com>

pkgname=rbenv
pkgver=1.3.2
pkgrel=1
pkgdesc="Manage your app's Ruby environment"
arch=('any')
url="https://github.com/rbenv/rbenv"
license=('MIT')
depends=('bash')
checkdepends=('bats')
optdepends=('ruby-build: install Ruby versions')
install="rbenv.install"
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('e2104f6472d7a8477409c46d4de39562b4d01899148a3dbed73c1d99a0b4bb2a')

check() {
  cd $pkgname-$pkgver
  bats test
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/lib/rbenv/libexec" libexec/*
  install -vDm644 -t "$pkgdir/usr/lib/rbenv/completions" completions/*
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/man/man1" share/man/man1/*
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -vdm755 "$pkgdir/usr/bin"
  ln -vs /usr/lib/rbenv/libexec/rbenv "$pkgdir/usr/bin/rbenv"
}
