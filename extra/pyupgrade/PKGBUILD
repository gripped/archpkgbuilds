# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=pyupgrade
pkgver=3.21.2
pkgrel=1
pkgdesc="A tool to automatically upgrade syntax for newer versions of the language"
arch=(any)
url="https://github.com/asottile/pyupgrade"
license=(MIT)
depends=(python python-tokenize-rt)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::"https://github.com/asottile/pyupgrade/archive/v$pkgver.tar.gz")
sha256sums=('35b0a60b26b2e3a45262331748e757eed5e8ad03a1d7c8b5ef015a0240584fc3')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest .
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
