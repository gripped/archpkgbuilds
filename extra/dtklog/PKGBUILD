# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtklog
pkgver=0.0.3
pkgrel=1
pkgdesc='Simple, convinient and thread safe logger for Qt-based C++ apps'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtklog"
license=('LGPL3')
depends=('glibc' 'libsystemd' 'qt5-base' 'spdlog' 'fmt' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtklog.git#tag=$pkgver")
sha512sums=('f70b5b706d7571d56860da90e449f9b97f839b269ccdf3b9a2865ecdeb4e7dab3c890a717b4b351a7fd2f4fd850824bfd882562c4a355c050c19c8e4cca6720f')

build() {
  cd dtklog
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_SYSTEMD=ON \
    -DBUILD_WITH_QT6=OFF \
    -DDLOG_VERSION=$pkgver
  ninja
}

package() {
  cd dtklog
  DESTDIR="$pkgdir" ninja install
}
