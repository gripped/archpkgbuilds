# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtklog
pkgver=0.0.2
pkgrel=2
pkgdesc='Simple, convinient and thread safe logger for Qt-based C++ apps'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtklog"
license=('LGPL3')
depends=('glibc' 'libsystemd' 'qt5-base' 'spdlog' 'fmt' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtklog.git#tag=$pkgver")
sha512sums=('11242d6696010012a57b322f443fe7964194b76488915b7f0bdf1e479c5b59f02bb1d839b17c8e268fbe95ba8fa2295ae64a6dc7d9a8f121cb41a45a3f14c1e8')

build() {
  cd dtklog
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_SYSTEMD=ON \
    -DBUILD_WITH_QT6=OFF \
    -DDLOG_VERSION=$pkgver
  ninja
}

package() {
  cd dtklog
  DESTDIR="$pkgdir" ninja install
}
