# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtklog
pkgver=0.0.4
pkgrel=1
pkgdesc='Simple, convinient and thread safe logger for Qt-based C++ apps'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtklog"
license=('LGPL-2.1-or-later')
depends=('glibc' 'libsystemd' 'qt5-base' 'spdlog' 'fmt' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtklog.git#tag=$pkgver")
sha512sums=('3811b4b88509d97451ce6f236580d0baf4199c679851923dc66969c520eb66360c7c4aef1b1d1bf3c6698fbf8dbf45583175b5e7494d1ffe6ad5d71a4e67f351')

build() {
  cd dtklog
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_SYSTEMD=ON \
    -DBUILD_WITH_QT6=OFF
  ninja
}

package() {
  cd dtklog
  DESTDIR="$pkgdir" ninja install
}
