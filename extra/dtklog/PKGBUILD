# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtklog
pkgver=0.0.6
pkgrel=3
pkgdesc='Simple, convinient and thread safe logger for Qt-based C++ apps'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtklog"
license=('LGPL-2.1-or-later')
depends=('glibc' 'libsystemd' 'qt5-base' 'spdlog' 'fmt' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtklog.git#tag=$pkgver")
sha512sums=('73604a45ceff5e1ce1a6898ca689a6a6fd75ed0805c0657a446bd2c4dda9e152e714d37dbb999ec38b9f3afd761682440bdd4d8aa0636a12de5aa78c32ac9fc2')

build() {
  cd dtklog
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_SYSTEMD=ON \
    -DBUILD_WITH_QT6=OFF
  ninja
}

package() {
  cd dtklog
  DESTDIR="$pkgdir" ninja install
}
