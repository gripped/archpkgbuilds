# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=bat-extras
pkgver=2024.06.01
pkgrel=1
pkgdesc='Bash scripts that integrate bat with various command line tools'
arch=('any')
url='https://github.com/eth-p/bat-extras'
license=('MIT')
depends=(
  'bat'
  'bash'
  'git'
  'ripgrep'
  'man'
)
makedepends=('shfmt')
checkdepends=('fish')
optdepends=(
  'ncurses: optional for batdiff script'
  'git-delta: optional for batdiff script'
  'fzf: optional for batman script'
  'exa: optional for batpipe script'
  'entr: optional for batwatch script'
  'prettier: various code formatting for prettybat script'
  'shfmt: bash formatting for prettybat script'
  'rustfmt: Rust formatting for prettybat script'
  'clang: C / C++ / Objective-C formatting for prettybat script'
  'python-black: Python formatting for prettybat script'
)
source=(
  "git+https://github.com/eth-p/bat-extras.git#tag=v$pkgver"
  'github.com-eth-p-best::git+https://github.com/eth-p/best.git'
  'github.com-eth-p-best-tests::git+https://github.com/eth-p/best-tests.git'
)
sha512sums=('32142b2ad9ef4cacb432589594795bbd165f8a78766ce5e54d5d5b0bb4b149fdb6cedd589cc65274f485bb72b61d1b75693cb2de447144bc546efc7a6f98d57e'
            'SKIP'
            'SKIP')
b2sums=('b2ae62216f126d82b7c287994eefb75e05331c87f48c55057f64a5b5f8a79a4f6ac841e2f1c3b9f5bbe236a1ae884449b3408594a4897040c70781ad7b428182'
        'SKIP'
        'SKIP')

prepare(){
  cd "$pkgname"

  # setup submodules
  git submodule init
  git config submodule.best.url "$srcdir/github.com-eth-p-best"
  git -c protocol.file.allow=always submodule update

  pushd .test-framework
  git submodule init
  git config submodule.best-tests.url "$srcdir/github.com-eth-p-best-tests"
  git -c protocol.file.allow=always submodule update
  popd

  # fix incorrect version string using commit date
  # https://github.com/eth-p/bat-extras/issues/68
  git show --no-patch --format=%cd --date=format:%Y.%m.%d > version.txt
}

check() {
  cd "$pkgname"

  ./test.sh --verbose --strict --snapshot:show
}

package() {
  cd "$pkgname"

  # scripts
  ./build.sh \
    --prefix="$pkgdir/usr" \
    --install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" \
    CONTRIBUTING.md README.md doc/*

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/*

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
