# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=ethersync
pkgname=teamtype
pkgver=0.9.1
pkgrel=1
pkgdesc='Peer-to-peer, editor-agnostic collaborative editing of local text files'
arch=('x86_64')
url="https://github.com/$pkgname/$pkgname"
license=('AGPL-3.0-only')
makedepends=(rust)
provides=($pkgbase)
replaces=($pkgbase)
conflicts=($pkgbase)
depends=(zlib glibc gcc-libs)
options=('!lto')
source=("$url/archive/refs/tags/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8caeeb57f74095671dff946860be252cc392452f80c414ebfcf06a63e2b63e45e6c3ff0bd92a7ab095cdc882b39c141b375314a74f1851006a07f4afe26c20a9')

prepare() {
    cd "$pkgname-$pkgver/daemon"
    cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
    cd "$pkgname-$pkgver/daemon"
    cargo build --frozen --release
}

check() {
    cd "$pkgname-$pkgver/daemon"
    cargo test --frozen
}

package() {
    cd "$pkgname-$pkgver/daemon/target"
    install -Dm755 -t "$pkgdir/usr/bin/" "release/$pkgname"
    pushd completions
    install -Dm0644 -t "$pkgdir/usr/share/bash-completion/completions/" "$pkgname.bash"
    install -Dm0644 -t "$pkgdir/usr/share/elvish/lib/" "$pkgname.elv"
    install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "$pkgname.fish"
    # install -Dm0644 -t "$pkgdir/usr/share/nushell/vendor/autoload/" "$pkgname.nu"
    install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "_$pkgname"
    pushd ../manpages
    install -Dm0644 -t "$pkgdir/usr/share/man/man1/" $pkgname*.1
}
