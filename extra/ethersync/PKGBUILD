# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ethersync
pkgver=0.8.0
pkgrel=1
pkgdesc="Peer-to-peer, editor-agnostic collaborative editing of local text files"
arch=('x86_64')
url="https://github.com/ethersync/ethersync"
license=('AGPL-3.0-only')
makedepends=(rust)
depends=(zlib glibc gcc-libs)
options=('!lto')
source=("$pkgname-$pkgver.tar.gz"::https://github.com/ethersync/ethersync/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('e022e367eb319d0afaf9f1867050249ed8c74f2a77765a56034b4b698913efce4407a51e0bd618ca285c0449669d63aa4bd7c5b3190997c5d355000f2b8bbb87')

prepare() {
    cd "$pkgname-$pkgver/daemon"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver/daemon"
    cargo build --frozen --release
}

check() {
    cd "$pkgname-$pkgver/daemon"
    cargo test --frozen
}

package() {
    cd "$pkgname-$pkgver/daemon"
    install -Dm755 "target/release/ethersync" "$pkgdir/usr/bin/ethersync"

    install -Dm644 ../LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
