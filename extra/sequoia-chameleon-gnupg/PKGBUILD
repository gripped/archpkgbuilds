# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sequoia-chameleon-gnupg
pkgver=0.7.0
pkgrel=1
pkgdesc="A re-implementation and drop-in replacement of gpg and gpgv"
arch=(x86_64)
url="https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg"
license=(GPL-3.0-or-later)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bzip2
  cargo
  clang
  git
  openssl
  sqlite
)
checkdepends=(
  gnupg
  libfaketime
)
source=(
  "git+$url#tag=v$pkgver?signed"
)
sha512sums=('c7534c89a373f7b76a6434028dce11a181e18a7a55cd8a2d682857e82159fdc0454fb2c52aa0638b5f6f4b710bc24893295e58b5999d398f99f191c3033fedc8')
b2sums=('2b25b9544bc8007be18b51709beb8a4c945968c2ff79e60db603f9ac4ed26bde4ce38460fd6d847781edb06440ea86ad3603787c828b211fbe99a5db7ed264a2')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --no-default-features --features=crypto-openssl
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  RUST_BACKTRACE=1
  # skip broken test: https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg/-/issues/58
  cargo test --frozen --no-default-features --features=crypto-openssl -- --skip password_store_git
}

package() {
  depends+=(
    bzip2 libbz2.so
    openssl libcrypto.so libssl.so
    sqlite libsqlite3.so
  )

  cd $pkgname
  install -vDm 755 target/release/gpg{,v}-sq -t "$pkgdir/usr/bin/"
  install -vDm 644 gpg-sq-parcimonie.service -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
