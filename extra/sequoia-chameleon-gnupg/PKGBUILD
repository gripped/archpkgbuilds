# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sequoia-chameleon-gnupg
pkgver=0.10.0
pkgrel=1
pkgdesc="A re-implementation and drop-in replacement of gpg and gpgv"
arch=(x86_64)
url="https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg"
license=(GPL-3.0-or-later)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bzip2
  cargo
  clang
  git
  openssl
  sqlite
)
checkdepends=(
  gnupg
  sequoia-sq
)
source=(
  git+$url?signed#tag=v$pkgver
)
sha512sums=('ea0b519347a2daafc329d5e5956a41b48b72fbf75065145f22276b2b69a6d3ae79f2c1ad7f0edafadf19a3d8fcd4c560c585aecef1203399a6cee6b9370ab042')
b2sums=('d7ac7bb9c9b9821b6856fb8b9338951c07edd1481c92757ae4b879acbec6fb1411518d4fed5ee9b65d36d581ed88ad13b72178f27fe1f3a8ca2a4e2b4e70a981')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --no-default-features --features=crypto-openssl
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  RUST_BACKTRACE=1
  # skip broken test: https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg/-/issues/58
  cargo test --frozen --no-default-features --features=crypto-openssl -- --skip password_store_git
}

package() {
  depends+=(
    bzip2 libbz2.so
    openssl libcrypto.so libssl.so
    sqlite libsqlite3.so
  )

  cd $pkgname
  install -vDm 755 target/release/gpg{,v}-sq -t "$pkgdir/usr/bin/"
  install -vDm 644 gpg-sq-parcimonie.service -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
