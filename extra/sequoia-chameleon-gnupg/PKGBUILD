# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sequoia-chameleon-gnupg
pkgver=0.8.0
pkgrel=1
pkgdesc="A re-implementation and drop-in replacement of gpg and gpgv"
arch=(x86_64)
url="https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg"
license=(GPL-3.0-or-later)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bzip2
  cargo
  clang
  git
  openssl
  sqlite
)
checkdepends=(
  gnupg
  sequoia-sq
)
source=(
  git+$url?signed#tag=v$pkgver
)
sha512sums=('c37341a961df47cbb9e76fd6c683371e0249e7dd23c0c8d1bee308c35873d4ef9e427b1bc8c71c6d30bad9cfed042869c15b6c1019f2b7e6ee919e6db3eff032')
b2sums=('656923346dd111c38dd0b30a65e7240cd263f7f0457fd1a029fdfabca0e4c6cc001b10907cdb6ae5ae3459a09496590a4983db0f02ce75b44bb91008d0bf8ae5')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --no-default-features --features=crypto-openssl
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  RUST_BACKTRACE=1
  # skip broken test: https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg/-/issues/58
  cargo test --frozen --no-default-features --features=crypto-openssl -- --skip password_store_git
}

package() {
  depends+=(
    bzip2 libbz2.so
    openssl libcrypto.so libssl.so
    sqlite libsqlite3.so
  )

  cd $pkgname
  install -vDm 755 target/release/gpg{,v}-sq -t "$pkgdir/usr/bin/"
  install -vDm 644 gpg-sq-parcimonie.service -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
