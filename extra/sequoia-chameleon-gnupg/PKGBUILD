# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sequoia-chameleon-gnupg
pkgver=0.11.0
pkgrel=1
pkgdesc="A re-implementation and drop-in replacement of gpg and gpgv"
arch=(x86_64)
url="https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg"
license=(GPL-3.0-or-later)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bzip2
  cargo
  clang
  git
  openssl
  sqlite
)
checkdepends=(
  gnupg
  sequoia-sq
)
source=(
  git+$url?signed#tag=v$pkgver
)
sha512sums=('c7f8f287be9ba12cfb9f7f79657f3ac506d76b005c1a73a3cfe8bed452fb90a10b9feca71be2008296804eef295b9a638dedcea00ffcf29ca5572dc65e2b9c85')
b2sums=('b0f103a2d8f3a9efd136bf2c800781af94259de1f41772336d4fa851eeed3d3c9e26359f8479a13c1d10b06a6f59f03d47699019ea8b3bf228fa6730eae2e89b')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --no-default-features --features=crypto-openssl
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  RUST_BACKTRACE=1
  # skip broken test: https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg/-/issues/58
  cargo test --frozen --no-default-features --features=crypto-openssl -- --skip password_store_git
}

package() {
  depends+=(
    bzip2 libbz2.so
    openssl libcrypto.so libssl.so
    sqlite libsqlite3.so
  )

  cd $pkgname
  install -vDm 755 target/release/gpg{,v}-sq -t "$pkgdir/usr/bin/"
  install -vDm 644 gpg-sq-parcimonie.service -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
