# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sequoia-chameleon-gnupg
pkgver=0.4.0
_commit=797e26458c5c36b8a38493bc8b11149ace053567  # refs/tags/0.4.0^{}
pkgrel=1
pkgdesc="A re-implementation and drop-in replacement of gpg and gpgv"
arch=(x86_64)
url="https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg"
license=(GPL-3.0-or-later)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bzip2
  cargo
  clang
  git
  openssl
  sqlite
)
checkdepends=(gnupg)
source=(
  # $url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  "git+$url#commit=$_commit?signed"
)
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=(
  D2F2C5D45BE9FDE6A4EE0AAF31855247603831FD  # Justus Winter (Code Signing Key) <justus@sequoia-pgp.org>
)


prepare() {
  cd $pkgname
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --no-default-features --features=crypto-openssl
}

# tests rely heavily on behavior of gnupg 2.2
# https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg/-/issues/40
# check() {
#   cd $pkgname
#   export RUSTUP_TOOLCHAIN=stable
#   RUST_BACKTRACE=1

#   cargo test --frozen --no-default-features --features=crypto-openssl
# }

package() {
  depends+=(
    bzip2 libbz2.so
    openssl libcrypto.so libssl.so
    sqlite libsqlite3.so
  )

  cd $pkgname
  install -vDm 755 target/release/gpg{,v}-sq -t "$pkgdir/usr/bin/"
  install -vDm 644 gpg-sq-parcimonie.service -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
