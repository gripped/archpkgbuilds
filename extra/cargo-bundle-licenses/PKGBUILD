# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-bundle-licenses
pkgver=2.3.0
pkgrel=1
pkgdesc="Bundle licensing of Rust dependencies"
url="https://github.com/sstadick/cargo-bundle-licenses"
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=('cargo' 'gcc-libs')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/${pkgname}/${pkgver}/download")
sha256sums=('3f242965642b4551a3d16e8af3c2c92b5899bc001c2526bbc281eba4ba9e6a37')
b2sums=('cd2df205dd32106c77397c3e2e79289ffbd8601d94b18d0056dcd196f6b41dad932796d60db59e1dbe4f9868d4a8c091376f1d4ff6bce1d5fa86fa0c57f302cf')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
