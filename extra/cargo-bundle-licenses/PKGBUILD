# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-bundle-licenses
pkgver=3.0.0
pkgrel=1
pkgdesc="Bundle licensing of Rust dependencies"
url="https://github.com/sstadick/cargo-bundle-licenses"
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=('cargo' 'gcc-libs')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/${pkgname}/${pkgver}/download")
sha256sums=('e973b611f81202b067c892d7b18dab29a043fe498c8268d7266675b5175878fb')
b2sums=('0afe211162a85a7cda603113b60a9f84f3994f779fe50ce18d88a796030dd285a0c8ce1e16d528e4927f02075e83e4a20e45c57bb0ebb4ee2f21923b00f3a069')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
