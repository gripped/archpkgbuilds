# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-bundle-licenses
pkgver=4.0.0
pkgrel=1
pkgdesc="Bundle licensing of Rust dependencies"
url="https://github.com/sstadick/cargo-bundle-licenses"
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=('cargo' 'gcc-libs')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/${pkgname}/${pkgver}/download")
sha256sums=('de9ecf3f0978a3eab9d2f5e44a9eab428e1e79621f6f13140d600532ced73c8a')
b2sums=('e6a42e6beed712042fa59f0014fa6f1f6f3e49c9ed2324ca18bcf4ad2823ef0d3ea82b504796da4186b2584dcb17bd8fd6ae4ec6f8693edacf089f258e6b9345')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
