# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-bundle-licenses
pkgver=2.1.1
pkgrel=1
pkgdesc="Bundle licensing of Rust dependencies"
url="https://github.com/sstadick/cargo-bundle-licenses"
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=('cargo' 'gcc-libs')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/${pkgname}/${pkgver}/download")
sha256sums=('849bd9700a207bbdc52df2688fc8dafa89a850b2ca7df7a0aacd1bbe7a88e3f0')
b2sums=('41865a517075f870e47ee32e971f3c8bbd1369f6775a3874b4dfd4f3f9e4f57dea868890e25d46faf0625919d7ccec4df2ec9d4ca78e48a2e61c69bbce410fe4')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
