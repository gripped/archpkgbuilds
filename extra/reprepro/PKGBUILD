# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Andre Klitzing <andre AT incubo DOT de>
# Contributor: max-k <max-k AT post DOT com>

pkgname=reprepro
pkgver=5.4.5
pkgrel=1
pkgdesc='Debian package repository producer'
url="https://salsa.debian.org/debian/reprepro"
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  'db5.3'
  'glibc'
  'libarchive.so'
  'libbz2.so'
  'libgpg-error'
  'libgpgme.so'
  'liblzma.so'
  'libz.so'
)
source=("https://salsa.debian.org/debian/reprepro/-/archive/reprepro-${pkgver}/reprepro-reprepro-${pkgver}.tar.bz2"
        bdb_5.3.patch)
sha256sums=('fb32d98f4e00e430a38bd7563be4b2c6caa7fedebfc79705d37b0fc0fe345ce1'
            'c5e982dc7fa1790aabd7976216c56019fdf1844972eca7c65a0cdc3a388564a7')
b2sums=('9a1f72a54ed58a57614737be20ff29db1755b0815630de754ea101567b9799f6d0511344f55650c606d4b78174dfd86adaf41f5603d5093d1f80d2582ca7e304'
        'be2bf6f0a198dd90d34a4976154970857463bca22d9df49e9f697586e60d4d577652f13cab4abf105681ad7a100522b29ce3f743ab98a696fe95348c308d782c')

prepare() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  patch -Np1 -i ../bdb_5.3.patch
}

build() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  set -x

  export CPPFLAGS="$CPPFLAGS -I/usr/include/db5.3"

  libtoolize --force
  aclocal
  autoheader
  automake --add-missing
  autoconf

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
