# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-beetle-pce
pkgver=1225
pkgrel=1
pkgdesc='NEC PC Engine/SuperGrafx/CD core'
arch=(x86_64)
url=https://github.com/libretro/beetle-pce-libretro
license=(GPL2)
groups=(libretro)
depends=(
  gcc-libs
  glibc
  libretro-core-info
  zlib
)
makedepends=(git)
_commit=3a75c7ce5fc62c524be8d0eb45cd4b39d08a885c
source=(libretro-beetle-pce::git+https://github.com/libretro/beetle-pce-libretro.git#commit=${_commit})
sha256sums=('4a0eea95ca1d2405b09e1f000b336d128fcddec1d4a762717628167259d65a61')

pkgver() {
  cd libretro-beetle-pce

  git rev-list --count HEAD
}

build() {
  make \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-pce
}

package() {
  install -Dm 644 libretro-beetle-pce/mednafen_pce_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
