# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-beetle-pce
pkgver=1243
pkgrel=1
pkgdesc='NEC PC Engine/SuperGrafx/CD core'
arch=(x86_64)
url=https://github.com/libretro/beetle-pce-libretro
license=(GPL2)
groups=(libretro)
depends=(
  gcc-libs
  glibc
  libretro-core-info
  zlib
)
makedepends=(git)
_commit=af28fb0385d00e0292c4703b3aa7e72762b564d2
source=(libretro-beetle-pce::git+https://github.com/libretro/beetle-pce-libretro.git#commit=${_commit})
sha256sums=('f15407fe7e8a7bb8253cb345e0806ecd202b5365cf3dd70f800fbeb64c47e9ef')

pkgver() {
  cd libretro-beetle-pce

  git rev-list --count HEAD
}

build() {
  make \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-pce
}

package() {
  install -Dm 644 libretro-beetle-pce/mednafen_pce_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
