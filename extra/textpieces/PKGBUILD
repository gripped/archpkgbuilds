# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=textpieces
pkgver=4.3.1
pkgrel=1
pkgdesc='Powerful scratchpad with ability to perform a lot of text transformations'
arch=(x86_64)
url='https://apps.gnome.org/TextPieces/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=(
  "git+https://gitlab.com/liferooter/textpieces.git#tag=${pkgver//_/-}"
  textpieces-activate.patch
)
b2sums=(
  7603e2170f5c6b2146c76890f06e2ce0fb23aaaa1cfce7f6e56ad2b29acf296a5e684bf44df60bd2c8571305d64a8c78b8422be220a33e4d6c7743ed89dc7e1c
  41fb9d980a6620a1ad6b70bb4ad9860de82139db20e3f07ddcbbeee5f59298d3b398309218f973a5d3b08245b08178b4e96205807535f252851b590f08b73d2f
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.com/liferooter/textpieces/-/merge_requests/10
  git apply -3 ../textpieces-activate.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
