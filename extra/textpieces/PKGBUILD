# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=textpieces
pkgver=4.1.1_1
pkgrel=1
pkgdesc='Powerful scratchpad with ability to perform a lot of text transformations'
arch=(x86_64)
url='https://apps.gnome.org/TextPieces/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/liferooter/textpieces.git#tag=v${pkgver//_/-}")
b2sums=(064a0cd791c27aa2356294f02759aef587e07069493126d50c59824aad1ffc0cb3c92afbd7be2e02a345409b17e1f8e7fa8ab2db55ca0b26c3dc37da61d95f69)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
