# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=textpieces
pkgver=4.2.0
pkgrel=1
pkgdesc='Powerful scratchpad with ability to perform a lot of text transformations'
arch=(x86_64)
url='https://apps.gnome.org/TextPieces/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=(
  "git+https://gitlab.com/liferooter/textpieces.git#tag=v${pkgver//_/-}"
  textpieces-activate.patch
)
b2sums=(
  07ddadfc8ad92e68799a4e4c59b383baebc87b31edfbf52d106053eb4c49d9fcb68e551337b841a30dd44df200f8a2d4919d22c061a18de24fabf16a6b93620e
  46f4e4df1490d5744a37402233edaacd0de512d87e20f18e7e70f3c5c864c5f6cea9118100e0ddc74c574821f2eb443f2c170e3a6b3b462bdcc92f495327d66c
)

prepare() {
  cd $pkgname

  # Cargo.lock needs to be updated
  cargo update

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.com/liferooter/textpieces/-/merge_requests/10
  git apply -3 ../textpieces-activate.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
