# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=textpieces
pkgver=4.3.0
pkgrel=1
pkgdesc='Powerful scratchpad with ability to perform a lot of text transformations'
arch=(x86_64)
url='https://apps.gnome.org/TextPieces/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=(
  "git+https://gitlab.com/liferooter/textpieces.git#tag=v${pkgver//_/-}"
  textpieces-activate.patch
)
b2sums=(
  366a1ba520e971e8c4665c59fdfd961a8056ab98cfa771cf011ae7fce4ac74238c5011295c3b4de0f45b524dda7da03b914aed030b2be5190f80e77c3e68bead
  41fb9d980a6620a1ad6b70bb4ad9860de82139db20e3f07ddcbbeee5f59298d3b398309218f973a5d3b08245b08178b4e96205807535f252851b590f08b73d2f
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.com/liferooter/textpieces/-/merge_requests/10
  git apply -3 ../textpieces-activate.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
