# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-sphinxcontrib-programoutput
_name="${pkgname#python-}"
pkgver=0.18
pkgrel=1
pkgdesc="Sphinx extension for capturing program output"
arch=(any)
url="https://github.com/OpenNTI/sphinxcontrib-programoutput/"
license=(BSD-3-Clause)
depends=(
  python
  python-docutils  # pulled in via python-sphinx
  python-sphinx
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('a4d5d146c8f83b4f72c4359b8980866134720288f13d4fe6f029b8fddac1d05c9f9fe777d0bef1aa0bebc17b540b2e19bef21ddc3e08af373d204fe8b50ec904')
b2sums=('2afba65e7d3be13585372ff7cb3eff68fa5161383e48b55c153844fb88d63f0578a3f4b98835f4a0df2acdcba3565e793c51428d23f44199652deaaacdbaca68')

prepare() {
  # remove problematic __init__.py: https://github.com/OpenNTI/sphinxcontrib-programoutput/issues/65
  rm -v $_name-$pkgver/src/sphinxcontrib/__init__.py
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )

  cd $_name-$pkgver
  PYTHONPATH=src pytest "${pytest_options[@]}" src/sphinxcontrib/programoutput
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
