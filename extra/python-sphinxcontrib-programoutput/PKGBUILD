# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-sphinxcontrib-programoutput
_name="${pkgname#python-}"
pkgver=0.17
pkgrel=6
pkgdesc="Sphinx extension for capturing program output"
arch=(any)
url="https://github.com/OpenNTI/sphinxcontrib-programoutput/"
license=(BSD-3-Clause)
depends=(
  python
  python-docutils  # pulled in via python-sphinx
  python-sphinx
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha256sums=('1406c26380d6ce26e4db65b89ae21d25b5bdf022d91f290522d19f95123aec51')
b2sums=('896988b32a1973874d9a0b2045f511014dddf3feec6d79923fd9698200f94f409e007401888bfc65616690d665b24791b555cc76185e1fe19ee83734b86c2fcc')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local pytest_options=(
    -vv
    --deselect test_dir/usr/lib/python3.13/site-packages/sphinxcontrib/programoutput/tests/test_directive.py::TestDirective::test_shell_with_unexpected_return_code
    --deselect test_dir/usr/lib/python3.13/site-packages/sphinxcontrib/programoutput/tests/test_directive.py::TestDirective::test_unexpected_return_code
  )

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest "${pytest_options[@]}" test_dir/$_site_packages
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
