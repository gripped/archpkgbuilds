# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-ca
pkgver=0.28.4
pkgrel=1
pkgdesc="A private certificate authority (X.509 & SSH) & ACME server for secure automated certificate management, so you can use TLS everywhere & SSO for SSH."
url="https://github.com/smallstep/certificates"
arch=(x86_64)
license=(Apache)
depends=(glibc pcsclite)
makedepends=(go)
source=("$url/releases/download/v${pkgver}/step-ca_${pkgver}.tar.gz")
sha512sums=('2eb592db97fb5e6b674fd39f04e9c8885ebd9e40bc8033ec8a4f259c89be7bec96595f966aacb20c6dcd9beec1bc9fb53670cb4d86f3bcc610ba7262bfa3e28a')

prepare(){
  mkdir -p bin
}

build() {
  BUILD_DATE="$(date --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" '+%Y-%m-%d %H:%M UTC')"
  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o bin ./cmd/...
}

check() {
  go test -v ./... || true
}

package() {
  install -Dm755 "bin/step-ca" "$pkgdir/usr/bin/step-ca"
  install -Dm644 "systemd/step-ca.service" "$pkgdir/usr/lib/systemd/system/step-ca.service"
}
