# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-ca
pkgver=0.27.4
pkgrel=1
pkgdesc="A private certificate authority (X.509 & SSH) & ACME server for secure automated certificate management, so you can use TLS everywhere & SSO for SSH."
url="https://github.com/smallstep/certificates"
arch=(x86_64)
license=(Apache)
depends=(glibc pcsclite)
makedepends=(go)
source=("$url/releases/download/v${pkgver}/step-ca_${pkgver}.tar.gz")
sha512sums=('c24117e3183f7af1f47f066c47dd6aecc1db38aa6b50d9f5aecc9b7fddd045bdbdbe84d226feceae4216cead7431d29379b0f2bea956ac48db711994d2b167ff')

prepare(){
  mkdir -p bin
}

build() {
  BUILD_DATE="$(date --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" '+%Y-%m-%d %H:%M UTC')"
  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o bin ./cmd/...
}

check() {
  go test -v ./... || true
}

package() {
  install -Dm755 "bin/step-ca" "$pkgdir/usr/bin/step-ca"
}
