# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hazelnut
pkgver=0.2.44
pkgrel=1
pkgdesc="Terminal-based automated file organizer"
arch=('x86_64')
url="https://github.com/ricardodantas/hazelnut"
license=('GPL-3.0-or-later')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('cdb30190b4c8dab671063657733e1c220a541ea957bd82b8ff85dd7a1c6648be')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/${pkgname}d" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
