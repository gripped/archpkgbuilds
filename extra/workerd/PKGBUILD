# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=workerd
pkgver=1.20240821.0
pkgrel=1
pkgdesc="The JavaScript / Wasm runtime that powers Cloudflare Workers"
url='https://github.com/cloudflare/workerd'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
  'libc++'
  'libc++abi'
)
makedepends=(
  'bazel'
  'clang'
  'git'
  'python'
  'rust'
  'tcl'
)
source=(https://github.com/cloudflare/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('10a85d1018f5828c603f85ea394f38657ff673596da14a61a9b9f25adc84a596')
b2sums=('bd5baddb657fc98dde3c98e83233cabec169d5fa0524e4c975daab0a518d644dfa354a106ecc3f59f10c8840d4faff34b51da9b84eedca03b457f1c5834197b9')

build() {
  cd ${pkgname}-${pkgver}
  rm .bazelversion
  bazel build \
    --config=thin-lto \
    --linkopt='-lc++' --linkopt='-lm' --host_linkopt='-lc++' --host_linkopt='-lm' \
    //src/workerd/server:workerd
}

check() {
  cd ${pkgname}-${pkgver}
  bazel-bin/src/workerd/server/workerd test samples/unit-tests/config.capnp
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    bazel-bin/src/workerd/server/workerd
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md
}

# vim: ts=2 sw=2 et:
