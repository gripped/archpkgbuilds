# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=workerd
pkgver=1.20240329.0
pkgrel=1
pkgdesc="The JavaScript / Wasm runtime that powers Cloudflare Workers"
url='https://github.com/cloudflare/workerd'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'bazel'
  'clang'
  'git'
  'libc++'
  'python'
  'rust'
  'tcl'
)
source=(https://github.com/cloudflare/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('65e81caf3a400b739d9ec93439e115d780c1e89b1e5f8829a59299ec316f336f')
b2sums=('7e5f3b1972d577814552436e3e9e7e350a4c95df05e6941d7deef53b39753f07f98f2030840a2bc6fc38710e3047cc91e9e068ede95b15f512ee9a64cf22f5e4')

build() {
  cd ${pkgname}-${pkgver}
  rm .bazelversion
  bazel build \
    --config=thin-lto \
    --action_env=BAZEL_LINKLIBS='-l%:libc++.a -l%:libc++abi.a -lm -static-libgcc' \
    //src/workerd/server:workerd
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    bazel-bin/src/workerd/server/workerd
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md
}

# vim: ts=2 sw=2 et:
