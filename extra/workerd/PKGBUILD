# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=workerd
pkgver=1.20250109.0
pkgrel=1
pkgdesc="The JavaScript / Wasm runtime that powers Cloudflare Workers"
url='https://github.com/cloudflare/workerd'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
  'libc++'
  'libc++abi'
)
makedepends=(
  'bazel'
  'clang'
  'git'
  'lld'
  'python'
  'rust'
  'tcl'
)
options=(!lto)
source=(https://github.com/cloudflare/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('38ec13b98f906fd61fde533f2f275bbfce7097d741e88bcf09e86288e55cebfb')
b2sums=('c31e078799cca545424dc9d59e51ff79c3d1f785b2939d4eec6b610b1994ecd6d62052aa301e0e841b1e3c0faf52cee1d0bacb9ffce7139584ae4dcec7c1b53d')

build() {
  cd ${pkgname}-${pkgver}
  rm .bazelversion
  bazel build \
    --config=thin-lto \
    --linkopt='-lc++' --linkopt='-lm' --host_linkopt='-lc++' --host_linkopt='-lm' \
    //src/workerd/server:workerd
}

check() {
  cd ${pkgname}-${pkgver}
  bazel-bin/src/workerd/server/workerd test samples/unit-tests/config.capnp
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    bazel-bin/src/workerd/server/workerd
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md
}

# vim: ts=2 sw=2 et:
