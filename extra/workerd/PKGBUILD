# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=workerd
pkgver=1.20240222.0
pkgrel=1
pkgdesc="The JavaScript / Wasm runtime that powers Cloudflare Workers"
url='https://github.com/cloudflare/workerd'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'bazel'
  'clang'
  'git'
  'libc++'
  'python'
  'tcl'
)
source=(https://github.com/cloudflare/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('38cdd1f4d58e9ae81dfb68e62550082d474307177186c9cb77fc0171abe32319')
b2sums=('b849ccb3dc9f01d36a7200e0d1ac18aea6176b0367021853d1974ad60583f773786747f074c947b5272ded058de2b7ec2b2018a27aeaaf9b18851b8f77cc36b4')

build() {
  cd ${pkgname}-${pkgver}
  rm .bazelversion
  bazel build \
    --config=thin-lto \
    --action_env=BAZEL_LINKLIBS='-l%:libc++.a -l%:libc++abi.a -lm -static-libgcc' \
    //src/workerd/server:workerd
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    bazel-bin/src/workerd/server/workerd
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md
}

# vim: ts=2 sw=2 et:
