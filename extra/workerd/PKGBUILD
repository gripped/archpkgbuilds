# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=workerd
pkgver=1.20240423.0
pkgrel=1
pkgdesc="The JavaScript / Wasm runtime that powers Cloudflare Workers"
url='https://github.com/cloudflare/workerd'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'bazel'
  'clang'
  'git'
  'libc++'
  'python'
  'rust'
  'tcl'
)
source=(https://github.com/cloudflare/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6527ffbbcfb2c704e6986d470554d10958dbd40b042a14e604474077f36d5d42')
b2sums=('a198c28e7f2b564b37716582b26bc0d3c30897c7a1ea7f40e8d70b5a44c39000d2ec0b411645ac4d707d728e8f3baecd5106c40931e790d3bd80c6976e8a3894')

build() {
  cd ${pkgname}-${pkgver}
  rm .bazelversion
  bazel build \
    --config=thin-lto \
    --action_env=BAZEL_LINKLIBS='-l%:libc++.a -l%:libc++abi.a -lm -static-libgcc' \
    //src/workerd/server:workerd
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    bazel-bin/src/workerd/server/workerd
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md
}

# vim: ts=2 sw=2 et:
