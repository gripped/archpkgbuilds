# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=workerd
pkgver=1.20250529.0
pkgrel=1
pkgdesc="The JavaScript / Wasm runtime that powers Cloudflare Workers"
url='https://github.com/cloudflare/workerd'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
  'libc++'
  'libc++abi'
)
makedepends=(
  'bazel'
  'clang'
  'git'
  'lld'
  'python'
  'rust'
  'tcl'
)
options=(!lto)
source=(https://github.com/cloudflare/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('83e7987833151f4095fc3998a1d612a96a131d7ccb33127ff23011d8c1b03f96')
b2sums=('b1c707e818aa42f5f299f127c5feb0ea0da3bcb40b85c2ae03e8d6b3a05f837c39606f4fed043d09605d6ec9fa0afdff22b5c417370c3c9efa5b6e276b4a7f26')

build() {
  cd ${pkgname}-${pkgver}
  rm .bazelversion
  bazel build \
    --config=thin-lto \
    --linkopt='-lc++' --linkopt='-lm' --host_linkopt='-lc++' --host_linkopt='-lm' \
    //src/workerd/server:workerd
}

check() {
  cd ${pkgname}-${pkgver}
  bazel-bin/src/workerd/server/workerd test samples/unit-tests/config.capnp
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    bazel-bin/src/workerd/server/workerd
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md
}

# vim: ts=2 sw=2 et:
