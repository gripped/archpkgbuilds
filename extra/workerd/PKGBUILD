# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=workerd
pkgver=1.20250719.0
pkgrel=1
pkgdesc="The JavaScript / Wasm runtime that powers Cloudflare Workers"
url='https://github.com/cloudflare/workerd'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
  'libc++'
  'libc++abi'
)
makedepends=(
  'bazel'
  'clang'
  'git'
  'lld'
  'python'
  'rust'
  'tcl'
)
options=(!lto)
source=(https://github.com/cloudflare/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2f6e263b32eee632591a2c9f2a307558a2a177f1b5495ae82cf02d6d592bf8c3')
b2sums=('b220b065f63d634bd4b7ec24d33eb6361d8da743a582bfbecff301097acd6681c8702191df466da3b78941dc5ce20ba040c66c0dbd1ef62646cfeffdf31dea45')

build() {
  cd ${pkgname}-${pkgver}
  rm .bazelversion
  bazel build \
    --config=thin-lto \
    --linkopt='-lc++' --linkopt='-lm' --host_linkopt='-lc++' --host_linkopt='-lm' \
    //src/workerd/server:workerd
}

check() {
  cd ${pkgname}-${pkgver}
  bazel-bin/src/workerd/server/workerd test samples/unit-tests/config.capnp
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    bazel-bin/src/workerd/server/workerd
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md
}

# vim: ts=2 sw=2 et:
