# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=workerd
pkgver=1.20241224.0
pkgrel=1
pkgdesc="The JavaScript / Wasm runtime that powers Cloudflare Workers"
url='https://github.com/cloudflare/workerd'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
  'libc++'
  'libc++abi'
)
makedepends=(
  'bazel'
  'clang'
  'git'
  'lld'
  'python'
  'rust'
  'tcl'
)
options=(!lto)
source=(https://github.com/cloudflare/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2df1af22720fbfc9a2f6c2d27bfb7d33ebb53213792c3dbf4d5da4b5b8dbe70b')
b2sums=('7ce3f9518116c4e65048b664235d5ba93d3834d58bf719d242931f2b6062a92279ee56e784386d1157b96728a9b55c3063394005920ffeef86661f9112cdf95b')

build() {
  cd ${pkgname}-${pkgver}
  rm .bazelversion
  bazel build \
    --config=thin-lto \
    --linkopt='-lc++' --linkopt='-lm' --host_linkopt='-lc++' --host_linkopt='-lm' \
    //src/workerd/server:workerd
}

check() {
  cd ${pkgname}-${pkgver}
  bazel-bin/src/workerd/server/workerd test samples/unit-tests/config.capnp
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    bazel-bin/src/workerd/server/workerd
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md
}

# vim: ts=2 sw=2 et:
