# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=smartcat
_pkgname=sc
pkgver=2.2.0
pkgrel=1
pkgdesc="Putting a brain behind cat"
arch=('x86_64')
url="https://github.com/efugier/smartcat"
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('bbed548ba4a9795437174472219a2eec6b891d77b4ae74a737378c0e77eb5c84b8dafdf4c25078404efa28287844ed5ac0fef00c389220345aaec6d9fd64816c')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
