# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-wallpapers
pkgver=1.0.0alpha5
pkgrel=1
epoch=1
pkgdesc='Wallpapers for the COSMIC Desktop Environment'
arch=(any)
url=https://github.com/pop-os/cosmic-wallpapers
license=(LicenseRef-unknown)
groups=(cosmic)
makedepends=(
  git
  git-lfs
)
_tag=cb8e6d653b5062e046e83b4670c3d9944fa39c39
source=(git+https://github.com/pop-os/cosmic-wallpapers.git#tag=${_tag})
b2sums=('1b2c00afcb1befb2a869c64d8d47bec01267607dafc09b841eee7ce985d497c02c417eb7e1d5b6e47f88c0677888ab66c02cfbee9fc84dc5e5103e8428a61d7c')

prepare() {
  cd cosmic-wallpapers
  git lfs install --local
  git remote add network-origin ${url}
  git lfs fetch network-origin
  git lfs checkout
}

#pkgver() {
#  cd cosmic-wallpapers
#  git describe --tags | sed 's/^epoch-//; s/-alpha./alpha/g'
#}

package() {
  cd cosmic-wallpapers
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
