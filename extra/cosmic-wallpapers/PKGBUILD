# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-wallpapers
pkgver=1.0.7
pkgrel=1
epoch=2
pkgdesc='Wallpapers for the COSMIC Desktop Environment'
arch=(any)
url=https://github.com/pop-os/cosmic-wallpapers
license=(LicenseRef-unknown)
groups=(cosmic)
makedepends=(
  git
  git-lfs
)
source=(git+https://github.com/pop-os/cosmic-wallpapers.git#tag=epoch-${pkgver})
b2sums=('f068b3ce098090e630a99de142d99be2ff1dcf55a4c900b3469f02ef6220626c01a55a9d078915fc45c18f62fd0da9b1f49b0f72c4a555301872cdcc2c7d26c6')

prepare() {
  cd cosmic-wallpapers
  git lfs install --local
  git remote add network-origin ${url}
  git lfs fetch network-origin
  git lfs checkout
}

package() {
  cd cosmic-wallpapers
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
