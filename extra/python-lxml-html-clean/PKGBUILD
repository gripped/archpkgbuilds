# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=lxml-html-clean
pkgname=python-$_pyname
pkgver=0.1.1
pkgrel=2
pkgdesc='HTML cleaner from lxml project'
arch=(any)
url="https://$_pyname.readthedocs.org/"
license=(BSD-3-Clause)
depends=(python
         python-lxml)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
checkdepends=(python-pytest)
_archive="${_pyname//-/_}-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('8a644ed01dbbe132fabddb9467f077f6dad12a1d4f3a6a553e280f3815fa46df')

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	export PYTHONPATH=.
	pytest
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
