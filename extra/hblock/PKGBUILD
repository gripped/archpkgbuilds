# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Héctor Molinero Fernández <hector at molinero dot dev>
# Contributor: Erazem Kokot <contact at erazem dot eu>

pkgname=hblock
pkgver=3.5.0
pkgrel=1
pkgdesc='Adblocker that creates a hosts file from multiple sources'
arch=('any')
url='https://hblock.molinero.dev/'
license=('MIT')
depends=('sh' 'curl' 'systemd')
makedepends=('git')
source=("$pkgname::git+https://github.com/hectorm/hblock#tag=v$pkgver")
sha512sums=('9f10c264aa2e0aa40fc5aa87bff8a9752424ff6a9862a434bc81ca55c0f2633872a8a375da68ce06c6ff20f7dfd24947d8d908b667132ae1f1cf39d98ab45ef3')
b2sums=('e1dd598b10309733889e68d02e10734e2a081cd0f64907666476b5138b4923d686f7822d74170a872073822c0ba76b9d53809c0eb74dbafe41abe018060e1bdd')

check() {
  cd "$pkgname"

  make test
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" prefix='/usr' install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr resources/alt-formats "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
