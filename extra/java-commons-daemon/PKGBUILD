# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>

pkgbase=java-commons-daemon
pkgname=(
  java-commons-daemon
  java-jsvc
)
pkgver=1.4.1
pkgrel=1
arch=(x86_64)
url=https://commons.apache.org/proper/commons-daemon/
license=(APACHE)
_tag=067ee7f609ad25c90fb1c0ff6ad049f33b2ed387
source=(git+https://github.com/apache/commons-daemon.git#tag=${_tag})
b2sums=('9955af72a58485d15597771dce6cf615455e8438d083331b2cf4f3384b2ec1b3b9206625e8b44c388466386481fb03ed1bb941620abc6bbd4634d5572adada90')
makedepends=(
  docbook2x
  docbook-utils
  docbook-xsl
  git
  java-environment
  maven
)

prepare() {
  cd commons-daemon/src/native/unix
  support/buildconf.sh
}

pkgver() {
  cd commons-daemon
  git describe --tags | sed 's/commons-daemon-//'
}

build() {
  cd commons-daemon
  mvn clean package
  cd src/native/unix
  ./configure \
    --with-java=/usr/lib/jvm/default
  make
  db2x_docbook2man man/jsvc.1.xml
}

package_java-commons-daemon() {
  pkgdesc='Alternative invocation mechanism for unix-daemon-like java code'
  depends=(java-runtime)

  cd commons-daemon
  install -Dm 644 target/commons-daemon-${pkgver}.jar -t "${pkgdir}"/usr/share/java/
  ln -s commons-daemon-${pkgver}.jar "${pkgdir}"/usr/share/java/commons-daemon.jar
}

package_java-jsvc() {
  pkgdesc='Apache application for making Java applications run on UNIX more easily'
  depends=(
    java-commons-daemon
    libcap
  )

  cd commons-daemon/src/native/unix
  install -Dm 755 jsvc -t "${pkgdir}"/usr/bin/
  install -Dm 644 JSVC.1 -t "${pkgdir}"/usr/share/man/man1/
}

# vim: ts=2 sw=2 et:
