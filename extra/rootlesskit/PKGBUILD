# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=rootlesskit
pkgver=2.3.6
pkgrel=1
pkgdesc='Native "fake root" for implementing rootless containers'
arch=('x86_64')
url='https://github.com/rootless-containers/rootlesskit'
license=('Apache-2.0')
depends=('glibc' 'shadow')
makedepends=('git' 'go')
checkdepends=('openbsd-netcat')
options=('!lto')
source=("$pkgname::git+https://github.com/rootless-containers/rootlesskit.git#tag=v$pkgver")
sha512sums=('4790991f333d75797ada293e0e522a12d126cad31cb1b867ca6ad92fa2a2ceca34b28b5600b84d7f76172bbfb5d96eb397f1b104473c086f0bd05e496731f29e')
b2sums=('8545dd3b9de56c1f27a3e9d4f064ca4fd1c556573ad9caaecdd76d7caa2b2dcd12ebd2218680904eec1ea36edd28c391f8599f94e42fbe7be9bc57ee21ff69bd')

prepare(){
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./cmd/...
}

# tests fail :(
#check() {
#  cd "$pkgname"
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" build/*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"
}
