# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=rootlesskit
pkgver=2.2.0
pkgrel=1
pkgdesc='Native "fake root" for implementing rootless containers'
arch=('x86_64')
url='https://github.com/rootless-containers/rootlesskit'
license=('Apache-2.0')
depends=('glibc' 'shadow')
makedepends=('git' 'go')
checkdepends=('openbsd-netcat')
options=('!lto')
source=("$pkgname::git+https://github.com/rootless-containers/rootlesskit.git#tag=v$pkgver")
sha512sums=('e5ef5bd02bc87d4125ec4b4cdb4294069d88d2d7aa5e763b8bb898a2f5dac7f004c3fe1786c662e4d31d6c9f90e1d943c39255ede8318ba093e7e482417c8c83')
b2sums=('670b4a7066e0f54009265a4aacb889116c21d2007cab23928d55fa1f6d29f017360ba29617467ab5f6e605cf3ca73956ba8cd38a439387fa973272e9714aae9d')

prepare(){
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./cmd/...
}

# tests fail :(
#check() {
#  cd "$pkgname"
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" build/*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"
}
