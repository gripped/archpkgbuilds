# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=rootlesskit
pkgver=2.3.5
pkgrel=1
pkgdesc='Native "fake root" for implementing rootless containers'
arch=('x86_64')
url='https://github.com/rootless-containers/rootlesskit'
license=('Apache-2.0')
depends=('glibc' 'shadow')
makedepends=('git' 'go')
checkdepends=('openbsd-netcat')
options=('!lto')
source=("$pkgname::git+https://github.com/rootless-containers/rootlesskit.git#tag=v$pkgver")
sha512sums=('3b09665dabbe6d7e797cbc85678c797f8b3cc5c05fa8302e9366f3f44410ae82b8479089a865ac560fe0ad8e28f7f4b8ca9b3e3f16a6b7f067ae34de068a32b1')
b2sums=('888b21359b7d35ad38a564a620428cd1456d31da0eaf9603a2196ccb091f1d1312cae0bac8b169bd07eb7368ab56bd6c1243e9a29bf67e0c29e173954bd5210b')

prepare(){
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./cmd/...
}

# tests fail :(
#check() {
#  cd "$pkgname"
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" build/*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"
}
