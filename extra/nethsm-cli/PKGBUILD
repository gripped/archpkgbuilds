# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nethsm-cli
_name=signstar
pkgver=0.2.0
pkgrel=1
pkgdesc="A command line interface (CLI) for the Nitrokey NetHSM"
arch=(x86_64)
url="https://gitlab.archlinux.org/archlinux/signstar/-/tree/main/nethsm-cli"
_url="https://gitlab.archlinux.org/archlinux/signstar"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  mold
  rust
  rust-script
)
options=(!lto)  # ring does not work with LTO
source=($_name::git+$_url.git?signed#tag=$pkgname/$pkgver)
sha256sums=('764cd4b9764c5a3d9e2c322f1297be9b226f643d42bde98e2f668cba788b0c8b')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features --package $pkgname
}

package() {
  install -vDm 755 $_name/target/release/nethsm -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/$pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $_name/LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/target/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 $_name/target/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm 644 $_name/target/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 $_name/target/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
}
