# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nethsm-cli
_name=signstar
pkgver=0.8.0
pkgrel=2
pkgdesc="A command line interface (CLI) for the Nitrokey NetHSM"
arch=(x86_64)
url="https://signstar.archlinux.page/nethsm-cli"
_url="https://gitlab.archlinux.org/archlinux/signstar"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  mold
  rust
  rust-script
)
source=($_name::git+$_url.git?signed#tag=$pkgname/$pkgver)
sha512sums=('f5c77c3de28651d8f1812771e5c46e7b3c67fc418b84fe02352438bc7d0ba25a0dfc344876a3a738b6438889a2aef70941fe3c84d2881438fca6ec9f16eb0590')
b2sums=('3eb010afbe68e8bdd9f13f5611b53597feeb281a8cd0580c1a99e678a012fc311fe5e24a108f34e431dcc2cfcdf34d452a6eeec61d7552ed46d6ec312f0302f3')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_name
  RUSTUP_TOOLCHAIN=stable cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  CFLAGS+=' -ffat-lto-objects'
  RUSTUP_TOOLCHAIN=stable CARGO_TARGET_DIR=target cargo build --frozen --release --all-features --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_name
  RUSTUP_TOOLCHAIN=stable cargo test --frozen --all-features --package $pkgname
}

package() {
  local command=${pkgname/-cli/}

  install -vDm 755 $_name/target/release/$command -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/$pkgname/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $_name/LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 $_name/output/shell_completions/$command.bash "$pkgdir/usr/share/bash-completion/completions/$command"
  install -vDm 644 $_name/output/shell_completions/_$command -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 $_name/output/shell_completions/$command.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
}
