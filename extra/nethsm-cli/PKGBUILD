# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nethsm-cli
_name=signstar
pkgver=0.9.0
pkgrel=1
pkgdesc="A command line interface (CLI) for the Nitrokey NetHSM"
arch=(x86_64)
url="https://signstar.archlinux.page/nethsm-cli"
_url="https://gitlab.archlinux.org/archlinux/signstar"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  mold
  rust
  rust-script
)
source=($_name::git+$_url.git?signed#tag=$pkgname/$pkgver)
sha512sums=('c82196b067634d12a7a86d67d1eadf31eb384e64941b66efc0a9388068914ce3261f5c0e4739f5a14df317680702e093f0071d2267393ceb3ecd1f9832f081cc')
b2sums=('46ac03369cdf041c4b3d5c3906e74b9a65e0596263ea91655ddcf0f27dc3cb02363e282556ee8baa99ba049b3b092890f45afddf79e43a3b8326cbf385424cac')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_name
  RUSTUP_TOOLCHAIN=stable cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  CFLAGS+=' -ffat-lto-objects'
  RUSTUP_TOOLCHAIN=stable CARGO_TARGET_DIR=target cargo build --frozen --release --all-features --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_name
  RUSTUP_TOOLCHAIN=stable cargo test --frozen --all-features --package $pkgname
}

package() {
  local command=${pkgname/-cli/}

  install -vDm 755 $_name/target/release/$command -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/$pkgname/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $_name/LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 $_name/output/shell_completions/$command.bash "$pkgdir/usr/share/bash-completion/completions/$command"
  install -vDm 644 $_name/output/shell_completions/_$command -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 $_name/output/shell_completions/$command.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
}
