# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=38.0.0
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(
  "https://github.com/cucumber/gherkin/archive/v$pkgver/cucumber-gherkin-$pkgver.tar.gz"
)
sha512sums=('73bd1e147ee540f8855e3a75b9b403aaf653191af23237793ffed473cc8c2c130886c9f72adbe64eebbedc04c2ca0812045a96df727d5cf33e38b0fb9fcee850')
b2sums=('48951a0ef8f3aab3cfee7e83a9cb9a494a220f79ee53ce4b194b06d2ff85efeb94479187bbe5bdd25aab2d10a681d1c9ec78533a1ac880c01549320e80268cf9')

prepare() {
  cd gherkin-$pkgver

  cd ruby

  sed -r -e 's|~>|>=|g' -e "s/, '< 24'//" -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin-$pkgver/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
