# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-filebrowsing
pkgver=4.0.1
pkgrel=1
pkgdesc='MauiKit File Browsing utilities and controls'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         ki18n
         kio
         kservice
         mauikit
         qt6-base
         qt6-declarative
         solid)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('88282ba048090ebe0c5f7a764ebd5bc5a9c61c528cfdf91819b340bfc6002323'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
