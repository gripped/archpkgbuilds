# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contibutor: Balló György <ballogyor+arch at gmail dot com>

pkgname=secrets
pkgver=12.2
pkgrel=1
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
url="https://apps.gnome.org/Secrets"
arch=(any)
license=('GPL-3.0-only')
groups=(gnome-circle)
depends=(
  dconf
  glib2
  graphene
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  python
  python-gobject
  python-pycryptodomex
  python-pyhibp
  python-pykcs11
  python-pykeepass
  python-pyotp
  python-pyusb
  python-requests
  python-validators
  python-yubico
  python-zxcvbn-rs
)
makedepends=(
  git
  gobject-introspection
  meson
)
checkdepends=(python-pytest)
provides=(gnome-passwordsafe)
conflicts=(gnome-passwordsafe)
replaces=(gnome-passwordsafe)
source=("git+https://gitlab.gnome.org/World/secrets.git#tag=${pkgver/[a-z]/.&}")
b2sums=('74f9fb0376d47cfa3a609b394461f0c75f86ed9fb6539ab6b4179850e7cf7f55f9fa6f7ff8eac647a5e5124671d7bd633b5190710cd97737062d57b85a55e420')

prepare() {
  cd secrets

  # safe_element: Do not load OTP codes lazily
  ## https://gitlab.gnome.org/World/secrets/-/merge_requests/1015
  git cherry-pick -n 6ad754725ee1280e8d499fabb8a59b160f4e6cec
}

build() {
  arch-meson secrets build
  meson compile -C build
}

check() (
  export GSETTINGS_SCHEMA_DIR="$PWD/build/data"
  glib-compile-schemas "$GSETTINGS_SCHEMA_DIR"

  PYTHONPATH="$PWD/build:$PWD/secrets" \
    meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"
}
