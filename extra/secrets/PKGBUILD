# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contibutor: Balló György <ballogyor+arch at gmail dot com>

pkgname=secrets
pkgver=11.1.1
pkgrel=1
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
url="https://apps.gnome.org/Secrets"
arch=(any)
license=('GPL-3.0-only')
depends=(
  dconf
  glib2
  graphene
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  python
  python-cairo
  python-gobject
  python-pycryptodomex
  python-pykcs11
  python-pykeepass
  python-pyotp
  python-pyusb
  python-validators
  python-yubico
  python-zxcvbn-rs
)
makedepends=(
  git
  gobject-introspection
  meson
)
checkdepends=(python-pytest)
provides=(gnome-passwordsafe)
conflicts=(gnome-passwordsafe)
replaces=(gnome-passwordsafe)
source=("git+https://gitlab.gnome.org/World/secrets.git#tag=${pkgver/[a-z]/.&}")
b2sums=('698c99c3c708ed769a67dfebc5386f303b2373c5ab42d634c2b960630775719d944a46b5ec7992aa33b250275aa981a1a359a63a23e9c833cf24580931306fd2')

build() {
  arch-meson secrets build
  meson compile -C build
}

check() (
  export GSETTINGS_SCHEMA_DIR="$PWD/build/data"
  glib-compile-schemas "$GSETTINGS_SCHEMA_DIR"

  PYTHONPATH="$PWD/build:$PWD/secrets" \
    meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"
}
