# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contibutor: Balló György <ballogyor+arch at gmail dot com>

pkgname=secrets
pkgver=9.5
pkgrel=1
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
arch=(any)
url="https://apps.gnome.org/Secrets"
license=('GPL-3.0-only')
depends=(
  dconf
  graphene
  gtk4
  glib2
  python
  hicolor-icon-theme
  libadwaita
  python-gobject
  python-pycryptodomex
  python-pykeepass
  python-pyotp
  python-pyusb
  python-pykcs11
  python-validators
  python-yubico
  python-zxcvbn
)
makedepends=(
  git
  gobject-introspection
  meson
)
checkdepends=(python-pytest)
provides=(gnome-passwordsafe)
conflicts=(gnome-passwordsafe)
replaces=(gnome-passwordsafe)
source=("git+https://gitlab.gnome.org/World/secrets.git#tag=${pkgver/[a-z]/.&}")
b2sums=('b0abfbdb96cd1b8aa9f31532c9b54884dfbc21e39e3e8e1b883b80b1ea62ad4872a1e5f0c76a9923538817a06f9f8395572e2e98305fbd88db71bf2f62a7e476')

build() {
  arch-meson secrets build
  ninja -C build
}

check() {
  # Exclude "test_element.py", as it will fail.
  ## https://gitlab.gnome.org/World/secrets/-/issues/481
  meson test -C build \
    'Validate desktop file' \
    'Validate metainfo file' \
    'Validate schema file'
}

package() {
  meson install -C build --destdir "$pkgdir"
}
