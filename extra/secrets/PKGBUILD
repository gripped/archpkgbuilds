# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contibutor: Balló György <ballogyor+arch at gmail dot com>

pkgname=secrets
pkgver=9.3
pkgrel=1
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
arch=(any)
url="https://apps.gnome.org/Secrets"
license=('GPL-3.0-only')
depends=(
  dconf
  graphene
  gtk4
  glib2
  python
  hicolor-icon-theme
  libadwaita
  python-gobject
  python-pycryptodomex
  python-pykeepass
  python-pyotp
  python-pyusb
  python-pykcs11
  python-validators
  python-yubico
  python-zxcvbn
)
makedepends=(
  git
  gobject-introspection
  meson
)
checkdepends=(python-pytest)
provides=(gnome-passwordsafe)
conflicts=(gnome-passwordsafe)
replaces=(gnome-passwordsafe)
source=("git+https://gitlab.gnome.org/World/secrets.git/#tag=${pkgver/[a-z]/.&}")
b2sums=('dc2ac38b3eaad9649c27ed78e51b2fd7997e06b0cfaf4a25de1ca1a507b1b0bd205a06b6aa6d156a990c8cab49d7187cd7cec72ba397b6e56c06814712de93e3')

build() {
  arch-meson secrets build
  ninja -C build
}

check() {
  # Exclude "test_element.py", as it will fail.
  ## https://gitlab.gnome.org/World/secrets/-/issues/481
  meson test -C build \
    'Validate desktop file' \
    'Validate metainfo file' \
    'Validate schema file'
}

package() {
  meson install -C build --destdir "$pkgdir"
}
