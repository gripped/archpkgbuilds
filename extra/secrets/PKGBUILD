# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contibutor: Balló György <ballogyor+arch at gmail dot com>

pkgname=secrets
pkgver=10.4
pkgrel=1
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
url="https://apps.gnome.org/Secrets"
arch=(any)
license=('GPL-3.0-only')
depends=(
  dconf
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  python
  python-cairo
  python-gobject
  python-pycryptodomex
  python-pykcs11
  python-pykeepass
  python-pyotp
  python-pyusb
  python-validators
  python-yubico
  python-zxcvbn-rs
)
makedepends=(
  git
  gobject-introspection
  meson
)
checkdepends=(python-pytest)
provides=(gnome-passwordsafe)
conflicts=(gnome-passwordsafe)
replaces=(gnome-passwordsafe)
source=("git+https://gitlab.gnome.org/World/secrets.git#tag=${pkgver/[a-z]/.&}")
b2sums=('884887fab657e41251888f3e2ff3c611b4c4bbe694ff7f01b2d5f0275eb54f54a3a79d243dafff431a7f1182f45ba209f10c903775c55a27444c81e59157174b')

build() {
  arch-meson secrets build
  meson compile -C build
}

check() (
  export GSETTINGS_SCHEMA_DIR="$PWD/build/data"
  glib-compile-schemas "$GSETTINGS_SCHEMA_DIR"

  PYTHONPATH="$PWD/build:$PWD/secrets" \
    meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"
}
