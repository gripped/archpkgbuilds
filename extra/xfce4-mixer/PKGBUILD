# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: yokirinjin <aur.country952@passmail.com>

pkgname=xfce4-mixer
pkgver=4.18.1
pkgrel=2
pkgdesc="A volume control application based on GStreamer"
arch=('x86_64')
url="https://docs.xfce.org/apps/xfce4-mixer/start"
license=('GPL-2.0-or-later')
depends=('xfce4-panel' 'alsa-utils' 'gstreamer')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/apps/xfce4-mixer.git#tag=$pkgname-$pkgver")
sha256sums=('a43e09dee80ba455c72bb9db9388274f5e1b2561da4c877dc84aa64a891e86c8')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
