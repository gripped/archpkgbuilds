# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Matteo Giordano <mail@matteogiordano.me>
pkgname=superfile
pkgver=1.3.2
pkgrel=1
pkgdesc='Pretty fancy and modern terminal file manager'
arch=('x86_64')
url='https://superfile.netlify.app/'
license=('MIT')
depends=('glibc')
makedepends=('go')
optdepends=(
  'exiftool: metadata plugin'
)
source=("https://github.com/yorukot/superfile/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('372fb9b6f617d64d6dc44bc3becef45c4f804d49374bbbff27c8a0c65fba6955')

build() {
    cd "${pkgname}-${pkgver}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -o "./bin/spf"
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 bin/spf -t "${pkgdir}/usr/bin/"
    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
