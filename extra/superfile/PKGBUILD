# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Matteo Giordano <mail@matteogiordano.me>
pkgname=superfile
pkgver=1.2.1
pkgrel=1
pkgdesc='Pretty fancy and modern terminal file manager'
arch=('x86_64')
url='https://github.com/MHNightCat/superfile'
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('371235eda1b4bf31e5ed23b4dd2e60de40cb09a86a061ba44dea9eff9d13c608')

build() {
    cd "${pkgname}-${pkgver}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -o "./bin/spf"
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 "bin/spf" ${pkgdir}/usr/bin/spf
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
