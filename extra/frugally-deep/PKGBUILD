# Maintainer: Torsten Ke√üler <tpkessler@archlinux.org>
pkgname=frugally-deep
pkgver=0.16.2
pkgrel=1
pkgdesc='Header-only library for using Keras models in C++.'
arch=('x86_64')
url='https://github.com/Dobiasd/frugally-deep'
license=('MIT')
depends=('functional-plus' 'eigen' 'nlohmann-json')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('b16af09606dcf02359de53b7c47323baaeda9a174e1c87e126c3127c55571971')

build() {
	local cmake_args=(
		-S $pkgname-$pkgver
		-B build
		-Wno-dev
		-DCMAKE_BUILD_TYPE=None
		-DCMAKE_INSTALL_PREFIX=/usr)
	cmake "${cmake_args[@]}"
	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build
	install -Dm644 $pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
