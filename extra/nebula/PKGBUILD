# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=nebula
pkgver=1.10.1
pkgrel=1
pkgdesc="A scalable overlay networking tool with a focus on performance, simplicity and security"
arch=('x86_64')
url="https://github.com/slackhq/nebula"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "${pkgname}.service")
sha512sums=('aaa5e684884605b6e4c857c89842aa4ffb7333e50dfc9ac08332ae9bc4478a898492c054eca61fa680ccd5dcd31c7a7dc1bf5d5b5265102a74c092eac6283bc0'
            '5237b50a98c9af95c97fed5d17cd313353b65e1ca981558e415fe95ce4296247fb3051bf6a59e2d2dd89aee3aef3bf3dbaa815becb5c2033523fbf163551b07f')
b2sums=('ffd8b7f0f33c9029c02243a4efb74d9695b6840d6f4fe8cac0582c67c283f79b1c8b7f4d45adf1c0a052cbf253f2df506964eac9206f3edf7f111dcebccd146d'
        '3fb0d210bf96d843c044078d2b7fc3c68f26754aa7b7cdfccf721b62e227de8e6c2ec7c8dbe20b3133c882d01c58c7423f87aa2ef61e9a2d6bbe846779085314')

prepare() {
	cd "${pkgname}-${pkgver}"
	mkdir -p build
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -ldflags "-X main.Build=${pkgver}" -o build ./cmd/...
}

check() {
	cd "${pkgname}-${pkgver}"
	go test ./...
}

package() {
	cd "${pkgname}-${pkgver}"
	install -vDm 755 build/* -t "${pkgdir}/usr/bin/"
	install -Dm 644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
