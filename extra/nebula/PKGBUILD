# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=nebula
pkgver=1.9.4
pkgrel=1
pkgdesc="A scalable overlay networking tool with a focus on performance, simplicity and security"
arch=('x86_64')
url="https://github.com/slackhq/nebula"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "${pkgname}.service")
sha512sums=('95cac08d8d9d7586ca6d2625a4638ce7bae3d9a122bcab97eb5257d7c5ac1299602b2452524dff1dda0ad860072a71362bfb71e4019480ea3f7218c9548c83df'
            '5237b50a98c9af95c97fed5d17cd313353b65e1ca981558e415fe95ce4296247fb3051bf6a59e2d2dd89aee3aef3bf3dbaa815becb5c2033523fbf163551b07f')
b2sums=('71ac85ca22d893ef37ff5b896ef572133a2f80a2defd4bbee7688aa51857b1b06ef3678445c5c537f9f2c8afa910f326e2c9c192a154fd0e20d21be5b90f74aa'
        '3fb0d210bf96d843c044078d2b7fc3c68f26754aa7b7cdfccf721b62e227de8e6c2ec7c8dbe20b3133c882d01c58c7423f87aa2ef61e9a2d6bbe846779085314')

prepare() {
	cd "${pkgname}-${pkgver}"
	mkdir -p build
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -ldflags "-X main.Build=${pkgver}" -o build ./cmd/...
}

check() {
	cd "${pkgname}-${pkgver}"
	go test ./...
}

package() {
	cd "${pkgname}-${pkgver}"
	install -vDm 755 build/* -t "${pkgdir}/usr/bin/"
	install -Dm 644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
