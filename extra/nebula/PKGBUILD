# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=nebula
pkgver=1.8.2
pkgrel=1
pkgdesc="A scalable overlay networking tool with a focus on performance, simplicity and security"
arch=('x86_64')
url="https://github.com/slackhq/nebula"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "${pkgname}.service"
        fix-tests-with-go-1.22.patch)
sha512sums=('48c89b33982cf4ed4b674890ef3f925b54f3a1221f4869a92d940674672f6c32f1e58a12d3b19190568fb5b7b848a46345c16d53a7ac55f8e2cf4a0fd474b3c3'
            '5237b50a98c9af95c97fed5d17cd313353b65e1ca981558e415fe95ce4296247fb3051bf6a59e2d2dd89aee3aef3bf3dbaa815becb5c2033523fbf163551b07f'
            'cebc9175939df08d22750c1d6a1aa44434142a18dfe897124577d0abb89e93867355e62070c6fd561a6aa0654b76e4c43b52af07628a67a27a3ac791f538160c')
b2sums=('516058ec4f057a4ff68eec62e05f525a0f4b5afd63a0e3c5281c8c72a8bc143045c329d392d356e3446e9cc9483e6f8426b90b1991e0b82f916fdec78a85bfb2'
        '3fb0d210bf96d843c044078d2b7fc3c68f26754aa7b7cdfccf721b62e227de8e6c2ec7c8dbe20b3133c882d01c58c7423f87aa2ef61e9a2d6bbe846779085314'
        'c1a6d346f9cb68fc6165285f368b5673cb377d7446311a7a830efb7f1fdf6d5502bc42b440588e54513e2203853afd51cbf221d539e3de0eae3da7acb2747b69')

prepare() {
	cd "${pkgname}-${pkgver}"
	mkdir -p build
	# Temporary patch to fix tests with GO 1.22
	# See https://github.com/slackhq/nebula/pull/981
	patch -Np1 < "${srcdir}/fix-tests-with-go-1.22.patch"
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -ldflags "-X main.Build=${pkgver}" -o build ./cmd/...
}

check() {
	cd "${pkgname}-${pkgver}"
	go test ./...
}

package() {
	cd "${pkgname}-${pkgver}"
	install -vDm 755 build/* -t "${pkgdir}/usr/bin/"
	install -Dm 644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
