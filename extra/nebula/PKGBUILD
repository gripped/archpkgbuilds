# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=nebula
pkgver=1.10.3
pkgrel=1
pkgdesc="A scalable overlay networking tool with a focus on performance, simplicity and security"
arch=('x86_64')
url="https://github.com/slackhq/nebula"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "${pkgname}.service")
sha512sums=('30c1362946de57c7f040bd83c917c73ce521f6fa9d05e4a23a39e45b4ea602e8db9a461f3afec8dfe761778db21db8b5d660ade5f489ae6f8c6f33f5eec97375'
            '5237b50a98c9af95c97fed5d17cd313353b65e1ca981558e415fe95ce4296247fb3051bf6a59e2d2dd89aee3aef3bf3dbaa815becb5c2033523fbf163551b07f')
b2sums=('5ea5ec42c10f1581ef7fa31239b0e01d4ddad488171646cb61efb6b31fd80a309f6f6431d87d673b4f285db661da33a386d80c355cf503e15f2381114f9026ff'
        '3fb0d210bf96d843c044078d2b7fc3c68f26754aa7b7cdfccf721b62e227de8e6c2ec7c8dbe20b3133c882d01c58c7423f87aa2ef61e9a2d6bbe846779085314')

prepare() {
	cd "${pkgname}-${pkgver}"
	mkdir -p build
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -ldflags "-X main.Build=${pkgver}" -o build ./cmd/...
}

check() {
	cd "${pkgname}-${pkgver}"
	go test ./...
}

package() {
	cd "${pkgname}-${pkgver}"
	install -vDm 755 build/* -t "${pkgdir}/usr/bin/"
	install -Dm 644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
