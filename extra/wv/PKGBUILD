# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=wv
pkgver=1.2.9
pkgrel=7
pkgdesc='MSWord library that can load and parse Word 2000, 97, 95 and 6 file formats'
arch=(x86_64)
url='http://www.nl.abisource.com/'
license=(GPL-2.0-or-later)
depends=(
  bash
  glib2
  glibc
  libgsf
  libxml2
  zlib
)
source=(
  "http://www.nl.abisource.com/downloads/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
  wv-1.2.9-format_security.patch
)
b2sums=(
  6a6aa6b5f87a934c703c8a74d85af3d7aa84dcd2bc613a3af7fb8d69a9339e71ba2618ae8f725b90c3fe0b0199f4b224bdb32a50c2b6b6614f84b6a14437633f
  18641a15c75a32250012771bd8ca8b0739f503ef8d1d2eebe23f2d378996aa3f6e2f0cfa72f8077add3db33f8ece86c57e80cc5b868719472b45ba1bed10a7d0
)

prepare() {
  cd $pkgname-$pkgver

  # Fix build
  patch -Np1 -i ../wv-1.2.9-format_security.patch

  touch AUTHORS ChangeLog
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
