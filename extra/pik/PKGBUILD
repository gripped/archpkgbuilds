# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=pik
pkgver=0.6.4
pkgrel=1
pkgdesc="Process interactive kill"
arch=('x86_64')
url="https://github.com/jacek-kurlit/pik"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('f998679f5cd79af062daf8ab39aa6e19c838b2e9be560ebd65d29f721e253089a1e71c6d93cbc5b15432919b4f335261bfb02aedbdb4a576fca5257e6fb4fcbd')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
