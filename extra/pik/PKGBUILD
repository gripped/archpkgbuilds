# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=pik
pkgver=0.7.0
pkgrel=1
pkgdesc="Process interactive kill"
arch=('x86_64')
url="https://github.com/jacek-kurlit/pik"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('764f1385fb3ebd06c0c54e657f1d2b6a7872579ce9baf6e81512b62227f249a10a9baa50b7937eeac51de256ac43a5e94e96c00b5e21b0c09dfe3f6a506e7569')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
