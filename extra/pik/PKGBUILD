# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=pik
pkgver=0.29.0
pkgrel=1
pkgdesc="Process interactive kill"
arch=('x86_64')
url="https://github.com/jacek-kurlit/pik"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('14727a4dc786af9b322032d65cbd3d45e67ae419e13fae8753f424f3a96a1bf0eca2c41322560dc607cec6fcd87d51c2f60477267ad1166b0428aa1a459fdda0')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
