# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=pik
pkgver=0.10.0
pkgrel=1
pkgdesc="Process interactive kill"
arch=('x86_64')
url="https://github.com/jacek-kurlit/pik"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('beb454794282c222dee329009f75f39ba7e5b582f5a9edbf0606829d9942751f62a893c38ba4166ccd6e8358916c25eabca0dba265ba157690dcf3246a6ec310')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
