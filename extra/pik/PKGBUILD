# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=pik
pkgver=0.28.0
pkgrel=1
pkgdesc="Process interactive kill"
arch=('x86_64')
url="https://github.com/jacek-kurlit/pik"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('82505613ad6175ec92f5c2ae54422b1aa51f0f7e34166ac7f74e9ea7092d8df21f85a8898f57b2638ba476e26e0e7535ec5c0fb5cea848bd9836891d0fac8086')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
