# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=clapper-enhancers
pkgver=0.8.2
pkgrel=1
pkgdesc="Plugins enhancing Clapper library capabilities"
arch=(x86_64)
url="https://github.com/Rafostar/clapper-enhancers"
license=('LGPL-2.1-or-later')
depends=(
  glib2
  glibc
  gstreamer
  json-glib
  libclapper-0.0.so
  libpeas-2
  libsoup3
  python
  python-gobject
  yt-dlp
)
makedepends=(
  git
  meson
)
source=("git+https://github.com/Rafostar/clapper-enhancers.git?signed#tag=$pkgver")
b2sums=('042de7666f712f4f3e325622aed82d3ec52a3641cc2b08df442a955a022245b228fbb521dc4e85e7d1260b2ba32b684a9ae6d1d87850994f538f13f47e3ffaa0')
validpgpkeys=(
  'D3B89B4D49ABFABA6A3EF1B9228FF576F6E2EC17'    # Rafał Dzięgiel <rafostar.github@gmail.com>
)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
