# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=clapper-enhancers
pkgver=0.8.3
pkgrel=1
pkgdesc="Plugins enhancing Clapper library capabilities"
arch=(x86_64)
url="https://github.com/Rafostar/clapper-enhancers"
license=('LGPL-2.1-or-later')
depends=(
  glib2
  glibc
  gstreamer
  json-glib
  libclapper-0.0.so
  libpeas-2
  libsoup3
  python
  python-gobject
  yt-dlp
)
makedepends=(
  git
  meson
)
source=("git+https://github.com/Rafostar/clapper-enhancers.git?signed#tag=$pkgver")
b2sums=('d63098ba29cc31221a556a05e7f711ff5fccb858261c11a9df4866f1f405ffee0492fb8f45af758909c29b208e7f026c507c3d281b7258f73cf72ef8805fdf1a')
validpgpkeys=(
  '0E0DC8F450DBFADF5BA08B117D6F9EA15BAE2562'    # Rafał Dzięgiel <rafostar.github@gmail.com>
)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
