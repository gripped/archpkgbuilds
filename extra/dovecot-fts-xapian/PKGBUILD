# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>

# This must be built against the version of dovecot being used,
# otherwise it will fail to load and thus work.
# Specify the version of dovecot to be used here:
_dcpkgver=2.3.21.1
# Make sure to bump pkgrel if changing this.

pkgname=dovecot-fts-xapian
_pkgname="${pkgname#dovecot-}"
_pkgver=1.7.17 # pacman's vercmp does not like alphabet characters without a dot prefix
pkgver=1.8.1
pkgrel=1
pkgdesc="Dovecot FTS plugin based on Xapian"
arch=('x86_64')
url="https://github.com/grosjo/fts-xapian"
license=('LGPL-2.1-or-later')
depends=("dovecot=${_dcpkgver}" 'icu' 'xapian-core')
makedepends=('git')
install="$pkgname.install"
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('2f89b3ca0b307fbd855e5af0522c9dcf7b4f91f1d093b3c06e401733c8e9a99c46855f4fc99b57e0ac35f70df82e61c4112ecb236ec4a388ff571b085d9893e0')
b2sums=('660bb695a2c757147bea1f45474f6c0dfeeec5515f1dd6408d966674ab8577909861f7a8e5f61ece6f350b2c51412271135ccc5dc92000713db17fc6530861ab')

build() {
  cd "$pkgname"
  autoreconf -vi
  ./configure \
    --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot

  make

  # generate systemd unit files
  mkdir build
  sed "s:@@prefix@@:/usr/bin:" contrib/systemd/dovecot-fts-optimize.service.in \
    > build/dovecot-fts-optimize.service
  sed "s:@@index_frequency@@:daily:" contrib/systemd/dovecot-fts-optimize.timer.in \
    > build/dovecot-fts-optimize.timer
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" \
    build/dovecot-fts-optimize.{service,timer}
}
