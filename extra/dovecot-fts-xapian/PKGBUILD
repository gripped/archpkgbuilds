# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>

# This must be built against the version of dovecot being used,
# otherwise it will fail to load and thus work.
# Specify the version of dovecot to be used here:
_dcpkgver=2.3.21.1
# Make sure to bump pkgrel if changing this.

pkgname=dovecot-fts-xapian
_pkgname="${pkgname#dovecot-}"
_pkgver=1.7.14 # pacman's vercmp does not like alphabet characters without a dot prefix
pkgver=1.7.14
pkgrel=1
pkgdesc="Dovecot FTS plugin based on Xapian"
arch=('x86_64')
url="https://github.com/grosjo/fts-xapian"
license=('LGPL-2.1-or-later')
depends=("dovecot=${_dcpkgver}" 'icu' 'xapian-core')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_pkgver.tar.gz")
sha512sums=('cc1308a4da7a48dcdc926e233ee5b8be34623816931a2392020c352f017338a0a8e6c569ac7caba67332f72505981648c5d23f0e77fcf04454de2405a5d1dc1c')
b2sums=('a6452ed5d940898d56fd0a40346e887a2dce03493b2b45963f0089b40737cdaaab02df5ad97d0a6b7ec606faf683a2e971bae6a3a206322bd3cfff42be3b8cf0')

build() {
  cd "$_pkgname-$_pkgver"
  autoreconf -vi
  ./configure \
    --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot

  make

  # generate systemd unit files
  mkdir build
  sed "s:@@prefix@@:/usr/bin:" contrib/systemd/dovecot-fts-optimize.service.in \
    > build/dovecot-fts-optimize.service
  sed "s:@@index_frequency@@:daily:" contrib/systemd/dovecot-fts-optimize.timer.in \
    > build/dovecot-fts-optimize.timer
}

package() {
  cd "$_pkgname-$_pkgver"

  make DESTDIR="$pkgdir" install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" \
    build/dovecot-fts-optimize.{service,timer}
}
