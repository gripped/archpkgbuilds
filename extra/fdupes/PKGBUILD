# Maintainer: Xyne <ca dot archlinux at xyne, backwards>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch at gmail dot com>
# Contributor: Chris Winter <twidds at gmail dot com>

pkgname=fdupes
pkgver=2.3.2
pkgrel=1
epoch=1 # Version changed from 1.51 to 1.6.1
pkgdesc="a program for identifying or deleting duplicate files residing within specified directories"
arch=(x86_64)
url="https://github.com/adrianlopezroche/fdupes"
license=(MIT)
depends=(
  glibc
  ncurses
  pcre2
  sqlite
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('735f0a8f3076184ee1966c5671dac05df5a9ab40ac04dce374ccb9fe43560398')

prepare() {
  cd $pkgname-$pkgver
  autoreconf --install
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # Extract license information from README
  sed -n '/^Legal Information/,$p' README \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 
}
