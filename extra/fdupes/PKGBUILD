# Maintainer: Xyne <ca dot archlinux at xyne, backwards>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch at gmail dot com>
# Contributor: Chris Winter <twidds at gmail dot com>

pkgname=fdupes
pkgver=2.4.0
pkgrel=2
epoch=1 # Version changed from 1.51 to 1.6.1
pkgdesc="a program for identifying or deleting duplicate files residing within specified directories"
arch=(x86_64)
url="https://github.com/adrianlopezroche/fdupes"
license=(MIT)
depends=(
  glibc
  ncurses
  pcre2
  sqlite
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        $pkgname-LICENSE::https://raw.githubusercontent.com/adrianlopezroche/fdupes/5eae26bc9ecf6ae8d52c36c02e612055b3c4dd12/LICENSE)
sha256sums=('2a11250ef0e9d82837dcf336853b2891732cc78e2888ccdc6b689ab7b47b0f5b'
            '9705b67c16d6d0960eb6c3fe8b21973f8c9822285d59a88585d8f0ba9d643c42')

prepare() {
  cd $pkgname-$pkgver
  autoreconf --install
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -vDm644 "$srcdir"/${pkgname}-LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
