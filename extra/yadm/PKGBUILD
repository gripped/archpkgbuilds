# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=yadm
pkgver=3.3.0
pkgrel=1
pkgdesc="Yet Another Dotfiles Manager"
arch=('any')
url="https://github.com/yadm-dev/yadm"
license=('GPL3')
depends=('git' 'awk')
optdepends=('gnupg: encrypt/decrypt sensitive files'
            'python-j2cli: use jinja2 templates'
            'esh: use shell templates')
source=("$pkgname-$pkgver.tar.gz::https://github.com/yadm-dev/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('fea6e5d2e940089caf8120072a32e2ee16764db7f66f5d4e060c7c1b6f95bc91e375a78c91a02fd520efaf2a6c33a64cb97618d953e1b27abb87a56f9fd032eb')
b2sums=('74287a5d43a47bd7c8baf36f4e054cdbdd02f6dbac298980439bc55e02c44a48372fe84bf8b2048329f683b393beb252b205f5c4eaf9ab53369554494a28da2f')

package() {
	cd $pkgname-$pkgver
	install -vDm755 yadm -t "$pkgdir/usr/bin"
	install -vDm644 yadm.1 -t "$pkgdir/usr/share/man/man1"
	install -vDm644 completion/bash/yadm -t "${pkgdir}/usr/share/bash-completion/completions"
	install -vDm644 completion/zsh/_yadm -t "${pkgdir}/usr/share/zsh/site-functions"
	install -vDm644 completion/fish/yadm.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"
}
