# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=yadm
pkgver=3.5.0
pkgrel=1
pkgdesc="Yet Another Dotfiles Manager"
arch=('any')
url="https://github.com/yadm-dev/yadm"
license=('GPL3')
depends=('git' 'awk')
optdepends=('gnupg: encrypt/decrypt sensitive files'
            'python-j2cli: use jinja2 templates'
            'esh: use shell templates')
source=("$pkgname-$pkgver.tar.gz::https://github.com/yadm-dev/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('dcaaa4520790d3e5870067d9bf1574362af8f87cc9195a8252818317a88820e99dd7e2eead0896000889988a331d2c36adf3dbd701458a8024f4206c207e8ab9')
b2sums=('e927c2941d824f4e37bf06159311c6040bc0197999b4b993b79d68a354235ea504beeebf63a39b36fbe1de95abfc1845286d62ec800370b8caee014ccb6a08fd')

package() {
	cd $pkgname-$pkgver
	install -vDm755 yadm -t "$pkgdir/usr/bin"
	install -vDm644 yadm.1 -t "$pkgdir/usr/share/man/man1"
	install -vDm644 completion/bash/yadm -t "${pkgdir}/usr/share/bash-completion/completions"
	install -vDm644 completion/zsh/_yadm -t "${pkgdir}/usr/share/zsh/site-functions"
	install -vDm644 completion/fish/yadm.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"
}
