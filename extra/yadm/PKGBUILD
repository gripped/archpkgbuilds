# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=yadm
pkgver=3.4.0
pkgrel=1
pkgdesc="Yet Another Dotfiles Manager"
arch=('any')
url="https://github.com/yadm-dev/yadm"
license=('GPL3')
depends=('git' 'awk')
optdepends=('gnupg: encrypt/decrypt sensitive files'
            'python-j2cli: use jinja2 templates'
            'esh: use shell templates')
source=("$pkgname-$pkgver.tar.gz::https://github.com/yadm-dev/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('7883dc441242b944ed189852d930db86e432a9ab2ed2de0e195a040f4bdce07701180543aa2c7abeb3cb3abc903ec0168da826780bed5df20abf620c5cde3e37')
b2sums=('7e3cebf64fa1b32eb0fabce1960614d450d7f861246baee2ba108de48bce9bc4243b82c60b0fbe6fd8a0aab673e971c4fd6123e7c2d29c980762464aa7030759')

package() {
	cd $pkgname-$pkgver
	install -vDm755 yadm -t "$pkgdir/usr/bin"
	install -vDm644 yadm.1 -t "$pkgdir/usr/share/man/man1"
	install -vDm644 completion/bash/yadm -t "${pkgdir}/usr/share/bash-completion/completions"
	install -vDm644 completion/zsh/_yadm -t "${pkgdir}/usr/share/zsh/site-functions"
	install -vDm644 completion/fish/yadm.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"
}
