# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apt-swarm
pkgver=0.5.0
pkgrel=1
pkgdesc="Experimental p2p gossip network for OpenPGP signature transparency ðŸ¥¸"
url='https://github.com/kpcyrd/apt-swarm'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
backup=("etc/$pkgname.conf")
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('82812656e480ecb87d7be2163adee9db1fb5b5ba7589761e1095dcbc16ba9242')
b2sums=('775d77b09fe3fd495c759359e4cc845353328f5360add7614e2eca29e9cd3fa331489ae540110fefa3ac086aa3e246697486abd4cae1184172a86a7cb181c3a9')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/apt-swarm

  # example config
  install -Dm 644 -t "${pkgdir}/etc" \
    contrib/apt-swarm.conf

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" plumbing completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" plumbing completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" plumbing completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
