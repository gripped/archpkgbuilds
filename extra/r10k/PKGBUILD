# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: arakmar

pkgname=r10k
pkgver=5.0.0
pkgrel=1
pkgdesc='Provides a general purpose toolset for deploying Puppet environments and modules.'
arch=('any')
url="https://github.com/puppetlabs/${pkgname}"
license=('Apache-2.0')
depends=(
  ruby
  ruby-colored2
  ruby-cri
  ruby-gettext-setup
  ruby-jwt
  ruby-log4r
  ruby-minitar
  ruby-multi_json
  ruby-puppet_forge
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  git
  ruby-rspec
  ruby-rugged
  ruby-syslog
)
options=('!emptydirs')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c54a213417690483acdac1a7fe2f1a43bda070bc6571ec7922c1ad6e84944aa97d6cca5f3cf7f1a7dfba8ce055cbd656c298758587c3e03ace1a033cfca498bf')
b2sums=('2177525b9ade8ccbab9e125ba630752a6bf03e0fb378c2929ac984c049a1882186c70bf11215d949f21d2121a58ea9c22d17ff4892202bd422f46f53bc67ec38')

prepare() {
  cd "${pkgname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression "s|dependency 'colored2',   '([0-9])|dependency 'colored2', '>= \1|g" \
    "${pkgname}.gemspec"

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|git ls-files|find . -type f -not -path "*/\.git*"|' "${pkgname}.gemspec"
}

build() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${pkgname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${pkgname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${pkgname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${pkgname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rspec
}

package() {
  cd "${pkgname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.mkd --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
