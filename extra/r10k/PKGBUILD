# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: arakmar
pkgname=r10k
pkgver=3.16.2
pkgrel=2
pkgdesc='Provides a general purpose toolset for deploying Puppet environments and modules.'
arch=('any')
url="https://github.com/puppetlabs/${pkgname}"
license=('Apache-2.0')
depends=('ruby' 'ruby-colored2' 'ruby-cri' 'ruby-gettext-setup' 'ruby-log4r' 'ruby-multi_json' 'ruby-semantic_puppet' 'ruby-puppet_forge' 'ruby-rugged' 'ruby-fast_gettext' 'ruby-gettext' 'ruby-faraday' 'ruby-faraday-net_http' 'ruby-jwt')
makedepends=('ruby-minitar' 'ruby-yard')
checkdepends=('ruby-rspec')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('38f0ba4599bb8999e6041e1fb313d924057edf4e57eb6e01956241deb497d0013a98f3ec84ffda4a6dedba6a391f250e248abf939d9cf587cb4b2f975027cc08')
options=('!emptydirs')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # update the gemspec to allow newer versions
  sed --in-place 's|~>|>=|g' "${pkgname}.gemspec"
  # don't use git to detect files for the package
  sed --in-place 's|git ls-files|find|g' "${pkgname}.gemspec"
  # drop dev/CI dependencies; they only create package dependecy circles
  sed --in-place '/add_development_dependency/d' "${pkgname}.gemspec"
  # We provide a newer jwt
  sed --in-place "s|s.add_dependency 'jwt', '>= 2.2.3', '< 2.8.0'|s.add_dependency 'jwt', '>= 2.2.3', '< 3'|" "${pkgname}.gemspec"
  # lockfiles should be removed
  rm -fv Gemfile.lock
  # do not rely on Gemfiles (unless the project *really* needs it)
  rm -fv Gemfile
}

build() {
  cd "${pkgname}-${pkgver}"
  gem build "${pkgname}.gemspec"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rspec --color --format documentation spec/unit
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${pkgname}-${pkgver}.gem"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm 644 r10k.yaml.example CHANGELOG.mkd README.mkd CONTRIBUTING.mkd -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv doc integration "${pkgdir}/usr/share/doc/${pkgname}/"

  # remove unrepreducible files
  rm -frv \
    "$pkgdir/$_gemdir/cache/" \
    "$pkgdir/$_gemdir/spec/" \
    "$pkgdir/$_gemdir/integration/" \
    "$pkgdir/$_gemdir/gems/$_name-$pkgver/vendor/" \
    "$pkgdir/$_gemdir/doc/$_name-$pkgver/ri/ext/"

  find "$pkgdir/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
}

# vim: ts=2 sw=2 et:
