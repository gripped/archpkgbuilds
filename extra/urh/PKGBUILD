# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinuc.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Johannes Pohl <johannes.pohl90@gmail.com>

pkgname=urh
pkgver=2.9.8
pkgrel=1
pkgdesc='Universal Radio Hacker: investigate wireless protocols like a boss'
arch=('x86_64')
url='https://github.com/jopohl/urh'
license=('GPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'python'
  'python-numpy'
  'python-psutil'
  'python-pyqt5'
  'python-setuptools'
)
makedepends=(
  'airspy'
  'bladerf'
  'cython'
  'gcc'
  'hackrf'
  'libiio'
  'libuhd'
  'limesuite'
  'python-build'
  'python-installer'
  'python-wheel'
  'rtl-sdr'
)
checkdepends=(
  'python-pytest-xdist'
  'xorg-server-xvfb'
)
optdepends=(
  'airspy: Airspy backend'
  'bladerf: BladeRF backend'
  'cython: compiling native modules inside URH'
  'gnuradio-osmosdr: OsmoSDR backend, via GNURadio'
  'gnuradio: GNURadio backend'
  'hackrf: HackRF backend'
  'libiio: IIO backend (PlutoSDR)'
  'libuhd: USRP backend'
  'limesuite: LimeSDR backend'
  'python-pyaudio: Soundcard backend'
  'python-pylibiio: IIO backend (PlutoSDR)'
  'rtl-sdr: RTL-SDR backend'
)
source=(
  "$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "$pkgname-numpy-gauss-fir.patch::$url/commit/0f9365bffbe4ca8327fc98c32a1dbf2e82381b4a.patch"
  "$pkgname-numpy-tobytes.patch::$url/commit/2aeadcf56676d95ce700c5677a851ce84ae98e5b.patch"
  "$pkgname-cython3.1.patch::$url/commit/4979a5c94d7c0c728fa2ff3fda8f564e6ed6c7b4.patch"
)
sha512sums=('a5327bee699bba8e6ffef2e7ec54f5f4b354feda18340c9b567b10b8c9283c5c907a546fd8a5660a2c325d42b8928cb9592630d0c2a5ed7dfdcf5dc434be4cb2'
            'b33b2ac570ac21485bdafac7d7df45c125c1b56e542179b3e927b617c9f602fb79e8dc5bc6435b9fe3613dc582a8823a344b732dada280794aed7022f196f905'
            'd62019ae3a442886d4f752125948c2adfe69ed49ce118b1f2decdb08fc7156df0580f2a51018131e7108d368ef0fcac621b916e09b1df0b781f31d2840aea3af'
            'f7de805c19985bb4e90c9782895437b9a5e64986e3d06955b171238a3ed98a7056892d8db52f5226e6c100df66ec851bc1cd567c6e20d2a28cdc10bda096cfb2')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../$pkgname-numpy-gauss-fir.patch
  patch -Np1 < ../$pkgname-numpy-tobytes.patch
  patch -Np1 < ../$pkgname-cython3.1.patch || :

  # No need to package this build script.
  rm -v src/urh/cythonext/build.py

  # These init-files mess with the PATH and break the tests.
  rm -v tests/__init__.py
  rm -v tests/auto_interpretation/__init__.py
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  export QT_QPA_PLATFORM=xcb
  xvfb-run test-env/bin/python -m pytest \
    --deselect tests/awre/test_generated_protocols.py::TestGeneratedProtocols::test_without_preamble
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 data/urh.desktop "$pkgdir/usr/share/applications/urh.desktop"
  install -vDm644 data/icons/appicon.png "$pkgdir/usr/share/pixmaps/urh.png"
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
