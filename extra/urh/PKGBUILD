# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinuc.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Johannes Pohl <johannes.pohl90@gmail.com>

pkgname=urh
pkgver=2.10.0
pkgrel=1
pkgdesc='Universal Radio Hacker: investigate wireless protocols like a boss'
arch=('x86_64')
url='https://github.com/jopohl/urh'
license=('GPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'python'
  'python-numpy'
  'python-psutil'
  'python-pyqt6'
  'python-setuptools'
)
makedepends=(
  'airspy'
  'bladerf'
  'cython'
  'gcc'
  'hackrf'
  'libiio'
  'libuhd'
  'limesuite'
  'python-build'
  'python-installer'
  'python-wheel'
  'rtl-sdr'
)
checkdepends=(
  'python-pytest-xdist'
  'xorg-server-xvfb'
)
optdepends=(
  'airspy: Airspy backend'
  'bladerf: BladeRF backend'
  'cython: compiling native modules inside URH'
  'gnuradio-osmosdr: OsmoSDR backend, via GNURadio'
  'gnuradio: GNURadio backend'
  'hackrf: HackRF backend'
  'libiio: IIO backend (PlutoSDR)'
  'libuhd: USRP backend'
  'limesuite: LimeSDR backend'
  'python-pyaudio: Soundcard backend'
  'python-pylibiio: IIO backend (PlutoSDR)'
  'rtl-sdr: RTL-SDR backend'
)
source=(
  "$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
)
sha512sums=('0e46c7f74b1108ed32128cb1b3034b62ec03595057e5822c7e27e9340d2b81cc6240376bef3d3ba5b03e47396ccdfd737cb911788a1f36be2be30928e101fc57')

prepare() {
  cd $pkgname-$pkgver

  # No need to package this build script.
  rm -v src/urh/cythonext/build.py

  # These init-files mess with the PATH and break the tests.
  rm -v tests/__init__.py
  rm -v tests/auto_interpretation/__init__.py
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  export QT_QPA_PLATFORM=xcb
  xvfb-run test-env/bin/python -m pytest \
    --deselect tests/awre/test_generated_protocols.py::TestGeneratedProtocols::test_without_preamble
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 data/urh.desktop "$pkgdir/usr/share/applications/urh.desktop"
  install -vDm644 data/icons/appicon.png "$pkgdir/usr/share/pixmaps/urh.png"
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
