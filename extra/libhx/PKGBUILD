# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Max Roder <maxroder@web.de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=libhx
pkgver=4.24
pkgrel=1
pkgdesc='A library providing queue, tree, I/O and utility functions'
arch=(x86_64)
url='http://libhx.sourceforge.net/'
license=(GPL-3.0-only
         LGPL-3.0-only)
depends=(glibc)
makedepends=(git)
source=(git+https://codeberg.org/jengelh/libhx#tag=v$pkgver)
sha256sums=('cfd32e95510a46f2e6fc5367b706d857b28b6f9eccc5074c83de2e07b60df1b5')

prepare() {
  cd libhx
  autoreconf -vi
}

build() {
  cd libhx
  ./configure --prefix=/usr
  make
}

package() {
  cd libhx
  make DESTDIR="$pkgdir" install
}
