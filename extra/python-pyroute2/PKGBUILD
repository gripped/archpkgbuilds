# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Contributor: Alexander Phinikarides <alexisph _AT_ gmail _DOT_ com>

pkgbase='python-pyroute2'
_pkgbase="${pkgbase//python-/}"
pkgname=("python-${_pkgbase}")
pkgdesc="A pure Python netlink and Linux network configuration library"
pkgver=0.9.5
pkgrel=3
arch=('any')
url="https://docs.pyroute2.org/"
license=('GPL-2.0-or-later OR Apache-2.0')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-netaddr' 'python-pytest-asyncio')
source=("${_pkgbase}-${pkgver}.tar.gz::https://github.com/svinota/${_pkgbase}/archive/${pkgver}.tar.gz")
sha512sums=('4a69f55886951afb9ae39cc866be89e968bd3efaf1c0abf8a7f6a38aae313c0fadf6fab270b0ef620cdd980d4ca49c86c20100d3a4039a5b26919eb837653a77')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  # Update the version manually. Otherwise it would look for a git tag which is not present in the distribution.
  sed -i "s/version = get_project_version.*$/version = '${pkgver}'/" util/update_version.py
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python -m build --wheel --no-isolation

  # TODO: building the manpages requires additional packages
  # - python-sphinx (available in repo)
  # - python-aafigure (available in repo)
  # - python-sphinx-code-include (NOT available in repo yet)
  # https://pypi.org/project/sphinx-code-include/

  #export PYTHONPATH="${PWD}"
  #cd docs
  #make man
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  export PYTHONPATH="${PWD}"
  pytest -vv \
    tests/test_minimal/*.py \
    tests/test_unit/*.py
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim:set ts=2 sw=2 et:
