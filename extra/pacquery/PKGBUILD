# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=pacquery
pkgver=0.0.2
pkgrel=2
pkgdesc="A CLI tool to get JSON output of pacman packages from the system's syncdb"
url='https://github.com/jelly/pacquery'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'libalpm.so')
makedepends=('cargo' 'git')
groups=('archlinux-tools')
source=(git+https://github.com/jelly/pacquery.git#tag=v$pkgver?signed
        pacman-7.patch)
sha512sums=('e9e46d13fe0613c2874319271b4633cd88f7477d78aa559816220be36939495079f93c184cdd511712dcb761dc25a1eb9137a66bc1bbcfa58bc2468662f7ff51'
            'c0b38c9a6c2eb2239db02f6ef41e246666d85e2f5b8c407075590e8edb87cd7a0ced45000684d416d66c2502adb3a107e2199157352583712f780a37b6495ddd')
validpgpkeys=("E499C79F53C96A54E572FEE1C06086337C50773E")

prepare() {
  cd ${pkgname}
  mkdir -p completions
  mkdir -p man
  patch -Np1 -i ../pacman-7.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}
  cargo build --frozen --release --all-features
  OUT_DIR=completions/ "./target/release/$pkgname-completions"
  OUT_DIR=man/ "./target/release/$pkgname-mangen"
}

check() {
  cd ${pkgname}
  cargo test --frozen --all-features
}

package() {
  cd ${pkgname}

  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm 644 "completions/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions"
  install -Dm 644 "man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"

}
