# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: John Robinson <jbr100uk@gmail.com>

pkgname=midish
pkgver=1.4.1
pkgrel=1
pkgdesc='Command-line MIDI sequencer and filter'
arch=(x86_64)
url='https://midish.org/'
license=(0BSD)
depends=(
  glibc
  alsa-lib
)
makedepends=(python-html2text)
optdepends=('sh: for smfplay/smfrec')
source=(
  "https://www.midish.org/$pkgname-$pkgver.tar.gz"{,.asc}
  change-examples-directory.patch
)
sha512sums=('f8aa67f7dbc8b3c1eebc439937c8e1048faf82bdd0fb72f61ab86cabac1484c47cf911818b89c7747e1d73d86402c6e582ace93bd9e773d24387dc83aadbba20'
            'SKIP'
            '8ca1460124477ceba4f1f84edaa47c60b3089bbcc3691c765def2eef9ce9bf14666242d70a7f84d3a4f7ab64b28698d8623bd8fbde244cfa0de9c83288b6d9ca')
b2sums=('07a6908156739a4d09460f493a66141d00055b893ca3f5458be226e8a02dcfc20d53e94a1e95095d9653b78296887ef29c8d98b19f9e1f05b156a0611d2e62d4'
        'SKIP'
        'bce5a65a20538167ccf4094b88e4d02b73b28f37939c6e044aa40fa7b74dc91457c3ba8719d4838ab8415c8739dc0a2b9fcf2deebc6095c1792503a730d4dda4')
validpgpkeys=('6B1A7447AAF091CCDD36BAA6015E339411694A6E') # Alexandre Ratchov <alex@caoua.org>

prepare() {
  cd "$pkgname-$pkgver"

  # install examples to standard directory
  patch -p1 -i "$srcdir/change-examples-directory.patch"

  # extract license from manual.html
  awk '/23 Copyright/{flag=1; next} /24 Thanks/{flag=0} flag' manual.html \
    | html2text \
    | sed -r -e 's/^>\s*//' > LICENSE
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
