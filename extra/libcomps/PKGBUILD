# Contributor: larchunix
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=libcomps
pkgver=0.1.21
pkgrel=4
pkgdesc="Comps XML file manipulation library"
arch=('x86_64')
url="https://github.com/rpm-software-management/$pkgname"
license=('GPL2')
depends=('expat' 'libxml2' 'zlib')
makedepends=('cmake' 'python' 'python-setuptools' 'doxygen' 'python-sphinx')
checkdepends=('check')
optdepends=('python: for python bindings')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz" "return-self-from-iter.patch")
sha256sums=('84d7555ed795458f3799d57cceccb096634c38461c3db7cb1739d42c038b2785'
            '191b72df5dae46f63e97a1d05f30e099059012d40351c837f2180f6990484042')

prepare() {
  cd "$pkgname-$pkgver"
  # https://github.com/rpm-software-management/libcomps/commit/c37f570dc25704525f0ca85e9b7edd681464888a
  patch -Np1 -i ${srcdir}/return-self-from-iter.patch
}

build() {
  cd "$pkgname-$pkgver"

  cmake -B build -S libcomps \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_C_FLAGS_RELEASE='-DNDEBUG' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -Wno-dev

  make -C build

  make -C build docs
  make -C build pydocs
}

check() {
  cd "$pkgname-$pkgver"

  make -C build test
  make -C build pytest
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir/usr/share/doc/$pkgname"/{doxygen,sphinx}
  cp -Rp build/docs/libcomps-doc/html/ "$pkgdir/usr/share/doc/$pkgname/doxygen/"
  rm -r build/src/python/docs/html/.doctrees
  cp -Rp build/src/python/docs/html/   "$pkgdir/usr/share/doc/$pkgname/sphinx/"

  install -Dp -m644 README.md  "$pkgdir/usr/share/doc/$pkgname/README.md"
}
