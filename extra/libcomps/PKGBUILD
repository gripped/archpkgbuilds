# Contributor: larchunix
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgbase=libcomps
pkgname=(
  libcomps
  libcomps-docs
)
pkgver=0.1.21
pkgrel=5
pkgdesc="Comps XML file manipulation library"
arch=('x86_64')
url="https://github.com/rpm-software-management/$pkgbase"
license=('GPL-2.0-or-later')
depends=('expat' 'glibc' 'libxml2' 'zlib')
makedepends=('cmake' 'python' 'python-setuptools' 'doxygen' 'graphviz' 'python-sphinx')
checkdepends=('check')
source=(
  "$url/archive/$pkgver/$pkgbase-$pkgver.tar.gz"
  '0001-Return-self-from-iter-iterator.patch'
  '0002-Build-with-CMake-4.patch'
)
sha256sums=('84d7555ed795458f3799d57cceccb096634c38461c3db7cb1739d42c038b2785'
            '88e414b33c6ae0af3e8cfc4f11ed20a38e70a31f29347f826723cfd4da2b033a'
            'b730d8476c38490f595a1e1c068d051f2493fe496db12897f5a4583f56b62871')

prepare() {
  cd "$pkgbase-$pkgver"

  # https://github.com/rpm-software-management/libcomps/pull/116
  patch -Np1 -i ../0001-Return-self-from-iter-iterator.patch

  # Build with CMake 4
  patch -Np1 -i ../0002-Build-with-CMake-4.patch
}

build() {
  cd "$pkgbase-$pkgver"

  cmake -B build -S libcomps \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_C_FLAGS_RELEASE='-DNDEBUG' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -Wno-dev

  make -C build

  make -C build docs
  make -C build pydocs
}

check() {
  cd "$pkgbase-$pkgver"

  make -C build test
  make -C build pytest
}

package_libcomps() {
  optdepends=('python: for python bindings')

  cd "$pkgbase-$pkgver"

  make -C build DESTDIR="$pkgdir/" install

  install -Dp -m644 README.md  "$pkgdir/usr/share/doc/$pkgbase/README.md"
}

package_libcomps-docs() {
  pkgdesc+=" - documentation"
  depends=()

  cd "$pkgbase-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/$pkgbase"/{doxygen,sphinx}
  cp -Rp build/docs/libcomps-doc/html/ "$pkgdir/usr/share/doc/$pkgbase/doxygen/"
  rm -r build/src/python/docs/html/.doctrees
  cp -Rp build/src/python/docs/html/   "$pkgdir/usr/share/doc/$pkgbase/sphinx/"
}
