# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Johan Rehnberg <cleanrock@gmail.com>

pkgname=naev
pkgver=0.12.3
pkgrel=2
pkgdesc='2D action/rpg space game'
arch=('x86_64')
url="https://github.com/naev/naev"
license=('GPL3')
depends=('glu' 'openal' 'libvorbis' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'libgl' 'libxml2' 'freetype2'
         'libpng' 'libzip' 'luajit' 'glpk' 'physfs' 'libunibreak' 'suitesparse' 'cblas'
         'cmark' 'enet')
makedepends=('freeglut' 'zip' 'meson' 'git' 'python-yaml')
source=("https://github.com/naev/naev/releases/download/v$pkgver/naev-$pkgver-source.tar.xz"
        https://github.com/naev/naev/commit/eb11709e3a38da52f93e5415cc7fc9152a081fef.patch
        https://github.com/naev/naev/commit/8cd181e8666e1250f39766dda742f00206f1edbf.patch)
sha512sums=('6ae2701d66a198f128e8f2d87a5f6177ef8dcba64ef5ac1110e0af94bd74a46b3183f1a6771d632e9da79e29fc8ee502cfaa36ab7a8e259e681efe9946a9d239'
            '364d9831563bfa5c4fb8ad103066909a8d0eab294f2f9513443ae7a72026b385104195ebaa439386f8e2a9ac94b6d1893f1c6b3568e0430e8d5394eb348a1fac'
            '8adc86c0b7c1a2d13752e3060854c52262fcee88f81b8cf6dcd9d58d5364ab7f01a0f671149453f9305fa78ae4aa45e69affe3b02d39bb9994331f59f88c6e84')

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 -i ../eb11709e3a38da52f93e5415cc7fc9152a081fef.patch
  patch -Np1 -i ../8cd181e8666e1250f39766dda742f00206f1edbf.patch
}

build() {
  cd "$pkgname-$pkgver"

  meson setup -Dprefix=/usr -Dndata_path=/usr/share/naev/ndata -Dblas=cblas build .
  cd build
  meson compile
}

package() {
  cd "$pkgname-$pkgver/build"

  DESTDIR="$pkgdir" meson install
  rm -r "$pkgdir"/usr/{include,lib}
}
