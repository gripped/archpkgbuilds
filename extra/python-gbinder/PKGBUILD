# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Danct12 <danct12@disroot.org>

pkgname=python-gbinder
_srcname=gbinder-python
pkgver=1.3.1
pkgrel=1
pkgdesc="Python bindings for libgbinder"
arch=('x86_64')
url="https://github.com/waydroid/${_srcname}"
license=('GPL-3.0-only')
depends=('libgbinder>=1.1.40')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'cython')
provides=("${_srcname}")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('85c78ad76cc9403b09be1fb96424f529aa0cee7a570bc5cb7a4515a4746b006998c6b32a6d59c9931846d825c99ae59e71e2e907940adc3ae13415d6655d5f00')

build() {
	cd "${_srcname}-${pkgver}/"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_srcname}-${pkgver}/"
	python -m installer --destdir="${pkgdir}" dist/*.whl
}
