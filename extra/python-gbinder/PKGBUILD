# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Danct12 <danct12@disroot.org>

pkgname=python-gbinder
_srcname=gbinder-python
pkgver=1.3.0
pkgrel=2
pkgdesc="Python bindings for libgbinder"
arch=('x86_64')
url="https://github.com/waydroid/${_srcname}"
license=('GPL-3.0-only')
depends=('libgbinder')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'cython')
provides=("${_srcname}")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('fda25822cc6021f11204029322458abf923ac326cc4aa5b9f36920022662460b02e18e45c4765d095bd074cdbfe43023237bc09dcab21cd9524146a62e302d5c')

build() {
	cd "${_srcname}-${pkgver}/"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_srcname}-${pkgver}/"
	python -m installer --destdir="${pkgdir}" dist/*.whl
}
