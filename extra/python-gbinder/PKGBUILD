# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Danct12 <danct12@disroot.org>

pkgname=python-gbinder
_srcname=gbinder-python
pkgver=1.1.2
pkgrel=6
pkgdesc="Python bindings for libgbinder"
arch=('x86_64')
url="https://github.com/erfanoabdi/${_srcname}"
license=('GPL-3.0-only')
depends=('libgbinder')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'cython')
provides=("${_srcname}")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('6f076a5cb265eed193d2ac2623921e76ced923230ca2131460efea941182f26b770d657f5155f6a29fa54a314f6dc62a8d5b96d14ee90a359389e60318a38b71')

prepare() {
	sed -i 's/USE_CYTHON = False/USE_CYTHON = True/' "${_srcname}-${pkgver}/setup.py"
}

build() {
	cd "${_srcname}-${pkgver}/"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_srcname}-${pkgver}/"
	python -m installer --destdir="${pkgdir}" dist/*.whl
}
