# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mbuffer
pkgver=20251025
pkgrel=1
pkgdesc="A tool for buffering data streams"
arch=(x86_64)
url="https://www.maier-komor.de/mbuffer.html"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(openssl)
source=(
  https://www.maier-komor.de/software/$pkgname/$pkgname-$pkgver.tgz
)
sha512sums=('d1b4d49ed1d6a685a2db5a81cb2203ef92b5f8867bc534b7de6435c80a8e408e245052abc2ff5f79934a0c4079b33d496ba60fae005a5bd3f58a76443b1d31ae')
b2sums=('9093d5c9e0a3554bb54af5781b32e9dc614d13771e23a660f0958ea99add18fcd8a5e1a491cc9c0f98267f70196b702528ad7e53f489c3bf25e9541ecdb3a66a')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    openssl libcrypto.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
