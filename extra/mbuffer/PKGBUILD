# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mbuffer
pkgver=20241007
pkgrel=1
pkgdesc="A tool for buffering data streams"
arch=(x86_64)
url="https://www.maier-komor.de/mbuffer.html"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(openssl)
source=(
  https://www.maier-komor.de/software/$pkgname/$pkgname-$pkgver.tgz
)
sha512sums=('cb8f9e962510030f8e1500dcd21a556b73144c445bc12ece3915df201fa5e8849c183546c7ce700e06d1c734c0fdf36e31046ac984e232b64f9b019b54e1ddd2')
b2sums=('d26c0d87abacc8826bf2e6f414fe9264da7b3196c62a9853a8ffb6b118df353977c38ea5b1bf79e71fadcae6585b90b1f21152f742c7fafee08ae4294acceb60')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    openssl libcrypto.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
