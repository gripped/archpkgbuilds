# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mbuffer
pkgver=20250429
pkgrel=1
pkgdesc="A tool for buffering data streams"
arch=(x86_64)
url="https://www.maier-komor.de/mbuffer.html"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(openssl)
source=(
  https://www.maier-komor.de/software/$pkgname/$pkgname-$pkgver.tgz
)
sha512sums=('9cbf233335b8f7fe4d607f93afdb1797293377f580def420a3b2285b1e777ef33fff7fd7ebbe67fd35dca48127e887de9f833c2d019075f51fcef2e479a8ff00')
b2sums=('702f028ff310370ceeb6894e0456e68452afec9e353b83bfa875d2f8171af8d008736384382e42a447c9572e7c55d924761d5043be84010589a597870238220b')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    openssl libcrypto.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
