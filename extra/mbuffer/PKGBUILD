# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mbuffer
pkgver=20250809
pkgrel=1
pkgdesc="A tool for buffering data streams"
arch=(x86_64)
url="https://www.maier-komor.de/mbuffer.html"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(openssl)
source=(
  https://www.maier-komor.de/software/$pkgname/$pkgname-$pkgver.tgz
)
sha512sums=('52ea00517182938726b00c51cdd83b9c7c7f3af78ce02632584822774776e2822f0f5568a84fb9673784fbcbf0a2fb16200c92fc80073858fa78583c764fb030')
b2sums=('def30d865fcd0a6c040b8c70b3bcaddc13eecb74bb792678dd82d3cd9927973ca76ff9e33e8a31a883a81025b217cb047ff2ad8caa86cffc7f1842c9098d4fea')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    openssl libcrypto.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
