# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Thomas Kiss <thokis@gmail.com>

pkgname=sane-airscan
pkgver=0.99.31
pkgrel=1
pkgdesc="SANE - SANE backend for AirScan (eSCL) and WSD document scanners"
url="https://github.com/alexpevzner/sane-airscan"
arch=('x86_64')
license=('GPL-2.0-or-later WITH SANE-exception AND MIT')
depends=('sane' 'avahi' 'gnutls' 'libxml2' 'libjpeg-turbo' 
        'libpng' 'libtiff' 'glibc')
#makedepends=('meson')
optdepends=('ipp-usb: allows to send HTTP requests via a USB connection on devices without Ethernet or WiFi connections')
backup=(etc/sane.d/airscan.conf)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/alexpevzner/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('67782be6a4fd36e753fc4766b8989d75f806bc6d1d2e92f617ea686be2924c14')

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
    # add license + exception
    install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" {COPYING,LICENSE}
    # add missing .so symlink
    cd "${pkgdir}"/usr/lib/sane
    ln -vsf libsane-airscan.so.1 libsane-airscan.so
}
