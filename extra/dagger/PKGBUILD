# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=dagger
pkgver=0.12.0
pkgrel=1
pkgdesc='A portable devkit for CI/CD pipelines'
arch=('x86_64')
url='https://dagger.io'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/dagger/dagger.git#tag=v$pkgver")
sha512sums=('a03fbc3c006634c8ac0267cee3b333afa04d81af70201f3f4e9258a246c2c1da031b2cdb59d2c4363faec8c5ab587068aa859a026a1d25b7d9d167d3264e2953')
b2sums=('22f548c9bdcac8aad85f3b9104c1fa3b29ee98b6ff6aef10b38980db4446ea00d157007947c005ecc9f79fbc75827da482acab55b5504c5a94e2773770b8aae5')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/dagger/dagger/engine.Version=$pkgver" \
    -o build \
    ./cmd/...
}

# TODO tests now require docker *kicks docker*
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" build/dagger
}
