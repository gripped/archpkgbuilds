# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=dagger
pkgver=0.19.7
pkgrel=1
pkgdesc='A portable devkit for CI/CD pipelines'
arch=(x86_64)
url='https://dagger.io'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+https://github.com/dagger/dagger.git#tag=v$pkgver")
sha512sums=('64cdea8bc35a2c7cf637893aadc6e30239ed07859db4a54ebd5bb4ed496d077d0302be75b7f803eab008f123d26ad2b70dab657ca744ccd9bdc7036302e3abfb')
b2sums=('cf02ce3c8f15531a5c740cc49ba405f8af6bf8faffb970b389ff305b3352ffab18cbe4953079b924df3c8c9e181d004c2c2878c1558166d4c44fc2c6f3c65afc')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/dagger/dagger/engine.Version=v$pkgver \
    -X github.com/dagger/dagger/engine.Tag=v$pkgver" \
    -o build \
    ./cmd/...
}

# TODO tests now require docker *kicks docker*
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" build/dagger
}
