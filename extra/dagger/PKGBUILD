# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=dagger
pkgver=0.20.0
pkgrel=1
pkgdesc='A portable devkit for CI/CD pipelines'
arch=(x86_64)
url='https://dagger.io'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
options=(!lto)
source=("$pkgname::git+https://github.com/dagger/dagger.git#tag=v$pkgver")
sha512sums=('ec5bddb8916f012322fb39d82248ad66a8d1dbdffb4c5f8f5c91dce6b30e9b96408370b45faedbc972d25c996c20b23c73e8ea0e625ce8473a5e9011946c8c64')
b2sums=('e9c849e8933451dbe45616153f0e9740fb32a280c776e6656b277a740ee9aaa6ea72be2f76104a9f1c0662e58d86e524fd07f01e89ede6f5365a345fe7e47a40')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/dagger/dagger/engine.Version=v$pkgver \
    -X github.com/dagger/dagger/engine.Tag=v$pkgver" \
    -o build \
    ./cmd/...
}

# TODO tests now require docker *kicks docker*
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" build/dagger
}
