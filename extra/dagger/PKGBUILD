# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=dagger
pkgver=0.19.11
pkgrel=1
pkgdesc='A portable devkit for CI/CD pipelines'
arch=(x86_64)
url='https://dagger.io'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
options=(!lto)
source=("$pkgname::git+https://github.com/dagger/dagger.git#tag=v$pkgver")
sha512sums=('593f962266919cf588f1256ab5477df1dff90c966307e4d54406c1bc0c8c0b0416abf8436de27502ce963ddc54be5cd62a3790115f0f5dcca1a515ba5114c20a')
b2sums=('484fc38c691a034d1192dd304857e7693a4867fa8dc3d7b68940102acfc0ff34d1983dd96501cf91d6024de0710c519c4d876810adce89699f13ced74fc74780')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/dagger/dagger/engine.Version=v$pkgver \
    -X github.com/dagger/dagger/engine.Tag=v$pkgver" \
    -o build \
    ./cmd/...
}

# TODO tests now require docker *kicks docker*
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" build/dagger
}
