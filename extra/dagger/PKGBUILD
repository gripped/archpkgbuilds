# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=dagger
pkgver=0.19.10
pkgrel=1
pkgdesc='A portable devkit for CI/CD pipelines'
arch=(x86_64)
url='https://dagger.io'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+https://github.com/dagger/dagger.git#tag=v$pkgver")
sha512sums=('e92af180efd0b9272fdbba1be87afb0d1fb4c753074d68c016b458c6e6741ff6d2331ac5dabcbb6cc8f3217d41e0d3fbdd4187f8bc2f90e5d4f8ce5981595075')
b2sums=('6ec133426346b177ed609bee9448ffb39724d23529335755f01d5a4f0add9bb23a02bcaa82e42c0245bcdc4492741071f6da36d82215f0ccde9d8cb35f49f313')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/dagger/dagger/engine.Version=v$pkgver \
    -X github.com/dagger/dagger/engine.Tag=v$pkgver" \
    -o build \
    ./cmd/...
}

# TODO tests now require docker *kicks docker*
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" build/dagger
}
