# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=dagger
pkgver=0.19.6
pkgrel=1
pkgdesc='A portable devkit for CI/CD pipelines'
arch=(x86_64)
url='https://dagger.io'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+https://github.com/dagger/dagger.git#tag=v$pkgver")
sha512sums=('7331f9f783b2845e829c4b706810264a26fec7fd1f398e34be8e908dfd4520a95b74bc817bc5c85b5911f555775e898549fc3dc0757e121b5b064b7a6b820e5c')
b2sums=('d921c5d875990620ae333ef76da75ba006808d6e81de10e8da0c24369f6f600751316f53fe7ee104be7db8d784f03ee6c940199912d85467fdba530e2141e23c')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/dagger/dagger/engine.Version=v$pkgver \
    -X github.com/dagger/dagger/engine.Tag=v$pkgver" \
    -o build \
    ./cmd/...
}

# TODO tests now require docker *kicks docker*
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" build/dagger
}
