# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=dagger
pkgver=0.19.0
pkgrel=1
pkgdesc='A portable devkit for CI/CD pipelines'
arch=('x86_64')
url='https://dagger.io'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/dagger/dagger.git#tag=v$pkgver")
sha512sums=('f141cda33902a0d915c88b1efda3836501590e6b5114c78c905490693568d4b2b1a9ab0c579f0fd7a531895f2bdeaf5b75b8f23b2c947eb90227e94a9d5e328d')
b2sums=('a4711eee6eb7467bbf968b91766eb6b7154315bb62a3ecbf6795e5eb975c1a0500eeb84d57492ebf5c7409f6b6d5003f52394d955aaf46e1863a6d127b2fdfac')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/dagger/dagger/engine.Version=v$pkgver \
    -X github.com/dagger/dagger/engine.Tag=v$pkgver" \
    -o build \
    ./cmd/...
}

# TODO tests now require docker *kicks docker*
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  install -vDm755 -t "$pkgdir/usr/bin" build/dagger
}
