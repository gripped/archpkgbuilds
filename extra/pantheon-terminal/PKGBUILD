# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=7.1.0
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=(x86_64)
url=https://github.com/elementary/terminal
license=(GPL3)
depends=(
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  pango
  vte3
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
  xorg-server-xvfb
)
groups=(pantheon)
_tag=0d6776d97b02017edb4798c77eb52d69e869acc3
source=(pantheon-terminal::git+https://github.com/elementary/terminal.git#tag=${_tag})
b2sums=('9bc9ca22aaff371a7a3a470308a67227588e1358fc2287e94e7e5b78c8dcdb4409e5b71200fb4ee87cb1203f6dbe1f4aa7212a4be40073b786736c129d895f68')

pkgver() {
  cd pantheon-terminal
  git describe --tags
}

build() {
  arch-meson pantheon-terminal build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
