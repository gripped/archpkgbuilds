# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=6.3.0
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=(x86_64)
url=https://github.com/elementary/terminal
license=(GPL3)
depends=(
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  pango
  vte3
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
  xorg-server-xvfb
)
groups=(pantheon)
_tag=b6ef92a8dc17b6d225bb06ff1dd29a7cd50f5f1f
source=(pantheon-terminal::git+https://github.com/elementary/terminal.git#tag=${_tag})
b2sums=('6a58d9d31aa13607e0f61b8da472d47cc5aedd116d9f76fb633a829caa7562ffa5197c9c6838e6576bf5d735f6b260a170d4dac8db139021f3f3ba68999b8f21')

pkgver() {
  cd pantheon-terminal
  git describe --tags
}

build() {
  arch-meson pantheon-terminal build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
