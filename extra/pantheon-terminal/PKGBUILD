# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=7.0.0
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=(x86_64)
url=https://github.com/elementary/terminal
license=(GPL3)
depends=(
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  pango
  vte3
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
  xorg-server-xvfb
)
groups=(pantheon)
_tag=d914b4ca4ea99012b30a236f3616aa8b45f3ab37
source=(pantheon-terminal::git+https://github.com/elementary/terminal.git#tag=${_tag})
b2sums=('d83929557049828b355e1d79774944280c981d01870ecac2cd48a9304e3244a13aba8a15a2ea76e5d0b3b1dc8c2a61e508226eecf6e0281cb1d679f8059f50c0')

pkgver() {
  cd pantheon-terminal
  git describe --tags
}

build() {
  arch-meson pantheon-terminal build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
