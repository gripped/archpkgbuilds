# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgbase=rebuilderd
pkgname=(rebuilderd rebuilderd-tools)
pkgver=0.24.0
pkgrel=2
pkgdesc='Independent verification system of binary packages'
url='https://github.com/kpcyrd/rebuilderd'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'libbz2.so'
  'liblzma.so'
  'libsqlite3.so'
  'libssl.so'
  'libzstd.so'
)
makedepends=(
  'bzip2'
  'cargo'
  'scdoc'
  'shared-mime-info'
  'sqlite'
  'zstd'
)
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kpcyrd/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('b5c59dea921d576f04df3ba77a820016ea1d2188601ff83abaa2a920d3e653d51e11f29f29ed15298af725a471c254548b52a3b5ab1c843a9eb693217692cf0f'
            'SKIP')
b2sums=('77c11dd702f5e7a17e55a0e234cc1976ba56ad49f46b374712950775f3fbf347fd2acffbdc8d49df7e3b29286321123bbf99dc90ff3ef2b622151e4b229f7677'
        'SKIP')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")

prepare() {
  cd ${pkgbase}-${pkgver}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgbase}-${pkgver}
  cargo build --frozen --release --workspace
  make docs
}

check() {
  cd ${pkgbase}-${pkgver}
  cargo test --frozen --workspace
}

package_rebuilderd() {
  pkgdesc='Independent verification system of binary packages (server package)'
  depends=(
    'archlinux-repro'
    'gcc-libs'
    'glibc'
    'libsqlite3.so'
    'libzstd.so'
    'rebuilderd-tools'
  )
  backup=('etc/rebuilderd.conf'
          'etc/rebuilderd-sync.conf'
          'etc/rebuilderd-worker.conf')

  cd ${pkgbase}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/rebuilderd \
    target/release/rebuilderd-worker

  # install rebuilder scripts
  install -Dm 755 -t "${pkgdir}/usr/libexec/rebuilderd" \
    worker/rebuilder-*.sh

  # install config files
  install -Dm 644 -t "${pkgdir}/etc" \
    contrib/confs/rebuilderd-sync.conf
  install -Dm 640 -g 212 -t "${pkgdir}/etc" \
    contrib/confs/rebuilderd-worker.conf \
    contrib/confs/rebuilderd.conf

  # install systemd configs
  install -Dm 644 -t "${pkgdir}/usr/lib/systemd/system" \
    contrib/systemd/rebuilderd-sync@.service \
    contrib/systemd/rebuilderd-sync@.timer \
    contrib/systemd/rebuilderd-worker@.service \
    contrib/systemd/rebuilderd.service

  install -Dm 644 contrib/systemd/rebuilderd.sysusers "${pkgdir}/usr/lib/sysusers.d/rebuilderd.conf"
  install -Dm 644 contrib/systemd/rebuilderd.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/rebuilderd.conf"

  # install docs
  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    contrib/docs/rebuilderd.1 \
    contrib/docs/rebuilderd-worker.1
  install -Dm 644 -t "${pkgdir}/usr/share/man/man5" \
    contrib/docs/rebuilderd.conf.5 \
    contrib/docs/rebuilderd-sync.conf.5 \
    contrib/docs/rebuilderd-worker.conf.5
}

package_rebuilderd-tools() {
  pkgdesc='Independent verification system of binary packages (tools package)'
  depends=(
    'gcc-libs'
    'glibc'
    'libbz2.so'
    'liblzma.so'
    'libzstd.so'
    'shared-mime-info'
  )

  cd ${pkgbase}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/rebuildctl

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/rebuildctl" completions bash > "${pkgdir}/usr/share/bash-completion/completions/rebuildctl"
  "${pkgdir}/usr/bin/rebuildctl" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_rebuildctl"
  "${pkgdir}/usr/bin/rebuildctl" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/rebuildctl.fish"

  # install docs
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgbase}"
  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    contrib/docs/rebuildctl.1
}

# vim: ts=2 sw=2 et:
