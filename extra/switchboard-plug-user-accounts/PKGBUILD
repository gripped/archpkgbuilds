# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-user-accounts
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard User Accounts Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-useraccounts
license=(LGPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libpwquality
  libswitchboard-3.so
  polkit
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_tag=799f12c0d4cd5c86c0650f83a2fa29fb573bbda8
source=(switchboard-plug-user-accounts::git+https://github.com/elementary/switchboard-plug-useraccounts.git#tag=${_tag})
b2sums=('7201fa01a0eca2f6f150c7044bf7e910ef62a8e084a6bb041f528a2dc14b4c5b7c888fcd19979e60149b080b6bb45792492c3ac1200e3a33f0891317133baf66')

pkgver() {
  cd switchboard-plug-user-accounts
  git describe --tags
}

build() {
  arch-meson switchboard-plug-user-accounts build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
