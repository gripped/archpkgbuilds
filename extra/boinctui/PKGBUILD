# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=boinctui
pkgver=2.7.1
pkgrel=1
pkgdesc="Curses based fullscreen BOINC manager"
arch=('x86_64')
url="https://sourceforge.net/projects/boinctui/"
license=('GPL')
depends=('expat' 'gcc-libs' 'ncurses' 'openssl')
makedepends=("cmake")
source=("https://sourceforge.net/projects/boinctui/files/${pkgname}_${pkgver}.tar.gz"{,.asc})
sha512sums=('c338109e66aa0ad3f53c5eac40d8acbed4543f58704cf8487b36d3665c1829f21c1dd92fea03d352efc89bce3f0adc19caf69ac74d1c454ac5eb952f2694347c'
            'SKIP')
validpgpkeys=('4D2DBBB4F06EC2C977B26BFD80878E59832BEC15')  # Sergey Suslov

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --without-gnutls --mandir=/usr/share/man
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
