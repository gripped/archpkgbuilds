# Maintainer: Peter Jung <ptr1337@archlinux.org>

pkgname=mdserve
pkgver=0.5.0
pkgrel=1
pkgdesc="Fast markdown preview server with live reload and theme support"
arch=('x86_64')
url="https://github.com/jfernandez/mdserve"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jfernandez/mdserve/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('0a548dbed151400ce6b6aea409d7e02bedfc96548c68239c635b4932a9475d0931e5a4d2d2ad7ec81e05cd61250ba11ff64d736443a35ef2ce460c91cc8389f5')


prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 "$srcdir/$pkgname-$pkgver/target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
