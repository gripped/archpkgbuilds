# Maintainer: Peter Jung <ptr1337@archlinux.org>

pkgname=mdserve
pkgver=1.0.0
pkgrel=1
pkgdesc="Fast markdown preview server with live reload and theme support"
arch=('x86_64')
url="https://github.com/jfernandez/mdserve"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jfernandez/mdserve/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('22186798664cad0639c7b0e19b99621869ab36102a7af2dccb901bbec3a2f3f92650d49745f8c64310cac4cd201355763177f0c539cbc00b8d5fd760645e0c96')


prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 "$srcdir/$pkgname-$pkgver/target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
