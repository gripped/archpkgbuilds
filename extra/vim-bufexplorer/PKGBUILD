# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=vim-bufexplorer
pkgver=7.4.27
pkgrel=1
pkgdesc='Simple buffer list/switcher for vim'
url='https://github.com/jlanzarotta/bufexplorer'
arch=(any)
license=(BSD-3-Clause)
depends=(
  vim-plugin-runtime
)
groups=(vim-plugins)
source=(https://github.com/jlanzarotta/bufexplorer/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('91d9ab6e6e988239d9f88221df35f11da9a464507ed0023c8f0399cc0874f3960cf43061242419aa076ef74b30dc0bc800b90e716cda79a940c57b2304753107')
b2sums=('015bbee4655397e695b87f87899ca76cc49f6681c88c96494e6a468a7236fdaa5de223dbdb458822818175f990fe63f8efb8e3d59dbaa1ba4ba95a0f7f950b3c')

package() {
  cd bufexplorer-${pkgver}
  installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm 644 doc/* -t "${installpath}/doc"
  install -Dm 644 plugin/* -t "${installpath}/plugin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
