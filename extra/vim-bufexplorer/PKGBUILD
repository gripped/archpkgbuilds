# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=vim-bufexplorer
pkgver=7.13.0
pkgrel=1
pkgdesc='Simple buffer list/switcher for vim'
url='https://github.com/jlanzarotta/bufexplorer'
arch=(any)
license=(BSD-3-Clause)
depends=(
  vim-plugin-runtime
)
groups=(vim-plugins)
source=(https://github.com/jlanzarotta/bufexplorer/archive/refs/tags/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('3d7dff41dd26e06e6769c3c1ee088b79b60afd38fff1c23b76bd1ddde2460d6cdf4c92132b5f6cdd93002adc06109ba6706723dee6bef0ea781d2632e8440fdd')
b2sums=('0d762fbf98d68591dac1e7133adec0186929f102f8e28cb126979a494efd9778235823e55a2a4e79ff5a2515bfbbf30ce12a9555cf521c33cc891e42c33e5cda')

package() {
  cd bufexplorer-${pkgver}
  installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm 644 doc/* -t "${installpath}/doc"
  install -Dm 644 plugin/* -t "${installpath}/plugin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
