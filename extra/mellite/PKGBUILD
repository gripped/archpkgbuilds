# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Mellite
_app_name=de.sciss.mellite.Mellite
pkgname=mellite
pkgver=3.14.9
pkgrel=2
pkgdesc="An environment for creating experimental computer-based music and sound art"
arch=(any)
url="https://www.sciss.de/mellite/"
_url=https://codeberg.org/sciss/Mellite
license=(
  0BSD
  AGPL-3.0-or-later
)
groups=(pro-audio)
_java_version=17
depends=(
  hicolor-icon-theme
  java-runtime=$_java_version
  sh
  supercollider
)
makedepends=(
  gendesk
  java-environment=$_java_version
  sbt
)
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/v$pkgver.tar.gz
  $pkgname.sh
  LICENSE
)
sha512sums=('bc135026c6ed9f8c3c4666c4412dda3b3bef0d2340abe4727066c253c5f5349e6384ba9e327e151c7b7b7707ed2ccc804706578f6b95db3aff81682ff7a1c286'
            'a4d046463774393fdbe2ff10d545d0a46ff62761effdca9e1beb7e7f8d43aebe7ee4dc48602c951b59dfa0c4add517c1c8b58284f5c10c751969826ff512529a'
            'a33658d9271e5c537ccd41bf540b463ad2a5eca4a060c80486ff42a736f0aa042d10436e7177c34d792177cb11285243dee1f31c4df54fb0bfaabbc306406930')
b2sums=('e960ab99283c4b14b1feb36dc6fa74f239bc53f91d73f173946d682acdae03dfd226717ab083271dfeaec9448422cde677d0695b65c0046072c12a5255012db2'
        '6ca9fa81baf35d0c93ac4d192c591320858e70bc7f981f5fed0a100bcc99d3595c7fbfba177747f7dd2e93e9e399ecb26041937eabacab2654b247df6f4827b9'
        'a29664104e1ee73ca0aee1d633e9095d92a57c92787f8d8740bdb7211ba3205782ed8677f539bdb8cae3dd75a3694be3132e185fa3fc4b3f401e1f88eb776101')

prepare() {
  sed "s/JAVA_VERSION/$_java_version/" $pkgname.sh > $pkgname/$pkgname

  gendesk -n \
          --pkgname "$_app_name" \
          --name "Mellite" \
          --pkgdesc "${meter_types[$type]}" \
          --exec "$pkgname" \
          --icon "$_app_name" \
          --genericname "Audio Programming" \
          --categories "AudioVideo;Audio"
}

build() {
  cd $pkgname
  sbt +mellite-app/test +mellite-app/assembly
}

package() {
  install -vDm 644 $pkgname/app/$_name.jar -t "$pkgdir/usr/share/scala/$pkgname"
  install -vDm 644 $pkgname/icons/application.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/de.sciss.mellite.Mellite.png"
  install -vDm 644 $pkgname/icons/application.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/de.sciss.mellite.Mellite.svg"

  install -vDm 755 $pkgname/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 ./*.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/0BSD.txt"
}
