# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Mellite
_app_name=de.sciss.mellite.Mellite
pkgname=mellite
pkgver=3.13.7
pkgrel=2
pkgdesc="An environment for creating experimental computer-based music and sound art"
arch=(any)
url="https://www.sciss.de/mellite/"
_url=https://codeberg.org/sciss/Mellite
license=(AGPL-3.0-or-later)
groups=(pro-audio)
_java_version=17
depends=(
  hicolor-icon-theme
  java-environment=$_java_version
  sh
  supercollider
)
makedepends=(
  gendesk
  sbt
)
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('e4dfa24cb2c84d418e143e6f52f6c0833e80a42dc2863ad826ef066b1043c40903a558112b0e2f52644a47aed72ed5853891052e8cf7021fcc4cef2187eaab22'
            'a4d046463774393fdbe2ff10d545d0a46ff62761effdca9e1beb7e7f8d43aebe7ee4dc48602c951b59dfa0c4add517c1c8b58284f5c10c751969826ff512529a')
b2sums=('4c5e32bc1ed2a6f745bf7223e33b93944268a2f817d2f97e9cb55eefd7c3289940aa3c52ae09341fe805910abe5714b499b699daa121f336444bb61b972a4df2'
        '6ca9fa81baf35d0c93ac4d192c591320858e70bc7f981f5fed0a100bcc99d3595c7fbfba177747f7dd2e93e9e399ecb26041937eabacab2654b247df6f4827b9')

prepare() {
  sed "s/JAVA_VERSION/$_java_version/" -i $pkgname.sh > $pkgname/$pkgname

  gendesk -n \
          --pkgname "$_app_name" \
          --name "Mellite" \
          --pkgdesc "${meter_types[$type]}" \
          --exec "$pkgname" \
          --icon "$_app_name" \
          --genericname "Audio Programming" \
          --categories "AudioVideo;Audio"
}

build() {
  cd $pkgname
  sbt +mellite-app/test +mellite-app/assembly
}

package() {
  install -vDm 644 $pkgname/app/$_name.jar -t "$pkgdir/usr/share/scala/$pkgname"
  install -vDm 644 $pkgname/icons/application.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/de.sciss.mellite.Mellite.png"
  install -vDm 644 $pkgname/icons/application.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/de.sciss.mellite.Mellite.svg"

  install -vDm 755 $pkgname/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 ./*.desktop -t "$pkgdir/usr/share/applications/"
}
