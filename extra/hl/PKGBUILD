# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hl
pkgver=0.36.0
pkgrel=1
pkgdesc="A fast and powerful log viewer and processor"
arch=('x86_64')
url="https://github.com/pamburus/hl"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f0de89479e0831467aa527a079ef5b7e78ba4d5be63b6d132da0849ebdf8e0129911f3bcef113c74911721881199d2265ec19e065baa07f31bb564f52ddd587c')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
