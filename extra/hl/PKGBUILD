# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hl
pkgver=0.33.1
pkgrel=1
pkgdesc="A fast and powerful log viewer and processor"
arch=('x86_64')
url="https://github.com/pamburus/hl"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('6572a4c62afc6c4df0479612a5aea75a99caeda49d65fe1b523e6a3308d137061742220aca2a84c7455067702221dd3bc15274235c63c5af94b4b503510acb44')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
