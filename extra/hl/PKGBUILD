# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hl
pkgver=0.31.0
pkgrel=1
pkgdesc="A fast and powerful log viewer and processor"
arch=('x86_64')
url="https://github.com/pamburus/hl"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f02efd97d810f396b9f0237099b0cdce1184c84d4cae0294efb3140117d1c919076ea2a016a430c48da41c3758dfe2e6cafe851a294840a68cb03f921d211b4e')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
