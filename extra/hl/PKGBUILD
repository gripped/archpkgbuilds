# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hl
pkgver=0.31.1
pkgrel=1
pkgdesc="A fast and powerful log viewer and processor"
arch=('x86_64')
url="https://github.com/pamburus/hl"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e711df30b102faa1a18df18b73d153628c65401dae83c78745ade1fe3beb60c0c474c39f796f78b59770b9e55f67b638f989d650b0ccc9ccd1707da87f238d75')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
