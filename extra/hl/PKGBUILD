# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=hl
pkgver=0.32.3
pkgrel=1
pkgdesc="A fast and powerful log viewer and processor"
arch=('x86_64')
url="https://github.com/pamburus/hl"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('74ba522d9599a87794375b4bee78774b57f877cf83424a06940f969f3bd54925a500d7ef75a1d7b03c4a752a41172b887e463b162e24c186d5a7846cd7b707ca')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
