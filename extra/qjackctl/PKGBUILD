# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=1.0.3
pkgrel=1
pkgdesc="A Qt front-end for the JACK low-latency audio server"
arch=(x86_64)
url="https://qjackctl.sourceforge.io"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  jack2
  jack2-dbus
  qt6-tools
  portaudio
)
optdepends=(
  'jack2-dbus: for JACK dbus support'
  'qt6-wayland: for native wayland support'
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('32ea26307efc22c0e4a6db91675c65237f654fd3f58150545170e70cb13d25a609a09c053ffec382e2f706059832ff765f9f62ab8eae5e704101a41cb2745ee5')
b2sums=('78d0f35184dfa5cbd137340615c21c3dc576da8875aed6a7ab0104b8aab4cebe8c1e1a8551e529ddba51965b4d110ac42c5c6232ae0d4681c92a34a137792e42')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
    jack libjack.so
    portaudio libportaudio.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README,TRANSLATORS} -t "$pkgdir/usr/share/doc/$pkgname"
}
