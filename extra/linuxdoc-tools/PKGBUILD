# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Ell <david at bytesizebits dot net>

pkgname=linuxdoc-tools
pkgver=0.9.85
pkgrel=2
pkgdesc="Convert LinuxDoc SGML source into other formats"
arch=('x86_64')
url="https://packages.debian.org/sid/linuxdoc-tools"
license=('GPL')
depends=('jade')
makedepends=('texlive-bin' 'texlive-latexrecommended' 'texlive-fontsrecommended')
optdepends=('groff' 'texlive-bin')
source=(https://mirrors.edge.kernel.org/debian/pool/main/l/linuxdoc-tools/linuxdoc-tools_${pkgver}.orig.tar.gz)
sha256sums=('ce294673c634faf38d8d83b18fa0003f73a0e4d90160b9faa474689543332d42')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '/extern int yyleng;/d' rtf-fix/rtf2rtf.l
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --docdir=/usr/share/doc/$pkgname \
    --with-texdir=/usr/share/texmf-dist/tex/linuxdoc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
