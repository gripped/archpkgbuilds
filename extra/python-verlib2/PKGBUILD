# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=verlib2
pkgname=python-$_pyname
pkgver=0.2.0
pkgrel=1
pkgdesc='a standalone variant of packaging.version, without anything else'
arch=(any)
url='https://github.com/pyveci/verlib2'
license=(BSD-2-Clause)
_pydeps=(versioningit)
depends=(python "${_pydeps[@]/#/python-}")
makedepends=(python-{build,installer,wheel}
             python-setuptools)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('0ab94087c094e27163948dfa817c98d62b6bdd01226cce5188f8cc3cd68345b9')

build() {
	cd "$_archive"
	python -m build -wn
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
