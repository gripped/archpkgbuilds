# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=verlib2
pkgname=python-$_pyname
pkgver=0.3.1
pkgrel=2
pkgdesc='a standalone variant of packaging.version, without anything else'
arch=(any)
url='https://github.com/pyveci/verlib2'
license=(BSD-2-Clause)
_pydeps=(versioningit)
depends=(python "${_pydeps[@]/#/python-}")
makedepends=(python-{build,installer,wheel}
             python-setuptools)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('2862f19528db400d130253a2b71c7c3616ee14e1d54bf6833bc0929d2cddd141')

build() {
	cd "$_archive"
	python -m build -wn
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
