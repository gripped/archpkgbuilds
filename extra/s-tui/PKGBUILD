# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Contributor: Robin Broda <coderobe @ archlinux.org>
# Contributor: Lars Hagström <lars@foldspace.nu>

pkgname=s-tui
pkgver=1.2.0
pkgrel=3
pkgdesc="Terminal UI stress test and monitoring tool "
arch=(any)
url="https://github.com/amanusk/s-tui"
license=(GPL-2.0-only)
depends=(
  python
  python-psutil
  python-urwid
  stress
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
        $pkgname-fix-urwid.patch::https://github.com/amanusk/s-tui/commit/11ad606e3e60c81801909b654f8adb1c5ee10c06.patch)
sha512sums=('c6024ceaaed4e27f6210c106a1550a88376995efc925f92d6ffc4309e8981a1b3d0e59c502fd16ad1786fb61fc28de2dadee668f838057cc8dd4e54908859bda'
            '2b2d3b9fd3293943828b05feba5d516c9bf5ddec531d52ca08dfe55311d31188ff006e2f7be6960c438f1bb053e45cc8850615b55e03cbc8a1f60121b14f1fad')

prepare() {
  cd $pkgname-$pkgver
  # Fix unsucceful import of curses_display https://github.com/amanusk/s-tui/pull/247
  patch -p1 < ../$pkgname-fix-urwid.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m unittest discover -v
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
