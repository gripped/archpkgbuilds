# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Contributor: Robin Broda <coderobe @ archlinux.org>
# Contributor: Lars Hagström <lars@foldspace.nu>

pkgname=s-tui
pkgver=1.3.0
pkgrel=1
pkgdesc="Terminal UI stress test and monitoring tool "
arch=(any)
url="https://github.com/amanusk/s-tui"
license=(GPL-2.0-only)
depends=(
  python
  python-psutil
  python-urwid
  stress
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('0ae12b93358e2f33aa7c4bcb697b1ff2a64915fdecd021b41416c59af83045c3cf6ca4e168f3b9da86047402f2ebadfc1a0c429873ad351410883ec641fcc514')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m unittest discover -v
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
