# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=nickel
pkgname=(
  nickel
  nickel-docs
  nickel-language-server
)
pkgver=1.15.1
pkgrel=2
pkgdesc='A generic configuration language'
arch=(x86_64)
url='https://nickel-lang.org'
license=(MIT)
depends=(
  glibc
  gcc-libs
  nix
)
makedepends=(
  git
  rust
  nlohmann-json
  boost
  python
)
checkdepends=(python)
options=(!lto)
source=("$pkgbase::git+https://github.com/tweag/nickel#tag=$pkgver")
sha512sums=('dcfc0f85ae6ca4fa0ccfada6aff210c329ec40aa23d0ed097bc94e34494436348f569bb0d8d29f53322f43817623e8f16fc35fe7f745e368e9ee9018c24d4013')
b2sums=('8e3707f0a22361f7e4930e2e66abaf8fdd3c3ea60276065c690a12a6b2dd97bdf71f2a9ea92a8e488280694e608825af24f237f73a8f27832e65ae6b083c8411')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgbase"

  # binaries
  PYO3_USE_ABI3_FORWARD_COMPATIBILITY=1 cargo build --frozen --release --all-features

  # generate shell completions
  pushd target/release
  for shell in bash fish zsh; do
    ./nickel gen-completions "$shell" > "$shell-completion"
  done
  popd
}

#check() {
#  cd "$pkgbase"
#
#  cargo test --frozen --all-features
#}

package_nickel() {
  optdepends=(
    'nickel-docs: documentation'
    'nickel-language-server: language server'
  )
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/nickel

  # shell completions
  pushd target/release
  install -vDm644 bash-completion "$pkgdir/usr/share/bash-completion/completions/nickel"
  install -vDm644 fish-completion "$pkgdir/usr/share/fish/vendor_completions.d/nickel.fish"
  install -vDm644 zsh-completion "$pkgdir/usr/share/zsh/site-functions/_nickel"
  popd

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

package_nickel-docs() {
  pkgdesc='Documentation & examples for the Nickel Programming Language'
  depends=()
  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr examples doc/manual "$pkgdir/usr/share/doc/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

package_nickel-language-server() {
  pkgdesc='A language server for the Nickel Programming Language'
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/nls

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
