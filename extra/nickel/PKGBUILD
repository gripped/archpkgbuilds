# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=nickel
pkgname=(
  nickel
  nickel-docs
  nickel-language-server
)
pkgver=1.16.0
pkgrel=1
pkgdesc='A generic configuration language'
arch=(x86_64)
url='https://nickel-lang.org'
license=(MIT)
depends=(
  glibc
  libgcc
  libstdc++
  gc
  nix
)
makedepends=(
  git
  rust
  nlohmann-json
  boost
  python
)
checkdepends=(python)
options=(!lto)
source=("$pkgbase::git+https://github.com/tweag/nickel#tag=$pkgver")
sha512sums=('60822d8c475f591b693bda23b761d1b756153e331b3603c097757999d07ddea16967c8127e3e6f3bbab10cfc1dbe9796cf4a61be8478a181a9c58866cf74b91f')
b2sums=('a1a9b0f78ce84f1fb14e8d9a308ada341d0f58cfc709f61b9b8d1cb7a4448066806b949273cf78e896aea1ad268fbb568104d41493264af37664875b61ad18f1')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgbase"

  # binaries
  PYO3_USE_ABI3_FORWARD_COMPATIBILITY=1 cargo build --frozen --release --all-features

  # generate shell completions
  pushd target/release
  for shell in bash fish zsh; do
    ./nickel gen-completions "$shell" > "$shell-completion"
  done
  popd
}

#check() {
#  cd "$pkgbase"
#
#  cargo test --frozen --all-features
#}

package_nickel() {
  optdepends=(
    'nickel-docs: documentation'
    'nickel-language-server: language server'
  )
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/nickel

  # shell completions
  pushd target/release
  install -vDm644 bash-completion "$pkgdir/usr/share/bash-completion/completions/nickel"
  install -vDm644 fish-completion "$pkgdir/usr/share/fish/vendor_completions.d/nickel.fish"
  install -vDm644 zsh-completion "$pkgdir/usr/share/zsh/site-functions/_nickel"
  popd

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

package_nickel-docs() {
  pkgdesc='Documentation & examples for the Nickel Programming Language'
  depends=()
  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr examples doc/manual "$pkgdir/usr/share/doc/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

package_nickel-language-server() {
  pkgdesc='A language server for the Nickel Programming Language'
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/nls

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
