# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-notifications
pkgver=8.0.1
pkgrel=1
pkgdesc='Switchboard Notifications Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-notifications
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=574e5836cfeb8f36763733f8c64eabbb5de5d7d0
source=(git+https://github.com/elementary/switchboard-plug-notifications.git#tag=${_tag})
b2sums=('90e5a782326b9f7179093c25b5f38d62902662ef0d5510cb0b60def5055227223b92fbec78cde6eb5e1f32598fd9ac4566d986ca6646f746941c77c79165d737')

pkgver() {
  cd switchboard-plug-notifications
  git describe --tags
}

build() {
  arch-meson switchboard-plug-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
