# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-notifications
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Notifications Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-notifications
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=57a34e85fa4805199b099cc6228911177c6c793b
source=(git+https://github.com/elementary/switchboard-plug-notifications.git#tag=${_tag})
b2sums=('58a7953b12c8ee7d1c2801c8bd81c58659947ca8619ec7a93f2c904259038c9174ae5bf18065cdbb17d1ca894f2969edf7f35f47a48a78152132fc67e0176336')

pkgver() {
  cd switchboard-plug-notifications
  git describe --tags
}

build() {
  arch-meson switchboard-plug-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
