# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=manuals
pkgver=49.0
pkgrel=1
pkgdesc='Developer documentation browser for GNOME'
arch=(x86_64)
url='https://apps.gnome.org/Manuals/'
license=(GPL-3.0-or-later)
groups=(gnome-extra)
depends=(
  dconf
  foundry
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libdex
  libpanel
  pango
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
source=(
  "git+https://gitlab.gnome.org/GNOME/manuals.git#tag=${pkgver/[a-z]/.&}"
  manuals-new-window.patch
)
b2sums=(
  704266b8cb8e75572ad9a40e9b036a83281f27a953cd078c750897dc98b5c29fc3c14baf88d9a60d941c9cfe48c2abc4b763e4bcd2ade3f390452a8ecff31a60
  fa11ae57de4377893231f4b111c94e0b7c1dda1d722aa6d1b872589008e296fe0fec6f7281f3d4ea68b58f5ca1b61d20a6d98df02c9f9af622aab8c190ed0206
)

prepare() {
  cd $pkgname

  # Implement "new-window" command-line option
  # https://gitlab.gnome.org/GNOME/manuals/-/merge_requests/15
  git apply -3 ../manuals-new-window.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
