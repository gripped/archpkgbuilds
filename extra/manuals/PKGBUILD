# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=manuals
pkgver=49.1
pkgrel=1
pkgdesc='Developer documentation browser for GNOME'
arch=(x86_64)
url='https://apps.gnome.org/Manuals/'
license=(GPL-3.0-or-later)
groups=(gnome-extra)
depends=(
  dconf
  foundry
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libdex
  libpanel
  pango
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/manuals.git#tag=${pkgver/[a-z]/.&}")
b2sums=(837a4882485d6e4e605c91bdbed88f14aa251de111efa6cfe738affd38cdc503dc444a0ed3191fac870a0004fb5dd62210fcf36fca786173dbff53fd24264928)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
