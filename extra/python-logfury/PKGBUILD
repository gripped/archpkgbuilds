# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-logfury
pkgver=1.0.1
pkgrel=8
pkgdesc='Low boilerplate logging of Python method calls'
arch=(any)
url='https://github.com/reef-technologies/logfury'
license=(BSD-3-Clause)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-pytest
  python-testfixtures
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  remove-version-constraint.patch
)
sha512sums=('8a73cb1eec96a8035eb613bc570444bbe67c8241cb8f164a87713a59a3386cdb906665d3c212ea30e49c0d5b5bcb2e7e422cbc04523a10b5b3c4c29ae51a2f84'
            '8ea2e1447da2289b1260d06d3552719d4e831e8d8b54cf3ee8a544fe7223f91af1e33261bcf62112f20e3504ce3341202e2b7d27a06fd890cbfd6ee1ac289b03')
b2sums=('8189b5a3d10e1f739bf99e650f3aa31e3074c25c059b3a0445ea6e028cd780bad958e13e5e62023979676cf764bc327176614785208cdf5b0fc241314988dd4d'
        'c641e732d6bfc82c0494c5e5362e1d875bb7b78df865f2330853693ba1985ffc35593efbcb268d747cbc3aba8344360616b71ab0b2eea11c51f10ee770358d44')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/remove-version-constraint.patch"

  # remove six (from example)
  # https://github.com/reef-technologies/logfury/pull/11
  git cherry-pick --no-commit 525bb4a7f6c7d595fdb06d229a4bc2f567474eb5
}

build() {
  cd "$pkgname"

  SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver" python -m build --wheel --no-isolation 
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
