# Maintainer: kpcyrd <git@rxv.cc>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-crev
pkgver=0.26.3
pkgrel=3
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/crev-dev/cargo-crev"
depends=('cargo' 'openssl' 'libcurl.so' 'libgit2.so')
makedepends=('clang')
arch=('x86_64')
license=('MPL-2.0' 'Apache-2.0' 'MIT')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/crev-dev/cargo-crev/archive/v${pkgver}.tar.gz"
        "libgit2.pc")
sha512sums=('7c731555cbb401a7babf6ec5a4af6f0ce447ee10993fc8141e5c5a30fd5907a98eebc368898ebf689a9ffd6eccbbd22386dd239483b610b820f7a901d734f70e'
            '95bf0ae63789e62eec0a52ec5d0f4e0b4e8040b6e2f9a22d161bc0c079c3bc70bba3a7b1e9a72554c94123c1c7e6dfb053ba866538b66236f7835d182820fec8')
b2sums=('384700754da5dd0601efc20765676bfa52ee90b83109354b291b314c4611a7fb40c6f1be6ed6f738e483915ebde8a97244be73fdb7edf223ab8f38e5af54ff6d'
        'aaf6383a11a3adcb17e9d690930572fb10358d284b1a5059aaeb4e4295d709f157465c7874fa9a5bf28c6910d2b537bf82c2fdd0745d7da150f29c749cca4b4c')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  # use custom libgit2.pc file to trick cargo into linking libgit2 1.9.x
  PKG_CONFIG_PATH="$srcdir" LIBGIT2_NO_VENDOR=1 LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo build -p "$pkgname" --frozen --release --no-default-features
}

check() {
  cd "${pkgname}-${pkgver}"
  PKG_CONFIG_PATH="$srcdir" LIBGIT2_NO_VENDOR=1 LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo test -p "$pkgname" --frozen --release --no-default-features
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
