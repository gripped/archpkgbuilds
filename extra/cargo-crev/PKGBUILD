# Maintainer: kpcyrd <git@rxv.cc>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-crev
pkgver=0.26.0
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/crev-dev/cargo-crev"
depends=('cargo' 'openssl' 'libcurl.so' 'libgit2.so')
makedepends=('clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/crev-dev/cargo-crev/archive/v${pkgver}.tar.gz")
sha512sums=('879df752a9ddd8880c2498b22b98852097be78c290ef91deb3cdd374392c1dfbfdae833b655faf553a21d62d98ec793e4c4fecbc96a4bd90e97574ead9571074')
b2sums=('eb253bd8b8ea3eacaa9962b3d96a0a3de1e11ae4d582de1f50f1c77715f2f3115b6729a998888bd2f17687ab6d2d643796a9dd6a86967bcabeaf347085b16623')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo build -p "$pkgname" --frozen --release --no-default-features
}

check() {
  cd "${pkgname}-${pkgver}"
  LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo test -p "$pkgname" --frozen --no-default-features
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
