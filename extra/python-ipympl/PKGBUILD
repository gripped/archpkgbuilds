# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Specter119 <spcter119 AT gmail.com>
# Contributor: Erich Eckner <arch at eckner dot net>
# Contributor: Philip Goto <philip.goto@gmail.com>

_pyname=ipympl
pkgname=python-$_pyname
pkgver=0.9.4
pkgrel=1
pkgdesc='Matplotlib Jupyter Extension'
url='https://pypi.org/project/ipympl/'
depends=(ipython
         python
         python-ipython-genutils
         python-ipywidgets
         python-matplotlib
         python-numpy
         python-pillow
         python-traitlets)
makedepends=(jupyterlab
             nodejs
             python-build
             python-hatch-jupyter-builder
             python-hatchling
             python-installer)
checkdepends=(python-numpy
              python-nbval
              python-pytest)
license=(BSD-3-Clause)
arch=(any)
source=(https://pypi.io/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('cfb53c5b4fcbcee6d18f095eecfc6c6c474303d5b744e72cc66e7a2804708907')

prepare() {
  sed -e 's|>=.*\"|\"|g' -e 's|~=.*\"|\"|g' -i $_pyname-$pkgver/pyproject.toml
  sed -e '/index.js/d' -e '/plugin.js/d' -i $_pyname-$pkgver/setup.py
}

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

# pypi tarball missing tests
#check() {
#  cd $_pyname-$pkgver
#  PYTHONPATH="$PWD" \
#  pytest -v
#}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/{usr/,}etc
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
