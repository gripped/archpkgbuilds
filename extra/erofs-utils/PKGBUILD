# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=erofs-utils
pkgver=1.8.5
pkgrel=1
pkgdesc="Userspace utilities for linux-erofs file system"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/linux/kernel/git/xiang/erofs-utils.git"
license=(
  Apache-2.0
  GPL-2.0-or-later
)
depends=(
  glibc
  libdeflate
  lz4
  zlib
)
makedepends=(
  fuse2
  util-linux
  xxhash
  xz
  zstd
)
optdepends=(
  'fuse2: for erofsfuse'
)
source=($url/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('c5c3a71494113ad69c5b9278ef682250b3a5eca7e6f6e92a68800f7c7ba7e69f1dbf65f877c856c619dc2276f9d3b1c82fc89853ae7938a63b390c0cb246a0da')
b2sums=('ec0b514fa56c6aea960c32c2def00ee3f5681fb4c2060974928efe77d3ab8c6622f4a2676171976817e3911685e7d1e0c7cc7504d38965b5f84d4255ba82f3e1')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --enable-fuse
    --enable-lzma
    --with-libdeflate
    --enable-multithreading
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    util-linux libuuid.so
    xxhash libxxhash.so
    xz liblzma.so
    zstd libzstd.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
