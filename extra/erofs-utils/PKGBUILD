# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=erofs-utils
pkgname=(erofs-utils erofsfuse)
pkgver=1.8.6
pkgrel=1
pkgdesc="Userspace utilities for linux-erofs file system"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/linux/kernel/git/xiang/erofs-utils.git"
license=(
  Apache-2.0
  GPL-2.0-or-later
)
makedepends=(
  fuse2
  glibc
  libdeflate
  lz4
  util-linux
  xxhash
  xz
  zlib
  zstd
)
source=($url/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('07c9c30f6fb0b889b8c664f233e7510a15aba46f097a4bd40fb6f8c379f578bba14694f6fa4d12099816d1c7944beaedafa687795e5c08f69cb916119cfd446f')
b2sums=('cdbcda78ee6b5a91d1c1f3455e16eff377facbd0f562f58dd0ecd00ff361a20474e124d1f1906a0f10f77946a690c9f14a823ccda036af2649bc572858a071d8')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --enable-fuse
    --enable-lzma
    --with-libdeflate
    --enable-multithreading
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -vp "$(dirname "$d")"
    mv -v "$f" "$d"
    rmdir -vp --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_erofs-utils() {
  depends+=(
    glibc
    libdeflate libdeflate.so
    lz4 liblz4.so
    util-linux libuuid.so
    xxhash libxxhash.so
    xz liblzma.so
    zstd libzstd.so
    zlib libz.so
  )
  optdepends=(
    'erofsfuse: for FUSE file system client'
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"

  cd "$pkgdir"
  _pick erofsfuse usr/bin/erofsfuse
  _pick erofsfuse usr/share/man/man1/erofsfuse*
}

package_erofsfuse() {
  pkgdesc="FUSE file system client for erofs file system"
  depends+=(
    fuse2
    glibc
    libdeflate libdeflate.so
    lz4 liblz4.so
    xxhash libxxhash.so
    xz liblzma.so
    zstd libzstd.so
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
