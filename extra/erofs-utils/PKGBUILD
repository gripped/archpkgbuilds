# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=erofs-utils
pkgver=1.8.2
pkgrel=1
pkgdesc="Userspace utilities for linux-erofs file system"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/linux/kernel/git/xiang/erofs-utils.git"
license=(
  Apache-2.0
  GPL-2.0-or-later
)
depends=(
  glibc
  libdeflate
  lz4
  zlib
)
makedepends=(
  fuse2
  util-linux
  xz
  zstd
)
optdepends=(
  'fuse2: for erofsfuse'
)
source=($url/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('474ea30662e498e6ece5d4e5171c333ec040eaffa4f5670d1b728b1461a2a18fa90ea6cc2ad7b26e87732d735985e9108a9c49a7072b4dc4fdfee7916df48593')
b2sums=('1b42d74e85a41ee6be1eff05e8cfcb8c8b6799ebccd3a1648d34e9f2d4dec4fe2fdac059c698ab9e181d49c29b63514aab9f0472f2753524a7f40034dbdcc72a')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --enable-fuse
    --enable-lzma
    --with-libdeflate
    --enable-multithreading
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    util-linux libuuid.so
    xz liblzma.so
    zstd libzstd.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
