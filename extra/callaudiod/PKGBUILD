# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker (CoderThomasB) <tw.booker@outlook.com>
# Contributor: Philip Goto <philip.goto@gmail.com>

pkgname=callaudiod
pkgver=0.1.99
pkgrel=1
pkgdesc='Call audio routing daemon (deprecated)'
arch=(
  x86_64
  aarch64
)
url='https://gitlab.com/mobian1/callaudiod'
license=(GPL-3.0-or-later)
depends=(
  alsa-lib
  appstream
  gcc-libs
  glib2
  glibc
  glibc
  libpulse
)
makedepends=(
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.com/mobian1/callaudiod.git#tag=${pkgver/[a-z]/.&}")
b2sums=('8f123d321bb6ffbe1e5d60f768306212fcd8c43b8a3de3eeaffb7edc0d2376a76a6213b54c047f3acd35fdbee449b02ee780e0bc127f25f42946ab3b234607cc')

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
