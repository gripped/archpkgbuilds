# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Gaute Hope <eg@gaute.vetsj.com>
# Contributor: Johannes Heinz <heinzeljo@web.de>
pkgname=astroid
pkgver=0.17
pkgrel=4
pkgdesc="A graphical threads-with-tags style, lightweight and fast, e-mail client for Notmuch"
arch=('x86_64')
url="https://github.com/astroidmail/astroid"
license=('GPL-3.0-or-later'
         'LGPL-2.1-or-later')
depends=('boost-libs'
         'gmime3'
         'gobject-introspection-runtime'
         'gtkmm3'
         'libpeas'
         'libsass'
         'notmuch'
         'protobuf'
         'vte3'
         'webkit2gtk-4.1')
makedepends=('boost'
             'cmake'
             'cmark'
             'gobject-introspection'
             'ruby-ronn'
             'w3m')
checkdepends=('notmuch-runtime'
              'xorg-server-xvfb')
optdepends=('cmark: for markdown to html composition'
            'emacs: can be used as editor'
            'gvim: default editor')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/astroidmail/astroid/archive/v${pkgver}.tar.gz")
sha256sums=('dd419d4e11d1efa95979472e17cca5066799a5c64a595d5cea2b9e8d9f60f9c8')

build() {
  cd "${pkgname}-${pkgver}"

  cmake -B build \
    -DCMAKE_BUILD_TYPE="None" \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_CXX_STANDARD=17 \
    -Wno-dev
  cmake --build build
}

check() {
  cd "${pkgname}-${pkgver}"

  cd build
  xvfb-run ctest --output-on-failure
}

package() {
  cd "${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}" cmake --install build
}
