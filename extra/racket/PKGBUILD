# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgbase=racket
pkgname=(racket racket-minimal)
pkgver=9.0
pkgrel=1
pkgdesc='A full-spectrum language with DrRacket IDE'
arch=(x86_64)
url='https://racket-lang.org/'
license=(Apache-2.0 GPL-3.0-only LGPL-3.0-only custom)
depends=(gtk3)
makedepends=(gsfonts sqlite)
options=(!strip !emptydirs)
conflicts=(racket-docs)
replaces=(racket-docs)
source=(
  "https://download.racket-lang.org/installers/${pkgver}/${pkgname}-${pkgver}-src.tgz"
  add-icon-to-desktop.patch
)
sha512sums=('235a8bea59ee27c2ec80a1e84c50eb0928fd49df25d5f87217b1209862079b01756c6b6cdedb11c1b9c21ffeb23dbcee04847dba3d14a5473ec44609d7cbde56'
            '3e0117152db78f059f0c676b587a05c5705a1ee97cd28e7af85aed003e26febc31c1d665509b452c5f74dc50cb557260820041a23ad127c4787aefb44c54d2d8')
b2sums=('a2e326416f1d0040d803c3a4b6cb0aa707142d3390ff416b70cc091c8085d11ab3351600a15d33bd5328939b3e38030db0704b4ea770df61e36965a0afadb240'
        'e8ff6ec630d237a31a929338848b1519275dda252cd44cb6ea7de6ee9920340ca3ad3847538ba56800cf3711fcc56c206797d863b5abab7fd2954fd9bcd50e98')

prepare() {
  cd "$pkgbase-$pkgver"

  patch -p1 -i "$srcdir/add-icon-to-desktop.patch"
}

build() {
  cd "$pkgbase-$pkgver/src"
  [ "$CARCH" == "x86_64" ] && export CFLAGS+=" -fPIC -ffat-lto-objects"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared
  make
}

package_racket() {
  cd "$pkgbase-$pkgver/src"
  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE*.txt
  install -Dm644 ../share/pkgs/drracket-core-lib/drracket/drracket.desktop "$pkgdir/usr/share/applications/drracket.desktop"
  install -d "$pkgdir"/usr/share/icons/hicolor/scalable/apps
  ln -s /usr/share/racket/pkgs/icons/racket-logo.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/drracket.svg"
}

package_racket-minimal() {
  pkgdesc='Minimal Racket installation, without DrRacket/docs'
  depends=(libffi)
  conflicts=(racket)
  provides=(racket)
  replaces=()

  # The -minimal tarball uses the SAME extract path.
  # 99% identical sources anyway, manually apply the difference.
  # Thankfully these builds happen in the given order
  # so we can save some time and reuse the previous build.
  cd "$pkgbase-$pkgver"
  echo '((root "pkgs/racket-lib"))' > share/links.rktd
  _libhash=$(grep -oP '\("racket-lib".*?#f\)\)' share/pkgs/pkgs.rktd)
  echo "#hash($_libhash)" > share/pkgs/pkgs.rktd
  cd share/pkgs
  find . -not -name '.' -not -name '*pkgs.rktd' -not -regex '.*/racket-lib.*' -delete
  cd ../../src

  make DESTDIR="${pkgdir}" install

  find "$pkgdir/usr/share/doc/" -delete

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE*.txt
}
