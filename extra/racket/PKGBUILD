# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgbase=racket
pkgname=('racket' 'racket-minimal')
pkgver=8.18
pkgrel=1
pkgdesc='A full-spectrum language with DrRacket IDE'
arch=('x86_64')
url='https://racket-lang.org/'
license=('Apache-2.0' 'GPL-3.0-only' 'LGPL-3.0-only' 'custom')
depends=('gtk3')
makedepends=('gsfonts' 'sqlite')
options=('!strip' '!emptydirs')
conflicts=('racket-docs')
replaces=('racket-docs')
source=("https://download.racket-lang.org/installers/${pkgver}/${pkgname}-${pkgver}-src.tgz")
sha512sums=('65da251921bbdf400ea816383f8a7172305784fb3fbf0af8814e5a505934fb7da46b26841b4d01c2d0501292511732da949190002b795ef3863c3df1d35e4b8b')
b2sums=('ae368ca34ab122b8dd1a22e772417a1dcba2fb51cfb8eaf23a55869dc112c9038ed96dfff969849ff616ebf7038a9bacf23c3853642fe302b613ef6f3fca62ce')

prepare() {
  cd "$pkgbase-$pkgver"
  echo "Icon=drracket" >> share/pkgs/drracket/drracket/drracket.desktop
}

build() {
  cd "$pkgbase-$pkgver/src"
  [ "$CARCH" == "x86_64" ] && export CFLAGS+=" -fPIC -ffat-lto-objects"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared
  make
}

package_racket() {
  cd "$pkgbase-$pkgver/src"
  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE*.txt
  install -Dm644 ../share/pkgs/drracket/drracket/drracket.desktop "$pkgdir/usr/share/applications/drracket.desktop"
  install -d "$pkgdir"/usr/share/icons/hicolor/scalable/apps
  ln -s /usr/share/racket/pkgs/icons/racket-logo.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/drracket.svg"
}

package_racket-minimal() {
  pkgdesc='Minimal Racket installation, without DrRacket/docs'
  depends=('libffi')
  conflicts=('racket')
  provides=('racket')
  replaces=()

  # The -minimal tarball uses the SAME extract path.
  # 99% identical sources anyway, manually apply the difference.
  # Thankfully these builds happen in the given order
  # so we can save some time and reuse the previous build.
  cd "$pkgbase-$pkgver"
  echo '((root "pkgs/racket-lib"))' > share/links.rktd
  _libhash=$(grep -oP '\("racket-lib".*?#f\)\)' share/pkgs/pkgs.rktd)
  echo "#hash($_libhash)" > share/pkgs/pkgs.rktd
  cd share/pkgs
  find . -not -name '.' -not -name '*pkgs.rktd' -not -regex '.*/racket-lib.*' -delete
  cd ../../src

  make DESTDIR="${pkgdir}" install

  find "$pkgdir/usr/share/doc/" -delete

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE*.txt
}
