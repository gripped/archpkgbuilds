# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=jellyfin-web
pkgver=10.9.3
pkgrel=2
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('f725e55cf3449c003134d6926d560f92d16fb836b1503fd5b6a8ba3e45ebb415ffe5869919efe30099653f70a5a2de40b9a1fe0800986ae7689a51e82b541f2c')
b2sums=('8c51f35cda77ec90c49b967e319c09fda2207f8df6f82221e2eb7dfc8a0aabf962be3ad306be9fe8e84bdae71394dce180d4ca9d9e6038b95850edf706e3e366')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
