# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=jellyfin-web
pkgver=10.10.1
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('f647b058de2b5479ca9d965b6eba5d186ccc4257122bc571ca3460013d49786a6190ad663b2981d2102ee3f27f6f38f7c2bb7136ce1cfa3b40023a951ef5ce2d')
b2sums=('ca3f0f8f834a6599dfc432abf176536d59eb86460115b83e2e80425b6820d19586161451832a275d9b5a1c5690ffa031ae402608f419bdae2de3e1a8e61f7b9b')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
