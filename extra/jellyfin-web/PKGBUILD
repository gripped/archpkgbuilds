# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=jellyfin-web
pkgver=10.10.7
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('823f3193428b0f134a677157c4ec6b31af49b3878aed69671494fe63a71dc67b6cd413f841c3f1f0fcdbeefbd30edf62ac0f7afaafd411ea159c43097b2aee2a')
b2sums=('4211e0f3dc9d9fa26afc6bb9d0d40d6bf6ca70c3addf7313fe2bdf34477e473a4dc001ae5a4e9b681fcf498c9e32b2eeae93e7f9b7408d722821c88788c4c76d')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
