# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=jellyfin-web
pkgver=10.9.7
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('ffa1e4727dc0762a45d696b23632672766696f5d03eb0bce56fea743c003192e44c2722e44ab77f8d5215598165cc9549ad422dd802fa241520257eafb789384')
b2sums=('e75d1a4a50a46963599d7998da9dea18e1f7fe2cb2a328eaa6515b9a7a7c0f20d9784ca1319bf4e43d5bd03ac4d9f9d5e7013b52afa81331dea409a3d0b9099f')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
