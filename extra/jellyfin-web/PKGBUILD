# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=jellyfin-web
pkgver=10.10.0
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('31e7e164855eb56a784a80ef5a03cd7150b81f5e31d0b64492044ec9e7e72b25e000919aec9b840c60d6dbc5e224acba7720405c6cf1623142e1188717e40c81')
b2sums=('f2c495bfe95adbeb614a9988c4ea70d709c0761930208f3dd0ffe23194d6ada872ec3c0fa246afa7097e64ac17fc3946061f2c1fb8d229d532079369091d8ac7')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
