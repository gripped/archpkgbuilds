# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=jellyfin-web
pkgver=10.9.6
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('ef8f329e2d477e1052ddbaa8cdf7292d6329bc4d580546a8ad83f3d941aec2eccc67d421895dd4e8edcf12c18c6a2e4ec02488bbfeeecb4f49310487dec85094')
b2sums=('ea7f60c5cf4177b881aef47651ec79001ff8bef51454f881edfbab775b0650219b593db1f0b88cf865b3e46cbc128976f792498b5db0ea76236b36dad951b7ec')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
