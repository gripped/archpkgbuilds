# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=jellyfin-web
pkgver=10.10.4
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('2df2e0dfbc7b6b64aa4c25de291b0e9f7af1fa88d8a9d4e77af21fa17ba183333bc7bbaa7cc453dc63386fa34f81bce2a3b7b27c19e88718ae1d465b638ffc67')
b2sums=('9bfa332b369b690e7dbf06868783fb26ce0b07c87b0e7318f3bc3fc84a3655cc1d38ed9e4311246d278378ecbd3ad91f352c1021c8da9298625edc798135687e')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
