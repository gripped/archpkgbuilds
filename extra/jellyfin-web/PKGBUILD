# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=jellyfin-web
pkgver=10.9.8
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('68026cb9663debb8ee2f88b8e916f43097b41a89c84b5f8baa4193f715460c4e7b4a77502dc5e190dc8c2912805e71b3738ce1afe3b62bb2ae14b61795dadcef')
b2sums=('2c06edd6ae6fb470138c41e66b4a64d5522ff6e75a348449e379f2601573b37f1f4f6dff218739d6229227f3a2240968aa9ff57e4ef1ffa081d3baac5f4ac76f')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
