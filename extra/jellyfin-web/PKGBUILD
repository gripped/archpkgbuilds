# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=jellyfin-web
pkgver=10.9.10
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('9c37e2ed326426dcd3a6d5445c78d2d7a30cb60fe9b90ebfebf619eb19ef35330d3ebcc3520d1b0e2343b25f2501d1e3f28fcc27f65178ea8de1d651528d836b')
b2sums=('e5cafc6af8801545a03de3d63c75aa0f5657fbba8e68d54f1f46f417240a0c5bfe7425c778babd3c95c9ecc93bcacb8648361d8b11d2f9d3a05f0934f215a76e')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
