# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=fyi
pkgdesc='A command line utility and notify-send alternative to send desktop notifications'
pkgver=1.0.3
pkgrel=2
url="https://codeberg.org/dnkl/$pkgname"
arch=(x86_64)
license=(MIT)
depends=(dbus)
makedepends=(meson
             ninja
             scdoc)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('39e563fc7db59608ffe8f34c96c04dbdce707ba4a8a97ed3300f03f50581ba66')

build() {
	cd "$pkgname"
	arch-meson . build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
