# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lcalc
pkgver=2.1.1
pkgrel=1
pkgdesc='C++ L-function class library and command line interface'
arch=(x86_64)
url='https://gitlab.com/sagemath/lcalc'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         pari)
makedepends=(gengetopt
             git)
source=(git+https://gitlab.com/sagemath/lcalc#tag=$pkgver)
sha256sums=('f3cf46fa25ed80ed863385ea7c2dd33250a9643a11ad901fb9a73a0bf681049e')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --with-pari
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
