# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lcalc
pkgver=2.1.0
pkgrel=1
pkgdesc='C++ L-function class library and command line interface'
arch=(x86_64)
url='https://gitlab.com/sagemath/lcalc'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         pari)
makedepends=(gengetopt
             git)
source=(git+https://gitlab.com/sagemath/lcalc#tag=$pkgver)
sha256sums=('0a301185a8528b19f61ace8c2b78d439cd70b9f003d4582e36b2d7d07b9ef3e7')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --with-pari
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
