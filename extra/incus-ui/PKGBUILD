pkgname=incus-ui
pkgver=0.15.2
pkgrel=1
pkgdesc="incus web interface"
arch=('i686' 'x86_64')
url="https://github.com/zabbly/incus-ui-canonical"
license=('GPL-3.0-only')
makedepends=('git' 'yarn' 'npm')
changelog=
source=("git+https://github.com/zabbly/incus-ui-canonical.git#tag=incus-${pkgver}")
sha256sums=('ccd9c120b9c15779f455f5bb097b76c1baebe85ef482a83048ae21e03b91adcf')

prepare() {
  cd "incus-ui-canonical"
  # See https://github.com/zabbly/incus/blob/2efccf9de6d2245b8c4f2b3203e94613b973cf24/.github/workflows/builds.yml#L366C11-L366C79
  find -type f -name "*.ts" -o -name "*.tsx" -o -name "*.scss" | xargs sed -i \
    -e 's/devlxd/guestapi/g' \
    -e 's/dev\/lxd/dev\/incus/g' \
    -e 's/LXD/Incus/g' \
    -e 's/Lxd/Incus/g' \
    -e 's/lxd/incus/g'
  yarn install
}


build() {
  cd "incus-ui-canonical"
  yarn build
}

package() {
  cd "incus-ui-canonical"
  install -dm755 "${pkgdir}/usr/share/incus-ui"
  cp -dR --preserve=timestamps "build/ui" -T "${pkgdir}/usr/share/incus-ui"
}
