# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-4-1
#_patchver=p7
_pkgver=${_majver}${_patchver}
pkgver=${_majver//-/.}${_patchver/p/.p}
pkgrel=5
pkgdesc='Computer Algebra System for polynomial computations'
arch=(x86_64)
url='https://www.singular.uni-kl.de/'
license=(GPL-2.0-only
         GPL-3.0-only)
depends=(bash
         cddlib
         flint
         gcc-libs
         glibc
         gmp
         ntl
         readline)
makedepends=(4ti2
             git
             latex2html
             lrcalc
             normaliz
             sharutils
             texlive-basic
             texlive-bin
             texlive-latex)
optdepends=('normaliz: Normaliz interface')
provides=(singular-factory)
source=(git+https://github.com/Singular/Singular#tag=Release-$_pkgver
        flint-3.3.patch
        normaliz-3.10.5.patch)
sha256sums=('f8a11fb68192bbd4856d59e3967bddcd564b12ddade7c3b752a7aebadf2d9dfb'
            '183ce2781c19042b9a0f2dbcc1de723b5df67892494f9a0470a793cca963766e'
            'de297036c840e26ee5031262ef63b8b1b26c8cab7f931cd3cdeb68f6066b172e')
options=(!zipman)
#_srcdir=singular-${_majver//-/.}
_srcdir=Singular

prepare() {
  cd $_srcdir
  patch -p1 -i ../flint-3.3.patch
  patch -p1 -i ../normaliz-3.10.5.patch
  ./autogen.sh
}

build() {
  cd $_srcdir

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-bigintm-module \
    --enable-bigintm-module \
    --enable-Order-module \
    --enable-python-module \
    --enable-gfanlib-module \
    --enable-polymake-module \
    --enable-doc-build
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool -i */libtool
  make
  make -C doc doc.tbz2
}

check() {
  cd $_srcdir
  make check
}
  
package() {
  cd $_srcdir
  make DESTDIR="$pkgdir" install
}
