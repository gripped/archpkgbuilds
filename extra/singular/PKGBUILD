# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-4-0
#_patchver=p16
_pkgver=Release-${_majver}${_patchver}
pkgver=${_majver//-/.}${_patchver/p/.p}
pkgrel=1
pkgdesc='Computer Algebra System for polynomial computations'
arch=(x86_64)
url='https://www.singular.uni-kl.de/'
license=(GPL)
depends=(bash
         cddlib
         flint
         gcc-libs
         glibc
         gmp
         ntl
         readline)
makedepends=(4ti2
             git
             latex2html
             lrcalc
             normaliz
             sharutils
             texlive-basic
             texlive-bin
             texlive-latex)
provides=(singular-factory)
#source=(https://www.singular.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_pkgver//-/.}.tar.gz)
source=(git+https://github.com/Singular/Singular#tag=$_pkgver)
sha256sums=('3d111eaa19abf091d9d44ab378accb3f2b9584d49ec903be35625aecfa17c70b')
options=(!zipman)

prepare() {
  cd Singular
  ./autogen.sh
}

build() {
  cd Singular

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-bigintm-module \
    --enable-bigintm-module \
    --enable-Order-module \
    --enable-python-module \
    --enable-gfanlib-module \
    --enable-polymake-module \
    --enable-doc-build
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool -i */libtool
  make
  make -C doc doc.tbz2
}

check() {
  cd Singular
  make check
}
  
package() {
  cd Singular
  make DESTDIR="$pkgdir" install
}
