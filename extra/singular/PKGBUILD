# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-4-0
_patchver=p3
_pkgver=${_majver}${_patchver}
pkgver=${_majver//-/.}${_patchver/p/.p}
pkgrel=2
pkgdesc='Computer Algebra System for polynomial computations'
arch=(x86_64)
url='https://www.singular.uni-kl.de/'
license=(GPL)
depends=(bash
         cddlib
         flint
         gcc-libs
         glibc
         gmp
         ntl
         readline)
makedepends=(4ti2
             git
             latex2html
             lrcalc
             normaliz
             sharutils
             texlive-basic
             texlive-bin
             texlive-latex)
provides=(singular-factory)
source=(https://www.singular.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_pkgver//-/.}.tar.gz
        https://github.com/Singular/Singular/commit/16936a5f.patch)
#source=(git+https://github.com/Singular/Singular#tag=$_pkgver)
sha256sums=('4798dddcc4cb51f1cba5114a5cb4783708b9ab4b7d5e9136cb264eb62190c706'
            '60ca48885f5f9393c2eb1e07e0be4e8e5306b308d73e1dfd10518ccfb2289c0b')
options=(!zipman)
_srcdir=singular-${_majver//-/.}

prepare() {
  cd $_srcdir
  ./autogen.sh
  patch -p1 -i ../16936a5f.patch # Fix test failures in SageMath
}

build() {
  cd $_srcdir

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-bigintm-module \
    --enable-bigintm-module \
    --enable-Order-module \
    --enable-python-module \
    --enable-gfanlib-module \
    --enable-polymake-module \
    --enable-doc-build
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool -i */libtool
  make
  make -C doc doc.tbz2
}

check() {
  cd $_srcdir
  make check
}
  
package() {
  cd $_srcdir
  make DESTDIR="$pkgdir" install
}
