# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=sig
pkgver=0.2.1
pkgrel=1
pkgdesc="Interactive grep for streaming"
arch=('x86_64')
url="https://github.com/ynqa/sig"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('863bf2cdf218f2b820fa3160418ee1c31810139d4724d169cc6ad31ae2e2d8ae5b097b260b4cfaeeeaa95f1ddc6bf6a9a0bfa67825b28a30371d22f9fea6dff4')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
