# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=flashgbx
pkgver=4.4
pkgrel=2
pkgdesc='Reads and writes Game Boy and Game Boy Advance cartridge data'
arch=(any)
url=https://github.com/lesserkuma/FlashGBX
license=(GPL3)
depends=(
  pyside6
  python-dateutil
  python-pillow
  python-pyserial
  python-requests
  python-setuptools
)
makedepends=(git)
_tag=b7a4988a6beb788378790b06f6282b5c08181c26
source=(git+https://github.com/lesserkuma/FlashGBX.git#tag=${_tag})
sha256sums=('018836c6d4dbbf1e1f0e029d2b84d47f608c2efb6f6e8efc34f69ba560db2909')

pkgver() {
  cd FlashGBX
  git describe --tags
}

build() {
  cd FlashGBX
  python setup.py build
}

package() {
  cd FlashGBX
  python setup.py install --skip-build --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
