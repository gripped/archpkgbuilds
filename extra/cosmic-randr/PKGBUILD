# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-randr
pkgver=1.0.0.alpha.5
pkgrel=1
pkgdesc='Library and utility for displaying and configuring Wayland outputs'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-randr
license=(MPL-2.0)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=311b944d3f549af21a57fd348d0ede8219dd7f9c
source=(
  git+https://github.com/pop-os/cosmic-randr.git#tag=${_tag}
  cosmic-randr-lto.patch
)
b2sums=('ed8163ccb9667cdabdab0278c2f5cdf1c931e8459f56175390d609ebda0f6c9bcab85ac22711b5882a8b6f97be4fcb07324c308bb8ac52839eebeb41f87e8cb3'
        'ea19e1316acfa3e4888264226180f4ee135d7d1b0d95f58310f35b9914b568edf31d0cd1d15f00ce26124dd9844a21b489cd2d0384ae096976a1433da9d3611b')

prepare() {
  cd cosmic-randr
  patch -Np1 -i ../cosmic-randr-lto.patch
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-randr
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-randr
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-randr
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
