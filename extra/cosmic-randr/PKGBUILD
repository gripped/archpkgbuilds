# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-randr
pkgver=1.0.0.alpha.3
pkgrel=1
pkgdesc='Library and utility for displaying and configuring Wayland outputs'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-randr
license=(MPL-2.0)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=8d0938029f223016fde11aef4c5233ddbfdb2796
source=(
  git+https://github.com/pop-os/cosmic-randr.git#tag=${_tag}
  cosmic-randr-lto.patch
)
b2sums=('b682f70321a0a4bc2a62a407a8178a1eeef1cd977611e482064eef42f597dd465f0d489c460749b59fa5fbe083bcd3ffab5257a332b1249d164eac181e335a1a'
        'ea19e1316acfa3e4888264226180f4ee135d7d1b0d95f58310f35b9914b568edf31d0cd1d15f00ce26124dd9844a21b489cd2d0384ae096976a1433da9d3611b')

prepare() {
  cd cosmic-randr
  patch -Np1 -i ../cosmic-randr-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-randr
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-randr
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-randr
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
