# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-randr
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='Library and utility for displaying and configuring Wayland outputs'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-randr
license=(MPL-2.0)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=c32d0d6bfd1dc7a268642d2276db74c15c285ebb
source=(
  git+https://github.com/pop-os/cosmic-randr.git#tag=${_tag}
  cosmic-randr-lto.patch
)
b2sums=('08763659fe8a7cfb08e33b2790ac36b55f1a84d61c149ac7fc38aff0feed946a7926952e331881563f808303f77c702e33e36a3c07afbe8bfe3ceb0428cdd95a'
        '3bcdff90827243e43c4c581a8982a2739c339a98de089739c0ceaea2fdd76e555bde0a81a52f3603ee3cf7ccf8cf9e1410fd7211e445252fda7ce909ce13d776')

prepare() {
  cd cosmic-randr
  patch -Np1 -i ../cosmic-randr-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-randr
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-randr
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-randr
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
