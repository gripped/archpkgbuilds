# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-randr
pkgver=1.0.0.alpha.6
pkgrel=1
pkgdesc='Library and utility for displaying and configuring Wayland outputs'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-randr
license=(MPL-2.0)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=c247019230c5d820dd1c3d47bc4e3c52fb03b42f
source=(
  git+https://github.com/pop-os/cosmic-randr.git#tag=${_tag}
  cosmic-randr-lto.patch
)
b2sums=('d24e0d06b1ef89583f4e22a338913256ee46e3659952c48a50131aa869f31b87f16a8169f856864b750a9335786726c3cddb7444471ac303ee105dbac247b7dc'
        '3bcdff90827243e43c4c581a8982a2739c339a98de089739c0ceaea2fdd76e555bde0a81a52f3603ee3cf7ccf8cf9e1410fd7211e445252fda7ce909ce13d776')

prepare() {
  cd cosmic-randr
  patch -Np1 -i ../cosmic-randr-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-randr
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-randr
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-randr
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
