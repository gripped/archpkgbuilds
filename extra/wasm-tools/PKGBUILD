# Maintainer: Quentin Michaud <mh4ckt3mh4ckt1c4s@archlinux.org>

pkgname=wasm-tools
pkgver=1.244.0
pkgrel=1
pkgdesc="Low level tooling for WebAssembly in Rust"
arch=("x86_64")
url="https://github.com/bytecodealliance/wasm-tools"
license=("Apache-2.0")
depends=("glibc" "gcc-libs")
# Update using ./retrieve-testsuite-commit.sh <pkgver>
_wasm_testsuite_commit=1144e51585d4571a443dbcbb3300d8d20d9b6d4d
makedepends=("cargo")
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/bytecodealliance/$pkgname/archive/refs/tags/v$pkgver.tar.gz"
	"wasm-testsuite-$_wasm_testsuite_commit.tar.gz::https://github.com/WebAssembly/testsuite/archive/$_wasm_testsuite_commit.tar.gz"
)
sha256sums=('405e19e651da2ffc5878b8ded2cfad357c8f9b069512470b17c4a6916249d185'
            '48dbcb3b4d1be129f7918fbeb0f7cccd9b0f215825a7b55b98863f6e851bbcf3')

prepare() {
    cd "$pkgname-$pkgver"
    rm -r tests/testsuite
    cp -r "$srcdir/testsuite-$_wasm_testsuite_commit" tests/testsuite
    cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
    cd "$pkgname-$pkgver"
    cargo build --frozen --release --all-features --bin wasm-tools
}

check() {
    cd "$pkgname-$pkgver"
    cargo test --frozen
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
