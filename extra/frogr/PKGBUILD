# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=frogr
pkgver=1.8.1
pkgrel=3
pkgdesc='Flickr uploader for GNOME'
arch=(x86_64)
url='https://wiki.gnome.org/Apps/Frogr'
license=(GPL-3.0-only)
depends=(
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gstreamer
  gtk3
  hicolor-icon-theme
  json-glib
  libexif
  libgcrypt
  libsoup3
  libxml2
)
makedepends=(
  git
  itstool
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/frogr.git?signed#tag=RELEASE_$pkgver")
b2sums=(639ac9be27491d86d18b770f57804818435dcb548fa09f9cb37c1998ebbb4dfd04aab49358f7031677d35fc1fa1aedda5123a855b9195c225382ad642e58c072)
validpgpkeys=(9A98AEBB7E250E7654CD627A1AB71886930A0CF6) # Mario Sanchez Prada <mario@endlessm.com>

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
