# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.29
pkgrel=4
pkgdesc='Tool for monitoring webpages for updates'
arch=(any)
url=https://thp.io/2008/urlwatch/
license=(BSD-3-Clause)
depends=(
  python-cssselect
  python-keyring
  python-lxml
  python-minidb
  python-platformdirs
  python-requests
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-docutils
  python-jq
  python-pdftotext
  python-pytesseract
  python-pytest
  tesseract-data-eng
)
optdepends=(
  'lynx: for one of the html2text filter methods'
  'python-beautifulsoup4: for the beautify filter and one of the html2text filter methods'
  'python-cssbeautifier: for the beautify filter'
  'python-html2text: for one of the html2text filter methods'
  'python-jq: for the jq filter'
  'python-jsbeautifier: for the beautify filter'
  'python-pdftotext: for the pdf2text filter'
  'python-playwright: for the browser job'
  'python-pytesseract: for the ocr filter'
)
source=("git+https://github.com/thp/urlwatch.git#tag=$pkgver")
sha256sums=('0dacf27a451845a326d9069d8298cf37b99d602b2bfe4bd6dbcbb1378beafdac')
b2sums=('e546395c89c37bcfd78d839dbfe9f3154fe728674a39aeb41192ac3ae4587bd0e6f6b0c9697d62b326ed35f5e465e30335e7bc0329472167dd37cad9fdb564f8')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  pytest --ignore lib/urlwatch/tests/test_pep8.py
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/COPYING \
    "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
