# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=epubcheck
pkgver=5.2.0
pkgrel=1
pkgdesc="The conformance checker for EPUB publications"
arch=(any)
url="https://github.com/w3c/epubcheck"
license=(BSD-3-Clause)
_java_version=21
depends=(
  bash
  java-runtime=$_java_version
)
makedepends=(maven)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('2d72e6089019559e96b0f3c64de7833b5384e3968745929ce45d3290990bfc0f52e433bf863c002c5b0d103caadcc11bfd4437182163315c105939afb546deb6'
            'bbb278800c7729c8c99f25b90b4138b489e65d6f8556fb09f50e3c49b33458838ff0e29c98aa767f0e838c9de7a677e7198d2d78cc89d09aa8c3fee4a11531c8')
b2sums=('353d4b198b956140af7c4910c0bac622cd61a8b6bd0c34f22bbbe962ff882697fce366cd2a7e3a1f536403e61f197373da759453d34927bf8c3bebba5f84b693'
        'da868aa7f6dfefd788d1d3378ca1107025ee0ea0b915fe0238f9c3777af40916817b0497e791379e96b3a7c2f19523be71fc248dfc491733bfe77fea473a19fc')

prepare() {
  sed -e "s/JAVA_VERSION/$_java_version/" $pkgname.sh > $pkgname
}

build() {
  local maven_options=(
    clean
    package
    -D maven.test.skip=true  # tests fail on trying to use deprecated features
    -D maven.repo.local="./"
  )

  cd $pkgname-$pkgver
  mvn "${maven_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 "../$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 644 target/$pkgname.jar -t "$pkgdir/usr/share/java/$pkgname/"
  install -vDm 644 target/lib/*.jar -t "$pkgdir/usr/share/java/$pkgname/lib/"
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
