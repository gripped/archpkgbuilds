# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=epubcheck
pkgver=5.2.1
pkgrel=3
pkgdesc="The conformance checker for EPUB publications"
arch=(any)
url="https://github.com/w3c/epubcheck"
license=(BSD-3-Clause)
_java_version=21
depends=(
  bash
  java-runtime=$_java_version
)
makedepends=(
  maven
  strip-nondeterminism
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname.sh
  LICENSE
)
sha512sums=('3b6f4de33b0a6316d570ff442d354558ca41d2af085e09d9317aac01d7e6c2ebfd757a6ac1b717a642c855ce3aee5a26a86f31875434221d7347953e2b740ba1'
            'bbb278800c7729c8c99f25b90b4138b489e65d6f8556fb09f50e3c49b33458838ff0e29c98aa767f0e838c9de7a677e7198d2d78cc89d09aa8c3fee4a11531c8'
            'a33658d9271e5c537ccd41bf540b463ad2a5eca4a060c80486ff42a736f0aa042d10436e7177c34d792177cb11285243dee1f31c4df54fb0bfaabbc306406930')
b2sums=('18593fc835454b94e1dbb9152038044bb1a482290a57bc9f06a498b2fda19eecb00fa0c9abf278d5878ad5709c0c3fdec4f72d9bbf350e7794f1493f5134dc54'
        'da868aa7f6dfefd788d1d3378ca1107025ee0ea0b915fe0238f9c3777af40916817b0497e791379e96b3a7c2f19523be71fc248dfc491733bfe77fea473a19fc'
        'a29664104e1ee73ca0aee1d633e9095d92a57c92787f8d8740bdb7211ba3205782ed8677f539bdb8cae3dd75a3694be3132e185fa3fc4b3f401e1f88eb776101')

prepare() {
  sed -e "s/JAVA_VERSION/$_java_version/" $pkgname.sh > $pkgname
}

build() {
  local maven_options=(
    clean
    package
    -D maven.test.skip=true  # tests fail on trying to use deprecated features
    -D maven.repo.local="./"
  )

  cd $pkgname-$pkgver
  mvn "${maven_options[@]}"

  # Strip non-deterministic information from the jar file (e.g. timestamps and filesystem ordering)
  # Required for reproducible builds
  find . -name "$pkgname.jar" -exec strip-nondeterminism {} +
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 "../$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 644 target/$pkgname.jar -t "$pkgdir/usr/share/java/$pkgname/"
  install -vDm 644 target/lib/*.jar -t "$pkgdir/usr/share/java/$pkgname/lib/"
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/0BSD.txt"
}
