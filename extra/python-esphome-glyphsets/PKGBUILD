# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=python-esphome-glyphsets
_name=${pkgname#python-}
pkgver=0.2.0
pkgrel=4
pkgdesc='A lightweight version of glyphsets for ESPHome'
arch=(any)
url="https://github.com/esphome/$_name"
license=(Apache-2.0)
depends=(python)
makedepends=(python-build python-installer python-wheel python-setuptools git)
source=("git+$url.git#tag=v$pkgver")
b2sums=('8b9f1b67d1080c2e0e58da5b8d6d2e099d327dd01cf293c18150c826997753308e9ea17b6bab39bc2f6ffa8b9b3032699d54d98a13762b4a53e865ca7ac93400')

prepare() {
  cd $_name
  git submodule update --init
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
