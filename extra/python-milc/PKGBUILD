# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-milc
pkgver=1.9.0
pkgrel=3
pkgdesc="Opinionated Batteries-Included Python 3 CLI Framework."
arch=("any")
url="https://milc.clueboard.co/#/"
license=("MIT")
depends=("python-platformdirs" "python-argcomplete" "python-colorama" "python-halo")
makedepends=("python-setuptools")
checkdepends=("python-semver" "python-nose2")
source=("$pkgname-$pkgver.tar.gz::https://github.com/clueboard/milc/archive/${pkgver}.tar.gz")
sha256sums=('a1eb2a874d1bd2dbdc159fb1ad118f15b9147a4898be2248f79dc8273043d13b')

build() {
    cd "${pkgname#python-}-${pkgver}"
    python setup.py build
}

check() {
    cd "${pkgname#python-}-${pkgver}"
    rm tests/test_script*.py
    nose2
}

package() {
    cd "${pkgname#python-}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
