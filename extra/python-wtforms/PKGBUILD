# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: neithere <neithere at gmail>
# Contributor: mitsuse <mitsuse at gmail>

_pyname=wtforms
pkgname=python-wtforms
pkgver=3.2.1
pkgrel=2
pkgdesc='Flexible forms validation and rendering library for python web development'
url='https://wtforms.readthedocs.io/'
arch=('any')
license=('BSD')
depends=('python' 'python-markupsafe')
makedepends=('python-build' 'python-installer' 'python-babel' 'python-hatchling' 'python-setuptools')
checkdepends=('python-pytest' 'python-email-validator')
optdepends=('python-email-validator: Email validation')
source=(https://github.com/wtforms/wtforms/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a0eb0c11ea241be3881bc43b14505e807015c9459ece5b5ffe29af38cb774d89ceefcf734d962d62d2c6f647e975b175ad4ede0bbd98fdf82e3b0213e30fba1c')
b2sums=('d8ad2ec5b0bd54410bcb71b769c59708e952b5716e88ee35d93fde613318ba79d1ee69ca8ffd1a639e639372b423586fec544643406eda758feebb3a8a69ef81')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  find "$pkgdir" -type f -name \*.po -delete
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
