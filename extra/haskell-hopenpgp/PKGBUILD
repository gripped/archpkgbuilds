# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hOpenPGP
pkgname=haskell-hopenpgp
pkgver=2.10.0
pkgrel=11
pkgdesc="Native Haskell implementation of OpenPGP (RFC4880)"
url="http://floss.scru.org/hOpenPGP/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-asn1-encoding' 'haskell-attoparsec'
         'haskell-base16-bytestring' 'haskell-bifunctors' 'haskell-bz2' 'haskell-binary-conduit'
         'haskell-conduit' 'haskell-conduit-extra' 'haskell-crypton' 'haskell-crypto-cipher-types'
         'haskell-errors' 'haskell-hashable' 'haskell-incremental-parser' 'haskell-ixset-typed'
         'haskell-lens' 'haskell-memory' 'haskell-monad-loops' 'haskell-nettle'
         'haskell-network-uri' 'haskell-openpgp-asciiarmor' 'haskell-prettyprinter'
         'haskell-resourcet' 'haskell-split' 'haskell-time-locale-compat' 'haskell-unliftio-core'
         'haskell-unordered-containers' 'haskell-zlib')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('df53987b622abda47ae351bcf936116ed98bbae3f96223036e966a008c5f0826b22589e1978586e7c474f43f1ebcba87469da5e93f1193cac9a01f23eb2a07a6')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
