# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-tie-cycle
pkgver=1.229
pkgrel=1
pkgdesc='Cycle through a list of values via a scalar'
arch=(any)
url='https://github.com/briandfoy/tie-cycle'
license=(Artistic-2.0)
depends=(perl)
makedepends=(git)
checkdepends=(perl-test-pod perl-test-pod-coverage)
source=("$pkgname::git+$url#tag=release-$pkgver")
sha512sums=('6c78d318345b1747b86723804cf244dd975efc4f587a1d2a99fc05d17e0e65d8df47a4d4bd8b28f99a48d99a765fe840c03bc48348e453d77692b532d6e4748b')
b2sums=('a19d76e9413c610161f1ed9da8749ba4b89fd76705d0811e4b497d8166784612b2664e15f7276c0a176b3a1e6dc51f728745e882977147e899b6bfdf0503e664')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
