# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-tie-cycle
pkgver=1.231
pkgrel=1
pkgdesc='Cycle through a list of values via a scalar'
arch=(any)
url='https://github.com/briandfoy/tie-cycle'
license=(Artistic-2.0)
depends=(perl)
makedepends=(git)
checkdepends=(perl-test-pod perl-test-pod-coverage)
source=("$pkgname::git+$url#tag=release-$pkgver")
sha512sums=('e3f64c814a841a3efdd1f53f14bce2f77d496e716050fd525ca401f3f80e40c687cc576741fa7b271436a77fdd7c6f90df5eee65814f21620ab017f692e50316')
b2sums=('21d56fbdf9ce8dfb328d5f9cb7093bf385e0b6fd30460fee737053a0d0240290afbf615530efb0564c60ae0ec4e6c0b96c56ee4d40dd084b7e77eea4609cd5a2')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
