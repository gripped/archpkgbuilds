# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_hkgname=tasty-lua
pkgname=haskell-tasty-lua
pkgver=1.1.1.1
pkgrel=30
pkgdesc="Write tests in Lua, integrate into tasty."
url="https://github.com/hslua/tasty-lua"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-file-embed' 'haskell-hslua-core' 'haskell-hslua-marshalling'
         'haskell-lua-arbitrary' 'haskell-quickcheck' 'haskell-tasty')
makedepends=('ghc' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8b97dcbd32f10e158d9f859d6e99f2c2595ad1833cee713495948c90c1b8a64f86e92ccde0506de6ed1a1c750a5562f550dc06632ae1df8fcf1e08bfda6242ad')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
