# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=arch-wiki-docs
pkgver=20260201
pkgrel=1
pkgdesc='Pages from Arch Wiki optimized for offline browsing'
arch=(any)
url='https://github.com/lahwaacz/arch-wiki-docs'
license=(GFDL-1.3-or-later)
makedepends=(
  'git'
  'python'
  'python-lxml'
  'python-cssselect'
  'python-wiki-scripts'
)
options=('!strip' '!debug')
_commit=36ddd12d7884bb09c8660d8dd086ad13fba3a552
source=("$pkgname::git+https://github.com/lahwaacz/arch-wiki-docs#commit=$_commit")
b2sums=('fe1bdbf8e96581b1c2fc03430c3675469e577d4f865c9dfec4a105a34904926104ab6f23f89f4e9c46b15e4cb79f8dd6c5238dfaddc1c333269b46b0de159169')

pkgver() {
  date "+%Y%m%d"
}

prepare() {
  cd "$pkgname"

  LANG=C.UTF-8 python \
    arch-wiki-docs.py \
    --api-url https://wiki.archlinux.org/api.php \
    --index-url https://wiki.archlinux.org/index.php \
    --output-directory "$srcdir/build_wiki" \
    --clean \
    --safe-filenames
}

package() {
  install -vdm755 "$pkgdir/usr/share/doc/arch-wiki/html"
  cp -r build_wiki/* "$pkgdir/usr/share/doc/arch-wiki/html"
}
