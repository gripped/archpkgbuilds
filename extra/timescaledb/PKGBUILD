# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Fung <fylixeoi@gmail.com>

# NOTE: 2.17.0 provides support for PG17, so as soon as Arch gets PG17,
# we can rebuild for PG17.

pkgname=timescaledb
_current_pg_version=17
_next_pg_version=$(( _current_pg_version + 1 ))
# limit to last 5 releases
declare -gA _commits=(
  [2.17.0]=43ef9b7bda813c10f845fe55f7fc6113bbd47b07
  [2.17.1]=beb03d6d4c61dbd02291ef2c661c00155ddb79c9
  [2.17.2]=b359d26de186ea43f93c28c08cd1b8c6449c91bd
)
pkgver=2.17.2
pkgrel=2
pkgdesc='An open-source time-series database optimized for fast ingest and complex queries'
arch=('x86_64')
url='https://www.timescale.com/'
license=('Apache-2.0' 'custom:Timescale')
depends=(
  'openssl'
  "postgresql-libs<$_next_pg_version"
  "postgresql-libs>=$_current_pg_version"
  "postgresql<$_next_pg_version"
  "postgresql>=$_current_pg_version"
)
makedepends=('gcc' 'cmake' 'git')
optdepends=(
  'timescaledb-tune: tune postgresql.conf for better performance'
  'timescaledb-old-upgrade: upgrade from previous major version of PostgreSQL'
)
install=timescaledb.install
source=("$pkgname::git+https://github.com/timescale/timescaledb#commit=${_commits[$pkgver]}")
sha512sums=('9a4b8ffc295fc0fdc0eac1d5c6addf44f4b0c2b21ced9500badbb23d842696ac147454c56336ddf63a389aad6c205f26978019f23f5d5f8fec3f0293f6a291a9')
b2sums=('8edbce8bd952be1b86cd672111076d6ef74685b5da8d2ca1dbe0685ae2d7b07c0057d50cd111790e1eda4adc701af4777b082bce63261720e1bb5db5ade3e9b9')

prepare() {
  mkdir -p build
}

build() {
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/$pkgname"

    git checkout "${_commits[$version]}"

    BUILD_DIR="$srcdir/build/$version" ./bootstrap \
      -DWARNINGS_AS_ERRORS=OFF \
      -DREGRESS_CHECKS=OFF \
      -DTAP_CHECKS=OFF \
      -DGENERATE_DOWNGRADE_SCRIPT=ON

    # build package or past shared library
    cd "$srcdir/build/$version"

    # ensure reproducible builds (value from `lsb_release -r`)
    # TODO: resolve https://github.com/timescale/timescaledb/issues/3480
    sed \
      -e "s:BUILD_OS_VERSION \".*\"$:BUILD_OS_VERSION \"rolling\":" \
      -i src/config.h

    if [[ $version == $pkgver ]]; then
      make
    else
      make timescaledb timescaledb-tsl sqlfile
    fi
  done
}

package() {
  cd "$pkgname"
  # install licenses from latest version
  git checkout "${_commits[$pkgver]}"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE LICENSE-APACHE tsl/LICENSE-TIMESCALE

  # install package or past shared library
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/build/$version"
    if [[ $version == $pkgver ]]; then
      make DESTDIR="$pkgdir/" install
    else
      install -vDm755 -t "$pkgdir/usr/lib/postgresql" \
        "src/$pkgname-$version.so" \
        "tsl/src/$pkgname-tsl-$version.so"
      install -Dm644 -t "$pkgdir/usr/share/postgresql/extension" \
        "sql/timescaledb--$version.sql"
    fi
  done
}
