# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Fung <fylixeoi@gmail.com>

pkgname=timescaledb
_current_pg_version=17
_next_pg_version=$(( _current_pg_version + 1 ))
# limit to last 5 releases
declare -gA _commits=(
  [2.19.0]=b15ef74acaaf8df5aa0dd0652a0dccddcdc974e6
  [2.19.1]=9e75bacecf6cf81366f113edadf2dec82352e62c
  [2.19.2]=d454524eb6ef63196e3d22bc8a2e3cdf0719cfaf
  [2.19.3]=95861e123bd439ed3c04dc5eeb86fef841bdf93b
  [2.20.0]=e70ab02556617a421928ad02bd790b2c809851e7
)
pkgver=2.20.0
pkgrel=1
pkgdesc='An open-source time-series database optimized for fast ingest and complex queries'
arch=('x86_64')
url='https://www.timescale.com/'
license=('Apache-2.0' 'LicenseRef-Timescale')
depends=(
  'openssl'
  "postgresql-libs<$_next_pg_version"
  "postgresql-libs>=$_current_pg_version"
  "postgresql<$_next_pg_version"
  "postgresql>=$_current_pg_version"
)
makedepends=('gcc' 'cmake' 'git')
optdepends=(
  'timescaledb-tune: tune postgresql.conf for better performance'
  'timescaledb-old-upgrade: upgrade from previous major version of PostgreSQL'
)
install=timescaledb.install
source=("$pkgname::git+https://github.com/timescale/timescaledb#commit=${_commits[$pkgver]}")
sha512sums=('b51e999329270006eea59cf4e7c03bc862ff4566c2f315419ac279a412754b541e24f348102d726185085d7a329cb0ee674ba5f3407b0d74ccbfaf5aeaf25ff0')
b2sums=('545bb74d39d73bb7470910100e44558fb05a8eb1b34b3137c89d309eb108779417c4678342bf47c33e3b8c37e58609d2de98e53d6a78fac50e80160a9573164e')

prepare() {
  mkdir -p build
}

build() {
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/$pkgname"

    git checkout "${_commits[$version]}"

    BUILD_DIR="$srcdir/build/$version" ./bootstrap \
      -DWARNINGS_AS_ERRORS=OFF \
      -DREGRESS_CHECKS=OFF \
      -DTAP_CHECKS=OFF \
      -DGENERATE_DOWNGRADE_SCRIPT=ON

    # build package or past shared library
    cd "$srcdir/build/$version"

    # ensure reproducible builds (value from `lsb_release -r`)
    # TODO: resolve https://github.com/timescale/timescaledb/issues/3480
    sed \
      -e "s:BUILD_OS_VERSION \".*\"$:BUILD_OS_VERSION \"rolling\":" \
      -i src/config.h

    if [[ $version == $pkgver ]]; then
      make
    else
      make timescaledb timescaledb-tsl sqlfile
    fi
  done
}

package() {
  cd "$pkgname"
  # install licenses from latest version
  git checkout "${_commits[$pkgver]}"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE LICENSE-APACHE tsl/LICENSE-TIMESCALE

  # install package or past shared library
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/build/$version"
    if [[ $version == $pkgver ]]; then
      make DESTDIR="$pkgdir/" install
    else
      install -vDm755 -t "$pkgdir/usr/lib/postgresql" \
        "src/$pkgname-$version.so" \
        "tsl/src/$pkgname-tsl-$version.so"
      install -Dm644 -t "$pkgdir/usr/share/postgresql/extension" \
        "sql/timescaledb--$version.sql"
    fi
  done
}
