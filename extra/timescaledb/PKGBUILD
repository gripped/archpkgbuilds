# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Fung <fylixeoi@gmail.com>

pkgname=timescaledb
_current_pg_version=17
_next_pg_version=$(( _current_pg_version + 1 ))
# limit to last 5 releases
declare -gA _commits=(
  [2.19.3]=95861e123bd439ed3c04dc5eeb86fef841bdf93b
  [2.20.0]=e70ab02556617a421928ad02bd790b2c809851e7
  [2.20.1]=4b72721692dfb0dc8726ccc5eb97b58c1a74a784
  [2.20.2]=b7c7cd02585388ac6b555097ef8831c1217cc402
  [2.20.3]=7b30533d4d9b6ce1353b8bac0ba6215be20f2633
)
pkgver=2.20.3
pkgrel=1
pkgdesc='An open-source time-series database optimized for fast ingest and complex queries'
arch=('x86_64')
url='https://www.timescale.com/'
license=('Apache-2.0' 'LicenseRef-Timescale')
depends=(
  'openssl'
  "postgresql-libs<$_next_pg_version"
  "postgresql-libs>=$_current_pg_version"
  "postgresql<$_next_pg_version"
  "postgresql>=$_current_pg_version"
)
makedepends=('gcc' 'cmake' 'git')
optdepends=(
  'timescaledb-tune: tune postgresql.conf for better performance'
  'timescaledb-old-upgrade: upgrade from previous major version of PostgreSQL'
)
install=timescaledb.install
source=("$pkgname::git+https://github.com/timescale/timescaledb#commit=${_commits[$pkgver]}")
sha512sums=('7963d0200e3c789ea23ad7c24ae044cb8122754444f7b65711d186efad667c6f39f22843cbbe68d94f601d5919e26a0602d8c17a51d614229e883666b7082796')
b2sums=('ead8ff86d16f5e045d41dae5385c0999ffd3662faefa86aa1a4fe0417d8e73ea3695616fa6bf19648af814e9c13a3fba515d3b65149ff3ce5ddcd4a9b4faca0b')

prepare() {
  mkdir -p build
}

build() {
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/$pkgname"

    git checkout "${_commits[$version]}"

    BUILD_DIR="$srcdir/build/$version" ./bootstrap \
      -DWARNINGS_AS_ERRORS=OFF \
      -DREGRESS_CHECKS=OFF \
      -DTAP_CHECKS=OFF \
      -DGENERATE_DOWNGRADE_SCRIPT=ON

    # build package or past shared library
    cd "$srcdir/build/$version"

    # ensure reproducible builds (value from `lsb_release -r`)
    # TODO: resolve https://github.com/timescale/timescaledb/issues/3480
    sed \
      -e "s:BUILD_OS_VERSION \".*\"$:BUILD_OS_VERSION \"rolling\":" \
      -i src/config.h

    if [[ $version == $pkgver ]]; then
      make
    else
      make timescaledb timescaledb-tsl sqlfile
    fi
  done
}

package() {
  cd "$pkgname"
  # install licenses from latest version
  git checkout "${_commits[$pkgver]}"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE LICENSE-APACHE tsl/LICENSE-TIMESCALE

  # install package or past shared library
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/build/$version"
    if [[ $version == $pkgver ]]; then
      make DESTDIR="$pkgdir/" install
    else
      install -vDm755 -t "$pkgdir/usr/lib/postgresql" \
        "src/$pkgname-$version.so" \
        "tsl/src/$pkgname-tsl-$version.so"
      install -Dm644 -t "$pkgdir/usr/share/postgresql/extension" \
        "sql/timescaledb--$version.sql"
    fi
  done
}
