# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Fung <fylixeoi@gmail.com>

pkgname=timescaledb
_current_pg_version=17
_next_pg_version=$(( _current_pg_version + 1 ))
# limit to last 5 releases
declare -gA _commits=(
  [2.18.1]=0660ebc3fcf4e6859159ba42aeced56333d02021
  [2.18.2]=bb49cfc6d626782a214b6b6e343a0666d7c40773
  [2.19.0]=b15ef74acaaf8df5aa0dd0652a0dccddcdc974e6
  [2.19.1]=9e75bacecf6cf81366f113edadf2dec82352e62c
  [2.19.2]=d454524eb6ef63196e3d22bc8a2e3cdf0719cfaf
)
pkgver=2.19.2
pkgrel=1
pkgdesc='An open-source time-series database optimized for fast ingest and complex queries'
arch=('x86_64')
url='https://www.timescale.com/'
license=('Apache-2.0' 'LicenseRef-Timescale')
depends=(
  'openssl'
  "postgresql-libs<$_next_pg_version"
  "postgresql-libs>=$_current_pg_version"
  "postgresql<$_next_pg_version"
  "postgresql>=$_current_pg_version"
)
makedepends=('gcc' 'cmake' 'git')
optdepends=(
  'timescaledb-tune: tune postgresql.conf for better performance'
  'timescaledb-old-upgrade: upgrade from previous major version of PostgreSQL'
)
install=timescaledb.install
source=("$pkgname::git+https://github.com/timescale/timescaledb#commit=${_commits[$pkgver]}")
sha512sums=('5166a6fc27269bc3df90d4b1a270b8adb9a964593f320bc3073d88b7fa216888805071a69cbe27df54ec0cbf284f01732b2a18b5449a529004e763efc0bc2302')
b2sums=('95ccedae792c0e624a416ba799000a0890a748a778fbb52ce35bbb39eeaf86aa3b2f9dc12a02ae134b41994bcb2f7acb5e9dc09b781a782fbc5768f6c7adb4f6')

prepare() {
  mkdir -p build
}

build() {
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/$pkgname"

    git checkout "${_commits[$version]}"

    BUILD_DIR="$srcdir/build/$version" ./bootstrap \
      -DWARNINGS_AS_ERRORS=OFF \
      -DREGRESS_CHECKS=OFF \
      -DTAP_CHECKS=OFF \
      -DGENERATE_DOWNGRADE_SCRIPT=ON

    # build package or past shared library
    cd "$srcdir/build/$version"

    # ensure reproducible builds (value from `lsb_release -r`)
    # TODO: resolve https://github.com/timescale/timescaledb/issues/3480
    sed \
      -e "s:BUILD_OS_VERSION \".*\"$:BUILD_OS_VERSION \"rolling\":" \
      -i src/config.h

    if [[ $version == $pkgver ]]; then
      make
    else
      make timescaledb timescaledb-tsl sqlfile
    fi
  done
}

package() {
  cd "$pkgname"
  # install licenses from latest version
  git checkout "${_commits[$pkgver]}"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE LICENSE-APACHE tsl/LICENSE-TIMESCALE

  # install package or past shared library
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/build/$version"
    if [[ $version == $pkgver ]]; then
      make DESTDIR="$pkgdir/" install
    else
      install -vDm755 -t "$pkgdir/usr/lib/postgresql" \
        "src/$pkgname-$version.so" \
        "tsl/src/$pkgname-tsl-$version.so"
      install -Dm644 -t "$pkgdir/usr/share/postgresql/extension" \
        "sql/timescaledb--$version.sql"
    fi
  done
}
