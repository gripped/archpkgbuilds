# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-ssh-agent
_name="${pkgname#openpgp-card-}"
pkgver=0.3.3
pkgrel=1
pkgdesc="A simple ssh-agent backed by OpenPGP card authentication keys"
arch=(x86_64)
url="https://codeberg.org/openpgp-card/ssh-agent/"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
  org.freedesktop.secrets
)
makedepends=(
  dbus
  pcsclite
  rust
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('202b0b2a5a3d8efceaf7dc5e67978222ac61a8fac11c611572e053b707a6b15a30c22270b1a41c3b5e8dfaf967395e8d286ca27d1a96e441b248517b6b1b2460')
b2sums=('7a809c144af5c097843d391f5bb553d38710fe4f0edb6a259ba9a307071a3fca9cb1507c7f5e76bdd4051c3e00c0f514169682f896f7679990496b35f5669461')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    dbus libdbus-1.so
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
  install -vDm 644 contrib/*.{service,socket} -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
