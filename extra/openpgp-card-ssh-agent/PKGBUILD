# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-ssh-agent
_name="${pkgname#openpgp-card-}"
pkgver=0.3.4
pkgrel=1
pkgdesc="A simple ssh-agent backed by OpenPGP card authentication keys"
arch=(x86_64)
url="https://codeberg.org/openpgp-card/ssh-agent/"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
  org.freedesktop.secrets
)
makedepends=(
  dbus
  pcsclite
  rust
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('10bc627a1d95a145b0a782cf3350f03bb53bbde78a781db539f77ea0a691b9126ca57bafea07b25e2026a404d6326281c8d6374333164c982b4349bfb5694dc7')
b2sums=('c565ed2b21ac47a085cdea25fbd54ae02626752a68e95c269074e2d297cbf0d6c72ed81276a417c281cadd7e7fd0aea51d39a6c474e296c418eee04c83a44e00')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    dbus libdbus-1.so
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
  install -vDm 644 contrib/*.{service,socket} -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
