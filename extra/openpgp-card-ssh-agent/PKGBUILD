# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-ssh-agent
_name="${pkgname#openpgp-card-}"
pkgver=0.3.1
pkgrel=1
pkgdesc="A simple ssh-agent backed by OpenPGP card authentication keys"
arch=(x86_64)
url="https://codeberg.org/openpgp-card/ssh-agent/"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
  gnome-keyring
)
makedepends=(
  pcsclite
  rust
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('13a33bee41787017099a5b2068aa52d46b62a5ea34cff91f03b6543cea9cf7320249b882219591bd5d0fd7def7c17a7154fd0fefbe4984ef57f76924e2b0898a')
b2sums=('3fcfddff27e6efeb821739e2fc8a79059fad8096a4c9bc03cf4621e99ef98082aa0bdc70b0c1d6da3dcd75082db00af322bdeaafdd64a74a515e4599192c67d1')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
  install -vDm 644 contrib/*.{service,socket} -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
