# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-babel-glade
pkgver=0.7.0
pkgrel=5
pkgdesc='Message catalog extractor for Glade XML, extending Babel'
arch=(any)
url='https://github.com/gnome-keysign/babel-glade'
license=(BSD-3-Clause)
depends=(
  python
  python-babel
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("git+https://github.com/gnome-keysign/babel-glade.git#tag=$pkgver")
b2sums=(e9b1b91bd778495d9e9ddbd80e7459389ffc22437263d4ea57424dfdfd4a0e14254bce41c9c974cd89b469958bc0fdbabbad0124434e90b6c0385ff7c1849066)

build() {
  cd babel-glade
  python -m build --wheel --no-isolation
}

check() {
  cd babel-glade
  pytest
}

package() {
  cd babel-glade
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  # Remove installed tests
  rm -r "$pkgdir$(python -c 'import site; print(site.getsitepackages()[0])')"/babelglade/tests/
}
