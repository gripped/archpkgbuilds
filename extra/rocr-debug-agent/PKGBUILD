# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: acxz <akashpatel2008 at yahoo dot com>
pkgname=rocr-debug-agent
pkgver=6.4.4
pkgrel=1
pkgdesc="ROCr Debug Agent Library"
arch=('x86_64')
url="https://rocm.docs.amd.com/projects/rocr_debug_agent/en/latest/"
url="https://github.com/ROCm-Developer-Tools/rocr_debug_agent"
license=('NCSA')
depends=('rocm-core' 'hip-runtime-amd' 'rocm-dbgapi')
makedepends=('cmake')
provides=("rocm-debug-agent=$pkgver")
_git='https://github.com/ROCm/rocr_debug_agent'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz")
sha256sums=('7016939b41fbca3cf8c1bbf6d5be04c78bd71d561250ea15033b95346c85b5f4')
_dirname="$(basename "$_git")-$(basename "${source[0]}" ".tar.gz")"

build() {
  local cmake_args=(
    -Wno-dev
    -B build
    -S "$_dirname"
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
  )
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm6444 "$_dirname/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
