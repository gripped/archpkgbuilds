# Maintainer: David Runge <dvzrv@archlinux.org>

# WARNING: this package needs to be kept in sync with the requirements of deltachat-desktop
_upstream=deltachat-core-rust
pkgname=deltachat-rpc-server
pkgver=1.155.5
pkgrel=1
pkgdesc="A JSON-RPC 2.0 interface to DeltaChat over standard I/O"
arch=(x86_64)
url="https://github.com/deltachat/deltachat-core-rust/tree/main/deltachat-rpc-server"
_url="https://github.com/deltachat/deltachat-core-rust"
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
# NOTE: the ring dependency can not be built with LTO
options=(!lto)
source=($_upstream-$pkgver.tar.gz::$_url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('1dc409275ebaa3438e931b1c4d8663e8b71e3f7092740be3785bad8e9f6edaf08eaead3c4e3d2b31cf2f41a0b33349cc9c518c797c4e8c6ee32874c454e8d037')
b2sums=('99869b368ba91eff741b3656fdc190c95011d12d9f8300795c5609d5a740d5a0fed7f9eeab3e863412904dbcb9da7be732ba0b4a2708e44ae062807909af3ba4')

prepare() {
  export RUSTUP_TOOLCHAIN=stable
  cd $_upstream-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

}

build() {
  cd $_upstream-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_upstream-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo test --frozen --all-features --package $pkgname
}

package() {
  cd $_upstream-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
