# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

# WARNING: this package needs to be kept in sync with the requirements of deltachat-desktop
_upstream=deltachat-core-rust
_name=core
pkgname=deltachat-rpc-server
pkgver=2.11.0
pkgrel=1
pkgdesc="A JSON-RPC 2.0 interface to DeltaChat over standard I/O"
arch=(x86_64)
url="https://github.com/deltachat/deltachat-core-rust/tree/main/deltachat-rpc-server"
_url="https://github.com/deltachat/deltachat-core-rust"
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
# NOTE: the ring dependency can not be built with LTO
options=(!lto)
source=("$_url/archive/v$pkgver/$_upstream-$pkgver.tar.gz")
sha512sums=('ea49e37e82d3fe81f933f35742208b4afbee07d7b4afe149883f987ee57130fddb9179134d111e82bb2fca93e551bd0b2b97f784a70a3a21fe2e82ec1af52dcb')
b2sums=('3795b84f7ca4bb3a6f63cda09379bae8d132840359d1a4a638cdd85969348bddabd984205d9b3d365ca05eaa9b8b29a71de2d4cd18578f0d06cbef2afb3155ec')

prepare() {
  export RUSTUP_TOOLCHAIN=stable
  cd $_name-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

}

build() {
  cd $_name-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_name-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo test --frozen --all-features --package $pkgname
}

package() {
  cd $_name-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
