# Maintainer: David Runge <dvzrv@archlinux.org>

# WARNING: this package needs to be kept in sync with the requirements of deltachat-desktop
_upstream=deltachat-core-rust
pkgname=deltachat-rpc-server
pkgver=1.142.12
pkgrel=1
pkgdesc="A JSON-RPC 2.0 interface to DeltaChat over standard I/O"
arch=(x86_64)
url="https://github.com/deltachat/deltachat-core-rust/tree/main/deltachat-rpc-server"
_url="https://github.com/deltachat/deltachat-core-rust"
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
# NOTE: the ring dependency can not be built with LTO
options=(!lto)
source=($_upstream-$pkgver.tar.gz::$_url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('cdb3b39713df6ab39bc34d83966e026804addc8c6179c3735c964637023a0e68dd95a4450755eeebd36e9b326ab97e6af814931ecfb0b02602f3a0277d29fa06')
b2sums=('c91456d2fd90117f57021e9b38b329dc03f3d1d24979a5f3a799cfae227e589b6b3f5b09b19dea5c3b86d1fdfa8af4004ac46fff870543b5c0b31c0f9a872206')

prepare() {
  export RUSTUP_TOOLCHAIN=stable
  cd $_upstream-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

}

build() {
  cd $_upstream-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_upstream-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo test --frozen --all-features --package $pkgname
}

package() {
  cd $_upstream-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
