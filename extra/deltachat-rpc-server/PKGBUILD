# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

# WARNING: this package needs to be kept in sync with the requirements of deltachat-desktop
_upstream=deltachat-core-rust
_name=core
pkgname=deltachat-rpc-server
pkgver=2.25.0
pkgrel=1
pkgdesc="A JSON-RPC 2.0 interface to DeltaChat over standard I/O"
arch=(x86_64)
url="https://github.com/deltachat/deltachat-core-rust/tree/main/deltachat-rpc-server"
_url="https://github.com/deltachat/deltachat-core-rust"
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=("$_url/archive/v$pkgver/$_upstream-$pkgver.tar.gz")
sha512sums=('308f12f5ef30b4004aeca861c482801c84813dae517adab34c91e065560e0198f01a4abb323b139f6fefbde34d4b62da602bd6e6892ff920051e254df98138a9')
b2sums=('7f1dda40478127d8c85678382d4750102af4f1ed3aaae9d8c78c382368028d2945b47f08de7dab5f3a583b9a4103fcdffc382e016542d90bfe5d33d2714dce53')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

}

build() {
  cd $_name-$pkgver
  CFLAGS+=" -ffat-lto-objects" cargo build --frozen --release --package $pkgname
}

check() {
  cd $_name-$pkgver
  CFLAGS+=" -ffat-lto-objects" cargo test --frozen --package $pkgname
}

package() {
  cd $_name-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
