# Maintainer: David Runge <dvzrv@archlinux.org>

# WARNING: this package needs to be kept in sync with the requirements of deltachat-desktop
_upstream=deltachat-core-rust
pkgname=deltachat-rpc-server
pkgver=1.154.3
pkgrel=1
pkgdesc="A JSON-RPC 2.0 interface to DeltaChat over standard I/O"
arch=(x86_64)
url="https://github.com/deltachat/deltachat-core-rust/tree/main/deltachat-rpc-server"
_url="https://github.com/deltachat/deltachat-core-rust"
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
# NOTE: the ring dependency can not be built with LTO
options=(!lto)
source=($_upstream-$pkgver.tar.gz::$_url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('d4e1a4e4f074d020bb03b049d905ba02ab2538dab8c89deb5c7e8e083661cd8fd0846d9f27f4d7004a3c01d7fba8062d36ea5ee9e44ee9b91b57cfa13909cdef')
b2sums=('77c85b16c84e637bb91d8dcac500f061236f5cb359569d8e76029fed60a91ffb088acc2a8a06952b1f93d4fd85ee546efc9d8e744fa70477a0361c4f0a6dce07')

prepare() {
  export RUSTUP_TOOLCHAIN=stable
  cd $_upstream-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

}

build() {
  cd $_upstream-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_upstream-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo test --frozen --all-features --package $pkgname
}

package() {
  cd $_upstream-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
