# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

# WARNING: this package needs to be kept in sync with the requirements of deltachat-desktop
_upstream=deltachat-core-rust
_name=core
pkgname=deltachat-rpc-server
pkgver=2.35.0
pkgrel=1
pkgdesc="A JSON-RPC 2.0 interface to DeltaChat over standard I/O"
arch=(x86_64)
url="https://github.com/deltachat/deltachat-core-rust/tree/main/deltachat-rpc-server"
_url="https://github.com/deltachat/deltachat-core-rust"
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=("$_url/archive/v$pkgver/$_upstream-$pkgver.tar.gz")
sha512sums=('5f44af2ddb0242d36d1b198b80b90ce696282c0a411794399f2c3ef2c540b93bbe7d7713c4804a282c4e9b8e01cd9da0686e26ee3c65f9b3ca136e6e571ba19a')
b2sums=('ade921259c3630d8a9c0d2ac5b6ef3e18855cf126f29a829c4c74abf22f6573fdac028f402b84a92b53a2e3b49cf6c02ad890567afa36f426d7e0761aaf2a300')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"

}

build() {
  cd $_name-$pkgver
  CFLAGS+=" -ffat-lto-objects" cargo build --frozen --release --package $pkgname
}

check() {
  cd $_name-$pkgver
  CFLAGS+=" -ffat-lto-objects" cargo test --frozen --package $pkgname
}

package() {
  cd $_name-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
