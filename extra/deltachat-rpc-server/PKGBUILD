# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

# WARNING: this package needs to be kept in sync with the requirements of deltachat-desktop
_upstream=deltachat-core-rust
_name=core
pkgname=deltachat-rpc-server
pkgver=2.33.0
pkgrel=1
pkgdesc="A JSON-RPC 2.0 interface to DeltaChat over standard I/O"
arch=(x86_64)
url="https://github.com/deltachat/deltachat-core-rust/tree/main/deltachat-rpc-server"
_url="https://github.com/deltachat/deltachat-core-rust"
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=("$_url/archive/v$pkgver/$_upstream-$pkgver.tar.gz")
sha512sums=('348569acd3b5ff262cb8f5800398c24c922d3f52e7c855a28abf917e7179511cf9a8f8a3327e82681760b147017a0a704c34bc6839624a7ddc59c7da85cf27bc')
b2sums=('d05c30b30b8630226257f0b93d597078a81322fce8afedb03d7a172cd6eb28edf4abe68872b0475fa9638ad5188a2125513b28a53199eb2e96e916960f98453e')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"

}

build() {
  cd $_name-$pkgver
  CFLAGS+=" -ffat-lto-objects" cargo build --frozen --release --package $pkgname
}

check() {
  cd $_name-$pkgver
  CFLAGS+=" -ffat-lto-objects" cargo test --frozen --package $pkgname
}

package() {
  cd $_name-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
