# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor:  Yangtse Su<yangtsesu@gmail.com>
# Contributor:  Jekyll Wu<adaptee [at] gmail [dot] com>
# Contributor: riverscn<riverscn at gmail.com>
# Contributor: rainy<rainylau at gmail.com>
# Contributor: Lee.MaRS<leemars at gmail.com>

pkgname=ibus-libpinyin
pkgver=1.15.8
pkgrel=2
pkgdesc="Intelligent Pinyin engine based on libpinyin for IBus"
arch=('x86_64')
license=('GPL-3.0-or-later')
url="https://github.com/libpinyin/ibus-libpinyin"
depends=('ibus' 'libpinyin' 'libsoup3' 'lua' 'opencc' 'python-pyxdg')
makedepends=('boost' 'git' 'gnome-common' 'intltool')
source=("git+https://github.com/libpinyin/ibus-libpinyin.git#tag=$pkgver")
sha512sums=('df7c7001b5b1d09c82d65b926df2f93b2ffc8dacaa912e0446fef45a4320435895764bb14790ace722c2408399629f086f8eaf61d87c87f6baa32be231c40c7d')

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/$pkgname --enable-boost --enable-opencc --enable-cloud-input-mode
  make
}

check() {
  make -C $pkgname check
}

package() {
  cd $pkgname
  make NO_INDEX=true DESTDIR="$pkgdir" install
}
