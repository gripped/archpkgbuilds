# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>
pkgname=cameractrls
pkgver=0.5.15
pkgrel=2
pkgdesc="Camera controls for Linux"
arch=('any')
url="https://github.com/soyersoyer/cameractrls"
license=('MIT')
depends=('gtk4' 'libjpeg-turbo' 'python' 'python-gobject' 'sdl2' 'systemd')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('76bc517e7f61c77ce72ea6359f3e045a81e4ec4a2f2f071714aaadeee92ec2fd')

prepare() {
  cd "$pkgname-$pkgver"

  # Use Gtk 4
  desktop-file-edit --set-key=Exec --set-value=cameractrlsgtk4 pkg/hu.irl.cameractrls.desktop
}

package() {
  cd "$pkgname-$pkgver"
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -Dm755 camera{ctrls,ctrlsgtk4,ptzgame,ptzmidi,ptzspnav,view}.py -t \
    "${pkgdir}${site_packages}/CameraCtrls/"
  install -Dm644 "pkg/hu.irl.cameractrls.svg" -t "${pkgdir}${site_packages}/CameraCtrls/images/"
  install -Dm644 "pkg/hu.irl.cameractrls.metainfo.xml" -t "$pkgdir/usr/share/metainfo/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 pkg/hu.irl.cameractrls.desktop -t "$pkgdir/usr/share/applications/"

  install -d "$pkgdir"/usr/{bin,share/icons/hicolor/scalable/apps}
  ln -s "${site_packages}/CameraCtrls/cameractrls.py" "$pkgdir/usr/bin/$pkgname"
  ln -s "${site_packages}/CameraCtrls/cameractrlsgtk4.py" "$pkgdir/usr/bin/${pkgname}gtk4"
  ln -s "${site_packages}/CameraCtrls/images/hu.irl.cameractrls.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/hu.irl.cameractrls.svg"
}
