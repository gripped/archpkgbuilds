# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=diskus
pkgver=0.8.0
pkgrel=1
pkgdesc="Minimal, fast alternative to 'du -sh'"
arch=(x86_64)
url=https://github.com/sharkdp/diskus
license=('Apache-2.0 OR MIT')
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('60a42ae06d892cfa46c9e077645e276931666cb8d6712c9ebe11292b4fe9dfb0ee3eaad0de955388d6a2671e4b2f735ebd34cf2a9402956ddabd794ca12e1605')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

check() {
  cd $pkgname
  cargo test --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 doc/$pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
