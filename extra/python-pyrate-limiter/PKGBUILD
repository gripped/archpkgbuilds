# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jordan Cook <JCook83@gmail.com>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>

pkgname=python-pyrate-limiter
pkgdesc="Python Rate-Limiter using Leaky-Bucket Algorithm"
pkgver=3.7.0
pkgrel=2
arch=(any)
url=https://github.com/vutran1710/PyrateLimiter
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
)
optdepends=(
  'python-filelock: concurrent sqlite backend'
  'python-redis: redis backend'
)
_tag=b30985422b4089a50774b8999b39b248647067d5
source=(git+https://github.com/vutran1710/PyrateLimiter.git#tag=${_tag})
b2sums=('0cb809a41c38aa4fff05025fbff57860416eb7cd85d78981890f24c70a1147d00abbee08f9db1d0a703b32b9413a31c07565a7041ade7b6189b56e05baf9dd7a')

pkgver() {
  cd PyrateLimiter
  git describe --tags | sed 's/^v//'
}

build() {
  cd PyrateLimiter
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" PyrateLimiter/dist/*.whl
  install -Dm 644 PyrateLimiter/LICENSE -t "${pkgdir}"/usr/share/licenses/python-pyrate-limiter/
}

# vim: ts=2 sw=2 et:
