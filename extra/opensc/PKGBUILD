# Maintainer: Alad
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: SÃ©bastien Luttringer
# Contributor: kevku <kevku@msn.com>

pkgname=opensc
pkgver=0.26.0
pkgrel=4
pkgdesc='Tools and libraries for smart cards'
arch=('x86_64')
url='https://github.com/OpenSC/OpenSC/wiki'
license=('LGPL')
backup=('etc/opensc.conf')
makedepends=('docbook-xsl' 'git')
depends=('pcsclite' 'glib2')
options=(!emptydirs !lto)
source=("git+https://github.com/OpenSC/OpenSC.git#tag=${pkgver}")
sha512sums=('6de92cfa2b6be790c22ffa81ae8e9a04f16d1897da56343d9a59df918fc98dce650ff6d7d583239e5289ef765d0c328aaedddd5099077924a8cf3920188db15f')

build() {
  cd OpenSC

  _sheetdir=(/usr/share/xml/docbook/xsl-stylesheets-*-nons)

  ./bootstrap
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-completiondir=/usr/share/bash-completion/completions \
    --enable-man \
    --enable-doc \
    --enable-readline \
    --enable-openssl \
    --enable-pcsc \
    --enable-zlib \
    --enable-sm \
    --with-xsl-stylesheetsdir="$_sheetdir"

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package(){
  cd OpenSC

  make DESTDIR="$pkgdir" install

  install -D -m644 etc/opensc.conf "$pkgdir/etc/opensc.conf"
}

# vim:set ts=2 sw=2 et:
