# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: taotieren <admin@taotieren.com>

pkgname=linyaps-box
pkgver=2.1.0
pkgrel=1
pkgdesc='A simple OCI runtime mainly used by linyaps (linglong)'
arch=(x86_64)
url='https://github.com/OpenAtom-Linyaps/linyaps-box'
license=('LGPL-3.0-or-later')
provides=(
  linglong-box
)
depends=(
  gcc-libs
  glibc
  libcap
)
makedepends=(
  cmake
  git
  gtest
  ninja
  libseccomp
  nlohmann-json
  pkgconf
  yaml-cpp
)
source=("git+https://github.com/OpenAtom-Linyaps/linyaps-box.git#tag=$pkgver")
sha256sums=('7d30dc0fbe3069a5c6eba49bd26f93dbcd959f36791808716c8f912aa9cc06ee')

build() {
  cd linyaps-box

  cmake -Dlinyaps-box_ENABLE_SECCOMP=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -B build \
    -G Ninja
  ninja -C build
}

package() {
  cd linyaps-box
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
