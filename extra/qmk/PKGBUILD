# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Akaash Suresh <https://github.com/Curry>
# Contributor: Batuhan Baserdem <lastname dot firstname at gmail>

pkgname=qmk
pkgver=1.2.0
_qmk_udev_pkgver=0.1.2
pkgrel=1
pkgdesc="CLI tool for customizing supported mechanical keyboards."
arch=('any')
url="https://github.com/qmk/qmk_cli"
license=('MIT')
depends=('python-platformdirs' 'python-argcomplete' 'python-colorama'
         'python-milc' 'python-dotty-dict' 'python-jsonschema' 'git' 
         'avr-gcc' 'arm-none-eabi-gcc' 'avr-libc'
         'arm-none-eabi-binutils' 'arm-none-eabi-newlib' 'avr-binutils'
         'dfu-programmer' 'dfu-util' 'avrdude' 'python-hjson' 'python-pygments'
         'python-hid' 'python-pyusb' 'python-pyserial' 'python-pillow')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
optdepends=('gcc'
            'libffi'
            'libusb-compat'
            'clang: chibios-upgrader'
            'zip: some distribution script'
            'wget: some update scripts'
            'diffutils: some update scripts')
source=("https://files.pythonhosted.org/packages/source/q/qmk/qmk-${pkgver}.tar.gz"
        "qmk-udev-${_qmk_udev_pkgver}.tar.gz::https://github.com/qmk/qmk_udev/archive/refs/tags/v${_qmk_udev_pkgver}.tar.gz")
sha256sums=('164bd16c4c401b2b765ee4f017bcfa8141942ddf362961c4cba1975d8cb28a48'
            'd5f3ba2ba97f1e70aec1c49cd8054577e35dcc436b310190dbbb1aa8296ff0e1')

build() {
  make -C qmk_udev-${_qmk_udev_pkgver}
  cd "${pkgname}-${pkgver}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  make -C qmk_udev-${_qmk_udev_pkgver} DESTDIR="$pkgdir" PREFIX=/usr install
  cd "${pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" --compile-bytecode=1 dist/*.whl
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
