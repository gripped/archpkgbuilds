# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fnott
pkgver=1.7.1
pkgrel=1
pkgdesc='Keyboard driven and lightweight Wayland notification daemon'
url="https://codeberg.org/dnkl/$pkgname"
arch=(x86_64)
license=(MIT)
depends=(dbus
         fcft
         wayland)
makedepends=(meson
             tllist
             scdoc
             wayland-protocols)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('39c732f2ac57d18f24ef9112524d71090e2b68b72a892f4a44f3a77a1f067487')

build() {
	cd "$pkgname"
	arch-meson . build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
