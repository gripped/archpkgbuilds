# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-bluetooth
pkgver=8.0.2
pkgrel=1
pkgdesc='Switchboard Bluetooth Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-bluetooth
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=d2f070d5fe8367c6a6a98f4ca79027b5367f3ee1
source=(git+https://github.com/elementary/switchboard-plug-bluetooth.git#tag=${_tag})
b2sums=('c428cc61d92377451603d9e2e2e2c9631b00a537014a5f127decf8d16f6a6b305df9a53827398502b356c9281451b8bd8094311df3e2a57fa8d13f0fb6337d38')

pkgver() {
  cd switchboard-plug-bluetooth
  git describe --tags
}

build() {
  arch-meson switchboard-plug-bluetooth build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
