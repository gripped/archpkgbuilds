# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-bluetooth
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Bluetooth Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-bluetooth
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=0c3644a863ef78668a0559434b2f0810405d8085
source=(git+https://github.com/elementary/switchboard-plug-bluetooth.git#tag=${_tag})
b2sums=('35bdd1c4163d31e15dc02606375b38dc4b48056c200b4192c745d16782a0f31ff5c6f419e051c064fce6bcd37e1c2e262d8b3baa8a7bfe4802d529f05895bf2e')

pkgver() {
  cd switchboard-plug-bluetooth
  git describe --tags
}

build() {
  arch-meson switchboard-plug-bluetooth build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
