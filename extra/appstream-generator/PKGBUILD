# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.9.2
pkgrel=1
pkgdesc='A fast AppStream metadata generator'
arch=(x86_64)
url='https://github.com/ximion/appstream-generator'
license=(LGPL-3.0-only)
depends=(appstream
         gcc-libs
         gdk-pixbuf2
         glib2
         glibc
         glibd
         libarchive
         liblphobos
         lmdb
         optipng)
makedepends=(curl
             docbook-xsl
             gir-to-d
             gobject-introspection
             ldc
             meson
             mustache-d
             ninja)
source=(https://github.com/ximion/appstream-generator/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('3ff3172ab91aa0459177706d700e2a0344433e1cb9ec06463e2e007e663573a8')

build() {
  export LDFLAGS="$(echo -ne $LDFLAGS | sed -e 's/-flto=auto/-flto=full/')"
  arch-meson $pkgname-$pkgver build \
    -D download-js=false \
    -D rpmmd=false
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
