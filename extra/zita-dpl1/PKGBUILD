# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=zita-dpl1
pkgver=0.3.3
pkgrel=5
pkgdesc="A look-ahead digital peak level limiter"
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  cairo
  clthreads
  clxclient
  gcc-libs
  glibc
  jack
  libpng
  libx11
)
makedepends=(gendesk)
source=(https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2)
sha512sums=('f1c5d3fb61feadb9c272d137669a5893fd949a9d75e76633baa0bcbc9755cb1ac0fb1faacffce03e747d383ddcf36f8c22ec83dea558c0fa674d6a898bb7e35c')
b2sums=('a47bc0a5ae700604941e0fee82b79cce95744d8b777e4823d0e9640bf2b37780d222565a48c239988c0cd872ea3f50ab01230e2a599e6f0fe470bd99053c846e')

prepare() {
  # strip -march=native
  sed -e '/native/d' \
      -e 's/freetype-config\ --cflags/pkgconf\ --cflags\ freetype2/' \
      -i $pkgname-$pkgver/source/Makefile
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name zita-dpl1 \
          --categories "AudioVideo;Audio"
}

build() {
  make PREFIX=/usr -C $pkgname-$pkgver/source
}

package() {
  make PREFIX=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver/source
  # XDG desktop integration
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # docs
  install -vDm 644 $pkgname-$pkgver/{doc/*,AUTHORS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
