# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Blair Noctis <ncts@nightsail.net>

pkgname=wireman
pkgver=0.2.7
pkgrel=1
pkgdesc='A gRPC client for the terminal'
arch=('x86_64')
url="https://github.com/preiter93/wireman"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('1a2860a9e51a44d9d8551c8e851ec8ce7115056ce0230669202d4cc8a945d5a20ee10eb752469946203f606be6d47dd6136bd5f4c8c73b070705357a4ea24a1a')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
