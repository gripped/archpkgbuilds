# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Blair Noctis <ncts@nightsail.net>

pkgname=wireman
pkgver=0.2.10
pkgrel=1
pkgdesc='A gRPC client for the terminal'
arch=('x86_64')
url="https://github.com/preiter93/wireman"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('7f1e822f1eb9dad9583ac6202e15d6461bedc9c0f881cf6298ba4ccc48548951a2bfbc6d7793384144311d889751f90e83e86664129ec42c30d8439b591eccfa')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
