# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Blair Noctis <ncts@nightsail.net>

pkgname=wireman
pkgver=0.3.1
pkgrel=1
pkgdesc='A gRPC client for the terminal'
arch=('x86_64')
url="https://github.com/preiter93/wireman"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('83d078fe59d78b54322516fa1b3b4a8fa15683eb58c137871aa9f36985885d97f44202b552c41a42446d66c0db33399f446ce8e2a3357251e00bff6720eb3610')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
