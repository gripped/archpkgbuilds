# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=brush
pkgver=0.2.19
pkgrel=1
pkgdesc="Bash/POSIX-compatible shell implemented in Rust"
arch=('x86_64')
url="https://github.com/reubeno/brush"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgname-shell-v$pkgver.tar.gz")
sha256sums=('21c1e8130a1b73287b9c1fbc900911c549673e184e66b6ffae9a1b05a9ef6ba5')

prepare() {
  mv "$pkgname-$pkgname-shell-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  # https://github.com/reubeno/brush/issues/429
  cargo test --frozen --lib -- --skip "complete"
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
