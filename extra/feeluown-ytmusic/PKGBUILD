# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

_name=fuo-ytmusic
pkgname=feeluown-ytmusic
pkgver=0.4.14
pkgrel=1
pkgdesc="YouTube Music for FeelUOwn music player"
arch=('any')
url="https://github.com/feeluown/feeluown-ytmusic"
license=('GPL-3.0-or-later')
# pyqt removed from depends because it's decided by feeluown
depends=('feeluown' 'python' 'python-pydantic' 'python-cachetools' 'python-requests' 'python-ytmusicapi' 'yt-dlp')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
optdepends=('feeluown-netease: Lyric source')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-ytmusic.git#tag=v$pkgver")
sha256sums=('ef57859b1785638254253e71dd0485acb85b2eea6f66885922eea2d29dfa2842')

build() {
	cd $pkgname
	python -m build --wheel --no-isolation
}

check() {
	local pytest_options=(
		-vv
	)
	cd $pkgname
	pytest "${pytest_options[@]}"
}

package() {
	cd $pkgname
	python -m installer --destdir="$pkgdir" dist/*.whl
}
