# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

_name=fuo-ytmusic
pkgname=feeluown-ytmusic
pkgver=0.4.0
pkgrel=1
pkgdesc="YouTube Music for FeelUOwn music player"
arch=('any')
url="https://github.com/feeluown/feeluown-ytmusic"
license=('GPL-3.0-or-later')
depends=('feeluown' 'python' 'python-pydantic' 'python-cachetools' 'python-pyqt5' 'python-pytube' 'python-requests' 'python-ytmusicapi')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core' 'python-wheel')
checkdepends=('python-pytest')
optdepends=('feeluown-netease: Lyric source')
source=("git+https://github.com/feeluown/feeluown-ytmusic.git#tag=v$pkgver"
		$pkgname-fix-test_stream_url.patch::https://github.com/feeluown/feeluown-ytmusic/pull/9.patch)
sha256sums=('d093d9d95fb1ee95394e78fa978b5842fb92e1913f0c3a750b87155e495a8c65'
            '1261f7a31798e6a088ce6b58cafe19f7bd460f140e7ee0257e1c15dd237d0bce')

prepare() {
	cd $pkgname
	patch -p1 -i ../$pkgname-fix-test_stream_url.patch
}

build() {
	cd $pkgname
	python -m build --wheel --no-isolation
}

check() {
	local pytest_options=(
		-vv
		# broken with pytest >= 8
		--deselect tests/test_service.py::TestService::test_search_song
		--deselect tests/test_service.py::TestService::test_search_album
		--deselect tests/test_service.py::TestService::test_stream_url
	)
	cd $pkgname
	pytest "${pytest_options[@]}"
}

package() {
	cd $pkgname
	python -m installer --destdir="$pkgdir" dist/*.whl
}
