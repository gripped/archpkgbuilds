# Maintainer: Alexander RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=festival-voices
pkgname=('festival-us' 'festival-english')
pkgver=2.5
pkgrel=3
arch=('any')
url='http://festvox.org/festival/'
license=('LicenseRef-festival')
depends=('festival')
source=("http://festvox.org/packed/festival/$pkgver/voices/festvox_cmu_us_awb_cg.tar.gz"
	"http://festvox.org/packed/festival/$pkgver/voices/festvox_cmu_us_rms_cg.tar.gz"
	"http://festvox.org/packed/festival/$pkgver/voices/festvox_cmu_us_slt_cg.tar.gz"
	"http://festvox.org/packed/festival/$pkgver/voices/festvox_kallpc16k.tar.gz"
	"http://festvox.org/packed/festival/$pkgver/voices/festvox_rablpc16k.tar.gz"
	'LICENSE-upstream')
sha512sums=('b1a21799ac8b7980566d91613a9aa64a9df1342cca96df20e14730af127f147605d7b8150ef295ad95f2056f7ff2c69525e0858f23bb5052d56fcd1a99a3b749'
            '5496c9767090591ad9525ad4cefbacbaa47ff27476a547e29f3d59c4c8c50d281bd9764c08f40abb3b7e6f49dbff3a03b408a83aa613aa886ebdde7ed8e5196f'
            '8dc516fcd280ce5ca2915deb9cc0d7704f37815f443342007383d8708ae7419c05357ad48f460b2171ab1417ec7c3c033442d9d3f4a631df49f6f2a9826c2c28'
            '5d8a57840220c00e281842fd38f16887b7403dc487782947eae4a0b2d36f951aa957182bb36a1b4824e40288a5ccc2e635f38f5a09277a0da8099aa6bf24d892'
            'c97a19f82eca65553846b83ae355852266408de1268b4d80af50d6e212a833d98f414d1e9b85751c8f074b285f01ead93cc42208e3c0bbca330524a35e936835'
            'd3f9dc6acdf2e088a2e9be5db82e2bb67418c548c1988adcc15d4e9407f775f2269aefd601e83a2d18bf64c3e26f5035abe8f83d5ba192e9a679d274f643e52c')

package_festival-us() {
  pkgdesc='American Male/Female and Scottish English Male speaker'
  replaces=('festival-awb-arctic')

  install -d "$pkgdir/usr/share/festival/voices/"
  cp -rf festival/lib/voices/us "$pkgdir/usr/share/festival/voices/"
  install -Dm644 LICENSE-upstream "$pkgdir/usr/share/licenses/festival-us/festival"
}

package_festival-english() {
  pkgdesc='British and American English Male speaker'
  replaces=('festival-kallpc16k' 'festival-rablpc16k')

  install -d "$pkgdir/usr/share/festival/voices/"
  cp -rf festival/lib/voices/english "$pkgdir/usr/share/festival/voices/"
  install -Dm644 LICENSE-upstream "$pkgdir/usr/share/licenses/festival-english/festival"
}
