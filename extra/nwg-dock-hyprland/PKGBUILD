# Maintainer: Robin Candau <antiz@archlinux.org>
# contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=nwg-dock-hyprland
pkgver=0.2.2
pkgrel=2
pkgdesc="GTK3-based dock for Hyprland Wayland compositor"
url="https://github.com/nwg-piotr/nwg-dock-hyprland"
arch=('x86_64')
license=('MIT')
depends=('gtk3' 'gtk-layer-shell')
makedepends=('go')
optdepends=('nwg-drawer: default application launcher')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('13ac6842db449ba23cca08a9901f29089b37d7804824a7e0c5272339e3571c22')

prepare(){
        cd "${pkgname}-${pkgver}"
        mkdir -p build/
}

build() {
        cd "${pkgname}-${pkgver}"
        export CGO_CPPFLAGS="${CPPFLAGS}"
        export CGO_CFLAGS="${CFLAGS}"
        export CGO_CXXFLAGS="${CXXFLAGS}"
        export CGO_LDFLAGS="${LDFLAGS}"
        export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
        go build -o "build/${pkgname}" *.go
}

package() {
        cd "${pkgname}-${pkgver}"
        install -Dm 755 "build/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
        install -Dm 644 config/* -t "${pkgdir}/usr/share/${pkgname}/"
        install -Dm 644 images/* -t "${pkgdir}/usr/share/${pkgname}/images/"
        install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
        install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
