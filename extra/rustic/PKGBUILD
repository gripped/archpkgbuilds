# Maintainer: Caleb Maclennan <caleb@alerque.com>
# contributor: Daniele Basso <d dot bass05 at proton dot me>

pkgname=rustic
pkgver=0.8.0
pkgrel=1
pkgdesc='Fast, encrypted, deduplicated backups powered by Rust (reads and writes restic repos)'
arch=(x86_64)
url="https://github.com/rustic-rs/$pkgname"
license=(Apache-2.0 MIT)
depends=(gcc-libs # libgcc_s.so
         glibc) # libc.so libm.so
makedepends=(cargo)
replaces=(rustic-rs
          rustic-bin)
options=(!lto)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('535e89ebe1c3f29f9b69d6fc26cdb98160f9908349ace1df0003137d1593e9bc')

prepare() {
	cd "$_archive"
	# https://github.com/rustic-rs/rustic/issues/1204
	sed -i '3355s/0.7.0-dev/0.8.0/' Cargo.lock
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	env \
		CARGO_PROFILE_RELEASE_DEBUG=2 \
		CARGO_PROFILE_RELEASE_STRIP=false \
	cargo build --frozen --release --no-default-features --features webdav,tui
}

check() {
	cd "$_archive"
	cargo test --frozen --no-default-features --features webdav,tui
}

package() {
	cd "$_archive"
	local _bin="target/release/$pkgname"
	install -Dm0755 -t "$pkgdir/usr/bin/" "$_bin"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-MIT
	"$_bin" completions bash | install -Dm0644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/$pkgname"
	"$_bin" completions fish | install -Dm0644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
	"$_bin" completions zsh  | install -Dm0644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
