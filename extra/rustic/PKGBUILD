# Maintainer: Caleb Maclennan <caleb@alerque.com>
# contributor: Daniele Basso <d dot bass05 at proton dot me>

pkgname=rustic
pkgver=0.11.0
pkgrel=1
pkgdesc='Fast, encrypted, deduplicated backups powered by Rust (reads and writes restic repos)'
arch=(x86_64)
url="https://github.com/rustic-rs/$pkgname"
license=(Apache-2.0 MIT)
depends=(gcc-libs # libgcc_s.so
         glibc) # libc.so libm.so
makedepends=(cargo)
replaces=(rustic-rs
          rustic-bin)
options=(!lto)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('760566567a1302db795a9657d1723bdf0b2a7edd111f8e2d1e1780d9cdbeaff7')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
	cd "$_archive"
	export CARGO_PROFILE_RELEASE_DEBUG=2
	export CARGO_PROFILE_RELEASE_STRIP=false
	cargo build --frozen --release
}

check() {
	cd "$_archive"
	local skipped=(
		# Assumes system has a locale not generated in our chroot
		test_backup_and_check_passes
	)
	cargo test --frozen --release -- ${skipped[@]/#/--skip }
}

package() {
	cd "$_archive"
	local _bin="target/release/$pkgname"
	install -Dm0755 -t "$pkgdir/usr/bin/" "$_bin"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-MIT
	"$_bin" completions bash | install -Dm0644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/$pkgname"
	"$_bin" completions fish | install -Dm0644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
	"$_bin" completions zsh  | install -Dm0644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
