# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=soundscope
pkgver=1.4.1
pkgrel=1
pkgdesc="A TUI app for analyzing audio data such as frequencies and loudness"
arch=('x86_64')
url="https://github.com/bananaofhappiness/soundscope"
license=('MIT')
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('176601c13948eb736fc02743bff6393eb2bcbd301bd0c92d849d61125a507e65958d73c57a95d346249301dd13c0f523c78ddbe15f9f5f163a54ef8580459089')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
