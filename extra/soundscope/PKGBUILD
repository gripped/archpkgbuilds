# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=soundscope
pkgver=1.7.0
pkgrel=1
pkgdesc="A TUI app for analyzing audio data such as frequencies and loudness"
arch=('x86_64')
url="https://github.com/bananaofhappiness/soundscope"
license=('MIT')
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d372a4a1c43666dc2519b17e4604fe355ca110359165d724d1770cfc6eaf4ec971a9090854efceab014f563356e81334e1f745600ede6574abd895a644689e3d')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
