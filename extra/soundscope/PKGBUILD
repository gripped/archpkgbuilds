# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=soundscope
pkgver=1.5.0
pkgrel=1
pkgdesc="A TUI app for analyzing audio data such as frequencies and loudness"
arch=('x86_64')
url="https://github.com/bananaofhappiness/soundscope"
license=('MIT')
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c4dbfe6c6059cdf3aff7b909ba0f07d4b65256a7907596b005166d7d75492142df11657206d8452482063755ad600d8c0cb731d40881ee0b6e4414e72c83097e')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
