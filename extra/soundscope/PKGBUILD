# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=soundscope
pkgver=1.4.0
pkgrel=1
pkgdesc="A TUI app for analyzing audio data such as frequencies and loudness"
arch=('x86_64')
url="https://github.com/bananaofhappiness/soundscope"
license=('MIT')
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a8911b4fdaaf6156950836b89585b5dcdc07a9e11e044ed07066ddcbb238fd582106b501316697197a8984f7b2a2a636ccfe4fa22662aec837164c9d42abcadb')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
