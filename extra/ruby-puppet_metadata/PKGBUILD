# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='puppet_metadata'
pkgname="ruby-${_gemname}"
pkgver=4.4.0
pkgrel=2
pkgdesc='Data structures for the Puppet Metadata'
arch=('any')
url="https://github.com/voxpupuli/${_gemname}"
license=('Apache-2.0')
depends=('ruby' 'ruby-metadata-json-lint' 'ruby-semantic_puppet')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rspec' 'ruby-rspec-its')
options=('!emptydirs')
source=("${url}/archive/${pkgver}/${_gemname}-${pkgver}.tar.gz")
sha512sums=('483f4fe09ad4e539b8098aa80d20e5b6116bc2454765cf21474d964d2e1a45a6b85f403562ee8d8526a2fdd1c5cee62ad5cc36a0abafc8bec5f9deb4512feb41')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # allow latest dependencies
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"

  # drop development deps, they only cause dependency issues and aren't required
  sed --in-place '/add_development_dependency/d' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md HISTORY.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
