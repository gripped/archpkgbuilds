# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>

pkgname=aspell-nb
pkgver=0.50.1.0
_pname=$pkgname-${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc='Norwegian Bokmål dictionary for aspell'
arch=(any)
url='http://aspell.net/' # HTTPS is not available
license=(GPL-2.0-or-later)
depends=(aspell)
makedepends=(convmv)
source=("https://ftp.gnu.org/gnu/aspell/dict/nb/$_pname.tar.bz2")
b2sums=('85f007c61066f0d8e30cd55d0bc2423ee1c02bd8cbd12b5a9793666e34e70359260dd4a861b24c324981368dd0f01a43dc1d2aba192783c32c09843b2ff16f29')

build() {
  cd $_pname
  ./configure
  make
}

package() {
  make -C $_pname DESTDIR="$pkgdir" install
  convmv --notest -f iso-8859-1 -t UTF-8 "$pkgdir"/usr/lib/aspell-*/bokm*l.alias
}
