# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=identity
pkgver=0.7.0
pkgrel=1
pkgdesc='Compare multiple versions of an image or video'
arch=(x86_64)
url='https://apps.gnome.org/Identity/'
license=(GPL-3.0-or-later)
depends=(
  bubblewrap
  dav1d
  dconf
  fontconfig
  gcc-libs
  glib2
  glibc
  glycin
  graphene
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libseccomp
  libwebp
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
options=(!lto)
source=("git+https://gitlab.gnome.org/YaLTeR/identity.git#tag=v$pkgver")
b2sums=(d67453bbe52e2044f8ce7dec4cd028a8d0ebc632844c5642ad78fa6c6f6fb3a03973b029ac16df6cb8884e68fb7b6da42679393cf92dad377c76bbad4e8521b0)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
