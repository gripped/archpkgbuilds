# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=identity
pkgver=25.10.1
pkgrel=1
pkgdesc='Compare multiple versions of an image or video'
arch=(x86_64)
url='https://apps.gnome.org/Identity/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dav1d
  dconf
  gcc-libs
  glib2
  glibc
  glycin
  glycin-gtk4
  graphene
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libwebp
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
options=(!lto)
source=("git+https://gitlab.gnome.org/YaLTeR/identity.git#tag=v$pkgver")
b2sums=(c7b234f047e86e28d9508dd35afc32f2d89a03e50e61998cd95d7f22c938dd50492ef46d93076ced5d2120c443b4a5dd9ba364b2324d060a3aebbea99dd7e922)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
