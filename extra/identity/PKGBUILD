# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=identity
pkgver=25.03
pkgrel=3
pkgdesc='Compare multiple versions of an image or video'
arch=(x86_64)
url='https://apps.gnome.org/Identity/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  bubblewrap
  dav1d
  dconf
  fontconfig
  gcc-libs
  glib2
  glibc
  glycin1
  graphene
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libseccomp
  libwebp
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
options=(!lto)
source=("git+https://gitlab.gnome.org/YaLTeR/identity.git#tag=v$pkgver")
b2sums=(8c6c009696eb284756d073d4ca2aaabef5c972bcc90d85fcec1325ceb099a8fa749f17a293672de9b31606b4ee177332a292a46f7ffd8f4b36699aea95931ed3)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
