# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemloaders
pkgver=1.3.2
pkgrel=4
pkgdesc="Library to populate items using XPath and CSS with a convenient API"
url="https://github.com/scrapy/itemloaders"
license=('BSD-3-Clause')
arch=('any')
depends=('python-parsel' 'python-jmespath' 'python-itemadapter')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/scrapy/itemloaders.git#tag=v$pkgver")
sha512sums=('9485efd85d1df1581016b984775861fbf51e6e6b016ee6f25590a47318da676d13d89e140c0162b2bfb127355f7945ad6e41c79a2f21134f90e960d887db3468')

build() {
  cd itemloaders
  python -m build --wheel --no-isolation
}

check() {
  cd itemloaders
  python -m pytest
}

package() {
  cd itemloaders
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
