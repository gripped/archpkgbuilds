# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemloaders
pkgver=1.1.0
pkgrel=2
pkgdesc="Library to populate items using XPath and CSS with a convenient API"
url="https://github.com/scrapy/itemloaders"
license=('BSD')
arch=('any')
depends=('python-w3lib' 'python-parsel' 'python-jmespath' 'python-itemadapter')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/scrapy/itemloaders/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4a52e75405409124a3c1f7e7079fedf36e34a76877b04d754006743a2f403602de9b463e1f338bead3a68a0c1cf32f2a50b4cbe4cc57a10539ec529a93b81607')

build() {
  cd itemloaders-$pkgver
  python setup.py build
}

check() {
  cd itemloaders-$pkgver
  python -m pytest
}

package() {
  cd itemloaders-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
