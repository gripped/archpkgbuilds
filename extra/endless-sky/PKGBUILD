# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Zhao <alcasa dot mz at gmail dot com>
# Contributor: Leonard KÃ¶nig <leonard.r.koenig at googlemail dot com>

pkgname=endless-sky
pkgver=0.10.12
pkgrel=1
pkgdesc='A sandbox-style space exploration and combat game'
arch=('x86_64')
url='https://endless-sky.github.io/'
# licenses obtained via following script:
# grep "^License:" copyright | sed -e "s/License: //" -e "s/ /-/" | sort | uniq
license=(
  'GPL-3.0-only'
  'CC-BY-3.0'
  'CC-BY-4.0'
  'CC-BY-NC-3.0'
  'CC-BY-SA-3.0'
  'CC-BY-SA-4.0'
  'LicenseRef-public-domain'
)
depends=(
  'glibc'
  'gcc-libs'
  'openal'
  'libpng'
  'glew'
  'hicolor-icon-theme'
  'libjpeg-turbo'
  'sdl2'
  'libmad'
  'util-linux-libs'
  'libglvnd'
)
makedepends=('git' 'scons')
optdepends=(
  'endless-sky-high-dpi: high resolution graphics assets'
)
source=(
  "$pkgname::git+https://github.com/endless-sky/endless-sky#tag=v$pkgver"
  'fix-binary-install.patch'
)
sha512sums=('48761f4e1e6b5db7fe7887338edf4f79634a0783dc1258a668de16f86bdbad777066fb3abfb999a8ee88809a218174b7b46382306ebaa5d382420d662948adde'
            '974abf4bf60b7db9917c88e540c9b76b93f41f1214c3fad4894ab1a1162c1c5e6f48ad310ae3e7fc44f984e46e9a5d50cc1508ba7a9a6a74934646a8f15aa5c6')
b2sums=('57e78b149fae88836199532ac3415e176e8ba156ec9024280e1cbf4c4b5225e5f6c80089100771d3106ca14290635f0cff18a2483378b27fda99ed84ff8cc3ac'
        'c32e4962ab7ceff081df54a7a1cf3488f57b87aef4533a18b0a8ecafa7a7bea29babeaec4a44767124716c8194d5088444fe17dc238ea05bb22c40e9d591700b')

prepare() {
  cd "$pkgname"

  # binary is installed to /usr/games instead of /usr/bin
  patch -p1 -i "$srcdir/fix-binary-install.patch"
}

build() {
  cd "$pkgname"

  scons -j "$(nproc)"
}

package() {
  cd "$pkgname"

  scons \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    install

  # license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" copyright
}
