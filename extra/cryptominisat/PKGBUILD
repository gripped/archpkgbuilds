# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=cryptominisat
pkgver=5.12.1
pkgrel=2
_cadiback=ea65a9442fc2604ee5f4ffd0f0fdd0bf481d5b42
_cadical=f13d74439a5b5c963ac5b02d05ce93a8098018b8
pkgdesc='A modern, multi-threaded, feature-rich, simplifying SAT solver'
arch=(x86_64)
url='https://github.com/msoos/cryptominisat/'
license=(MIT)
depends=(gcc-libs
         glibc
         gmp
         zlib)
makedepends=(cmake
             git
             help2man
             python-build
             python-installer
             python-setuptools
             python-tomli
             python-wheel
             vim)
optdepends=('python: python module')
source=(git+https://github.com/msoos/cryptominisat#tag=$pkgver
        git+https://github.com/meelgroup/cadiback#commit=$_cadiback
        git+https://github.com/meelgroup/cadical.git#commit=$_cadical
        python-system-libs.patch
        link-private.patch
        python-3.13.patch)
sha256sums=('684c306c11e6e102667b9e39714f61bc5c0b0dbcb541f6c168369ac6af4d9a45'
            'e7e96d6e91bba3c9d28c798545f25236f107aec97780ee0c459352606c7c3a0c'
            '88001b8122c8629e5026aa13753a47f0ed556e9e05a9a6c6b81759a3aac2550b'
            '0fb932fbf83c351568f54fc238827709e6cc2646d124af751050cfde0c255254'
            'a5006f49e8adf1474725d2a3e4205cdd65beb2f100f5538b2f89e14de0613e0f'
            '369e1e31936c0fce8960eb9130a03ec2550f5f379af2a31164c2b4234503e81c')

prepare() {
  cd $pkgname
  sed -e 's|, \"pathlib\"||' -i pyproject.toml
  patch -p1 -i ../python-system-libs.patch # Build python module against shared library
  patch -p1 -i ../python-3.13.patch # Fix tests with python 3.13
  sed -e '/(TestDump)/d' -i python/tests/test_pycryptosat.py # Remove broken tests
  sed -e 's|/$COMPILE/|/${COMPILE//\\//\\\\/}/|' -i ../cadiback/configure # Honor system build flags
  patch -p1 -i ../link-private.patch # Link cadiback and cadical privately
}

build() {
  (
   cd cadical
   ./configure -static
   make -C build libcadical.a
  )
  (
   cd cadiback
   CXX="g++ $CXXFLAGS" ./configure
   make libcadiback.a
  )
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build

  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  PYTHONPATH="$PWD/"`echo build/lib.*` \
  LD_LIBRARY_PATH="$srcdir/build/lib" \
  python python/tests/test_pycryptosat.py
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
# Remove stray source files
  rm -r "$pkgdir"/usr/lib/python*/site-packages/{msvc,oracle,mpicosat}
}
