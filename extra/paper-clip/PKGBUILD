# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=paper-clip
pkgver=5.5.1
pkgrel=5
pkgdesc='PDF document metadata editor'
arch=(x86_64)
url='https://apps.gnome.org/PdfMetadataEditor/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  exempi
  gdk-pixbuf2
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  poppler-glib
)
makedepends=(
  appstream
  git
  meson
  vala
)
source=(
  "git+https://github.com/Diego-Ivan/Paper-Clip.git#tag=v$pkgver"
  paper-clip-metainfo.patch
  paper-clip-thumbnail-pixbuf.patch
  paper-clip-tmp-file.patch
  paper-clip-new-window.patch
  paper-clip-dbus-activatable.patch
)
b2sums=(
  3232196b481d7a1364af55f107e24ac322c068efb7723a0d0463525b887ce060d6057c305a03783528e08eee4de440b15a688c94b9363443bef199a9abfeafec
  c4ed3f355cd4e636bab32e1977fb9544ece9f042add66c80880226d458f19c403c191a720cebde0d0d41304e0de6653e3365160e640ce1cd7d6dfc4ea304adb0
  e0d1ff1f607b2308e54bb6fca9e218e056dec5f28390a282196b0b2e8eed2be41c3a58b4f8e29514be413d248f5002c043d93f06d3b5c1a234fa41b46f38af48
  945cba12f6a7bce77c5dd914258f8b5fdc042dfe21ecc94793866b161b75abf83955cfe6ca92bdf704efaeca6b65c2ecba316275ea7e705d9380a9e72cfd849e
  611721eec0df306580ac1233230a42d3e71a20142e83d69d88000acc90d05a764d834b88b6301854ea7b4bdb87ffcf32bc7a5fe4a6219cf2f67c36768b9cbb7c
  cae393e38bd26e08ada75b54af8ffb2d7483a6853e9c338417760fc75071bcf5aa10b62827cc89b9d4798220977794f421c6be1a2ea780367e89f02bd1a09598
)

prepare() {
  cd Paper-Clip

  # Fix build
  git cherry-pick -n 33a64d825b87c842b9ecc243297c7a0248db7180

  # Fix drop overlay
  git cherry-pick -n bcd4cce79b7bc61a1c35661e23a8649b44fa9ea3
  git cherry-pick -n cefb4359ebb95f648847b5379a6d84c344a272e9

  # https://github.com/Diego-Ivan/Paper-Clip/pull/100
  git apply -3 ../paper-clip-metainfo.patch

  # https://github.com/Diego-Ivan/Paper-Clip/pull/107
  git apply -3 ../paper-clip-thumbnail-pixbuf.patch

  # https://github.com/Diego-Ivan/Paper-Clip/pull/108
  git cherry-pick -n 93e1c00bca9078fa4b21e42a4560011cce768142
  git apply -3 ../paper-clip-tmp-file.patch

  # https://github.com/Diego-Ivan/Paper-Clip/pull/109
  git apply -3 ../paper-clip-new-window.patch

  # https://github.com/Diego-Ivan/Paper-Clip/pull/110
  git apply -3 ../paper-clip-dbus-activatable.patch
}

build() {
  arch-meson Paper-Clip build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
