# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kdiff3
pkgver=1.12.1
pkgrel=2
pkgdesc='A file comparator/merge tool'
arch=(x86_64)
url='https://apps.kde.org/kdiff3/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         icu
         kconfig
         kcoreaddons
         kconfigwidgets
         kcrash
         ki18n
         kio
         kwidgetsaddons
         kxmlgui
         qt6-base)
makedepends=(boost
             extra-cmake-modules
             kdoctools)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('17234ed446d41d2c59026b7937915dbe368e56f5999ae435dab756966166334b'
            'SKIP')
validpgpkeys=(EE56D710885D36FB1E4D21F3F442B36D614B06BC) # Michael Reeves <reeves.87@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
