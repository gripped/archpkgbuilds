# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=arch-wiki-lite
pkgver=20250402
pkgrel=2
pkgdesc='Arch Wiki without HTML. 1/9 as big, easily searched & viewable on console'
arch=('any')
url='https://gitlab.archlinux.org/grawlinson/arch-wiki-lite'
license=(GPL-2.0-or-later GFDL-1.3-or-later)
depends=('bash' 'gzip')
makedepends=(
  'git'
  'python'
  "arch-wiki-docs=$pkgver"
)
optdepends=(
  'dialog: for interactive menu'
  'arch-wiki-docs: for wiki-search-html'
  'xdg-utils: for wiki-search-html'
)
_commit='3b8b9c929a529a95ab36281f6c602d75d9a11318'
source=("$pkgname::git+$url.git#commit=$_commit")
sha512sums=('ca447e0a5a1924ad9c9bd1e3cd12dc761f7213b883e137d5e9f2c809af32168e9d7ec61124e8c850b5bc469f6127e6df8ed567fa42f39d5ab7ea8e76172da9c9')
b2sums=('749766228ecd70b686780a090bf3720949cd0c816fd7c2a259de4e4411583c327c206ba04f209ee36d4ad7fccba603f7cebf7c565b47ebea8927351ed91bc902')

build() {
  cd "$pkgname"

  LC_ALL=en_US.UTF-8 python3 wiki_lite.py
}

package() {
  cd "$pkgname"

  # cli scripts
  install -vDm755 -t "$pkgdir/usr/bin" wiki-search{,-html}

  # assets
  install -vd "$pkgdir/usr/share/doc/arch-wiki/text"
  cp -vr wiki/* "$pkgdir/usr/share/doc/arch-wiki/text"

  # dialog
  install -vDm644 -t "$pkgdir/etc/dialog.d" wiki-search.dialog.rc
}

