# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=arch-wiki-lite
pkgver=20260201
pkgrel=1
pkgdesc='Arch Wiki without HTML. 1/9 as big, easily searched & viewable on console'
arch=('any')
url='https://gitlab.archlinux.org/grawlinson/arch-wiki-lite'
license=(GPL-2.0-or-later GFDL-1.3-or-later)
depends=('bash' 'gzip')
makedepends=(
  'git'
  'python'
  "arch-wiki-docs=$pkgver"
)
optdepends=(
  'dialog: for interactive menu'
  'arch-wiki-docs: for wiki-search-html'
  'xdg-utils: for wiki-search-html'
  'less: pager to view content'
)
_commit='b87bada8c48378c5c48d7e10f9d54a6d2f01867d'
source=("$pkgname::git+$url.git#commit=$_commit")
sha512sums=('36c9bcdc66700e172c9cb7b40353f7615e6b9786327edbe000e61c029d9e4b9f960a94e76d07723a5d7208f1bdae6c6a563f2a2442091dda3734cd40ef7b9dec')
b2sums=('6a145d401e6f758483ac3f5634fedac93d17076983f724c5c321c21716d8f8ed6de65877da495c609a31027b31ca52d5541c775b18c213aa730a7b7ad66312c8')

build() {
  cd "$pkgname"

  LC_ALL=C.UTF-8 python3 wiki_lite.py
}

package() {
  cd "$pkgname"

  # cli scripts
  install -vDm755 -t "$pkgdir/usr/bin" wiki-search{,-html}

  # assets
  install -vd "$pkgdir/usr/share/doc/arch-wiki/text"
  cp -vr wiki/* "$pkgdir/usr/share/doc/arch-wiki/text"

  # dialog
  install -vDm644 -t "$pkgdir/etc/dialog.d" wiki-search.dialog.rc
}
