# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=drm-info
pkgver=2.9.0
pkgrel=1
pkgdesc='Small utility to dump info about DRM devices'
url='https://gitlab.freedesktop.org/emersion/drm_info'
arch=('x86_64')
license=('MIT')
depends=(
  'glibc'
  'json-c' 'libjson-c.so'
  'libdisplay-info' 'libdisplay-info.so'
  'libdrm'
  'pciutils'
)
makedepends=(
  'meson'
  'scdoc'
)
source=(https://gitlab.freedesktop.org/emersion/drm_info/-/archive/v${pkgver}/drm_info-v${pkgver}.tar.gz)
sha256sums=('b1000e95eb4f57cfe7819c5b8afd880ac07dde25d9c3839ec52e0aeadeb83234')
b2sums=('c5045a56ed9fc3935e24e47b31b3e3c4d9c36ba71f26536236a30d2a13207be34665b3edf616f895e4444d3aa4f7a05585d7c59f247113dc1f53a00ed7b10a77')

build() {
  cd "drm_info-v${pkgver}"
  arch-meson build
  ninja -C build
}

package() {
  cd "drm_info-v${pkgver}"
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

# vim: ts=2 sw=2 et:
