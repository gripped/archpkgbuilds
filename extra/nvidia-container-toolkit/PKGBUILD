# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Kien Dang <mail at kien dot ai>
# Contributor: Julie Shapiro <jshapiro at nvidia dot com>

pkgname=nvidia-container-toolkit
pkgver=1.18.0
pkgrel=1
pkgdesc="NVIDIA container toolkit"
arch=(x86_64)
url="https://github.com/NVIDIA/nvidia-container-toolkit"
license=(Apache-2.0)
depends=(glibc libnvidia-container=$pkgver)
makedepends=(go)
backup=('etc/nvidia-container-runtime/config.toml')
# we cannot use LTO as otherwise we do not get reproducible package with full RELRO
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        $pkgname-$pkgver-fix-generate-tests.patch::https://patch-diff.githubusercontent.com/raw/NVIDIA/nvidia-container-toolkit/pull/1416.patch
        nvidia-ctk-cdi
        nvidia-ctk-cdi.hook)
b2sums=('0ac6d294b80ea6cffd6d9fc4794e0b4048da425de7d115de645afb3a585e4862e42e2bf092f2333312ea2e117db1b0d488d51aab986d96c52e008dcc423e6b02'
        'eab7b9c4b0cee1123165ebe5b028748ef88606d17d8b5708f63ff1f8632768995e231b1912c6082074070c32a415a3a7690fb6c29d9acb6de97f73e37c1818c7'
        '196ba959e25b7bc9a6087b192a09196c6c46e4a2a0a6dbf1de158c06dc852b822235fb6193f10311c89d06bc722035fd807cab3afa560bf49e6181a9e14b6bd6'
        'c0a4febd599e56252f8fa60e23ce23349cfe2867960007c0149f6fa32525a9784cf6b802416cedec175cd100deff0d819a21b66249a4bea55887a258fd983271')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -p build

  # Fix generate tests when cdi hook exists in path
  # https://github.com/NVIDIA/nvidia-container-toolkit/pull/1416
  patch -p1 < ../$pkgname-$pkgver-fix-generate-tests.patch
}

build() {
  cd "$pkgname-$pkgver"

  # set GOPATH so makepkg puts source files into the debug package
  export GOPATH="$srcdir"

  # FIXME: nvml requires lazy binding (-Wl,-z,lazy) which prevents FULL RELRO: https://github.com/NVIDIA/nvidia-container-toolkit/issues/49
  go build -v \
    -buildmode=pie \
    -mod=vendor \
    -modcacherw \
    -ldflags "-compressdwarf=false -linkmode external -extldflags \"$LDFLAGS -Wl,-z,lazy\" -X github.com/NVIDIA/$pkgname/internal/info.version=$pkgver" \
    -o build ./...
}

check() {
  cd "$pkgname-$pkgver"
  PATH="$PATH:$PWD/build" go test -v ./...
}

package() {
  cd "$pkgname-$pkgver"

  # install binaries
  install -vDm 755 build/nvidia-{ctk,container-runtime{,.cdi,.legacy,-hook},cdi-hook} -t "$pkgdir/usr/bin/"
  ln -s nvidia-container-runtime-hook "$pkgdir/usr/bin/nvidia-container-toolkit"

  # install pacman hook
  install -vDm 644 ../nvidia-ctk-cdi.hook -t "$pkgdir/usr/share/libalpm/hooks/"
  install -vDm 755 ../nvidia-ctk-cdi -t "$pkgdir/usr/share/libalpm/scripts/"

  # install the license
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # generate the default config
  "$pkgdir"/usr/bin/nvidia-ctk --quiet config --config-file="$pkgdir"/etc/nvidia-container-runtime/config.toml --in-place
}
