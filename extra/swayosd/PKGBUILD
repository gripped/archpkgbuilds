# Maintainer: Giovanni Harting <anonfunc@archlinux.org>

pkgname=swayosd
_name=SwayOSD
pkgver=0.2.1
pkgrel=2
pkgdesc='A GTK based on screen display for keyboard shortcuts like caps-lock and volume'
arch=(x86_64)
url='https://github.com/ErikReider/SwayOSD'
license=(GPL-3.0-only)
depends=(glibc gtk4 gtk4-layer-shell libinput gdk-pixbuf2 pango libevdev libpulse systemd-libs glib2 gcc-libs cairo dbus)
makedepends=(cargo meson ninja sassc)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('e90762cf45c8208b9fc50988a466507009220652da778bfe234b09f0fd825583d933074dc7e46dd0a6329ac6e4be8726a3e3d2da1adf786fb1e21dde90838b9d')

prepare() {
  cd $_name-$pkgver

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  arch-meson --buildtype=release build
	ninja -C build
}

package() {
  cd $_name-$pkgver
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
