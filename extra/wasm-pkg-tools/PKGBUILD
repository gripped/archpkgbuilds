# Maintainer: Frederik Schwan <freswa at archlinux.org>

pkgname=wasm-pkg-tools
pkgver=0.15.0
pkgrel=1
pkgdesc="Tools to package up Wasm Components"
url="https://github.com/bytecodealliance/wasm-pkg-tools"
arch=('x86_64')
license=('MIT')
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(
  git
)
options=(!lto)
source=("git+https://github.com/bytecodealliance/wasm-pkg-tools.git#tag=v${pkgver}")
sha256sums=('f088deb1ed835f3deee36ae7bd67e6d92edf5afcef27d19468278e318505b496')
b2sums=('1551bb2242096bedef7169b70ab891adfebcf45f0e0fce6d433e804bf491cc37f137d61cac227afa96f756fbddcaa1ef5998adbf17b1076ffe70fb2f417703bf')

prepare() {
  cd ${pkgname}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}
  cargo build --release --locked
}

package() {
  cd ${pkgname}
  install -Dm755 target/release/wkg -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
