# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=3.9.0
pkgrel=2
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('alsa-lib' 'glibc' 'gcc-libs')
makedepends=('git' 'rust')
optdepends=('cargo: for use with Rust'
            'clang: for use with C++ via clang'
            'gcc: for use with C++ via gcc'
            'eslint: for use with JavaScript'
            'python: for use with Python via unittest'
            'python-pytest: for use with Python via pytest')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('054fbd080188286b10d004effaa7b33ee269e2e56bad9672170ca80c9102859363563246a87b96aa73cb837472e8f36a0f6766198806a99e6f3058679da3be7b')
b2sums=('c5d73be035435bb138a1927343025956dd4975e0e0eac3dd6f11b2fa8aa6be425ae19871aa6015f18da8bed0608fefa498a53246e154d6c3fd03b4e107a8153e')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"
  depends+=(libasound.so)

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
