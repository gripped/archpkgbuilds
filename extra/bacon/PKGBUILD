# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=3.4.0
pkgrel=2
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
optdepends=('cargo: for use with Rust'
            'eslint: for use with JavaScript'
            'python: for use with Python via unittest'
            'python-pytest: for use with Python via pytest')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('ebc8b48da81bb9c53e7ad1af9dfb2e1cddfb7180061970e576fd792ce13d5ec0d4504e9f9e6b88bb681148a6e968a0121057f354b6f96972a5a2abfd7e2b20a2')
b2sums=('d0319719594089f0f44cbab2216bc6526bb90fd28b02821f347984c2dd2c6d1862b53649f3e9cf29eb2bba212e30e021cc355a67d1aaec6e447ea0feb55558d0')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
