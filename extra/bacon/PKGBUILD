# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=3.3.0
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('a38cf7830e3a48ceef55211c0f814f5b882771be955bff29be4b07ca39077d612d311714ca121dc7faf685c2dcb4cd80e9e4b2f275ad131ae948a416d35d72e2')
b2sums=('c3d104c9f384346588c229f1cf2278c2b66c23b215c979f3d0e3c828240bdf20dc2ac7b15850e9ba6edc6b7f9951b66e2643f81380edc53827734878550adaac')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
