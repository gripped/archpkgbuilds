# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=2.21.0
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('639f1d658124c6ddf3e29e43e45679ed4d235312357418f6d98cb40059b89d841d8218f002683861ec9ecd1171c2367308ba76a1ca2e229516c0c8974eaf938d')
b2sums=('6a440ac324976433925bd5b1964f863c567366e467f0bcd23f2cfffef98d7142e8e4455f95656e83960d1312b552dbc9e0e2737e3d1232cdfa811e13e2b99a02')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
