# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>
# Contributor: JisuWoniu <jswn@jswn9945.xyz>

pkgname=bacon
pkgver=3.20.1
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('alsa-lib' 'glibc' 'gcc-libs')
makedepends=('git' 'rust')
optdepends=('bash-completion: enable bash completions'
            'cargo: for use with Rust'
            'clang: for use with C++ via clang'
            'gcc: for use with C++ via gcc'
            'eslint: for use with JavaScript'
            'python: for use with Python via unittest'
            'python-pytest: for use with Python via pytest')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('237a6aa5f4f2f00604cf225518e58984417577d430de507fc1a9dcb9aeb3e2b781d6ceb527af22bd0332425df32fb1a6c4f032d20dcd4d4e66c688bb20498cbd')
b2sums=('5e79e9778ad91e26ada585b0d2fd27ef268f09e8720e6cfaf3c71d21df1e380804047b385e024f29cdf7ef656173eae011ad9de987429c71216b0bde9fbd6a94')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"
  depends+=(libasound.so)

  local _binary="target/release/$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "$_binary"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # completions
  install -vDm644 <(COMPLETE=bash   exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 <(COMPLETE=zsh    exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -vDm644 <(COMPLETE=fish   exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -vDm644 <(COMPLETE=elvish exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/elvish/lib/$pkgname.elv"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
