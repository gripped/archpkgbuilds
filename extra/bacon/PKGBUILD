# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=3.1.1
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('84d65d36f5adada5b88534ae8988ebb5d9b4766eeb3c709a28afa3122f58d0f28669532dc9f620ade93ec7fbed7802d8c07128a20be09195f3b796e30667692d')
b2sums=('26b86eeb786029b841ab8dd570896fe7201cd3cb0b7ee3c0f50af7e5c9deb3871bea394a862dd9f2ad7b7f8ad52f11c58107d1fa6494611ca1bebf1c593650b5')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
