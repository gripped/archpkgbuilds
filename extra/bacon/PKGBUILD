# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>
# Contributor: JisuWoniu <jswn@jswn9945.xyz>

pkgname=bacon
pkgver=3.17.0
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('alsa-lib' 'glibc' 'gcc-libs')
makedepends=('git' 'rust')
optdepends=('bash-completion: enable bash completions'
            'cargo: for use with Rust'
            'clang: for use with C++ via clang'
            'gcc: for use with C++ via gcc'
            'eslint: for use with JavaScript'
            'python: for use with Python via unittest'
            'python-pytest: for use with Python via pytest')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('a854c0e93d92e53728ef1bb1c2c1a6f802ecddcd2843850a31669b728ab4107a9091d0ad25821e22ec9b5c9bd7b94e54d8a8e32c43d91a11488e6a43593148d5')
b2sums=('3336eabf62e71a7abc7ecb89bee3ea07ed9c392a45955475336f6c4bc311e6663a56b0d475d94bc83158516cf013960ed1e05baf9db3ba672caa314cb1275b71')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"
  depends+=(libasound.so)

  local _binary="target/release/$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "$_binary"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # completions
  install -vDm644 <(COMPLETE=bash   exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 <(COMPLETE=zsh    exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -vDm644 <(COMPLETE=fish   exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -vDm644 <(COMPLETE=elvish exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/elvish/lib/$pkgname.elv"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
