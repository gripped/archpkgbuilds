# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>
# Contributor: JisuWoniu <jswn@jswn9945.xyz>

pkgname=bacon
pkgver=3.20.3
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('alsa-lib' 'glibc' 'gcc-libs')
makedepends=('git' 'rust')
optdepends=('bash-completion: enable bash completions'
            'cargo: for use with Rust'
            'clang: for use with C++ via clang'
            'gcc: for use with C++ via gcc'
            'eslint: for use with JavaScript'
            'python: for use with Python via unittest'
            'python-pytest: for use with Python via pytest')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('e42925b930d6a566f0b1d374b45ea79a8180056fcb2bfeb92f4f6765bcdf773bb1768c90c4181aa73841c8262fe15304f4571e6c37a23d4bcf442d85e596b8a4')
b2sums=('2b3303d7a29de58efa716efd4d59411e7af247e408c4e84ca2fe80a3f572a8aac8ac23f278ef3aee60e9d18cc8824259af70f1b8aeb52397c0d1c1dff6286ae1')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"
  depends+=(libasound.so)

  local _binary="target/release/$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "$_binary"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # completions
  install -vDm644 <(COMPLETE=bash   exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 <(COMPLETE=zsh    exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -vDm644 <(COMPLETE=fish   exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -vDm644 <(COMPLETE=elvish exec -a "$pkgname" "$_binary") "$pkgdir/usr/share/elvish/lib/$pkgname.elv"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
