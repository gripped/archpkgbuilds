# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=3.5.0
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
optdepends=('cargo: for use with Rust'
            'eslint: for use with JavaScript'
            'python: for use with Python via unittest'
            'python-pytest: for use with Python via pytest')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('a382eef8eca1cccc5a1e048678a787130ecd0148e4fefa5fbf337aed68e577f0e9855535c0152ca6bd3784897fbdb20ea9e3336df49e65f1fb169a8025f33095')
b2sums=('44bb71198183f70f17f9e453bcb296067c260fed7db44c2d6b8436643190b4c0efbb1d771394b0abea958b8b0a54c02c7ddd1592fea4e47cc0ba88f9a8003732')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
