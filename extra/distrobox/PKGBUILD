# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: robertfoster

pkgname=distrobox
pkgver=1.6.0.1
pkgrel=2
pkgdesc="Use any linux distribution inside your terminal."
arch=('any')
url="https://distrobox.it"
license=('GPL-3.0-only')
depends=('bash')
optdepends=('docker: Docker support'
	    'podman: Podman support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/89luca89/distrobox/archive/${pkgver}.tar.gz"
	"fix-sudo-root-passwd.patch::https://github.com/89luca89/distrobox/commit/3b1e312f884cad2185c3b57dcde470a5d7914c61.patch")
sha256sums=('d6b1330b56f6a1bf844c26a27d87f39efd8ae088ed3063f6513d48cf9c18f57e'
            '9bc5c67be3a0a1adf70dc27fd5d19ba00ecdcfc44fbeaa36b153c0b7dfe23b88')

prepare() {
	cd "${pkgname}-${pkgver}"
	# Temporary patch to fix https://github.com/89luca89/distrobox/issues/1092
	patch -Np1 <${srcdir}/fix-sudo-root-passwd.patch
}

package() {
	cd "${pkgname}-${pkgver}"
	./install -P "${pkgdir}/usr"
	find docs -name "*.md" -exec install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" {} +
}
