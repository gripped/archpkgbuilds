# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=pantry
pkgname=haskell-pantry
pkgver=0.8.2
pkgrel=2
pkgdesc="Content addressable Haskell package management"
url="https://github.com/commercialhaskell/pantry"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-casa-client'
         'haskell-casa-types' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-crypton'
         'haskell-crypton-conduit' 'haskell-digest' 'haskell-filelock' 'haskell-generic-deriving'
         'haskell-hackage-security' 'haskell-hpack' 'haskell-http-client' 'haskell-http-client-tls'
         'haskell-http-conduit' 'haskell-http-download' 'haskell-http-types' 'haskell-memory'
         'haskell-mono-traversable' 'haskell-network-uri' 'haskell-path' 'haskell-path-io'
         'haskell-persistent' 'haskell-persistent-sqlite' 'haskell-primitive' 'haskell-resourcet'
         'haskell-rio' 'haskell-rio-orphans' 'haskell-rio-prettyprint' 'haskell-tar-conduit'
         'haskell-text-metrics' 'haskell-unix-compat' 'haskell-unliftio'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-yaml' 'haskell-zip-archive')
makedepends=('ghc' 'uusi' 'haskell-hedgehog' 'haskell-hspec' 'haskell-quickcheck'
             'haskell-raw-strings-qq')
checkdepends=('git' 'mercurial')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('82cfafd335cbd72bcf3347cacda5d96853b7e8d9b81027f5e0ccb4742e8929a4f93d7ca35e3bd3764d89e717098d2427cd7aa8eb7b14c7166dd92b819316131a')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
    uusi -d persistent-template -r cryptonite:crypton -r cryptonite-conduit:crypton-conduit $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # Key signer updates not synced here? TODO
    LANG=en_US.UTF-8 runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
