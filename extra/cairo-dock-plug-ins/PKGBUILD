# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: zhuqin <zhuqin83@gmail.com>
# Contributor: tri1976 <trile7@gmail.com>
# Contributor: snoopy33 <snoopy33@no-log.org>

pkgname=cairo-dock-plug-ins
pkgver=3.5.2
pkgrel=2
pkgdesc='Plugins for Cairo-Dock'
arch=('x86_64')
url='https://glx-dock.org'
license=('GPL')
depends=('cairo-dock')
makedepends=('alsa-lib' 'cmake' 'fftw' 'gnome-menus'
             'gvfs' 'libetpan' 'libexif' 'libical' 'libpulse'
             'libxklavier' 'lm_sensors' 'python' 'ruby' 'upower'
             'vala' 'vte3' 'zeitgeist' 'python-setuptools')
optdepends=('alsa-lib: Sound Control, Sound Effects applets'
            'fftw: Impulse applet'
            'gnome-menus: Applications Menu applet'
            'gvfs: GVFS integration'
            'libetpan: Mail applet'
            'libexif: Slider applet'
            'libical: Clock applet'
            'libpulse: Impulse applet'
            'libxklavier: Keyboard Indicator applet'
            'lm_sensors: System Monitor applet'
            'python: Python 3 API'
            'ruby: Ruby API'
            'upower: Power Manager applet'
            'vte3: Terminal applet'
            'wireless_tools: Wifi applet'
            'zeitgeist: Recent Events applet')
replaces=('cairo-dock-plugins')
source=("cairo-dock-plug-ins-${pkgver}.tar.gz::https://github.com/Cairo-Dock/cairo-dock-plug-ins/archive/${pkgver}.tar.gz")
sha256sums=('811fb91f1b37f072801616e5489fea1e5817d914c762dd3bde350c16b1d43571')

build() {
  CXXFLAGS="${CXXFLAGS} -Wno-incompatible-pointer-types"
  CFLAGS="${CFLAGS} -Wno-incompatible-pointer-types"
  cmake -B build -S cairo-dock-plug-ins-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_VALA_SRC=yes \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}

# vim: ts=2 sw=2 et:
