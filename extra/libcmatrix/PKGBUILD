# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libcmatrix
pkgver=0.0.2
pkgrel=1
pkgdesc='Matrix client library written in GObject'
arch=(x86_64)
url='https://source.puri.sm/Librem5/libcmatrix'
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  json-glib
  libgcrypt
  libolm
  libsecret
  libsoup3
  sqlite
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
)
source=("git+https://source.puri.sm/Librem5/libcmatrix.git?signed#tag=v$pkgver")
b2sums=(d6e7b101685101c32b5e060163559e466d9fcffd0783be3dec52190ec622f218afe5764d0dc645cab8d05beccc383a51a52cd5c4f2eaf7a073dbdb1432218974)
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido Günther <guido@xcpu.ch>

prepare() {
  cd $pkgname

  # Secure memory tests fail
  sed -i "/'client',/d;/'cm-enc',/d" tests/meson.build
}

build() {
  arch-meson $pkgname build \
    -D introspection=true \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
