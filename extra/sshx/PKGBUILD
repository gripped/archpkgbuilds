# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Hoream <hoream@qq.com>
# Contributor: Asuka Minato

pkgname=sshx
pkgver=0.3.1
pkgrel=2
pkgdesc='A secure web-based, collaborative terminal'
url="https://github.com/ekzhang/$pkgname"
license=(MIT)
arch=(x86_64 aarch64)
depends=(gcc-libs
         glibc)
makedepends=(cargo
             protobuf)
options=(!lto) # https://github.com/briansmith/ring/issues/1444
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
# source=("$_archive.tar.gz::https://static.crates.io/crates/$pkgname/$_archive.crate"
        # "$pkgname-LICENSE::$url/raw/$pkgver/LICENSE")
sha256sums=('3bc2abc59be26c9eefaf7fc999aa4a3723d2916414c319fd06555415278acb64')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	cargo build --frozen --release
}

check() {
	cd "$_archive"
	cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"{,-server}
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
