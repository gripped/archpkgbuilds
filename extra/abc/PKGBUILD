# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=abc
pkgver=0.0.0.20260215
_commit=8475386dfa921e6337d312f0b37e4eac47d02bf6
pkgrel=1
pkgdesc="System for Sequential Logic Synthesis and Formal Verification"
arch=("x86_64")
license=("MIT")
url="https://github.com/berkeley-abc/abc"
depends=('readline')
makedepends=('cmake')
checkdepends=('gtest')
source=(
  https://github.com/berkeley-abc/abc/archive/$_commit/abc-$_commit.tar.gz
  cmake.patch
)
sha512sums=(
  'b85f479d064509643cc949935e01883646029c6215e3b864c07bb31cc1e947fe08055e08f50517ce5addfe4b8dfe17fa3b05f69a913781daf980c5367b0ccbd4'
  '38d2f03ba1d60dde3ac00529ed9f87899e79bf159bf5f79faaada3b7cdc990e805caa257452d96e44da40138e0187c0d459d2260a81fa82ec2d62e4734dbb289'
)

prepare() {
  cd $pkgname-$_commit
  patch -Np1 -i "$srcdir"/cmake.patch
}

build() {
  cd $pkgname-$_commit
  cmake \
    -Wno-dev \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -B build
  cmake --build build
}

check() {
  cd $pkgname-$_commit

  # ctest
  (cd build && ctest --output-on-failure)

  # Test with demo
  ## Executable
  ./build/abc -c "r i10.aig; b; ps; b; rw -l; rw -lz; b; rw -lz; b; ps; cec"
  ## Library
  gcc -Wall -c src/demo.c -o demo.o
  g++ -o demo demo.o build/libabc.so -lm -ldl -lreadline -lpthread
  LD_LIBRARY_PATH=./build/ ./demo i10.aig
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 copyright.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
