# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-gtts
pkgver=2.5.3
pkgrel=3
pkgdesc="Python library and CLI tool to interface with Google Translate's text-to-speech API"
arch=(any)
url='https://github.com/pndurette/gTTS'
license=(MIT)
depends=(
  python
  python-click
  python-requests
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-testfixtures
)
source=("git+https://github.com/pndurette/gTTS.git#tag=v$pkgver")
b2sums=(568b48d4746496383002e5665f9d8c6c775c5658e4eafae182f2ab75f82778282d254a5bfcb59ddd852221d4e58011c16475c8451505fac5ce28a62a9cb21ce8)

prepare() {
  cd gTTS

  # https://github.com/pndurette/gTTS/pull/483
  git cherry-pick -n c8cb679109f9734f27f6dd6b60e5334a7c956435
}

build() {
  cd gTTS
  python -m build --wheel --no-isolation
}

check() {
  cd gTTS
  pytest
}

package() {
  cd gTTS
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  # Remove installed tests
  rm -r "$pkgdir$(python -c 'import site; print(site.getsitepackages()[0])')"/gtts/{,tokenizer/}tests/
}
