# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-bsnes
pkgver=3212
pkgrel=1
epoch=1
pkgdesc='Super Nintendo Entertainment System cores'
arch=(x86_64)
url=https://github.com/libretro/bsnes-libretro
license=(GPL-3.0-only)
groups=(libretro)
depends=(
  gcc-libs
  libretro-core-info
)
makedepends=(git)
_commit=c11dd1f8551ad9b6668ebfcbb5833cfc034b7205
source=(
  libretro-bsnes::git+https://github.com/libretro/bsnes-libretro.git#commit=${_commit}
  libretro-bsnes-flags.patch
)
b2sums=('af0ec20775e2f9de86a2f4d603ee0b15d71793062c9fe65a39b9acd59994493c1c45b8c05da965fdfc520352b6153df4efd9fc19469b849e704b8efaa1832801'
        'd8d87b92fc0f6d4020333230580bb0e63c73e6833365868f8c71fced923dbbf9df4842d1e7b7fb6bd493ba1e22e53757ae6656a3bcc3408220a18b06f41ea81f')

pkgver() {
  cd libretro-bsnes
  git rev-list --count HEAD
}

prepare() {
  cd libretro-bsnes
  patch -Np1 -i ../libretro-bsnes-flags.patch
}

build() {
  make -C libretro-bsnes/bsnes target=libretro binary=library local=false platform=linux
}

package() {
  install -Dm 644 libretro-bsnes/bsnes/out/bsnes_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
