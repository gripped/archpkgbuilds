# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-bsnes
pkgver=3182
pkgrel=1
epoch=1
pkgdesc='Super Nintendo Entertainment System cores'
arch=(x86_64)
url=https://github.com/libretro/bsnes-libretro
license=(GPL3)
groups=(libretro)
depends=(
  gcc-libs
  libretro-core-info
)
makedepends=(git)
_commit=ec353ea2502be9b71f3d9830b7a7b66ee69e254c
source=(
  libretro-bsnes::git+https://github.com/libretro/bsnes-libretro.git#commit=${_commit}
  libretro-bsnes-flags.patch
)
b2sums=('6442fb888ca6991deae3a2b40e18e9801ebedce48d04ed6e8a4da3b917bdcf852bdab2eff4b564b1eaedb7112dbd66c404cff09fb5c82278687b5c0d49289082'
        'd8d87b92fc0f6d4020333230580bb0e63c73e6833365868f8c71fced923dbbf9df4842d1e7b7fb6bd493ba1e22e53757ae6656a3bcc3408220a18b06f41ea81f')

pkgver() {
  cd libretro-bsnes
  git rev-list --count HEAD
}

prepare() {
  cd libretro-bsnes
  patch -Np1 -i ../libretro-bsnes-flags.patch
}

build() {
  make -C libretro-bsnes/bsnes target=libretro binary=library local=false platform=linux
}

package() {
  install -Dm 644 libretro-bsnes/bsnes/out/bsnes_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
