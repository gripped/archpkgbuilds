# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-binutils
pkgver=0.4.0
pkgrel=1
pkgdesc='Cargo subcommands to invoke the LLVM tools shipped with the Rust toolchain'
url="https://github.com/rust-embedded/$pkgname"
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=('cargo')
install="$pkgname.install"
source=("$url/archive/v$pkgver.tar.gz")
sha256sums=('06474f7f06b26dd9201c5089b23dc8b76a7af910e3a32c1a46def759c74df52b')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"

  for binary in 'ar' 'cov' 'ld' 'lld' 'nm' 'objcopy' 'objdump' 'profdata' 'readobj' 'size' 'strip'; do
    install -Dm 755 "target/release/rust-$binary" "$pkgdir/usr/bin/rust-$binary"
  done

  for binary in 'cov' 'nm' 'objcopy' 'objdump' 'profdata' 'readobj' 'size' 'strip'; do
    install -Dm 755 "target/release/cargo-$binary" "$pkgdir/usr/bin/cargo-$binary"
  done

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
