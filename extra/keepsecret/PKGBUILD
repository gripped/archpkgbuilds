# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=keepsecret
pkgver=1.0.0
pkgrel=2
pkgdesc='Password manager'
arch=(x86_64)
url='https://apps.kde.org/keepsecret/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glib2
         glibc
         kconfig
         kcoreaddons
         kdbusaddons
         ki18n
         kitemmodels
         kirigami
         kirigami-addons
         libsecret
         org.freedesktop.secrets
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-svg)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d3360caaa527af41f49f2758a93edd83b932a1de2d0993572c958b1eec6f0fe9'
            'SKIP')
validpgpkeys=('1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
