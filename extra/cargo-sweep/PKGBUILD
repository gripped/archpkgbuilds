# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-sweep
pkgver=0.8.0
pkgrel=2
pkgdesc="A cargo subcommand for cleaning up unused build files generated by Cargo"
url='https://github.com/holmgr/cargo-sweep'
arch=('x86_64')
license=('MIT')
depends=(
  'glibc'
  'libgcc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/holmgr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f35aed1b18c7852684ed98be79e68585a9322cf868d75b71f017d24a1b7e3ee9')
b2sums=('8d638bb97c59941d52402545a4243be7acf5dbf2312dad8c8bca1919e7d89fb0f7f495ba91c54a3c5a064d34920252bdc8e41987e0a0cce6649947c217e178c3')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip "check_toolchain_listing_on_multiple_projects"
}

package() {
  cd ${pkgname}-${pkgver}

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/cargo-sweep

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim: ts=2 sw=2 et:
