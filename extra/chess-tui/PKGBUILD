# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=chess-tui
pkgver=2.5.1
pkgrel=1
pkgdesc="Play chess in your terminal"
arch=('x86_64')
url="https://github.com/thomas-mauran/chess-tui"
license=('MIT')
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('9034bf20db8fdfc62ce474f538bb5b0baec6f446010ca3990705e2057a54eb14e0bb230743da3e80d5b2ee394a5f29224fb9ae16403f2a53dce0453a7cf7a16f')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
