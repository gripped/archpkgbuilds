# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: ThatOneCalculator <kainoa@t1c.dev>

pkgname=hyprpaper
pkgver=0.7.1
pkgrel=3
pkgdesc='a blazing fast wayland wallpaper utility with IPC controls'
arch=(x86_64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD-3-Clause)
depends=(cairo libcairo.so
         file libmagic.so
         gcc-libs
         glibc
         hyprlang libhyprlang.so
         hyprutils libhyprutils.so
         hyprwayland-scanner
         libglvnd
         libjpeg-turbo libjpeg.so
         libwebp libwebp.so
         pango
         wayland) # libwayland-cursor.so libwaylant-client.so
makedepends=(cmake
             wayland-protocols
             xorgproto)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('1086a9c7e4126446315aed23fc610f0869b77c899b9d828f997e7188341391e7')

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=Release
	make protocols
	cmake --build build
}

package() {
	cd "$_archive"
	# DESTDIR="$pkgdir" cmake --install build
	install -Dm0755 -t "$pkgdir/usr/bin/" "build/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
