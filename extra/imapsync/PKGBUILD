# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=imapsync
pkgver=2.290
pkgrel=1
pkgdesc='IMAP synchronisation, sync, copy or migration tool'
arch=('any')
url='https://imapsync.lamiral.info/'
license=('custom:imapsync')
depends=(
  perl-cgi
  perl-crypt-openssl-rsa
  perl-data-uniqid
  perl-date-manip
  perl-digest-sha1
  perl-encode-imaputf7
  perl-file-copy-recursive
  perl-file-tail
  perl-html-parser
  perl-io-socket-inet6
  perl-io-socket-ssl
  perl-io-tee
  perl-json-webtoken
  perl-libwww
  perl-mail-imapclient
  perl-module-runtime
  perl-module-scandeps
  perl-net-server
  perl-ntlm
  perl-package-stash
  perl-proc-processtable
  perl-readonly
  perl-regexp-common
  perl-sys-meminfo
  perl-term-readkey
  perl-unicode-string
)
makedepends=(
  cpanminus
  perl-par-packer
  perl-test-deep
  perl-test-mockobject
  perl-test-pod
)
checkdepends=(
  lsb-release
  perl-test-fatal
  perl-test-mock-guard
  perl-test-requires
  perl-test-warn
  procps-ng
  time
)
source=("https://imapsync.lamiral.info/dist2/${pkgname}-${pkgver}.tgz")
b2sums=('33ab6ec69ca1cb7f658296dea53056e94cba5bb5194e435e2449661572647432d42893c0c2d12e93e17b27058e416152215e5ca8a39768778307773e3be09cf4')

prepare() {
  sed -i 's/`hostname`/`uname -n`/' ${pkgname}-${pkgver}/tests.sh
}

build() {
  make -C ${pkgname}-${pkgver}
}

check() {
  cd ${pkgname}-${pkgver}
  perl imapsync --tests
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
