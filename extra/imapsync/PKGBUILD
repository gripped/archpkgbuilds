# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=imapsync
pkgver=2.314
pkgrel=1
pkgdesc='IMAP synchronisation, sync, copy or migration tool'
arch=('any')
url='https://imapsync.lamiral.info/'
license=('custom:imapsync')
depends=(
  perl-cgi
  perl-crypt-openssl-rsa
  perl-data-uniqid
  perl-date-manip
  perl-digest-sha1
  perl-email-address
  perl-encode-imaputf7
  perl-file-copy-recursive
  perl-file-tail
  perl-html-parser
  perl-http-daemon-ssl
  perl-io-socket-inet6
  perl-io-socket-ssl
  perl-io-tee
  perl-json-webtoken
  perl-libwww
  perl-mail-imapclient
  perl-module-runtime
  perl-module-scandeps
  perl-net-dns
  perl-net-server
  perl-ntlm
  perl-package-stash
  perl-proc-processtable
  perl-readonly
  perl-regexp-common
  perl-sys-meminfo
  perl-term-readkey
  perl-unicode-string
)
makedepends=(
  cpanminus
  perl-par-packer
  perl-test-deep
  perl-test-mockobject
  perl-test-pod
)
checkdepends=(
  lsb-release
  perl-test-fatal
  perl-test-mock-guard
  perl-test-requires
  perl-test-warn
  procps-ng
  time
)
source=("https://imapsync.lamiral.info/dist2/${pkgname}-${pkgver}.tgz")
b2sums=('d2fde66acd7d868021c52ae4b700020ee0686c1ecfa7166fc04060d018f225410afdd49749e8fdff9fda7be3d793deda7cc332ec31533e068373a3c2aba45d18')

prepare() {
  sed -i 's/`hostname`/`uname -n`/' ${pkgname}-${pkgver}/tests.sh
}

build() {
  make -C ${pkgname}-${pkgver}
}

check() {
  cd ${pkgname}-${pkgver}
  perl imapsync --tests
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
