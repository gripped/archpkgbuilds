# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: Ondrej Kucera <ondrej.kucera@centrum.cz>

pkgname=httping
pkgver=4.4.0
pkgrel=1
pkgdesc='A ping-like tool for HTTP requests'
url='https://www.vanheusden.com/httping/'
arch=(x86_64)
license=(GPL-3.0-only)
depends=(
  openssl
  ncurses
  fftw
)
makedepends=(
  cmake
)
source=(
  "https://github.com/folkertvanheusden/HTTPing/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
)
sha512sums=('739b376e27e3997a461de46e12d0eb7b2a5c65873f1b00d73c75fb0d03de7816bcb996b890a052556386a2b5439cd63d15e5b1f09eedb6e1bdb2ac87f405d7ef')
b2sums=('699f3367333d34460f8c28699b605ff2b02106af100feacb4b50391b79171126d0e77049292b6c15ca6ef374b0d10e51ecdd129f2697d5a6503a71c82472251b')

build() {
  cd HTTPing-${pkgver}
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build -v
}

package() {
  cd HTTPing-${pkgver}
  DESTDIR="${pkgdir}" cmake --install build
}

# vim: ts=2 sw=2 et:
