# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Danct12 <danct12@disroot.org>
# Contributor: Bart Ribbers <bribbers@disroot.org>

pkgname=waydroid
pkgver=1.6.1
pkgrel=1
pkgdesc="A container-based approach to boot a full Android system on a regular Linux system"
arch=('any')
url="https://waydro.id/"
license=('GPL-3.0-or-later')
depends=(
	'lxc'
	'python'
	'python-gbinder>=1.3.0'
	'python-gobject'
	'nftables'
	'dnsmasq'
	'gtk3'
	'python-dbus'
	'pulse-native-provider'
)
optdepends=('python-pyclip: clipboard manager support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('f81ae0281701942a8beb749397a4fa8ce828c2dc727878b44d1385e23a921bcd')

package() {
	DESTDIR="${pkgdir}" USE_NFTABLES=1 make -C "${pkgname}-${pkgver}" install

	# Byte-compile Python files during the packaging process instead of at runtime
	python -m compileall -o 0 -o 1 -p / -s "${pkgdir}" "${pkgdir}/usr/lib/waydroid/tools/"
}
