# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Danct12 <danct12@disroot.org>
# Contributor: Bart Ribbers <bribbers@disroot.org>

pkgname=waydroid
pkgver=1.6.2
pkgrel=1
pkgdesc="A container-based approach to boot a full Android system on a regular Linux system"
arch=('any')
url="https://waydro.id/"
license=('GPL-3.0-or-later')
depends=(
	'lxc'
	'python'
	'python-gbinder>=1.3.0'
	'python-gobject'
	'nftables'
	'dnsmasq'
	'gtk3'
	'python-dbus'
	'pulse-native-provider'
)
optdepends=('python-pyclip: clipboard manager support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('4b963aceb9de2884020e98b26e40147b3f26a0444606633adc45b63752f57dca')

package() {
	DESTDIR="${pkgdir}" USE_NFTABLES=1 make -C "${pkgname}-${pkgver}" install

	# Byte-compile Python files during the packaging process instead of at runtime
	python -m compileall -o 0 -o 1 -p / -s "${pkgdir}" "${pkgdir}/usr/lib/waydroid/tools/"
}
