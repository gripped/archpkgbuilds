# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=(lua-luaossl lua51-luaossl lua52-luaossl lua53-luaossl)
pkgver=20250929
pkgrel=1
pkgdesc='Most comprehensive OpenSSL module in the Lua universe'
arch=('x86_64')
url='http://25thandclement.com/~william/projects/luaossl.html'
license=('MIT')
makedepends=('luarocks' 'lua' 'lua51' 'lua52' 'lua53')
depends=('openssl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wahern/luaossl/archive/rel-$pkgver.tar.gz"
      "https://github.com/wahern/luaossl/releases/download/rel-$pkgver/luaossl-$pkgver-0.rockspec")
sha256sums=('2f695aea5f0bc0b9b32d3de56fffc5d41630f30bdd7e15bd68ce1f4bd3e8f61e'
            'b4a726aa81341b877ca305f45f6386a7056f05019b02418a1e8c33d4f610a886')

build() {
  cd "luaossl-rel-$pkgver"
  for v in 5.1 5.2 5.3 5.4; do
    mkdir -p "$v/"
    luarocks make --pack-binary-rock --lua-version="$v" --deps-mode=none \
      CFLAGS="-std=c17 $CPPFLAGS $CFLAGS -fPIC" \
      LIBFLAG="$LDFLAGS -shared" \
      ../luaossl-"$pkgver"-0.rockspec
    mv luaossl-"$pkgver"-0.*.rock "$v/"
  done
}

package_lua-luaossl() {
  pkgdesc="$pkgdesc for Lua 5.4"

  cd "luaossl-rel-$pkgver"
  luarocks install --lua-version=5.4 --tree="$pkgdir/usr/" --deps-mode=none 5.4/*.rock --no-manifest
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua51-luaossl() {
  pkgdesc="$pkgdesc for Lua 5.1"

  cd "luaossl-rel-$pkgver"
  luarocks install --lua-version=5.1 --tree="$pkgdir/usr/" --deps-mode=none 5.1/*.rock --no-manifest
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-luaossl() {
  pkgdesc="$pkgdesc for Lua 5.2"

  cd "luaossl-rel-$pkgver"
  luarocks install --lua-version=5.2 --tree="$pkgdir/usr/" --deps-mode=none 5.2/*.rock --no-manifest
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua53-luaossl() {
  pkgdesc="$pkgdesc for Lua 5.3"

  cd "luaossl-rel-$pkgver"
  luarocks install --lua-version=5.3 --tree="$pkgdir/usr/" --deps-mode=none 5.3/*.rock --no-manifest
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
