# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Andrea Feletto <andrea@andreafeletto.com>

pkgname=river
pkgver=0.3.14
pkgrel=1
pkgdesc='A dynamic tiling wayland compositor'
arch=('x86_64')
url='https://codeberg.org/river/river'
license=('GPL-3.0-or-later')
depends=('glibc'
         'libevdev'
         'libinput'
         'libxkbcommon'
         'mesa'
         'pixman'
         'sh'
         'wayland'
         'wlroots0.19'
         'xorg-xwayland')
makedepends=('git'
             'scdoc'
             'wayland-protocols'
             'zig')
optdepends=('polkit: access seat through systemd-logind')
provides=('wayland-compositor' 'river-classic')
source=(git+https://codeberg.org/river/river-classic.git#tag=v${pkgver}?signed)
# PACKAGING.md -> build.zig.zon
source+=(zig-pixman-v0.3.0.tar.gz::https://codeberg.org/ifreund/zig-pixman/archive/v0.3.0.tar.gz
         zig-wayland-v0.4.0.tar.gz::https://codeberg.org/ifreund/zig-wayland/archive/v0.4.0.tar.gz
         zig-wlroots-v0.19.3.tar.gz::https://codeberg.org/ifreund/zig-wlroots/archive/v0.19.3.tar.gz
         zig-xkbcommon-v0.3.0.tar.gz::https://codeberg.org/ifreund/zig-xkbcommon/archive/v0.3.0.tar.gz)
noextract=("${source[@]:1}")
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>
sha256sums=('6c19855ad1ecbe5a8102cc79ba965b50388bc1a3398f5781062e8bdb24616289'
            'cd7fe3415d4d58685a94fdedd308e9994a37f012828940cfb603461de7f2c6ad'
            '907fdd18c7ede1ab1f249c618ba5dce3b8a72c26e50090b4ae7010033a419ba9'
            'bffcfa2af6b90d07cb187ceb3d70aeffb0ed11b3d3f9737ea004e722bc031d6b'
            '1e185423e6b23ed9729614e66751ab7522db4487df4e0dcc7a2b06375aacda23')

prepare() {
    zig fetch --global-cache-dir ./zig-global-cache "./${source[1]%%::*}"
    zig fetch --global-cache-dir ./zig-global-cache "./${source[2]%%::*}"
    zig fetch --global-cache-dir ./zig-global-cache "./${source[3]%%::*}"
    zig fetch --global-cache-dir ./zig-global-cache "./${source[4]%%::*}"
}

build() {
  cd $pkgname-classic

  DESTDIR="build" zig build \
    --summary all \
    --prefix /usr \
    --search-prefix /usr \
    --global-cache-dir ../zig-global-cache \
    --system ../zig-global-cache/p \
    --build-id=sha1 \
    -Dtarget=native-linux.6.6-gnu.2.40 \
    -Dcpu=baseline \
    -Dpie \
    -Doptimize=ReleaseSafe \
    -Dxwayland
}

check() {
  cd $pkgname-classic
  zig build test \
    --summary all \
    --prefix /usr \
    --search-prefix /usr \
    --global-cache-dir ../zig-global-cache \
    --system ../zig-global-cache/p \
    --build-id=sha1 \
    -Dtarget=native-linux.6.6-gnu.2.40 \
    -Dcpu=baseline \
    -Dpie \
    -Doptimize=ReleaseSafe \
    -Dxwayland
}

package() {
  cd $pkgname-classic

  cp -a build/* "$pkgdir"

  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 contrib/river.desktop -t "$pkgdir/usr/share/wayland-sessions"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -a example "$pkgdir/usr/share/$pkgname"
}
