# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Andrea Feletto <andrea@andreafeletto.com>

pkgname=river
pkgver=0.3.0
pkgrel=1
pkgdesc='A dynamic tiling wayland compositor'
arch=('x86_64')
url='https://codeberg.org/river/river'
license=('GPL-3.0-or-later')
depends=('libevdev'
         'libxkbcommon'
         'mesa'
         'pixman'
         'wayland'
         'wlroots'
         'xorg-xwayland')
makedepends=('scdoc'
             'wayland-protocols'
             'zig')
optdepends=('polkit: access seat through systemd-logind')
source=("${pkgname}-${pkgver}.tar.gz::https://codeberg.org/river/river/archive/v${pkgver}.tar.gz"
        "https://codeberg.org/river/river/releases/download/v${pkgver}/river-${pkgver}.tar.gz.sig")
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>
sha256sums=('3e8161f4443e7366e1ef0e0a3062daeb7fd5014cae953a95e3b878e8fc30adec'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"

  export PKG_CONFIG_PATH='/usr/lib/wlroots0.16/pkgconfig'
  DESTDIR="build" zig build \
    --prefix /usr \
    --search-prefix /usr \
    -Dtarget=native-linux.5.15-gnu.2.37 \
    -Dcpu=baseline \
    -Dpie \
    -Doptimize=ReleaseSafe \
    -Dxwayland
}

check() {
  cd "$pkgname-$pkgver"

  zig build test \
    --prefix /usr \
    --search-prefix /usr \
    -Dtarget=native-linux.5.15-gnu.2.37 \
    -Dcpu=baseline \
    -Dpie \
    -Doptimize=ReleaseSafe \
    -Dxwayland
}

package() {
  depends+=(libwlroots.so=11)
  cd "$pkgname-$pkgver"

  cp -a build/* "$pkgdir"

  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 contrib/river.desktop -t "$pkgdir/usr/share/wayland-sessions"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -a example "$pkgdir/usr/share/$pkgname"
}
