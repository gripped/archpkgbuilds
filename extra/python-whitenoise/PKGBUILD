# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=6.8.2
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='https://github.com/evansd/whitenoise'
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-brotli' 'python-django' 'python-pytest' 'python-requests')
source=("git+https://github.com/evansd/whitenoise.git#tag=$pkgver")
sha512sums=('2e5b476ac72fb2dae4d930a4ea80decbf5aa6e1e90b164f83c61114cafe51b5768f8201f5afa1e152e0b9c45edf03481b752dddc20af7b87c914826eefafdf04')

build() {
  cd whitenoise
  python -m build -nw
}

check() {
  cd whitenoise
  DJANGO_SETTINGS_MODULE=tests.django_settings PYTHONPATH="$PWD"/src pytest
}

package() {
  cd whitenoise
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
