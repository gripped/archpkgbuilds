# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=6.11.0
pkgrel=2
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='https://github.com/evansd/whitenoise'
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-brotli' 'python-django' 'python-pytest' 'python-requests')
source=("git+https://github.com/evansd/whitenoise.git#tag=$pkgver")
sha512sums=('760ce87339b99ca8341378c7bd4362aa28436ed0b2854d2a091939a0242f4929441f10414bbd112c7104c22ae87ae9af17541910e7e03dfec048ce4928a614cc')

build() {
  cd whitenoise
  python -m build -nw
}

check() {
  cd whitenoise
  DJANGO_SETTINGS_MODULE=tests.django_settings PYTHONPATH="$PWD"/src pytest
}

package() {
  cd whitenoise
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
