# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=6.8.1
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='https://github.com/evansd/whitenoise'
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-brotli' 'python-django' 'python-pytest' 'python-requests')
source=("git+https://github.com/evansd/whitenoise.git#tag=$pkgver")
sha512sums=('5d5528c238b744c730a988d1c81aaf7d98f73c66ac4ba9f5146d021b2412b7f065c314527a1147ef2a4aa0417b6919e49c30ee46c8924fda01b0fbd671e12d2e')

build() {
  cd whitenoise
  python -m build -nw
}

check() {
  cd whitenoise
  DJANGO_SETTINGS_MODULE=tests.django_settings PYTHONPATH="$PWD"/src pytest
}

package() {
  cd whitenoise
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
