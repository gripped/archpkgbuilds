# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=6.9.0
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='https://github.com/evansd/whitenoise'
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-brotli' 'python-django' 'python-pytest' 'python-requests')
source=("git+https://github.com/evansd/whitenoise.git#tag=$pkgver")
sha512sums=('9ccb358291a42a214f11c8e427c01bcf5b7372393201c65db47e09290201daa5a5a0903061bc6413101327f52323e230335d23a82ccf1d72280f6fd2eb488821')

build() {
  cd whitenoise
  python -m build -nw
}

check() {
  cd whitenoise
  DJANGO_SETTINGS_MODULE=tests.django_settings PYTHONPATH="$PWD"/src pytest
}

package() {
  cd whitenoise
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
