# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=6.10.0
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='https://github.com/evansd/whitenoise'
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-brotli' 'python-django' 'python-pytest' 'python-requests')
source=("git+https://github.com/evansd/whitenoise.git#tag=$pkgver")
sha512sums=('57b47cdcd24ef31de881907b85d72edcaec40f729aee8ed25067ab3bee91458de1d72cd4ee621a41d66710dc4e311422ca4ade28226acd8e60c9abbc3ca00a64')

build() {
  cd whitenoise
  python -m build -nw
}

check() {
  cd whitenoise
  DJANGO_SETTINGS_MODULE=tests.django_settings PYTHONPATH="$PWD"/src pytest
}

package() {
  cd whitenoise
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
