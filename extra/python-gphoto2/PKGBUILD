# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-gphoto2
pkgver=2.5.1
pkgrel=3
pkgdesc="Python interface to libgphoto2"
arch=('x86_64')
url="https://github.com/jim-easterbrook/python-gphoto2"
license=('LGPL-3.0-or-later')
depends=(
  'libgphoto2'
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-toml'
  'python-wheel'
  'swig'
)
source=($pkgname-$pkgver.tar.gz::https://github.com/jim-easterbrook/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('2cdfd8ddb676a8041298bee32b7943d0f3a261067a5b0b95e238f1647ebe0316')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package(){
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
