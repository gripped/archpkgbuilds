# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=6.5.15
pkgrel=1
pkgdesc='Default terminal emulation application for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'fontconfig' 'gcc-libs' 'glib2' 'glibc'
         'hicolor-icon-theme' 'icu' 'qt6-5compat' 'qt6-base' 'libchardet' 'libsecret' 'libxcb')
makedepends=('git' 'cmake' 'ninja' 'lxqt-build-tools' 'qt6-tools')
#checkdepends=('xorg-server-xvfb')
optdepends=('zssh: for remote management support')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-terminal.git#tag=$pkgver")
sha512sums=('fac79c861b5fa63e3a9ef08a14f42c7c7b7cc522b73de0b6769c3bc1aae3064fdebf8c2bd5915fe0ead74265bbbfa16d9e31ec0eece5d0f73881904647310b71')

build(){
  cd deepin-terminal
  cmake . -GNinja -DVERSION=$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  ninja
}

check() {
  cd deepin-terminal
  #LD_LIBRARY_PATH="$PWD/3rdparty/terminalwidget" xvfb-run ninja test || echo "Tests failed"
}

package() {
  cd deepin-terminal
  DESTDIR="$pkgdir" ninja install
}
