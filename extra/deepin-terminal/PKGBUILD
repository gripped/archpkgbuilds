# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=6.5.18
pkgrel=2
pkgdesc='Default terminal emulation application for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'fontconfig' 'gcc-libs' 'glib2' 'glibc'
         'hicolor-icon-theme' 'icu' 'qt6-5compat' 'qt6-base' 'libchardet' 'libsecret' 'libxcb')
makedepends=('git' 'cmake' 'ninja' 'lxqt-build-tools' 'qt6-tools')
#checkdepends=('xorg-server-xvfb')
optdepends=('zssh: for remote management support')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-terminal.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('3e9df2da3073d5c752513fad01182b221240145fa911aaf4a869d882160f8d1abb8992500b3952e403758af38fa58917b0826c9f3bfbc16424734c43540af1f9'
            'bb165c3394147e0e3a83391ee2d06d7a5b2328cf301ca0a4f2bbfcfb8aca47ee9b31c5f980ad47726096a2aec845b41daebba9ad52a8b111773efa4ea66b1d05')

prepare() {
  cd deepin-terminal
  patch -p1 -i ../qt-6.10.patch
}

build(){
  cd deepin-terminal
  cmake . -GNinja -DVERSION=$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  ninja
}

check() {
  cd deepin-terminal
  #LD_LIBRARY_PATH="$PWD/3rdparty/terminalwidget" xvfb-run ninja test || echo "Tests failed"
}

package() {
  cd deepin-terminal
  DESTDIR="$pkgdir" ninja install
}
