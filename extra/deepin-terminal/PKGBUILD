# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=6.5.3
pkgrel=2
pkgdesc='Default terminal emulation application for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtk6core' 'dtk6gui' 'dtk6widget' 'fontconfig' 'gcc-libs' 'glib2'
         'glibc' 'hicolor-icon-theme' 'icu' 'qt6-5compat' 'qt6-base' 'libchardet' 'libsecret'
         'libxcb')
makedepends=('git' 'cmake' 'lxqt-build-tools-qt5' 'ninja' 'qt6-tools')
#checkdepends=('xorg-server-xvfb')
optdepends=('zssh: for remote management support')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-terminal.git#tag=$pkgver")
sha512sums=('66ba8360aadab592e81381336483f0854d4bb6a6ef034ea9d0ef69629a7d5206f3341586eef6442271c1c52d5c4a0d3c71a4b96d3455e5f6b84fca1aeb7a2c08')

prepare() {
  cd deepin-terminal
  sed -i 's/QT NAMES Qt6 Qt5/QT NAMES Qt6/' CMakeLists.txt 3rdparty/terminalwidget/CMakeLists.txt
}

build(){
  cd deepin-terminal
  cmake . -GNinja -DVERSION=$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  ninja
}

check() {
  cd deepin-terminal
  #LD_LIBRARY_PATH="$PWD/3rdparty/terminalwidget" xvfb-run ninja test || echo "Tests failed"
}

package() {
  cd deepin-terminal
  DESTDIR="$pkgdir" ninja install
}
