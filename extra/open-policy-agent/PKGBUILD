# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jonathan Wright <jon@than.io>
# Contributor: Brian Atkinson <brian@atkinson.mn>
# Contributor: David Birks <david@birks.dev>

pkgname=open-policy-agent
pkgver=1.10.0
pkgrel=1
pkgdesc='Command-line utility and REPL for Open Policy Agent'
arch=(x86_64)
url=https://github.com/open-policy-agent/opa
license=(Apache-2.0)
depends=(glibc)
makedepends=(
  git
  go
)
source=("git+https://github.com/open-policy-agent/opa.git#tag=v$pkgver")
b2sums=('a4b9af9457a5ee65684a9ed64381c379ccfc6e47a90a4c5606d80882097f0f4848210f7ccc0d5983f9399d4b95c8e58c6194f63dec077e20eb091fecd76d258c')

build() {
  cd opa

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  go build \
    -ldflags "-linkmode=external -X github.com/open-policy-agent/opa/version.Version=${pkgver}" \
    -o opa .

  mkdir completion
  ./opa completion bash > completion/opa
  ./opa completion zsh > completion/_opa
  ./opa completion fish > completion/opa.fish
}

package() {
  install -Dm 755 opa/opa -t "${pkgdir}"/usr/bin/
  install -Dm 644 opa/completion/opa -t "${pkgdir}"/usr/share/bash-completion/completions/
  install -Dm 644 opa/completion/_opa -t "${pkgdir}"/usr/share/zsh/site-functions/
  install -Dm 644 opa/completion/opa.fish -t "${pkgdir}"/usr/share/fish/completions/
}

# vim: ts=2 sw=2 et:
