# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jonathan Wright <jon@than.io>
# Contributor: Brian Atkinson <brian@atkinson.mn>
# Contributor: David Birks <david@birks.dev>

pkgname=open-policy-agent
pkgver=1.3.0
pkgrel=1
pkgdesc='Command-line utility and REPL for Open Policy Agent'
arch=(x86_64)
url=https://github.com/open-policy-agent/opa
license=(Apache-2.0)
depends=(glibc)
makedepends=(
  git
  go
)
_tag=89f48353959c9b08608b6d7160c1f1c5ae2763ee
source=(git+https://github.com/open-policy-agent/opa.git#tag=${_tag})
b2sums=('27a545bfe8b6a5d33a3edd3faa8426014411f6e93b751e6166e9b854264cd4b9a0e3fcdbe25a6a8210f5c4897641a2aa702f1d33005a9425c11d482c4dd502d7')

pkgver() {
  cd opa
  git describe --tags | sed 's/^v//'
}

build() {
  cd opa

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  go build \
  -ldflags "-linkmode=external -X github.com/open-policy-agent/opa/version.Version=${pkgver}" \
  -o opa \
  .

  mkdir completion
  ./opa completion bash > completion/opa
  ./opa completion zsh > completion/_opa
  ./opa completion fish > completion/opa.fish
}

package() {
  install -Dm 755 opa/opa -t "${pkgdir}"/usr/bin/
  install -Dm 644 opa/completion/opa -t "${pkgdir}"/usr/share/bash-completion/completions/
  install -Dm 644 opa/completion/_opa -t "${pkgdir}"/usr/share/zsh/site-functions/
  install -Dm 644 opa/completion/opa.fish -t "${pkgdir}"/usr/share/fish/completions/
}

# vim: ts=2 sw=2 et:
