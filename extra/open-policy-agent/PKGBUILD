# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jonathan Wright <jon@than.io>
# Contributor: Brian Atkinson <brian@atkinson.mn>
# Contributor: David Birks <david@birks.dev>

pkgname=open-policy-agent
pkgver=1.6.0
pkgrel=1
pkgdesc='Command-line utility and REPL for Open Policy Agent'
arch=(x86_64)
url=https://github.com/open-policy-agent/opa
license=(Apache-2.0)
depends=(glibc)
makedepends=(
  git
  go
)
_tag=710b5a62789fbe8ad231b2fc0090c664f1ac93ba
source=(git+https://github.com/open-policy-agent/opa.git#tag=${_tag})
b2sums=('37cf9523a090819d9b4176036eeee58196dc5b2eedf229b55f057e4d1387070565fa70a903689ef8675b6f67c82d45e6d1071578177b4fcebca0ec95d129baa6')

pkgver() {
  cd opa
  git describe --tags | sed 's/^v//'
}

build() {
  cd opa

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  go build \
  -ldflags "-linkmode=external -X github.com/open-policy-agent/opa/version.Version=${pkgver}" \
  -o opa \
  .

  mkdir completion
  ./opa completion bash > completion/opa
  ./opa completion zsh > completion/_opa
  ./opa completion fish > completion/opa.fish
}

package() {
  install -Dm 755 opa/opa -t "${pkgdir}"/usr/bin/
  install -Dm 644 opa/completion/opa -t "${pkgdir}"/usr/share/bash-completion/completions/
  install -Dm 644 opa/completion/_opa -t "${pkgdir}"/usr/share/zsh/site-functions/
  install -Dm 644 opa/completion/opa.fish -t "${pkgdir}"/usr/share/fish/completions/
}

# vim: ts=2 sw=2 et:
