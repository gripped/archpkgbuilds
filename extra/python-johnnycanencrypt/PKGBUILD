# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-johnnycanencrypt
_name="${pkgname#python-}"
pkgver=0.17.0
pkgrel=1
pkgdesc="Python module for OpenPGP written in Rust"
arch=(x86_64)
url="https://github.com/kushaldas/johnnycanencrypt"
license=(LGPL-3.0-or-later)
depends=(
  bzip2
  gcc-libs
  glibc
  gmp
  nettle
  pcsclite
  python
  python-httpx
)
makedepends=(
  clang
  git
  python-build
  python-installer
  python-maturin
  rust
)
checkdepends=(
  python-pytest
  python-vcrpy
)
source=(git+$url.git?signed#tag=v$pkgver)
sha512sums=('3449b7410bb837b0a312916d4b07a5d28041e2a7ea3d483df0f5a59655653540de551ab0d9b796b876b488ae856830b01cfb3c22fcde0d910069185002e2f2e1')
b2sums=('e30b496c1923e5041be3218d26417aeb81f3d259b12914c0357cd8da3c9fc1bb4a43b3cc6d16ad20d075779c7627a3ea8b278769983b6a4c476838a399166c0d')
validpgpkeys=(A85FF376759C994A8A1168D8D8219C8C43F6C5E1)  # Kushal Das <kushal@freedom.press>

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  # install to temporary location and copy tests in place because test imports are relative :(
  python -m installer --destdir=test_dir dist/*.whl
  cp -av -- tests "test_dir/$site_packages/"
  export PYTHONPATH="$PWD/test_dir/$site_packages"
  pytest "${pytest_options[@]}" test_dir/$site_packages/tests/
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
}
