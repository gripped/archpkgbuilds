# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-johnnycanencrypt
_name="${pkgname#python-}"
pkgver=0.16.0
pkgrel=1
pkgdesc="Python module for OpenPGP written in Rust"
arch=(x86_64)
url="https://github.com/kushaldas/johnnycanencrypt"
license=(LGPL-3.0-or-later)
depends=(
  bzip2
  gcc-libs
  glibc
  gmp
  nettle
  pcsclite
  python
  python-httpx
)
makedepends=(
  clang
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-rust
  python-wheel
  rust
)
checkdepends=(
  python-pytest
  python-vcrpy
)
source=(git+$url.git?signed#tag=v$pkgver)
sha512sums=('abdbf05e22736c3d63e8863363cb7ff4492e0cbdc3378804db42b24427d05788c72abca1ece751c5a1ce484bee1d8f85fdba1477f8b2e1b9d4616df5d415a1a9')
b2sums=('f5662e01c8167267c5d362dce3bdb8b644b3825928865b1c9af2f8d793900c55da3b6898b98348b6b2b7e8806b0890ffa92417ae3cdef993d5d40719ccdbeae5')
validpgpkeys=(A85FF376759C994A8A1168D8D8219C8C43F6C5E1)  # Kushal Das <kushal@freedom.press>

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  # install to temporary location and copy tests in place because test imports are relative :(
  python -m installer --destdir=test_dir dist/*.whl
  cp -av -- tests "test_dir/$site_packages/"
  export PYTHONPATH="$PWD/test_dir/$site_packages"
  pytest "${pytest_options[@]}" test_dir/$site_packages/tests/
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
}
