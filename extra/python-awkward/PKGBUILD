# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>

_name=awkward
pkgbase=python-$_name
# pkgname=($pkgbase $pkgbase-docs) - docs require many new dependencies
pkgname=$pkgbase
pkgver=2.8.9
pkgrel=1
pkgdesc="Manipulate jagged, chunky, and/or bitmasked arrays as easily as Numpy"
url="https://github.com/scikit-hep/awkward"
license=(BSD-3-Clause)
arch=(x86_64)
depends=(
  gcc-libs
  glibc
  python
  python-fsspec
  python-numpy
  python-packaging
  python-typing_extensions
)
makedepends=(
  cmake
  cuda
  doxygen
  git
  python-build
  python-hatch-fancy-pypi-readme
  python-hatchling
  python-installer
  python-nox
  python-scikit-build-core
  python-sphinx
  python-sphinx_rtd_theme
)
checkdepends=(
  pybind11
  python-hist
  python-numba
  python-numexpr
  python-pandas
  python-pyaml
  python-pyarrow
  python-pytest
  python-scikit-hep-testdata
  python-vector
  root
)
optdepends=(
  'cuda: CUDA support'
  'python-numba: for using in Numba JIT functions'
  'python-numexpr: numexpr connector'
  'python-pandas: pandas connector'
  'python-pyarrow: pyArrow connector'
  'root: rdataframe connector'
)
source=(
  $pkgname::git+https://github.com/scikit-hep/$_name#tag=v$pkgver
  $pkgname-rapidjson::git+https://github.com/Tencent/rapidjson.git
)

sha512sums=('dd320fdfd04ae2fb6b5e5618fc9feae5640c16ffd11d6f9a3b732ee6395f74f5e2246f4af1e7e7cabd2d4b7edd2fe3ef28f53329434513f4c91f3dc25c9f04ff'
            'SKIP')

prepare() {
  cd $pkgbase
  git submodule init

  git config submodule."rapidjson".url "$srcdir"/$pkgname-rapidjson

  git -c protocol.file.allow=always submodule update --init --recursive
}

build() {
  cd $pkgbase

  nox -s prepare
  python -m build --wheel --no-isolation

  cd awkward-cpp
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
      -vv
      # hangs...
      --deselect tests/test_1374_to_rdataframe.py
      --deselect tests/test_1473_from_rdataframe.py
      --deselect tests/test_1508_awkward_from_rdataframe.py
      --deselect tests/test_1620_layout_builders.py
      --deselect tests/test_1625_multiple_columns_from_rdataframe.py
      --deselect tests/test_1781_rdataframe_snapshot.py
      --deselect tests/test_1829_to_from_rdataframe_bool.py
      --deselect tests/test_2202_filter_multiple_columns_from_rdataframe.py
      --deselect tests/test_2234_from_rdataframe_keep_order.py
      --deselect tests/test_2258_from_rdataframe_with_arguments.py
      --deselect tests/test_3375_add_rdf_column.py
  )

  cd $pkgbase
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m installer awkward-cpp/dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package_python-awkward() {
  optdepends+=("$pkgbase-docs: documentation")

  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  cd awkward-cpp
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# package_python-awkward-docs() {
#   pkgdesc+=" - documentation"

#   cd $pkgbase

#   install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
#   install -vDm 644 README.md -t "$pkgdir"/usr/share/$pkgbase/README.md

#   python -m installer --destdir=tmp-install dist/*.whl
#   python -m installer --destdir=tmp-install awkward-cpp/dist/*.whl
#   install -d "$pkgdir"/usr/share/doc/$pkgbase
#   local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
#   PYTHONPATH="$PWD/tmp-install/$site_packages" sphinx-build "$PWD"/docs "$pkgdir"/usr/share/doc/$pkgbase
# }
