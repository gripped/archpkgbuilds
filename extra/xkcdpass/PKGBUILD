# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Jason Papakostas <vithos@gmail.com>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Perttu Luukko <perttu.luukko@iki.fi>

pkgname=xkcdpass
_pkgname='XKCD-password-generator'
pkgver=1.20.0
pkgrel=3
pkgdesc='Generate secure passphares inspired by XKCD'
arch=('any')
url='https://github.com/redacted/XKCD-password-generator'
license=('BSD-3-Clause')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
optdepends=('cracklib: alternative wordlist'
    'words: alternative wordlist')
source=("$url/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('7a7a8adb847c8ea4048318c801d3b3dcf967ced6b35d2ec1e567fce6bf2fa7fc')

build() {
    cd "$_pkgname-$pkgname-$pkgver"
    python -m build --wheel --no-isolation
}

check() {
    cd "$_pkgname-$pkgname-$pkgver"
    python -m unittest discover -vs .
}

package() {
    cd "$_pkgname-$pkgname-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl

    install -m644 -D LICENSE.BSD "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -m644 -D xkcdpass.1 "$pkgdir/usr/share/man/man1/xkcdpass.1"
}
