# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.356
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://microsoft.github.io/pyright
license=('MIT')
depends=('nodejs')
makedepends=('git' 'npm')
source=("git+https://github.com/microsoft/pyright.git#tag=$pkgver")
b2sums=('61fb796a95e9dd267c9dc1c31954a1e3c14d154d94e8db61173a854b6e11b9b5d7d08eb02bea82b2df2026d92e2b592187ecbc2035039df63adaaaef4fb0f5c0')

prepare() {
  cd $pkgname
  npm ci
}

build() {
  cd $pkgname/packages/$pkgname
  npm run build
}

check() {
  cd $pkgname/packages/$pkgname-internal
  npm test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/langserver.index.js "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  cp -r dist {,langserver.}index.js package.json "$pkgdir"/$mod_dir
}
