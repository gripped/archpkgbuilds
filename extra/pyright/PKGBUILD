# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.364
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://microsoft.github.io/pyright
license=('MIT')
depends=('nodejs')
makedepends=('git' 'npm')
source=("git+https://github.com/microsoft/pyright.git#tag=$pkgver")
b2sums=('9c7565304de9471cd72a60e5dd97a7966f8b543bb10c51817499c5b36b3b06847673df5e10e9b5aa8f299fc2a667f9cc35f0cd8143e4bb7944f3c8a4398729e8')

prepare() {
  cd $pkgname
  npm ci
}

build() {
  cd $pkgname/packages/$pkgname
  npm run build
}

check() {
  cd $pkgname/packages/$pkgname-internal
  npm test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/langserver.index.js "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  cp -r dist {,langserver.}index.js package.json "$pkgdir"/$mod_dir
}
