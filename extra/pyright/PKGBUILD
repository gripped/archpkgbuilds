# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.379
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=(any)
url=https://microsoft.github.io/pyright
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  npm
)
source=("git+https://github.com/microsoft/pyright.git#tag=$pkgver")
b2sums=('c9295d082aa3c185ff518a9602b6264bd04157a43b8f87792b9c26cb2ec19a627e81f695337f8a7aab73c8bd15b025c6478ed5a943ea4ed0978a1ee38453ec8b')

prepare() {
  cd $pkgname
  npm ci
}

build() {
  cd $pkgname/packages/$pkgname
  npm run build
}

check() {
  cd $pkgname/packages/$pkgname-internal
  npm test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/langserver.index.js "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  cp -r dist {,langserver.}index.js package.json "$pkgdir"/$mod_dir
}
