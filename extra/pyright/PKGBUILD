# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.322
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://microsoft.github.io/pyright
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://github.com/microsoft/pyright/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('6f09a4a970a332474c9604f4f506c2448f0e4cb8f0af7242e97ecd86e8f893871984a4edc24050fd5e9d1e61e7e0660e190dd4dce9e001526679116802e5b14f')

prepare() {
  cd $pkgname-$pkgver
  npm ci
}

build() {
  cd $pkgname-$pkgver/packages/$pkgname
  npm run build
}

check() {
  cd $pkgname-$pkgver/packages/$pkgname-internal
  npm test
}

package() {
  local _npmdir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$_npmdir}
  ln -s $_npmdir/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s $_npmdir/langserver.index.js \
    "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname-$pkgver
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  cp -r dist {,langserver.}index.js "$pkgdir"/$_npmdir
}
