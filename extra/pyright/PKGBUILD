# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.378
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=(any)
url=https://microsoft.github.io/pyright
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  npm
)
source=("git+https://github.com/microsoft/pyright.git#tag=$pkgver")
b2sums=('e6b572479bcdabf8c63945c77f5a4ff5fcf70627e6845e08d23390c1294953847947b7ccb7cb64bff0d614cc81c63ab8ba1dceadfd6a3505eccea8413fdec67d')

prepare() {
  cd $pkgname
  npm ci
}

build() {
  cd $pkgname/packages/$pkgname
  npm run build
}

check() {
  cd $pkgname/packages/$pkgname-internal
  npm test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/langserver.index.js "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  cp -r dist {,langserver.}index.js package.json "$pkgdir"/$mod_dir
}
