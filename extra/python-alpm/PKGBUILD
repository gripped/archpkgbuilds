# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-alpm
_upstream=alpm
pkgver=0.4.0
pkgrel=2
pkgdesc="Python bindings for the Rust-based ALPM project"
arch=(x86_64)
url="https://alpm.archlinux.page/python-alpm/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  git
  python-build
  python-maturin
  python-installer
)
checkdepends=(
  alpm-pkgbuild-bridge
  python-pytest
)
optdepends=(
  'alpm-docs: for additional specifications'
  'alpm-pkgbuild-bridge: for creating SRCINFO data from PKGBUILD files'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('d4a2aef90b2ec4546bc1bdb35208cacf16a190e647e445b6e895eb3b18e142c6dc72effdf42eb9685caafc9a668d42b7b8c5fda27fc37a452ed4cf84dcfa113e')
b2sums=('cd53f20a411be672be73232c0e8247ef0719d0fb48ed6db61cae9e8e97eaba50abeb68ff3b95755efa8bd69b2510b34dffda1a450a2845051c718aa06ba98518')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream/$pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $_upstream/$pkgname
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer --destdir="tmp_install" dist/*.whl
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest -vv tests/
}

package() {
  cd $_upstream/$pkgname
  python -m installer -d "$pkgdir" dist/*.whl

  install -vDm 644 {ARCHITECTURE,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ../LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
