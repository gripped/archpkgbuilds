# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-alpm
_upstream=alpm
pkgver=0.3.0
pkgrel=1
pkgdesc="Python bindings for the Rust-based ALPM project"
arch=(x86_64)
url="https://alpm.archlinux.page/python-alpm/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  git
  python-build
  python-maturin
  python-installer
)
checkdepends=(
  alpm-pkgbuild-bridge
  python-pytest
)
optdepends=(
  'alpm-docs: for additional specifications'
  'alpm-pkgbuild-bridge: for creating SRCINFO data from PKGBUILD files'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('f5cfeb4a9a87bc6db529120626c5b833ea6ded812c73a7e12d1b5380ce9babc89c61c3e936b927ddbaf6fbf2d1bd5733917a428b026f5dd374c0933530bd94a8')
b2sums=('5b53fa4896cf8ed3e69a37a6635be0c6ff7e68d15ab3e7e1245aaecffb6e08171fab4d75473be233677a24aa8585bf44da08b48b437613ccf4d6dc0cdaee363c')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream/$pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $_upstream/$pkgname
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer --destdir="tmp_install" dist/*.whl
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest -vv tests/
}

package() {
  cd $_upstream/$pkgname
  python -m installer -d "$pkgdir" dist/*.whl

  install -vDm 644 {ARCHITECTURE,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ../LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
