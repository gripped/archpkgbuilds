# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-alpm
_upstream=alpm
pkgver=0.4.1
pkgrel=1
pkgdesc="Python bindings for the Rust-based ALPM project"
arch=(x86_64)
url="https://alpm.archlinux.page/python-alpm/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  git
  python-build
  python-maturin
  python-installer
)
checkdepends=(
  alpm-pkgbuild-bridge
  python-pytest
)
optdepends=(
  'alpm-docs: for additional specifications'
  'alpm-pkgbuild-bridge: for creating SRCINFO data from PKGBUILD files'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('7ca2eeeb9be1aaa6637d62b94ba9c41c5f03e8008050ae8b35d2fa4eb1c0dd4361c0cc9108ee9946bf0f7d8740f196dd8aba9a03089c296e1846d40be1bfc10f')
b2sums=('0bc19c55f75e4f1c60d4d516de5a120ed1876c8a944ddf733c332689a14d3dd1adb0cf49d8af0dec1af0aedb40bd525889bf6719ba3e65851f7fb5c7f3223547')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream/$pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $_upstream/$pkgname
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer --destdir="tmp_install" dist/*.whl
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest -vv tests/
}

package() {
  cd $_upstream/$pkgname
  python -m installer -d "$pkgdir" dist/*.whl

  install -vDm 644 {ARCHITECTURE,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ../LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
