# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Gaetan Bisson
# Contributor: damir

pkgname=x11vnc
pkgver=0.9.17
pkgrel=1
epoch=1
_commit='e2b726a8c0464051afda01648072af6835aaa5f7'
pkgdesc='VNC server for real X displays'
url='https://github.com/LibVNC/x11vnc/'
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
    'avahi'
    'glibc'
    'openssl'
    'libvncserver'
    'libx11'
    'libxcomposite'
    'libxcrypt'
    'libxdamage'
    'libxext'
    'libxfixes'
    'libxi'
    'libxinerama'
    'libxrandr'
    'libxtst'
    'sh'
    'xorg-xdpyinfo')
optdepends=(
    'net-tools: for -auth guess'
    'tk: for GUI support'
    'xf86-video-dummy: for Xdummy script')
makedepends=('git')
source=("git+https://github.com/LibVNC/x11vnc.git#commit=${_commit}?signed"
        'x11vnc.service')
validpgpkeys=('968479A1AFF927E37D1A566BB5690EEEBB952194')
sha256sums=('8fb290d3c56970441651f541f1dff3993c6b17147b84483a38f607ef02875eb8'
            'cfb19d44e09e960e2fdb958c9258bccf23c2677715314985f7e819f1dcedb6e4')

prepare() {
	autoreconf -fi x11vnc
}

build() {
	cd "$pkgname"
	./configure --prefix='/usr' --mandir='/usr/share/man' --with-x
	make
}

package() {
	make -C x11vnc DESTDIR="$pkgdir" install
	install x11vnc/misc/{rx11vnc,Xdummy} -t "${pkgdir}/usr/bin"
	install -D -m644 x11vnc.service -t "${pkgdir}/usr/lib/systemd/system"
}
