# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-tray-loader
pkgver=2.0.12
pkgrel=1
pkgdesc='A set of tray plugins that integrated into task bar and the tool loader which can load the plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-tray-loader"
license=('GPL-3.0-or-later')
depends=('dconf' 'deepin-api' 'deepin-daemon' 'deepin-qt6platform-plugins' 'dtk6core' 'dtk6gui'
         'dtk6widget' 'gcc-libs' 'glibc' 'libx11' 'libxcb' 'libxcursor' 'libxtst' 'qt6-base'
         'qt6-svg' 'qt6-wayland' 'wayland' 'xcb-util-image' 'xcb-util-wm')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'ninja' 'qt6-tools')
source=("git+https://github.com/linuxdeepin/dde-tray-loader.git#tag=$pkgver")
sha512sums=('92076d416177ad3af94d52ef3f6ff1f78c230d1fd9dc87dccf14a92a9a03bfb28f7aa385d45af49cca5c5a1bca4a7332b7550dfda84c75c5b527264d080c81ba')

build() {
  cd dde-tray-loader
  cmake -B build -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  cd dde-tray-loader/build
  DESTDIR="$pkgdir" ninja install
}
