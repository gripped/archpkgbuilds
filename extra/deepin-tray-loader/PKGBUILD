# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-tray-loader
pkgver=1.0.1
pkgrel=1
pkgdesc='A set of tray plugins that integrated into task bar and the tool loader which can load the plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-tray-loader"
license=('LGPL3')
depends=('dconf' 'deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'dtkwidget' 'gcc-libs' 'glibc'
         'gsettings-qt' 'libdbusmenu-qt5' 'libx11' 'libxcb' 'libxcursor' 'libxtst'
         'networkmanager-qt5' 'qt5-base' 'qt5-svg' 'qt5-wayland' 'wayland' 'xcb-util-image'
         'xcb-util-wm')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'ninja' 'qt5-tools')
source=("git+https://github.com/linuxdeepin/dde-tray-loader.git#tag=$pkgver")
sha512sums=('15aacb8cf46de34861c6d929e7e8603140a47317c1ff3beb3a8196581c75df19350dfe36acd4227e99e2c7327562d8a8bc1200e8b635fc959612750dbdfd0c30')

build() {
  cd dde-tray-loader
  cmake -B build -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  cd dde-tray-loader/build
  DESTDIR="$pkgdir" ninja install
}
