# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-tray-loader
pkgver=2.0.22
pkgrel=1
pkgdesc='A set of tray plugins that integrated into task bar and the tool loader which can load the plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-tray-loader"
license=('GPL-3.0-or-later')
depends=('dconf' 'deepin-api' 'deepin-daemon' 'deepin-qt6platform-plugins' 'dtk6core' 'dtk6gui'
         'dtk6widget' 'gcc-libs' 'glibc' 'kwindowsystem' 'libx11' 'libxcb' 'libxcursor' 'libxtst'
         'qt6-base' 'qt6-svg' 'wayland' 'xcb-util-image' 'xcb-util-wm')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'ninja' 'qt6-tools')
source=("git+https://github.com/linuxdeepin/dde-tray-loader.git#tag=$pkgver")
sha512sums=('3c00d204493d9d1da60963b65c32625cbf78c47cd73be414af474d18c6e412556fffe5cd0a5121ac3eba664b90fab74b9d73f2ef8e900b60fc0d30bffaf6e0ec')

build() {
  cd dde-tray-loader
  cmake -B build -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  cd dde-tray-loader/build
  DESTDIR="$pkgdir" ninja install
}
