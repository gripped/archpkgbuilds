# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-tray-loader
pkgver=2.0.12
pkgrel=3
pkgdesc='A set of tray plugins that integrated into task bar and the tool loader which can load the plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-tray-loader"
license=('GPL-3.0-or-later')
depends=('dconf' 'deepin-api' 'deepin-daemon' 'deepin-qt6platform-plugins' 'dtk6core' 'dtk6gui'
         'dtk6widget' 'gcc-libs' 'glibc' 'libx11' 'libxcb' 'libxcursor' 'libxtst' 'qt6-base'
         'qt6-svg' 'wayland' 'xcb-util-image' 'xcb-util-wm')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'ninja' 'qt6-tools')
source=("git+https://github.com/linuxdeepin/dde-tray-loader.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('92076d416177ad3af94d52ef3f6ff1f78c230d1fd9dc87dccf14a92a9a03bfb28f7aa385d45af49cca5c5a1bca4a7332b7550dfda84c75c5b527264d080c81ba'
            'bb2e4779672c533652efde32037b109a34ed89e8bf7b1ea5eeaa46f2d94d813355560dc46c1f170011812f9bf55de6d7cbc739637ff772ae5a513f8c7d9b1149')

prepare() {
  cd dde-tray-loader
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd dde-tray-loader
  cmake -B build -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  cd dde-tray-loader/build
  DESTDIR="$pkgdir" ninja install
}
