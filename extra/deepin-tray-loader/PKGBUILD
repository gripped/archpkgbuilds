# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-tray-loader
pkgver=2.0.15
pkgrel=1
pkgdesc='A set of tray plugins that integrated into task bar and the tool loader which can load the plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-tray-loader"
license=('GPL-3.0-or-later')
depends=('dconf' 'deepin-api' 'deepin-daemon' 'deepin-qt6platform-plugins' 'dtk6core' 'dtk6gui'
         'dtk6widget' 'gcc-libs' 'glibc' 'libx11' 'libxcb' 'libxcursor' 'libxtst' 'qt6-base'
         'qt6-svg' 'wayland' 'xcb-util-image' 'xcb-util-wm')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'ninja' 'qt6-tools')
source=("git+https://github.com/linuxdeepin/dde-tray-loader.git#tag=$pkgver")
sha512sums=('10de6056df4c12a50deeb0b7e3320acb182c7f0ae09903f7036801732b7a8d23dad1bc70e142362a6063c5f9de6e6d1f4e75c4eefd49dff565d9a5060e9300cd')

build() {
  cd dde-tray-loader
  cmake -B build -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  cd dde-tray-loader/build
  DESTDIR="$pkgdir" ninja install
}
