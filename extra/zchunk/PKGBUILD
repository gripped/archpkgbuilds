# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Yurii Kolesnykov <root@yurikoles.com>
# Contributor: Nikita Puzyryov <PuzyryovN@gmail.com>

pkgname=zchunk
pkgver=1.5.2
pkgrel=1
pkgdesc="A file format designed for highly efficient deltas while maintaining good compression"
url="https://github.com/zchunk/zchunk"
arch=('x86_64')
license=('BSD-2-Clause')
depends=('glibc' 'libcurl.so' 'openssl' 'zstd')
makedepends=('meson')
provides=('libzck.so')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('b7346d950fec2e0c72761f2a9148b0ece84574c49076585abf4bebd369cd4c60')

build() {
	cd "${pkgname}-${pkgver}" 

	arch-meson build
	meson compile -C build
}

check() {
	cd "${pkgname}-${pkgver}" 

	meson test -C build
}

package() {
	cd "${pkgname}-${pkgver}" 

	DESTDIR="${pkgdir}" meson install -C build
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
