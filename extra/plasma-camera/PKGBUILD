# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=plasma-camera
pkgver=2.1.1
pkgrel=2
pkgdesc='Camera application for Plasma Mobile'
arch=(x86_64)
url='https://apps.kde.org/plasma.camera/'
license=(GPL-3.0-or-later)
depends=(
  exiv2
  gcc-libs
  glibc
  kconfig
  kcoreaddons
  ki18n
  kirigami
  kirigami-addons
  libcamera
  qt6-base
  qt6-declarative
  qt6-multimedia
  qt6-sensors
)
makedepends=(
  cmake
  extra-cmake-modules
  git
  ninja
)
source=("git+https://invent.kde.org/plasma-mobile/plasma-camera.git?signed#tag=v$pkgver")
b2sums=(adaaf7b82a48b86cf8e7b48792804e511a855bf5a9ef8b6856d6072ee511b710bb2f5ad089590d7de7fe9003531c74d79871526e2b44b2b976d34acab8bef403)
validpgpkeys=(23919AE71103C02137422E3C5E023F97C5BB4C13) # Devin Lin <espidev@gmail.com>

prepare() {
  cd $pkgname

  # Fix selecting default audio input
  # https://invent.kde.org/plasma-mobile/plasma-camera/-/merge_requests/52
  git cherry-pick -n 19b26dd63af7736f7672de4501361c1950dc34d9
}

build() {
  cmake -S $pkgname -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
