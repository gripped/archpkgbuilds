# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: CosmicHorror <CosmicHorrorDev@pm.me>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-insta
_pkgname=insta
pkgver=1.40.0
pkgrel=3
pkgdesc="Cargo plugin for snapshot testing in Rust"
url="https://github.com/mitsuhiko/insta"
depends=('gcc-libs' 'cargo')
arch=('x86_64')
license=('Apache-2.0')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('94fe673475c2e73125831e851f6b207c505f8a0432f4e14e691046ae2dbf34f4103badce3d86074ae2858981b7c7e9c266baa288ce9a2c0056818c4c23e0451b')

prepare() {
  cd "$_pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$_pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/$pkgname"
}

# vi: filetype=sh shiftwidth=2 expandtab
