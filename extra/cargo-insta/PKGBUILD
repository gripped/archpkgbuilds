# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: CosmicHorror <CosmicHorrorDev@pm.me>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-insta
_pkgname=insta
pkgver=1.42.0
pkgrel=1
pkgdesc="Cargo plugin for snapshot testing in Rust"
url="https://github.com/mitsuhiko/insta"
depends=('gcc-libs' 'cargo')
checkdepends=('git')
arch=('x86_64')
license=('Apache-2.0')
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
  "$pkgname-$pkgver-fix-tests.patch::https://github.com/mitsuhiko/insta/commit/949ac940aec545dad5d971e83abe44d11e35982d.patch"
)
sha512sums=('cc37ff8c499868c1d6a65b2097f44ea3beacba175efa94aae74d36d0c84b13e4286edd3b24c3d26524bef122c7af52304fca7566bf1281cf12f04cf55c48036d'
            'c7ef8ee25dbfa97fc4ab52bca9b454e4f7f18abd2dc97649b592a76e718e93db870400bb3f60eb01248bc57ed94fe9a36332cac5117c717b4e8cbe0c7a3b451e')

prepare() {
  cd "$_pkgname-$pkgver"
  patch -Np1 -i ../$pkgname-$pkgver-fix-tests.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$_pkgname-$pkgver"
  # workspace test checks if srcdir ends with "insta"
  cargo test --frozen --all-features -- --skip "test_get_cargo_workspace"
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/$pkgname"
}

# vi: filetype=sh shiftwidth=2 expandtab
