# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=profile-sync-daemon
pkgver=6.55
pkgrel=1
epoch=1
pkgdesc="Symlinks and syncs browser profile dirs to RAM"
arch=(any)
url="https://github.com/graysky2/profile-sync-daemon"
license=(MIT)
depends=(bash findutils procps-ng rsync systemd)
optdepends=('zsh-completions: for completion when using zsh')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('8d885b35900848a905c0c1c77fdb34aca4bf551b59211cd884041658ab97340e34dd86678ecb4dbbeef0325b7dada171c8fef106edf83b5f02b7da5fdc581b95')
b2sums=('29753477db6dda3ec72b030f0b0a032d5ed42ef594c1b848fc57a2fda73c6e13939100b4ed962e2ff5410ec213693043e9d1cb2d3b9bdd815d4493420727de82')

build() {
  make -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
