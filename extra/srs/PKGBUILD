# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Dragutin Cirkovic <dragonmen@gmail.com>
# Contributor: heinrich5991 <heinrich5991@gmail.com>

pkgname=srs
pkgver=5.0_r3
pkgrel=2
pkgdesc="High performance realtime video server"
arch=('x86_64')
url="https://github.com/ossrs/srs"
license=('MIT')
depends=('openssl'
         'srt'
         'libsrtp'
         'srs-state-threads'
         'tcl')
makedepends=('zlib'
             'unzip'
             'net-tools'
             'python'
             'git'
             'cmake')
backup=(etc/srs/srs.conf)
options=('docs')
source=("git+https://github.com/ossrs/srs/#tag=v${pkgver/_/-}")
sha256sums=('8c77718dc7745120fc59cda75db44666d6b0bc868483cd499ad71775c2934959')

build() {
  cd "${srcdir}"/srs/trunk

  export CMAKE_POLICY_VERSION_MINIMUM=3.5

  ./configure --prefix=/install --use-sys-ssl --use-shared-st --generic-linux=on
  make
  make DESTDIR="${srcdir}" install
}

package() {
  install -Dm644 "${srcdir}"/srs/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "${srcdir}"/install
  install -d "${pkgdir}"/usr/bin "${pkgdir}"/etc/srs
  cp -r conf/. "${pkgdir}"/etc/srs
  install -m755 objs/srs "${pkgdir}"/usr/bin/srs
}
