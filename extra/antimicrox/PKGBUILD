# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=antimicrox
pkgver=3.5.0
pkgrel=1
pkgdesc='Graphical program used to map keyboard buttons & mouse controls to a gamepad'
arch=('x86_64')
url='https://github.com/AntiMicroX/antimicrox'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'gcc-libs'
  'hicolor-icon-theme'
  'libxtst'
  'libxi'
  'libx11'
  'qt5-base'
  'sdl2'
)
makedepends=(
  'git'
  'cmake'
  'extra-cmake-modules'
  'itstool'
  'qt5-tools'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('e7e2bfa3b7af8b46a8e0136ebfdad8d4cace18d8344021a9dcae9d2ef99975ebb43e0afdeb2d131369da02b5da20cbddd156ab4206691c54c17bc03f99e8f5d9')
b2sums=('045f1e10002f32878729a042b9bface09b7f454721157cadecf29526957c9b155663574c5cfbac1962ef13f968b7c4dc4c05636754704eaf8dabda6d9db5e411')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -D CMAKE_BUILD_TYPE=None \
    -D ANTIMICROX_PKG_VERSION="$pkgver"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
