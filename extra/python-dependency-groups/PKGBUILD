# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Daniel Bershatsky <bepshatsky@yandex.ru>

_name=dependency-groups
pkgname=python-$_name
pkgver=1.3.1
pkgrel=2
pkgdesc="A standalone implementation of PEP 735 Dependency Groups"
arch=(any)
url="https://github.com/pypa/dependency-groups"
license=(MIT)
depends=(
  python
  python-packaging
  python-tomli
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
b2sums=('d63d8dabab400f1d6f614114ec632d1a90ac08d2905647fdaddfe0803a9e5d1c8731b6c0f18f7f8ee0e21761ba2c837fb12a50aae13fffc9961feb344e736dbe')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
