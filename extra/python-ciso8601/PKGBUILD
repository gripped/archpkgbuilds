# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-ciso8601
pkgver=2.3.2
pkgrel=1
pkgdesc='Fast ISO8601 date time parser for Python written in C'
arch=(x86_64)
url=https://github.com/closeio/ciso8601
license=(MIT)
depends=(
  glibc
  python
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=9412b8c6fe9905d64e48e5571cb0ef48d16bdd78
source=(git+https://github.com/closeio/ciso8601.git#tag=${_tag})
sha256sums=('a481e1b24c3ea0a7944960755700ccbf23e2908257df9f768318e2e4d713f2e3')

pkgver() {
  cd ciso8601

  git describe --tags | sed 's/^v//'
}

build() {
  cd ciso8601
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" ciso8601/dist/*.whl
  install -Dm 644 ciso8601/LICENSE -t "${pkgdir}"/usr/share/licenses/python-ciso8601/
}

# vim: ts=2 sw=2 et:
