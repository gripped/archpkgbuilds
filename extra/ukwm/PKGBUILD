# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukwm
pkgver=1.2.3
pkgrel=1
pkgdesc="A lightweight GTK+ window manager"
arch=('x86_64')
license=('GPL-2.0-or-later')
url="https://github.com/ukui/ukwm"
groups=('ukui')
depends=('at-spi2-core' 'cairo' 'dconf' 'fontconfig' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'gnome-desktop' 'gsettings-desktop-schemas' 'gtk3'
         'json-glib' 'libcanberra' 'libdrm' 'libgirepository' 'libglvnd' 'libgudev' 'libice' 'libinput' 'libsm' 'libwacom' 'libx11' 'libxcb'
         'libxcomposite' 'libxcursor' 'libxdamage' 'libxext' 'libxfixes' 'libxi' 'libxinerama' 'libxkbcommon' 'libxkbcommon-x11' 'libxkbfile'
         'libxrandr' 'libxrender' 'libxtst' 'mesa' 'pango' 'startup-notification' 'systemd-libs' 'ukui-settings-daemon' 'upower' 'wayland' 'zenity')
makedepends=('glib2-devel' 'gobject-introspection' 'intltool' 'wayland-protocols')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukwm/archive/v$pkgver.tar.gz")
sha512sums=('6ba134f12e9d3f87de6b1962f64fdf8c171495d4fc5b6751cd12a9e81c479d071841e96f6f3b28ce11a7fba946cb448dadf7e6c88bd770ecb1f4e19e07f9a309')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc --enable-introspection --enable-compile-warnings=yes CFLAGS="$CFLAGS -std=gnu17"
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
