# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=arithmoi
pkgname=haskell-arithmoi
pkgver=0.13.0.1
pkgrel=29
pkgdesc="Efficient basic number-theoretic functions."
url="https://github.com/Bodigrim/arithmoi"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-chimera' 'haskell-constraints' 'haskell-exact-pi'
         'haskell-infinite-list' 'haskell-integer-logarithms' 'haskell-integer-roots' 'haskell-mod'
         'haskell-random' 'haskell-semirings' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-quickcheck-classes' 'haskell-smallcheck'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-tasty-rerun'
             'haskell-tasty-smallcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('00d414d5b7e0f897e479c89fde9ba0807de5e1b3a50f43a59f7899029ed36235d5f7ee41ba26ae721cb0e82f025144e14fb5598058820a30bc330b499f06ea0d')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
