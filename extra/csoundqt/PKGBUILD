# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ainola
# Contributor: rtfreedman

_name=CsoundQt
pkgname=csoundqt
pkgver=1.1.3
pkgrel=2
epoch=1
pkgdesc="A frontend for Csound"
arch=(x86_64)
url="https://csoundqt.github.io/"
license=(
  CC-BY-SA-4.0
  GFDL-1.3-only
  GPL-2.0-or-later
  LGPL-2.1-or-later
  LGPL-3.0-or-later
)
groups=(pro-audio)
depends=(
  csound-doc
  gcc-libs
  glibc
  hicolor-icon-theme
  qt5-base
  qt5-declarative
)
makedepends=(
  csound
  rtmidi
)
optdepends=('python: for examples')
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('f3315a2d161e2247610d61e55f57d522037d39693d2986c273cee69f5e829872dab2a3c25eaff7d811fd032c83af6f6be93127863d79f4de1b9565eabbff0f34')
b2sums=('d46a8e5dd855d682d0876835155d7caba78b3631b0ce5b857e02ba5ec352c393c29009918d949dbd2091f289828fd2446a25b10e779eb53c453f897218a684f8')

build() {
  cd $_name-$pkgver
  qmake-qt5 qcs.pro \
    SHARE_DIR="$pkgdir/usr/share" \
    INSTALL_DIR="$pkgdir/usr" \
    CONFIG+=rtmidi \
    CONFIG+=record_support
#   CONFIG+=html_webengine - depends on qtwebengine, reenable when ported to Qt 6
  make
}

package() {
  depends+=(
    csound libcsnd6.so libcsound64.so
    rtmidi librtmidi.so
  )

  make DESTDIR="${pkgdir}" install -C $_name-$pkgver
  install -vDm 644 $_name-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
