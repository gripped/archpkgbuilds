# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=fsnotify
pkgname=haskell-fsnotify
pkgver=0.4.1.0
pkgrel=15
pkgdesc="Cross platform library for file change notification."
url="https://github.com/haskell-fswatch/hfsnotify"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-hinotify' 'haskell-monad-control'
         'haskell-safe-exceptions' 'haskell-unix-compat')
makedepends=('ghc' 'haskell-retry' 'haskell-sandwich' 'haskell-temporary' 'haskell-unliftio')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('bf924fb9712572e1ded14c630b1fab98daadb81cc87bbd18964176e0db3b41e1024744b996bcc205c7a57f55e63fc28db0ae4fa4009623d26316d1322ddf1c03')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
