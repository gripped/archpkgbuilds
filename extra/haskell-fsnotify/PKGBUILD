# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=fsnotify
pkgname=haskell-fsnotify
pkgver=0.4.3.0
pkgrel=25
pkgdesc="Cross platform library for file change notification."
url="https://github.com/haskell-fswatch/hfsnotify"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-hinotify' 'haskell-monad-control'
         'haskell-monad-logger' 'haskell-random' 'haskell-retry' 'haskell-safe-exceptions'
         'haskell-string-interpolate' 'haskell-temporary' 'haskell-unix-compat' 'haskell-unliftio')
makedepends=('ghc' 'haskell-sandwich')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('344f4598ce5dfe977bce89699ac4ed06565fd5d4702d8f75546f88f4f1cbdf29fab771dcb6353e102a0f2906cbe6f0673378ae5fb8e711f56af61c08467de4c8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

    mv "$pkgdir"/usr/bin/{,fsnotify-}example
}
