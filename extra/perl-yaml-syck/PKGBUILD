# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-yaml-syck
pkgver=1.36
pkgrel=1
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('x86_64')
url="https://search.cpan.org/dist/YAML-Syck/"
license=('BSD' 'custom')
depends=('perl')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/T/TO/TODDR/YAML-Syck-$pkgver.tar.gz)
sha512sums=('596ddbb0502da94c78d4398b7f66311362b0bff1f9dd0ee58199e7a963d0a985f32404de365784d296d16579f6e288b0aade38988f58aaa1c9f82790eac97b01')

build() {
  cd YAML-Syck-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor OPTIMIZE="$CFLAGS -std=gnu17"
  make
}

check() {
  cd YAML-Syck-$pkgver
  make test
}

package() {
  cd YAML-Syck-$pkgver
  make install DESTDIR="${pkgdir}"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
