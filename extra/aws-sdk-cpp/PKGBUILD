# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=aws-sdk-cpp
pkgver=1.11.472
pkgrel=1
pkgdesc='AWS SDK for C++'
arch=(x86_64)
url='https://github.com/aws/aws-sdk-cpp'
license=(Apache-2.0)
depends=(
  aws-c-auth
  aws-c-common
  aws-c-event-stream
  aws-c-http
  aws-c-io
  aws-c-s3
  aws-checksums
  aws-crt-cpp
  curl
  gcc-libs
  glibc
  libpulse
  zlib
)
makedepends=(cmake)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('33bf070483dbdba3f6866e6113e5649c992c3695cc59c84a8cd6ed2bb30f168f')

prepare() {
  cd $pkgname-$pkgver
  # Remove failing test - unsure why it fails.
  rm tests/aws-cpp-sdk-core-tests/aws/auth/AWSAuthSignerTest.cpp
}

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_DEPS=OFF \
    -DCUSTOM_MEMORY_MANAGEMENT=OFF \
    -DAUTORUN_UNIT_TESTS=OFF
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  # Infer non-generated unit test targets by if their CMakeLists.txt contain
  # the variable AUTORUN_UNIT_TESTS:
  local unit_tests=$(
    grep AUTORUN_UNIT_TESTS -l -r tests/ \
      | awk -F'/' '{print $2}'
  )
  for test in $unit_tests; do
    cmake --build build --target "$test"
    "./build/tests/$test/$test"
  done

  local generated_unit_tests=$(
    cmake --build build --target help \
      | grep gen-tests \
      | awk '{print $2}'
  )
  for test in $generated_unit_tests; do
    cmake --build build --target "$test"
    "./build/generated/tests/$test/$test"
  done
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
}
