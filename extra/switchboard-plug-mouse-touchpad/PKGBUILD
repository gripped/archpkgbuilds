# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-mouse-touchpad
pkgver=8.0.2
pkgrel=2
pkgdesc='Switchboard Mouse & Touchpad Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-mouse-touchpad
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
  libxml2
)
makedepends=(
  git
  meson
  vala
)
_tag=f71fd55d1bd6efda613bda26fba357fe759573c9
source=(git+https://github.com/elementary/switchboard-plug-mouse-touchpad.git#tag=${_tag})
b2sums=('063a93b8bafcb12e73e05f7a73ff3d63ec9e7f96d772cd0374f79606a6c8538e7864756675824d0de8ad395fb83629a10d8af6f5608594ce83648624b65a6f77')

pkgver() {
  cd switchboard-plug-mouse-touchpad
  git describe --tags
}

build() {
  arch-meson switchboard-plug-mouse-touchpad build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
