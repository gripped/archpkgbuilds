# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-mouse-touchpad
pkgver=8.0.1
pkgrel=1
pkgdesc='Switchboard Mouse & Touchpad Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-mouse-touchpad
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
  libxml2
)
makedepends=(
  git
  meson
  vala
)
_tag=4028de94b2b295f838c20e75f93455a70169bed7
source=(git+https://github.com/elementary/switchboard-plug-mouse-touchpad.git#tag=${_tag})
b2sums=('08a9d85bea89eeb5231bd027b59782662467002e54cc983a0f2904a04fab205241297d3e65ead0cd3b6fe943502f3ba7417923b9c0dbe10bf88351829dde216c')

pkgver() {
  cd switchboard-plug-mouse-touchpad
  git describe --tags
}

build() {
  arch-meson switchboard-plug-mouse-touchpad build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
