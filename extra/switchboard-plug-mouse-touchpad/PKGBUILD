# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-mouse-touchpad
pkgver=8.0.3
pkgrel=1
pkgdesc='Switchboard Mouse & Touchpad Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-mouse-touchpad
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
  libxml2
)
makedepends=(
  git
  meson
  vala
)
_tag=7b37169e0cb99df48e920ff584f602d6186925c5
source=(git+https://github.com/elementary/switchboard-plug-mouse-touchpad.git#tag=${_tag})
b2sums=('e649a16c7f1363054935efdc83e285a00101cedefcfe84124e76fa061c4c5bfceb57ca5f6b6ee06cea57405c8712fb6e4edba279878b17ea6ca04ce96843c805')

pkgver() {
  cd switchboard-plug-mouse-touchpad
  git describe --tags
}

build() {
  arch-meson switchboard-plug-mouse-touchpad build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
