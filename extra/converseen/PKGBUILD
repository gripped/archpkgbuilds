# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.12.2.4
pkgrel=1
pkgdesc='Batch image converter and resizer'
arch=('x86_64')
url='http://converseen.fasterland.net/'
license=('GPL3')
depends=('hicolor-icon-theme' 'imagemagick' 'qt6-base')
makedepends=('cmake' 'qt6-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('8b69271fa035f3efedf2df12a1f26708f08bcf6d9b49b775273aad3d4aa74d94')

prepare() {
  cd $pkgname-$pkgver
  convert res/converseen.png +set date:create +set date:modify -resize 256x256 -alpha on res/converseen.png
  sed -i 's|/share/pixmaps|/share/icons/hicolor/256x256/apps|' CMakeLists.txt
# Install service menu to modern path
  sed -e 's|kservices5/ServiceMenus|kio/servicemenus|' -i CMakeLists.txt
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT6=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
