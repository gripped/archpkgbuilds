# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.15.0.2
pkgrel=1
pkgdesc='Batch image converter and resizer'
arch=('x86_64')
url='https://converseen.fasterland.net/'
license=('GPL3')
depends=('hicolor-icon-theme' 'imagemagick' 'qt6-base')
makedepends=('cmake' 'qt6-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('3588c4a6e21573fe9dc59be756408efa07a024a50339c10b78db9613bc9131ff')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT6=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
