# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.15.2.0
pkgrel=1
pkgdesc="Batch image converter and resizer"
arch=('x86_64')
url="https://${pkgname}.fasterland.net"
license=('GPL-3.0-or-later')
depends=('glibc' 'hicolor-icon-theme' 'imagemagick' 'libgcc' 'libstdc++' 'qt6-base')
makedepends=('cmake' 'qt6-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('447ffc21c9470b666ac0123cacca93ca370b923329cf29ffe9a63f61a97bfb50')

build() {
	cmake -B build \
		-S "${pkgname}-${pkgver}" \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D USE_QT6=ON

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
