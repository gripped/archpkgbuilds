# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.15.1.2
pkgrel=1
pkgdesc="Batch image converter and resizer"
arch=('x86_64')
url="https://${pkgname}.fasterland.net"
license=('GPL-3.0-or-later')
depends=('hicolor-icon-theme' 'imagemagick' 'qt6-base')
makedepends=('cmake' 'qt6-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('f052433e956ec5371824e99d48f66374838c4f84c942caa82d06512f9230a809')

build() {
	cmake -B build \
		-S "${pkgname}-${pkgver}" \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D USE_QT6=ON

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
