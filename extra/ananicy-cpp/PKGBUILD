# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>
# Contributor: Antoine Viallon <antoine@lesviallon.fr>

pkgname=ananicy-cpp
pkgver=1.1.1
pkgrel=10
pkgdesc="Ananicy Cpp is a full rewrite of Ananicy in C++, featuring lower CPU and RAM usage."
arch=(x86_64)
url="https://gitlab.com/ananicy-cpp/ananicy-cpp"
license=('GPL-3.0-or-later')
depends=(
  gcc-libs
  glibc
  libbpf
  libelf
  libfmt.so
  libspdlog.so
  systemd
  systemd-libs
  zlib
)
makedepends=(
  bpf
  clang
  cmake
  git
  ninja
  nlohmann-json
)
source=("git+https://gitlab.com/ananicy-cpp/ananicy-cpp.git#tag=v${pkgver}"
        "https://gitlab.com/ananicy-cpp/ananicy-cpp/-/commit/99e64815bacaf3baa28ad89d022e33ebede94fa9.patch")
sha512sums=('e7aed4f39d50ac2ea300b08192d1f0000e91c6985111418c24551252243ca883551afe290c8cb9fc4fce213d1ed6b1e919f93600abeaa8176b448b4f28bf3c3d'
            'b04c6890dafb5d878fa0b0b1ea4611ddcc7c4d37ac327829255f50e8ed92940bb9aa3d22c9969bdcf2f49af74fc09317ae86558db0703d6aa2059b6194cba42e')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -Np1 < ../99e64815bacaf3baa28ad89d022e33ebede94fa9.patch
}
build() {
  cd "${srcdir}/${pkgname}"

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_EXTERNAL_SPDLOG=ON \
        -DUSE_EXTERNAL_JSON=ON \
        -DUSE_EXTERNAL_FMTLIB=ON \
        -DENABLE_SYSTEMD=ON \
        -DUSE_BPF_PROC_IMPL=ON \
        -DBPF_BUILD_LIBBPF=OFF \
        -DVERSION=${_pkgver}

  cmake --build build --target ananicy-cpp
}

package() {
  cd "${srcdir}/${pkgname}"
  DESTDIR="${pkgdir}" cmake --install build --component Runtime

  install -m755 -d "${pkgdir}/etc/ananicy.d"
}

# vim:set sw=2 sts=2 et:
