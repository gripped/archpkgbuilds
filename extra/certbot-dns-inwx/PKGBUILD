# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='certbot-dns-inwx'
pkgver=3.0.1
pkgrel=1
pkgdesc="INWX DNS authenticator plugin for certbot"
arch=('any')
url="https://github.com/oGGy990/certbot-dns-inwx"
license=('Apache')
depends=(
  certbot
  python
  python-inwx-domrobot
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  python-dnspython
  python-pytest
)
optdepends=(
  'python-dnspython: needed for CNAME redirects'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('73ff2ea8a9effca4e4d5a4d57822167d5a92fb28f947d6c96181cda31fac155aa626dafb56896696e3788ebb54cf45786c8dbb7a9799fc5abb4710679bbef010')
b2sums=('842f79077aea8eccfac4f1dba73d8fdb56e0b8be2c7d9aa479948620abcd53338d85441030d6a50679144ff105e7da791b287a60464e37ea5b4dce9cf3ba1cf3')
backup=(
  'etc/letsencrypt/inwx.cfg'
)

build() {
  cd "${pkgname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${pkgname}-${pkgver}"

  pytest -vv
}

package() {
  cd "${pkgname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install --verbose -D --mode=0600 inwx.cfg "${pkgdir}/etc/letsencrypt/inwx.cfg"
  install --verbose -D --mode=0644 README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
