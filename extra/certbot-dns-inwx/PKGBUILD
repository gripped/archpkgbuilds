# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='certbot-dns-inwx'
pkgver=3.0.2
pkgrel=1
pkgdesc="INWX DNS authenticator plugin for certbot"
arch=('any')
url="https://github.com/oGGy990/certbot-dns-inwx"
license=('Apache')
depends=(
  certbot
  python
  python-inwx-domrobot
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  python-dnspython
  python-pytest
)
optdepends=(
  'python-dnspython: needed for CNAME redirects'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('70493e26f66a774fe4290fda03a36941d5d62f3f065f582ea8bdc28c233e09f8df480f987f500379b40b5e9c7c4602dd868a2fa8f959f291c1d51aacc2c6d73a')
b2sums=('81161fa84187f2e9b61ffd1614d2423378dd9a85642d82a62dc8261d728c80bf0b007aa4029b8c1a20e97343b0f590b2e59a5753548513215f902de73f1c3dd1')
backup=(
  'etc/letsencrypt/inwx.cfg'
)

build() {
  cd "${pkgname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${pkgname}-${pkgver}"

  pytest -vv
}

package() {
  cd "${pkgname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install --verbose -D --mode=0600 inwx.cfg "${pkgdir}/etc/letsencrypt/inwx.cfg"
  install --verbose -D --mode=0644 README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
