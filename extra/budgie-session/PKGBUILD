# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=budgie-session
pkgver=1.0.0
pkgrel=1
pkgdesc="The Budgie Desktop session handler"
arch=(x86_64)
url="https://github.com/BuddiesOfBudgie/budgie-session"
license=(GPL-2.0-or-later)
groups=(budgie)
depends=(
	bash
	dconf
	gcc-libs
	glib2
	glibc
	gnome-desktop
	gsettings-desktop-schemas
	gtk3
	json-glib
	libepoxy
	libgl
	libice
	libsm
	libx11
	libxcomposite
	systemd-libs
)
makedepends=(
	docbook-xsl
	git
	glib2-devel
	gtk-doc
	mesa
	meson
	xmlto
	xtrans
)
source=("git+https://github.com/BuddiesOfBudgie/budgie-session.git#tag=v$pkgver")
b2sums=('3bc5f66ce287c8b9bd1a873753457902cf9a13446d7e8088bb9ba1dfacd9dff5ba04efbd64f3bd4407fa5d0be27bee21fac26d7a9031edf128726e52d50148f5')
validpgpkeys=(
	1E1FB0017C998A8AE2C498A6C2EAA8A26ADC59EE # David Mohammed <fossfreedom at ubuntu dot com>
	0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B # Joshua Strobl <me at joshuastrobl dot com>
)

build() {
	arch-meson $pkgname build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
