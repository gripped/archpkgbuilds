# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=castget
pkgver=2.0.1
pkgrel=4
pkgdesc="Command-line based RSS enclosure downloader primarily intended for downloading of podcasts"
arch=('x86_64')
url="https://johndal.com/castget/"
license=('LGPL-2.1-or-later')
depends=(
  'curl'
  'glib2'
  'glibc'
  'id3lib'
  'libxml2'
)
source=(
  "https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2"
  "https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2.sig"
  "fix-build.patch"
)
b2sums=('a4c74857fdc301121f82468565c11aceae386133a7b1472617e9fa7ac7395fb64f19871cf56380fa3e7b3115679ba6ad8fd5c15a6f75bc683045df6c6f0fd265'
        'SKIP'
        'bf96f3d9df86f04880070cd4a55f5d4df0042e7b1f833cc6705eb790cd8d0f004f98392c94bb07ae3179d7377c5057ffa595a279bbafc2938befa242dbd6d0be')
validpgpkeys=('561BCBC3DF8A53503A62416EA7DE43E1D881FF60') # Marius Larsen JÃ¸hndal <mariuslj@ifi.uio.no>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
  # Naively replace deprecated curl option CURLOPT_PROGRESSFUNCTION with
  # CURLOPT_XFERINFOFUNCTION and add a missing include.
  patch -Np1 < ../fix-build.patch
}

build() {
  cd $pkgname-$pkgver
  CFLAGS+=" -std=gnu17" ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
