# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrián Pérez de Castro <aperez@igalia.com>

pkgname=lowdown
pkgver=2.0.3
pkgrel=1
pkgdesc='A simple Markdown translator'
arch=(x86_64)
url='https://kristaps.bsd.lv/lowdown'
license=(ISC)
depends=(glibc libmd)
makedepends=(git bmake)
provides=(liblowdown.so)
source=("$pkgname::git+https://github.com/kristapsdz/lowdown#tag=$(echo $pkgver | sed -e 's/^/VERSION_/' -e 's/\./_/g')")
sha512sums=('bcccccd3be9d5114880540673ca756aa5a16f4703f1bf80e5e8f4269d4352fd76c2785da397ca097ca875ea8ebd8667b82d0e5fd5143e0a385aa826417db4c42')
b2sums=('b4affea53d7d7ed19db99aa768d2e8421cd94985a00862236b0eb3170e2389c512c20299606292a1728832e6ebd59b8313f05bd3affc82b91981449a536b2ff8')

build () {
  cd "$pkgname"

	./configure \
    PREFIX=/usr \
    MANDIR=/usr/share/man

  # ensure LDFLAGS is passed correctly
  sed -i "s/^LDFLAGS.*/LDFLAGS = $LDFLAGS/" Makefile.configure

	bmake
}

check () {
	bmake -C "$pkgname" regress
}

package () {
  cd "$pkgname"

  # package
	bmake DESTDIR="$pkgdir" \
    install \
    install_lib_common \
    install_shared

  # symlink unversioned to versioned shared library
  local LIBVER=$(grep "^LIBVER" Makefile | sed "s/.*= //")
  ln -sf "/usr/lib/liblowdown.so.$LIBVER" "$pkgdir/usr/lib/liblowdown.so"

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
