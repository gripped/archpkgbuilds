# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cryptol
pkgver=3.3.0
pkgrel=36
pkgdesc="The Language of Cryptography"
url="https://tools.galois.com/cryptol"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'z3' 'haskell-ansi-terminal' 'haskell-arithmoi' 'haskell-async'
         'haskell-base-compat' 'haskell-blaze-html' 'haskell-bv-sized'
         'haskell-criterion-measurement' 'haskell-cryptohash-sha256' 'haskell-extra'
         'haskell-file-embed' 'haskell-gitrev' 'haskell-graphscc' 'haskell-heredoc' 'haskell-hgmp'
         'haskell-language-c99' 'haskell-language-c99-simple' 'haskell-libbf' 'haskell-libffi'
         'haskell-memotrie' 'haskell-monad-control' 'haskell-monadlib'
         'haskell-optparse-applicative' 'haskell-panic' 'haskell-parameterized-utils'
         'haskell-pretty-show' 'haskell-prettyprinter' 'haskell-sbv' 'haskell-simple-smt'
         'haskell-strict' 'haskell-temporary' 'haskell-tf-random' 'haskell-toml-parser'
         'haskell-transformers-base' 'haskell-vector' 'haskell-what4')
makedepends=('ghc' 'uusi' 'alex' 'happy' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1467009042c041bf795995003faefdc4fd2fa7b24e8dcee66eee99d105c0a3712a55bb5a4d9e2dfcbce0cacb8d4bf4b274dfa00ddb9a2323de493f1999f164b8')

prepare() {
    cd $pkgname-$pkgver
    uusi -u base-compat
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-static -f-relocatable -fffi --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
