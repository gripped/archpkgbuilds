# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Butui Hu <hot123tea123@gmail.com>

pkgname=python-lazy-loader
pkgver=0.4
pkgrel=6
pkgdesc='Populate library namespace without incurring immediate import costs'
arch=(any)
url=https://github.com/scientific-python/lazy_loader
license=(BSD-3-Clause)
depends=(
  python
  python-importlib-metadata
  python-packaging
)
makedepends=(
  git
  python-build
  python-flit-core
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
_tag=4596986a8d276d19e2ad8713ec4fb8329d743a08
source=(git+https://github.com/scientific-python/lazy-loader.git#tag=${_tag})
b2sums=('c929483f1e0a40e5738841740f93d00c481bde4110d75ee1447042dd3931a9ab23ee13e0f88b860461ad2f59e16f24b655ab696866556d2a33b9de5eef89ddd3')

pkgver() {
  cd lazy-loader
  git describe --tags | sed 's/^v//'
}

build() {
  cd lazy-loader
  python -m build --wheel --no-isolation
}

check() {
  cd lazy-loader
  pytest -v
}

package() {
  python -m installer --destdir="${pkgdir}" lazy-loader/dist/*.whl
  install -Dm 644 lazy-loader/LICENSE.md -t "${pkgdir}"/usr/share/licenses/python-lazy-loader/
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf ${pkgdir}${site_packages}/lazy_loader/tests
}

# vim: ts=2 sw=2 et:
