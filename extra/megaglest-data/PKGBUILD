# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=megaglest-data
_pkgbasename=megaglest
pkgver=3.13.0
pkgrel=4
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world (data files)"
arch=('any')
url="https://megaglest.org"
license=('CC-BY-3.0')
makedepends=('cmake' 'xz' 'git')
source=("git+https://github.com/MegaGlest/megaglest-data#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  cd megaglest-data

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr .. \
    -DMEGAGLEST_APPDATA_INSTALL_PATH=share/metainfo
}

package() {
  cd megaglest-data

  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 ../docs/README.data-license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim: sw=2:ts=2 et:
