# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=sheldon
pkgname=('sheldon' 'sheldon-docs')
pkgver=0.8.5
pkgrel=1
pkgdesc='A fast and configurable shell plugin manager'
arch=('x86_64')
url='https://sheldon.cli.rs'
license=('Apache-2.0' 'MIT')
makedepends=(
  'git'
  'rust'
  'mdbook-linkcheck'
  'curl'
)
options=('!lto')
source=("git+https://github.com/rossmacarthur/sheldon.git#tag=$pkgver")
sha512sums=('a3197bf88d9b187b546c6a2ff06a5a99611395d02fb05a4c11fad847255a6423925d2b21c4bf592f374f7dc75c72b0e61d0d61ae5735bd7604e3f2ba45ed3e5a')
b2sums=('779c158aa26e1145c79253077af4ac2bddcef437044590780cad2ec0c0a5a7a90e647d9ce45392ffffeb6da3b5ec10aca371a442cda35df4c5a544b9554b15ae')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgbase"

  # binary
  cargo build --frozen --release --all-features

  # documentation
  mdbook build docs
}

check() {
  cd "$pkgbase"

  cargo test --frozen --all-features
}

package_sheldon() {
  depends=(
    'glibc'
    'gcc-libs'
    'openssl'
    'zlib'
    'curl'
  )
  optdepends=('sheldon-docs: documentation')

  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  install -vDm644 completions/sheldon.bash "$pkgdir/usr/share/bash-completion/completions/$pkgbase"
  install -vDm644 completions/sheldon.zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

package_sheldon-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md

  # html manual
  cp -vr docs/book/html "$pkgdir/usr/share/doc/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
