# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=sheldon
pkgname=('sheldon' 'sheldon-docs')
pkgver=0.8.4
pkgrel=1
pkgdesc='A fast and configurable shell plugin manager'
arch=('x86_64')
url='https://sheldon.cli.rs'
license=('Apache-2.0' 'MIT')
makedepends=(
  'git'
  'rust'
  'mdbook-linkcheck'
  'curl'
)
options=('!lto')
source=("git+https://github.com/rossmacarthur/sheldon.git#tag=$pkgver")
sha512sums=('65a2a9a4b9c383409807a9cb9dfdda0b0ab5f8aa772dc1306f5bb99b2f5055ba02269ccc4e0e802e71bfb27b73ac4787ff43b0b74b20e4326473f3f285abdde3')
b2sums=('00e981f09010cd293b94e76fd1c004c320bc4278dca508fad739aa43e8742088e6be20fd39c7128721480b5f3c9c1fa8adb75289d9b27450e944f362617060ec')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgbase"

  # binary
  cargo build --frozen --release --all-features

  # documentation
  mdbook build docs
}

check() {
  cd "$pkgbase"

  cargo test --frozen --all-features
}

package_sheldon() {
  depends=(
    'glibc'
    'gcc-libs'
    'openssl'
    'zlib'
    'curl'
  )
  optdepends=('sheldon-docs: documentation')

  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  install -vDm644 completions/sheldon.bash "$pkgdir/usr/share/bash-completion/completions/$pkgbase"
  install -vDm644 completions/sheldon.zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

package_sheldon-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md

  # html manual
  cp -vr docs/book/html "$pkgdir/usr/share/doc/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
