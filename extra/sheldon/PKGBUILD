# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=sheldon
pkgname=('sheldon' 'sheldon-docs')
pkgver=0.8.2
pkgrel=1
pkgdesc='A fast and configurable shell plugin manager'
arch=('x86_64')
url='https://sheldon.cli.rs'
license=('Apache-2.0' 'MIT')
makedepends=(
  'git'
  'rust'
  'mdbook-linkcheck'
  'curl'
)
options=('!lto')
source=("git+https://github.com/rossmacarthur/sheldon.git#tag=$pkgver")
sha512sums=('2a0a1b4b5005d62241aa27ad0285ce6c64c26d5334832b71e49c03d30cc5ba6aa19696e25e73da7f8c8b3c3e5b7f515dd3030219b81fec0b2fcccc13a0375438')
b2sums=('ddef8d79d73fb4b7e0286fc9eaa69b6e3c84a956b5a85b0e7fd3cc33e40c99d673e0be874dfcfa3508a203caff3c28173227277705fbd60dc9d196ab68ce7bd1')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgbase"

  # binary
  cargo build --frozen --release --all-features

  # documentation
  mdbook build docs
}

check() {
  cd "$pkgbase"

  cargo test --frozen --all-features
}

package_sheldon() {
  depends=(
    'glibc'
    'gcc-libs'
    'openssl'
    'zlib'
    'curl'
  )
  optdepends=('sheldon-docs: documentation')

  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  install -vDm644 completions/sheldon.bash "$pkgdir/usr/share/bash-completion/completions/$pkgbase"
  install -vDm644 completions/sheldon.zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

package_sheldon-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md

  # html manual
  cp -vr docs/book/html "$pkgdir/usr/share/doc/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
