# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=sheldon
pkgname=('sheldon' 'sheldon-docs')
pkgver=0.8.3
pkgrel=1
pkgdesc='A fast and configurable shell plugin manager'
arch=('x86_64')
url='https://sheldon.cli.rs'
license=('Apache-2.0' 'MIT')
makedepends=(
  'git'
  'rust'
  'mdbook-linkcheck'
  'curl'
)
options=('!lto')
source=("git+https://github.com/rossmacarthur/sheldon.git#tag=$pkgver")
sha512sums=('d53510ab68c8e1f9f9f06104a4042140a1d4cdbe9ef0c79206fe90b4db69820bd129afd917dc414cbeed77903fbdc241a97a31701fe47c3fa8cfddcfa6148d66')
b2sums=('5bc6bc99c5b19ea0b7c8f7df085d7d4abe2db1046c5ff7b0ed0ed41ae823506a981980f79565eefa14dd3d6c03deb2ef7b7218d6602341794c20d651abfe4098')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgbase"

  # binary
  cargo build --frozen --release --all-features

  # documentation
  mdbook build docs
}

check() {
  cd "$pkgbase"

  cargo test --frozen --all-features
}

package_sheldon() {
  depends=(
    'glibc'
    'gcc-libs'
    'openssl'
    'zlib'
    'curl'
  )
  optdepends=('sheldon-docs: documentation')

  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  install -vDm644 completions/sheldon.bash "$pkgdir/usr/share/bash-completion/completions/$pkgbase"
  install -vDm644 completions/sheldon.zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

package_sheldon-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md

  # html manual
  cp -vr docs/book/html "$pkgdir/usr/share/doc/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
