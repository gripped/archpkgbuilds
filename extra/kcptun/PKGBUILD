# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=kcptun
pkgver=20240701
_commit=226c4bac9da529129ebb5bedc79f868e9a9992b4
pkgrel=1
pkgdesc="A Secure Tunnel Based On KCP with N:M Multiplexing"
arch=('x86_64')
url="https://github.com/xtaci/kcptun"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
options=('!lto')
source=("git+https://github.com/xtaci/kcptun.git#commit=$_commit"
        'kcptun@.service' 'kcptun-server@.service')
sha512sums=('c24e6e6b9886c7ce9baf0d4b59c9ce2adf77d91b24d6b9828a7fd80bed846eb304182a1591394ee882cd119ddb2e5ccf9834b7e250a59c260481ed9533f8ee91'
            'f6041eae26db4f11bf6dc22443548354114b8b5a2a436949513a168d5b271707e680dd1d6bdcfc4385945ae2cb1bc82736e605ca49c6c94904922b43d1917a5c'
            '88607e7c6cba7628bf2c9229396fcf285227c56d2fd208f3d0200fb670f8919939ef05aa5e87fc3aceedb4f41e5bca597138c88930a23dbe4b03e22b42d64cec')

prepare() {
  cd kcptun

  mkdir -p .gopath/src/github.com/xtaci
  ln -sf "$PWD" .gopath/src/github.com/xtaci/kcptun
  export GOPATH="$PWD/.gopath:/usr/share/gocode"
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd "$srcdir"/kcptun/client
  go build -o build/kcptun-client \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\""

  cd "$srcdir"/kcptun/server
  go build -o build/kcptun-server \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\""
}

package() {
  cd kcptun

  install -Dm755 client/build/kcptun-client -t "$pkgdir"/usr/bin/
  install -Dm755 server/build/kcptun-server -t "$pkgdir"/usr/bin/

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/

  install -dm755 "$pkgdir"/etc/kcptun
  install -Dm644 "$srcdir"/kcptun@.service -t "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 "$srcdir"/kcptun-server@.service -t "$pkgdir"/usr/lib/systemd/system/
}
