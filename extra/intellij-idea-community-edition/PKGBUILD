# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=intellij-idea-community-edition
pkgver=2024.3.1.1
_build=243.22562.218
_jrever=21
_jdkver=21
pkgrel=1
epoch=4
pkgdesc='IDE for Java, Groovy and other programming languages with advanced refactoring features'
url='https://www.jetbrains.com/idea/'
arch=('x86_64')
license=('Apache-2.0')
backup=('usr/share/idea/bin/idea64.vmoptions')
depends=('giflib' "java-environment-openjdk=${_jrever}" 'python' 'sh' 'ttf-font' 'libdbusmenu-glib' 'fontconfig' 'hicolor-icon-theme')
makedepends=('ant' 'git' maven)
optdepends=(
  'lldb: lldb frontend integration'
)
source=("git+https://github.com/JetBrains/intellij-community.git#tag=idea/${_build}"
        idea-android::"git+https://github.com/JetBrains/android.git#tag=idea/${_build}"
        idea.desktop
        idea.sh
        enable-no-jdr.patch
        # The class src/com/intellij/openapi/projectRoots/ex/JavaSdkUtil.java:56 (git commit 0ea5972cdad569407078fb27070c80e2b9235c53)
        # assumes the user's maven repo is at {$HOME}/.m2/repository and it contains junit-3.8.1.jar
        https://repo1.maven.org/maven2/junit/junit/3.8.1/junit-3.8.1.jar)
noextract=('junit-3.8.1.jar')
sha256sums=('a12fbb5878f4c604216a2a6b659be640fc2f9ae00100746af66d52f60f8338ce'
            '9e8c1ce4ba1e9a1cbe189d51cb18b1aca6300b1d86aa8f8031a7319f79cb5c78'
            '049c4326b6b784da0c698cf62262b591b20abb52e0dcf869f869c0c655f3ce93'
            'c5787f6777fa292c6376f3fcddc89f1d0ae4b71f3284d9bfefcea0b0ab3d8ea9'
            'b7858737346fb08423ee7fd177f9e59180d2173d988dd8622b84d58426fcb3a7'
            'b58e459509e190bed737f3592bc1950485322846cf10e78ded1d065153012d70')

prepare() {
  cd intellij-community

  # build system doesn't like symlinks
  mv "${srcdir}"/idea-android android

  export MAVEN_REPOSITORY=${srcdir}/.m2/repository
  mvn install:install-file \
    -Dfile="${srcdir}"/junit-3.8.1.jar \
    -DgroupId=junit \
    -DartifactId=junit \
    -Dversion=3.8.1 \
    -Dpackaging=jar \
    -DgeneratePom=true

  patch -Np1 < "${srcdir}/enable-no-jdr.patch"
}

build() {
  cd intellij-community
  
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export MAVEN_REPOSITORY=${srcdir}/.m2/repository

  ./installers.cmd -Dintellij.build.use.compiled.classes=false -Dintellij.build.target.os=linux -Dbuild.number="${_build}"
  tar -xf out/idea-ce/artifacts/ideaIC-${_build}-no-jbr.tar.gz -C "${srcdir}"
}

package() {
  cd idea-IC-${_build}

  install -dm 755 "${pkgdir}"/usr/share/{licenses,pixmaps,idea,icons/hicolor/scalable/apps}
  cp -dr --no-preserve='ownership' bin lib plugins "${pkgdir}"/usr/share/idea/
  cp -dr --no-preserve='ownership' license "${pkgdir}"/usr/share/licenses/idea
  ln -s /usr/share/idea/bin/idea.png "${pkgdir}"/usr/share/pixmaps/
  ln -s /usr/share/idea/bin/idea.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/
  install -Dm 644 ../idea.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 755 ../idea.sh "${pkgdir}"/usr/bin/idea
  install -Dm 644 build.txt -t "${pkgdir}"/usr/share/idea
  install -Dm 644 product-info.json -t "${pkgdir}"/usr/share/idea
}

# vim: ts=2 sw=2 et:
