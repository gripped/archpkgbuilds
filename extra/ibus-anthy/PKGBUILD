# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.5.16
pkgrel=2
pkgdesc='Japanese input method Anthy IMEngine for IBus Framework'
arch=('x86_64')
license=('LGPL')
url='https://github.com/ibus/ibus-anthy'
depends=('ibus' 'anthy' 'python' 'gtk-update-icon-cache' 'python-gobject')
makedepends=('git' 'intltool' 'gobject-introspection')
source=("git+https://github.com/ibus/ibus-anthy.git#tag=$pkgver")
sha512sums=('6e44a344f9e86d40f9ad2a8d2e094b20c6ee5e1804a98aa5f76f48f752926a51bc9b01e31782f44f3b2f455e01a61324071e2367a98676e4aa5a236e68638840')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
