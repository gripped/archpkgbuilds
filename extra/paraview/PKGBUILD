# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Contributor: Mathieu Westphal <mathieu.westphal@kitware.com>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: <xantares09@hotmail.com>

pkgname=paraview
pkgver=5.13.3
pkgrel=3
pkgdesc="Parallel Visualization application using VTK"
arch=(x86_64)
url="https://www.paraview.org"
license=(BSD-3-Clause)
depends=(
  adios2
  cgns
  double-conversion
  expat libexpat.so
  ffmpeg libavcodec.so libavformat.so libavutil.so libswscale.so
  fmt libfmt.so
  freetype2 libfreetype.so
  gcc-libs
  gdal
  gl2ps
  glew libGLEW.so
  glibc
  hdf5
  jsoncpp libjsoncpp.so
  libglvnd libGLX.so libOpenGL.so
  libjpeg-turbo
  liblas
  libogg libogg.so
  libpng libpng16.so
  libtheora
  libtiff libtiff.so
  libx11
  libxcursor
  libxml2 libxml2.so
  lz4 liblz4.so
  netcdf
  onetbb
  openmpi libmpi.so
  ospray
  paraview-catalyst
  pdal
  proj
  protobuf libprotobuf.so
  pugixml
  python
  python-mpi4py
  python-numpy
  qt5-base
  qt5-tools
  verdict
  xz liblzma.so
  zlib libz.so
)
optdepends=(
  python-matplotlib
  python-pandas
)
makedepends=(
  boost
  cli11
  cmake
  eigen
  fast_float
  gcc-fortran
  mesa
  ninja
  nlohmann-json
  qt5-svg
  qt5-xmlpatterns
  utf8cpp
)
# pegtl: https://gitlab.kitware.com/vtk/vtk/-/issues/18151
# rapidjson: https://gitlab.kitware.com/vtk/vtk/-/issues/18366
# exprtk, ioss: not packaged
conflicts=(paraview-opt)
replaces=(paraview-opt)
source=(
  $url/files/v${pkgver%.*}/ParaView-v${pkgver/R/-R}.tar.xz
  paraview-wrapper.sh
  template.sh
  $pkgname-fix-pvx-option.patch::https://gitlab.kitware.com/lahwaacz/paraview/-/commit/7f9f90bcb2512df950a7bfa380e16c7bd21cd25c.patch
  fix-fmt-11.patch
  fix-vtk-python313-dict.patch
  fix-vtk-python313-encoding.patch
  netcdf-4.9.3.patch
  cli11-2.5.0.patch
)
sha256sums=('3bd31bb56e07aa2af2a379895745bbc430c565518a363d935f2efc35b076df09'
            'd1cdb4a81db280d6b21bee3d9e5f8fc3a87f00a909d33850a1964c72091ec138'
            '844d44a14817c656ba8d6998298785125a98f03f7b5ece58d966c226db1e0d83'
            '71b3e049f4086ad570c0ffe3d9625ce63ee8abbf42017837495145e43d6aceab'
            '8768d0b68fc1a29eb8ca973dada61338390d7a956051773c7852f7cb76a8fef4'
            '83fcd0c7709ccbea87b39a7702d6f777d6a5c4e5dfbb9e5adf066b5fecdfa9d7'
            'ab1b5b0ee08ae7714aa5c4924de0e2997b0feac1d4563ce73ef4b8c8204a4777'
            '87535578bbb0023ede506fd64afae95cdf4fb698c543f9735e6267730634afbc'
            'dd4053537565d0929ea87caa8f182b5451adb4b9e64f3297a60648c43f27c3f2')

prepare() {
  # Specify python version in wrapper
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  sed -i "s|@PYTHON@|${python_version}|" paraview-wrapper.sh
  # Arch required changes
  cd ParaView-v${pkgver/R/-R}
  local fast_float_version=$(pacman -Q fast_float | sed -e 's/.* //; s/-.*//g')
  sed -i "s|3.9.0|${fast_float_version}|" VTK/ThirdParty/fast_float/CMakeLists.txt

  # Fix compatibility with CLI11 2.4.0 and newer
  # https://gitlab.kitware.com/paraview/paraview/-/merge_requests/6777
  patch -Np1 -i "$srcdir"/$pkgname-fix-pvx-option.patch

  # Fix build with CMake 3.31 https://gitlab.kitware.com/paraview/paraview/-/issues/22797
  sed -i 's|"LINKER:SHELL:${PARAVIEW_EXTRA_SHARED_LINKER_FLAGS} -lc ${CMAKE_SHARED_LINKER_FLAGS}"|"LINKER:SHELL:${PARAVIEW_EXTRA_SHARED_LINKER_FLAGS} -lc"|' CMake/ParaViewCompilerExtraFlags.cmake

  # Fix build with fmt 11
  # (see https://gitlab.kitware.com/vtk/vtk/-/merge_requests/11550 but also
  # added the include to many other files)
  patch -Np2 -i "$srcdir"/fix-fmt-11.patch

  # Backport fixes for Python 3.13
  # https://gitlab.kitware.com/vtk/vtk/-/merge_requests/11486
  # https://gitlab.kitware.com/vtk/vtk/-/merge_requests/11494
  patch -Np1 -d VTK -i "$srcdir"/fix-vtk-python313-dict.patch
  patch -Np1 -d VTK -i "$srcdir"/fix-vtk-python313-encoding.patch

  # Fix build with netcdf 4.9.3
  patch -Np1 -d VTK -i "$srcdir"/netcdf-4.9.3.patch

  # Fix build with cli11 2.5.0
  patch -Np1 -i "$srcdir"/cli11-2.5.0.patch
}

build() {
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"
  local cmake_options=(
    -B build
    -S ParaView-v${pkgver/R/-R}
    -G Ninja
    -W no-dev
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/opt/paraview
    -DCMAKE_INSTALL_LICENSEDIR=share/licenses/paraview
    -DCMAKE_CXX_STANDARD=17
    -DPARAVIEW_IGNORE_CMAKE_CXX11_CHECKS=ON
    -DCMAKE_SKIP_INSTALL_RPATH=OFF
    -DPARAVIEW_ENABLE_ADIOS2=ON
    -DPARAVIEW_ENABLE_CATALYST=ON
    -DPARAVIEW_ENABLE_FFMPEG=ON
    -DPARAVIEW_ENABLE_FIDES=ON
    -DPARAVIEW_ENABLE_GDAL=ON
    -DPARAVIEW_ENABLE_LAS=ON
    -DPARAVIEW_ENABLE_MOTIONFX=ON
    -DPARAVIEW_ENABLE_PDAL=ON
    -DPARAVIEW_ENABLE_RAYTRACING=ON
    -DPARAVIEW_ENABLE_VISITBRIDGE=ON
    -DPARAVIEW_ENABLE_XDMF3=ON
    -DPARAVIEW_USE_MPI=ON
    -DPARAVIEW_USE_PYTHON=ON
    -DPARAVIEW_VERSIONED_INSTALL=OFF
    -DPARAVIEW_BUILD_WITH_EXTERNAL=ON
    -DVTK_SMP_IMPLEMENTATION_TYPE=TBB
    -DVTKm_ENABLE_MPI=ON
    -DCATALYST_USE_MPI=ON
    -DFIDES_USE_EXTERNAL_RAPIDJSON=OFF
    -DVTK_MODULE_USE_EXTERNAL_VTK_exprtk=OFF
    -DVTK_MODULE_USE_EXTERNAL_VTK_ioss=OFF
    -DVTK_MODULE_USE_EXTERNAL_VTK_pegtl=OFF
    -DVTK_MODULE_USE_EXTERNAL_VTK_token=OFF
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # Install wrappers
  install -Dm755 paraview-wrapper.sh "${pkgdir}"/usr/bin/paraview-wrapper
  for binary in paraview "${pkgdir}"/opt/paraview/bin/pv*
  do
    install -Dm755 template.sh "${pkgdir}"/usr/bin/${binary##*/}
    sed -i "s|@BINARY@|${binary##*/}|" "${pkgdir}"/usr/bin/${binary##*/}
  done

  # Install licenses, shortcuts, icons
  install -dm755 "${pkgdir}"/usr/share
  mv "${pkgdir}"/{opt/paraview,usr}/share/applications
  mv "${pkgdir}"/{opt/paraview,usr}/share/icons
  mv "${pkgdir}"/{opt/paraview,usr}/share/licenses
  mv "${pkgdir}"/{opt/paraview,usr}/share/metainfo

  # byte-compile python modules since the CMake build does not do it.
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m compileall -o 0 -o 1 -o 2 --hardlink-dupes -s "${pkgdir}" "${pkgdir}"${site_packages/usr/opt/paraview}
}
