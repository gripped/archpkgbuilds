# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=shadowsocks-v2ray-plugin
pkgver=5.14.1
pkgrel=1
pkgdesc='A SIP003 plugin based on v2ray for shadowsocks'
arch=('x86_64')
url='https://github.com/teddysun/v2ray-plugin'
license=('GPL')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/teddysun/v2ray-plugin.git#tag=v$pkgver")
sha512sums=('faa6012a3fdbcc6e83da6ce3fa7a4ffa3801323d2c8ba7f62da8c0e9c5ba5b2a28c984148ed1c9abe8ea89f233f800c65cb60bd5c91e4c888a8cb34863ee4a0d')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd v2ray-plugin
  go build
}

package() {
  cd v2ray-plugin
  install -Dm755 v2ray-plugin "$pkgdir"/usr/bin/v2ray-plugin
}
