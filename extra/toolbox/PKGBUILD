# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=toolbox
pkgver=0.3
pkgrel=1
pkgdesc='Unprivileged development environment'
arch=(x86_64)
url='https://github.com/containers/toolbox'
license=(Apache-2.0)
depends=(podman bash flatpak)
makedepends=(go shellcheck go-md2man bash-completion ninja git meson podman fish)
source=("git+$url#tag=$pkgver")
sha512sums=('0d0c88c5af5783e62eb0bfe200707599cad2ad05a2485e8afbc38e32d44f1eddfea6ec4ae67bc45d1dce6d8e2e8c6be519ed4bacf6c2d8b123dac1726ff69698')
b2sums=('49bf22c8f432928fb5b32b8b90b95dce963e2287d5165c627c60ecf05e50e7ed407af54c224fc43afaac90c95f4b1009c0fe1c0928518acf96f2b2c805a0c14f')

build() {
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  arch-meson toolbox build -D profile_dir=/etc/profile.d
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
