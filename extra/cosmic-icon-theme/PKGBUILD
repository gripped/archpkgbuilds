# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-icon-theme
pkgver=1.0.0.alpha.5.1
pkgrel=1
pkgdesc='Cosmic icon theme'
arch=(any)
url=https://github.com/pop-os/cosmic-icons
license=(CC-BY-SA-4.0)
depends=(pop-icon-theme)
makedepends=(
  git
  just
)
options=(!strip)
_tag=52ad55cba5272630f345e14bee69c9fbe7aa98c4
source=(git+https://github.com/pop-os/cosmic-icons.git#tag=${_tag})
b2sums=('59f1e82f257ea756db8550a95123c40f2807108a96bb9db1f748edbffb359cecbdceb54be27c0154525341938e24ca9c670088115face09483ade91a65f02048')

pkgver() {
  cd cosmic-icons
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

package() {
  cd cosmic-icons
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
