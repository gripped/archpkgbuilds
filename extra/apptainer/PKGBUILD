# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maciej Sieczka <msieczka at sieczka dot org>

pkgname=apptainer
pkgver=1.4.1
pkgrel=1
pkgdesc='Application containers for secure high performance computing'
arch=('x86_64')
url='https://apptainer.org/'
license=('BSD-3-Clause' 'BSD-3-Clause-LBNL')
backup=(
  'etc/apptainer/apptainer.conf'
  'etc/apptainer/capability.json'
  'etc/apptainer/cgroups/cgroups.toml'
  'etc/apptainer/dmtcp-conf.yaml'
  'etc/apptainer/ecl.toml'
  'etc/apptainer/global-pgp-public'
  'etc/apptainer/network/00_bridge.conflist'
  'etc/apptainer/network/10_ptp.conflist'
  'etc/apptainer/network/20_ipvlan.conflist'
  'etc/apptainer/network/30_macvlan.conflist'
  'etc/apptainer/network/40_fakeroot.conflist'
  'etc/apptainer/nvliblist.conf'
  'etc/apptainer/remote.yaml'
  'etc/apptainer/rocmliblist.conf'
  'etc/apptainer/seccomp-profiles/default.json'
)
depends=(
  'glibc'
  'fakeroot'
  'fuse2fs'
  'fuse-overlayfs'
  'shadow'
  'squashfs-tools'
  'squashfuse'
  'libseccomp'
  'e2fsprogs'
)
makedepends=(
  'git'
  'go'
  'cryptsetup'
)
conflicts=('singularity')
replaces=('singularity-container')
options=('!lto' '!debug')
source=(
  "$pkgname::git+https://github.com/apptainer/apptainer.git#tag=v$pkgver"
  'tmpfiles.conf'
  'ftbfs-redefined-fortify_source.patch'
)
sha512sums=('2b3c06740e3a0fa5de45204600cfa0d003a0d7a8108bc24ab8734924418db3fcaed0927f882cc7a8040431d2a886c9e0e99d0c72e52bc1dcc20b5ce34383c699'
            '5f10f162e7e48d2ca821529906a4ef692fc32cec8f61d8f03ea60361e1a204eacbeca8b6214bea0accd019c765d9c4c15300ca01c83ab3cbbdc44e82c7e4908d'
            '320bb41a26ce350886543f161ec47317390c49e6aae726a0e8bb0e2d38c8620f32d1be22d79f9ee1d34b4ccc1ed5629ddf10411e08f16e440ca0f34d76726e99')
b2sums=('01db92141e1a7d4808ebcfc2a2ccf2611243e9803a1a9ec45c77bee12a007e19abdd3f7a01383cdaf899ad557ec72ddc0a9726b506b4127a7376e0902e18e15a'
        'dc9bfeb268d297b8785348c83f89a68ff760de278c8f101831a930ca0b42d2052d0498d7dd8c66f48b9fef2eb3c901de082939e85b22d0a99f7db4e22744bced'
        'd8a93e88823a386327ba9269de8280abb534bd86b32ebeb2aab67c9c2831dff90241ae06035152223c8b50af9579befcfaddddbd2bd21dcc7fc05ea383e61d47')

prepare() {
  cd "$pkgname"

  # fix bash completion path
  sed -e "s|/etc/bash_completion.d|/usr/share/bash-completion/completions|" \
    -e "s|\$(SYSCONFDIR)/bash_completion.d|/usr/share/bash-completion/completions|" \
    -i  mlocal/frags/build_cli.mk


  # fix FTBFS due to redefined -D_FORTIFY_SOURCE
  # arch uses v3, upstream still uses v2
  patch -p1 -i "$srcdir/ftbfs-redefined-fortify_source.patch"
}

build() {
  cd "$pkgname"

  # provide version to build script
  echo "$pkgver" > VERSION

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  ./mconfig \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var/lib \
    -P release-stripped \
    --without-suid \
    -v

  make -C builddir
}

package() {
  cd "$pkgname"

  # binaries & man pages
  make -C builddir DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # let systemd-tmpfiles generate state directory
  rm -rf "$pkgdir/var"
  install -vDm644 "$srcdir/tmpfiles.conf" \
    "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
