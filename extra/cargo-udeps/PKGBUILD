# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-udeps
pkgver=0.1.54
pkgrel=4
pkgdesc="Find unused dependencies in Cargo.toml"
arch=('x86_64')
url="https://github.com/est31/cargo-udeps"
license=('MIT' 'Apache-2.0')
depends=('curl' 'libgit2' 'libssh2' 'openssl' 'zlib')
makedepends=('cargo' 'libssh2')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz" 'libgit2.pc')
sha256sums=('52fcea433a514bf85493432c94704c790ae1a86d3c01fe5670ddd1000ed0a206'
            'f0b4952bd7ac83d19e11b491af6debc6cce1263070943a886726915f8c4283df')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_force_libgit19() {
  # Force compilation against libgit2 1.9
  export PKG_CONFIG_PATH="$srcdir"
}

build() {
  cd "$pkgname-$pkgver"
  _force_libgit19
  export LIBGIT2_NO_VENDOR=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
  CFLAGS+=" -ffat-lto-objects"
  cargo build --release --frozen
}

# Tests require rustup nightly
#check() {
#  cd "$pkgname-$pkgver"
#  cargo test --release --locked
#}

package() {
  cd "$pkgname-$pkgver"
  depends+=('libcurl.so' 'libgit2.so' 'libssh2.so' 'libssl.so' 'libz.so')
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  depends+=('rustup')
}

# vim:set ts=2 sw=2 et:
