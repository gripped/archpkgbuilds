# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=python-pykcs11
pkgver=1.5.17
pkgrel=2
pkgdesc='PKCS#11 Wrapper for Python'
arch=('x86_64')
url='https://github.com/LudovicRousseau/PyKCS11'
license=('GPL-2.0-or-later')
depends=(
  gcc-libs
  glibc
  opensc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  softhsm
  swig
)
source=("git+https://github.com/LudovicRousseau/PyKCS11.git/?signed#tag=$pkgver")
b2sums=('aee0a61fa0d39415997315b00cb8281295d78bab9996b65e31abcb21f5742caa22f58ff2c2223982a1f0de9b8b2b9d804aca4e221e70b09361cc982a169785c4')

validpgpkeys=(F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E) # Ludovic Rousseau <ludovic.rousseau@free.fr>

build() {
  cd PyKCS11
  python -m build
}

package() {
  cd PyKCS11
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
