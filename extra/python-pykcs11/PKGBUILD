# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=python-pykcs11
pkgver=1.5.18
pkgrel=2
pkgdesc='PKCS#11 Wrapper for Python'
arch=('x86_64')
url='https://github.com/LudovicRousseau/PyKCS11'
license=('GPL-2.0-or-later')
depends=(
  gcc-libs
  glibc
  opensc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  softhsm
  swig
)
source=("git+https://github.com/LudovicRousseau/PyKCS11.git/?signed#tag=$pkgver")
b2sums=('ac722db41993145061e625dd1227eed884aac9ffd2511479b65d0a74cd1ff7acc76556c4ad0ef8afcfe498c4a503a56a05b83edabd4824d115f3569464613721')

validpgpkeys=(F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E) # Ludovic Rousseau <ludovic.rousseau@free.fr>

build() {
  cd PyKCS11
  python -m build
}

package() {
  cd PyKCS11
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
