# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mort Yao <soi@mort.ninja>
# Contributor: Alex Merry <dev@randomguy3.me.uk>

pkgname=polyml
pkgver=5.9.2
pkgrel=1
pkgdesc='Standard ML compiler originally written in Poly'
arch=(x86_64)
url='https://polyml.org/'
license=(LGPL)
options=(!libtool)
depends=(gmp libffi)
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('5cf5f77767568c25cf880acc2d0a32ee3d399e935475ab1626e8192fc3b07390')
b2sums=('71659af033cd5349dd0fb2cfc0f5eb4f2de5878e463226c18ff97b02d06da0ab0700f8e219d7a992632f8c622d7b0b1df9f17300e4c6f4d5c8657af5c335270f')

build() {
  cd $pkgname-$pkgver
  CFLAGS+=' -ffat-lto-objects' \
  ./configure \
    --enable-shared \
    --prefix=/usr \
    --with-gmp \
    --with-system-libffi
  make
}

check() {
  make -C $pkgname-$pkgver -k check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
