
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=25.1.0
pkgrel=1
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt6-base' 'qt6-webengine' 'libofx'
         'knewstuff' 'knotifyconfig' 'kparts' 'ktexttemplate'
         'sqlcipher' 'python' 'hicolor-icon-theme' 'qt6-declarative')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'qt6-tools' 'kstatusnotifieritem' 'krunner' 'plasma-activities')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D'  #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
              'F1955ED9D1C2D10D80B31B3B14EE37CD15C53BDD') 
sha256sums=('b7c03d7a0a2d4765e8301a39ba81f64413c4a371fa9cf4894b93a2e0c912570c'
            'SKIP')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DSKG_BUILD_TEST=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
