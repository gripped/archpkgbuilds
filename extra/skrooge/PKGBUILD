
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=25.4.0
pkgrel=1
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt6-base' 'qt6-webengine' 'libofx'
         'knewstuff' 'knotifyconfig' 'kparts' 'ktexttemplate'
         'sqlcipher' 'python' 'hicolor-icon-theme' 'qt6-declarative'
         'kiconthemes' 'knotifications' 'ki18n' 'kconfigwidgets'
         'kcompletion' 'kdbusaddons' 'kwidgetsaddons' 'kxmlgui'
         'kwallet' 'kio' 'kcoreaddons' 'karchive'
         'kcolorscheme' 'kconfig' 'qt6-5compat' 'qt6-svg' 'gcc-libs' 'glibc')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'qt6-tools' 'kstatusnotifieritem' 'krunner' 'plasma-activities')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D'  #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
              'F1955ED9D1C2D10D80B31B3B14EE37CD15C53BDD') 
sha256sums=('1cdba2fd28c237d2d6c54c470da7b9f67e6a3c8c185875f5b854a5567c0194bf'
            'SKIP')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DSKG_BUILD_TEST=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
