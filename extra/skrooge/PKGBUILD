
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.33.0
pkgrel=4
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qt5-script' 'qt5-webengine' 'qt5-xmlpatterns' 'libofx'
         'grantlee' 'knewstuff5' 'knotifyconfig5' 'kparts5'
         'sqlcipher' 'python' 'hicolor-icon-theme' 'qt5-quickcontrols2')
makedepends=('cmake' 'kdoctools5' 'extra-cmake-modules' 'qt5-tools')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('f4ae3faf723d54d7542878b8142a344b147e4337b0bc42863907af44cfebf465'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  cmake . -DSKG_WEBENGINE=ON \
          -DSKG_DESIGNER=OFF \
          -DSKG_BUILD_TEST=OFF
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
