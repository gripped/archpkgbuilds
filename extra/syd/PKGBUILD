# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=syd
_pkgname=sydbox
pkgver=3.37.8
pkgrel=2
pkgdesc="Seccomp and landlock based application sandbox with support for namespaces"
url="https://lib.rs/crates/syd"
arch=('x86_64')
license=('GPL-3.0-only')
depends=('bash' 'gcc-libs' 'glibc' 'libseccomp')
makedepends=('cargo' 'scdoc')
provides=("${_pkgname}")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.exherbo.org/${_pkgname}/${_pkgname}/-/archive/v${pkgver}/${_pkgname}-v${pkgver}.tar.gz"
        'reproducible_build.patch')
sha256sums=('9263f87f63eb42b7783c95686d321715aa4386859897cfb47dc6e008cfa29bef'
            '85566cf3982324c749fd4ffc104c1ca97dec166541d227a8cd3945e4e52c8fd3')

prepare() {
	cd "${_pkgname}-v${pkgver}"

	# Don't record non-deterministic information if SDE is set
	# This is required for reproducible builds
	# See https://gitlab.exherbo.org/sydbox/sydbox/-/merge_requests/14
	patch -Np1 -i "${srcdir}/reproducible_build.patch"

	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${_pkgname}-v${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release

	find man -type f -name "*.scd" -exec sh -c 'scdoc < "${1}" > "${1%.scd}"' _ {} \;
}

check() {
	cd "${_pkgname}-v${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	# "syd_test" and "fs" tests fail with sandbox
	# "hash" tests seems flaky (they fail on the build server for some reason)
	cargo test --frozen -- \
		--skip=syd_test \
		--skip=fs::tests::test_canonicalize_non_directory_with_slash \
		--skip=fs::tests::test_canonicalize_self_referential_symlink \
		--skip=fs::tests::test_canonicalize_symlink_loop \
		--skip=hash::tests::test_aes_ctr_enc_and_dec \
		--skip=hash::tests::test_aes_ctr_enc_and_dec_tmp \
		--skip=hash::tests::test_aes_ctr_enc_with_more_flag \
		--skip=hash::tests::test_aes_ctr_init \
		--skip=hash::tests::test_aes_ctr_setup
}

package() {
	cd "${_pkgname}-v${pkgver}"
	install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

	install -Dm 755 src/esyd.sh "${pkgdir}/usr/lib/${pkgname}/esyd.sh"

	install -Dm 644 "data/user.${pkgname}-3" "${pkgdir}/etc/user.${pkgname}-3.example"

	install -Dm 644 "vim/ftdetect/${pkgname}.vim" "${pkgdir}/usr/share/vim/vimfiles/ftdetect/${pkgname}.vim"
	install -Dm 644 "vim/syntax/${pkgname}-3.vim" "${pkgdir}/usr/share/vim/vimfiles/syntax/${pkgname}-3.vim"

	install -Dm 644 man/*.1 -t "${pkgdir}/usr/share/man/man1/"
	install -Dm 644 man/*.2 -t "${pkgdir}/usr/share/man/man2/"
	install -Dm 644 man/*.5 -t "${pkgdir}/usr/share/man/man5/"
	install -Dm 644 man/*.7 -t "${pkgdir}/usr/share/man/man7/"

	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
