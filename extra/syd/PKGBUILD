# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=syd
_pkgname=sydbox
pkgver=3.41.0
pkgrel=1
pkgdesc="Seccomp and landlock based application sandbox with support for namespaces"
url="https://lib.rs/crates/syd"
arch=('x86_64')
license=('GPL-3.0-only')
depends=('bash' 'gcc-libs' 'glibc' 'libseccomp')
makedepends=('cargo' 'git' 'scdoc')
provides=("${_pkgname}")
source=("git+https://gitlab.exherbo.org/sydbox/sydbox.git#tag=v${pkgver}?signed")
sha256sums=('dcefca31412655994707a0ab15a3bc86d5442b2cee0f4ff11684fad0bb08ea38')
validpgpkeys=('5DF763560390A149AC6C14C7D076A377FB27DE70')
options=('!lto')

prepare() {
	cd "${_pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${_pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release

	find man -type f -name "*.scd" -exec sh -c 'scdoc < "${1}" > "${1%.scd}"' _ {} \;
}

check() {
	cd "${_pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	# "syd_test" and "fs" tests fail with sandbox
	# "hash" tests seems flaky (they fail on the build server for some reason)
	# "proc / executables paths [...]" fails if `/usr/lib/libgcc_s.so.1` is not executable (which is shipped by `gcc_libs`)
	cargo test --frozen -- \
		--skip=syd_test \
		--skip=fs::tests::test_canonicalize_non_directory_with_slash \
		--skip=fs::tests::test_canonicalize_self_referential_symlink \
		--skip=fs::tests::test_canonicalize_symlink_loop \
		--skip=hash::tests::test_hmac_sha256_simple stdout \
		--skip=hash::tests::test_aes_ctr_enc_and_dec \
		--skip=hash::tests::test_aes_ctr_enc_and_dec_tmp \
		--skip=hash::tests::test_aes_ctr_enc_with_more_flag \
		--skip=hash::tests::test_aes_ctr_init \
		--skip=hash::tests::test_aes_ctr_setup \
		--skip=proc::tests::test_proc_executables_paths_exist_and_executable
}

package() {
	cd "${_pkgname}"
	install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

	install -Dm 755 src/esyd.sh "${pkgdir}/usr/lib/${pkgname}/esyd.sh"

	install -Dm 644 "data/user.${pkgname}-3" "${pkgdir}/etc/user.${pkgname}-3.example"

	install -Dm 644 "vim/ftdetect/${pkgname}.vim" "${pkgdir}/usr/share/vim/vimfiles/ftdetect/${pkgname}.vim"
	install -Dm 644 "vim/syntax/${pkgname}-3.vim" "${pkgdir}/usr/share/vim/vimfiles/syntax/${pkgname}-3.vim"

	install -Dm 644 man/*.1 -t "${pkgdir}/usr/share/man/man1/"
	install -Dm 644 man/*.2 -t "${pkgdir}/usr/share/man/man2/"
	install -Dm 644 man/*.5 -t "${pkgdir}/usr/share/man/man5/"
	install -Dm 644 man/*.7 -t "${pkgdir}/usr/share/man/man7/"

	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
