# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=6.5.1
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-multimedia' 'qt6-declarative'
         'qt6-webchannel' 'qt6-webengine' 'gstreamer' 'vlc' 'glib2' 'gcc-libs' 'deepin-movie')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja' 'qt6-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-voice-note.git#tag=$pkgver")
sha512sums=('6b950cf848837cee0fd30bc7aa96ee98eec4e68f52b32156db63c5a5e16076fa76ab795a5fe6d5e8905ee866ea702ff986c6a72d55e13a078926b974c687dff0')

build() {
  cd deepin-voice-note
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

check() {
  cd deepin-voice-note
  ninja test || :
}

package() {
  cd deepin-voice-note
  DESTDIR="$pkgdir" ninja install
}
