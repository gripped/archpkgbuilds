# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=6.5.6
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL-3.0-or-later')
replaces=('deepin-voice-recorder')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-multimedia' 'qt6-declarative'
         'qt6-webchannel' 'qt6-webengine' 'gstreamer' 'glib2' 'gcc-libs' 'deepin-movie')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja' 'qt6-tools' 'vlc')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-voice-note.git#tag=$pkgver")
sha512sums=('d321d9bce6c8cd0764134b5b655e6278f9f4748c8311f1513914dbe5263754e16b6f5098c91d7b100025f3b8a6204d4380db3a05748dcbd104984be5a9d85d0b')

prepare() {
  cd deepin-voice-note
  sed -i '/#include <QDBusError>/a#include <QJsonDocument>' src/common/vnotea2tmanager.cpp
}

build() {
  cd deepin-voice-note
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

check() {
  cd deepin-voice-note
  ninja test || :
}

package() {
  cd deepin-voice-note
  DESTDIR="$pkgdir" ninja install
}
