# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=6.5.14
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL-3.0-or-later')
replaces=('deepin-voice-recorder')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-multimedia' 'qt6-declarative'
         'qt6-webchannel' 'qt6-webengine' 'gstreamer' 'glib2' 'gcc-libs' 'deepin-movie')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja' 'qt6-tools' 'vlc')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-voice-note.git#tag=$pkgver")
sha512sums=('a2c9f279679f80c07d15a4441a5020e2b1f05386e0d4a799eb31be6032a6c4a28a10627ac4f11184bf17e757817f0b62da3f3ca4168d9c8a21dfbb75f6993909')

prepare() {
  cd deepin-voice-note
  sed -i '/#include <QDBusError>/a#include <QJsonDocument>' src/common/vnotea2tmanager.cpp
}

build() {
  cd deepin-voice-note
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

check() {
  cd deepin-voice-note
  ninja test || :
}

package() {
  cd deepin-voice-note
  DESTDIR="$pkgdir" ninja install
}
