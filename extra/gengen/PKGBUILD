# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=gengen
pkgver=1.4.3
pkgrel=1
pkgdesc='parameterized-text-generator generator based on a template '
arch=('x86_64')
url='https://www.gnu.org/software/gengen/'
license=('GPL-3.0-or-later')
depends=('glibc' 'gcc-libs')
makedepends=('git')
validpgpkeys=('D459FCC2041AC8FEF297D53EB4AA8C689E629EAC')  # Lorenzo Bettini <lorenzo.bettini@gmail.com>
source=("git+https://git.savannah.gnu.org/git/gengen.git#tag=rel_${pkgver//./_}")
sha256sums=('e057458122d1dc8b732fc1f65928e8aec08eab4ab586d23fa4ac25285dc6fbcb')

prepare() {
  cd "${pkgname}"

  autoreconf -fi
}

build() {
  cd "${pkgname}"

  ./configure \
    --prefix='/usr'
  make
}

check() {
  cd "${pkgname}"

  make -j1 check
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install
}
