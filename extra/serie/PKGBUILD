# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=serie
pkgver=0.5.0
pkgrel=1
pkgdesc="A rich git commit graph in your terminal"
url="https://github.com/lusingander/serie"
depends=(gcc-libs
         glibc)
makedepends=(cargo 'git')
arch=(x86_64)
license=(MIT)
source=("git+$url#tag=v$pkgver")
sha512sums=('bcf09188ff31f4020053f37edce2b4da8c7314e0d55dc5cbe431b2ed1b354305a059fd38ee8afc844c0ca08d10539e30239657379eadc00af7ed74a9d8690354')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
