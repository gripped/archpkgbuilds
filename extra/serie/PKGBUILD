# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=serie
pkgver=0.4.5
pkgrel=1
pkgdesc="A rich git commit graph in your terminal"
url="https://github.com/lusingander/serie"
depends=(gcc-libs
         glibc)
makedepends=(cargo 'git')
arch=(x86_64)
license=(MIT)
source=("git+$url#tag=v$pkgver")
sha512sums=('b41e976fea42a8492cc30c4db3ec5bc114cf6dbed447a796bbd412bb499e30318b059c3266ade80d61b6d7f1bfce083ee211096e20a048d5f4f79dd7ae0c4f76')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
