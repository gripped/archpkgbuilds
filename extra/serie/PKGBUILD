# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=serie
pkgver=0.5.2
pkgrel=1
pkgdesc="A rich git commit graph in your terminal"
url="https://github.com/lusingander/serie"
depends=(gcc-libs
         glibc)
makedepends=(cargo 'git')
arch=(x86_64)
license=(MIT)
source=("git+$url#tag=v$pkgver")
sha512sums=('43e2fc96330e9c18ec264c513dd4b12cbf1a13652320c38eb68c803005fb557f90f771eb0ea2a65b434d05b516fe509525e2862b050f93d9eb3c54bdab1a5afd')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
