# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=serie
pkgver=0.5.6
pkgrel=1
pkgdesc="A rich git commit graph in your terminal"
url="https://github.com/lusingander/serie"
depends=(gcc-libs
         glibc)
makedepends=(cargo 'git')
arch=(x86_64)
license=(MIT)
source=("git+$url#tag=v$pkgver")
sha512sums=('3209fbbed278aa82ff3b8dcb6ed1fbea9145d80527a58a4fee0d9598f5ae67e90c1d12a9d38df1f382d3a63a7bca3807e81b8c78aac1738196a1f797da1c62ac')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
