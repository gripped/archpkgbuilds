# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=serie
pkgver=0.5.5
pkgrel=1
pkgdesc="A rich git commit graph in your terminal"
url="https://github.com/lusingander/serie"
depends=(gcc-libs
         glibc)
makedepends=(cargo 'git')
arch=(x86_64)
license=(MIT)
source=("git+$url#tag=v$pkgver")
sha512sums=('97a466c6ed66cf70917b39ec0d91306e84f8654905cbcd937f6247e997362ac4843e202beea699b0cb517d62c82d80747490d8ec0b69dcc864651ddcd27848c3')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
