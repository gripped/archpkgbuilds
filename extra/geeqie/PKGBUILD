# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=2.6.1
pkgrel=4
pkgdesc='Lightweight image viewer'
arch=('x86_64')
url="http://www.geeqie.org/"
license=('GPL-2.0-or-later')
# https://github.com/BestImageViewer/geeqie#optional-libraries
depends=('cairo'
         'cfitsio'
         'clutter'
         'clutter-gtk'
         'djvulibre'
         'exiv2'
         'ffmpegthumbnailer'
         'gcc-libs'
         'gdk-pixbuf2'
         'glib2'
         'glibc'
         'gspell'
         'gtk3'
         'hicolor-icon-theme'
         'lcms2'
         'libarchive'
         'libchamplain'
         'libheif'
         'libjpeg-turbo'
         'libjxl'
         'libraw'
         'libtiff'
         'libunwind'
         'lua'
         'openexr'
         'openjpeg2'
         'pango'
         'poppler-glib'
         'sh')
makedepends=('evince'
             'fbida'
             'gawk'
             'glib2-devel'
             'imagemagick'
             'intltool'
             'librsvg'
             'libwebp'
             'libwmf'
             'meson'
             'perl-image-exiftool'
             'python'
             # for the docs
             'doxygen' 'yelp-tools' 'graphviz'
             'pandoc-cli'
             #'fop' - for pdf format but 'pdfoutline' is missing
)
checkdepends=('xorg-server-xvfb' 'shellcheck' 'markdownlint' 'appstream')
optdepends=('librsvg: SVG rendering'
            'fbida: for jpeg rotation'
            'gawk: to use the geo-decode function'
            'perl-image-exiftool: for the jpeg extraction plugin'
            'gphoto2: command-line tools for various (plugin) operations'
            'imagemagick: command-line tools for various (plugin) operations'
            'evince: for print preview'
            'libwebp: for WebP image format')
source=(#"http://www.geeqie.org/${pkgname}-${pkgver}.tar.xz"{,.asc}
        https://github.com/BestImageViewer/geeqie/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc}
        0001-Don-t-remove-PATH-from-tests.patch
        0002-Fix-error-from-shellcheck-0.11.0.patch)
sha256sums=('164b768b8a387edf654112428adb8fd88c265c76b7bc84a490158e6923da3a55'
            'SKIP'
            '504cc1233dd5dba72c5550d6a9a569ab7a5fb8b551c1bc2a8339602aad3aa168'
            'c6b8bce2062f1b09d2edd7a172982082b193f52f578548fa06f609de556a4310')
validpgpkeys=('91EC400226201276E2ADCEC7D0DA6F44C936D1DA') # "Colin Clark <colin.clark@cclark.uk>"

# main repo: http://www.geeqie.org/cgi-bin/gitweb.cgi?p=geeqie.git
# bug tracker: https://github.com/BestImageViewer/geeqie/issues

prepare() {
  cd $pkgname-$pkgver

  # Fix tests
  patch -Np1 -i ../0001-Don-t-remove-PATH-from-tests.patch
  patch -Np1 -i ../0002-Fix-error-from-shellcheck-0.11.0.patch
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package(){
  DESTDIR="$pkgdir" meson install -C build

  # add Changelog listed in help menu
  install -m644 $pkgname-$pkgver/ChangeLog.gqview "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"

  # add missing html doc
  cp -arv build/doc/html "${pkgdir}/usr/share/doc/${pkgname}/"
}
