# Maintainer: Peter Jung <ptr1337@archlinux.org>

pkgname=proton-vpn-cli
pkgver=0.1.3
pkgrel=3
pkgdesc="Official ProtonVPN CLI Linux app"
arch=('any')
url="https://github.com/ProtonVPN/proton-vpn-cli"
license=('GPL-3.0-only')
depends=(
  python
  python-click
  python-dbus-fast
  python-packaging
  python-proton-core
  python-proton-vpn-api-core
  python-proton-keyring-linux
  python-proton-vpn-network-manager
  python-proton-vpn-local-agent
  proton-vpn-daemon
  python-sentry_sdk
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
source=("git+https://github.com/ProtonVPN/proton-vpn-cli.git#tag=v$pkgver")
sha256sums=('b3bcc2f2c93895ca4eb0692b56c52b52d9378d9d4cd5ce805b945e1d436a8230')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
