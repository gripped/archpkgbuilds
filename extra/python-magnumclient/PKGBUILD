# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-magnumclient
pkgver=4.2.0
_commit=ef63e2e1bb063f285fd669021dd70fbbbaca95f0
pkgrel=1
pkgdesc="Client library for Magnum API"
arch=('any')
url="https://docs.openstack.org/python-magnumclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-keystoneauth1' 'python-stevedore' 'python-requests'
         'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization' 'python-oslo-utils'
         'python-os-client-config' 'python-osc-lib' 'python-prettytable' 'python-cryptography'
         'python-decorator')
makedepends=('git')
checkdepends=('python-openstackclient' 'python-oslotest' 'python-osprofiler' 'python-stestr'
              'python-testscenarios' 'python-testtools')
source=("git+https://github.com/openstack/python-magnumclient.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd python-magnumclient
  python setup.py build
}

check() {
  cd python-magnumclient
  stestr run
}

package() {
  cd python-magnumclient
  python setup.py install --root="$pkgdir" --optimize=1
}
