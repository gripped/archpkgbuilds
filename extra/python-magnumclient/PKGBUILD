# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-magnumclient
pkgver=4.9.0
pkgrel=2
pkgdesc="Client library for Magnum API"
arch=('any')
url="https://docs.openstack.org/python-magnumclient/latest/"
license=('Apache-2.0')
depends=('python' 'python-pbr' 'python-keystoneauth1' 'python-stevedore' 'python-requests'
         'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization' 'python-oslo-utils'
         'python-openstacksdk' 'python-osc-lib' 'python-prettytable' 'python-cryptography'
         'python-decorator')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-openstackclient' 'python-oslotest' 'python-osprofiler' 'python-stestr'
              'python-testscenarios' 'python-testtools')
source=("git+https://github.com/openstack/python-magnumclient.git#tag=$pkgver")
sha512sums=('c317132946ba8eeb5107d6e8026264db4c5d1c776e32eb76ab5f4547e67d41cbe3885b124a8b7764f6e6c84c1d515aff7fa5f685ca05228bdc6b1a2383639bb0')
b2sums=('9e2a83fc3fc066eecdb5e76a0fa6e1a077c425f8f41b5590765f50eced9dcd428e6eadeb49c16e59446b998cbcda339df07307af4bc76facedc52cacef5e9e46')

build() {
  cd python-magnumclient
  python -m build --wheel --no-isolation
}

check() {
  local stestr_run_options=(
  )

  cd python-magnumclient
  stestr run "${stestr_run_options[@]}"
}

package() {
  cd python-magnumclient
  python -m installer --destdir="$pkgdir" dist/*.whl
}
