# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-magnumclient
pkgver=4.8.0
pkgrel=1
pkgdesc="Client library for Magnum API"
arch=('any')
url="https://docs.openstack.org/python-magnumclient/latest/"
license=('Apache-2.0')
depends=('python' 'python-pbr' 'python-keystoneauth1' 'python-stevedore' 'python-requests'
         'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization' 'python-oslo-utils'
         'python-os-client-config' 'python-osc-lib' 'python-prettytable' 'python-cryptography'
         'python-decorator')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-openstackclient' 'python-oslotest' 'python-osprofiler' 'python-stestr'
              'python-testscenarios' 'python-testtools')
source=("git+https://github.com/openstack/python-magnumclient.git#tag=$pkgver")
sha512sums=('19d196ca423df8b6ba1c6e2c73e82f375c8ecaadc6d85553f0c1e637703f76c72c42b5e29fdbfe059eb00e452a0f1b19e3b627256535b69a40e13a9933dcd30b')
b2sums=('be505f0ee0b3a3246d45c66f0a6341f06bcfff7905d4df4bab0835f17e5392ce5c6bd0abf016b921ce224ab29e436c08835f9f9cfd71e822991c8bdaea1477a6')

build() {
  cd python-magnumclient
  python -m build --wheel --no-isolation
}

check() {
  local stestr_run_options=(
    # breaks with Python 3.12
    -E 'magnumclient.tests.test_httpclient.HttpClientTest.test_get_connection'
  )

  cd python-magnumclient
  stestr run "${stestr_run_options[@]}"
}

package() {
  cd python-magnumclient
  python -m installer --destdir="$pkgdir" dist/*.whl
}
