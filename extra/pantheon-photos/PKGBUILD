# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-photos
pkgver=8.0.1
pkgrel=3
pkgdesc='The Pantheon Photos Manager'
arch=(x86_64)
url=https://github.com/elementary/photos
license=(GPL-3.0-only)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  geocode-glib-2
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  gtk3
  hicolor-icon-theme
  libexif
  libgee
  libgexiv2
  libgphoto2
  libgranite.so
  libgudev-1.0.so
  libhandy-1.so
  libportal
  libportal-gtk3
  libraw
  libwebp
  pango
  sqlite
)
makedepends=(
  git
  intltool
  meson
  vala
)
groups=(pantheon)
_tag=f976b6a2d2f46a68ad989860a338e0d47bdb6801
source=(pantheon-photos::git+https://github.com/elementary/photos.git#tag=${_tag})
sha256sums=('446de5fee6d4dfe09d98fe71420b3fc5566268d88472b6d86de89556d699d236')

prepare() {
  cd pantheon-photos
}

pkgver() {
  cd pantheon-photos
  git describe --tags
}

build() {
  export LDFLAGS+=" -lm"
  arch-meson pantheon-photos build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
