# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-a11y
pkgver=1.0.2
pkgrel=2
pkgdesc='Wingpanel Universal Access Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-a11y
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk3
  libgranite.so
  libwingpanel.so
)
makedepends=(
  git
  meson
  vala
)
_tag=ccf803ed1d91b1daaa23457d33eef6fa571863bb
source=(git+https://github.com/elementary/wingpanel-indicator-a11y.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-a11y
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-a11y build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
