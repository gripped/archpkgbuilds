# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Bernard Baeyens (berbae) <berbae52 at sfr dot fr>

pkgname=pan
pkgver=0.162
pkgrel=2
pkgdesc='Powerful Usenet newsreader'
arch=(x86_64)
url='http://pan.rebelbase.com/'
license=(GPL-2.0-only)
depends=(
  cairo
  gcc-libs
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gmime3
  gnutls
  gspell
  gtk3
  hicolor-icon-theme
  libnotify
  libsecret
  pango
  zlib
)
makedepends=(
  cmake
  git
  itstool
  ninja
)
source=(
  "git+https://gitlab.gnome.org/GNOME/pan.git?signed#tag=v$pkgver"
  pan-prgname.patch
)
b2sums=(
  4b6ebaac0c1d02e031aa2973c8fd3b06dc26ea7ccc0c0ed06517b7a6683107d4b6675b208fc6235eaceef45de9eb8ce82dc44bf6bf9c6bb5a0f6051269519abc
  9f85c78deff424fddf40d8961cecc709b765486de5e0f71d14004a3d645e16dd598902267d1b5bf1f65f2e579f4324e974b916fe33eda11b05a6afcb9ab15632
)
validpgpkeys=(9F7239FCB664F10B33E9DC03C31F4FD949AB2B6C) # Dominique Dumont <domi.dumont@free.fr>

prepare() {
  cd $pkgname
  git apply -3 ../pan-prgname.patch
}
build() {
  cmake -S $pkgname -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D WANT_DBUS=1 \
    -D WANT_GMIME_CRYPTO=1 \
    -D WANT_NOTIFY=1 \
    -D WANT_GKR=1 \
    -D ENABLE_MANUAL=1
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
