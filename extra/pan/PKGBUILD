# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Bernard Baeyens (berbae) <berbae52 at sfr dot fr>

pkgname=pan
pkgver=0.163
pkgrel=1
pkgdesc='Powerful Usenet newsreader'
arch=(x86_64)
url='http://pan.rebelbase.com/'
license=(GPL-2.0-only)
depends=(
  cairo
  gcc-libs
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gmime3
  gnutls
  gspell
  gtk3
  hicolor-icon-theme
  libnotify
  libsecret
  pango
  zlib
)
makedepends=(
  cmake
  git
  itstool
  ninja
)
source=("git+https://gitlab.gnome.org/GNOME/pan.git?signed#tag=v$pkgver")
b2sums=(139c973ecc3d136bc15c0d47ede396a27c408b9560c2e222271c65727d663a13df11ae1ce9150b7fbdf15102a326a51ea3f646cc3d003276b4fff739d0d698c2)
validpgpkeys=(9F7239FCB664F10B33E9DC03C31F4FD949AB2B6C) # Dominique Dumont <domi.dumont@free.fr>

build() {
  cmake -S $pkgname -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D WANT_DBUS=1 \
    -D WANT_GMIME_CRYPTO=1 \
    -D WANT_NOTIFY=1 \
    -D WANT_GKR=1 \
    -D ENABLE_MANUAL=1
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
