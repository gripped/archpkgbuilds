# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: osch <oliver at luced de>

pkgbase=lua-lanes
_rockname=${pkgbase#lua-}
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
pkgname=lua-lanes
pkgver=3.17.1
pkgrel=1
_rockrel=0
pkgdesc='a lightweight, native, lazy evaluating multithreading library'
arch=(x86_64)
url="https://github.com/LuaLanes/$_rockname"
license=(MIT)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$_rockname-$pkgver"
_rock="$_archive-$_rockrel.linux-$CARCH.rock"
# upstream retagged https://github.com/LuaLanes/lanes/issues/225
source=("$url/archive/v$pkgver/$_archive-$pkgrel.tar.gz")
sha256sums=('ac454ef7fe14b1ffe43110b91cebd88d9e502d60733deb694607eb040e1c391a')

build() {
	cd "$_archive"
	for LUAVER in 5.{1,2,3,4}; do
		luarocks --lua-version "$LUAVER" \
			make --pack-binary-rock --deps-mode none -- "$_archive-$_rockrel.rockspec"
		install -Dm0644 -t "lua-$LUAVER/" "$_rock"
	done
}

_package() {
	cd "$_archive"
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		install --deps-mode none --no-manifest -- "lua-$1/$_rock"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYRIGHT
}

package_lua-lanes() {
	_package 5.4
	depends=("${pkgname%-*}")
}

package_lua53-lanes() {
	_package 5.3
	depends=("${pkgname%-*}")
}

package_lua52-lanes() {
	_package 5.2
	depends=("${pkgname%-*}")
}

package_lua51-lanes() {
	_package 5.1
	depends=("${pkgname%-*}")
}
