# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Gabriele Musco <emaildigabry@gmail.com>

pkgname=(
  openrazer-daemon
  openrazer-driver-dkms
  python-openrazer
)
pkgbase=openrazer
pkgver=3.10.1
pkgrel=1
pkgdesc='Community-led effort to support Razer peripherals on Linux'
arch=(any)
url=https://openrazer.github.io
license=(GPL)
makedepends=(
  git
  python-setuptools
)
source=("git+https://github.com/$pkgbase/$pkgbase.git#tag=v$pkgver")
b2sums=('79aad72a259d1a673e0eab6773461a5b7d16913f7ca09dd0817d1ab68dee29af4cfa767f66d8bcb44ddca35f19f6340c9a983b6b1c472eaccdda6c3340662991')

package_openrazer-daemon() {
  pkgdesc='Userspace daemon that abstracts access to the kernel driver. Provides a DBus service for applications to use'
  depends=(
    dbus-python
    openrazer-driver-dkms
    python-daemonize
    python-gobject
    python-pyudev
    python-setproctitle
    xautomation
  )
  optdepends=('libnotify: for the battery notifier')
  install=$pkgbase-daemon.install

  cd $pkgbase
  make DESTDIR="$pkgdir" daemon_install
}

package_openrazer-driver-dkms() {
  pkgdesc='OpenRazer kernel modules sources'
  depends=(dkms)

  cd $pkgbase
  make DESTDIR="$pkgdir" setup_dkms udev_install
}

package_python-openrazer() {
  pkgdesc='Library for interacting with the OpenRazer daemon'
  depends=(
    openrazer-daemon
    python-numpy
  )

  cd $pkgbase
  make DESTDIR="$pkgdir" python_library_install
}
