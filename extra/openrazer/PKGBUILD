# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Gabriele Musco <emaildigabry@gmail.com>

pkgname=(
  openrazer-daemon
  openrazer-driver-dkms
  python-openrazer
)
pkgbase=openrazer
pkgver=3.11.0
pkgrel=3
pkgdesc='Community-led effort to support Razer peripherals on Linux'
arch=(any)
url=https://openrazer.github.io
license=(GPL)
makedepends=(
  git
  python-setuptools
)
source=(
  "git+https://github.com/$pkgbase/$pkgbase.git#tag=v$pkgver"
  sysusers.conf
  0001-Use-openrazer-group.patch
)
b2sums=('1de999028896195de90a95cbf8c2a5f3d265a02942d3a2a035b46a2e580d72d22df45a8cb360121819fe39a6fa5206a2b0901cc7766888a947fb668377bd36b4'
        '4315fa4a7c79eb678c1d357b3326fcfec438b83e12741c86605dc4440414950b3b0e267b83056500f70b09a08a41004f46f40182d04fcd3ba8c55f724f1b436d'
        'd24c3dba7300bc375f11cc168f44b3488933b660355d101cd2f4b006c6ca33baa9d96f9523807d6392620f4d17349dd140ba1a58b84de930d117e548a4727d46')

prepare() {
  cd $pkgbase
  patch --forward --strip=1 --input=../0001-Use-openrazer-group.patch
}

package_openrazer-daemon() {
  pkgdesc='Userspace daemon that abstracts access to the kernel driver. Provides a DBus service for applications to use'
  depends=(
    dbus-python
    openrazer-driver-dkms
    python-daemonize
    python-gobject
    python-pyudev
    python-setproctitle
    xautomation
  )
  optdepends=('libnotify: for the battery notifier')

  cd $pkgbase
  make DESTDIR="$pkgdir" daemon_install
}

package_openrazer-driver-dkms() {
  pkgdesc='OpenRazer kernel modules sources'
  depends=(dkms)

  cd $pkgbase
  make DESTDIR="$pkgdir" setup_dkms udev_install
  install -Dm644 ../sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgbase.conf
}

package_python-openrazer() {
  pkgdesc='Library for interacting with the OpenRazer daemon'
  depends=(
    openrazer-daemon
    python-numpy
  )

  cd $pkgbase
  make DESTDIR="$pkgdir" python_library_install
}
