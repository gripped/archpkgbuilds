# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: revel <revelΘmuub·net>
# Contributor: Marcos J. S. Magalhaes <mjsmagalhaes ^dot^ insc _at_ gmail ~dot~ com>

pkgname=premake
pkgver=5.0beta3
_pkgver=5.0.0-beta3
pkgrel=2
pkgdesc="A simple build configuration and project generation tool"
arch=('x86_64')
url="https://premake.github.io/"
license=('BSD-3-Clause')
depends=('glibc' 'util-linux-libs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/premake/premake-core/archive/refs/tags/v${_pkgver}.tar.gz")
sha512sums=('3fd5cf526e28b36bf32d3015bcb5841b8dc3e3570029dd25442bba118369f104e7504682f7e1a409d879de562b19db55bccfed97939fef376c211cfb77ceff6e')

build() {
  cd "premake-core-$_pkgver"

  make -f Bootstrap.mak linux
}

package() {
  cd "premake-core-$_pkgver"

  install -Dm755 "bin/release/premake5" "${pkgdir}/usr/bin/premake5"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
