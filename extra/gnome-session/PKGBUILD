# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-session
pkgver=49.2
pkgrel=1
pkgdesc="The GNOME Session Handler"
url="https://gitlab.gnome.org/GNOME/gnome-session"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gnome-desktop-4
  gsettings-desktop-schemas
  gtk4
  json-glib
  systemd-libs
  xdg-desktop-portal-gnome
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  libxslt
  meson
  xmlto
)
conflicts=(gnome-mimeapps)
replaces=(gnome-mimeapps)
provides=(gnome-mimeapps)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-session.git#tag=${pkgver/[a-z]/.&}")
b2sums=('41a30f04b060adfd05662b7097a6553c6b71977b7dac33e682725ee2431128be91609ce3a266b9c65cbfed352dd6a07ed769981ddd876f0707ba3b12b57051cf')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D docbook=true
    -D man=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
