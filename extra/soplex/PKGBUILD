# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=soplex
pkgver=8.0.1
pkgrel=2
pkgdesc='Sequential object-oriented simPlex'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(Apache-2.0)
depends=(gcc-libs
         glibc
         gmp
         mpfr
         zlib)
makedepends=(boost
             cmake
             git
             papilo)
source=(git+https://github.com/scipopt/soplex#tag=v$pkgver)
sha256sums=('e91f51b412e237136c87af3ce04040d85a23077a90ccb9af3adc7c45cd39fd4f')

prepare() {
  cd soplex
  git cherry-pick -n 08cc39c1ac64095e1fda91729590101477091bf4 # Fix build with GCC 14
}

build() {
  cmake -B build -S soplex \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DMPFR=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Use shared library by default
  sed -e 's|SOPLEX_LIBRARIES libsoplex|SOPLEX_LIBRARIES libsoplexshared|' -i "$pkgdir"/usr/lib/cmake/soplex/soplex-config.cmake
}
