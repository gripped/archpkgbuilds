# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=soplex
pkgver=8.0.1
pkgrel=4
pkgdesc='Sequential object-oriented simPlex'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(Apache-2.0)
depends=(gcc-libs
         glibc
         gmp
         mpfr
         onetbb
         zlib)
makedepends=(boost
             cmake
             git
             papilo)
source=(git+https://github.com/scipopt/soplex#tag=v$pkgver
        no-static.patch)
sha256sums=('e91f51b412e237136c87af3ce04040d85a23077a90ccb9af3adc7c45cd39fd4f'
            '40ab0ed540fd85bf5c2346ea46ca3302bb24c051f577d7aa91103851519ac328')

prepare() {
# Do not build static libraries
  patch -d soplex -p1 < no-static.patch
}

build() {
  cmake -B build -S soplex \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DMPFR=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Replace static libraries with symlinks to shared library
  ln -s libsoplexshared.so "$pkgdir"/usr/lib/libsoplex.so
  ln -s libsoplexshared.so "$pkgdir"/usr/lib/libsoplex-pic.so
}
