# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=soplex
pkgver=8.0.0
pkgrel=1
pkgdesc='Sequential object-oriented simPlex'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(Apache-2.0)
depends=(gcc-libs
         glibc
         gmp
         mpfr
         zlib)
makedepends=(boost
             cmake
             git
             papilo)
source=(git+https://github.com/scipopt/soplex#tag=v$pkgver)
sha256sums=('189c725ded504fbef447e8de7bdca858ef1b06f191ed3a06dfb87c70247e0505')

prepare() {
  cd soplex
  git cherry-pick -n 08cc39c1ac64095e1fda91729590101477091bf4 # Fix build with GCC 14
}

build() {
  cmake -B build -S soplex \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DMPFR=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
