# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Michael Migliore <mcmigliore+aur@gmail.com>
# Contributor: Mathieu Wespthal <mathieu.westphal+aur@gmail.com>

pkgname=f3d
pkgver=3.1.0
pkgrel=2
pkgdesc='A fast and minimalist 3D viewer'
arch=('x86_64')
url="https://github.com/${pkgname}-app/${pkgname}"
license=('BSD-3-Clause')
depends=('alembic'
         'assimp'
         'boost'
         'draco'
         'fmt'
         'glew'
         'jsoncpp' # optdepend of vtk for features required by f3d
         'libxcursor'
         'netcdf'
         'nlohmann-json'
         'opencascade'
         'openexr'
         'ospray'
         'pugixml'
         'usd'
         'verdict'
         'vtk')
makedepends=('cmake'
             'cuda'
             'eigen'
             'fast_float'
             'help2man'
             'jdk-openjdk'
             'ninja'
             'openmp'
             'pybind11'
             'python'
             'strip-nondeterminism'
             'utf8cpp')
             source=("https://github.com/$pkgname-app/$pkgname/archive/refs/tags/v$pkgver/$pkgname-$pkgver.tar.gz")
             sha256sums=('93ba23078133122e929d9c1e2946c86da1f08fe56b9ffae40ebfd8185e91380a')

build() {
  cd "$pkgname-$pkgver"
  export CXXFLAGS+=' -ffat-lto-objects'
  cmake \
    -G Ninja \
    -B build \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None \
    -D F3D_BINDINGS_JAVA=ON \
    -D F3D_BINDINGS_PYTHON=ON \
    -D F3D_LINUX_GENERATE_MAN=ON \
    -D F3D_MODULE_RAYTRACING=ON \
    -D F3D_MODULE_EXR=ON \
    -D F3D_PLUGINS_STATIC_BUILD=ON \
    -D F3D_PLUGIN_BUILD_ALEMBIC=ON \
    -D F3D_PLUGIN_BUILD_ASSIMP=ON \
    -D F3D_PLUGIN_BUILD_DRACO=ON \
    -D F3D_PLUGIN_BUILD_OCCT=ON \
    -D F3D_PLUGIN_BUILD_USD=ON
  ninja -C build

  # Strip non determinism information from the jar file (e.g. timestamps and filesystem ordering)
  # Required for reproducible builds
  find . -name "$pkgname.jar" -exec strip-nondeterminism {} +
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir" cmake --install build
  DESTDIR="$pkgdir" cmake --install build --component mimetypes
  DESTDIR="$pkgdir" cmake --install build --component sdk
  DESTDIR="$pkgdir" cmake --install build --component configuration
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
