# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Michael Migliore <mcmigliore+aur@gmail.com>
# Contributor: Mathieu Wespthal <mathieu.westphal+aur@gmail.com>

pkgname=f3d
pkgver=2.5.1
pkgrel=3
pkgdesc='A fast and minimalist 3D viewer'
arch=('x86_64')
url="https://github.com/${pkgname}-app/${pkgname}"
license=('BSD-3-Clause')
depends=('alembic'
         'assimp'
         'boost'
         'draco'
         'fmt'
         'glew'
         'jsoncpp' # optdepend of vtk for features required by f3d
         'libxcursor'
         'netcdf'
         'nlohmann-json'
         'opencascade'
         'openexr'
         'ospray'
         'pugixml'
         'usd'
         'verdict'
         'vtk')
makedepends=('cmake'
             'eigen'
             'fast_float'
             'help2man'
             'jdk-openjdk'
             'ninja'
             'openmp'
             'pybind11'
             'python'
             'utf8cpp')
source=("https://github.com/$pkgname-app/$pkgname/archive/refs/tags/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('55ea01931f90f066df1abc0ae4e9575672e80b83b241f51884224baa8dccac24')

build() {
  cd "$pkgname-$pkgver"
  export CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DF3D_BINDINGS_JAVA=ON \
    -DF3D_BINDINGS_PYTHON=ON \
    -DF3D_LINUX_GENERATE_MAN=ON \
    -DF3D_MODULE_EXTERNAL_RENDERING=ON \
    -DF3D_MODULE_RAYTRACING=ON \
    -DF3D_MODULE_EXR=ON \
    -DF3D_PLUGINS_STATIC_BUILD=ON \
    -DF3D_PLUGIN_BUILD_ALEMBIC=ON \
    -DF3D_PLUGIN_BUILD_ASSIMP=ON \
    -DF3D_PLUGIN_BUILD_DRACO=ON \
    -DF3D_PLUGIN_BUILD_OCCT=ON \
    -DF3D_PLUGIN_BUILD_USD=ON
  ninja -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir" cmake --install build
  DESTDIR="$pkgdir" cmake --install build --component mimetypes
  DESTDIR="$pkgdir" cmake --install build --component sdk
  DESTDIR="$pkgdir" cmake --install build --component configuration
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
