# Maintainer: Dan Johansen <strit@archlinux.org>

pkgname=cellbroadcastd
pkgver=0.0.2
pkgrel=1
pkgdesc='A Cell broadcast dameon'
arch=('x86_64' 'aarch64')
url='https://gitlab.freedesktop.org/devrtz/cellbroadcastd'
license=(GPL-3.0-or-later)
depends=(
        "mobile-broadband-provider-info"
        "libmm-glib"
        "libgmobile"
        "dconf"
)
makedepends=(
            "git"
            "meson"
            "glib2-devel"
            "gobject-introspection"
)

_gvdb=4758f6fb7f889e074e13df3f914328f3eecb1fd3
source=(
        "${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
        "git+https://gitlab.gnome.org/GNOME/gvdb.git#commit=${_gvdb}"
)
sha256sums=('beba7d5081262e0b69db9bea1dfeabaa9bc8d296e14956b32ca1882973bec720'
            'ebe771e60943547279fbf29acb6aea6346fc20df6388252f71bf2bf679d3a7a8')

prepare() {
  cp -r gvdb "${pkgname}-v${pkgver}/subprojects/gvdb"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
