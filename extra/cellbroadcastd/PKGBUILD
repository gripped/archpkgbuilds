# Maintainer: Dan Johansen <strit@archlinux.org>

pkgname=cellbroadcastd
pkgver=0.0.3
pkgrel=1
pkgdesc='A Cell broadcast dameon'
arch=(x86_64)
url='https://gitlab.freedesktop.org/devrtz/cellbroadcastd'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  libgmobile
  libmm-glib
  mobile-broadband-provider-info
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=(
  "git+https://gitlab.freedesktop.org/devrtz/cellbroadcastd.git#tag=v$pkgver"
  git+https://gitlab.gnome.org/GNOME/gvdb.git
)
b2sums=(
  56651fb96131283c01392b4edcaf6755fbce0ff913b0e34cd36d6aa8ce3bd072fe757ecc7a16256b324da993386a153c62fed785bbb3d10cc27ec1dd43bc5f1c
  SKIP
)

prepare() {
  ln -s "$srcdir/gvdb" "$pkgname/subprojects/gvdb"
  meson subprojects checkout --sourcedir $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
