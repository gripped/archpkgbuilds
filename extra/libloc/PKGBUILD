# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Macxcool <macx cool at tutanota dot com>
# Contributor: loqs

pkgbase=libloc
pkgname=(
  libloc
  libloc-database
  lua-location
  python-location
)
pkgver=0.9.18
pkgrel=3
pkgdesc="IPFire Location Database - Determine the location of someone on the Internet"
url='https://git.ipfire.org/?p=location/libloc.git;a=summary'
arch=('x86_64')
license=('LGPL-2.1-or-later')
makedepends=(
  asciidoc
  intltool
  libcrypto.so
  lua
  python
  systemd
)
source=(https://source.ipfire.org/releases/libloc/libloc-${pkgver}.tar.gz)
sha256sums=('16c76689fa0ea8ece4fca6abcf942f236c48440387bee1d9331ea937ccd7b87d')
b2sums=('ccb730684c98756ef318f0cd5698b42ee4a997083811025ac5bf650798efb70dca97e1638b3f20da5d7b38a204fe5214a87fa7b61b8b70d71e6c8ad0577b0839')

prepare() {
  cd ${pkgbase}-${pkgver}
  intltoolize -f
  autoreconf -fi
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --localstatedir=/var --disable-perl
  make
}

check() {
  cd ${pkgbase}-${pkgver}
  make -k check
}

package_libloc() {
  depends=(
    glibc
    libcrypto.so
    libloc-database
  )
  provides=(libloc.so)

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -r \
    "${pkgdir}/usr/bin" \
    "${pkgdir}/usr/lib/lua" \
    "${pkgdir}/usr/lib/python3"* \
    "${pkgdir}/usr/lib/systemd" \
    "${pkgdir}/usr/share/bash-completion" \
    "${pkgdir}/usr/share/man/man1" \
    "${pkgdir}/var"

  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_libloc-database() {
  pkgdesc+=' (Database files)'
  license=('CC-BY-SA-4.0')
  install="${pkgname}.install"

  cd ${pkgbase}-${pkgver}
  install -Dm 644 data/database.db "${pkgdir}/usr/share/libloc-location/location.db"
}

package_lua-location() {
  pkgdesc+=' (Lua bindings)'
  depends=(
    glibc
    libcrypto.so
    libloc.so
    libssl.so
    lua
  )

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -r \
    "${pkgdir}/usr/bin" \
    "${pkgdir}/usr/include" \
    "${pkgdir}/usr/lib/libloc.so"* \
    "${pkgdir}/usr/lib/pkgconfig" \
    "${pkgdir}/usr/lib/python3"* \
    "${pkgdir}/usr/lib/systemd" \
    "${pkgdir}/usr/share/bash-completion" \
    "${pkgdir}/usr/share/locale" \
    "${pkgdir}/usr/share/man" \
    "${pkgdir}/var"

  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python-location() {
  pkgdesc+=' (Python tooling)'
  depends=(
    glibc
    libcrypto.so
    libloc.so
    python
    python-psycopg2
  )

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -r \
    "${pkgdir}/usr/include" \
    "${pkgdir}/usr/lib/libloc.so"* \
    "${pkgdir}/usr/lib/lua" \
    "${pkgdir}/usr/lib/pkgconfig" \
    "${pkgdir}/usr/share/locale" \
    "${pkgdir}/usr/share/man/man3" \
    "${pkgdir}/var/lib/location/database.db"

  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
