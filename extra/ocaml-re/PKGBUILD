# Maintainer: Jürgen Hötzel <juergen@archlinux.org>

_pkgname=re
pkgname=ocaml-${_pkgname}
pkgver=1.13.3
pkgrel=2
pkgdesc="A regular expression library for OCaml"
arch=('x86_64')
url="https://github.com/ocaml/ocaml-re"
license=('LGPL2.1')
depends=('ocaml')
makedepends=('dune')
options=('!strip')
source=("${url}/archive/${pkgver}/${_pkgname}-${pkgver}.tar.gz" "${pkgname}-remove-seq.patch")
sha256sums=('7d95f98286206be469dc87c34915f4e2f995f0cbd358899011f3ff4e2bf260a2'
            'bc074a19437db2010aa187d25350070239fba52e52331249ab96e3df05c1c415')

prepare() {
	cd "${pkgname}-${pkgver}"
	patch -p1 -i "${srcdir}/${pkgname}-remove-seq.patch"
}

build() {
  cd ${pkgname}-$pkgver
  dune build --profile release -p $_pkgname
}

package() {
  cd ${pkgname}-$pkgver

  DESTDIR="${pkgdir}" dune install --prefix '/usr' --libdir '/usr/lib/ocaml' '--docdir' '/usr/share/doc' --release
}
