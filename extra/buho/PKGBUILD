# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=buho
pkgver=4.0.0
pkgrel=1
pkgdesc='Task and note keeper'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         mauikit
         mauikit-accounts
         mauikit-filebrowsing
         mauikit-texteditor
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        fix-build.patch)
sha256sums=('ffa4125150b35f3e2944a8ef04a781ce5d2f874bd7c2c44bf7be969f836dbddf'
            'SKIP'
            'ad61cf43d27226a2af4f0cae19a73d4c9db8ea77606c2e6e90961f4c9aa6bfe2')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

prepare() {
  patch -d $pkgname-$pkgver -p1 < fix-build.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
