# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=buho
pkgver=4.0.1
pkgrel=1
pkgdesc='Task and note keeper'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         mauikit
         mauikit-accounts
         mauikit-filebrowsing
         mauikit-texteditor
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('87a3a8fa4beccbcda31fdc5393c7eabc688be98bb99c24f4d9ac7fdc80efeb4e'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
