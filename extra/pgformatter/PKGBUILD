# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco44 (Marc Cousin) <cousinmarc at gmail dot com>

pkgname=pgformatter
pkgver=5.8
pkgrel=1
pkgdesc='A PostgreSQL SQL syntax beautifier'
arch=(any)
url='https://github.com/darold/pgformatter'
license=(PostgreSQL)
depends=(perl)
makedepends=(git)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('9fe53a60a4069b053edc6326e07b6361407cac3fd7b32340f0d2e7393d01f1bcceb1bb24ef24fd4d2d0ff49d3b70ff3fe5431d567a5cbd7cc9bd19399035daa6')
b2sums=('6aa5ac5a7da1442d036268eac59adf42fd8e6243734c2922efea37f2177d8f08a699b52d9720b8c113224ae2917b3b4bcd625d6eeda74aca5a7a8397b66fbd61')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps

  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1

  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"

  # provide symlink in /usr/bin for non-interactive shells
  ln -sf /usr/bin/vendor_perl/pg_format "$pkgdir/usr/bin/pg_format"

  # delete usr/lib as it's empty ...
  rm -rf "$pkgdir/usr/lib"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
