# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco44 (Marc Cousin) <cousinmarc at gmail dot com>

pkgname=pgformatter
pkgver=5.6
pkgrel=1
pkgdesc='A PostgreSQL SQL syntax beautifier'
arch=('any')
url='https://github.com/darold/pgformatter'
license=('PostgreSQL')
depends=('perl')
makedepends=('git')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('49ab930e3e4a771c046f7164b5f4072c1e9889c82a4f1e74da88c2bbe9d3b4b71eb05a58f74e0ff910a685e832fb967f9457f2aecef77c3b9c0f89dcaabc0e3a')
b2sums=('0f6d70b26ab9683e0ae0af9d40713e1a808279a81fc679af2c69781347d84c16fb3e237fde455c4ca5e17f56e4d2487450364c4ecfd48997a040df4b4569d454')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps

  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1

  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"

  # provide symlink in /usr/bin for non-interactive shells
  ln -sf /usr/bin/vendor_perl/pg_format "$pkgdir/usr/bin/pg_format"

  # delete usr/lib as it's empty ...
  rm -rf "$pkgdir/usr/lib"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
