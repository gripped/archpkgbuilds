# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco44 (Marc Cousin) <cousinmarc at gmail dot com>

pkgname=pgformatter
pkgver=5.9
pkgrel=1
pkgdesc='A PostgreSQL SQL syntax beautifier'
arch=(any)
url='https://github.com/darold/pgformatter'
license=(PostgreSQL)
depends=(perl)
makedepends=(git)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('d100459939cb6f31f9ec3b131d910de73b94f7da65d251c3c7966c25ac84de757a20a9de1716730506654f602969e616aaec3f98f13bd98f61355ab9445cf002')
b2sums=('5526b81ba4aba0da873be2fe96c2edf3930203468ed3ace474e7c23e41eaf268a10e48e0144b19f85842f0f677de6712009b7d1fccbe4f253cdd7317e57be958')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps

  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1

  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"

  # provide symlink in /usr/bin for non-interactive shells
  ln -sf /usr/bin/vendor_perl/pg_format "$pkgdir/usr/bin/pg_format"

  # delete usr/lib as it's empty ...
  rm -rf "$pkgdir/usr/lib"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
