# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco44 (Marc Cousin) <cousinmarc at gmail dot com>

pkgname=pgformatter
pkgver=5.7
pkgrel=1
pkgdesc='A PostgreSQL SQL syntax beautifier'
arch=(any)
url='https://github.com/darold/pgformatter'
license=(PostgreSQL)
depends=(perl)
makedepends=(git)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('935ba278a1664e5dba6f3da02b41818f9e3378c46141d82aaff32c61bf4b64adbc9874624c72be832cfa9ae153ac9ce486dd608643518508d1e522987807e1a6')
b2sums=('afb77ac9422bdab780a6515b9e26b4f3568a55491f3b48d6e5fb1467b8a36ab5a7e63801c6d42bc3d82238172ca81df7adafc7543c4cc9b64728a3a9541cd257')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps

  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1

  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"

  # provide symlink in /usr/bin for non-interactive shells
  ln -sf /usr/bin/vendor_perl/pg_format "$pkgdir/usr/bin/pg_format"

  # delete usr/lib as it's empty ...
  rm -rf "$pkgdir/usr/lib"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
