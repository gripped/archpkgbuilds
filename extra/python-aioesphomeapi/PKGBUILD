# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: sgar < swhaat at github >

pkgname=python-aioesphomeapi
_name=${pkgname#python-}
pkgver=27.0.1
pkgrel=1
pkgdesc='Python Client for ESPHome native API'
arch=(x86_64)
url='https://github.com/esphome/aioesphomeapi'
depends=(
	glibc
	python
	python-aiohappyeyeballs
	python-async_interrupt
	python-protobuf
	python-zeroconf
	python-chacha20poly1305-reuseable
	python-cryptography
	python-noiseprotocol
	python-async-timeout
)
makedepends=(python-build python-installer python-wheel python-setuptools git cython)
license=(MIT)
source=(git+$url.git#tag=v$pkgver)
b2sums=('504bc26d2b8517b7460f187e94e95b3dd51fa482d85f5b16fefa3c6fb939fd48fe5a13b498c94e3f939c3812c68beeca4bdd3a8bf54371a5e0ccc7121e101ffa')

prepare() {
  cd $_name
  sed -i 's/==.*//' requirements.txt
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

package() {
	cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
