# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: sgar < swhaat at github >

pkgname=python-aioesphomeapi
_name=${pkgname#python-}
pkgver=29.0.2
pkgrel=1
pkgdesc='Python Client for ESPHome native API'
arch=(x86_64)
url='https://github.com/esphome/aioesphomeapi'
depends=(
	glibc
	python
	python-aiohappyeyeballs
	python-async_interrupt
	python-protobuf
	python-zeroconf
	python-chacha20poly1305-reuseable
	python-cryptography
	python-noiseprotocol
	python-async-timeout
)
makedepends=(python-build python-installer python-wheel python-setuptools git cython)
license=(MIT)
source=(git+$url.git#tag=v$pkgver)
b2sums=('119c5a010e46c8b07654d9d6c471ef712352f7db8fcb68a11c6cd8ab01cf5cf1162594863aeb9ffe6ad7fed47b8a5c7782d4b9d46e212e50b18ae94d0188f6f5')

prepare() {
  cd $_name
  sed -i 's/==.*//' requirements/base.txt
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

package() {
	cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
