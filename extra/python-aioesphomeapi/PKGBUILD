# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: sgar < swhaat at github >

pkgname=python-aioesphomeapi
_name=${pkgname#python-}
pkgver=32.0.0
pkgrel=1
pkgdesc='Python Client for ESPHome native API'
arch=(x86_64)
url='https://github.com/esphome/aioesphomeapi'
depends=(
	glibc
	python
	python-aiohappyeyeballs
	python-async_interrupt
	python-protobuf
	python-zeroconf
	python-chacha20poly1305-reuseable
	python-cryptography
	python-noiseprotocol
)
makedepends=(python-build python-installer python-wheel python-setuptools git cython)
license=(MIT)
source=(git+$url.git#tag=v$pkgver)
b2sums=('8ab2dcbf424528e7dd2f2015b6230933417b64cb652dd794594038f2307aa05b98476e9e5769709a97fcedff95869640e180abaf1cf8dc612d8a38d36a813314')

prepare() {
  cd $_name
  sed -i 's/==.*//' requirements/base.txt
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

package() {
	cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
