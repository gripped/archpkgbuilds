# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: sgar < swhaat at github >

pkgname=python-aioesphomeapi
_name=${pkgname#python-}
pkgver=29.8.0
pkgrel=1
pkgdesc='Python Client for ESPHome native API'
arch=(x86_64)
url='https://github.com/esphome/aioesphomeapi'
depends=(
	glibc
	python
	python-aiohappyeyeballs
	python-async_interrupt
	python-protobuf
	python-zeroconf
	python-chacha20poly1305-reuseable
	python-cryptography
	python-noiseprotocol
)
makedepends=(python-build python-installer python-wheel python-setuptools git cython)
license=(MIT)
source=(git+$url.git#tag=v$pkgver)
b2sums=('09271599b9e7caa5ba6a39f45d00c3e46e42abcddb9d3c7806e900ed5c834c1a4b2e30c1268e9c6b793240e5856ffb6ea2b728b7e47456194ccd1fcefb02b0fd')

prepare() {
  cd $_name
  sed -i 's/==.*//' requirements/base.txt
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

package() {
	cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
