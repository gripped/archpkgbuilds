# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: sgar < swhaat at github >

pkgname=python-aioesphomeapi
_name=${pkgname#python-}
pkgver=29.4.1
pkgrel=1
pkgdesc='Python Client for ESPHome native API'
arch=(x86_64)
url='https://github.com/esphome/aioesphomeapi'
depends=(
	glibc
	python
	python-aiohappyeyeballs
	python-async_interrupt
	python-protobuf
	python-zeroconf
	python-chacha20poly1305-reuseable
	python-cryptography
	python-noiseprotocol
)
makedepends=(python-build python-installer python-wheel python-setuptools git cython)
license=(MIT)
source=(git+$url.git#tag=v$pkgver)
b2sums=('7bd5e51fe9d739616af6c375ff87f11674afbc82980fa764b60dc6987df85635658aa826585f075319d7aaaa8c9a48adace95504a2d9ab0fe3e72473ee53c2fd')

prepare() {
  cd $_name
  sed -i 's/==.*//' requirements/base.txt
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

package() {
	cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
