# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SÃ©bastien Luttringer

pkgname=arpwatch
pkgver=3.9
pkgrel=1
pkgdesc='Ethernet/FDDI station activity monitor'
arch=('x86_64')
url='https://ee.lbl.gov/'
license=('BSD')
depends=('libpcap' 'smtp-forwarder')
source=("https://ee.lbl.gov/downloads/arpwatch/arpwatch-$pkgver.tar.gz"
        "$pkgname-license.txt::https://ee.lbl.gov/nrg-copyright.txt"
        "ethercodes.dat.gz"
        "$pkgname.service")
sha512sums=('b6fdda79caf6c79d78d629b28987d381981d8ac9150dd95c44ba0ed634d905446a8b64d73cdacca89d42a77965e0710d0a60184010440fed19168dae4e3dd4bd'
            'c0ec84cb61b96f9b89227e841b4ccad7007b2a86c379d527386ff75a68b56be8c70e32f044bb533282071e9bc66448a8c2ac3d290738bf7dddd6c9c0b757994f'
            'dc9818e6453e0d9ef38864536ea66b15a8acb48091b609e422227538d3d4c6b93cc853778f8f4ea8cd887dc15c1c62da66f4325c540eb80f9c78705d63a65226'
            '4a9460c8a376d9183265b73e2c3b02965223144fecfeeb630ec5e70d40d4aed5725e5a43ae370938afa0a2fd362785020eca679e585a7f400e4e67f255807a84')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/@HAVE_FREEBSD_TRUE@/d' Makefile.in
  sed -i '1i#include <time.h>' report.c
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin \
    --with-watcher=arpwatch \
    ac_cv_have_dn_skipname=no
  make SENDMAIL=/usr/bin/sendmail
}

package() {
  cd $pkgname-$pkgver
  install -d -m 0755 "$pkgdir"/usr/{bin,share/man/man8}
  make DESTDIR="$pkgdir" install
  # install ether prefix database
  install -Dm644 "$srcdir"/ethercodes.dat "$pkgdir/var/lib/$pkgname/ethercodes.dat"
  # license
  install -Dm644 "$srcdir"/$pkgname-license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # systemd
  install -Dm644 "$srcdir"/$pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname@.service"
}
