# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gio-qt
pkgver=0.0.14
pkgrel=1
pkgdesc='Gio wrapper for Qt applications'
arch=('x86_64')
url="https://github.com/linuxdeepin/gio-qt"
license=('LGPL-3.0-or-later')
depends=('glibmm' 'qt6-base')
makedepends=('git' 'cmake' 'qt6-tools')
source=("git+https://github.com/linuxdeepin/gio-qt.git#tag=$pkgver")
sha512sums=('2ae1fbc977d210aa377ba88271c9ca25cb6fc77c3ae0073b2a840ceac87120b64f1afc1ff8f690a8759217fadd19e82141f9321b36ee50fb64b3cfd13590c2bb')

prepare() {
  cd gio-qt
  sed -i '/include(qt5.cmake)/d' gio-qt/CMakeLists.txt qgio-tools/CMakeLists.txt
}

build() {
  cd gio-qt
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_DOCS=OFF .
  make
}

package() {
  cd gio-qt
  make DESTDIR="$pkgdir" install
}
