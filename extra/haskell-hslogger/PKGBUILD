# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haskell-hslogger
pkgver=1.3.1.1
pkgrel=7
pkgdesc="Versatile logging framework"
url="https://hackage.haskell.org/package/hslogger"
license=('LGPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-network' 'haskell-network-bsd' 'haskell-old-locale')
makedepends=('ghc' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/hslogger/${pkgver}/hslogger-${pkgver}.tar.gz")
sha512sums=('b1ac3b378600b179c96fa4ec84daaf2edf58cc83830cfa8a3bce062a38e8e46790d1acc9a58b9b4b84ffed8eea644dbaf271f5dec4c7cde2c4f5eaf4f4221b88')

build() {
    cd "${srcdir}/hslogger-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd "${srcdir}/hslogger-${pkgver}"    
    runhaskell Setup test --show-details=direct
}

package() {
    cd "${srcdir}/hslogger-${pkgver}"
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
