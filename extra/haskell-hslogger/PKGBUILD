# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haskell-hslogger
pkgver=1.3.1.2
pkgrel=10
pkgdesc="Versatile logging framework"
url="https://hackage.haskell.org/package/hslogger"
license=('LGPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-network' 'haskell-network-bsd' 'haskell-old-locale')
makedepends=('ghc' 'uusi' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/hslogger/$pkgver/hslogger-$pkgver.tar.gz")
sha512sums=('b7b6e1cfc809f577541699106ec497da0241ea726ed0b9ed647dab4b98a99ffeb6a80b946fa6a4a535232f0a3e3051c48d2810d72e07c07871be530911aaadfb')

prepare() {
    cd hslogger-$pkgver
    gen-setup
}

build() {
    cd hslogger-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd hslogger-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd hslogger-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
