# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Loui Chang <louipc dot ist at gmail company>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=tig
pkgver=2.6.0
pkgrel=1
pkgdesc='Text-mode interface for Git.'
depends=('git'
         'ncurses' 'libncursesw.so'
         'pcre2' 'libpcre2-posix.so' 'libpcre2-8.so'
         'readline' 'libreadline.so')
makedepends=('asciidoc' 'xmlto')
url='https://jonas.github.io/tig/'
license=('GPL')
arch=('x86_64')
backup=('etc/tigrc')
source=("https://github.com/jonas/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('6fc529025432932eb42183a36f827c00')
sha256sums=('99d4a0fdd3d93547ebacfe511195cb92e4f75b91644c06293c067f401addeb3e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install install-doc-man

  install -Dm0644 contrib/tig-completion.bash "$pkgdir/usr/share/bash-completion/completions/tig"
}
