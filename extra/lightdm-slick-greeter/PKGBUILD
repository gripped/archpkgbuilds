# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sam Burgos <santiago.burgos1089@gmail.com>

pkgname=lightdm-slick-greeter
pkgver=2.2.4
pkgrel=1
pkgdesc='A slick-looking LightDM greeter'
arch=('x86_64')
url="https://github.com/linuxmint/slick-greeter"
license=('GPL3.0-only')
depends=('cairo' 'freetype2' 'gtk3' 'libcanberra' 'libxext' 'lightdm' 'pixman'
         'python' 'xorg-server' 'xapp')
optdepends=('numlockx: enable numerical keypad on supported keyboard')
makedepends=('intltool' 'vala' 'gnome-common' 'meson')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha512sums=('0dfef12f4a85c79eef184c32d0cdbf992f96b8fad93551b1d5cb0c529ed727902033ba0634b33c036c01dcaeef91c446962ce538bfff2e838c57332e55c07d0c')
b2sums=('716dee9282bb21141e09fed988eeead487cf8f158291d3c1f753377f260c484fea7da251e952df3d14666942eb8f3fb122f92170c36bc0a0e727dfef8db467da')

build() {
	cd slick-greeter-${pkgver}
	arch-meson . build
	meson compile -C build
}

check() {
	cd slick-greeter-${pkgver}
	meson test -C build
}

package() {
	cd slick-greeter-${pkgver}
	meson install -C build --destdir "$pkgdir"
	# adjust launcher name
	mv $pkgdir/usr/share/xgreeters/slick-greeter.desktop \
    $pkgdir/usr/share/xgreeters/lightdm-slick-greeter.desktop
}
