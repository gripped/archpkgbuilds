# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=fpylll
pkgname=python-fpylll
pkgver=0.6.2
pkgrel=2
pkgdesc='A Python interface for fplll'
arch=(x86_64)
url='https://github.com/fplll/fpylll'
license=(GPL-2.0-only)
depends=(fplll
         gcc-libs
         glibc
         gmp
         mpfr
         python
         python-cysignals
         python-numpy
         qd)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
source=(git+https://github.com/fplll/fpylll#tag=$pkgver)
sha256sums=('a7a5373957b7ea95dd17013b42f73a2fe29c6cb69e8f4a20f17fc7c516c08ccf')

prepare() {
  cd $pkgbase
  sed -e 's|<1.12.0||' -i pyproject.toml # Drop cysignals version constraints
}

build() {
  cd $pkgbase
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgbase
  PYTHONPATH="$PWD"/$(ls -d build/lib.*) \
  pytest -v tests
}

package_python-fpylll() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
