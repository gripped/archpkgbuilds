# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=fpylll
pkgname=python-fpylll
pkgver=0.6.1
pkgrel=4
pkgdesc='A Python interface for fplll'
arch=(x86_64)
url='https://github.com/fplll/fpylll'
license=(GPL-2.0-only)
depends=(fplll
         gcc-libs
         glibc
         gmp
         mpfr
         python
         python-cysignals
         python-numpy
         qd)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
source=(git+https://github.com/fplll/fpylll#tag=$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgbase
  git cherry-pick -n bc4396095e0efb683bcc579c45c4c7527574a4b0 5e16a94815a6124937c0a26e355bbf7caddd39ad # Fix build with fplll 5.5
}

build() {
  cd $pkgbase
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgbase
  PYTHONPATH="$PWD"/$(ls -d build/lib.*) \
  pytest -v tests
}

package_python-fpylll() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
