# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname=reuse
pkgver=4.0.2
pkgrel=1
pkgdesc='Helper tool for providing and confirming copyright and licensing information'
arch=('any')
url='https://reuse.software/'
license=(
  'Apache-2.0'
  'CC-BY-SA-4.0'
  'CC0-1.0'
  'GPL-3.0-or-later'
)
depends=(
  'python'
  'python-debian'
  'python-license-expression'
  'python-boolean.py'
  'python-jinja'
  'python-binaryornot'
  'python-attrs'
  'python-tomlkit'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-poetry-core'
)
checkdepends=('python-pytest' 'python-freezegun')
optdepends=(
  'git: git repository support'
  'mercurial: mercurial repository support'
)
source=("$pkgname::git+https://github.com/fsfe/reuse-tool#tag=v$pkgver")
sha512sums=('efd429810a159192e50cffc58f5fe8bc0ddb5f3254f62c22e2eb5df4ba504681673d59fbd471ca0ffe1a7b847ed07fbbf58462dee0c72882f9a7daadc309859f')
b2sums=('199c8139894449064776993e31dd1f862bd55938ef3d686a5e135d4fd79068cc90298ef8153cc15a9d40e9c0f98dd35f805d64f9cb795e7e4cbd1d56df7e9c18')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.{rst,md}

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSES/*

  sed -n '/## License/,$ p' README.md > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
