# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname=reuse
pkgver=5.1.0
pkgrel=1
pkgdesc='Helper tool for providing and confirming copyright and licensing information'
arch=(any)
url='https://reuse.software/'
license=(
  Apache-2.0
  CC-BY-SA-4.0
  CC0-1.0
  GPL-3.0-or-later
)
depends=(
  python
  python-debian
  python-license-expression
  python-boolean.py
  python-jinja
  python-binaryornot
  python-attrs
  python-tomlkit
  python-click
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-poetry-core
  python-sphinx
  python-sphinxcontrib-apidoc
  python-myst-parser
  python-sphinx-furo
)
checkdepends=(python-pytest python-freezegun)
optdepends=(
  'git: git repository support'
  'mercurial: mercurial repository support'
)
source=("$pkgname::git+https://github.com/fsfe/reuse-tool#tag=v$pkgver")
sha512sums=('75cdcedd254fd379b53a9fc175ed67eed1e295e57f45c224fdbff9865e89e1245c7f0bb29056fed059223bbf155a16a2482761211cd6435473e3751427f7a467')
b2sums=('7831a96807d139f8e5c4844967299a72f8e366df40d316e10a93f1a34f60833a004954f4bcef739924b09d85ec376ee3389ac1e68b7f29f4a2e039be5b0f0d0a')

prepare() {
  cd "$pkgname"

  # FTBFS
  sed -e 's/poetry run //g' -i docs/Makefile
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(test-env/bin/python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  make -C docs man

  export PATH="$PATH:$(pwd)/tmp/build/reuse/src/reuse/test-env/bin/"
  mkdir completions
  _REUSE_COMPLETE=bash_source reuse > completions/bash
  _REUSE_COMPLETE=fish_source reuse > completions/fish
  _REUSE_COMPLETE=zsh_source  reuse > completions/zsh
}

check() {
  cd "$pkgname"

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  python -P -m pytest -k 'not test_version and not test_help_is_default'
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.{rst,md}
  install -vDm644 -t "$pkgdir/usr/share/man/man1" ./docs/_build/man/*.1

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSES/*

  sed -n '/## License/,$ p' README.md > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -Dm644 completions/bash \
    "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  install -Dm644 completions/fish \
    "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgname.fish
  install -Dm644 completions/zsh \
    "$pkgdir"/usr/share/zsh/site-functions/_$pkgname
}
