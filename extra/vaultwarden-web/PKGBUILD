# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
pkgver=2025.12.1.1
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=(any)
url=https://github.com/vaultwarden/vw_web_builds
license=(GPL-3.0-only)
depends=(vaultwarden)
makedepends=(
  git
  nodejs-lts
  npm
)
install=$pkgname.install
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0d759802510d629508f5c6d84a046b59f5704dc55a63dd19dc2fc51123fc376bb835f557034aef72233e17e11ae5950bbc3ed3437e695e1ef379681695bbcfb1')
b2sums=('3cefc96134c5888d3d410dca7aae34914051b58d4b3c96900829d1a129bb3f55beb55938493f26d6c2d1f6a933db4db2c5ce0d6a9d2a55162d30964cc1038bc8')

prepare() {
  cd "$pkgname"

  export ELECTRON_SKIP_BINARY_DOWNLOAD=1

  npm ci
}

build() {
  cd "$pkgname/apps/web"

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    npm pkg set scripts.build:oss='cross-env NODE_OPTIONS="--max-old-space-size=1536" webpack'
  fi

  npm run dist:oss:selfhost

  # https://github.com/dani-garcia/bw_web_builds/blob/a84aca58170a60ac04a695851eeb6e70f577de09/scripts/build_web_vault.sh#L30-L33
  printf '{"version":"%s"}' "$pkgver" > build/vw-version.json
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/usr/share/webapps/$pkgname"

  cp -R ./apps/web/build/* "$pkgdir/usr/share/webapps/$pkgname"
}
