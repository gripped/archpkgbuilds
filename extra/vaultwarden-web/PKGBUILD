# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2025.1.1
pkgver=2025.1.1
_upstreamver=${_pkgver%[[:lower:]]}
_patchver=$_upstreamver
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=(any)
url=https://github.com/dani-garcia/bw_web_builds
license=(GPL-3.0-only)
depends=(vaultwarden)
makedepends=(
  git
  nodejs
  npm
)
install=$pkgname.install
source=(
  "bitwarden-clients::git+https://github.com/bitwarden/clients.git#tag=web-v$_upstreamver"
  "git+https://github.com/dani-garcia/bw_web_builds#tag=v$_pkgver"
)
b2sums=('ae29d83d9d05d8ba945e53066c12f01556d695b64c16cabfaa395d465b261e9154cab8fd241cfad79b771138d3c96c16ba5a8f44df0ba0755cb28425dc0c0296'
        '7758c03c100db0a2df07d94ad7c6cd2cc4b77f0e3e5aa1d33dc4cc2728d5322fd066d8c31fa3178a2e1465ad7acc64b88f39a5c2d9820090cce72355e344d4e4')

# Appease vercmp by prepending trailing letter with a period
# shellcheck disable=SC2001
pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-clients

  # Copy Vaultwarden images
  cp -vr ../bw_web_builds/resources/src/images/{logo-{dark,white}@2x,icon-white}.png \
    apps/web/src/images

  # Apply Vaultwarden patch
  patch --forward --strip=1 --input=../bw_web_builds/patches/v"$_patchver".patch

  npm ci
}

build() {
  cd bitwarden-clients/apps/web

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    export NODE_OPTIONS="--max-old-space-size=1536"
  fi
  npm run dist:oss:selfhost
}

package() {
  install -d "$pkgdir"/usr/share/webapps/$pkgname

  cp -R bitwarden-clients/apps/web/build/* "$pkgdir"/usr/share/webapps/$pkgname
}
