# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
pkgver=2025.12.0.0
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=(any)
url=https://github.com/vaultwarden/vw_web_builds
license=(GPL-3.0-only)
depends=(vaultwarden)
makedepends=(
  git
  nodejs-lts
  npm
)
install=$pkgname.install
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('e3e8cb1294fa83ca86763d5e496d3ed3350b1ae9f843f4f6ab5339e1394c55f610e0312bf37d08e5f9478cc3630df5abd42b3b3227ed6164f8dfdbe676b2b38c')
b2sums=('60f54a8d5a79491cf19bf0daa83472c48d15606e6bb15657cad4976ec96e77cd3635de82582cad17eae6f61d145a7fc2b17274ec77f092db9549f1dcdc97379c')

prepare() {
  cd "$pkgname"

  export ELECTRON_SKIP_BINARY_DOWNLOAD=1

  npm ci
}

build() {
  cd "$pkgname/apps/web"

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    npm pkg set scripts.build:oss='cross-env NODE_OPTIONS="--max-old-space-size=1536" webpack'
  fi

  npm run dist:oss:selfhost

  # https://github.com/dani-garcia/bw_web_builds/blob/a84aca58170a60ac04a695851eeb6e70f577de09/scripts/build_web_vault.sh#L30-L33
  printf '{"version":"%s"}' "$pkgver" > build/vw-version.json
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/usr/share/webapps/$pkgname"

  cp -R ./apps/web/build/* "$pkgdir/usr/share/webapps/$pkgname"
}
