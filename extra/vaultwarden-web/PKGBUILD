# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
pkgver=2025.8.0.0
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=(any)
url=https://github.com/vaultwarden/vw_web_builds
license=(GPL-3.0-only)
depends=(vaultwarden)
makedepends=(
  git
  nodejs-lts
  npm
)
install=$pkgname.install
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('d0cc35cf3372ad1e0e72b6f83409941de6ea66d06707c2ed41719fa0a9800f1224d55c7d12c3a0779a75e9de5c5a0022c9df6902102448d0e48c5d6e1fe26119')
b2sums=('b853d89cb1bdfb3eef88dd114aa308bc6c98d5dfe4c54d45cc7451c97a6d091c0c2dd499fbb430e144d51f80bde2702f7195800a90fe297b0f915797b5ee50f1')

prepare() {
  cd "$pkgname"

  export ELECTRON_SKIP_BINARY_DOWNLOAD=1

  npm ci
}

build() {
  cd "$pkgname/apps/web"

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    npm pkg set scripts.build:oss='cross-env NODE_OPTIONS="--max-old-space-size=1536" webpack'
  fi

  npm run dist:oss:selfhost

  # https://github.com/dani-garcia/bw_web_builds/blob/a84aca58170a60ac04a695851eeb6e70f577de09/scripts/build_web_vault.sh#L30-L33
  printf '{"version":"%s"}' "$pkgver" > build/vw-version.json
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/usr/share/webapps/$pkgname"

  cp -R ./apps/web/build/* "$pkgdir/usr/share/webapps/$pkgname"
}
