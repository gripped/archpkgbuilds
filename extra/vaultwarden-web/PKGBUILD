# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2024.5.0
pkgver=2024.5.0
_upstreamver=${_pkgver%[[:lower:]]}
_patchver=$_upstreamver
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=('any')
url='https://github.com/dani-garcia/bw_web_builds'
license=('GPL3')
depends=('vaultwarden')
makedepends=('git' 'nodejs' 'npm')
install=$pkgname.install
source=(
  "bitwarden-clients::git+https://github.com/bitwarden/clients.git#tag=web-v$_upstreamver"
  "git+https://github.com/dani-garcia/bw_web_builds#tag=v$_pkgver"
)
b2sums=('d953f07996da35ee2d07bd6deb70c4ead0efd63f9316b7f66d20fd85779c723fbf77944b4e7146090844d30bde1f843442a298a63a9d8de575ac83f821b3f985'
        '67070dd5e93e3234c2c344fe416cdf13f92986bf4569be432820e30eb43347abc0d90ccea4f185ab19f88ad87c9b5b9b00b410cf19965bde36467f73b82ffe62')

# Appease vercmp by prepending trailing letter with a period
# shellcheck disable=SC2001
pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-clients

  # copy Vaultwarden images
  cp -vr "$srcdir/bw_web_builds/resources/src/images/"{logo-{dark,white}@2x,icon-white}.png apps/web/src/images

  # apply Vaultwarden patch
  patch --forward --strip=1 --input="$srcdir/bw_web_builds/patches/v$_patchver.patch"

  npm ci
}

build() {
  cd bitwarden-clients/apps/web

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    export NODE_OPTIONS="--max-old-space-size=1536"
  fi
  npm run dist:oss:selfhost
}

package() {
  install -d "$pkgdir/usr/share/webapps/$pkgname"

  cp -R bitwarden-clients/apps/web/build/* "$pkgdir/usr/share/webapps/$pkgname"
}

# vim:set ts=2 sw=2 et:
