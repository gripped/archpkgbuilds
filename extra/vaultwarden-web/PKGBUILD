# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
pkgver=2026.1.1p0
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=(any)
url=https://github.com/vaultwarden/vw_web_builds
license=(GPL-3.0-only)
depends=(vaultwarden)
makedepends=(
  git
  nodejs-lts
  npm
)
install=$pkgname.install
source=("$pkgname::git+$url#tag=v${pkgver//p/+}")
sha512sums=('0ace8299bf38dcb17a24634d99a7ca8eeee20017573d079defcb3d1671c1052921d5cc7cfcc803117999504b60579376221caf38c86f82fd43b2af3ddf156309')
b2sums=('29c6f5482d19594bb55a35c151ab8146bf50b63cd186f0c95b7e748e2d49637a3f3e84b37513d0344cf0e158f79b7d83ace99181b0da16ceec658d8091cb3f9d')

prepare() {
  cd "$pkgname"

  export ELECTRON_SKIP_BINARY_DOWNLOAD=1

  npm ci
}

build() {
  cd "$pkgname/apps/web"

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    npm pkg set scripts.build:oss='cross-env NODE_OPTIONS="--max-old-space-size=1536" webpack'
  fi

  npm run dist:oss:selfhost

  # https://github.com/dani-garcia/bw_web_builds/blob/a84aca58170a60ac04a695851eeb6e70f577de09/scripts/build_web_vault.sh#L30-L33
  printf '{"version":"%s"}' "$pkgver" > build/vw-version.json
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/usr/share/webapps/$pkgname"

  cp -R ./apps/web/build/* "$pkgdir/usr/share/webapps/$pkgname"
}
