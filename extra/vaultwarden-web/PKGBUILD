# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2024.5.1b
pkgver=2024.5.1.b
_upstreamver=${_pkgver%[[:lower:]]}
_patchver=2024.5.0
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=(any)
url=https://github.com/dani-garcia/bw_web_builds
license=(GPL-3.0-only)
depends=(vaultwarden)
makedepends=(
  git
  nodejs
  npm
)
install=$pkgname.install
source=(
  "bitwarden-clients::git+https://github.com/bitwarden/clients.git#tag=web-v$_upstreamver"
  "git+https://github.com/dani-garcia/bw_web_builds#tag=v$_pkgver"
)
b2sums=('4f76462beca9d6dac7102a7e67cd2a362d0b1269bc0ee68025b3a22ad2ef1010dfdc5f8642118014e0b286e53d02cefaa405bf8aaa3e84ea150ff5b4127c5adb'
        'e4d33e76e1309b30b78c2c01979c4a8d63caf1e59decf4993a9189628540c8b2998e23e08e1311dafca9b4f3ae102db83c127f8ac81894bd882ab86a9a5388e9')

# Appease vercmp by prepending trailing letter with a period
# shellcheck disable=SC2001
pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-clients

  # Copy Vaultwarden images
  cp -vr ../bw_web_builds/resources/src/images/{logo-{dark,white}@2x,icon-white}.png \
    apps/web/src/images

  # Apply Vaultwarden patch
  patch --forward --strip=1 --input=../bw_web_builds/patches/v$_patchver.patch

  npm ci
}

build() {
  cd bitwarden-clients/apps/web

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    export NODE_OPTIONS="--max-old-space-size=1536"
  fi
  npm run dist:oss:selfhost
}

package() {
  install -d "$pkgdir"/usr/share/webapps/$pkgname

  cp -R bitwarden-clients/apps/web/build/* "$pkgdir"/usr/share/webapps/$pkgname
}
