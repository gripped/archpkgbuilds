# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
pkgver=2025.12.1.0
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=(any)
url=https://github.com/vaultwarden/vw_web_builds
license=(GPL-3.0-only)
depends=(vaultwarden)
makedepends=(
  git
  nodejs-lts
  npm
)
install=$pkgname.install
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('17a3656188ccebc2cad5ed2b3cb28ce01d01ef9c95d3377965edf61553370f96edff0c688e96cc73f2d315f67dd2c8305344e013261ece1a9ec3dc4edba227d3')
b2sums=('e0c3fec906beea9c64c4118440e6889d5d717aa98f5a3991b14112ce3bd6c442b039836e2e8df07dd2f82650dc98252a2ba9219a40755b05cfc2013a8a9f61ec')

prepare() {
  cd "$pkgname"

  export ELECTRON_SKIP_BINARY_DOWNLOAD=1

  npm ci
}

build() {
  cd "$pkgname/apps/web"

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    npm pkg set scripts.build:oss='cross-env NODE_OPTIONS="--max-old-space-size=1536" webpack'
  fi

  npm run dist:oss:selfhost

  # https://github.com/dani-garcia/bw_web_builds/blob/a84aca58170a60ac04a695851eeb6e70f577de09/scripts/build_web_vault.sh#L30-L33
  printf '{"version":"%s"}' "$pkgver" > build/vw-version.json
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/usr/share/webapps/$pkgname"

  cp -R ./apps/web/build/* "$pkgdir/usr/share/webapps/$pkgname"
}
