# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-applications
pkgver=8.2.0
pkgrel=1
pkgdesc='Switchboard Applications Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-applications
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  flatpak
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=eaeb40817c76089d574658255d0fc75766862f27
source=(git+https://github.com/elementary/switchboard-plug-applications.git#tag=${_tag})
b2sums=('eacd60b45d383545be4aaa5986944a30a9cbc4b81aa3371bc6878dd8688c31e125339422687860be98accbc7dab8cc7a9a87a8d10b61161e9fdd04cd10d8f591')

pkgver() {
  cd switchboard-plug-applications
  git describe --tags
}

build() {
  arch-meson switchboard-plug-applications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
