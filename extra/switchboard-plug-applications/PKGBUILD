# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-applications
pkgver=8.1.0
pkgrel=1
pkgdesc='Switchboard Applications Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-applications
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  flatpak
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=718ebc652ec548645ac4f12e0705bd68fc90636b
source=(git+https://github.com/elementary/switchboard-plug-applications.git#tag=${_tag})
b2sums=('1601f0a58e432ccc429ef961f6d41ff89459f12d57498a15ce715ddf5c218dbc4adbf9721fe8ae2a536dc323887fe2c5e7183c8484386ec950801ad37eb56080')

pkgver() {
  cd switchboard-plug-applications
  git describe --tags
}

build() {
  arch-meson switchboard-plug-applications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
