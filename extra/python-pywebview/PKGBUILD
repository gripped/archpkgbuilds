# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: keutain < quentin dot aniere at gmail dot com>
# Contributor: Shae VanCleave

pkgname=python-pywebview
_name=${pkgname#python-}
pkgver=5.4
pkgrel=1
pkgdesc='Build GUI for your Python program with JavaScript, HTML, and CSS'
arch=(any)
url='https://github.com/r0x0r/pywebview'
license=(BSD-3-Clause)
depends=(python glib2 python-bottle python-cryptography python-proxy_tools python-typing_extensions)
makedepends=(python-build python-installer python-setuptools-scm python-wheel git)
optdepends=('pyside6: use with QtPy, PySide6'
            'python-gobject: use with GTK'
            'python-pygobject-stubs: use with GTK'
            'python-pyqt5: use with Qt'
            'python-pyqt5-webengine: use with Qt'
            'python-qtpy: use with Qt'
            'webkit2gtk-4.1: use with GTK')
source=("git+$url.git#tag=$pkgver")
b2sums=('b8dfc4b368aa6edaa35a7e5f4d50da2f88f372f2985bc9ec1ff28ee806fc0335457c910f4386c10c02a2dc3caf8d67cf421397d4288847cd22196384e4cd700d')

build() {
  cd $_name

  python -m build --wheel --no-isolation
}

package() {
  cd $_name
  
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
