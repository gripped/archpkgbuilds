# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: keutain < quentin dot aniere at gmail dot com>
# Contributor: Shae VanCleave

pkgname=python-pywebview
_name=${pkgname#python-}
pkgver=5.3.2
pkgrel=2
pkgdesc='Build GUI for your Python program with JavaScript, HTML, and CSS'
arch=(any)
url='https://github.com/r0x0r/pywebview'
license=(BSD-3-Clause)
depends=(python glib2 python-bottle python-cryptography python-proxy_tools python-typing_extensions)
makedepends=(python-build python-installer python-setuptools-scm python-wheel git)
optdepends=('pyside6: use with QtPy, PySide6'
            'python-gobject: use with GTK'
            'python-pygobject-stubs: use with GTK'
            'python-pyqt5: use with Qt'
            'python-pyqt5-webengine: use with Qt'
            'python-qtpy: use with Qt'
            'webkit2gtk-4.1: use with GTK')
source=("git+$url.git#tag=$pkgver")
b2sums=('f5468ce2d6969a293b79d5327aa82ac86da7859d9621d64d1eb71eda0a4eb81726cf57d3c7ca9ce6e5962db028d0c0d973319953cc1f8761d8d464eaea87ad03')

build() {
  cd $_name

  python -m build --wheel --no-isolation
}

package() {
  cd $_name
  
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
