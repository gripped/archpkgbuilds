# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: keutain < quentin dot aniere at gmail dot com>
# Contributor: Shae VanCleave

pkgname=python-pywebview
_name=${pkgname#python-}
pkgver=6.1
pkgrel=2
pkgdesc='Build GUI for your Python program with JavaScript, HTML, and CSS'
arch=(any)
url='https://github.com/r0x0r/pywebview'
license=(BSD-3-Clause)
depends=(python glib2 python-bottle python-cryptography python-proxy_tools python-typing_extensions)
makedepends=(python-build python-installer python-setuptools-scm python-wheel git)
optdepends=('pyside6: use with QtPy, PySide6'
            'python-gobject: use with GTK'
            'python-pygobject-stubs: use with GTK'
            'python-qtpy: use with Qt'
            'webkit2gtk-4.1: use with GTK')
source=("git+$url.git#tag=$pkgver")
b2sums=('b7c748edacbefb1111ce5a10f38a95644e255b12ad68df637466b82f433bd8b5158acc3d20a9fd64a945815e6963b8eca47e961ea03b2de6e741a1c0c427824c')

build() {
  cd $_name

  python -m build --wheel --no-isolation
}

package() {
  cd $_name
  
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
