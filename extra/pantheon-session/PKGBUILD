# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Helder Bertoldo <helder.bertoldo@gmail.com>

pkgname=pantheon-session
pkgver=8.0.1
pkgrel=1
pkgdesc='Session settings for Pantheon'
arch=(any)
url=https://github.com/elementary/session-settings
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gala
  gnome-keyring
  gnome-session
  gnome-settings-daemon
  pantheon-applications-menu
  onboard
  orca
  plank
  wingpanel
  xdg-user-dirs-gtk
)
makedepends=(
  git
  meson
)
optdepends=(pantheon-default-settings)
_tag=4977435ad247972ce2658443c9900a3e8c8a3baf
source=(pantheon-session-settings::git+https://github.com/elementary/session-settings.git#tag=${_tag})
b2sums=('88d49c5cf42efd4d0cf02c886e6038b0574b1fedde76e9e9bf22556221b15688ad509275fda0e6d4305413e378b630c9ccd62e10e768e490f547c824098302bc')

pkgver() {
  cd pantheon-session-settings
  git describe --tags
}

build() {
  arch-meson pantheon-session-settings build \
    -Dfallback-session=gnome \
    -Dwayland=true
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
