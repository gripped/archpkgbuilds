# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-pinyin-simp
pkgver=0.0.0.20241229
_commit=0c6861ef7420ee780270ca6d993d18d4101049d0
pkgrel=1
pkgdesc="Simple pinyin input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-pinyin-simp"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-pinyin-simp.git#commit=$_commit")
sha512sums=('8393b2c8936506f6ceda49eb89a56ed49bb41285bf72dd48eeeb0edded55c69f8bbe5a18eae645cb46f21000879da66b76d58c93131b462dbca6337b74a4bed7')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
