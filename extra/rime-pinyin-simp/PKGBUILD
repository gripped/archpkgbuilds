# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-pinyin-simp
pkgver=0.0.0.20240714
_commit=c0b1cc5553caed77e1338adc08d6dc048c50c813
pkgrel=1
pkgdesc="Simple pinyin input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-pinyin-simp"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-pinyin-simp.git#commit=$_commit")
sha512sums=('ddaba846bc3f9c3560b06a1778b152c6ac0cc79e11b1b39bbc82d4842951676307c4a752421b7816ec3c66d2e995073b877e41f32e325a2e3ac903fd5b745918')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
