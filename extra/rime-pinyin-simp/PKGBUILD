# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-pinyin-simp
pkgver=0.0.0.20240217
_commit=321630658d12beee8320af93cf6e4ca6f3042211
pkgrel=1
pkgdesc="Simple pinyin input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-pinyin-simp"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-pinyin-simp.git#commit=$_commit")
sha512sums=('dc7a2feb298115ca185de6ff029d8978042a3a66272663601fda7f9110d89be929f6c44abc54c22b3bd5aaa75162356d42b6df406d305b7ca24ea1d5cf20d229')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
