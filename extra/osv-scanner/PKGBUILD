# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=osv-scanner
pkgver=2.0.1
pkgrel=1
pkgdesc="Vulnerability scanner written in Go which uses the data provided by https://osv.dev"
url='https://github.com/google/osv-scanner'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=(https://github.com/google/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('66bba28548a0e841407ef667c2ab13f5219edda3f4d2c9d0054d07b41e9008bf')
b2sums=('6bb1b520900d96cf882e42a0de4e6a8ca57720f2c19738e1f9bdb16bffb559cf60d105e6a5bd21c69de5b37cffa145b00071b46bd0719cabdf08c52390fef32d')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    ./cmd/osv-scanner
}

check() {
  cd ${pkgname}-${pkgver}
  ./osv-scanner --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    osv-scanner
}

# vim: ts=2 sw=2 et:
