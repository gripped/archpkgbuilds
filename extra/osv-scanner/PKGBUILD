# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=osv-scanner
pkgver=1.7.4
pkgrel=1
pkgdesc="Vulnerability scanner written in Go which uses the data provided by https://osv.dev"
url='https://github.com/google/osv-scanner'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
source=(https://github.com/google/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('cf1daa47cdab503149d25d03287543866c53b4ab04b3e02ca0e6fc47caf1f696')
b2sums=('ff7913e4b5a3f131af441fd8e2f2988e724943496cb00209ac64672c6a8d43aa615cf8126369e4087b65a0cea7d54fee1826c21576af00be0992c58beaa3e550')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    ./cmd/osv-scanner
}

check() {
  cd ${pkgname}-${pkgver}
  ./osv-scanner --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    osv-scanner
}

# vim: ts=2 sw=2 et:
