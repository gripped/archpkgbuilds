# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Conntributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-mime-tools
pkgver=5.517
pkgrel=1
pkgdesc="Parses streams to create MIME entities"
arch=('any')
url="https://metacpan.org/pod/MIME::Tools"
license=('GPL-1.0-or-later' 'Artistic-1.0-Perl')
depends=('perl-mailtools' 'perl-io-stringy' 'perl-convert-binhex')
checkdepends=('perl-test-deep')
options=('!emptydirs')
provides=('perl-mime-body=5.428.0' 'perl-mime-decoder' 'perl-mime-decoder-base64'
	'perl-mime-decoder-binary' 'perl-mime-decoder-binhex' 'perl-mime-decoder-gzip64'
	'perl-mime-decoder-nbit' 'perl-mime-decoder-quotedprint' 'perl-mime-decoder-uu'
	'perl-mime-entity' 'perl-mime-field-contdisp=5.428.0' 'perl-mime-field-contraenc=5.428.0'
	'perl-mime-field-conttype=5.428.0' 'perl-mime-field-paramval=5.428.0' 'perl-mime-head=5.428.0'
	'perl-mime-parser' 'perl-mime-parser-filer' 'perl-mime-parser-reader'
	'perl-mime-parser-results' 'perl-mime-worddecoder' 'perl-mime-words=5.428.0')
source=("https://www.cpan.org/authors/id/D/DS/DSKOLL/MIME-tools-${pkgver}.tar.gz")
sha512sums=('e7935a1f4219b5de87f5752cf87978083ffe55ebd6a929f8f9fed63595553059fed6fe98ee18f151b0a250a57d8f239ba8ebd5d34b5e5d407b30ed9a5de95547')

build() {
  cd  "${srcdir}/MIME-tools-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "${srcdir}/MIME-tools-${pkgver}"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd  "${srcdir}/MIME-tools-${pkgver}"

  make install DESTDIR="${pkgdir}"
}
