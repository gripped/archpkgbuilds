# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-marshmallow-sqlalchemy
pkgver=1.4.1
pkgrel=1
pkgdesc='SQLAlchemy integration with marshmallow'
url=https://marshmallow-sqlalchemy.readthedocs.io/
arch=(any)
license=(MIT)
depends=(
  python-marshmallow
  python-sqlalchemy
)
makedepends=(
  git
  python-build
  python-flit-core
  python-installer
)
_tag=a90da5c56ed0812fab3cbce40921b025d487f781
source=(git+https://github.com/marshmallow-code/marshmallow-sqlalchemy.git#tag=${_tag})
sha256sums=('b078357cfd7bc64c5c2275983d62ad3120451746e857ec58aed7d2d0967b56ec')

pkgver() {
  cd marshmallow-sqlalchemy
  git describe --tags
}

build() {
  cd marshmallow-sqlalchemy
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" marshmallow-sqlalchemy/dist/*.whl
  install -Dm 644 marshmallow-sqlalchemy/LICENSE -t "${pkgdir}"/usr/share/licenses/python-marshmallow-sqlalchemy/
}

# vim: ts=2 sw=2 et:
