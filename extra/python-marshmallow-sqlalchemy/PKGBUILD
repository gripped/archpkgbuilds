# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-marshmallow-sqlalchemy
pkgver=1.4.2.post0
pkgrel=2
pkgdesc='SQLAlchemy integration with marshmallow'
url=https://marshmallow-sqlalchemy.readthedocs.io/
arch=(any)
license=(MIT)
depends=(
  python-marshmallow
  python-sqlalchemy
)
makedepends=(
  git
  python-build
  python-flit-core
  python-installer
)
_tag=8bb47a67fb7d108f23e1b9f483ecac2a61e878b9
source=(git+https://github.com/marshmallow-code/marshmallow-sqlalchemy.git#tag=${_tag})
sha256sums=('bbd612bf736b7ce85f362cbb99b385546ee9e4da9a49d6758528f8d000bccbc7')

pkgver() {
  cd marshmallow-sqlalchemy
  git describe --tags
}

build() {
  cd marshmallow-sqlalchemy
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" marshmallow-sqlalchemy/dist/*.whl
  install -Dm 644 marshmallow-sqlalchemy/LICENSE -t "${pkgdir}"/usr/share/licenses/python-marshmallow-sqlalchemy/
}

# vim: ts=2 sw=2 et:
