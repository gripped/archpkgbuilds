# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-marshmallow-sqlalchemy
pkgver=1.4.0
pkgrel=1
pkgdesc='SQLAlchemy integration with marshmallow'
url=https://marshmallow-sqlalchemy.readthedocs.io/
arch=(any)
license=(MIT)
depends=(
  python-marshmallow
  python-sqlalchemy
)
makedepends=(
  git
  python-build
  python-flit-core
  python-installer
)
_tag=263197855ccf6e0e6fb6f04b8011d0b7e22cb12f
source=(git+https://github.com/marshmallow-code/marshmallow-sqlalchemy.git#tag=${_tag})
sha256sums=('625e1991dd23cfa6164069f56aa07034ac166a968af0268f33db917c202966fb')

pkgver() {
  cd marshmallow-sqlalchemy
  git describe --tags
}

build() {
  cd marshmallow-sqlalchemy
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" marshmallow-sqlalchemy/dist/*.whl
  install -Dm 644 marshmallow-sqlalchemy/LICENSE -t "${pkgdir}"/usr/share/licenses/python-marshmallow-sqlalchemy/
}

# vim: ts=2 sw=2 et:
