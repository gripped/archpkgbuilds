# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Clement Tsang (xoronth) <cjhtsang@uwaterloo.ca>

pkgname=bottom
pkgver=0.11.3
pkgrel=1
pkgdesc='A graphical process/system monitor'
arch=(x86_64)
url='https://github.com/ClementTsang/bottom'
license=(MIT)
depends=(gcc-libs)
makedepends=(git rust)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('8524fd485fa3aeb556df1ac15e7ed3f7cced09849442407ba9c61bec0af4bef19c463355172fa182cd67726e99d770dea6bb45d1593ee331d2427f4a39e29a66')
b2sums=('04428fd7a194e5f16fd62a5e010990ec291507d16183a303e3a86b647b3dfab7c6396a85ad3b16172a44c3f443fb1b14e5fd5d3f0a57671bc05841489da85d38')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  BTM_GENERATE=true cargo build --locked --release --features deploy
}

check() {
  cd "$pkgname"

  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --locked --features deploy
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs sample_configs "$pkgdir/usr/share/doc/$pkgname"
  rm -f "$pkgdir/usr/share/doc/$pkgname/docs/.gitignore"

  cd target/tmp/bottom

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" manpage/btm.1

  # completions
  install -Dm644 completion/_btm "$pkgdir/usr/share/zsh/site-functions/_btm"
  install -Dm644 completion/btm.bash "$pkgdir/usr/share/bash-completion/completions/btm"
  install -Dm644 completion/btm.fish "$pkgdir/usr/share/fish/vendor_completions.d/btm.fish"
}

# vim:set ts=2 sw=2 et:
