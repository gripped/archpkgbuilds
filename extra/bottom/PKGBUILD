# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Clement Tsang (xoronth) <cjhtsang@uwaterloo.ca>

pkgname=bottom
pkgver=0.11.2
pkgrel=1
pkgdesc='A graphical process/system monitor'
arch=(x86_64)
url='https://github.com/ClementTsang/bottom'
license=(MIT)
depends=(gcc-libs)
makedepends=(git rust)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('aba4216b1661eee8bc409301f46779162497b96060847353ed344b27ae0086943416b37917046fa7e5aa8b3c389a8ee6683c9fcb859108ded3b05fe2693d0018')
b2sums=('d79bc71306f9da6d5f60d48e78df09680b444721f12911fb5f4db0463b6857be06cad365d325542b9e5ca5c5a48dcbfea3780b27797f4eaccb70284094956173')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  BTM_GENERATE=true cargo build --locked --release --features deploy
}

check() {
  cd "$pkgname"

  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --locked --features deploy
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs sample_configs "$pkgdir/usr/share/doc/$pkgname"
  rm -f "$pkgdir/usr/share/doc/$pkgname/docs/.gitignore"

  cd target/tmp/bottom

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" manpage/btm.1

  # completions
  install -Dm644 completion/_btm "$pkgdir/usr/share/zsh/site-functions/_btm"
  install -Dm644 completion/btm.bash "$pkgdir/usr/share/bash-completion/completions/btm"
  install -Dm644 completion/btm.fish "$pkgdir/usr/share/fish/vendor_completions.d/btm.fish"
}

# vim:set ts=2 sw=2 et:
