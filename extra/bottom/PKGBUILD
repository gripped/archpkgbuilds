# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Clement Tsang (xoronth) <cjhtsang@uwaterloo.ca>

pkgname=bottom
pkgver=0.11.0
pkgrel=1
pkgdesc='A graphical process/system monitor'
arch=(x86_64)
url='https://github.com/ClementTsang/bottom'
license=(MIT)
depends=(gcc-libs)
makedepends=(git rust)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('ca3eb620c928012f9cf151d62ca478ab9a09c5ce85d5063321d721f3e9d05d6089baa3616a5ccfa219f7c2bcd42673288236d1d4105d4680f901ab16f6e7f003')
b2sums=('add397043edfe9d8e44f6a47765d608752d9389a946573bda29639268ca670ce684d37aeadbed60062fecdd3df19ecc8f2acb20be8208b3bebaca2f51793fd41')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  BTM_GENERATE=true cargo build --locked --release --features deploy
}

check() {
  cd "$pkgname"

  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --locked --features deploy
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs sample_configs "$pkgdir/usr/share/doc/$pkgname"
  rm -f "$pkgdir/usr/share/doc/$pkgname/docs/.gitignore"

  cd target/tmp/bottom

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" manpage/btm.1

  # completions
  install -Dm644 completion/_btm "$pkgdir/usr/share/zsh/site-functions/_btm"
  install -Dm644 completion/btm.bash "$pkgdir/usr/share/bash-completion/completions/btm"
  install -Dm644 completion/btm.fish "$pkgdir/usr/share/fish/vendor_completions.d/btm.fish"
}

# vim:set ts=2 sw=2 et:
