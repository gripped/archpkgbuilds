# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Clement Tsang (xoronth) <cjhtsang@uwaterloo.ca>

pkgname=bottom
pkgver=0.10.0
pkgrel=1
pkgdesc='A graphical process/system monitor'
arch=('x86_64')
url='https://github.com/ClementTsang/bottom'
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('76d7c0ed0c423b589606a6d8b6c6f6a7adf4a90f0bf63b36e18d85efab9b066b31bb771b866f8b6dfc57d907e1bcfee4d05c10157081f5c305135d01bede708a')
b2sums=('dcd70d4ea1e44f34f8c8b31861b5a5aedd6b8dfecad12e18259e7312a72cf754059434504ff91412ed827e31160ad6c4e1ffcd1618c1ad6e0f5ab08894fde076')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  BTM_GENERATE=true cargo build --frozen --release --features deploy
}

check() {
  cd "$pkgname-$pkgver"
  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --frozen --features deploy
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs sample_configs "$pkgdir/usr/share/doc/$pkgname"
  rm -f "$pkgdir/usr/share/doc/$pkgname/docs/.gitignore"

  cd target/tmp/bottom

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" manpage/btm.1

  # completions
  install -Dm644 completion/_btm "$pkgdir/usr/share/zsh/site-functions/_btm"
  install -Dm644 completion/btm.bash "$pkgdir/usr/share/bash-completion/completions/btm"
  install -Dm644 completion/btm.fish "$pkgdir/usr/share/fish/vendor_completions.d/btm.fish"
}

# vim:set ts=2 sw=2 et:
