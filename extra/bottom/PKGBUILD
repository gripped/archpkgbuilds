# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Clement Tsang (xoronth) <cjhtsang@uwaterloo.ca>

pkgname=bottom
pkgver=0.12.1
pkgrel=1
pkgdesc='A graphical process/system monitor'
arch=(x86_64)
url='https://github.com/ClementTsang/bottom'
license=(MIT)
depends=(gcc-libs)
makedepends=(git rust)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('121b169835ff626e88643f1391dff18acf6a2777359ad8d6dcf851f504a6e321fccd469c579939230284a5fbc0cc630e043c7825721a9339bf25b57708080b76')
b2sums=('f25f66067381c54fed2d6c298d020cde09e6a2913ee8bc285f720059b602061aeeecd88f18ec5d79a3adae550f3c90f935d1eb6c71e1a1bfb454db7abae03f2d')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  BTM_GENERATE=true cargo build --locked --release --features deploy
}

check() {
  cd "$pkgname"

  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --locked --features deploy
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs sample_configs "$pkgdir/usr/share/doc/$pkgname"
  rm -f "$pkgdir/usr/share/doc/$pkgname/docs/.gitignore"

  cd target/tmp/bottom

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" manpage/btm.1

  # completions
  install -Dm644 completion/_btm "$pkgdir/usr/share/zsh/site-functions/_btm"
  install -Dm644 completion/btm.bash "$pkgdir/usr/share/bash-completion/completions/btm"
  install -Dm644 completion/btm.fish "$pkgdir/usr/share/fish/vendor_completions.d/btm.fish"
}

# vim:set ts=2 sw=2 et:
