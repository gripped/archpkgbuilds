# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
pkgname=hsa-amd-aqlprofile
pkgver=7.2.0
pkgrel=1
pkgdesc='Architected Queuing Language Profiling Library'
arch=('x86_64')
url='https://github.com/ROCm/rocprofiler-register'
license=('MIT')
depends=('rocm-core' 'glibc' 'gcc-libs' 'hsa-rocr')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ROCm/rocm-systems/releases/download/rocm-$pkgver/aqlprofile.tar.gz")
sha256sums=('28841d6ae6bf67df467ddd95e21a4359bea580e798b61c2024da246887b534e6')
replaces=(hsa-amd-aqlprofile-bin)

build() {
  local cmake_args=(
    -Wno-dev
    -S aqlprofile
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
  )
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 aqlprofile/LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
