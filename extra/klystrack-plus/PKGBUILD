# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=klystrack-plus
pkgver=0.10.0.alpha4
pkgrel=1
pkgdesc='A tracker for making C64/NES/Amiga-style chiptunes on a modern platform'
arch=('x86_64')
url='https://github.com/LTVA1/klystrack'
license=('MIT')
depends=(
  'glibc'
  'alsa-lib'
  'sdl2'
  'sdl2_image'
  'hicolor-icon-theme'
  'zlib'
)
makedepends=('git')
options=('!lto')
source=(
  "$pkgname::git+$url#commit=01a2f7a95cb03cd927dd662d92cd37929f427055" # change to tag on next release
  'github.com-LTVA1-klystron::git+https://github.com/LTVA1/klystron'
  'update-desktop-file.patch'
  'repro-klystrack-plus.patch'
  'repro-klystron.patch'
)
sha512sums=('75f501ef34a1e9e0787b19a439925a17a85b164c3e2414bd4c26cd56090d9e55d6abbd20654368d45e6a385e15a43ba6f8712900ae043da5c137ddd0771a45fb'
            'SKIP'
            '92a125c4ac2589ac9f0b2dd3139ee6b451a49dc181a67e29da7a12050c7948aec1145dc830d8ef87fb58474b27d986707ee81524ca93b970aca759ce8bab09f5'
            'bf74826db72571fe4b02a915c9e4ee4c19d096fa7b044149187ce6a42bc90856389f7837651ff822d75b3c686606e7f0628224729518b1b09db79118ed416f14'
            'ac4f493d81f560c79a412d9e2c732839dbbf3bc188d11e62465bcfa55f7c103e9f3200e8f746ce658580a7bc61d95cdf545b8b4e47fa1e25ad8a961a8c1b6119')
b2sums=('8cc2dfb211ae0efb41c0f1882890a10e8b4656a96407259f5ed8d76e0e66d9f165d5e62840937b8b69c4684a8a92fd358887fca2a3fcd0eb13c58c0e9206834e'
        'SKIP'
        '5543cae63098a27ee6539cf92f06a87756bbfcbad0e6000eed8c5ad32fb354f18439730d021a278ffd8d4359b8c6847d8c0ae06a882faf1887b8a4c1c912ec50'
        '761ef03457b9ea2009b8483608a99befe3444641ef5f351ab3d3d33271bedca1b32a2cec8c9d6fbec0a6d871134915cd5c11111c48b5015019021b61388e0a2c'
        'c22be63b5103b9c9c21366df6d0a76b59d3f39054204a56845fe105de54240fc0f269db48330fb52269e9b32bef147bd529c39d1f26a088386c70f5d702168ff')


prepare() {
  cd "$pkgname"

  # prepare git submodules
  git submodule init
  git config submodule.klystron.url "$srcdir/github.com-LTVA1-klystron"
  git -c protocol.file.allow=always submodule update

  # update desktop file to point to klystrack-plus,
  # just in case users have the original klystrack installed
  patch --strip 1 --input "$srcdir/update-desktop-file.patch"

  # first pass at ensuring reproducibility by using 
  # git commit date instead of date
  patch --strip 1 --input "$srcdir/repro-klystrack-plus.patch"
  patch --directory klystron --strip 1 --input "$srcdir/repro-klystron.patch"
}

build() {
  cd "$pkgname"

  make CFG=release RES_PATH=/usr/lib/klystrack-plus
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" CFG=release RES_PATH=/usr/lib/klystrack-plus install

  # rename binary
  mv -v "$pkgdir/usr/bin/"klystrack{,-plus}

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # additional data
  cp -vr examples "$pkgdir/usr/share/$pkgname"

  # man page
  install -vDm644 doc/klystrack.1 "$pkgdir/usr/share/man/man1/$pkgname.1"

  # desktop file
  install -vDm644 linux/klystrack.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  # application icon
  install -vDm644 icon/256x256.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/$pkgname.png"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
