# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=ruby-pdf-inspector
_pkgname=${pkgname#ruby-}
pkgver=1.3.0
_commit=1f6e59cb7d56b956783d2cbb0b67db8cd2a4d006
pkgrel=6
pkgdesc="Collection of PDF::Reader based analysis classes for inspecting PDF output"
arch=(any)
url="https://github.com/prawnpdf/pdf-inspector"
license=('GPL-2.0-only OR GPL-3.0-only OR Ruby')
depends=(
  ruby
  ruby-pdf-reader
)
makedepends=(rubygems)
checkdepends=(ruby-rspec)
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_commit.tar.gz")
sha256sums=('bb86a6a6ee801ca41b5b44ca2a0cba3fac9a5d89e84772e36ca691c220cfba43')

prepare() {
  cd "$_pkgname-$_commit"
  sed -i -E 's|~>|>=|g' "$_pkgname.gemspec"
  # Upstream looks for signing key in a wierd place: .ssh/gem-private_key.pem
  sed --in-place '/spec.signing_key/d' "$_pkgname.gemspec"
}

build() {
  cd "$_pkgname-$_commit"
  local gemdir="$(gem env gemdir)"

  gem build "$_pkgname.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "$_pkgname-$pkgver.gem"

  rm --force --recursive --verbose \
    "tmp_install/$gemdir/cache/" \
    "tmp_install/$gemdir/gems/$_pkgname-$pkgver/vendor/" \
    "tmp_install/$gemdir/doc/$_pkgname-$pkgver/ri/ext/"

  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
    -iname "*.o" -o \
    -iname "*.c" -o \
    -iname "*.so" -o \
    -iname "*.time" -o \
    -iname "gem.build_complete" -o \
    -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/$gemdir/extensions/" \
    -type f \
    \( \
    -iname "mkmf.log" -o \
    -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "$_pkgname-$_commit"
  GEM_HOME="tmp_install/$(gem env gemdir)" rspec
}

package() {
  cd "$_pkgname-$_commit"
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
