# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=qflipper
pkgver=1.3.3
pkgrel=4
pkgdesc='Desktop application for updating Flipper Zero firmware via PC'
arch=(x86_64)
url='https://github.com/flipperdevices/qFlipper'
license=(GPL-3.0-only)
depends=(
  glibc
  hicolor-icon-theme
  libgcc
  libstdc++
  libusb
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-serialport
  zlib
)
makedepends=(
  git
  qt6-svg
  qt6-tools
)
source=($pkgname::git+$url.git#tag=$pkgver
        git+https://github.com/pbatard/libwdi.git
        git+https://github.com/nanopb/nanopb.git)
sha512sums=('d779c2071d5eb64058cafaa09e3163de5b57473b35e46bf30f645fcf0e74a8e217da3c4d6aedb182189ba84d038e4492d3a8f4fe13e5055ab40afd8080233159'
            'SKIP'
            'SKIP')
b2sums=('ce0f5059545ed05ff13c7556e4cde4fdd10e3a8a8ae46661a6cf8aa43cd286c46d4cf406eafbccdd39d6988d43ba8e15a0f6390f7efa5c47c3971cb5147788e3'
        'SKIP'
        'SKIP')

prepare() {
  mkdir build
  cd $pkgname
  git submodule init
  git config submodule.driver-tool/libwdi.url "$srcdir/libwdi"
  git config submodule.3rdparty/nanopb.url "$srcdir/nanopb"
  git -c protocol.file.allow=always submodule update

  # https://github.com/flipperdevices/qFlipper/pull/233
  git cherry-pick -n 57316893037d119931011d9abd4a05ed158c7a24

  # Use uucp group instead of dialout for udev rules
  sed -i 's/dialout/uucp/g' installer-assets/udev/42-flipperzero.rules
}

build() {
  local _qmake_options=(
    ../$pkgname/qFlipper.pro
    -spec linux-g++
    CONFIG+=qtquickcompiler
    DEFINES+=DISABLE_APPLICATION_UPDATES
    PREFIX=/usr
  )

  cd build
  qmake6 "${_qmake_options[@]}"
  make qmake_all
  make
}

package() {
  make -C build INSTALL_ROOT="$pkgdir" install
}
