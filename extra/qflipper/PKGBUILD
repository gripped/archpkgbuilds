# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=qflipper
pkgver=1.3.3
pkgrel=3
pkgdesc="Desktop application for updating Flipper Zero firmware via PC"
arch=(x86_64)
url="https://github.com/flipperdevices/qFlipper"
license=(GPL-3.0-only)
depends=(
  gcc-libs
  libusb
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-serialport
  zlib
)
makedepends=(
  git
  qt6-svg
  qt6-tools
)
source=($pkgname::git+$url.git#tag=$pkgver
        git+https://github.com/pbatard/libwdi.git
        git+https://github.com/nanopb/nanopb.git)
sha256sums=('a156996738211416e7631ac58e0252415c86f4d7c3096c839f1fe551403f82b1'
            'SKIP'
            'SKIP')

prepare() {
  mkdir build
  cd $pkgname
  git submodule init
  git config submodule.driver-tool/libwdi.url "$srcdir/libwdi"
  git config submodule.3rdparty/nanopb.url "$srcdir/nanopb"
  git -c protocol.file.allow=always submodule update

  # https://github.com/flipperdevices/qFlipper/pull/233
  git cherry-pick -n 57316893037d119931011d9abd4a05ed158c7a24

  # Use uucp group instead of dialout for udev rules
  sed -i 's/dialout/uucp/g' installer-assets/udev/42-flipperzero.rules
}

build() {
  local _qmake_options=(
    ../$pkgname/qFlipper.pro
    -spec linux-g++
    CONFIG+=qtquickcompiler
    DEFINES+=DISABLE_APPLICATION_UPDATES
    PREFIX=/usr
  )

  cd build
  qmake6 "${_qmake_options[@]}"
  make qmake_all
  make
}

package() {
  make -C build INSTALL_ROOT="$pkgdir" install
}
