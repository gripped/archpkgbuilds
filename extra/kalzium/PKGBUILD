# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalzium
pkgver=23.08.2
pkgrel=2
pkgdesc='Periodic Table of Elements'
url='https://apps.kde.org/kalzium/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kparts5 knewstuff5 kplotting5 kunitconversion5 qt5-script avogadrolibs-qt5 openbabel hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools5 eigen ocaml facile python libxcursor openmpi fmt)
groups=(kde-applications kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        avogadro-1.98.patch)
sha256sums=('9074ea7e74f0bd117565964c86de150b3b8fbf86fc0efb2cb44e63f5baa05f88'
            'SKIP'
            '0b226d42d05b563be351b3d016aa23a55e14222bfe89e97532b1d6d705290fab')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < avogadro-1.98.patch # Fix build with avogadro 1.98
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
