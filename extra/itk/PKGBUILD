# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Butui Hu <hot123tea123@gmail.com>

pkgbase=itk
pkgname=(itk python-itk)
pkgver=5.4.3
pkgrel=2
pkgdesc="An open-source toolkit for multidimensional image analysis"
arch=(x86_64)
url="https://www.itk.org"
license=(Apache-2.0)
makedepends=(
  castxml
  clang
  cmake
  eigen
  git   # used for fetching remote modules
  lld
  ninja
  python
  swig
  # fftw is needed only for detection by cmake, ITK links to static libs from MKL
  fftw
  intel-oneapi-mkl
  # depends
  dcmtk
  double-conversion
  expat
  gdcm
  hdf5
  libjpeg-turbo
  libpng
  libtiff
  zlib
)
source=(
  ${pkgbase^^}-$pkgver.tar.gz::https://github.com/InsightSoftwareConsortium/ITK/archive/refs/tags/v$pkgver.tar.gz
  fix-cstdint.patch
)
b2sums=('a360bac255bda6a51930355e1b8e9b9aeca0f6a7404fd43986dbe2d4491d3558b17af89f5961e78754946541900489d5f4346075d7eaf9650f11a1a8be05ea67'
        '5c1413b2c0b1c586d930b4803b0dbff881948eb980fad8940d8fe46a0f69177ee42b001d07a988f92227a4e8da8b2c7b4f33b08e50676f191cc5c4b583964fa6')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  cd ${pkgbase^^}-$pkgver
  # https://github.com/InsightSoftwareConsortium/ITK/pull/5387
  patch -p1 -i ../fix-cstdint.patch
}

build() {
  local cmake_options=(
    -B build
    -S ${pkgbase^^}-$pkgver
    -G Ninja
    -W no-dev
    -DBUILD_SHARED_LIBS=ON
    -DBUILD_TESTING=OFF
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_CXX_COMPILER=clang++
    -DCMAKE_C_COMPILER=clang
    -DCMAKE_LINKER_TYPE=LLD
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_SKIP_INSTALL_RPATH=ON
    -DCMAKE_SKIP_RPATH=ON
    -DITK_BUILD_DEFAULT_MODULES=ON
    -DITK_USE_GPU=OFF
    -DITK_USE_MKL=ON
    -DITK_USE_SYSTEM_CASTXML=ON
    -DITK_USE_SYSTEM_DCMTK=ON
    -DITK_USE_SYSTEM_DOUBLECONVERSION=ON
    -DITK_USE_SYSTEM_EIGEN=ON
    -DITK_USE_SYSTEM_EXPAT=ON
    -DITK_USE_SYSTEM_FFTW=ON
    -DITK_USE_SYSTEM_GDCM=ON
    -DITK_USE_SYSTEM_GOOGLETEST=OFF
    -DITK_USE_SYSTEM_HDF5=ON
    -DITK_USE_SYSTEM_JPEG=ON
    -DITK_USE_SYSTEM_MINC=OFF
    -DITK_USE_SYSTEM_PNG=ON
    -DITK_USE_SYSTEM_SWIG=ON
    -DITK_USE_SYSTEM_TIFF=ON
    -DITK_USE_SYSTEM_VXL=OFF  # not packaged
    -DITK_USE_SYSTEM_ZLIB=ON
    -DITK_WRAP_IMAGE_DIMS="2;3;4"
    -DITK_WRAP_PYTHON=ON
    -DITK_WRAP_complex_double=ON
    -DITK_WRAP_complex_float=ON
    -DITK_WRAP_covariant_vector_double=ON
    -DITK_WRAP_covariant_vector_float=ON
    -DITK_WRAP_double=ON
    -DITK_WRAP_float=ON
    -DITK_WRAP_rgb_unsigned_char=ON
    -DITK_WRAP_rgb_unsigned_short=ON
    -DITK_WRAP_rgb_unsigned_short=ON
    -DITK_WRAP_rgba_unsigned_char=ON
    -DITK_WRAP_rgba_unsigned_short=ON
    -DITK_WRAP_rgba_unsigned_short=ON
    -DITK_WRAP_signed_char=ON
    -DITK_WRAP_signed_long_long=ON
    -DITK_WRAP_signed_short=ON
    -DITK_WRAP_unsigned_char=ON
    -DITK_WRAP_unsigned_long_long=ON
    -DITK_WRAP_unsigned_short=ON
    -DITK_WRAP_vector_double=ON
    -DModule_GenericLabelInterpolator=ON
    -DModule_ITKIODCMTK=ON
    -DModule_ITKIOMINC=ON
    -DModule_ITKIOTransformMINC=ON
    -DModule_ITKReview=ON
    -DModule_MorphologicalContourInterpolation=ON
    -DModule_SimpleITKFilters=ON
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package_itk() {
  depends=(
    dcmtk
    double-conversion
    expat libexpat.so
    gcc-libs
    gdcm libgdcmCommon.so libgdcmDICT.so libgdcmDSED.so libgdcmMSFF.so
    glibc
    hdf5
    libjpeg-turbo libjpeg.so
    libpng libpng16.so
    libtiff libtiff.so
    zlib libz.so
  )
  provides=(insight-toolkit)
  replaces=(insight-toolkit)

  DESTDIR="$pkgdir" cmake --install build
  (
    cd "$pkgdir"
    site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
    _pick python-$pkgbase "${site_packages#/}"
  )
}

package_python-itk() {
  pkgdesc+=" - Python bindings"
  depends=(
    itk
    python
    python-numpy
    python-xarray
  )

  mv -v $pkgname/* "$pkgdir"
  python -O -m compileall "$pkgdir"/usr/lib
}
