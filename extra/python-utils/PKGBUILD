# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.6.1
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-loguru
  python-pytest-runner
)
optdepends=('python-loguru: for logging support')
source=(
  $url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc}
)
sha512sums=('f7c6b3c5951c2902e94f3c17791178813695bcc15828ed52db6e071f9e08528a8202be1cff29bab2ce534e822f358cf41b8082fbf40db16e657b0b1c1dfa71a8'
            'SKIP')
b2sums=('c4e4f640c6e402b3c308b6a7b3ac4cfd0f4ff8c6fe7553a87d263f697ced0da0b63aa6d8d7ff6a02af1974e32c644e214ddefa6bd0a5f272f37d65017d4fb009'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -vv -c /dev/null
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
