# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.9.0
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD-3-Clause)
depends=(
  python
  python-typing_extensions
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-loguru
  python-pytest
)
optdepends=('python-loguru: for logging support')
source=(
  git+$url.git?signed#tag=v$pkgver
)
sha512sums=('546bfa5c5a69017a7db090a010ff74b5aba2f148d3dfba23e6944c822893a014d577e8162cd74a1744fdbbca6498c0e5a0d19edeb005af5d5ae9c0e340e42188')
b2sums=('161fbbe2a15fb4c807f249a72632576b1ea238fb6d6498cc21fb772b392ce9539a36e524963c28bde98cc0b0b34e57397507ae66c188b779b15a256737987b1d')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  pytest -vv -c /dev/null
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
