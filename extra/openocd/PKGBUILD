# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Matthias Bauch <matthias.bauch@gmail.com>
# Contributor: Laszlo Papp <djszapi2 at gmail com>
# Contributor: Samuel Tardieu <sam@rfc1149.net>


pkgname=openocd
epoch=1
pkgver=0.12.0
pkgrel=4
pkgdesc='Debugging, in-system programming and boundary-scan testing for embedded target devices'
arch=(x86_64)
url='https://openocd.org'
license=(GPL-2.0-or-later)
depends=(
  glibc
  libftdi-compat
  libusb
  hidapi
  capstone
  jimtcl
)
makedepends=(
  git
  libjaylink
  libudev.so
)
source=(
  "$pkgname::git+https://github.com/openocd-org/openocd#tag=v$pkgver"
  'github.com-msteveb-jimtcl::git+https://github.com/msteveb/jimtcl.git'
  'gitlab.zapb.de-libjaylink-libjaylink::git+https://gitlab.zapb.de/libjaylink/libjaylink.git'
  'git.savannah.nongnu.org-git-git2cl::git+https://git.savannah.nongnu.org/git/git2cl.git'
)
sha512sums=('77585a809c9eaf055b5b096c75f6d6e14461c0b4cbf0d2285dd3e878b7e31299bbbd5535f402da57599f4afe614e232a6a561ad98e467bfcca33dddfedf9b2b7'
            'SKIP'
            'SKIP'
            'SKIP')
b2sums=('c9f00dc50a3b20eca4f10c8dd1e1a4c28963afe5942e7f8d8fa7c43f0baf037241f1d7886557536df1a741065e046b81cdfbcb8701665df0651d7952de40547b'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"

  # modify udev rules
  sed -i 's|GROUP="plugdev", ||g' contrib/60-openocd.rules

  # revert workarounds for jimtcl expr syntax changes
  git cherry-pick --no-commit 95603fae18f81eebdafc5b318e70f9e2cdefab9e

  # add missing includes (stdio.h)
  git cherry-pick --no-commit 73390332d203f02aa5b9798a7550191d55650d97

  # setup submodules
  git submodule init
  git config submodule.jimtcl.url "$srcdir/github.com-msteveb-jimtcl"
  git config submodule.src/jtag/drivers/libjaylink.url "$srcdir/gitlab.zapb.de-libjaylink-libjaylink"
  git config submodule.tools/git2cl.url "$srcdir/git.savannah.nongnu.org-git-git2cl"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$pkgname"


  local _features=(
    aice
    amtjtagaccel
    armjtagew
    at91rm9200
    buspirate
    capstone
    cmsis-dap
    dummy
    ep93xx
    ftdi
    gw16012
    ioutil
    jlink
    jtag_vpi
    legacy-ft2232_libftdi
    oocd_trace
    opendous
    openjtag_ftdi
    osbdm
    parport
    presto_libftdi
    remote-bitbang
    rlink
    stlink
    sysfsgpio
    ti-icdi
    ulink
    usb-blaster-2
    usb_blaster_libftdi
    usbprog
    vsllink
    xlnx_pcie_xvc
  )

  ./bootstrap
  ./configure \
    --prefix=/usr \
    --disable-werror \
    --disable-internal-jimtcl \
    ${_features[@]/#/--enable-}

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  install -vDm644 -t "$pkgdir/usr/lib/udev/rules.d" contrib/60-openocd.rules
}
