# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-power
pkgver=8.0.2
pkgrel=1
pkgdesc='Wingpanel Power Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-power
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk3
  libgee
  libgranite.so
  libgtop
  libudev.so
  libwingpanel.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=4aaf2fda76cbf09b953fe916cb1480a0a715b780
source=(git+https://github.com/elementary/wingpanel-indicator-power.git#tag=${_tag})
b2sums=('ff919d21c82ebe36570b4bf4950686f08be8e86768925c72779bbb67c2386326e24f21f731216a1cac5f55c4688a25c1bd2a5cfa2f0243a24a310dbbd403e385')

pkgver() {
  cd wingpanel-indicator-power
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
