# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall-lsp-server
pkgver=1.1.3
pkgrel=151
pkgdesc="Language Server Protocol (LSP) server for Dhall"
url="https://github.com/dhall-lang/dhall-haskell/tree/master/dhall-lsp-server#readme"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-data-default' 'dhall'
         'dhall-json' 'haskell-lsp1.4' 'haskell-hslogger' 'haskell-lens' 'haskell-lens-family-core'
         'haskell-megaparsec' 'haskell-network-uri' 'haskell-optparse-applicative'
         'haskell-prettyprinter' 'haskell-rope-utf16-splay' 'haskell-unordered-containers'
         'haskell-uri-encode')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-doctest' 'haskell-hspec'
             'haskell-lsp-test0.14.0' 'haskell-lsp-types1.4' 'haskell-tasty' 'haskell-tasty-hspec')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
        dc489116bd1babdc32d2bbc55291364ce76b888b.patch)
sha512sums=('2a70e44ea9144eea8adb57da56e4f3249ff38fcb6d53e6d3e3b423a8481b63d5f77d5a306eb9f5bab2f3953235cdb0a5558706de22f62d9871378f52e92d33db'
            '0b1ace255d8440df64cbcb6ae38228329e2db6b924069c75be0e5acdc2979945e8d85f500b5b010fdf32e860dc3b9ac4f165f5933bd95fb4ef870820e87110da')

prepare() {
  cd $pkgname-$pkgver
  patch -p2 -i ../dc489116bd1babdc32d2bbc55291364ce76b888b.patch
  uusi -u lens
}

build() {
  cd $pkgname-$pkgver    

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  LD_LIBRARY_PATH="$PWD/dist/build" PATH="$PWD/dist/build/dhall-lsp-server:$PATH" runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
