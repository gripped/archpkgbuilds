# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.19.7
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs # libgcc_s.so ld-linux-x86-64.so
  glibc # libm.so libc.so
  libsass # libsass.so
  openssl
)
makedepends=(
  cargo
)
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('7f0dc0c9df9b9b6597a3c76e71199f520541a4d938eb3dc2abafbf11d082fbd9c0a03f6c23e80503624f0d9bc49fdb83d9443515da90c0b27b10b546994d5f2d')
b2sums=('27fab7c90870da21e56fea021d5bdd3596164bb49f6a637c1d18025481951e49144dd1a1303bcab1fc92114af1b47c3fb1fb3c7919840c5ab74c7ce2b1fed2a2')

prepare() {
  cd ${pkgname}.rs-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}.rs-${pkgver}
  export OPENSSL_NO_VENDOR=true
  cargo build --frozen --release
}

check() {
  cd ${pkgname}.rs-${pkgver}
  export OPENSSL_NO_VENDOR=true
  cargo test --frozen
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
