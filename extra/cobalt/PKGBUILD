# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.20.3
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs # libgcc_s.so ld-linux-x86-64.so
  glibc # libm.so libc.so
)
makedepends=(
  cargo
)
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('fda34b9e146571fa53820e6ab79515765568e1632ab5180103b18390c204346ce2962aba7c2d8d68f8c9bbeed2d6be1527c3cbd04c762d2167db82b57f88a06d')
b2sums=('90406b6e167b504c2c0fccc03228c3aa66d5c4bbf4f0767cbb7c0eb723e6ce4cf7408c8f6459f288ff81559a7edf611fc99dd530b63be307144dba33605c3872')

prepare() {
  cd ${pkgname}.rs-${pkgver}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}.rs-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}.rs-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
