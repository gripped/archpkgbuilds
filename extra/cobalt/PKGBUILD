# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.20.2
pkgrel=2
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs # libgcc_s.so ld-linux-x86-64.so
  glibc # libm.so libc.so
)
makedepends=(
  cargo
)
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('9f8b0e4f8ad0e9b624bb3df257126aa75270b877b05d74d82ae2a4d50a5133ade51d237f6f64a491ec8011f00d30977a5d3d7838bc0e374887b66bc7ed799ec3')
b2sums=('33ff47108ac8a871f388940feb3cac0aeef3b0db57f61f03e2470e276970c844ed6eb09c29e07d69c591cf847ae7b44c440dd7fb70c4591a4204453a70b207a3')

prepare() {
  cd ${pkgname}.rs-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}.rs-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}.rs-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
