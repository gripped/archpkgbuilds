# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.20.4
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs # libgcc_s.so ld-linux-x86-64.so
  glibc # libm.so libc.so
)
makedepends=(
  cargo
)
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('b9edbf2fddd56d0fe3f95662980ec53d45c676829d0766cfdce4a1e5fb38d7d5894f51ac74f1fa4592a84c7d9f58d0b616bb1e1ddee0a9208b384dbd42fbdf4f')
b2sums=('e6291d7cd5469cd0cc871df19669893e33b722f485230138a732b5ae4287c3b8ac054c7e0d33cca75b62c5170330934f070528cf47d2ac1305af27f004e63390')

prepare() {
  cd ${pkgname}.rs-${pkgver}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}.rs-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}.rs-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
