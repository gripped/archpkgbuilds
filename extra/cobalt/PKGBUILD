# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.19.9
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs # libgcc_s.so ld-linux-x86-64.so
  glibc # libm.so libc.so
  libsass # libsass.so
  openssl
)
makedepends=(
  cargo
)
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('e6977db916414b50bc45c5e9cd6fc17f24b02baa51bf8d007ced26629b09ac38de12ffae3269041a623aa1d280ab6d355f13a9efa23b766163cfd7d5141bab2d')
b2sums=('77f0a10698ccf8e90f8b48f68da6304f26a2676c5ba5e994619f11ea62ff57b8d1fdb4bc3d2e8e57fa0e2725dd8e53467418de97d8bf6a9df486be292d310b24')

prepare() {
  cd ${pkgname}.rs-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}.rs-${pkgver}
  export OPENSSL_NO_VENDOR=true
  cargo build --frozen --release
}

check() {
  cd ${pkgname}.rs-${pkgver}
  export OPENSSL_NO_VENDOR=true
  cargo test --frozen
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
