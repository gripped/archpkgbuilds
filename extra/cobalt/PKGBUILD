# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.19.5
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs
  glibc
  libsass
  openssl
)
makedepends=(
  rust
  cargo
)
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('bbb4af85a3d00ab4c3dac1ee70012fd2cca46466a4b969ccf47ce51342eca85ad2198c74cb17132b42f2e0e4c6a311c81fe0af90cb99524f12ee1041de3b5de8')
b2sums=('6e358a0e5ede7382f06f03a43eb1e995d0b95682e37618c4fc25aefe4e8b82325523482f14ce22adf0761cc35afd6c0609701b27f6dfb1e2e0dd0f30ccb9c5dd')

prepare() {
  cd ${pkgname}.rs-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}.rs-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
}

check() {
  cd ${pkgname}.rs-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
