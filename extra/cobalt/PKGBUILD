# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.19.8
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs # libgcc_s.so ld-linux-x86-64.so
  glibc # libm.so libc.so
  libsass # libsass.so
  openssl
)
makedepends=(
  cargo
)
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('1faf6657d2d1fbbcc9c8619b1447da5f6576cdb8a20e17cac841042f095745bcc22278e21095aa6051b1087890e79c868dcd633619748d81e297aa59142955bb')
b2sums=('61c09705dd9ca0ed456f50ca18fc4fe95355f98f42275a475a56045690315fc3db240cd0626dd34bfb3ea40396e98c08d178212e03e8c8464c92186047df9793')

prepare() {
  cd ${pkgname}.rs-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}.rs-${pkgver}
  export OPENSSL_NO_VENDOR=true
  cargo build --frozen --release
}

check() {
  cd ${pkgname}.rs-${pkgver}
  export OPENSSL_NO_VENDOR=true
  cargo test --frozen
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
