# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sefa Eyeoglu <contact@scrumplex.net>
# Contributor: Daniel Maslowski <info@orangecms.org>

pkgname=fisher
pkgver=4.4.5
pkgrel=1
pkgdesc='A package manager for the fish shell'
arch=('any')
url='https://github.com/jorgebucaran/fisher'
license=('MIT')
depends=('fish' 'curl')
makedepends=('git')
# TODO add fishtape for tests
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('d9ba01b9fe23623360572107409d2c161e5a94df7af69cd5399084f80ca19a60a581c4185502badfd4889eeaf243d7342314b9ef8c30ab0591f6c23e3aed392a')
b2sums=('a54cc3fcb8e5c11f66bf4351fafc4255b8819fa01990b4d0740b35b3f6d029d8e47797a155dabb119ccd69cd1bab556f24e79534272dd2e81cf0c104129ef145')

package() {
  cd "$pkgname"

  # install to global fish directory
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_functions.d" functions/fisher.fish
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_completions.d" completions/fisher.fish

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
