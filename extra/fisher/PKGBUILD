# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sefa Eyeoglu <contact@scrumplex.net>
# Contributor: Daniel Maslowski <info@orangecms.org>

pkgname=fisher
pkgver=4.4.6
pkgrel=1
pkgdesc='A package manager for the fish shell'
arch=(any)
url='https://github.com/jorgebucaran/fisher'
license=(MIT)
depends=(fish curl)
makedepends=(git)
# TODO add fishtape for tests
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('23a610dc305bc8d29980f29cacda36789ea982d53c3c1fc8a05375367b386331c0a3a799376b6f03cdd7f3cfb875063a1771fe2b905c4c9c2f963be67d9f8dbc')
b2sums=('65fda39e1436dfc95fac9285976963167cf23bfc7f011b44dd928bbbf0bc528920425b4c509c6e0f85ee8e0c510be302a3ad8ef0bf234f0754c7be6de376f6dd')

package() {
  cd "$pkgname"

  # install to global fish directory
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_functions.d" functions/fisher.fish
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_completions.d" completions/fisher.fish

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
