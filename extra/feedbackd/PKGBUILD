# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker (CoderThomasB) <tw.booker@outlook.com>
# Contributor: <reg-archlinux AT klein DOT tuxli DOT ch>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: Sam Whited <sam@samwhited.com>

pkgname=feedbackd
pkgver=0.3.0
pkgrel=1
pkgdesc='A daemon to provide haptic feedback on events'
arch=(x86_64 aarch64)
url='https://source.puri.sm/Librem5/feedbackd'
license=(GPL3)
depends=(dconf gsound json-glib libgudev gsettings-desktop-schemas)
makedepends=(gobject-introspection meson vala git python-docutils
             python-packaging libgmobile)

source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('ca7761d384bfa309fbdaa2349d99d8391875d0b64ef9a000fc37db60089a8876')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "${pkgname}"
}

build() {
  arch-meson "${pkgname}" build -Dgtk_doc=false -Dman=true -Dtests=true -Ddaemon=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build

  install -Dm644 "$srcdir/$pkgname"/debian/feedbackd.udev \
                 "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules
}
