# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker (CoderThomasB) <tw.booker@outlook.com>
# Contributor: <reg-archlinux AT klein DOT tuxli DOT ch>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: Sam Whited <sam@samwhited.com>

pkgname=feedbackd
pkgver=0.8.4
pkgrel=1
pkgdesc='A daemon to provide haptic feedback on events'
arch=(x86_64)
url='https://gitlab.freedesktop.org/agx/feedbackd'
license=(GPL-3.0-or-later)
depends=(
        "dconf"
        "gsound"
        "json-glib"
        "libgudev"
        "gsettings-desktop-schemas"
        "libgmobile"
)
checkdepends=("umockdev")
makedepends=(
            "gobject-introspection"
            "meson"
            "vala"
            "git"
            "python-docutils"
            "glib2-devel"
)
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('4672ce47396dac81b8271e4354b2a44e0b6bedb8dacf53ebf5effdcdbf7e9055')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "${pkgname}"
}

build() {
  arch-meson "${pkgname}" build -Dgtk_doc=false -Dman=true -Dtests=true -Ddaemon=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
