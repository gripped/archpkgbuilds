# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker (CoderThomasB) <tw.booker@outlook.com>
# Contributor: <reg-archlinux AT klein DOT tuxli DOT ch>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: Sam Whited <sam@samwhited.com>

pkgname=feedbackd
pkgver=0.5.0
pkgrel=2
pkgdesc='A daemon to provide haptic feedback on events'
arch=(x86_64)
url='https://source.puri.sm/Librem5/feedbackd'
license=(GPL3)
depends=(dconf gsound json-glib libgudev gsettings-desktop-schemas libgmobile)
checkdepends=(umockdev)
makedepends=(gobject-introspection meson vala git python-docutils glib2-devel)
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('2692b6227521c2bed07c3495f2d15eee63a84a71ffa6e5dc82f153a972c677a2')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "${pkgname}"
}

build() {
  arch-meson "${pkgname}" build -Dgtk_doc=false -Dman=true -Dtests=true -Ddaemon=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
