# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker (CoderThomasB) <tw.booker@outlook.com>
# Contributor: <reg-archlinux AT klein DOT tuxli DOT ch>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: Sam Whited <sam@samwhited.com>

pkgname=feedbackd
pkgver=0.8.7
pkgrel=1
pkgdesc='A daemon to provide haptic feedback on events'
arch=(x86_64)
url='https://gitlab.freedesktop.org/agx/feedbackd'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gsettings-desktop-schemas
  gsound
  json-glib
  libgmobile
  libgudev
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  python-docutils
  umockdev
  vala
)
source=("git+https://gitlab.freedesktop.org/agx/feedbackd.git?signed#tag=v$pkgver")
b2sums=(06d7f05b83932a2b2ee4c9c7393bfa508687bd4a443b565b2208ecf7843ebea5c65e701b18acd1f597652b8ff66b741193aae8a4087b3a3b40dcf1c7908c28c1)
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido GÃ¼nther <guido@xcpu.ch>

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true \
    -D man=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
