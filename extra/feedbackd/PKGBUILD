# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker (CoderThomasB) <tw.booker@outlook.com>
# Contributor: <reg-archlinux AT klein DOT tuxli DOT ch>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: Sam Whited <sam@samwhited.com>

pkgname=feedbackd
pkgver=0.8.8
pkgrel=1
pkgdesc='A daemon to provide haptic feedback on events'
arch=(x86_64)
url='https://gitlab.freedesktop.org/agx/feedbackd'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gsettings-desktop-schemas
  gsound
  json-glib
  libgmobile
  libgudev
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  python-docutils
  umockdev
  vala
)
source=("git+https://gitlab.freedesktop.org/agx/feedbackd.git?signed#tag=v$pkgver")
b2sums=('38a3fdeb6b999c29480198a922ef1037f8c9fec67e80c30d55250e41df37bc0a859eb48b633bff2f24713b7b813da6c3a6c879908583c7e2057f1b4bb2c4d897')
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido GÃ¼nther <guido@xcpu.ch>

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true \
    -D man=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
