# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Kyle Manna <kyle [at] kylemanna [dot] com>

pkgname=lurk
pkgver=0.3.11
pkgrel=1
pkgdesc="A pretty (simple) alternative to strace"
arch=('x86_64')
url="https://github.com/JakWai01/lurk"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('5541190b8e3ea7136c0570b4471cb467ed1365502c5cad0cb18d8d81848e6e06871bcd92e0cab0ad88c041c70d2ea597f5d2053f71ffc49e8d3d0ca63c0db893')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
