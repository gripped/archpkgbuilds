# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Carl Smedstad <carl.smedstad at protonmail dot com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Ronan Pigott <rpigott@berkeley.edu>

pkgname=python-pypresence
_name=${pkgname#python-}
pkgver=4.6.1
pkgrel=3
pkgdesc='Discord RPC and Rich Presence wrapper library'
url='https://github.com/qwertyquerty/pypresence'
arch=(any)
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
checkdepends=(python-pytest python-pytest-asyncio)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('04673899211c651b7cff674378a37af1e21675576184afb96f2abdd6426d404fed0e25008eb1da5d32f4dbf7ea2aed57852afee1d37ee05eda3ab57ace6da6ae')

check() {
  cd $_name-$pkgver
  pytest -v
}

build() {
  cd $_name-$pkgver

  python -m build --wheel --no-isolation
  sphinx-build -b man docs/sphinx man
}

package() {
  cd $_name-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/pypresence.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
