# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Carl Smedstad <carl.smedstad at protonmail dot com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Ronan Pigott <rpigott@berkeley.edu>

pkgname=python-pypresence
_name=${pkgname#python-}
pkgver=4.6.1
pkgrel=1
pkgdesc='Discord RPC and Rich Presence wrapper library'
url='https://github.com/qwertyquerty/pypresence'
arch=(any)
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
checkdepends=(python-pytest python-pytest-asyncio)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('83aab75b1b25bf21ab822b23287c160682f7d98e4e37ecf916497107fe2417465aed280f2aff5aa6bcc83a7342b87659e1946ab1e0d6e61d54fdc7f51d7547d1')

check() {
  cd $_name-$pkgver
  pytest -v
}

build() {
  cd $_name-$pkgver

  python -m build --wheel --no-isolation
  sphinx-build -b man docs/sphinx man
}

package() {
  cd $_name-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/pypresence.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
