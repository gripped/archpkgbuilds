# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Carl Smedstad <carl.smedstad at protonmail dot com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Ronan Pigott <rpigott@berkeley.edu>

pkgname=python-pypresence
_name=${pkgname#python-}
pkgver=4.5.1
pkgrel=1
pkgdesc='Discord RPC and Rich Presence wrapper library'
url='https://github.com/qwertyquerty/pypresence'
arch=(any)
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('109a64cf717d448ea2463ac61d867bb068a688fe3459e683d3b64edc796f1f14613c4d7c4670857445a261a3110902aa57096e9798505ece8af806be0729a28c')

build() {
  cd $_name-$pkgver

  python -m build --wheel --no-isolation
  sphinx-build -b man docs/sphinx man
}

package() {
  cd $_name-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/pypresence.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
