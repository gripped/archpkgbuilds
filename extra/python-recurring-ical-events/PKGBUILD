# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-recurring-ical-events
pkgver=3.6.0
pkgrel=1
pkgdesc='Python library to calculate recurrence times of events, todos and journals based on icalendar RFC5545'
arch=(any)
url='https://github.com/niccokunzmann/python-recurring-ical-events'
license=(LGPL-3.0-or-later)
depends=(
  python
  python-dateutil
  python-icalendar
  python-x-wr-timezone
)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytz
  python-tzdata
)
source=("git+https://github.com/niccokunzmann/python-recurring-ical-events.git#tag=v$pkgver")
b2sums=(ac39618db2f3f5875cf5a3252017ce0e7a3b944f985f2bad0faacdaebc824f243dd1a06f06e6259c582d626058ca8d417ac201b7869112c8d041cf1a15ebf98a)

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  pytest --ignore=recurring_ical_events/test/test_readme.py --ignore=recurring_ical_events/test/test_with_doctest.py
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Remove installed tests
  rm -r "$pkgdir$(python -c 'import site; print(site.getsitepackages()[0])')"/recurring_ical_events/test/
}
