# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-recurring-ical-events
pkgver=3.4.0
pkgrel=1
pkgdesc='Python library to calculate recurrence times of events, todos and journals based on icalendar RFC5545'
arch=(any)
url='https://github.com/niccokunzmann/python-recurring-ical-events'
license=(LGPL-3.0-or-later)
depends=(
  python
  python-dateutil
  python-icalendar
  python-x-wr-timezone
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytz
  python-tzdata
)
source=("git+https://github.com/niccokunzmann/python-recurring-ical-events.git#tag=v$pkgver")
b2sums=('0b8ccc1b767f27d46027942350f35e199ec6e469080d79891edc1c8d5bd8a23fe37c9a6cedd3e64fb034c9e0274a2ca8d339a3ccd10436a4e68905c26994e52d')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  pytest --ignore=test/test_readme.py --ignore=test/test_with_doctest.py
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
