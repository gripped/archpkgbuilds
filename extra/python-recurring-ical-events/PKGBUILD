# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-recurring-ical-events
pkgver=3.3.4
pkgrel=2
pkgdesc='Python library to calculate recurrence times of events, todos and journals based on icalendar RFC5545'
arch=(any)
url='https://github.com/niccokunzmann/python-recurring-ical-events'
license=(LGPL-3.0-or-later)
depends=(
  python
  python-dateutil
  python-icalendar
  python-x-wr-timezone
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-tzdata
)
source=("git+https://github.com/niccokunzmann/python-recurring-ical-events.git#tag=v$pkgver")
b2sums=('1756056ac39a18a71374f242a534ebd40b9fc1fc0ab9e2f07421b301a43bd9220a6cb9ee6845df7e057a07b8bf20f346495afb02ea1df54f6b3ce68613c45ec7')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  pytest --ignore=test/test_readme.py --ignore=test/test_with_doctest.py
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
