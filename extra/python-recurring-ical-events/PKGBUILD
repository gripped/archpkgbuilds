# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-recurring-ical-events
pkgver=3.6.1
pkgrel=1
pkgdesc='Python library to calculate recurrence times of events, todos and journals based on icalendar RFC5545'
arch=(any)
url='https://github.com/niccokunzmann/python-recurring-ical-events'
license=(LGPL-3.0-or-later)
depends=(
  python
  python-dateutil
  python-icalendar
  python-x-wr-timezone
)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytz
  python-tzdata
)
source=("git+https://github.com/niccokunzmann/python-recurring-ical-events.git#tag=v$pkgver")
b2sums=(529f4cd0a6e2969b6ac8acc4102df27ad4f7e6eb9a22fc29df294b7b0190626d2807d6e82b22c5d9217ac9b92b612ea6c706fac28a287791682d32ec5df10589)

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  pytest --ignore=recurring_ical_events/test/test_readme.py --ignore=recurring_ical_events/test/test_with_doctest.py
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Remove installed tests
  rm -r "$pkgdir$(python -c 'import site; print(site.getsitepackages()[0])')"/recurring_ical_events/test/
}
