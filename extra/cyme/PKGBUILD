# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor:  Vitalii Kuzhdin <vitaliikuzhdin@gmail.com>

pkgname=cyme
pkgver=2.2.1
pkgrel=1
pkgdesc="List system USB buses and devices; a lib and modern cross-platform lsusb"
arch=('x86_64')
url="https://github.com/tuna-f1sh/${pkgname}"
license=('GPL-3.0-or-later')
depends=('gcc-libs' 'libusb')
makedepends=('cargo')
_pkgsrc="${pkgname}-${pkgver}"
source=("${_pkgsrc}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('c02856a2e360d2ccb05137352a2aad3805a35d32571839d4fb232a2d40c630f8')

prepare() {
  cd "${srcdir}/${_pkgsrc}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_pkgsrc}"
  cargo build --frozen --release --all-features
}

check() {
  cd "${srcdir}/${_pkgsrc}"
  cargo test --frozen
}

package() {
  cd "${srcdir}/${_pkgsrc}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "doc"
  install -Dm644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"

  install -Dm644 "${pkgname}.bash" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -Dm644 "${pkgname}.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
  install -Dm644 "_${pkgname}" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 "_${pkgname}.ps1" "${pkgdir}/usr/share/powershell/Modules/${pkgname}/${pkgname}.ps1"
}

# vim: ts=2 sw=2 et:
