# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Filip <fila pruda.com>

pkgname=postfixadmin
pkgver=3.3.15
pkgrel=2
pkgdesc="Web based interface to manage Postfix mailboxes, virtual domains and aliases"
arch=(any)
url="https://postfixadmin.sourceforge.io/"
license=(GPL-2.0-or-later)
depends=(php)
optdepends=(
  'apache: run in webserver'
  'dovecot: interface local IMAP/POP3 server'
  'mariadb: use local MySQL server'
  'perl: for example scripts'
  'php-fpm: run in fastCGI process manager'
  'php-imap: use IMAP'
  'php-sqlite: use SQLite database backend'
  'php-pgsql: use PostgreSQL database backend'
  'postfix: interface local mail server'
  'postgresql: use local PostgreSQL server'
  'python: for example scripts'
  'uwsgi-plugin-php: run as application container'
)
backup=(
  etc/webapps/postfixadmin/config.inc.php
  etc/webapps/postfixadmin/config.local.php
  etc/webapps/postfixadmin/.htaccess
  etc/uwsgi/postfixadmin.ini
)
source=(
  https://github.com/postfixadmin/$pkgname/archive/$pkgname-$pkgver.tar.gz
  $pkgname.sysusers
  $pkgname.tmpfiles
  $pkgname.uwsgi
  LICENSE
)
install=$pkgname.install
sha512sums=('02c4a7fb0d5b148a2f9e73e0278a47d1ee63b29a0019cf510f04d33386fc50727c0dae728eafee688a136159ba462af1931fe0658daa06671459c43668867865'
            '896abf7fadb17c8d55136e116b201ae8a1292ac903478f77b61e19ebd19bea79e4cc71e3b3d11675e15603f0ea25e3cb3200b1f5a1e2f5c27e72722bb7316173'
            '7e55c844be7b186d9eb568fe097632651c683582da83e0ee24cd6dd7c5a581fe05e5462174237f542ff8dc5d17a8ab651a77f0087a0c8df01d7b36c7722b1440'
            '2e3854d97bd74dc2501260c557549b0162f4a7749261538672de052b252e56fa62ef052bafe6d8681f2f87a94b71039fc9a68ff622a04dd2d6232213143d1840'
            'a33658d9271e5c537ccd41bf540b463ad2a5eca4a060c80486ff42a736f0aa042d10436e7177c34d792177cb11285243dee1f31c4df54fb0bfaabbc306406930')
b2sums=('3df3f4745c9743d5e2d700fed2f08c607817b4264ba91ed91801235a64bdb3bdda489a1279b5dfb579f3eabbbed2f105d2f29759f0aff2c1c3f4aa6e4246f687'
        '48ffad587d69a85276a241dc70c1849f67008af9d102618d207173aef29447a7c25801a73724dc4d7f5e31f87f0e3ead699dab74062fdaa5d4215209f45448ca'
        '0e0170b08284dcfc5a68afd46a2e726d696905f3ddcd8158fed68c84fec039fabe60379005055e5fa1cc1fbb098c107b739d0e107f027bd7054d0e31b6e9cea6'
        'b59885eec24483d0f65614918f7c8cd70402600ac645f14560d5d28dfddce84a41a3ce1e2f9ea4bc0364ce299f468a2a243b0cbe920c0526f3c92e3d0269fa0c'
        'a29664104e1ee73ca0aee1d633e9095d92a57c92787f8d8740bdb7211ba3205782ed8677f539bdb8cae3dd75a3694be3132e185fa3fc4b3f401e1f88eb776101')

prepare(){
  cd $pkgname-$pkgname-$pkgver
  # create empty local config (for overrides)
  printf "<?php\n" > config.local.php
  # create .htaccess file
  printf "#Require all denied\n" > .htaccess
  # remove unneeded executable bits
  find . -executable -type f -and -not -path "*scripts*" -exec chmod -c 644 {} \;
  # remove symlinks
  find . -type l -exec rm -v {} +
  # remove unneeded debian directories
  rm -rv ADDITIONS/squirrelmail-plugin/debian
}

package() {
  cd $pkgname-$pkgname-$pkgver

  # sysusers.d integration
  install -vDm 644 ../$pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  # tmpfiles.d integration
  install -vDm 644 ../$pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -vDm 644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/0BSD.txt"
  # docs
  install -vDm 644 {README.md,CHANGELOG.TXT} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 DOCUMENTS/{README*,*.{txt,md}} -t "$pkgdir/usr/share/doc/$pkgname/documents"
  cp -av ADDITIONS VIRTUAL_VACATION "$pkgdir/usr/share/doc/$pkgname"

  # webapp
  find configs languages lib model public scripts templates tests \
    -type f \( \
    -iname "*.conf" -or \
    -iname "*.css" -or \
    -iname "*.eot" -or \
    -iname "*.gif" -or \
    -iname "*.ico" -or \
    -iname "*.js" -or \
    -iname "*.lang" -or \
    -iname "*.lib" -or \
    -iname "*.map" -or \
    -iname "*.php" -or \
    -iname "*.png" -or \
    -iname "*.sql" -or \
    -iname "*.svg" -or \
    -iname "*.tpl" -or \
    -iname "*.ttf" -or \
    -iname "*.woff" -or \
    -iname "*.woff2" \
    \) -exec install -vDm 644 {} "$pkgdir/usr/share/webapps/$pkgname/"{} \;
  install -vDm 644 {common,functions.inc,index}.php phpunit.xml -t "$pkgdir/usr/share/webapps/$pkgname"
  (
    # adding weird symlinks
    cd "$pkgdir/usr/share/webapps/$pkgname/public"
    ln -sv users/password-change.php .
    ln -sv users/password-recover.php .
  )

  # config
  install -vDm 640 config.{inc,local}.php -t "$pkgdir/etc/webapps/$pkgname/"
  ln -sv /etc/webapps/$pkgname/config.inc.php "$pkgdir/usr/share/webapps/$pkgname/config.inc.php"
  ln -sv /etc/webapps/$pkgname/config.local.php "$pkgdir/usr/share/webapps/$pkgname/config.local.php"
  install -vDm 644 .htaccess -t "$pkgdir/etc/webapps/$pkgname/"
  ln -sv /etc/webapps/$pkgname/.htaccess "$pkgdir/usr/share/webapps/$pkgname/.htaccess"
  install -vDm 644 ../$pkgname.uwsgi "$pkgdir/etc/uwsgi/$pkgname.ini"
  # template caches
  install -vdm 755 "$pkgdir/var/cache/$pkgname/templates_c"
  ln -sv /var/cache/$pkgname/templates_c "$pkgdir/usr/share/webapps/$pkgname/"
}
