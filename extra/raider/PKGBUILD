# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=raider
pkgver=3.0.1
pkgrel=1
pkgdesc='File deletion program designed to permanently remove sensitive files from your computer, enhancing data privacy'
arch=(x86_64)
url='https://apps.gnome.org/Raider/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://github.com/ADBeveridge/raider.git#tag=v$pkgver")
b2sums=(5ab2884afa8d161955a16f760ac9f0aa50ca547b6148ca84845a47d72abcdcf74a33fb54a3e8ec7eca43a68035acab571e527a3782986b1a515d00c7058f8366)

prepare() {
  cd $pkgname

  # https://github.com/ADBeveridge/raider/pull/152
  git cherry-pick -n a34c8e832d6139219bf2bf14c1a2e05606974f62
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
