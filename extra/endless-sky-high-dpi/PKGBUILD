# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=endless-sky-high-dpi
pkgver=0.10.16
pkgrel=1
pkgdesc='High DPI sprites for Endless Sky'
arch=(any)
url='https://github.com/endless-sky/endless-sky-high-dpi'
license=(
  GPL-3.0-only
  CC-BY-4.0
  CC-BY-SA-3.0
  CC-BY-SA-4.0
  LicenseRef-public-domain
)
depends=(endless-sky)
makedepends=(git)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('5d1f983a55fadc8b10dddd3f9a4200b58289bb2f051eeb823b1f9765fee1b0259e30b37fd7ec7eee625167cab000c784658e73075d9d9428f56ebfb827f647df')
b2sums=('95870eb2de02165b60473d760d00d37d154132e99440d2d10e0572029f5e7cd8e461e46a806e0557595864d437df46979d15269dc8f71c1bdeffb59e3b80ecd8')

package() {
  cd "$pkgname"

  # create plugin directory
  install -d "$pkgdir/usr/share/games/endless-sky/plugins/$pkgname"

  # install plugin
  cp -r * "$pkgdir/usr/share/games/endless-sky/plugins/$pkgname"

  # license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" copyright
}
