# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=endless-sky-high-dpi
pkgver=0.10.15
pkgrel=1
pkgdesc='High DPI sprites for Endless Sky'
arch=(any)
url='https://github.com/endless-sky/endless-sky-high-dpi'
license=(
  GPL-3.0-only
  CC-BY-4.0
  CC-BY-SA-3.0
  CC-BY-SA-4.0
  LicenseRef-public-domain
)
depends=(endless-sky)
makedepends=(git)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('a18bbc8af547c92264d0f6d54532fa6024646e667aaa614dc8396d5e5c85c2de0c823f7da4fad8cc340604cb2966d721962879d1899500fd817eb8acf7772bab')
b2sums=('447b0b60ccd0a69e9f73bde7579bb8dbd6d163fb4688ffcdad0f8bfbcdf11bb524efe0ee104cbcf07636ff230e587528ecbcd18b9f030d632a071af6c29643a7')

package() {
  cd "$pkgname"

  # create plugin directory
  install -d "$pkgdir/usr/share/games/endless-sky/plugins/$pkgname"

  # install plugin
  cp -r * "$pkgdir/usr/share/games/endless-sky/plugins/$pkgname"

  # license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" copyright
}
