# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=endless-sky-high-dpi
pkgver=0.10.10
pkgrel=1
pkgdesc='High DPI sprites for Endless Sky'
arch=('any')
url='https://github.com/endless-sky/endless-sky-high-dpi'
license=(
  'GPL-3.0-only'
  'CC-BY-4.0'
  'CC-BY-SA-3.0'
  'CC-BY-SA-4.0'
  'LicenseRef-public-domain'
)
depends=('endless-sky')
makedepends=('git')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('98c5c220c1f99f80295c4e957f251cde964d10bf57bf08680c56ec40bad2594c8fa1bbf837ffd0dcd4732c9de31a2b49c5cd9ac9e214b5e090158ce85fabad4e')
b2sums=('bbf15b349f8e2a72cee5176b9bb9f6ea64c408b311bd37782553b833bbbb4544c911e471f7fd332f6b029da3666d596827bea7ba23331e9d1be23de15e955760')

package() {
  cd "$pkgname"

  # create plugin directory
  install -d "$pkgdir/usr/share/games/endless-sky/plugins/$pkgname"

  # install plugin
  cp -r * "$pkgdir/usr/share/games/endless-sky/plugins/$pkgname"

  # license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" copyright
}
