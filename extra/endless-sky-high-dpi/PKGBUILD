# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=endless-sky-high-dpi
pkgver=0.10.14
pkgrel=1
pkgdesc='High DPI sprites for Endless Sky'
arch=('any')
url='https://github.com/endless-sky/endless-sky-high-dpi'
license=(
  'GPL-3.0-only'
  'CC-BY-4.0'
  'CC-BY-SA-3.0'
  'CC-BY-SA-4.0'
  'LicenseRef-public-domain'
)
depends=('endless-sky')
makedepends=('git')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('96a3954755d7b709edcca26f823b71f236f138792ceb03e11024b02d41c49d0245721ec28dab63c282632b006e47661982adc16d7471ed9f5a2cd0bf55eef066')
b2sums=('c6179e611d22fef439a59c08f3e355a500cd61d19de68c19854580a767c8fd2919ec903a45b1c90963c6738642489a97aa5a3e9d878499e23f0ad20001f3bc50')

package() {
  cd "$pkgname"

  # create plugin directory
  install -d "$pkgdir/usr/share/games/endless-sky/plugins/$pkgname"

  # install plugin
  cp -r * "$pkgdir/usr/share/games/endless-sky/plugins/$pkgname"

  # license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" copyright
}
