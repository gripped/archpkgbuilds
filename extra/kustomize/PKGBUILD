# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=kustomize
pkgver=5.8.0
pkgrel=1
pkgdesc="Template-free customization of Kubernetes YAML manifests"
arch=(x86_64)
url="https://github.com/kubernetes-sigs/kustomize"
license=(Apache-2.0)
makedepends=(go)
depends=(glibc)
optdepends=(helm)
source=("https://github.com/kubernetes-sigs/kustomize/archive/kustomize/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('003545b211d5eb0a266e8c75ea389bcbb219cd0be9f43b0b56ced4953cf88adc0e2a6880f19cafad13da494c79c6ae849c1b7d7d543b9b475d928807b05d2e5b')
b2sums=('fa5466670fc6e40e123601efc6f715d5d549a492fbd3e7f844fa2097cf79d294fdf4513430491cf756d387f90f25a1ef794b193dafee37b77ba0d4d347d60ce5')

prepare() {
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  GOPATH="${srcdir}"
  go mod download
}

build(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  GOPATH="${srcdir}"
  export GOPROXY=off
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags " \
      -X sigs.k8s.io/kustomize/api/provenance.version=${pkgver} \
      -X sigs.k8s.io/kustomize/api/provenance.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
      -compressdwarf=false \
      -linkmode external \
      -extldflags \"${LDFLAGS}\"" \
    .
}

check(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  go test ./...
}

package(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  # install completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./kustomize completion bash >  "${pkgdir}/usr/share/bash-completion/completions/kustomize"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  ./kustomize completion zsh >  "${pkgdir}/usr/share/zsh/site-functions/_kustomize"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d"
  ./kustomize completion fish >  "${pkgdir}/usr/share/fish/vendor_completions.d/kustomize.fish"

  # install binary
  install -D kustomize "${pkgdir}/usr/bin/kustomize"
}

# vim: ts=2 sw=2 et:
