# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=kustomize
pkgver=5.7.0
pkgrel=1
pkgdesc="Template-free customization of Kubernetes YAML manifests"
arch=('x86_64')
url="https://github.com/kubernetes-sigs/kustomize"
license=('Apache-2.0')
makedepends=('go')
depends=('glibc')
optdepends=('helm')
options=('!lto')
source=("https://github.com/kubernetes-sigs/kustomize/archive/kustomize/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ecefbf45cdebc619575a61f712eb6d093aaa0e66eb998d902376d26b05c12b8fadb61ec6d191ebe280f3a9c665fb1e69580a628f975cd67488fd1fc07f44f1a3')
b2sums=('248f0bca7838741df8f2b8da1ff3a2bfe1e7d2e6ad7f7077de5d5dda943062f270ab355fb1386dbc3b5c8409dddb3f2402023d2542361fd20f4af9aa4a6a86f8')

prepare() {
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  GOPATH="${srcdir}"
  go mod download
}

build(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  GOPATH="${srcdir}"
  export GOPROXY=off
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags " \
      -X sigs.k8s.io/kustomize/api/provenance.version=${pkgver} \
      -X sigs.k8s.io/kustomize/api/provenance.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
      -compressdwarf=false \
      -linkmode external \
      -extldflags \"${LDFLAGS}\"" \
    .
}

check(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  go test ./...
}

package(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  # install completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./kustomize completion bash >  "${pkgdir}/usr/share/bash-completion/completions/kustomize"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  ./kustomize completion zsh >  "${pkgdir}/usr/share/zsh/site-functions/_kustomize"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d"
  ./kustomize completion fish >  "${pkgdir}/usr/share/fish/vendor_completions.d/kustomize.fish"

  # install binary
  install -D kustomize "${pkgdir}/usr/bin/kustomize"
}

# vim: ts=2 sw=2 et:
