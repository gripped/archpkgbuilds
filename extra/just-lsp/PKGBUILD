# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=just-lsp
pkgver=0.2.7
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('f481792f2316b9023e8d388a8c568466c060500a359ae878c9a573029b1ec8742ee69c695cac8bffec9016de696d9d9762053f459eb9b90ece43ff5827f66c17')
b2sums=('279e0f86fd7d13f452df26607f5f1d745b38d7c41fd1f23be13319e18c230270baffba986c9086abf8025bc70af00513d2a62d5059d0c8ccafb279296b95edde')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  cd $pkgname-$pkgver
  cargo build --frozen --release
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
