# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=just-lsp
pkgver=0.3.1
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('65bdea623121d224613aeca825b796e7ddc35c06e9195d16bceca3cc8d57723691fb4acf3767d659dc0c973e39941cd88019c3549e1ab041bfd42a5d590699bc')
b2sums=('ba0a17b15d269f7ba4b002ae8f17ca191cbb60d50166ba123c3fcf55442d4cf4970388ad8396e3f34c96dd5e36da8ec70031e0e7e3c1c00e0e774783046b28f3')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  cd $pkgname-$pkgver
  cargo build --frozen --release
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
