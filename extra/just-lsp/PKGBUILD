# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=just-lsp
pkgver=0.2.4
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
options=(!lto) # LTO does not work as the sources include C-code from tree-sitter
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('cb25b08fe3c2b85216f153b4aa23c6240fcb834424b6d3d00200f1f3f1d29c6a08ac8c66326314471474c3eb932397dd890a81d4cdbc1feabea3153093e306d3')
b2sums=('d2c83ab0e89ceeef60b60bb43e78c58e732a29169b1825217713d5a4beb03f6cf037af60c424bc1283ccda704314cb82f63f4e600d7807797bd34eee8b03e201')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
