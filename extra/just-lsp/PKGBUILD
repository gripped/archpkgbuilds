# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=just-lsp
pkgver=0.1.2
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
options=(!lto)  # LTO does not work as the sources include C-code from tree-sitter
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('84d3d0487233b0f5bab7e9d5a4505c499b058a93a8d580aa11855d46dfd59ddecfe579a933fee0996e8387f72dcdd2e140fc27ae8e3e086b03135d8a152fe9c9')
b2sums=('f8a058c6fc4082e6eb7462934870d045c39b074aa68f8a21ef036a1758ea4fb5f56d602928fc13aba60a485bda3728ad9f5aa036472d0e8d62ef2049ad9f698f')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
