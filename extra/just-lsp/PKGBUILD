# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=just-lsp
pkgver=0.3.0
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('b4177156112230f2d00f7eb4584f21c03cb2d1720d303f86f2f5b304b976f55a3d2d8796806e84e17f3ed86c66c6d52121f311af5c6e0553d75a287767c20fd4')
b2sums=('9e8eaa32cdd14c4afcee6758a2033ca06bb5b4f606447e618a7cf8e46760969e153010f91cfaec8c711751a737369df577bd88d026d27afa62d1b53e40e86f22')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  cd $pkgname-$pkgver
  cargo build --frozen --release
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
