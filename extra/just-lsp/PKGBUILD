# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=just-lsp
pkgver=0.2.0
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
options=(!lto)  # LTO does not work as the sources include C-code from tree-sitter
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('205ab23bf93bad6eea098ae2c3e04c2ca5fd33bde9c98700d1d7336103c1abb08ad54965a7b916c8e695c46b1e278f1e6817903c3b59163de900c1bec41dd505')
b2sums=('be7aa691f2a0bf9c8b4eeabb39c9ac03d18638371fa2da1fffa11eb90c083bd55ffa392057a5d3ed6f323aa6155a85a43b3261c262085c5c1f0d8b29dd4bb783')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
