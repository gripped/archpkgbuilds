# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=just-lsp
pkgver=0.2.3
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
options=(!lto) # LTO does not work as the sources include C-code from tree-sitter
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('9554f365973fb8daaa9c305ed1ba30c9642db0bf404cc7813f3de40d20bd6ad97a5855227b78c99b89bcda6686fe703c8e46857955cdaee698056db8ab287840')
b2sums=('71b8f676dafd81d5d98b92b5ac680cf075e859c1cde47316bba80d6ccdb34f4262b4f377d0946ca6d5a6aafbdbeb305519d6f043d835679686731455a7085b08')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
