# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=just-lsp
pkgver=0.2.8
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('9934443d4f64bcb9a1079a12ab1a49271f2b5185140087d4a24a163d4433cde2ed225fc7d10f0d97b70166c23a3d7879c302a7c6e90f48e37a132fe60832bd10')
b2sums=('3c80a7de7e142f88f051b009e0a45c6b404364f95b84bec42072a133a78ff8efa5a83e4154fc3aab7a279287d50804af6fe75d34c4dc49a752c39cc02962a15e')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  cd $pkgname-$pkgver
  cargo build --frozen --release
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
