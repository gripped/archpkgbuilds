# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=just-lsp
pkgver=0.2.1
pkgrel=1
pkgdesc="A language server for just"
arch=(x86_64)
url="https://github.com/terror/just-lsp"
license=(CC0-1.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
options=(!lto)  # LTO does not work as the sources include C-code from tree-sitter
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('78a59e6fe673a15d60138f9e1691e1a1680f151e57fa736dd6e50f709b4aa043aa91eb2a350558d59489489ea0a12377fbf694646b90830b844ca1f4a39c3da6')
b2sums=('af4dbc44846a8d2c8cfe583a96209fee98ee41ffbc06296f3f8e68baedf9357a9403d9e7e8522ffc4a7fd9378024d50e66729a9e95b03e45f7b0c5399d76cae7')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
