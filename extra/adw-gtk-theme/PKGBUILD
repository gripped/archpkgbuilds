# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Max Harmathy <harmathy@mailbox.org>
# Contributor: Dušan Simić <dusan.simic1810@gmail.com>

pkgname=adw-gtk-theme
pkgver=6.2
pkgrel=1
pkgdesc='Unofficial GTK 3 port of the libadwaita theme'
arch=(any)
url=https://github.com/lassekongo83/adw-gtk3
license=(LGPL-2.1-only)
makedepends=(
  dart-sass
  git
  meson
)
_tag=1fe75126e1e8b056c817daf513f363e8877d3e36
source=(git+https://github.com/lassekongo83/adw-gtk3.git#tag=${_tag})
b2sums=('5ff6f36cc007bb9c1a74933de6ca8dd85f8a52b523aee40a1c9a109f0e4769936f5ca3d15875126d481995fc95726c7faa0c35b84445e1bce9966f9e1c099a57')

pkgver() {
  cd adw-gtk3
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson adw-gtk3 build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
