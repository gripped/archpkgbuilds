# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Max Harmathy <harmathy@mailbox.org>
# Contributor: Dušan Simić <dusan.simic1810@gmail.com>

pkgname=adw-gtk-theme
pkgver=5.6
pkgrel=1
pkgdesc='Unofficial GTK 3 port of the libadwaita theme'
arch=(any)
url=https://github.com/lassekongo83/adw-gtk3
license=(LGPL-2.1-only)
makedepends=(
  git
  meson
  sassc
)
_tag=a88950d8d66308606a6cbb0cfece91376213ad2d
source=(git+https://github.com/lassekongo83/adw-gtk3.git#tag=${_tag})
b2sums=('20206c04d5b624ff5da083f8b533167bd9f10401ff7c45c8d92fe8bf6a3aac36d754918305c322f1855098dbe85fc742e19558c95c078c8922d25833cd887ddd')

pkgver() {
  cd adw-gtk3
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson adw-gtk3 build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
