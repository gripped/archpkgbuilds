# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Max Harmathy <harmathy@mailbox.org>
# Contributor: Dušan Simić <dusan.simic1810@gmail.com>

pkgname=adw-gtk-theme
pkgver=5.8
pkgrel=1
pkgdesc='Unofficial GTK 3 port of the libadwaita theme'
arch=(any)
url=https://github.com/lassekongo83/adw-gtk3
license=(LGPL-2.1-only)
makedepends=(
  git
  meson
  sassc
)
_tag=72b805550d6245f18021e1530f329ce81999445e
source=(git+https://github.com/lassekongo83/adw-gtk3.git#tag=${_tag})
b2sums=('c60c91919dadf1cc855cd97861d1d1947114ab09c7382c27f982bd4bd619c9d700fcd1f19d0aea4349e4ca6514f9c97e96ca3fe4e608a1196703733d47352c00')

pkgver() {
  cd adw-gtk3
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson adw-gtk3 build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
