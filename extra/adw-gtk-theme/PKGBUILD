# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Max Harmathy <harmathy@mailbox.org>
# Contributor: Dušan Simić <dusan.simic1810@gmail.com>

pkgname=adw-gtk-theme
pkgver=5.5
pkgrel=1
pkgdesc='Unofficial GTK 3 port of the libadwaita theme'
arch=(any)
url=https://github.com/lassekongo83/adw-gtk3
license=(LGPL-2.1-only)
makedepends=(
  git
  meson
  sassc
)
_tag=385a900628644dc81fa4942ac848a765229be93a
source=(git+https://github.com/lassekongo83/adw-gtk3.git#tag=${_tag})
b2sums=('df9cf7728ab939fe0831de47553f385692c07b8ca6b37a48f140d5b97b53e7fc36af9acb5deb29da2198397090c9b8152eb93ff456f12e442ef1e6965ebfdc99')

pkgver() {
  cd adw-gtk3
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson adw-gtk3 build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
