# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=pkgstats
pkgver=3.3.2
pkgrel=1
pkgdesc='Submit a list of installed packages to the Arch Linux project'
url='https://pkgstats.archlinux.de/'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('glibc' 'systemd' 'ca-certificates')
makedepends=('git' 'go' 'just' 'gcc')
source=("${pkgname}::git+https://github.com/archlinux-de/pkgstats-cli.git?signed#tag=${pkgver}")
sha256sums=('e70d855827e44b2cb3403b11b857450f196756cd4d24b5381f181fbdcb228076')
validpgpkeys=('3E80CA1A8B89F69CBA57D98A76A5EF9054449A5C')

prepare() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just prepare
}

check() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just test
}

build() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just build
}

package() {
	cd "${srcdir}/${pkgname}"

	just install "${pkgdir}"
}
