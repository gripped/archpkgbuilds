# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=pkgstats
pkgver=3.5.0
pkgrel=1
pkgdesc='Submit a list of installed packages to the Arch Linux project'
url='https://pkgstats.archlinux.de/'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('glibc' 'systemd' 'ca-certificates')
makedepends=('git' 'go' 'just' 'gcc')
source=("${pkgname}::git+https://github.com/archlinux-de/pkgstats-cli.git?signed#tag=${pkgver}")
sha256sums=('a2de12956a85b8afc1061c7f2d499017930f9244db1267032bee46c831da24c5')
validpgpkeys=('3E80CA1A8B89F69CBA57D98A76A5EF9054449A5C')

prepare() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just prepare
}

check() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just test
}

build() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just build
}

package() {
	cd "${srcdir}/${pkgname}"

	just install "${pkgdir}"
}
