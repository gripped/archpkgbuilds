# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=clpeak
pkgver=1.1.5
pkgrel=1
pkgdesc="A tool which profiles OpenCL devices to find their peak capacities"
arch=('x86_64')
url="https://github.com/krrishnarraj/clpeak"
license=("Unlicense")
depends=('ocl-icd' 'gcc-libs')
makedepends=('cmake' 'opencl-headers' 'ninja' 'git')
source=(git+https://github.com/krrishnarraj/clpeak#tag=$pkgver)
sha512sums=('2e6b62fb50a66d3ce1c393dc5fd171a9385c0013f9f78ac68e60716223f2ffbd94d1c179db2c4639b3c99ae9181f87475dc462b106ba938b1b75872b17e78cf2')

build() {
    cd "$pkgname"
    cmake \
        -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    ninja -C build
}

package() {
    cd "$pkgname"

    install -Dm644 README.md "$pkgdir/usr/share/$pkgname/README.md"
    cp -r results "$pkgdir"/usr/share/$pkgname/results
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
