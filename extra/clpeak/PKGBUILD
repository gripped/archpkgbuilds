# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=clpeak
pkgver=1.1.4
pkgrel=1
pkgdesc="A tool which profiles OpenCL devices to find their peak capacities"
arch=('x86_64')
url="https://github.com/krrishnarraj/clpeak"
license=("Unlicense")
depends=('ocl-icd' 'gcc-libs')
makedepends=('cmake' 'opencl-headers' 'ninja' 'git')
source=(git+https://github.com/krrishnarraj/clpeak#tag=$pkgver)
sha512sums=('0c52ebbaf868c4b16adacfdb9b5fcba8a2b3cfe8530c5afd6f654df509eac3985b9617795a4aca5b40937fe452764d6ccf8d89aef12a0be8ace63e26af65fe09')

build() {
    cd "$pkgname"
    cmake \
        -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    ninja -C build
}

package() {
    cd "$pkgname"

    install -Dm644 README.md "$pkgdir/usr/share/$pkgname/README.md"
    cp -r results "$pkgdir"/usr/share/$pkgname/results
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
