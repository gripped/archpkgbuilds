# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Anatoly Bashmakov <anatoly at posteo dot net>

pkgname=ruby-prawn-svg
_pkgname=${pkgname#ruby-}
pkgver=0.37.0
pkgrel=3
pkgdesc="SVG renderer for Prawn Ruby PDF library"
arch=(any)
url="https://github.com/mogest/prawn-svg"
license=(MIT)
depends=(
  ruby
  ruby-css_parser
  ruby-matrix
  ruby-prawn
  ruby-rexml
)
makedepends=(
  git
  rubygems
)
checkdepends=(
  ruby-bundler
  ruby-rspec
)
options=(!emptydirs)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('8c7d892db4f28d855ccc6c967cde9f581c5ab4d428133e221c736ac1b8b9f4f6')

prepare() {
  cd "$_pkgname"
  sed -i -E 's|~>|>=|g' "$_pkgname.gemspec"
  git rm Gemfile.lock
  sed -i '/rake/d' Gemfile
  sed -i '/rubocop/d' Gemfile
}

build() {
  cd "$_pkgname"
  local gemdir="$(gem env gemdir)"

  gem build "$_pkgname.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "$_pkgname-$pkgver.gem"

  rm --force --recursive --verbose \
    "tmp_install/$gemdir/cache/" \
    "tmp_install/$gemdir/gems/$_pkgname-$pkgver/vendor/" \
    "tmp_install/$gemdir/doc/$_pkgname-$pkgver/ri/ext/"

  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
    -iname "*.o" -o \
    -iname "*.c" -o \
    -iname "*.so" -o \
    -iname "*.time" -o \
    -iname "gem.build_complete" -o \
    -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/$gemdir/extensions/" \
    -type f \
    \( \
    -iname "mkmf.log" -o \
    -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "$_pkgname"
  GEM_HOME="tmp_install/$(gem env gemdir)" rspec
}

package() {
  cd "$_pkgname"
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
