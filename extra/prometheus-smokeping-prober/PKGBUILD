# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=prometheus-smokeping-prober
_pkgname=smokeping_prober
pkgver=0.10.0
pkgrel=1
pkgdesc="Prometheus exporter style smokeping"
arch=('x86_64')
url="https://github.com/SuperQ/smokeping_prober"
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=(!lto)
backup=(etc/prometheus/smokeping_prober.yml)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  'systemd.service'
)
sha512sums=('7c7a7dd913782ad8d1847bbc28ee3dfd2a9e9a2b1917a82bd11c0610d77a4e757f9361d50b9b2b3ee09b900e6ef21df1cd1dce33c28df3c3f2fe81a5cbf4cd78'
            '7afca18a0426dbba0dce9ea1315d683a7f6b746efd4222ad687bd7a2c54f8ef1b1308159b8892fd18f4ee8a48b3e2be3800556dea603a14ee4a21df07fb5df2b')

prepare() {
  cd "$_pkgname-$pkgver"

  # create folder for build output
  rm -Rf build
  mkdir build

  # download dependencies
  go mod download
}

build() {
  cd "$_pkgname-$pkgver"
  go build -v \
    -buildmode=pie \
    -trimpath \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o build .
}

check() {
  cd "$_pkgname-$pkgver"
  go test ./...
}

package() {
  # systemd integration
  install -Dm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  # example config
  install -Dm644 "$_pkgname-$pkgver/smokeping_prober.yml" "$pkgdir/etc/prometheus/smokeping_prober.yml"
  install -Dm644 "$_pkgname-$pkgver/example-rules.yml" "$pkgdir/usr/share/doc/$pkgname/example-rules.yml"
  install -Dm644 "$_pkgname-$pkgver/dashboard.json" "$pkgdir/usr/share/doc/$pkgname/dashboard.json"

  # docs
  install -Dm644 "$_pkgname-$pkgver/README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 "$_pkgname-$pkgver/CHANGELOG.md" "$pkgdir/usr/share/doc/$pkgname/CHANGELOG.md"

  # license
  install -Dm644 "$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # binary
  install -Dm755 "$_pkgname-$pkgver/build/$_pkgname" "$pkgdir/usr/bin/$pkgname"
}

