# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-debstatus
pkgver=0.6.3
pkgrel=1
pkgdesc="cargo-tree for Debian packaging"
url='https://github.com/kpcyrd/cargo-debstatus'
arch=('x86_64')
license=('(MIT OR Apache-2.0) AND GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ddba5e4d253a52009a3309ad0e214a7e9ef319cd42896679904c0fd6d74da118')
b2sums=('172dd4369b4475211490d27a5088088415fd64837d5a719354a198c4a64f28e2fad93292eeb8d084ad855d5235ef952aa1e5b78e0693c90e17733f684a8873f4')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    "target/release/${pkgname}"

  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
