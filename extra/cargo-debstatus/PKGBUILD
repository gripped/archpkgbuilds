# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-debstatus
pkgver=0.6.5
pkgrel=1
pkgdesc="cargo-tree for Debian packaging"
url='https://github.com/kpcyrd/cargo-debstatus'
arch=('x86_64')
license=('(MIT OR Apache-2.0) AND GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('38dd43e2ef321ea4d50882f8f482dde0f34602ee13162e9dd6c53a5412b6d9c2')
b2sums=('f3982908367a2212e7b68e11cb4cf2d6e82b407250aea627cb486f2106756409f5a6363b485a550b69ac717d6788b173dfb1234d69e76ba0119ef6360da820d8')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    "target/release/${pkgname}"

  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
