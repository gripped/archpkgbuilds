# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-debstatus
pkgver=0.6.1
pkgrel=1
pkgdesc="cargo-tree for Debian packaging"
url='https://github.com/kpcyrd/cargo-debstatus'
arch=('x86_64')
license=('(MIT OR Apache-2.0) AND GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('30dbffd2becc5ad1ee0976c7339df76f6953980008a6f4f87f2a58315a783668')
b2sums=('20c61c7decf9ddfd6d74ed5f37574a7ea4a1f55267b7e7673ae37302a3280620953417da5e61761e8f7fc451e3c1113a22c20477b57362ddb5bc8af59beb96ba')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    "target/release/${pkgname}"

  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
