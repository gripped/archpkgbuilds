# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=release-plz
pkgver=0.3.120
pkgrel=1
pkgdesc="Release Rust packages without using the command line"
arch=('x86_64')
url="https://github.com/MarcoIeni/release-plz"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'curl' 'libgit2' 'openssl')
makedepends=('cargo' 'git')
optdepends=('cargo-semver-checks: check for API breaking changes')
source=("$pkgname-$pkgver::git+$url.git#tag=$pkgname-v$pkgver" 'libgit2.pc')
sha512sums=('627b81daf19348aa55a55f413fccde6101f64a5f08c5719589b9ef5b48c49ad7cc4f302cda0ff76c103e1af6c2d7194d065370f29cf2c89aee1ec73eec73a31b'
            '95bf0ae63789e62eec0a52ec5d0f4e0b4e8040b6e2f9a22d161bc0c079c3bc70bba3a7b1e9a72554c94123c1c7e6dfb053ba866538b66236f7835d182820fec8')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  mkdir completions
}

_force_libgit19() {
  # Force compilation against libgit2 1.9
  export PKG_CONFIG_PATH="$srcdir"
}

build() {
  cd "$pkgname-$pkgver"
  _force_libgit19
  export LIBGIT2_NO_VENDOR=1
  cargo build --release --frozen --no-default-features
  local compgen="target/release/$pkgname generate-completions"
  $compgen bash >"completions/$pkgname"
  $compgen fish >"completions/$pkgname.fish"
  $compgen zsh >"completions/_$pkgname"
}

check() {
  cd "$pkgname-$pkgver"
  _force_libgit19
  export LIBGIT2_NO_VENDOR=1
  cargo test --frozen --no-default-features -- --skip "next_ver"
}

package() {
  cd "$pkgname-$pkgver"
  depends+=('libgit2.so')
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "completions/$pkgname" -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm 644 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions/"
}

# vim: ts=2 sw=2 et:
