# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

# NOTE: python-cuda-pathfinder shares the git repository with python-cuda,
# but it has different versioning scheme and release frequency
pkgname=python-cuda-pathfinder
epoch=1
pkgver=1.3.3
pkgrel=1
pkgdesc="Utilities for locating CUDA components"
arch=(any)
url="https://nvidia.github.io/cuda-python/cuda-pathfinder/latest/index.html"
license=(Apache-2.0)
depends=(
  python
)
makedepends=(
  cuda
  git
  python-wheel
  python-build
  python-installer
  python-setuptools-scm
)
checkdepends=(
  python-pytest
)
source=(
  python-cuda::git+https://github.com/NVIDIA/cuda-python#tag=cuda-pathfinder-v$pkgver
)
b2sums=('89ec9c5c81b665efc206965e2fbe818a88b446d7ec56cd7079ca4c8d16823d49e1e1ccfd38c10e518063177241fc94e1680d16c3c889479e6ffb625485d791f9')

build() {
  cd python-cuda/cuda_pathfinder

  python -m build --wheel --no-isolation
}

check() {
  cd python-cuda/cuda_pathfinder
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests
}

package() {
  cd python-cuda/cuda_pathfinder
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
