# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

# NOTE: python-cuda-pathfinder shares the git repository with python-cuda,
# but it has different versioning scheme and release frequency
pkgname=python-cuda-pathfinder
epoch=1
pkgver=1.3.5
pkgrel=1
pkgdesc="Utilities for locating CUDA components"
arch=(any)
url="https://nvidia.github.io/cuda-python/cuda-pathfinder/latest/index.html"
license=(Apache-2.0)
depends=(
  python
)
makedepends=(
  cuda
  git
  python-wheel
  python-build
  python-installer
  python-setuptools-scm
)
checkdepends=(
  python-pytest
  python-pytest-mock
)
source=(
  python-cuda::git+https://github.com/NVIDIA/cuda-python#tag=cuda-pathfinder-v$pkgver
)
b2sums=('084a17d5a72a8cc4c4378f77dfbb56a29912ff3317a9d0f89344e222fafc13d4e5fd342e1d89caf02bcbe9d231b9987ad191df997536de0d5e190a027e0618ce')

build() {
  cd python-cuda/cuda_pathfinder

  python -m build --wheel --no-isolation
}

check() {
  cd python-cuda/cuda_pathfinder
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests
}

package() {
  cd python-cuda/cuda_pathfinder
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
