# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

# NOTE: python-cuda-pathfinder shares the git repository with python-cuda,
# but it has different versioning scheme and release frequency
pkgname=python-cuda-pathfinder
epoch=1
pkgver=1.3.4
pkgrel=1
pkgdesc="Utilities for locating CUDA components"
arch=(any)
url="https://nvidia.github.io/cuda-python/cuda-pathfinder/latest/index.html"
license=(Apache-2.0)
depends=(
  python
)
makedepends=(
  cuda
  git
  python-wheel
  python-build
  python-installer
  python-setuptools-scm
)
checkdepends=(
  python-pytest
  python-pytest-mock
)
source=(
  python-cuda::git+https://github.com/NVIDIA/cuda-python#tag=cuda-pathfinder-v$pkgver
)
b2sums=('2edf5effc781b2ae1187b28860f32f66adc89d9b84d6c401191acab6cbbdfd2660f17dbc2122af03953bf64fda3a071a75cafa3f78dfb4dd7b18d8140a41573e')

build() {
  cd python-cuda/cuda_pathfinder

  python -m build --wheel --no-isolation
}

check() {
  cd python-cuda/cuda_pathfinder
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests
}

package() {
  cd python-cuda/cuda_pathfinder
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
