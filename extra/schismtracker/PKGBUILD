# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tercio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>
# Contributor: Brandon Mulcahy <brandon@jangler.info>

pkgname=schismtracker
pkgver=20251014
pkgrel=1
pkgdesc='An old-school sample-based music composition tool'
arch=('x86_64')
url='https://schismtracker.org'
license=('GPL-2.0-only')
depends=(
  'flac'
  'libx11'
  'libxv'
  'libutf8proc'
  'sdl2'
  'hicolor-icon-theme'
)
makedepends=('git' 'python' 'alsa-lib' 'jack2')
source=("$pkgname::git+https://github.com/schismtracker/schismtracker#tag=$pkgver")
sha512sums=('22d7fa5c55eac8af6b51140c8c7ad7abbe534b5c3f9e87b55bf66ebd998e1256faa95fe690819ac804df7f3162ced04f60eedb85e28f1c57534e61f844f524fe')
b2sums=('f72fe41908416e54cbd35ae20b81050589fe0c91e5cbdcc242efd5f2c2f0455edadc788945caba2564151c681222b79ecff3ff44b9e49e4d4aa86aaa8710b28e')

prepare() {
  cd "$pkgname"

  # modify desktop file & application icon
  sed -e 's/schism.desktop/schismtracker.desktop/' -i Makefile.am
  sed -e 's/schism-icon-128/schismtracker/' -i sys/fd.org/schism.desktop
  mv sys/fd.org/{schism,schismtracker}.desktop
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # delete old application icon
  rm -rf "$pkgdir/usr/share/pixmaps"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/configuration.md README.md

  # application icons
  for i in 16 22 24 32 36 48 64 72 96 128 192; do
    install -vDm644 "icons/schism-icon-${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png" 
  done
  install -vDm644 icons/schism-icon.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
}
