# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tercio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>
# Contributor: Brandon Mulcahy <brandon@jangler.info>

pkgname=schismtracker
pkgver=20250825
pkgrel=1
pkgdesc='An old-school sample-based music composition tool'
arch=('x86_64')
url='https://schismtracker.org'
license=('GPL-2.0-only')
depends=(
  'flac'
  'libx11'
  'libxv'
  'libutf8proc'
  'sdl2'
  'hicolor-icon-theme'
)
makedepends=('git' 'python' 'alsa-lib' 'jack2')
source=("$pkgname::git+https://github.com/schismtracker/schismtracker#tag=$pkgver")
sha512sums=('6115b5678b389df95d19d7541a5e37bc8f01d7d9a166403a302c1ed2a16f75aace3616026b9925012fd202f5f877bb669e62927cf048e3b3e6eb6dd8c3c37edb')
b2sums=('9ce23349b2fe0d71935611bb063a54ec2885703f71f13d2343ee0603502aa2089664d832d792f6e370a5733d6e498e41366ab37fc423cecb2a8117538b0bee0b')

prepare() {
  cd "$pkgname"

  # modify desktop file & application icon
  sed -e 's/schism.desktop/schismtracker.desktop/' -i Makefile.am
  sed -e 's/schism-icon-128/schismtracker/' -i sys/fd.org/schism.desktop
  mv sys/fd.org/{schism,schismtracker}.desktop
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # delete old application icon
  rm -rf "$pkgdir/usr/share/pixmaps"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/configuration.md README.md

  # application icons
  for i in 16 22 24 32 36 48 64 72 96 128 192; do
    install -vDm644 "icons/schism-icon-${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png" 
  done
  install -vDm644 icons/schism-icon.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
}
