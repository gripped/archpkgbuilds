# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tercio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>
# Contributor: Brandon Mulcahy <brandon@jangler.info>

pkgname=schismtracker
pkgver=20241021
pkgrel=1
pkgdesc='An old-school sample-based music composition tool'
arch=('x86_64')
url='https://schismtracker.org'
license=('GPL-2.0-only')
depends=(
  'flac'
  'libx11'
  'libxv'
  'libutf8proc'
  'sdl2'
  'hicolor-icon-theme'
)
makedepends=('git' 'python')
source=("$pkgname::git+https://github.com/schismtracker/schismtracker#tag=$pkgver")
sha512sums=('2aa9ebc30eb2af04a65970642eab6e94a4d130e22b96bd3b2ac21e201dec4b6c425b978ad9e8a055757b8834f25689b3c2683867d767140159ce33c22642edec')
b2sums=('3c007916a5bcbd0fec207150be9780b5bef34c6c8cbbe6922dbc2d0b0fa805c40d32fd98af3cc44f5344589fb2aaf3f2726afdb7d28b1933affaebd1cd660f3c')

prepare() {
  cd "$pkgname"

  # modify desktop file & application icon
  sed -e 's/schism.desktop/schismtracker.desktop/' -i Makefile.am
  sed -e 's/schism-icon-128/schismtracker/' -i sys/fd.org/schism.desktop
  mv sys/fd.org/{schism,schismtracker}.desktop
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # delete old application icon
  rm -rf "$pkgdir/usr/share/pixmaps"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/configuration.md README.md

  # application icons
  for i in 16 22 24 32 36 48 64 72 96 128 192; do
    install -vDm644 "icons/schism-icon-${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png" 
  done
  install -vDm644 icons/schism-icon.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
}
