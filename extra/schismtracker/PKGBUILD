# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tercio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>
# Contributor: Brandon Mulcahy <brandon@jangler.info>

pkgname=schismtracker
pkgver=20240609
pkgrel=1
pkgdesc='An old-school sample-based music composition tool'
arch=('x86_64')
url='https://schismtracker.org'
license=('GPL-2.0-only')
depends=(
  'libx11'
  'libxv'
  'sdl2'
  'hicolor-icon-theme'
)
makedepends=('git' 'python')
source=("$pkgname::git+https://github.com/schismtracker/schismtracker#tag=$pkgver")
sha512sums=('6d53f257dff6120ca4fb021dbd3f4379cba69e3ef139b2247456d049dce4074b00b13e8b0ad4a0f0755203501b81d8bf95f6fe41ce8b1f13f153e3011a2cd2a1')
b2sums=('81477f589eef7457ac2cb73845e25e334bef1bb6ff095f89497d36dfb1755b0cde2bba74ea85389c02c64ca80346e88b97d727a68a01722f37559d020344409f')

prepare() {
  cd "$pkgname"

  # modify desktop file & application icon
  sed -e 's/schism.desktop/schismtracker.desktop/' -i Makefile.am
  sed -e 's/schism-icon-128/schismtracker/' -i sys/fd.org/schism.desktop
  mv sys/fd.org/{schism,schismtracker}.desktop
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # delete old application icon
  rm -rf "$pkgdir/usr/share/pixmaps"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/configuration.md README.md

  # application icons
  for i in 16 22 24 32 36 48 64 72 96 128 192; do
    install -vDm644 "icons/schism-icon-${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png" 
  done
  install -vDm644 icons/schism-icon.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
}
