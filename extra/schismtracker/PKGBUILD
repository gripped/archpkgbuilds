# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tercio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>
# Contributor: Brandon Mulcahy <brandon@jangler.info>

pkgname=schismtracker
pkgver=20250208
pkgrel=2
pkgdesc='An old-school sample-based music composition tool'
arch=('x86_64')
url='https://schismtracker.org'
license=('GPL-2.0-only')
depends=(
  'flac'
  'libx11'
  'libxv'
  'libutf8proc'
  'sdl2'
  'hicolor-icon-theme'
)
makedepends=('git' 'python' 'alsa-lib' 'jack2')
source=("$pkgname::git+https://github.com/schismtracker/schismtracker#tag=$pkgver")
sha512sums=('ee78f11fbd7d66dfa4218d2bbc1052c4ab6feb68a420fcd01b05e511818bb4ff0aec68b851d832238614903b3b599fb706a7b79ac13240161a3dd1d9455f0834')
b2sums=('cf560cb115ddb2d40f14e652a9abc00844f869a919ba73ca4bbdb9995da3f07724ce6e851f6c19f79e4dbdf0df58744367d88609865780af91b1202321db0b62')

prepare() {
  cd "$pkgname"

  # modify desktop file & application icon
  sed -e 's/schism.desktop/schismtracker.desktop/' -i Makefile.am
  sed -e 's/schism-icon-128/schismtracker/' -i sys/fd.org/schism.desktop
  mv sys/fd.org/{schism,schismtracker}.desktop
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # delete old application icon
  rm -rf "$pkgdir/usr/share/pixmaps"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/configuration.md README.md

  # application icons
  for i in 16 22 24 32 36 48 64 72 96 128 192; do
    install -vDm644 "icons/schism-icon-${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png" 
  done
  install -vDm644 icons/schism-icon.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
}
