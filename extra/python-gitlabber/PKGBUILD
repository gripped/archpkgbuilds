# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=python-gitlabber
pkgver=2.1.0
pkgrel=2
pkgdesc='Clones or pulls entire groups tree from GitLab'
arch=('any')
license=('MIT')
url='https://github.com/ezbz/gitlabber'
depends=(
  'python-setuptools'
  # https://github.com/ezbz/gitlabber/blob/master/requirements.txt
  'python-anytree'
  'python-gitpython'
  'python-gitlab'
  'python-globre'
  'python-pyyaml'
  'python-rich'
  'python-typer'
  'python-pydantic'
  'python-pydantic-settings'
)
optdepends=(
  'python-keyring: Store GitLab tokens securely using your OS keyring'
)
makedepends=(
  'git'
)
checkdepends=(
  'python-pytest-integration'
)
source=("$pkgname::git+${url}#tag=v${pkgver}")
sha256sums=('7c996119c734bf554c376a4bb2c5dbb4de7b941b3cbd93fef7418e415463cffd')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$pkgname"

  sed -i '/optional arguments/d' tests/test_integration.py
}

build() {
  cd "$pkgname"

  python setup.py build
}

check() {
  cd "$pkgname"

  python -m pytest
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
  install -Dm644 README.rst "$pkgdir/usr/share/doc/$pkgname/README.rst"
}
