# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=python-gitlabber
pkgver=1.2.8
_commit=1ee3667638e65374e9ae6404019a37d0de2f08a2
pkgrel=1
pkgdesc='Clones or pulls entire groups tree from GitLab'
arch=('any')
license=('MIT')
url='https://github.com/ezbz/gitlabber'
depends=(
  'python-setuptools'
  # https://github.com/ezbz/gitlabber/blob/master/requirements.txt
  'python-anytree'
  'python-gitpython'
  'python-gitlab'
  'python-globre'
  'python-pyyaml'
  'python-tqdm'
  'python-docopt'
)
makedepends=(
  'git'
)
checkdepends=(
  'python-pytest-integration'
)
source=("$pkgname::git+${url}#commit=$_commit")
sha256sums=('7d877b57f1ab2ee014e06378bc95ef7332e91fd585086d5a135d1f3f3ad1217c')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$pkgname"

  sed -i '/optional arguments/d' tests/test_integration.py
}

build() {
  cd "$pkgname"

  python setup.py build
}

check() {
  cd "$pkgname"

  python -m pytest
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
  install -Dm644 README.rst "$pkgdir/usr/share/doc/$pkgname/README.rst"
}
