# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>

pkgname=qxlsx
pkgver=1.5.0
pkgrel=2
pkgdesc='Excel file reader/writer library using Qt'
arch=(x86_64)
url='https://github.com/QtExcel/QXlsx'
license=(MIT)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git)
conflicts=(qxlsx-qt6)
replaces=(qxlsx-qt6)
source=(git+https://github.com/QtExcel/QXlsx#tag=v$pkgver)
sha256sums=('5edc0f44c112946213db51954dacb0a1e60c3a11299001156a6703d7d61a5232')

build() {
  cmake -B build -S QXlsx/QXlsx \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 QXlsx/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname 
}
