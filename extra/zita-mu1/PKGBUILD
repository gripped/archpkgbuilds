# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=zita-mu1
pkgver=0.3.3
pkgrel=5
pkgdesc="A simple JACK app used to organise stereo monitoring"
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio/zita-mu1-doc/quickguide.html"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  cairo
  clxclient
  clthreads
  gcc-libs
  glibc
  jack
  libpng
  libx11
)
makedepends=(gendesk)
source=(https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2)
sha512sums=('1e01a92840e12854ffeb33e713a6257bc60ce140f0e7786a06dee73bd2cd8ce7f4904327ea107456a4c64eb8bf636a863ff12d0505512d21471ff08eb9d3f12c')
b2sums=('4ef52d84f8327b7b4f802692ce6b5ea65e051f403aaf111e29525d3e7dc17dc0dd75ada452a6403754cb82a7309c2876366ba1305e5bdb4adf852fc8898b0615')

prepare() {
  # remove optimization
  # fix binary install
  sed -e '/native/d' \
      -i $pkgname-$pkgver/source/Makefile
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name zita-mu1 \
          --categories "AudioVideo;Audio"
}

build() {
  make PREFIX=/usr -C $pkgname-$pkgver/source
}

package() {
  make PREFIX=/usr DESTDIR="$pkgdir" install -C $pkgname-$pkgver/source
  # docs
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
  # XDG desktop integration
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # html
  install -vDm 644 $pkgname-$pkgver/doc/html/* -t "$pkgdir/usr/share/doc/$pkgname/html/"
}
# vim:set ts=2 sw=2 et:
