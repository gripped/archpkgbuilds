# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=4.11.2
pkgrel=2
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs' 'libharu' 'graphicsmagick' 'pango' 'zlib' 'libgl' 'harfbuzz'
         'qt5-base')
makedepends=('boost' 'cmake' 'postgresql-libs' 'fcgi' 'sqlite' 'mysql++' 'doxygen'
             'graphviz' 'mesa' 'glu' 'ninja')
optdepends=('openssl: for SSL support in built-in webserver'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
        'mariadb-libs: for MySQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'mysql++: for the hangman example')
backup=('etc/wt/wt_config.xml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kdeforche/wt/archive/${pkgver}.tar.gz"
         https://github.com/emweb/wt/commit/f39d3a00e0b3f9bdda60b061c80001420f6e2703.patch)
sha512sums=('e512c88616878b8fa3b992aa9d2f232d994ee6108607af4e5d511a2fecd7ae35cf33a63435a0ff356df715b386f2061731dcfb28c529bc43c901f17787686f34'
            'ba3427b2f415868d555a7db6a5fc384de898be332c562f19c2275b80261d66ed272cfd0d649af1678f15c4de09bc80a1440a4b71c4e5720e3e7ab51dab1468f9')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../f39d3a00e0b3f9bdda60b061c80001420f6e2703.patch
}

build() {
  cd ${pkgname}-${pkgver}

  cmake \
      -Bbuild \
      -GNinja \
      -DCONNECTOR_HTTP=ON \
      -DWT_WRASTERIMAGE_IMPLEMENTATION=GraphicsMagick \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      -DBUILD_EXAMPLES=ON
  ninja -C build
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
}
