# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgbase=clapper
pkgname=(
  clapper
  clapper-docs
  libclapper-gtk
  libclapper
)
pkgver=0.8.0
pkgrel=1
pkgdesc="Modern and user-friendly media player"
arch=(x86_64)
url="https://rafostar.github.io/clapper"
license=('LGPL-2.1-or-later AND GPL-3.0-or-later')
makedepends=(
  # used as depends split-depends
  dconf
  glib2
  glibc
  graphene
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libmicrodns
  libpeas-2
  libsoup3
  pango

  # make-only
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+https://github.com/Rafostar/clapper.git?signed#tag=$pkgver")
b2sums=('805ffa7c4ba6b8422b7261c97fee45e129d561765997fa335b35afc9ed29a862b81a8bc7bbd9e745b17b16a311813145179d4caad9e525b6541d1faea4e0c5df')
validpgpkeys=(
  'D3B89B4D49ABFABA6A3EF1B9228FF576F6E2EC17'    # Rafał Dzięgiel <rafostar.github@gmail.com>
)

build() {
  local meson_options=(
    # Build
    -D clapper=enabled
    -D clapper-gtk=enabled
    -D clapper-app=enabled
    -D gst-plugin=enabled
    -D introspection=enabled
    -D vapi=enabled
    -D doc=true

    # Features
    -D enhancers-loader=enabled
    -D discoverer=enabled
    -D mpris=enabled
    -D server=enabled

    # GStreamer plugin options
    -D glimporter=enabled
    -D gluploader=enabled
    -D rawimporter=enabled
  )

  arch-meson clapper build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_clapper() {
  license=('GPL-3.0-or-later')
  depends=(
    dconf
    glib2
    glibc
    graphene
    gstreamer
    gtk4
    hicolor-icon-theme
    libadwaita
    libclapper-0.0.so
    libclapper-gtk-0.0.so
    pango
  )

  meson install -C build --destdir "$pkgdir"

  _pick docs \
    "$pkgdir"/usr/share/doc/*

  _pick libclapper-gtk \
    $pkgdir/usr/include/clapper-0.0/clapper-gtk/ \
    $pkgdir/usr/lib/clapper-0.0/ \
    $pkgdir/usr/lib/girepository-1.0/ClapperGtk-0.0.typelib \
    $pkgdir/usr/lib/gstreamer-1.0/ \
    $pkgdir/usr/lib/libclapper-gtk* \
    $pkgdir/usr/lib/libgstclapperglcontexthandler.so* \
    $pkgdir/usr/lib/pkgconfig/clapper-gtk-0.0.pc \
    $pkgdir/usr/share/gir-1.0/ClapperGtk-0.0.gir \
    $pkgdir/usr/share/locale/*/LC_MESSAGES/clapper-gtk.mo \
    $pkgdir/usr/share/vala/vapi/clapper-gtk-*

  _pick libclapper \
    $pkgdir/usr/include/clapper-0.0/clapper/ \
    $pkgdir/usr/lib/ \
    $pkgdir/usr/share/gir-1.0/Clapper-0.0.gir \
    $pkgdir/usr/share/vala/vapi/clapper-*
}

package_clapper-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv docs/* "$pkgdir"
}

package_libclapper-gtk() {
  pkgdesc="Clapper GTK integration library"
  license=('LGPL-2.1-or-later')
  depends=(
    glib2
    glibc
    gst-plugins-base-libs
    graphene
    gstreamer
    gtk4
    libclapper-0.0.so
    libmicrodns
    libsoup3
  )
  provides=(libclapper-gtk-0.0.so)
  mv libclapper-gtk/* $pkgdir/
}

package_libclapper() {
  pkgdesc="Clapper playback library"
  license=('LGPL-2.1-or-later')
  depends=(
    glib2
    glibc
    gst-plugins-base
    gst-plugins-base-libs
    gst-plugins-good
    gstreamer
    libmicrodns
    libpeas-2
    libsoup3
  )
  optdepends=(
    "gst-libav: Extra media decoders (via FFmpeg)"
    "gst-plugins-bad: Extra media decoders"
    "gst-plugins-ugly: Extra media decoders"
    "gst-plugin-va: Hardware-accelerated video decode (via VA-API)"
  )
  provides=(libclapper-0.0.so)
  mv libclapper/* $pkgdir/
}
