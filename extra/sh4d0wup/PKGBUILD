# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=sh4d0wup
pkgver=0.10.0
pkgrel=1
pkgdesc="Signing-key abuse and update exploitation framework"
url='https://github.com/kpcyrd/sh4d0wup'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'bzip2' 'libbz2.so'
  'gcc'
  'gcc-libs'
  'glibc'
  'openssl' 'libssl.so'
  'pcsclite' 'libpcsclite.so'
  'podman'
  'shared-mime-info'
  'xz' 'liblzma.so'
  'zstd' 'libzstd.so'
)
checkdepends=('sequoia-sq')
makedepends=(
  'cargo'
  'clang'
)
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('63662f386302ceb06470cab62a36924a1a2efcb51602913f8cf6e2a5a2a34acc')
b2sums=('7109e4523a61478a7f51e28f1a850071c3c9b966e47a5ef4e59c9e62b5267ae255b1d6be16a1d7ba9f965795d181297c7779a375f7f24c0592586298f7a1539d')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/sh4d0wup

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
