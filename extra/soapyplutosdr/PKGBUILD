# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=soapyplutosdr
_pkgname=SoapyPlutoSDR
pkgver=0.2.2
_gitver=soapy-plutosdr-$pkgver
pkgrel=1
pkgdesc='Soapy SDR plugin for PlutoSDR'
arch=('x86_64')
url='https://github.com/pothosware/SoapyPlutoSDR'
license=('LGPL-2.1-only')
depends=(
  'gcc-libs'
  'glibc'
  'libad9361'
  'libiio.so'
  'libusb'
  'soapysdr'
)
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz")
sha512sums=('c500346566c2215a65d2ce37f77d3e3317ca1ab8cd59001e7f51b0f3a1bcc703acf634034a55a077ee798376f14b2d96e229d073a1975ff4944314498e537f72')

build() {
  cd $_pkgname-$_gitver
  cmake -S . -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  cmake --build build
}

package() {
  cd $_pkgname-$_gitver
  DESTDIR="$pkgdir" cmake --install build
}

