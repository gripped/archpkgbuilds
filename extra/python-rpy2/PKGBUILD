# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>
# Contributor: David Scholl <djscholl@gmail.com> 
# Contributor: David Pretty <david.pretty@gmail.com> 

pkgname=python-rpy2
pkgver=3.5.17
pkgrel=2
pkgdesc='A very simple, yet robust, Python interface to the R Programming Language'
arch=(x86_64)
url='https://rpy2.github.io/'
license=(MPL GPL LGPL)
depends=(python
         python-cffi
         python-jinja
         python-numpy
         python-pytz
         python-tzlocal
         r)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
optdepends=('python-pandas: conversion between rpy and pandas')
source=(git+https://github.com/rpy2/rpy2#tag=RELEASE_${pkgver//./_})
sha256sums=('7945658af4478d744b5d8e4afa60ef7f791fe16055070e5604cebec7f4c80c2c')

prepare() {
  cd rpy2
  git cherry-pick -n c31ec06cb913ae2bebf46753a2751ca1a1b5660b # Fix tests with numpy 2.0
}

build() {
  cd rpy2
  python -m build --wheel --no-isolation
}

check() {
  cd rpy2
  PYTHONPATH="$PWD"/`echo build/lib.*` \
  pytest -v -k 'not test_POSIXct_datetime_from_timestamp' # Skip test that fails with tzlocal 5.x
}

package() {
  cd rpy2

  python -m installer --destdir="$pkgdir" dist/*.whl
}
