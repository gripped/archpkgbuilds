# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pysequoia
pkgname=python-pysequoia
pkgver=0.1.27
pkgrel=1
pkgdesc="Provides OpenPGP facilities in Python through the Sequoia PGP library"
arch=(x86_64)
url="https://github.com/wiktor-k/pysequoia"
license=(Apache-2.0)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  clang
  python-build
  python-installer
  python-maturin
  python-wheel
)
source=($_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('8ed636715c6e0b155a06d1391b38299bd870f5a213846f44ee97a014fdd8316d3f224cb49689c1d73a1e8c406d3435d116b15a2ab5cb1fbb09a2348855cd9324')
b2sums=('ec40516a42ad090d8f4f18b4e1ad13e9a22913e9667f86f29083ae3a5be3a055acf3cca2d655c6ceb0c0065f7acaad83e2c88ef06441c1914cb19b858f436deb')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
