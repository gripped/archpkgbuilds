# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pysequoia
pkgname=python-pysequoia
pkgver=0.1.29
pkgrel=2
pkgdesc="Provides OpenPGP facilities in Python through the Sequoia PGP library"
arch=(x86_64)
url="https://github.com/wiktor-k/pysequoia"
license=(Apache-2.0)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  clang
  python-build
  python-installer
  python-maturin
  python-wheel
)
source=($_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('b306d5fe66501876566c135d49ed137456e68cff382b020330f70a4958ac2f038624ecaf6fffc60cbf689f354a4145584632a2bde4e1633b346a305048c56506')
b2sums=('c2dec80a1be72d6d540666f03b9c664351114299de9fc8a7e5c2b5170c7bddbf45e85706d973a5fb88ae622c67d27465ce9bffcfa63f91ad55ce3b4dd16181ce')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
