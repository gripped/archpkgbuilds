# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pysequoia
pkgname=python-pysequoia
pkgver=0.1.25
pkgrel=1
pkgdesc="Provides OpenPGP facilities in Python through the Sequoia PGP library"
arch=(x86_64)
url="https://github.com/wiktor-k/pysequoia"
license=(Apache-2.0)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  clang
  python-build
  python-installer
  python-maturin
  python-wheel
)
source=($_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('da9c1b286a1a2864e47641f2d60dc0ec7293ac1a455ae88f8e58b523ff0c110c22fd1d435c0168afd909df8a5b7b47b2c9f99394a8fa104b2efefc6f1bf8a852')
b2sums=('5a10d8677f900d42a04e448bc8ea6b63b85c86044bb2c21d5d7d18a9db440db5337de14519a102c9d36f0483b310e2678769af9bcc8d3bda615b720173eaeaa4')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
