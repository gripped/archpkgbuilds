# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pysequoia
pkgname=python-pysequoia
pkgver=0.1.23
pkgrel=1
pkgdesc="Provides OpenPGP facilities in Python through the Sequoia PGP library"
arch=(x86_64)
url="https://github.com/wiktor-k/pysequoia"
license=(Apache-2.0)
groups=(sequoia)
depends=(
  bzip2
  gcc-libs
  glibc
  gmp
  nettle
  pcsclite
  python
)
makedepends=(
  clang
  python-build
  python-installer
  python-maturin
  python-wheel
)
source=($_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('c45fa4a05b242a8703d4fcd2e5f8e3148f2bd7a40141bc40441079a9b7a2a868bde44dddcf72af5f01a2fb54f6561b91729d54ff5dd6ed633c4357330819d324')
b2sums=('56d3054574d2e3fef62991aa06fc9590a2b1a11898514fc7a73f954955e4e20e0566899b0075754ac4365a8943e292545a85d120f2f04c3238ba4bea955c5ac2')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgnam/"
}
