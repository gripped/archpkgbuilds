# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pysequoia
pkgname=python-pysequoia
pkgver=0.1.26
pkgrel=1
pkgdesc="Provides OpenPGP facilities in Python through the Sequoia PGP library"
arch=(x86_64)
url="https://github.com/wiktor-k/pysequoia"
license=(Apache-2.0)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  clang
  python-build
  python-installer
  python-maturin
  python-wheel
)
source=($_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('f3e1b62df1417d031735c350e632ae0eadb88fe22aba0480f6e33f9879d17a85bb20a6bf704466be7e4f347aba6aa5b1d378fef6b01fd0c664f65c61be9ad29d')
b2sums=('49a812a6849524b82e3180ca19cb7dcfff34478a485847744311a0282b83931b75d772d0a8010e80ee5e23bd9bc61008d044a8a71b35dddb637bb7874d42f66c')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
