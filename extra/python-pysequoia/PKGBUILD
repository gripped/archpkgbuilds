# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pysequoia
pkgname=python-pysequoia
pkgver=0.1.28
pkgrel=1
pkgdesc="Provides OpenPGP facilities in Python through the Sequoia PGP library"
arch=(x86_64)
url="https://github.com/wiktor-k/pysequoia"
license=(Apache-2.0)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  clang
  python-build
  python-installer
  python-maturin
  python-wheel
)
source=($_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('bd4f8920118c68da923ff19100f0c1eb3693d433d2f68edd49a9a8f99a2be0584d4a8c0c8a442f2d0b1de14a528033414eedf040d44d4fcf9f2b344f82c1edd3')
b2sums=('669c8d79a6d2dfd675889f9ac22cd8aefa98e9e6e0249c7d75eb40de83ba6b5f4e75b52881b48e0dcd996e4206901e322ae7ca041a0f7ff33df68a21106bd846')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
