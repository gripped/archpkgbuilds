# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-accounts
pkgver=4.0.1
pkgrel=1
pkgdesc='MauiKit utilities to handle User Accounts'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         mauikit
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3165b3978dda06f0b978faae7d5eafd7df1abd26e5d124c81b7555aead03234e'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
