# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.11.1
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
depends=('qt6-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11'
         'qt6-declarative' 'qt6-svg' 'zstd')
makedepends=('mesa' 'qt6-tools' 'python' 'qbs')
optdepends=('python: Python plugin'
            'qt6-imageformats: Support for additional image formats (including WebP)')
source=($pkgname-$pkgver.tar.gz::https://github.com/mapeditor/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('b5498cfa5396f02e997f4ad02ded205e446fcc799fd3314dcc93c68ecdc6bb5efd753e756191081400d0b0edea4dc6b745285f8a50f3386de55e2ec660514797')

prepare() {
  sed -e '/QT_DISABLE_DEPRECATED_BEFORE/d' -i $pkgname-$pkgver/*/*/*.qbs
  qbs setup-qt /usr/bin/qmake6 qt6
  qbs config defaultProfile qt6
}

build() {
  cd $pkgname-$pkgver
  qbs config:release qbs.installPrefix:"/usr" projects.Tiled.useRPaths:false projects.Tiled.installHeaders:true
}

package() {
  cd $pkgname-$pkgver
  qbs install config:release --install-root "$pkgdir"
}
