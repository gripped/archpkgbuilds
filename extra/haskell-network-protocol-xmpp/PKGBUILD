# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=network-protocol-xmpp
pkgname=haskell-network-protocol-xmpp
pkgver=0.5.1
pkgrel=4
pkgdesc="Client library for the XMPP protocol."
url="https://git.sr.ht/~singpolyma/network-protocol-xmpp"
license=('GPL-3.0-only')
arch=('x86_64')
depends=('ghc-libs' "haskell-gnuidn" "haskell-gnutls" "haskell-gsasl" "haskell-libxml-sax"
         "haskell-network" "haskell-network-simple" "haskell-xml-types")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ba17e30d6ab5471a25633179acd87c43353d5b67ab5f8a3b5408ef76ad73533fbe5ef7afbc346a14cc85f992aa96836d596b3ca2dc0c5dd1bc09829266a8b980')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
