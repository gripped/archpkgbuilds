# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Tobias Bohrmann (PlainTextField) <honeypot.carbage@aechelon.de>

pkgname=tepl
pkgver=6.9.0
pkgrel=1
epoch=1
pkgdesc='Library that eases the development of GtkSourceView-based text editors and IDEs'
arch=('x86_64')
url='https://gitlab.gnome.org/swilmet/tepl'
license=('LGPL-3.0-or-later')
depends=('cairo' 'glib2' 'glibc' 'gsettings-desktop-schemas' 'gtk3' 'icu'
         'libgedit-amtk' 'libgedit-gtksourceview' 'libhandy' 'pango')
makedepends=('gtk-doc' 'gobject-introspection' 'meson')
checkdepends=('xorg-server-xvfb')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('67e761564acd1905e719a7fb4c836f5943cd76212d312c57fb9cea09059f02b8')

build() {
  arch-meson $pkgname-$pkgver build -D gtk_doc=true
  meson compile -C build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
