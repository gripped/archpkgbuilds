# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Firegem <mrfiregem [at] protonmail [dot] ch>

pkgname=taplo-cli
_name=${pkgname%-cli}
pkgver=0.10.0
pkgrel=1
pkgdesc='TOML toolkit written in Rust'
arch=(x86_64)
url=https://taplo.tamasfe.dev/cli/introduction.html
license=(MIT)
depends=(gcc-libs openssl)
makedepends=(
  cargo
  git
)
provides=("$_name")
options=(!lto)
source=("git+https://github.com/tamasfe/$_name.git#tag=$pkgver")
b2sums=('feeca936e65be30109cc4fa6fa3eb915082d1b1d2e44ed05ee9d9e20a0151320e850d240d9d9b5caf05562dbff6a87091c0c4b7c58dc0f4d001b40e1d96b17ad')

prepare() {
  cd "$_name"
  mkdir completions/
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_name"
  cargo build --release --frozen --all-features
  for shell in bash fish zsh; do
    ./target/release/$_name completions "$shell" >"completions/$_name.$shell"
  done
}

package() {
  cd "$_name"
  install -Dt "$pkgdir"/usr/bin target/release/"$_name"
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
  install -Dm 644 "completions/$_name.bash" "$pkgdir/usr/share/bash-completion/completions/$_name"
  install -Dm 644 "completions/$_name.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/$_name.zsh" "$pkgdir/usr/share/zsh/site-functions/_$_name"
}

# vim: ts=2 sw=2 et:
