# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Emiliano Vavassori <syntaxerrormmm@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz@mail.ru>

_gemname=mail
pkgname=ruby-mail
pkgver=2.9.0
pkgrel=1
pkgdesc='A really Ruby Mail handler'
arch=(any)
url='https://github.com/mikel/mail'
license=(MIT)
depends=(
  ruby-mini_mime
  ruby-net-imap
  ruby-net-pop
  ruby-net-smtp
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-activesupport
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('bfb04ecdc2464301460847ef6b839f82d065f1cafb90f21643d20e6f4034cf5169ac4010360fa1945ea275d8d0d4e6bae71e9b3a90f015442d3648bb8b0f0800')
b2sums=('4abfcda5b905bf6e7472f8d938543e930a9cfbe077d1cc0dd288d18c4cd38e77328c4ce7deb515fd6f92095d6edd367946fec739e4960335638c678c568efc32')

prepare() {
  cd "$pkgname"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # Remove unpackaged development dependencies
  sed --in-place '/rspec-benchmark/d;/rufo/d' "${_gemname}.gemspec"

  # Do not use git checkout of activesupport
  sed --in-place 's/, :git.*//' Gemfile

  # Do not require byebug
  sed --in-place '/byebug/d' Gemfile

  # Remove benchmarking
  sed --in-place '/[Bb]enchmark/d' spec/spec_helper.rb
}

build() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "$pkgname"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 MIT-LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
