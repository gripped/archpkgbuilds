# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

# NOTE:
# Since 2.20.0 the gc feature (--enable-gc) requires a patched bwd-gc or newer
# version, since this is a build time dependency we don't have yet we can't
# enable that feature. Can be re-enabled when we can supply bwd.
# c.f. https://github.com/NixOS/nix/pull/9512/files

pkgbase=nix
pkgname=(nix perl-nix)
pkgver=2.31.2
pkgrel=1
pkgdesc='A purely functional package manager'
arch=(x86_64)
url='https://nixos.org/nix'
license=(LGPL-2.1-only)
makedepends=(
  aws-sdk-cpp-core aws-sdk-cpp-iam aws-sdk-cpp-s3
  boost libboost_context.so
  brotli libbrotlienc.so libbrotlidec.so
  bzip2
  curl libcurl.so
  cmake
  editline libeditline.so
  gc
  git
  graphviz
  gtest
  jq
  libblake3
  libsodium libsodium.so
  libarchive libarchive.so
  libcpuid libcpuid.so
  libgit2 libgit2.so
  libseccomp libseccomp.so
  lowdown liblowdown.so
  meson
  nix-busybox
  nlohmann-json
  openssl libcrypto.so
  perl
  perl-dbd-sqlite
  rapidcheck
  sqlite libsqlite3.so
  toml11
)
source=(
  "$pkgbase::git+https://github.com/NixOS/nix.git#tag=${pkgver}?signed"
  sysusers.conf
  nix.conf
  skip-functional-tests.patch
  perl-vendor-path.patch
  remove-unused-sh-files.patch
)
sha512sums=('bc04b631b60a7856e832f11ce8cc77dd02bbc59f4862e7d9718e46af58e2ac31a6bde87c9d92f63805288211b5c9d5f0c39e2e9b2a3cc29179fe66e34dbb7c28'
            '982e1f23401fa00ba9fc3b4533b08c41547f3675a2c75ab3969956c96d3bd30a8f14458f0590053275a9658f6f4f3fabaf56feda7df1464ccff27abe49345d49'
            '9e5305680feac56c207a63d3db8dcac2eb8b7d82499e864ce618bbd4be09862b267965fc1378e30426fdcecc1e55b5b65d780355778e0d851a77e6d56a11679c'
            '740d1f5df55c5760f244bd7da291f1560ca4ae54dee76e863df374996199163dc869e9af1a8b3ddb8f9e23f256671ec5b819a15dd2480bd19474529a3fc121ec'
            '4b052d1b3e7defc6265738b3ff57088c39b6e47cc2e5d6672de7c4bdf610b42c2a132ea6bd624665e91f0705d8b4928dcb76835cd1cf2f7362166f304639162b'
            '9708a14b83a10af236fb5004c16ba7cf057ccc4d500b7957893782456ab4c9db2e20d00d196d93afcb2180e09f8099746c0e831db161322cc8e6886d1f12c322')
b2sums=('3de003f4f03a0eb29f685dd5c429c5e7bd679cb8763988afb5883aca41e7f9128a4cb6fc253f6bca5c31892af5be5dffabd5ac03899d7c286348f149e3342c76'
        '93082469f333ef6c55428c82f4cb208a3c1ddd765ce83e8744761764ad4c0277c00ef62e848794edfd847914077e161098c163e4df759bd3a355b91d5e4850d6'
        'da4733fa3b5bf20e7be59a260f8dc0ddfd52ad610e312d792e11d729a2c20834b34fbbfc8f0a56128b0cc15035c06d244be93174b7264e132c66f18ec63d048b'
        '17f745339a6f027889c86dd2a315d28f0f13ae7d59b53e0c4a0c6d17a5ca658c442dbe5b3cdae7a1efd83b4ecd7b26e79ac0bf8397518c0a1ba43a971c2aecff'
        '21e1705c8ca6d47488cb14b82fb758c35c89ed0abf056b09ea29f4066f755175e4383b4351d1f8965e2e90a25eea3048c0735e1df2b2d17e4fd7704c1e2531e5'
        '9a6091950e3ed3f9622630e16e5defc84d754562707d4ab8773e7b0cf5b52f2794de1b71cabce5705d140deb7703dc6effa355430b50ce1e397cbeba3ffe86b3')
validpgpkeys=('B541D55301270E0BCF15CA5D8170B4726D7198DE') # Eelco Dolstra <edolstra@gmail.com>

prepare() {
  cd "$pkgname"

  # skip functional tests since tests are not run
  patch -p1 -i "$srcdir/skip-functional-tests.patch"

  # install perl bindings to correct path
  patch -p1 -i "$srcdir/perl-vendor-path.patch"

  # skip unused sh files from being installed
  patch -p1 -i "$srcdir/remove-unused-sh-files.patch"
}

build() {
  local meson_opts=(
    libstore:sandbox-shell=/usr/lib/nix/busybox
    nix:profile-dir=/etc/profile.d
    bindings=true
    doc-gen=false
    unit-tests=false
  )

  arch-meson "$pkgbase" build ${meson_opts[@]/#/-D }
}

package_nix() {
  depends=(
    aws-sdk-cpp-core aws-sdk-cpp-iam aws-sdk-cpp-s3
    boost-libs libboost_context.so
    brotli libbrotlienc.so libbrotlidec.so
    curl libcurl.so
    editline libeditline.so
    gc
    gcc-libs
    glibc
    libarchive libarchive.so
    libblake3
    libcpuid libcpuid.so
    libgit2 libgit2.so
    libseccomp libseccomp.so
    libsodium libsodium.so
    lowdown liblowdown.so
    nix-busybox
    nlohmann-json
    openssl libcrypto.so
    sqlite libsqlite3.so
  )
  optdepends=('perl-nix: Perl bindings for Nix')
  backup=('etc/nix/nix.conf')

  DESTDIR="$pkgdir" meson install -C build

  # move perl bindings out for perl-nix split-package
  mv "$pkgdir/usr/lib/perl5" perl-nix

  # systemd integration
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/nix-daemon.conf"
  install -vDm644 nix.conf -t "$pkgdir/etc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgbase/COPYING"
}

package_perl-nix() {
  pkgdesc+=' (Perl bindings)'
  depends=(
    glibc
    gcc-libs
    libsodium
    nix
    perl
  )
  replaces=(nix-perl)

  install -d "$pkgdir/usr/lib/perl5"
  mv perl-nix/* "$pkgdir/usr/lib/perl5"
}

# vim:set ts=2 sw=2 et:
