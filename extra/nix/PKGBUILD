# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

# NOTE:
# Since 2.20.0 the gc feature (--enable-gc) requires a patched bwd-gc or newer
# version, since this is a build time dependency we don't have yet we can't
# enable that feature. Can be re-enabled when we can supply bwd.
# c.f. https://github.com/NixOS/nix/pull/9512/files

pkgbase=nix
pkgname=(nix perl-nix)
pkgver=2.33.2
pkgrel=1
pkgdesc='A purely functional package manager'
arch=(x86_64)
url='https://nixos.org/nix'
license=(LGPL-2.1-only)
makedepends=(
  aws-sdk-cpp-core aws-sdk-cpp-iam aws-sdk-cpp-s3
  boost libboost_context.so
  brotli libbrotlienc.so libbrotlidec.so
  bzip2
  curl libcurl.so
  cmake
  editline libeditline.so
  gc
  git
  graphviz
  gtest
  jq
  libblake3
  libsodium libsodium.so
  libarchive libarchive.so
  libcpuid libcpuid.so
  libgit2 libgit2.so
  libseccomp libseccomp.so
  lowdown liblowdown.so
  meson
  nix-busybox
  nlohmann-json
  openssl libcrypto.so
  perl
  perl-dbd-sqlite
  rapidcheck
  sqlite libsqlite3.so
  toml11
)
source=(
  "$pkgbase::git+https://github.com/NixOS/nix.git#tag=${pkgver}?signed"
  sysusers.conf
  nix.conf
  skip-functional-tests.patch
  perl-vendor-path.patch
  remove-unused-sh-files.patch
)
sha512sums=('829a387b56b8f21996d684f386550ae9184c0de3c9cf695dabacf98966986a3c945d3becabcf626cd2dd1de4627e048c12711863e2ec56335e8a5a7d8b5ab398'
            '982e1f23401fa00ba9fc3b4533b08c41547f3675a2c75ab3969956c96d3bd30a8f14458f0590053275a9658f6f4f3fabaf56feda7df1464ccff27abe49345d49'
            '9e5305680feac56c207a63d3db8dcac2eb8b7d82499e864ce618bbd4be09862b267965fc1378e30426fdcecc1e55b5b65d780355778e0d851a77e6d56a11679c'
            '5c9bbd43ed4a1ae19c9d43a44f14aad413d653f73b3e078e77ae252136b5166a4a7af40cfac5f320ee8a1bd9551f388d234f5cc708f51f06aedd71998c245a60'
            '4b052d1b3e7defc6265738b3ff57088c39b6e47cc2e5d6672de7c4bdf610b42c2a132ea6bd624665e91f0705d8b4928dcb76835cd1cf2f7362166f304639162b'
            '9708a14b83a10af236fb5004c16ba7cf057ccc4d500b7957893782456ab4c9db2e20d00d196d93afcb2180e09f8099746c0e831db161322cc8e6886d1f12c322')
b2sums=('42acdb35a21f6291794d3fb719f5dd29f62771bb58c83e6461edaaa2847f52ac84d5fc7196d930cb43512f598579b4b29a23392186b95d15c892c1a16e4a4432'
        '93082469f333ef6c55428c82f4cb208a3c1ddd765ce83e8744761764ad4c0277c00ef62e848794edfd847914077e161098c163e4df759bd3a355b91d5e4850d6'
        'da4733fa3b5bf20e7be59a260f8dc0ddfd52ad610e312d792e11d729a2c20834b34fbbfc8f0a56128b0cc15035c06d244be93174b7264e132c66f18ec63d048b'
        'd47c01a745c7d8a46d2af28178b81a9527c27e7a2fc818a1783d385161c803facb535d120d5fd9baaabf97027c94d1e7db9bf735700cf5c4b0fc152a8adc49e6'
        '21e1705c8ca6d47488cb14b82fb758c35c89ed0abf056b09ea29f4066f755175e4383b4351d1f8965e2e90a25eea3048c0735e1df2b2d17e4fd7704c1e2531e5'
        '9a6091950e3ed3f9622630e16e5defc84d754562707d4ab8773e7b0cf5b52f2794de1b71cabce5705d140deb7703dc6effa355430b50ce1e397cbeba3ffe86b3')
validpgpkeys=(
  'B541D55301270E0BCF15CA5D8170B4726D7198DE' # Eelco Dolstra <edolstra@gmail.com>
  '158A6F530EA202E5F651611314FAEA63448E1DF9' # Sergei Zimmerman <sergei@zimmerman.foo>
)

prepare() {
  cd "$pkgname"

  # skip functional tests since tests are not run
  patch -p1 -i "$srcdir/skip-functional-tests.patch"

  # install perl bindings to correct path
  patch -p1 -i "$srcdir/perl-vendor-path.patch"

  # skip unused sh files from being installed
  patch -p1 -i "$srcdir/remove-unused-sh-files.patch"
}

build() {
  local meson_opts=(
    libstore:sandbox-shell=/usr/lib/nix/busybox
    nix:profile-dir=/etc/profile.d
    bindings=true
    doc-gen=false
    unit-tests=false
    kaitai-struct-checks=false
    json-schema-checks=false
  )

  arch-meson "$pkgbase" build ${meson_opts[@]/#/-D }

  meson compile -C build
}

package_nix() {
  depends=(
    aws-sdk-cpp-core aws-sdk-cpp-iam aws-sdk-cpp-s3
    boost-libs libboost_context.so
    brotli libbrotlienc.so libbrotlidec.so
    curl libcurl.so
    editline libeditline.so
    gc
    gcc-libs
    glibc
    libarchive libarchive.so
    libblake3
    libcpuid libcpuid.so
    libgit2 libgit2.so
    libseccomp libseccomp.so
    libsodium libsodium.so
    lowdown liblowdown.so
    nix-busybox
    nlohmann-json
    openssl libcrypto.so
    sqlite libsqlite3.so
  )
  optdepends=('perl-nix: Perl bindings for Nix')
  backup=('etc/nix/nix.conf')

  DESTDIR="$pkgdir" meson install -C build

  # move perl bindings out for perl-nix split-package
  mv "$pkgdir/usr/lib/perl5" perl-nix

  # systemd integration
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/nix-daemon.conf"
  install -vDm644 nix.conf -t "$pkgdir/etc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgbase/COPYING"
}

package_perl-nix() {
  pkgdesc+=' (Perl bindings)'
  depends=(
    glibc
    gcc-libs
    libsodium
    nix
    perl
  )
  replaces=(nix-perl)

  install -d "$pkgdir/usr/lib/perl5"
  mv perl-nix/* "$pkgdir/usr/lib/perl5"
}

# vim:set ts=2 sw=2 et:
