# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

# NOTE:
# Since 2.20.0 the gc feature (--enable-gc) requires a patched bwd-gc or newer
# version, since this is a build time dependency we don't have yet we can't
# enable that feature. Can be re-enabled when we can supply bwd.
# c.f. https://github.com/NixOS/nix/pull/9512/files

pkgbase=nix
pkgname=(nix perl-nix)
pkgver=2.28.2
pkgrel=1
pkgdesc='A purely functional package manager'
arch=(x86_64)
url='https://nixos.org/nix'
license=(LGPL-2.1-only)
makedepends=(
  boost libboost_context.so
  brotli libbrotlienc.so libbrotlidec.so
  bzip2
  curl libcurl.so
  cmake
  editline libeditline.so
  gc
  git
  graphviz
  gtest
  jq
  libblake3
  libsodium libsodium.so
  libarchive libarchive.so
  libcpuid libcpuid.so
  libgit2 libgit2.so
  libseccomp libseccomp.so
  lowdown liblowdown.so
  meson
  nix-busybox
  nlohmann-json
  openssl libcrypto.so
  perl
  perl-dbd-sqlite
  rapidcheck
  sqlite libsqlite3.so
  toml11
)
source=(
  "$pkgbase::git+https://github.com/NixOS/nix.git#tag=${pkgver}?signed"
  sysusers.conf
  nix.conf
  skip-functional-tests.patch
  perl-vendor-path.patch
  remove-unused-sh-files.patch
)
sha512sums=('14fc6a7ef13ee4994a93eeed34c3b36ef6656cea95f36d1be3ce9c31d0d2b7fc6983c98a33c44ec0e69baf4bbc174773d85ccfdd47169be251dd336e441c15c0'
            '982e1f23401fa00ba9fc3b4533b08c41547f3675a2c75ab3969956c96d3bd30a8f14458f0590053275a9658f6f4f3fabaf56feda7df1464ccff27abe49345d49'
            '9e5305680feac56c207a63d3db8dcac2eb8b7d82499e864ce618bbd4be09862b267965fc1378e30426fdcecc1e55b5b65d780355778e0d851a77e6d56a11679c'
            '740d1f5df55c5760f244bd7da291f1560ca4ae54dee76e863df374996199163dc869e9af1a8b3ddb8f9e23f256671ec5b819a15dd2480bd19474529a3fc121ec'
            '102a173525adb49bbade22bcd9deb4b70a1ea9cb3af5a691a10b4ca4b12cfab1c9f96d03c06c7434f0a10549c6536ba401d91062a70067f11f9c435729a4dc68'
            '9708a14b83a10af236fb5004c16ba7cf057ccc4d500b7957893782456ab4c9db2e20d00d196d93afcb2180e09f8099746c0e831db161322cc8e6886d1f12c322')
b2sums=('4f8baac8126ccdd4adb518cdf198f01e917ed0412725c18e50cd8548f69837780a753052ea5ab4e7c4bd20062fbec9e8edbd89f8a0d3d20e5ac418a1d0ef1fd5'
        '93082469f333ef6c55428c82f4cb208a3c1ddd765ce83e8744761764ad4c0277c00ef62e848794edfd847914077e161098c163e4df759bd3a355b91d5e4850d6'
        'da4733fa3b5bf20e7be59a260f8dc0ddfd52ad610e312d792e11d729a2c20834b34fbbfc8f0a56128b0cc15035c06d244be93174b7264e132c66f18ec63d048b'
        '17f745339a6f027889c86dd2a315d28f0f13ae7d59b53e0c4a0c6d17a5ca658c442dbe5b3cdae7a1efd83b4ecd7b26e79ac0bf8397518c0a1ba43a971c2aecff'
        '468f3ef1eb12fb6dc52365a191a6b2bf6f7d7fc551be137ba628572671fef3e860b620f57f147c41f8e2bbef7bf8c77bbf6c2c68c8d4a0ff597dd0cedfbaef01'
        '9a6091950e3ed3f9622630e16e5defc84d754562707d4ab8773e7b0cf5b52f2794de1b71cabce5705d140deb7703dc6effa355430b50ce1e397cbeba3ffe86b3')
validpgpkeys=('B541D55301270E0BCF15CA5D8170B4726D7198DE') # Eelco Dolstra <edolstra@gmail.com>

prepare() {
  cd "$pkgname"

  # skip functional tests since tests are not run
  patch -p1 -i "$srcdir/skip-functional-tests.patch"

  # install perl bindings to correct path
  patch -p1 -i "$srcdir/perl-vendor-path.patch"

  # skip unused sh files from being installed
  patch -p1 -i "$srcdir/remove-unused-sh-files.patch"
}

build() {
  local meson_opts=(
    libstore:sandbox-shell=/usr/lib/nix/busybox
    nix:profile-dir=/etc/profile.d
    bindings=true
    doc-gen=false
    unit-tests=false
  )

  arch-meson "$pkgbase" build ${meson_opts[@]/#/-D }
}

package_nix() {
  depends=(
    boost-libs libboost_context.so
    brotli libbrotlienc.so libbrotlidec.so
    curl libcurl.so
    editline libeditline.so
    gc
    gcc-libs
    glibc
    libarchive libarchive.so
    libblake3
    libcpuid libcpuid.so
    libgit2 libgit2.so
    libseccomp libseccomp.so
    libsodium libsodium.so
    lowdown liblowdown.so
    nix-busybox
    nlohmann-json
    openssl libcrypto.so
    sqlite libsqlite3.so
  )
  optdepends=('perl-nix: Perl bindings for Nix')
  backup=('etc/nix/nix.conf')

  DESTDIR="$pkgdir" meson install -C build

  # move perl bindings out for perl-nix split-package
  mv "$pkgdir/usr/lib/perl5" perl-nix

  # systemd integration
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/nix-daemon.conf"
  install -vDm644 nix.conf -t "$pkgdir/etc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgbase/COPYING"
}

package_perl-nix() {
  pkgdesc+=' (Perl bindings)'
  depends=(
    glibc
    gcc-libs
    libsodium
    nix
    perl
  )
  replaces=(nix-perl)

  install -d "$pkgdir/usr/lib/perl5"
  mv perl-nix/* "$pkgdir/usr/lib/perl5"
}

# vim:set ts=2 sw=2 et:
