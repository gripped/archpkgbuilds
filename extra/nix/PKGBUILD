# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

# NOTE:
# Since 2.20.0 the gc feature (--enable-gc) requires a patched bwd-gc or newer
# version, since this is a build time dependency we don't have yet we can't
# enable that feature. Can be re-enabled when we can supply bwd.
# c.f. https://github.com/NixOS/nix/pull/9512/files

pkgbase=nix
pkgname=(nix nix-perl)
pkgver=2.26.3
pkgrel=1
pkgdesc='A purely functional package manager'
arch=(x86_64)
url='https://nixos.org/nix'
license=(LGPL-2.1-only)
makedepends=(
  boost libboost_context.so
  brotli libbrotlienc.so libbrotlidec.so
  bzip2
  curl libcurl.so
  cmake
  editline libeditline.so
  gc
  git
  graphviz
  gtest
  jq
  libsodium libsodium.so
  libarchive libarchive.so
  libcpuid libcpuid.so
  libgit2 libgit2.so
  libseccomp libseccomp.so
  lowdown liblowdown.so
  meson
  nix-busybox
  nlohmann-json
  openssl libcrypto.so
  perl
  perl-dbd-sqlite
  rapidcheck
  sqlite libsqlite3.so
  toml11
)
source=(
  "$pkgbase::git+https://github.com/NixOS/nix.git#tag=${pkgver}?signed"
  sysusers.conf
  nix.conf
  skip-functional-tests.patch
)
sha512sums=('3e9563727359000b49c4673adc1ac4ea2e7f08c67bfb66b3037bb91ac547e477b337bc80e921ce9641a576c4f2fa833fc027de6e4fa298bcaead3bcdd4e044df'
            '6511badd6e4c71d9b7f6e6a87ea521a9727a5569ecf48dca11ee0d04151adf083453dcf7e05eadd5c28278fd8aee9fe44bbb90067f5fc8067c99c5e9665affd9'
            '9e5305680feac56c207a63d3db8dcac2eb8b7d82499e864ce618bbd4be09862b267965fc1378e30426fdcecc1e55b5b65d780355778e0d851a77e6d56a11679c'
            '740d1f5df55c5760f244bd7da291f1560ca4ae54dee76e863df374996199163dc869e9af1a8b3ddb8f9e23f256671ec5b819a15dd2480bd19474529a3fc121ec')
b2sums=('531efc48c2d8631ba55affaf08fc8e9ba1b2874912e36573fee83dd316229858bfb8d6a6dcaa879dc7c9a1dd6cb29245ed0604f39ea72f3e0784bc3292219dec'
        'e92b8192bae89f0bc8c62371fbe63b4bfd68352699e5ea6900bf4313fd573322a06cb85074b292a005cbf644a50abbb67694f9458f071b0fd223d220494f6b6c'
        'da4733fa3b5bf20e7be59a260f8dc0ddfd52ad610e312d792e11d729a2c20834b34fbbfc8f0a56128b0cc15035c06d244be93174b7264e132c66f18ec63d048b'
        '17f745339a6f027889c86dd2a315d28f0f13ae7d59b53e0c4a0c6d17a5ca658c442dbe5b3cdae7a1efd83b4ecd7b26e79ac0bf8397518c0a1ba43a971c2aecff')
validpgpkeys=('B541D55301270E0BCF15CA5D8170B4726D7198DE') # Eelco Dolstra <edolstra@gmail.com>

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/skip-functional-tests.patch"
}

build() {
  local meson_opts=(
    libstore:sandbox-shell=/usr/lib/nix/busybox
    nix:profile-dir=/etc/profile.d
    bindings=true
    doc-gen=false
    unit-tests=false
  )

  arch-meson "$pkgbase" build ${meson_opts[@]/#/-D }
}

package_nix() {
  depends=(
    boost-libs libboost_context.so
    brotli libbrotlienc.so libbrotlidec.so
    curl libcurl.so
    editline libeditline.so
    gc
    gcc-libs
    glibc
    libarchive libarchive.so
    libcpuid libcpuid.so
    libgit2 libgit2.so
    libseccomp libseccomp.so
    libsodium libsodium.so
    lowdown liblowdown.so
    nix-busybox
    nlohmann-json
    openssl libcrypto.so
    sqlite libsqlite3.so
  )
  backup=('etc/nix/nix.conf')
  install='nix.install'

  DESTDIR="$pkgdir" meson install -C build

  # move perl bindings out for nix-perl split-package
  mv "$pkgdir/usr/lib/perl5" perl-nix

  # systemd integration
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 nix.conf -t "$pkgdir/etc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgbase/COPYING"
}

package_nix-perl() {
  pkgdesc+=' (Perl bindings)'
  depends=(nix perl)
        
  install -d "$pkgdir/usr/lib/perl5"
  mv perl-nix "$pkgdir/usr/lib/perl5/"
}

# vim:set ts=2 sw=2 et:
