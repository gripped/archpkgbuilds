# Maintainer: Robin Candau <antiz@archlinux.org>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=python-pillow-heif
pkgname=(
  python-pillow-heif
  python-pillow-heif-docs
)
pkgver=1.2.0
pkgrel=1
pkgdesc='Python bindings to libheif for working with HEIF images and plugin for Pillow'
arch=(x86_64)
url='https://github.com/bigcat88/pillow_heif'
license=(BSD-3-Clause)
depends=(
  glibc
  libheif
  python
  python-pillow
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-sphinx-issues
  python-sphinx_rtd_theme
  python-wheel
)
checkdepends=(
  python-defusedxml
  python-pytest
)
source=("git+https://github.com/bigcat88/pillow_heif.git#tag=v$pkgver")
b2sums=('54e96a7b337427a047aafc39f866333e5ded045c036c05b9df88ab9dc072c7726c3ea278d9e204d5bb52700623d5fb58bfd223d4be8f59aea2f961a89cfc5efc')

build() {
  cd pillow_heif
  python -m build --wheel --no-isolation
}

check() {
  cd pillow_heif
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')" \
    pytest
}

package_python-pillow-heif() {
  conflicts=(python-pillow_heif)
  provides=("python-pillow_heif=$pkgver")
  replaces=(python-pillow_heif)

  cd pillow_heif
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}

package_python-pillow-heif-docs() {
  pkgdesc="Developer documentation for python-pillow-heif"
  depends=()

  cd pillow_heif
  python -m sphinx -b html docs "$pkgdir/usr/share/doc/$pkgbase/html"
  rm -r "$pkgdir/usr/share/doc/$pkgbase/html/.doctrees"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
