# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Nathaniel Chin <thegamingorangutans+aur at gmail.com>

pkgname=av1an
pkgver=0.5.2
pkgrel=1
pkgdesc='Cross-platform command-line encoding framework with per scene quality encoding'
arch=('x86_64')
url='https://github.com/rust-av/Av1an'
license=('GPL-3.0-only')
makedepends=('rust' 'nasm' 'clang' 'git')
depends=('gcc-libs' 'glibc' 'ffmpeg' 'aom' 'vapoursynth')
optdepends=('svt-av1: SVT-AV1 encoder support'
            'rav1e: rav1e encoder support'
            'libvpx: vpx encoder support'
            'vmaf: VMAF calculation support'
            'mkvtoolnix-cli: mkvmerge support'
            'ffms2: FFMS2 chunk detection support'
            'vapoursynth-plugin-bestsource: Preferred chunk detection plugin'
            'vapoursynth-plugin-lsmashsource: L-SMASH chunk detection support')
source=("git+https://github.com/master-of-zen/Av1an#tag=v$pkgver")
sha256sums=('01c7e71296c60b1584fb1ecaf5cca9ecef849ba65bbd8a765be195432e445695')

build() {
  cd "Av1an"
  export CFLAGS+=" -ffat-lto-objects"
  cargo build --release --locked
}

package() {
  cd "Av1an"
  install -Dm755 target/release/av1an "${pkgdir}/usr/bin/av1an"
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE.md
}
