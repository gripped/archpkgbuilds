# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Nathaniel Chin <thegamingorangutans+aur at gmail.com>

pkgname=av1an
pkgver=0.5
pkgrel=1
pkgdesc='Cross-platform command-line encoding framework with per scene quality encoding'
arch=('x86_64')
url='https://github.com/rust-av/Av1an'
license=('GPL-3.0-only')
makedepends=('rust' 'nasm' 'clang')
depends=('ffmpeg' 'aom' 'vapoursynth')
optdepends=('svt-av1: SVT-AV1 encoder support'
            'rav1e: rav1e encoder support'
            'libvpx: vpx encoder support'
            'vmaf: VMAF calculation support'
            'mkvtoolnix-cli: mkvmerge support'
            'ffms2: FFMS2 chunk detection support'
            'vapoursynth-plugin-lsmashsource: L-SMASH chunk detection support')
source=("$pkgname-$pkgver.tar.gz"::https://github.com/master-of-zen/Av1an/archive/refs/tags/$pkgver.tar.gz)
sha256sums=('edfbe6df5fdee9a63f1fc7462198714ce4ace4d00bac7eb09d34d8bfe5798e1c')

build() {
  cd "Av1an-${pkgver}"
  export CFLAGS+=" -ffat-lto-objects"
  cargo build --release --locked
}

package() {
  cd "Av1an-${pkgver}"
  install -Dm755 target/release/av1an "${pkgdir}/usr/bin/av1an"
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE.md
}
