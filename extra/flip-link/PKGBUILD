# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=flip-link
pkgver=0.1.9
pkgrel=1
pkgdesc="Flips the memory layout of embedded programs to protect against stack overflows"
url='https://github.com/knurling-rs/flip-link'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/knurling-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f0a8f6eabec1552671815ec4fc4a61d127a36f12a9f9888c0f66e02096c51ef6')
b2sums=('81a15f5c130195225d07cf4197d2fe14661ac5b372af20a6afe82a90caa1eea752862de79f20ea298d5b6c06c2d3b4048b64cbb9a092558b998049b253a59c98')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/flip-link

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
