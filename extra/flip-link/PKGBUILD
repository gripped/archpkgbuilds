# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=flip-link
pkgver=0.1.12
pkgrel=1
pkgdesc="Flips the memory layout of embedded programs to protect against stack overflows"
url='https://github.com/knurling-rs/flip-link'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/knurling-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3608b9adddd5415d1a897947abaaf0438a8a4459292054dc7eb1f809ecab1366')
b2sums=('7537f0a0ed6e10a47b42a2b7942385f64e7fd75c51d0a7e05af40ad809794bcbfbac0f903746f3d2dee6f9118ba85b3826253f98e1362b69cceb294cc0b73c4a')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/flip-link

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
