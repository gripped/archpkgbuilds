# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=flip-link
pkgver=0.1.10
pkgrel=1
pkgdesc="Flips the memory layout of embedded programs to protect against stack overflows"
url='https://github.com/knurling-rs/flip-link'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/knurling-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('9389806ffda4ed5aa47f39fc71ac2a19be59cc28aab93bfb32bb514ed7165f75')
b2sums=('ad282650e97135d00ee39ded79c9b7a5c37c9e7c1c05fc4c02e3167cd4ad085119e3e4a394c066870f716f20a30272d6eace07b2298cd1d3bf5a041e15eba346')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/flip-link

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
