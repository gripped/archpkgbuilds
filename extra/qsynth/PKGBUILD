# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=1.0.4
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=(x86_64)
url="https://qsynth.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  fluidsynth
  pipewire
  qt6-tools
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('721e6e6685d7b12d9eba0475674488936c2d79b19968cb59a3a62ab4deb15dae64c9880361ed49afe0ec4fa13cf2e5135781051eaa97a73fac37b2472a636e39')
b2sums=('ee75ec9ffd23c397bbf60f4f4375e11ae965d8219b836dfd90816290f10c131b01f8aa0500cd516add8b08316de4b1cfae2433b925bdeb66409c41c58d51d99f')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    fluidsynth libfluidsynth.so
    libpipewire libpipewire-0.3.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README,TRANSLATORS} -t "$pkgdir/usr/share/doc/$pkgname"
}
