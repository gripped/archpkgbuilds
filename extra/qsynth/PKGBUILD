# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=1.0.3
pkgrel=2
pkgdesc="Qt GUI for FluidSynth"
arch=(x86_64)
url="https://qsynth.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  fluidsynth
  pipewire
  qt6-tools
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('2f9c53caeb2cdab7b05612f4425ee95886200ba229b3cb4fd37a95081d4557954ba4dfb18859e5cefb55512c962283838ef2ffb325bac33bcc1fc01faf2c9b30')
b2sums=('d67da38b985debe3065f9aa9eca8ecaf4999cd084154ccd2f61cb5b37a2917e29bcb5c4ec378aed5a38ab3083d1842ad8dfe2731f9f60918e5005205520a97cb')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    fluidsynth libfluidsynth.so
    libpipewire libpipewire-0.3.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README,TRANSLATORS} -t "$pkgdir/usr/share/doc/$pkgname"
}
