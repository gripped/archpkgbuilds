# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Loodse <aur@loodse.com>

pkgname=kubeone
_pkgver=1.10.0
pkgver="${_pkgver/-/}"
# We check out the repo via git for retrieving necessary information
# for `kubeone version`.
_kubeone_commit="62d86847ec8e5263942ba1732340fe802e70a246"
pkgrel=1
pkgdesc="A lifecycle management tool for Highly-Available Kubernetes clusters"
url="https://github.com/kubermatic/kubeone"
arch=("x86_64")
license=("Apache")
makedepends=("go" "git")
optdepends=("terraform: sourcing data about infrastructure and control plane nodes")
source=("${pkgname}-${_pkgver}::git+https://github.com/kubermatic/kubeone#commit=${_kubeone_commit}")
sha512sums=('500afbc6b693fc506ce1c51db37666477e2c490e60c90d6f78265b9baf18ebcedc997b1a57faec01002e172c1a00a00633b69a0b814a8bfa2739df48c7251166')
options=('!lto')

prepare() {
  cd "${pkgname}-${_pkgver}"
  sed -E 's/(CGO_ENABLED)=0/\1=1/g' -i Makefile
  sed '/^export GOFLAGS/ s/$/ -modcacherw -buildmode=pie/' -i Makefile
  sed '/^GOLDFLAGS?=/ s/=/= -linkmode=external /' -i Makefile

}

build() {
  cd "${pkgname}-${_pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  make build
}

check() {
  cd "${pkgname}-${_pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  make test
}

package() {
  cd "${pkgname}-${_pkgver}"
  install -Dm755 "dist/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # move documentation
  mkdir -p "${pkgdir}/usr/share/doc"
  cp -avr docs "${pkgdir}/usr/share/doc/${pkgname}"
  cp -avr examples "${pkgdir}/usr/share/doc/${pkgname}/examples"

  # build man page
  mkdir -p "${pkgdir}/usr/share/man/man1"
  ./dist/"${pkgname}" document man -o "${pkgdir}/usr/share/man/man1"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./dist/"${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  ./dist/"${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
