# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Loodse <aur@loodse.com>

pkgname=kubeone
_pkgver=1.12.2
pkgver="${_pkgver/-/}"
# We check out the repo via git for retrieving necessary information
# for `kubeone version`.
_kubeone_commit="fce20b815d35d73d30237729770a79bb58b539c3"
pkgrel=2
pkgdesc="A lifecycle management tool for Highly-Available Kubernetes clusters"
url="https://github.com/kubermatic/kubeone"
arch=("x86_64")
license=("Apache")
makedepends=("go" "git")
optdepends=("terraform: sourcing data about infrastructure and control plane nodes")
source=("${pkgname}-${_pkgver}::git+https://github.com/kubermatic/kubeone#commit=${_kubeone_commit}")
sha512sums=('2fdfdbeffc6f5410c8b84434ae709803c8920ff2bfc2d532ad2f855f26597c59845dfd7c75e4631831abd61e5e77b0d613bc43f4d02483f56b54087ae096077b')
options=('!lto')

prepare() {
  cd "${pkgname}-${_pkgver}"
  sed -E 's/(CGO_ENABLED)=0/\1=1/g' -i Makefile
  sed '/^export GOFLAGS/ s/$/ -modcacherw -buildmode=pie/' -i Makefile
  sed '/^GOLDFLAGS?=/ s/=/= -linkmode=external /' -i Makefile
}

build() {
  cd "${pkgname}-${_pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  make build
}

check() {
  cd "${pkgname}-${_pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  make test
}

package() {
  cd "${pkgname}-${_pkgver}"
  install -Dm755 "dist/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # move documentation
  mkdir -p "${pkgdir}/usr/share/doc"
  cp -avr docs "${pkgdir}/usr/share/doc/${pkgname}"
  cp -avr examples "${pkgdir}/usr/share/doc/${pkgname}/examples"

  # build man page
  mkdir -p "${pkgdir}/usr/share/man/man1"
  ./dist/"${pkgname}" document man -o "${pkgdir}/usr/share/man/man1"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./dist/"${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  ./dist/"${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
