# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=sscg
pkgver=4.0.1
pkgrel=1
pkgdesc="Simple Signed Certificate Generator"
url="https://github.com/sgallagher/sscg"
license=('GPL-3.0-or-later')
arch=(x86_64)
depends=(openssl popt talloc ding-libs)
makedepends=(git help2man meson)
source=(git+https://github.com/sgallagher/sscg.git?signed#tag=${pkgname}-${pkgver})
# https://github.com/sgallagher.gpg
validpgpkeys=('850A9DD1DBF2ACCC6FC4949145DB85A568286D11')
sha512sums=('a40fb1ef0a8ae768dcd5990e3b70679fb62eee4ea4817aa0d0dcf1202f09b2c510d8c43a4b77fd5b8e07e9e1d9d8bed35a755f35db58db82c21c133d7f102f3b')

build() {
  cd $pkgname
  meson --prefix=/usr \
        --buildtype plain \
        --auto-features enabled \
        -D b_lto=true \
        -D b_pie=true \
  build
  ninja -C build
}

check() {
  cd $pkgname
  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname
  DESTDIR="${pkgdir}" meson install -C build
}
