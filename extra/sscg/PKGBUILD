# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=sscg
pkgver=4.0.3
pkgrel=1
pkgdesc="Simple Signed Certificate Generator"
url="https://github.com/sgallagher/sscg"
license=('GPL-3.0-or-later')
arch=(x86_64)
depends=(openssl popt talloc ding-libs)
makedepends=(git help2man meson)
source=(git+https://github.com/sgallagher/sscg.git?signed#tag=${pkgname}-${pkgver})
# https://github.com/sgallagher.gpg
validpgpkeys=('850A9DD1DBF2ACCC6FC4949145DB85A568286D11')
sha512sums=('d3b38554f040a7589c44d87e42c6fab3b2df425c223e8b1d4bfbde3e09109d2b01d25d6525ee8cc963fe89cf07262caac0e06aec566004914470e77cc28a0ce2')

build() {
  cd $pkgname
  meson --prefix=/usr \
        --buildtype plain \
        --auto-features enabled \
        -D b_lto=true \
        -D b_pie=true \
  build
  ninja -C build
}

check() {
  cd $pkgname
  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname
  DESTDIR="${pkgdir}" meson install -C build
}
