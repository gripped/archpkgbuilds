# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=sscg
pkgver=4.0.0
pkgrel=1
pkgdesc="Simple Signed Certificate Generator"
url="https://github.com/sgallagher/sscg"
license=('GPL-3.0-or-later')
arch=(x86_64)
depends=(openssl popt talloc ding-libs)
makedepends=(git help2man meson)
source=(git+https://github.com/sgallagher/sscg.git?signed#tag=${pkgname}-${pkgver})
# https://github.com/sgallagher.gpg
validpgpkeys=('850A9DD1DBF2ACCC6FC4949145DB85A568286D11')
sha512sums=('8d2cace0890a43750eab75b4d48a440f48e5b894d919c1767aec228662616e1463195c4a6c50ed94af4ad25ed28ff9bb3aa9ed6770297d95864e24774b71c419')

build() {
  cd $pkgname
  meson --prefix=/usr \
        --buildtype plain \
        --auto-features enabled \
        -D b_lto=true \
        -D b_pie=true \
  build
  ninja -C build
}

check() {
  cd $pkgname
  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname
  DESTDIR="${pkgdir}" meson install -C build
}
