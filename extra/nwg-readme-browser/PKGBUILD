# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=nwg-readme-browser
pkgver=0.1.7
pkgrel=3
pkgdesc="nwg-shell WebKitGTK-based README file browser"
url="https://github.com/nwg-piotr/nwg-readme-browser"
arch=('any')
license=('MIT')
depends=('python' 'python-gobject' 'gtk3' 'webkit2gtk-4.1' 'gtk-layer-shell' 'python-markdown2' 'python-docutils')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('d5f3cd29dcd987badae6fe8bb1a1ed79ca4cf221d9f5052d7d9594bdb73c020d')

build() {
	cd "${pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	install -Dm 644 "${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
	install -Dm 644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
