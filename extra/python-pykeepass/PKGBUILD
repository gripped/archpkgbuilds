# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

_pkgname=pykeepass
pkgname=python-pykeepass
pkgver=4.0.7
pkgrel=4
pkgdesc="Python library to interact with keepass databases"
arch=('any')
url="https://github.com/libkeepass/pykeepass"
license=('GPL-3.0-only')
depends=(python python-argon2_cffi python-construct python-lxml python-pycryptodomex python-setuptools)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pyotp)
source=("https://github.com/libkeepass/$_pkgname/archive/v$pkgver/$_pkgname-$pkgver.tar.gz"
        "0001-fix_missing_pykeepass_kdbx_parsing.patch"
        "0002-Use_built_in_isoformat_support.patch")
sha256sums=('c1c8bba7314a8aff196dbb3c2bcbde6dfa64008f4e67637f06d5139d13095cfe'
            'd371c5a9fdccee937e17553c1adf60d88d7690ebe90995ed6b590bb862c1c77c'
            '3004ef018d2766b2c7c58f3a9606fdcda12a599ba0719f6aa579ae3ce5ec0ef9')

prepare() {
  cd $_pkgname-$pkgver

  # Fix missing pykeepass.kdbx_parsing when built with modern tools
  # https://github.com/libkeepass/pykeepass/pull/378
  patch --forward --strip=1 --input=../0001-fix_missing_pykeepass_kdbx_parsing.patch

  # Use built-in isoformat support
  # https://github.com/libkeepass/pykeepass/pull/383
  patch --forward --strip=1 --input=../0002-Use_built_in_isoformat_support.patch
}

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  python -m unittest tests.tests
}

package() {
  cd $_pkgname-$pkgver
  python3 -m installer --destdir="$pkgdir" dist/*.whl
}
