# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-pykeepass
pkgver=4.1.1.post1
pkgrel=2
pkgdesc="Python library to interact with keepass databases"
arch=('any')
url="https://github.com/libkeepass/pykeepass"
license=('GPL-3.0-only')
depends=(
  python
  python-argon2_cffi
  python-construct
  python-lxml
  python-pycryptodomex
  python-setuptools
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
checkdepends=(python-pyotp)
source=("git+https://github.com/libkeepass/pykeepass.git#tag=v${pkgver}")
b2sums=('efc366773af80eb31d23d95231b0c01f51558ffa9b88c1d5c7564519e181d29a42549b95a470416c2f6b17650035e9d0c8846a15d0a6d8dccc10a792f513913e')

prepare() {
  cd pykeepass
}

build() {
  cd pykeepass
  python -m build --wheel --no-isolation
}

check() {
  cd pykeepass
  python -m unittest tests.tests
}

package() {
  cd pykeepass
  python3 -m installer --destdir="$pkgdir" dist/*.whl
}
