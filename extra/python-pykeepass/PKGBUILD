# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-pykeepass
pkgver=4.1.0
pkgrel=1
pkgdesc="Python library to interact with keepass databases"
arch=('any')
url="https://github.com/libkeepass/pykeepass"
license=('GPL-3.0-only')
depends=(
  python
  python-argon2_cffi
  python-construct
  python-lxml
  python-pycryptodomex
  python-setuptools
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
checkdepends=(python-pyotp)
source=("git+https://github.com/libkeepass/pykeepass.git#tag=v${pkgver}")
b2sums=('bc73719af18f89014456d00d72b75494bdb778a86106512344a7776ea4d098151d9d3c6766ca2a46b98a5f5d4d5a9cca348c400e0257b2a575e4aa231cb0bf62')

prepare() {
  cd pykeepass
}

build() {
  cd pykeepass
  python -m build --wheel --no-isolation
}

check() {
  cd pykeepass
  python -m unittest tests.tests
}

package() {
  cd pykeepass
  python3 -m installer --destdir="$pkgdir" dist/*.whl
}
