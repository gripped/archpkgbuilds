# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=havn
pkgver=0.3.3
pkgrel=1
pkgdesc="A fast configurable port scanner with reasonable defaults"
arch=('x86_64')
url="https://github.com/mrjackwills/havn"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('70c21f379b0a3264cca153b8f971fe45588a574333b71191d195ace7729c29cf')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

# https://github.com/mrjackwills/havn/issues/2
# check() {
#   cd "$pkgname-$pkgver"
#   cargo test --frozen
# }

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
