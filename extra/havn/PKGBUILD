# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=havn
pkgver=0.3.4
pkgrel=1
pkgdesc="A fast configurable port scanner with reasonable defaults"
arch=('x86_64')
url="https://github.com/mrjackwills/havn"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('1648ad392093ea3dee85e9a1d6c8309a1733034f3f489fb0d6ef2289f0babca4')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

# https://github.com/mrjackwills/havn/issues/2
# check() {
#   cd "$pkgname-$pkgver"
#   cargo test --frozen
# }

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
