# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=mwparserfromhell
pkgname=python-$_name
pkgver=0.7.1
pkgrel=1
pkgdesc="A Python parser for MediaWiki wikicode"
arch=(x86_64)
url="https://github.com/earwig/mwparserfromhell"
license=(MIT)
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-pytest
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
b2sums=('4f906c87e21aedcde1bdc318e877275a39324085b3c12844dd63f0a16dad604e699509009a2dee370c66076a3d3478bc9a22013ddbea81fc534d34e0f561467c')

build() {
  cd $_name-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
