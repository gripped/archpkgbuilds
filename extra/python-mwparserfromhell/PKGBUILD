# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=mwparserfromhell
pkgname=python-$_name
pkgver=0.7.0
pkgrel=2
pkgdesc="A Python parser for MediaWiki wikicode"
arch=(x86_64)
url="https://github.com/earwig/mwparserfromhell"
license=(MIT)
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
b2sums=('003254ef428be111829386d51554d524d9e2c45be6ab2e13ea6664beeb7976fb447e68e8305cfebb86b7bca2b6d6f0111f6a68bdd22bf1e328eb3452b80d9785')

build() {
  cd $_name-$pkgver
  # avoid error with c23 https://github.com/earwig/mwparserfromhell/issues/340
  CFLAGS+=" -std=gnu17"
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
