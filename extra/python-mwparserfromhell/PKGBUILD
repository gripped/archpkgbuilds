# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=mwparserfromhell
pkgname=python-$_name
pkgver=0.7.2
pkgrel=2
pkgdesc="A Python parser for MediaWiki wikicode"
arch=(x86_64)
url="https://github.com/earwig/mwparserfromhell"
license=(MIT)
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-pytest
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
b2sums=('d0c77fea4aded8889babadccd104a6fb390ff2bfeae34b1e47c64db868fb5b4c454fa185c1aea209f91546e8a98693e073efae6a4f61d6b1de0573a8479b915a')

build() {
  cd $_name-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
