# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=swaptop
pkgver=1.0.4
pkgrel=1
pkgdesc='Swap usage monitor'
url='https://github.com/luis-ota/swaptop'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/luis-ota/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1af6bb43be0407e1b497a4f7c868b39dd771a758fadffd29ccf1af9867f60b2f')
b2sums=('692c93b5b5f0035ed9dc46e7f7dca36c0a6ce83d973734c6fbfd04834dde97217d17da27fb0e76c10821e83d4109448a828c3cbb90d02acb3f29e26c46b607b7')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/swaptop

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
