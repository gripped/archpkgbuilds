# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: googol <googol@posteo.de>
# Contributor: Oliver Mangold <o.mangold at gmail dot com>

pkgname=python-alembic
pkgver=1.18.4
pkgrel=1
pkgdesc='Lightweight database migration tool for usage with SQLAlchemy'
url='https://github.com/sqlalchemy/alembic'
arch=(any)
license=(MIT)
depends=(
  python
  python-mako
  python-sqlalchemy
  python-typing_extensions
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-black
  python-pytest
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/sqlalchemy/alembic/archive/refs/tags/rel_${pkgver//./_}.tar.gz
)
sha512sums=('e470479fc0cdc35c6f3bce75a982f9d13034008356977eaf83d72811072b1ee4a93c177d30a37d228825b052edf83626aebfc1408b538e554eb09c3626773056')
b2sums=('34cb6effb6809c1fe67f9b2e13512413e5f3c83daa8f74db52c949b12869f690add08931145be2a24b6436b876c0f921307443928539c52396c1403c371bd3a5')

prepare() {
  cd alembic-rel_${pkgver//./_}
  # do not treat warnings as errors
  sed 's|-p no:warnings||g' -i setup.cfg

  sed -i '/tag_build=dev/d' setup.cfg
}

build() {
  cd alembic-rel_${pkgver//./_}
  python -m build --wheel --no-isolation
}

check() {
  cd alembic-rel_${pkgver//./_}
  pytest -vv
}

package() {
  cd alembic-rel_${pkgver//./_}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
