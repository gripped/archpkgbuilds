# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=9.6.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=(any)
url=https://eslint.org
license=(MIT)
depends=(
  acorn
  nodejs
)
makedepends=(
  git
  jq
  npm
)
options=('!emptydirs')
source=("git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver")
b2sums=('19d3e78e57f712760ec4c4a90be794c7537b891bc7cb58f5d0d2192d2c9e31a4bbcb0d88f5680432499c672c2863abe9c23c582caf22b904504640f57be96201')

prepare() {
  cd $pkgname
  npm install
}

check() {
  cd $pkgname
  node Makefile.js mocha
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/{bin,share/{doc,licenses}/$pkgname},$mod_dir}
  ln -s $mod_dir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/README.md "$pkgdir"/usr/share/doc/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname

  cd $pkgname
  npm prune --production

  readarray -t mod_files < <(jq -r '.files[]' package.json)
  cp -r "${mod_files[@]}" node_modules package.json "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/bin/$pkgname.js

  # Experimental dedup
  rm -r "$pkgdir"/$mod_dir/node_modules/acorn
}
