# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=9.4.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='https://eslint.org'
license=('MIT')
depends=('acorn' 'nodejs')
makedepends=('git' 'jq' 'npm')
options=('!emptydirs')
source=("git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver")
b2sums=('46f371cb306c79ae241a66eb08e1526668abe6fe4c0eea99b1ca41058f8e578351105d38373f9d49459efe9f0723769ba3a9f007f27643c2623575d646c5f3c8')

prepare() {
  cd $pkgname
  npm install
}

check() {
  cd $pkgname
  node Makefile.js mocha
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/{bin,share/{doc,licenses}/$pkgname},$mod_dir}
  ln -s $mod_dir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/README.md "$pkgdir"/usr/share/doc/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname

  cd $pkgname
  npm prune --production

  readarray -t mod_files < <(jq -r '.files[]' package.json)
  cp -r "${mod_files[@]}" node_modules package.json "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/bin/$pkgname.js

  # Experimental dedup
  rm -r "$pkgdir"/$mod_dir/node_modules/acorn
}
