# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=9.20.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=(any)
url=https://eslint.org
license=(MIT)
depends=(
  acorn
  nodejs
)
makedepends=(
  git
  jq
  npm
  python
)
options=(!emptydirs)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver")
b2sums=('518f6fa4119ba89fb65ea3661828ece7cd6b7eda5355202c27f5dad478494a5357f6c54682dbeeb234147a7926269a09dd6e991557a18b4dfe8222339cef869d')

prepare() {
  cd $pkgname
  npm install
}

check() {
  cd $pkgname
  node Makefile.js mocha
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/{bin,share/{doc,licenses}/$pkgname},$mod_dir}
  ln -s $mod_dir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/README.md "$pkgdir"/usr/share/doc/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname

  cd $pkgname
  npm prune --production

  readarray -t mod_files < <(jq -r '.files[]' package.json)
  cp -r "${mod_files[@]}" node_modules package.json "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/bin/$pkgname.js

  # Experimental dedup
  rm -r "$pkgdir"/$mod_dir/node_modules/acorn
}
