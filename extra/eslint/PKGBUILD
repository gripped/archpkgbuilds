# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=9.22.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=(any)
url=https://eslint.org
license=(MIT)
depends=(
  acorn
  nodejs
)
makedepends=(
  git
  jq
  npm
  python
)
options=(!emptydirs)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver")
b2sums=('c855772b0a5ad81093906994a9a8b62fb40db6fd8c84828751aeee0ac87d0e54457b5778998fffb9bcf11648d3d2c0ad56e225b18fde754e4bd4275f72010de9')

prepare() {
  cd $pkgname
  npm install
}

check() {
  cd $pkgname
  node Makefile.js mocha
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/{bin,share/{doc,licenses}/$pkgname},$mod_dir}
  ln -s $mod_dir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/README.md "$pkgdir"/usr/share/doc/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname

  cd $pkgname
  npm prune --omit=dev

  mapfile -t mod_files < <(jq -r '.files[]' package.json)
  cp -r "${mod_files[@]}" node_modules package.json "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/bin/$pkgname.js

  # Experimental dedup
  rm -r "$pkgdir"/$mod_dir/node_modules/acorn
}
