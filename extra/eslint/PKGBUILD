# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=8.43.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='https://eslint.org'
license=('MIT')
depends=('acorn' 'nodejs')
makedepends=('npm' 'rsync')
options=('!emptydirs')
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('ca57917d02e1ae75f84ef5aaba19361171c49ffe738a1a5ea61827366e84a68828403160bf1154d8a2813fddbed02a125ded90c965608d3ac7928bc9675a1842')

prepare() {
  cd $pkgname-$pkgver
  npm install
}

check() {
  cd $pkgname-$pkgver
  node Makefile mocha
}

package() {
  cd $pkgname-$pkgver

  npm prune --production

  local _npmdir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/usr/bin
  ln -s $_npmdir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  install -Dt "$pkgdir"/$_npmdir/bin bin/$pkgname.js
  rsync -r --exclude .cache --exclude .package-lock.json conf lib messages \
    node_modules package.json "$pkgdir"/$_npmdir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE

  # Experimental dedup
  rm -r "$pkgdir"/$_npmdir/node_modules/acorn
}
