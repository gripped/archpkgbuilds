# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=9.15.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=(any)
url=https://eslint.org
license=(MIT)
depends=(
  acorn
  nodejs
)
makedepends=(
  git
  jq
  npm
  python
)
options=(!emptydirs)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver")
b2sums=('97f768d20b494a333f84f90a2e045a30990413005f4588d99c6dadb0b877fde1b995f5a413fc8783d270da626a606ec106c7421882d0a16b8a86705a13a2a999')

prepare() {
  cd $pkgname
  npm install
}

check() {
  cd $pkgname
  node Makefile.js mocha
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/{bin,share/{doc,licenses}/$pkgname},$mod_dir}
  ln -s $mod_dir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/README.md "$pkgdir"/usr/share/doc/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname

  cd $pkgname
  npm prune --production

  readarray -t mod_files < <(jq -r '.files[]' package.json)
  cp -r "${mod_files[@]}" node_modules package.json "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/bin/$pkgname.js

  # Experimental dedup
  rm -r "$pkgdir"/$mod_dir/node_modules/acorn
}
