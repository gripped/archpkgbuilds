# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=msolve
pkgver=0.9.3
pkgrel=1
pkgdesc='Library for polynomial system solving through algebraic methods'
url='https://msolve.lip6.fr/'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(flint
         gcc-libs
         glibc
         gmp)
makedepends=(git)
source=(git+https://github.com/algebraic-solving/msolve#tag=v$pkgver)
sha256sums=('c2db5fdd5ce7e1b4f26f42b5135a4ee23603509c17325f80882883c9bc9c5658')

prepare() {
  cd $pkgname
  sed -e '/AX_EXT/d' -i configure.ac # Disable CPU autodetection
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
