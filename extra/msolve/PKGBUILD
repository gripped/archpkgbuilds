# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=msolve
pkgver=0.6.4
pkgrel=1
pkgdesc='Library for polynomial system solving through algebraic methods'
url='https://msolve.lip6.fr/'
arch=(x86_64)
license=(GPL2)
depends=(flint
         gcc-libs
         glibc
         gmp)
makedepends=()
source=(https://github.com/algebraic-solving/msolve/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('cda6805b6a9612eebf3f438f1fcf69ecda7424f96a4ae4ce5e311923a8af6d21')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
