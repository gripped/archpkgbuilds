# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=msolve
pkgver=0.8.0
pkgrel=2
pkgdesc='Library for polynomial system solving through algebraic methods'
url='https://msolve.lip6.fr/'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(flint
         gcc-libs
         glibc
         gmp)
makedepends=(git)
source=(git+https://github.com/algebraic-solving/msolve#tag=v$pkgver)
sha256sums=('63c933ece4261dfb2f0e7a90a2e440cecfccdf3f101f88fba6a5478535683518')

prepare() {
  cd $pkgname
  sed -e '/AX_EXT/d' -i configure.ac # Disable CPU autodetection
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
