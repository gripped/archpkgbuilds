# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gpaste
pkgver=45.2
pkgrel=1
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD-2-Clause)
arch=(x86_64)
depends=(
  cairo
  dconf
  gcc-libs
  gcr-4
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  gtk4
  libadwaita
  libx11
  libxi
)
makedepends=(
  git
  gjs
  gnome-control-center
  gobject-introspection
  meson
  pango
  systemd
  vala
)
optdepends=("wgetpaste: Upload clipboard contents")
source=("git+https://github.com/keruspe/gpaste#tag=v$pkgver")
b2sums=('1a93cf1ee4d9b31d1da74229c29afb2b6e1d628df959c06e177e008b56bb20f17428112dd724af85be84d2872d89a38f0cb872d345d6642a4c0d21de64b9f19f')

prepare() {
  cd gpaste
}

build() {
  arch-meson gpaste build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 gpaste/COPYING
}

# vim:set sw=2 sts=-1 et:
