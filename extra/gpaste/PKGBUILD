# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gpaste
pkgver=45.1
pkgrel=2
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD-2-Clause)
arch=(x86_64)
depends=(
  cairo
  dconf
  gcc-libs
  gcr-4
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  gtk4
  libadwaita
  libx11
  libxi
)
makedepends=(
  git
  gjs
  gnome-control-center
  gobject-introspection
  meson
  pango
  systemd
  vala
)
optdepends=("wgetpaste: Upload clipboard contents")
source=("git+https://github.com/keruspe/gpaste#tag=v$pkgver")
b2sums=('e29065afdf50d95c282978647753b0a8e03b9460258df126524014b9dbe49b3251b9ac0d18b93321c872a1d590370ecc5d6603c48673817823f93cd8c57c1551')

prepare() {
  cd gpaste
  git cherry-pick -n 862ea0f6cd22c846aa3dde2c2817ee05a0623407 # 47 compat
}

build() {
  arch-meson gpaste build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 gpaste/COPYING
}

# vim:set sw=2 sts=-1 et:
