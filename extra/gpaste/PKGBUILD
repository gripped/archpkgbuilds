# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gpaste
pkgver=45.3
pkgrel=1
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD-2-Clause)
arch=(x86_64)
depends=(
  cairo
  dconf
  gcc-libs
  gcr-4
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  gtk4
  libadwaita
  libx11
  libxi
)
makedepends=(
  git
  gjs
  gnome-control-center
  gobject-introspection
  meson
  pango
  systemd
  vala
)
optdepends=("wgetpaste: Upload clipboard contents")
source=("git+https://github.com/keruspe/gpaste#tag=v$pkgver")
b2sums=('0d1918b67076f191cfa0d0b65062aa188ca61b893f93dade0536cca0a2cd785c6c237e8777e5fd0e8de9d499e1e12fba67cd184932181e5eb40451e685d98500')

prepare() {
  cd gpaste
}

build() {
  arch-meson gpaste build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 gpaste/COPYING
}

# vim:set sw=2 sts=-1 et:
