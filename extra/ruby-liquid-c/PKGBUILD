# Maintainer: Bert Peters <bertptrs@archlinux.org>
_name=liquid-c
pkgname=ruby-$_name
pkgver=4.2.0
pkgrel=5
pkgdesc='Liquid performance extension in C'
url='https://github.com/shopify/liquid-c'
arch=('x86_64')
license=('MIT')
makedepends=('ruby-rdoc')
depends=('ruby' 'ruby-liquid')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/Shopify/${_name}/archive/v${pkgver}.tar.gz)
sha512sums=('4a138eeea4fe4ea3dbfe32f790f92b0aada461daf017edd74c489c09f94d0ce5a7336a454535dbc9061915d448e4479483371e209fc38a29558d4561da001c13')

prepare() {
  cd "${_name}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_name}.gemspec"

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|git ls-files -z|find . -print0 -type f -not -path "*/\.git/*"|' "${_name}.gemspec"
}

build() {
  cd "${_name}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build "${_name}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_name}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_name}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_name}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

# Tests currently broken due to too old ruby-ruby_memcheck version
check() {
  cd "${_name}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  # The original tests for this project don't work with modern libraries but it
  # still works, so just verify that the module loads normally.
  GEM_HOME="tmp_install/${_gemdir}" ruby -e 'require "liquid/c"; puts Liquid::C::VERSION'
}

package() {
  cd "${_name}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE.txt --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
