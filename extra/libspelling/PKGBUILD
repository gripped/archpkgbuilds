# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libspelling
pkgname=(
  libspelling
  libspelling-docs
)
pkgver=0.4.3
pkgrel=1
pkgdesc="Spellcheck library for GTK 4"
url="https://gitlab.gnome.org/chergert/libspelling"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  enchant
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  icu
  pango
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  vala
)
checkdepends=(
  aspell
  aspell-en
  enchant
  hspell
  hunspell
  nuspell
  libvoikko
)
source=("git+https://gitlab.gnome.org/chergert/libspelling.git#tag=$pkgver")
b2sums=('e20d41d01169be9cbdcdc683a3863d23e373cf84b15fe0ac5e96eeacc08a19d19ef0cf881fe962f3aa288980831645a9b0e04a7f2790f200d0f72c4dc7faa480')

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libspelling() {
  provides=(libspelling-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libspelling-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
