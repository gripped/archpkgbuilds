# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=6.1.4
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gio-qt' 'qt6-base' 'qt6-declarative'
         'deepin-shell' 'deepin-tray-loader')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'extra-cmake-modules')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/dde-clipboard.git#tag=$pkgver")
sha512sums=('5f64ca0833eb73dda0fd49c19eec77a4a0727d7526033653162ccda0851bfbc28da31d53da7e9ed5eae9100ae8c4d7b06ddfaed73ccf66466f82166bd4a5ebf4')

prepare() {
  mkdir -p build
  cd dde-clipboard
  # qt 6.10
  git cherry-pick -n 88dc9376619350325ddb9d4531b801d16c53a871
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
