# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=6.1.0
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gio-qt' 'qt6-base' 'qt6-declarative' 'qt6-wayland'
         'deepin-shell' 'deepin-tray-loader')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'extra-cmake-modules')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/dde-clipboard.git#tag=$pkgver")
sha512sums=('e98ccbba5ec03b1f23b0abb05173a3acdd4299ca2e224cffff2b5a68875ee1875fd7cb55837906df223764bda533e442bd36d8eb3323519707387072a185650d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
