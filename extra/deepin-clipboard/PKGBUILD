# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=6.1.1
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gio-qt' 'qt6-base' 'qt6-declarative'
         'deepin-shell' 'deepin-tray-loader')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'extra-cmake-modules')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/dde-clipboard.git#tag=$pkgver")
sha512sums=('5d20957960097e3eeedc9bf8f45756039a797b339f13db5e012e1c17282a21f1099797b50a1474961fc9dba607b84c2ca9197b9080cadbe9ce7b8bda33a084b2')

prepare() {
  mkdir -p build
  cd dde-clipboard
  # qt 6.10
  git cherry-pick -n 88dc9376619350325ddb9d4531b801d16c53a871
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
