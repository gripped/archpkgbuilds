# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=6.1.2
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gio-qt' 'qt6-base' 'qt6-declarative'
         'deepin-shell' 'deepin-tray-loader')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'extra-cmake-modules')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/dde-clipboard.git#tag=$pkgver")
sha512sums=('bc9872119be1b562a073572727b42c20ea870b7c7c895001bf763a331fc5b8d215a006e7104d73fcc2345954026b5752e298018839c3536e99dc2503a45e3536')

prepare() {
  mkdir -p build
  cd dde-clipboard
  # qt 6.10
  git cherry-pick -n 88dc9376619350325ddb9d4531b801d16c53a871
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
