# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=6.1.5
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gio-qt' 'qt6-base' 'qt6-declarative'
         'deepin-shell' 'deepin-tray-loader')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'extra-cmake-modules')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/dde-clipboard.git#tag=$pkgver")
sha512sums=('5de450cde34f9f7c75eab24d2018a1e3f99b000bd44df8cf18468d3f1d2acace83f614a974435ccdf47de600c5723a332a312c65151e825cd01634ca93477d86')

prepare() {
  mkdir -p build
  cd dde-clipboard
  # qt 6.10
  git cherry-pick -n 88dc9376619350325ddb9d4531b801d16c53a871
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
