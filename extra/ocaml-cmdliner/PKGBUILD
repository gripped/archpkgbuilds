# Maintainer: Jürgen Hötzel <juergen@archlinux.org>

_pkgname=cmdliner
pkgname=ocaml-${_pkgname}
pkgver=2.1.0
pkgrel=1
pkgdesc="Declarative definition of command line interfaces for OCaml"
arch=('x86_64')
url="https://erratique.ch/software/cmdliner"
license=('ISC')
depends=('ocaml')
makedepends=('ocaml-findlib' 'ocamlbuild' 'ocaml-topkg' 'dune')
options=('!strip')
source=("https://erratique.ch/software/${_pkgname}/releases/${_pkgname}-${pkgver}.tbz")
sha256sums=('8814c614cd43d52fd1ebc8af5a31d9125c214c498fa605660e4ed195ff8434e9')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  dune build --profile release
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" dune install --prefix "/usr" --libdir "/usr/lib/ocaml" --docdir "/usr/share/doc"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  mv "${pkgdir}/usr/share/doc/$_pkgname/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
