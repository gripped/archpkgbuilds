# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: jgmdev <jgmdev at gmail dot com>
# Contributor: TÃ©rence Clastres <t dot clastres at gmail dot com>
# Contributor: redtide <redtid3 at gmail dot com>

pkgname=lite-xl
pkgver=2.1.7
pkgrel=3
pkgdesc='A lightweight text editor written in Lua'
arch=(x86_64 aarch64)
url="https://$pkgname.com"
license=(MIT)
depends=(freetype2
         glibc # libc.so
         lua
         pcre2
         sdl2)
makedepends=(git # used by Meson to fetch submodule, TODO: move to source()
             meson)
conflicts=(lite)
provides=(lite)
_archive="$pkgname-$pkgver"
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('69d1ce4c1d148d382ccb06f45feca2565c5c8fe9d0b1b9bc1cbe014f6826ce6b')

build() {
	local meson_opts=(
		use_system_lua=true
	)
	arch-meson "$_archive" ${meson_opts[@]/#/-D } build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir" --skip-subprojects
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$_archive/LICENSE"
}
