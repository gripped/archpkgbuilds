# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=js80p
pkgver=3.4.0
pkgrel=1
pkgdesc='A MIDI driven, performance oriented, versatile synthesizer'
arch=('x86_64')
url='https://attilammagyar.github.io/js80p/'
license=('GPL-3.0-only')
groups=('pro-audio' 'vst3-plugins')
depends=(
  'glibc'
  'gcc-libs'
  'libxcb'
  'cairo'
)
makedepends=(
  'git'
  'alsa-lib'
  'libx11'
  'zenity'
  'kdialog'
)
install=js80p.install
source=(
  "$pkgname::git+https://github.com/attilammagyar/js80p#tag=v$pkgver"
  'do-not-run-cppcheck.patch'
)
sha512sums=('5c7d72607577c76ceb0c0dedc04199057eaabb99fdc1f5c8c13b1ceef279910525911b60f21f64051e535e3411a3f030a8f81947e4f9f978fdcb39002683567d'
            'e3f6e69f8fd81bd28883a6e74f19e72a887f1c53eb695cd3e6eecd8d15ce457417dbdc635bf8273b4f51490284033112723db288689d80cf39f59b54cfc439f2')
b2sums=('4f20761b145de51d778e5f7f4f1ecdaa6b1e4466955c5cf1080e92468d99814d5d673ab45c185a292b26c6540d1c93f2291a093dceaf6d0bb372458dfd3e0915'
        '9042a7bdde673d0a6884b047f9d37362cb4b4a1b00e13129c73ceb233a4235a677c990f872f58d9c3bf79b74786f6ccc96426a742baaf9b9cea27d475a9b931f')

prepare() {
  cd "$pkgname"

  # remove cppcheck from makedepends
  patch -p1 -i "$srcdir/do-not-run-cppcheck.patch"
}

build() {
  cd "$pkgname"

  # supports AVX, but that's not in x86_64_v1
  make \
    SYS_LIB_PATH=/usr/lib \
    TARGET_PLATFORM="$CARCH-gpp" \
    INSTRUCTION_SET=sse2 \
    VERSION_STR=$(git describe --tags) \
    VERSION_INT=$(git describe --tags | sed "s/[^0-9]//g") \
    vst3
}

package() {
  cd "$pkgname"

  # vst3
  install -vDm644 \
    "dist/js80p-dev-linux-$CARCH-sse2-vst3_single_file/js80p.vst3" \
    "$pkgdir/usr/lib/vst3/$pkgname.vst3/Contents/$CARCH-linux/$pkgname.so"

  # presets
  install -vDm644 -t "$pkgdir/usr/share/$pkgname/presets" presets/*.js80p

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
