# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=js80p
pkgver=3.5.0
pkgrel=1
pkgdesc='A MIDI driven, performance oriented, versatile synthesizer'
arch=(x86_64)
url='https://attilammagyar.github.io/js80p/'
license=(GPL-3.0-only)
groups=(pro-audio vst3-plugins)
depends=(
  glibc
  gcc-libs
  libxcb
  cairo
)
makedepends=(
  git
  alsa-lib
  libx11
  zenity
  kdialog
)
install=js80p.install
source=(
  "$pkgname::git+https://github.com/attilammagyar/js80p#tag=v$pkgver"
  do-not-run-cppcheck.patch
  suppress-deprecated-declarations.patch
)
sha512sums=('5dea9a5f28a62e8c8689a5e29d850f17cae2840abb22b638dbe11de96214eef19e027301a196ddcc0f7ebba06f9efa437b3ddf7be9c2816112a2d9e0a36022e0'
            'e3f6e69f8fd81bd28883a6e74f19e72a887f1c53eb695cd3e6eecd8d15ce457417dbdc635bf8273b4f51490284033112723db288689d80cf39f59b54cfc439f2'
            'f5795b93060227ccfc3f6a1ad4ad2534848d1fb8ae012e4eda86d0c5cab5894f85c96a362c0e1fcc5dad24d37e4bb7cc924113b0ea469d1835c67a25d69d6410')
b2sums=('965d139f59a8fddc9de04df54c525871c2c42e3f93cac7cfe73a0ec8413fd63ada4a31be99ac5e72b59f4e97fce3332d352ca0e2040dc03ab5d41a0fdfaee16b'
        '9042a7bdde673d0a6884b047f9d37362cb4b4a1b00e13129c73ceb233a4235a677c990f872f58d9c3bf79b74786f6ccc96426a742baaf9b9cea27d475a9b931f'
        'c1050e218c4150261a1efeefd98f3682ad3bd50cbd07231cade9b1b65194e2aea7ac64d56a12824b22a4758065c1e218a4f6f80962d159cf55375879683f043f')

prepare() {
  cd "$pkgname"

  # remove cppcheck from makedepends
  patch -p1 -i "$srcdir/do-not-run-cppcheck.patch"

  # suppress deprecated declarations
  patch -p1 -i "$srcdir/suppress-deprecated-declarations.patch"
}

build() {
  cd "$pkgname"

  # supports AVX, but that's not in x86_64_v1
  make \
    SYS_LIB_PATH=/usr/lib \
    TARGET_PLATFORM="$CARCH-gpp" \
    INSTRUCTION_SET=sse2 \
    VERSION_STR=$(git describe --tags) \
    VERSION_INT=$(git describe --tags | sed "s/[^0-9]//g") \
    vst3
}

package() {
  cd "$pkgname"

  # vst3
  install -vDm644 \
    "dist/js80p-dev-linux-$CARCH-sse2-vst3_single_file/js80p.vst3" \
    "$pkgdir/usr/lib/vst3/$pkgname.vst3/Contents/$CARCH-linux/$pkgname.so"

  # presets
  install -vDm644 -t "$pkgdir/usr/share/$pkgname/presets" presets/*.js80p

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
