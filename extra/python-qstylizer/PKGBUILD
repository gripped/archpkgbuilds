# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=qstylizer
pkgname=python-${_pkg}
pkgver=0.2.4
pkgrel=3
pkgdesc="Qt stylesheet generation utility for PyQt/PySide"
arch=(any)
url="https://github.com/blambright/qstylizer"
license=(MIT)
depends=(python-inflection python-tinycss2)
makedepends=(python-pbr
             python-setuptools
             python-build
             python-installer
             python-wheel
)
checkdepends=(
    python-pytest
    python-pytest
    python-pytest-mock
)
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('5ad15eeafdb140af3711531a1468382e3a15d8a416e5b1bcc6bf28693a7c37ee')

prepare() {
  cd ${_pkg}-${pkgver}
  sed -e 's|\.called_once_with|.assert_called_once_with|' -i test/unit/test_style.py # Fix test with python 3.12
}

build() {
  cd ${_pkg}-${pkgver}
  export PBR_VERSION=${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}"/build/lib/ pytest
}

package() {
  cd ${_pkg}-${pkgver}
  export PBR_VERSION=${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
