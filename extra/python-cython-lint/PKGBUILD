# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-cython-lint
pkgver=0.16.7
pkgrel=1
pkgdesc='A tool to lint Cython files'
arch=(any)
url='https://github.com/MarcoGorelli/cython-lint'
license=(MIT)
depends=(
  python
  cython
  python-tokenize-rt
  python-pycodestyle
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
  python-hatchling
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('30cd69e1123da3be532337d2d95d905cc1603272212b62811904aaa0d992c052a911d2c1694f933e7052989924b1a18df824edf24dad8a2b45acf2b9ba66f6dd')
b2sums=('66b8c0e27100a2cff00015d36c4c8e5ef225e283c1853607426423e236aca98955ea5063cdbb5d7a059e70b23444081a495f49ab0ece9a9bc2b283da5d0b87d6')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd)" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
