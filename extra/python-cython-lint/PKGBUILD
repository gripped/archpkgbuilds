# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-cython-lint
pkgver=0.17.0
pkgrel=1
pkgdesc='A tool to lint Cython files'
arch=(any)
url='https://github.com/MarcoGorelli/cython-lint'
license=(MIT)
depends=(
  python
  cython
  python-tokenize-rt
  python-pycodestyle
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
  python-hatchling
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('b6782dd797f00d34d23501cdd96bbde15f1815d9094df15ad3fc7f1ce5a2e9776fd9b8dfc6f9fef9c31f9e2deb87e75884e0e98e4910a389227dfba7fc3dca4e')
b2sums=('d455f55b589d8161a63ec0a3438d849f03f40e4e28ec9062b849e77d991dbaa89c30f2e892b03aa939d93273473f8a0734acd9cd6a58bccadc7cc424020ca355')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd)" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
