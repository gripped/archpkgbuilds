# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-cython-lint
pkgver=0.19.0
pkgrel=1
pkgdesc='A tool to lint Cython files'
arch=(any)
url='https://github.com/MarcoGorelli/cython-lint'
license=(MIT)
depends=(
  python
  cython
  python-tokenize-rt
  python-pycodestyle
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
  python-hatchling
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('636dacef5dcba442d512b46d9416d8e9b2e9381aaf895bb1db1aeee669b40182f9eba887b660239c28c986e05bdec413c3ea5cca71c228972a1aa2cc19056dd6')
b2sums=('e9a85660b026c5cefe4f67e84709b776ffa43f20f626f84d655a80d64b416c43246e5fb892ce7101af20cf083ff89afa8140579515e94fcb3cfdde8e999ab64a')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd)" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
