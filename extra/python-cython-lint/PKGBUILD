# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-cython-lint
pkgver=0.18.0
pkgrel=1
pkgdesc='A tool to lint Cython files'
arch=(any)
url='https://github.com/MarcoGorelli/cython-lint'
license=(MIT)
depends=(
  python
  cython
  python-tokenize-rt
  python-pycodestyle
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
  python-hatchling
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('77c02bad59e43a8f23dbec6123bbaa894874ccd181b3ea39569764f8a5eb832123594f08678d1c0f7b34f9322165f3803f0fcf34028ce578105d3d07714d9047')
b2sums=('bfae8e062e2365b79b3362888e278083630963e2cf18cc339a12af6daa29b890626b76ac10217304a04fd8154fa6829477eed35998932c4f1ef28b79f47ab417')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd)" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
