# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-cython-lint
pkgver=0.18.1
pkgrel=2
pkgdesc='A tool to lint Cython files'
arch=(any)
url='https://github.com/MarcoGorelli/cython-lint'
license=(MIT)
depends=(
  python
  cython
  python-tokenize-rt
  python-pycodestyle
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
  python-hatchling
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('61e2893181231e5bb38c1fc87b31b85bc91de44729093df9909c01cc521a4a34a301e06bf7f0364b91038749b4e962e3655c16ffa0dbb07e0ac75f721f4f8638')
b2sums=('c5db88ecca6aa726e094b68e02abecd696009ece8833b01e80e62ef6166939cf908640cc8608051af9c8e548a08284069877e8256816f49ce038caac2f75383b')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd)" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
