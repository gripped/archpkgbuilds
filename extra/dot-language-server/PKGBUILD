# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=dot-language-server
pkgver=3.0.2
pkgrel=1
pkgdesc="A language server for the DOT language"
arch=(x86_64)
url="https://github.com/nikeee/dot-language-server"
license=(MIT)
depends=(
  gcc-libs
  glibc
  nodejs
)
makedepends=(npm)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('7c4f3ce5a6393e54e142b35562aab0cc17c73df5664a38409f850e9402e846ba47447838db3779030b98bfb9a5017c4db55fd52172b09f9334110fccb10aaaff')
b2sums=('9a24e54413eb0bad87f7481baf37dcb2bd42b62082dae8bf0574d73cbf3cc7231cd73a9a306111ad7856a7fea948a55e91e79d3c9fc6f0d6b24d2fb905a0ec62')

build() {
  cd $pkgname-$pkgver
  npm install --cache "$srcdir"/npm-cache
  npm run build --cache "$srcdir"/npm-cache
}

check() {
  cd $pkgname-$pkgver
  npm test
}

package() {
  local find_files=(
    -iname '*Makefile*' -o
    -iname '*appveyor.yml' -o
    -iname '*.babelrc' -o
    -iname '*.bak' -o
    -iname '*bower.json' -o
    -iname '*.c' -o
    -iname '*.cc' -o
    -iname '*.cpp' -o
    -iname '*.md' -o
    -iname '*.markdown' -o
    -iname '*.rst' -o
    -iname '*.nycrc' -o
    -iname '*.npmignore' -o
    -iname '*.editorconfig' -o
    -iname '*.el' -o
    -iname '*.eslintignore' -o
    -iname '*.eslintrc*' -o
    -iname '*.fimbullinter.yaml' -o
    -iname '*.gitattributes' -o
    -iname '*.gitignore' -o
    -iname '*.gitmodules' -o
    -iname '*.h' -o
    -iname '*.html' -o
    -iname '*.jshintrc' -o
    -iname '*.jscs.json' -o
    -iname '*.log' -o
    -iname '*logo.svg' -o
    -iname '*.nvmrc' -o
    -iname '*.o' -o
    -iname '*package-lock.json' -o
    -iname '*.travis.yml' -o
    -iname '*.prettierrc' -o
    -iname '*.sh' -o
    -iname '*.tags*' -o
    -iname '*.tm_properties' -o
    -iname '*.wotanrc.yaml' -o
    -iname '*tsconfig.json' -o
    -iname '*yarn.lock'
  )
  local find_dirs=(
    -iwholename '*.github' -o
    -iwholename '*.idea' -o
    -iwholename '*.tscache' -o
    -iwholename '*/man' -o
    -iwholename '*/test' -o
    -iwholename '*/scripts' -o
    -iwholename '*/git-hooks'
  )

  cd $pkgname-$pkgver
  # remove unneeded files
  find node_modules -type f \( "${find_files[@]}" \) -delete
  find node_modules -type d \( "${find_dirs[@]}" \) -exec rm -rvf {} +

  install -vdm 755 "$pkgdir/usr/lib/node_modules/$pkgname/"
  # copy vendored modules, lib and entry point
  cp -av {bin,doc,lib,node_modules,package.json} "$pkgdir/usr/lib/node_modules/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vdm 755 "$pkgdir/usr/bin/"
  ln -sv /usr/lib/node_modules/$pkgname/bin/server.js "$pkgdir/usr/bin/$pkgname"
}
