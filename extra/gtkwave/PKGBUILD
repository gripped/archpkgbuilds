# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jared Casper <jaredcasper@gmail.com>

pkgname=gtkwave
pkgver=3.3.124
pkgrel=1
pkgdesc="A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files"
arch=('x86_64')
url="http://${pkgname}.sourceforge.net"
license=('GPL-2.0-or-later AND MIT')
depends=('bzip2' 'xz' 'gtk3' 'dconf')
makedepends=('gperf')
source=("https://downloads.sourceforge.net/project/${pkgname}/gtkwave-gtk3-${pkgver}/gtkwave-gtk3-${pkgver}.tar.gz")
sha256sums=('4b1590e05e3e3ae26e34fa80aff369254397379b2f970cfa99150b8b97e535cf')

build() {
	cd "gtkwave-gtk3-${pkgver}/"
	./configure \
		--prefix=/usr \
		--disable-tcl \
		--disable-mime-update \
		--with-gsettings \
		--enable-gtk3

	make
}

package() {
	cd "gtkwave-gtk3-${pkgver}/"
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE.TXT -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
