# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=rime-prelude
pkgname=(rime-prelude librime-data)
pkgver=0.0.0.20230921
_commit=ccb0fc092ccdfa95be74fbb42936d036edc2683c
pkgrel=1
pkgdesc="Essential files for building up your Rime configuration"
arch=('any')
url="https://github.com/rime/rime-prelude"
license=('LGPL')
makedepends=('git')
source=("git+https://github.com/rime/rime-prelude.git#commit=$_commit")
sha512sums=('d7295459b9955838ed7628073eb597d6fefc8d20ff8f7baae65e23ab6cb13fd730cb1db083844748dba18646eeacdec3fb1365bc2057f4eaee4b9d8d4a31eefd')

package_rime-prelude() {
  pkgdesc="Essential files for building up your Rime configuration"

  cd $pkgname
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
}

package_librime-data() {
  pkgdesc="Meta package of preset librime data"
  depends=('rime-prelude' 'rime-essay' 'rime-luna-pinyin' 'rime-bopomofo' 'rime-cangjie' 'rime-stroke' 'rime-terra-pinyin')
  replaces=('brise')
  conflicts=('brise')
}
