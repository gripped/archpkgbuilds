# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=rime-prelude
pkgname=(rime-prelude librime-data)
pkgver=0.0.0.20230922
_commit=ad742b87cf9a902287146ee3d3cdba2033f8b835
pkgrel=1
pkgdesc="Essential files for building up your Rime configuration"
arch=('any')
url="https://github.com/rime/rime-prelude"
license=('LGPL')
makedepends=('git')
source=("git+https://github.com/rime/rime-prelude.git#commit=$_commit")
sha512sums=('a14b54cd7d6d45b7c11e9c4ba0b67c641dbd48a3c403ebecb6cbc94648a668b58ba4f27f73594344f981473f72d559f8c9aa4253d21de5da6ab5bb8ca2f67e8a')

package_rime-prelude() {
  pkgdesc="Essential files for building up your Rime configuration"

  cd $pkgname
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
}

package_librime-data() {
  pkgdesc="Meta package of preset librime data"
  depends=('rime-prelude' 'rime-essay' 'rime-luna-pinyin' 'rime-bopomofo' 'rime-cangjie' 'rime-stroke' 'rime-terra-pinyin')
  replaces=('brise')
  conflicts=('brise')
}
