# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=0.7.1
pkgrel=3
pkgdesc='Podcast application for GNOME'
arch=(x86_64)
url='https://apps.gnome.org/Podcasts/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  sqlite
)
makedepends=(
  git
  meson
  rust
)
checkdepends=(xorg-server-xvfb)
source=(
  "git+https://gitlab.gnome.org/World/podcasts.git#tag=$pkgver"
  gnome-podcasts-desktop-category.patch
)
b2sums=(
  0ef2173b4661b709f0bb976cc82cbbd32571b9ae2d09feacb3100554b6d3012a1aaf4729d49d716468fa8bd7062096b53e3316f7f98c5ef71ea0d231544887a4
  bc527c5351b13ee1813f30d7e17b58fa5a521d18d2982553a93f6640966b3440798fa2ae3cd13be969be89976657306985eb09dd62a686b23124906093a772fb
)

prepare() {
  cd podcasts

  # https://gitlab.gnome.org/World/podcasts/-/merge_requests/394
  git apply -3 ../gnome-podcasts-desktop-category.patch
}

build() {
  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  arch-meson podcasts build
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
