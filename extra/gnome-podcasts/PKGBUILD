# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=25.2
pkgrel=1
pkgdesc='Podcast application for GNOME'
arch=(x86_64)
url='https://apps.gnome.org/Podcasts/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  sqlite
)
makedepends=(
  appstream
  git
  meson
  rust
)
checkdepends=(xorg-server-xvfb)
source=("git+https://gitlab.gnome.org/World/podcasts.git#tag=$pkgver")
b2sums=(8beb835f7400d2e5ea7a6a62a9fa18ceb11f2f9a184aad31e582f74b106b682595e66a1b98b28d2fe2061217f16679ce24ebbe3d524f22253bdc92807fc93027)

prepare() {
  cd podcasts

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson podcasts build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
