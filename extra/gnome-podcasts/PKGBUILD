# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=0.7.2
pkgrel=1
pkgdesc='Podcast application for GNOME'
arch=(x86_64)
url='https://apps.gnome.org/Podcasts/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  sqlite
)
makedepends=(
  appstream
  git
  meson
  rust
)
checkdepends=(xorg-server-xvfb)
source=(
  "git+https://gitlab.gnome.org/World/podcasts.git#tag=$pkgver"
  gnome-podcasts-desktop-category.patch
)
b2sums=('983c11796641978fb1b840d2cceba89f1cb24e2c8403c617d07b2b5f4ee84806bf62a6d747085fd9788832e8142d921d16f5c8c67a40fea47cceea590eaa8b5f'
        'bc527c5351b13ee1813f30d7e17b58fa5a521d18d2982553a93f6640966b3440798fa2ae3cd13be969be89976657306985eb09dd62a686b23124906093a772fb')

prepare() {
  cd podcasts

  # https://gitlab.gnome.org/World/podcasts/-/merge_requests/394
  git apply -3 ../gnome-podcasts-desktop-category.patch

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson podcasts build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
