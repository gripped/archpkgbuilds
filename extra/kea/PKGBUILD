# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: nfnty

pkgbase=kea
pkgname=("${pkgbase}" "${pkgbase}-docs")
pkgver=3.0.0
pkgrel=5
pkgdesc="High-performance, extensible DHCP server engine from ISC, supporting both DHCPv4 and DHCPv6"
url="https://kea.isc.org"
arch=('x86_64')
license=('MPL-2.0')
makedepends=('boost' 'mariadb' 'meson' 'postgresql' 'python' 'python-sphinx' 'python-sphinx_rtd_theme' 'doxygen' 'graphviz'
             'log4cplus' 'mariadb-libs' 'openssl' 'postgresql-libs' 'texlive-bibtexextra' 'texlive-basic' 'texlive-fontsextra'
             'texlive-formatsextra' 'texlive-games' 'texlive-humanities' 'texlive-latexextra' 'texlive-music' 'texlive-pictures'
             'texlive-pstricks' 'texlive-publishers' 'texlive-science')
checkdepends=('gtest' 'procps-ng')
source=("https://ftp.isc.org/isc/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.xz"{,.asc}
        'tmpfiles-kea.conf'
        'kea-dhcp4.service'
        'kea-dhcp6.service'
        'kea-dhcp-ddns.service'
        'kea-ctrl-agent.service')
b2sums=('1d08d226b2e8523355c734968032ffa30035b6daebe4b9ce2db45fee887100d54b422f7cb8771d1e7b0b04e0f13329b8ddd03c4e7b81e73bd60f2435623ccd89'
        'SKIP'
        '485e1700f842e65fbcf3f9e5f67abe2da3b8ea697fc7f852a8e7ad8a535975cdf919e1791deb0a530ff62b823e78d11c71c42d6e33c27f4e61bb6872a2d0f6b9'
        '4000c128807c0a4dd2c2e48b47b8effe1b4e011e517575bb824601f9ae5121ee5d64ca0643b0192cad75bc1daa4a17680e171b3497b9926fcff10fa05e834e7e'
        'd08096cb995b0bf0133ba2c287df3ec44a6090a041e6fc0676422243a7b84c7cfc03f9b8a1739a89216d9bf81cf9f2fdb222dc73f3ee3b486b349679d751e74d'
        '65660fb97706c8ccf0c18cbdf0a0aaedead4ebc23276ddcbedbd287996c51b7c5c46e552fa9b9ae2a2589a9c609bc921f8d696e2ac66df84d72ee25b4e449566'
        '0385f582889ce832e3aaabb413128503cb83e5cad27a89b8c91025bcf2be716994c117e212cc119f2a1d4bc004401ab0dffe765fd5da5e644f3654a1f1091d54')
validpgpkeys=('BE0E9748B718253A28BB89FFF1B11BF05CF02E57'  # Internet Systems Consortium, Inc. (Signing key, 2017-2018) <codesign@isc.org>
              'AE3FAC796711EC59FC007AA474BB6B9A4CBB3D38'  # Internet Systems Consortium, Inc. (Signing key, 2019-2020) <codesign@isc.org>
              '7E1C91AC8030A5A59D1EFAB9750F3C87723E4012'  # Internet Systems Consortium, Inc. (Signing key, 2021-2022) <codesign@isc.org>
              '090A2A07923F925B5767803A42E5DF78C83271DB'  # Marcin Godzina (Code-Signing Key) <mgodzina@isc.org>
              'DA6A3508E672A49DD382AFD95B8F4D91B88ED909'  # Andrei Pavel (Code-Signing Key) <andrei@isc.org>
              '0259A33B5F5A3A4466CF345C7A5E084CACA51884') # Wlodek Wencel (Code-Signing Key) <wlodek@isc.org>

build() {
	arch-meson "${pkgbase}-${pkgver}" build -D netconf=disabled -D tests=enabled
	meson compile -C build
	meson compile -C build devel doc
}

check() {
	# Ignore tests that require live DBs
	meson test -C build $(meson test -C build --list | \
		grep -v 'kea / kea-mysql-tests' | \
		grep -v 'kea / kea-pgsql-tests' | \
		grep -v 'kea / kea-dhcp-tests' | \
		grep -v 'kea / dhcp-mysql-lib-tests' | \
		grep -v 'kea / dhcp-pgsql-lib-tests' | \
		grep -v 'kea / dhcp-forensic-log-libloadtests' | \
		grep -v 'kea / dhcp-lease-query-tests' | \
		grep -v 'kea / kea-dhcp4-tests' | \
		grep -v 'kea / kea-dhcp6-tests' | \
		grep -v 'kea:shell-tests / kea_admin_mysql_tests.sh' | \
		grep -v 'kea:shell-tests / kea_admin_pgsql_tests.sh' | \
		awk '{print $NF}')
}

package_kea() {
	depends=('log4cplus' 'mariadb-libs' 'openssl' 'postgresql-libs')
	optdepends=('mariadb: lease information database'
	            'postgresql: lease information database'
	            'python: to use kea-shell'
	            'krb5: Kerberos support'
	            'kea-docs: user and developer documentation')
	backup=('etc/kea/kea-dhcp4.conf'
	        'etc/kea/kea-dhcp6.conf'
	        'etc/kea/kea-dhcp-ddns.conf'
	        'etc/kea/keactrl.conf'
	        'etc/kea/kea-ctrl-agent.conf')

	meson install -C build --destdir "${pkgdir}"

	install -Dm 644 tmpfiles-kea.conf "${pkgdir}/usr/lib/tmpfiles.d/${pkgbase}.conf"
	install -Dm 644 kea-dhcp4.service "${pkgdir}/usr/lib/systemd/system/kea-dhcp4.service"
	install -Dm 644 kea-dhcp6.service "${pkgdir}/usr/lib/systemd/system/kea-dhcp6.service"
	install -Dm 644 kea-dhcp-ddns.service "${pkgdir}/usr/lib/systemd/system/kea-dhcp-ddns.service"
	install -Dm 644 kea-ctrl-agent.service "${pkgdir}/usr/lib/systemd/system/kea-ctrl-agent.service"

	# Do not package /var/run
	rm -rf "${pkgdir}/var/run"

	# Split docs in a separate package
	rm -rf "${pkgdir}/usr/share/"{doc,man}

	# Upstream changed the default permissions of all directories to 750 to address [CVE-2025-32803]
	# To avoid pacman warning notices about top level directories' permissions differing between the package and the filesystem,
	# yet comply with upstream change regarding [CVE-2025-32803], we are switching back filesystem's directory tree to 755
	# but leave kea's directories in 750
	for dir in etc \
		   usr{,/bin,/share,/include,/lib{,/pkgconfig,/python3.13{,/site-packages}}} \
		   var{,/lib,/log}
	do
		chmod 755 "${pkgdir}/${dir}"
	done
}

package_kea-docs() {
	pkgdesc="${pkgdesc} (user and developer documentation)"
	provides=('kea-devel-docs')
	replaces=('kea-devel-docs')

	install -d "${pkgdir}/usr/share/doc/${pkgbase}/"

	cp -r build/doc/devel/html "${pkgdir}/usr/share/doc/${pkgbase}/devel"
	cp -r build/doc/sphinx/_build/html "${pkgdir}/usr/share/doc/${pkgbase}/html"
	cp -r "${pkgbase}-${pkgver}/doc/examples" "${pkgdir}/usr/share/doc/${pkgbase}/examples"
	rm -f "${pkgdir}/usr/share/doc/kea/devel/doxygen"{,-error}.log

	install -Dm 644 build/doc/sphinx/_build/man/*.1 -t "${pkgdir}/usr/share/man/man1/"
	install -Dm 644 build/doc/sphinx/_build/man/*.8 -t "${pkgdir}/usr/share/man/man8/"
}
