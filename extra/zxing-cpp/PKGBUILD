# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Maxime Arthaud <maxime@arthaud.me>

pkgname=zxing-cpp
pkgver=3.0.2
pkgrel=1
pkgdesc='An open-source, multi-format linear/matrix barcode image processing library implemented in C++'
arch=(x86_64)
url='https://github.com/zxing-cpp/zxing-cpp'
license=(Apache-2.0)
depends=(glibc
         libgcc
         libstdc++
         zint)
makedepends=(cmake
             git
             libpng
             opencv
             qt6-base
             qt6-declarative
             qt6-multimedia
             stb)
checkdepends=(gtest)
optdepends=('qt6-multimedia: for ZXingQtCamReader')
source=(git+https://github.com/zxing-cpp/zxing-cpp#tag=v$pkgver
        system-stb.patch)
sha256sums=('6e9eccb47422cf8d5c89c4f29a29c7c1688903202df844b29622aaa187982d12'
            'cc83c917902c3551ff0a4b7752baf895044d40901eba74678aeef19e236266f1')

prepare() {
  cd $pkgname
  patch -p1 -i ../system-stb.patch # Do not download stb at build time
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DZXING_EXAMPLES=ON \
    -DZXING_EXAMPLES_QT=ON \
    -DZXING_UNIT_TESTS=ON \
    -DZXING_C_API=ON \
    -DZXING_USE_BUNDLED_ZINT=OFF \
    -DZXING_WRITERS=BOTH
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
