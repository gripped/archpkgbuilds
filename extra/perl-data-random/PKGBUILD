# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Kogan <michael dot kogan at gmx dot net>

pkgname=perl-data-random
pkgver=0.14
pkgrel=1
pkgdesc="Data::Random - Perl module to generate random data"
arch=('any')
url="https://search.cpan.org/~barefoot/Data-Random/lib/Data/Random.pm"
license=('GPL' 'PerlArtistic')
depends=('perl-gd' 'perl-yaml-tiny')
makedepends=('perl-file-sharedir-install' 'perl-test-mocktime')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/B/BA/BAREFOOT/Data-Random-$pkgver.tar.gz")
sha512sums=('4e7f36dada36f971a047932d7b49d0c3aae71ec820f614251529acfe4013dfb0b9d8b5b00339fcce095a78e0f67e9f3b589ade38c560c1fa9c6aef2801e9bf95')

build() {
  cd Data-Random-$pkgver
  PERL_USE_UNSAFE_INC=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Data-Random-$pkgver
  make test
}

package() {
  cd Data-Random-$pkgver
  make DESTDIR="$pkgdir" install
}
