# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=tracer
pkgver=1.2
pkgrel=1
pkgdesc="Tracer finds outdated running applications in your system"
arch=('any')
url="http://tracer-package.com/"
license=('GPL')
depends=(python python-distro python-psutil python-six python-dbus pyalpm)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/FrostyX/tracer/archive/refs/tags/$pkgname-$pkgver-1.tar.gz")
sha512sums=('381dd61d9f4ceee85b7324ec64860a7d29bd8371fabd0223f744762d022b4b0bf07ecd4f58044dca4ebd14baa0f266ad5397835a972e6ac11d59dad6abed8368')

prepare() {
    cd "$pkgname-$pkgname-$pkgver-1"
    cp LICENSE LICENCE.md
}

check() {
    cd "$pkgname-$pkgname-$pkgver-1"
    PYTHONPATH=. pytest tests
}

build() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py build
}

package() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm644 LICENCE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
