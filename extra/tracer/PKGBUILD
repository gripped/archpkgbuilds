# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=tracer
pkgver=1.4
pkgrel=2
pkgdesc="Tracer finds outdated running applications in your system"
arch=('any')
url="http://tracer-package.com/"
license=('GPL')
depends=(python python-distro python-psutil python-six python-dbus pyalpm)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/FrostyX/tracer/archive/refs/tags/$pkgname-$pkgver-1.tar.gz")
sha512sums=('bc41c758568a233ba5bcbdb065c2a8bba166736a35188682227811b95ff094ad47246d46ee6e32e931f51f9fca793f12a9222ee1616ca23d462c73abf76899f6')

prepare() {
    cd "$pkgname-$pkgname-$pkgver-1"
    cp LICENSE LICENCE.md
}

check() {
    cd "$pkgname-$pkgname-$pkgver-1"
    PYTHONPATH=. pytest tests
}

build() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py build
}

package() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm644 LICENCE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
