# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Agorgianitis Loukas <agorglouk@gmail.com>

pkgname=tailwindcss-language-server
pkgver=0.0.26
pkgrel=1
pkgdesc='Tailwind CSS Language Server'
arch=(any)
url=https://github.com/tailwindlabs/tailwindcss-intellisense/tree/master/packages/tailwindcss-language-server
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  pnpm
)
source=("git+https://github.com/tailwindlabs/tailwindcss-intellisense.git#tag=@tailwindcss/language-server@v$pkgver")
b2sums=('65c0c04fff00de374d3192554043a340b14aac3486da35ed5c2cf00d103c8ae0822cb42d330619a0c7907db769c05cf376f4528560476dc8af94a20f762439cf')

prepare() {
  cd tailwindcss-intellisense
  pnpm install --frozen-lockfile
}

build() {
  cd tailwindcss-intellisense/packages/$pkgname
  pnpm build
}

package() {
  cd tailwindcss-intellisense/packages/$pkgname
  install -Dt "$pkgdir"/usr/bin bin/$pkgname
  install -Dm644 -t "$pkgdir"/usr/lib/node_modules/@tailwindcss/language-server \
    package.json
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE \
    ThirdPartyNotices.txt
}
