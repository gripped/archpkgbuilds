# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Agorgianitis Loukas <agorglouk@gmail.com>

pkgname=tailwindcss-language-server
pkgver=0.14.26
pkgrel=1
pkgdesc='Tailwind CSS Language Server'
arch=(any)
url=https://github.com/tailwindlabs/tailwindcss-intellisense/tree/main/packages/tailwindcss-language-server
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  pnpm
)
source=("git+https://github.com/tailwindlabs/tailwindcss-intellisense.git#tag=v$pkgver")
b2sums=('59f22795cd2e27cb140f8f1a3b2aa50784e6a21adf495a7d3da6748021de3f4ea5dad52725ec07b11d40bb60ace37dde3d762744970849f0fe72a4f76cbfd9f4')

prepare() {
  cd tailwindcss-intellisense
  pnpm install --frozen-lockfile
}

build() {
  cd tailwindcss-intellisense/packages/$pkgname
  pnpm build
}

package() {
  cd tailwindcss-intellisense/packages/$pkgname
  install -Dt "$pkgdir"/usr/bin bin/$pkgname
  install -Dm644 -t "$pkgdir"/usr/lib/node_modules/@tailwindcss/language-server \
    package.json
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE \
    ThirdPartyNotices.txt
}
