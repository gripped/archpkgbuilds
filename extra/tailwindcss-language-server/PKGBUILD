# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Agorgianitis Loukas <agorglouk@gmail.com>

pkgname=tailwindcss-language-server
pkgver=0.14.11
pkgrel=1
pkgdesc='Tailwind CSS Language Server'
arch=(any)
url=https://github.com/tailwindlabs/tailwindcss-intellisense/tree/main/packages/tailwindcss-language-server
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  pnpm
)
source=("git+https://github.com/tailwindlabs/tailwindcss-intellisense.git#tag=v$pkgver")
b2sums=('64e402f7215c341ba33bf5120a43868a1e907b78b096be4918ed5504fe4087d08f543ffffb501f5c49de430703c770ce78bc105d921cb35838f564e4e813c864')

prepare() {
  cd tailwindcss-intellisense
  pnpm install --frozen-lockfile
}

build() {
  cd tailwindcss-intellisense/packages/$pkgname
  pnpm build
}

package() {
  cd tailwindcss-intellisense/packages/$pkgname
  install -Dt "$pkgdir"/usr/bin bin/$pkgname
  install -Dm644 -t "$pkgdir"/usr/lib/node_modules/@tailwindcss/language-server \
    package.json
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE \
    ThirdPartyNotices.txt
}
