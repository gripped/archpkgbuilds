# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Agorgianitis Loukas <agorglouk@gmail.com>

pkgname=tailwindcss-language-server
pkgver=0.14.22
pkgrel=1
pkgdesc='Tailwind CSS Language Server'
arch=(any)
url=https://github.com/tailwindlabs/tailwindcss-intellisense/tree/main/packages/tailwindcss-language-server
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  pnpm
)
source=("git+https://github.com/tailwindlabs/tailwindcss-intellisense.git#tag=v$pkgver")
b2sums=('f0fed86fcd52d4ac69a8ac747bdf60d280d685201512139f8a6608ad71cf9c7d9d81d990d9505c19ffb1796643b66f1d01c5a56a108277b1b01b3b590854efdb')

prepare() {
  cd tailwindcss-intellisense
  pnpm install --frozen-lockfile
}

build() {
  cd tailwindcss-intellisense/packages/$pkgname
  pnpm build
}

package() {
  cd tailwindcss-intellisense/packages/$pkgname
  install -Dt "$pkgdir"/usr/bin bin/$pkgname
  install -Dm644 -t "$pkgdir"/usr/lib/node_modules/@tailwindcss/language-server \
    package.json
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE \
    ThirdPartyNotices.txt
}
