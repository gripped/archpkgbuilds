# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Agorgianitis Loukas <agorglouk@gmail.com>

pkgname=tailwindcss-language-server
pkgver=0.14.13
pkgrel=1
pkgdesc='Tailwind CSS Language Server'
arch=(any)
url=https://github.com/tailwindlabs/tailwindcss-intellisense/tree/main/packages/tailwindcss-language-server
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  pnpm
)
source=("git+https://github.com/tailwindlabs/tailwindcss-intellisense.git#tag=v$pkgver")
b2sums=('db4027911617c243fedb39f6e1bf1b93d2246781fd8823a8e63b53bc95e9a6bc20fdd7e7f098963d3e00832f5a981a10d72b75ccc40cc6a45b1c08f37a367604')

prepare() {
  cd tailwindcss-intellisense
  pnpm install --frozen-lockfile
}

build() {
  cd tailwindcss-intellisense/packages/$pkgname
  pnpm build
}

package() {
  cd tailwindcss-intellisense/packages/$pkgname
  install -Dt "$pkgdir"/usr/bin bin/$pkgname
  install -Dm644 -t "$pkgdir"/usr/lib/node_modules/@tailwindcss/language-server \
    package.json
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE \
    ThirdPartyNotices.txt
}
