# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Agorgianitis Loukas <agorglouk@gmail.com>

pkgname=tailwindcss-language-server
pkgver=0.0.21
pkgrel=1
pkgdesc='Tailwind CSS Language Server'
arch=(any)
url=https://github.com/tailwindlabs/tailwindcss-intellisense/tree/master/packages/tailwindcss-language-server
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  npm
)
source=("git+https://github.com/tailwindlabs/tailwindcss-intellisense.git#tag=@tailwindcss/language-server@v$pkgver")
b2sums=('1b6ab4a6e016c4b72785c51775fe7da42ab372badf3c4fd96b59a6b6ca6225aa0c3f88db7cf86e46df5ac52365c8ddc188cb7515d89943f42a18ec9be88b499d')

prepare() {
  cd tailwindcss-intellisense
  npm ci
}

build() {
  cd tailwindcss-intellisense/packages/$pkgname
  npm run build
}

package() {
  cd tailwindcss-intellisense/packages/$pkgname
  install -Dt "$pkgdir"/usr/bin bin/$pkgname
  install -Dm644 -t "$pkgdir"/usr/lib/node_modules/@tailwindcss/language-server \
    package.json
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE \
    ThirdPartyNotices.txt
}
