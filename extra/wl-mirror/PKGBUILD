# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=wl-mirror
pkgver=0.18.1
pkgrel=1
pkgdesc="a simple Wayland output mirror client"
url="https://github.com/Ferdi265/wl-mirror"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  bash
  glibc
  libdrm
  libglvnd
  mesa
  wayland
)
makedepends=(
  cmake
  ninja
  scdoc
)
optdepends=(
  'dmenu: for interactive selection of options and wl-present'
  'pipectl: for wl-present'
  'rofi: for interactively selecting options and wl-present'
  'slurp: for selecting regions and outputs and wl-present'
)
source=($pkgname-$pkgver.tar.gz::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('787a571faa58853769abb732cc82fe99c051a58a1c423df6636a496d34394e5abb76a9d95aa42b1afe7405140fdfbd1a0e689271a2600a5273a33afeee337fa2')
b2sums=('3c9b15ae9790756df344a5299ea6520297215671128ef6108e744bbc4083ec0098fc31b575f1af2d746d3d232bc9892b2d2e15e28293bf987bb4c5f56eae6c67')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_EXAMPLE_SCRIPTS=ON
    -D INSTALL_DOCUMENTATION=ON
    -D WITH_GBM=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
    )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -dm755 "$pkgdir/usr/share/zsh/site-functions" \
                 "$pkgdir/usr/share/bash-completion/completions"

  cd "$pkgname-$pkgver"
  install -Dm644 scripts/completions/zsh-completions/* \
    -t "$pkgdir/usr/share/zsh/site-functions/"
  install -Dm644 scripts/completions/bash-completions/* \
    -t "$pkgdir/usr/share/bash-completion/completions"
}
