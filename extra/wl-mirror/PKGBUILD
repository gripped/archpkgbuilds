# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=wl-mirror
pkgver=0.18.4
pkgrel=1
pkgdesc="a simple Wayland output mirror client"
url="https://github.com/Ferdi265/wl-mirror"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  bash
  glibc
  libdrm
  libglvnd
  mesa
  wayland
)
makedepends=(
  cmake
  ninja
  scdoc
)
optdepends=(
  'dmenu: for interactive selection of options and wl-present'
  'pipectl: for wl-present'
  'rofi: for interactively selecting options and wl-present'
  'slurp: for selecting regions and outputs and wl-present'
)
source=("$pkgname-$pkgver.tar.gz::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig")
sha512sums=('3fcf05187256270312506264681498233e07a1928c76cf4fd9df97e90f4d6e9f5ed1a2ffa26983dfd528a03dd6557221000b78e028264a88ead02c0acbb8ba93'
            '3fcf05187256270312506264681498233e07a1928c76cf4fd9df97e90f4d6e9f5ed1a2ffa26983dfd528a03dd6557221000b78e028264a88ead02c0acbb8ba93')
b2sums=('23eb501534adfedb2a7ce3c0830d5a6ac7845fd2426e95672dab39ea8ab3db85208d44b63c18803af6e3e916b714d9d0cc5fcef680a4ac38f493c01a8442caaf'
        '23eb501534adfedb2a7ce3c0830d5a6ac7845fd2426e95672dab39ea8ab3db85208d44b63c18803af6e3e916b714d9d0cc5fcef680a4ac38f493c01a8442caaf')
validpgpkeys=('087489B679A69AD292EE5FDEA4FAD9CEDA213AF3')  # Ferdinand Bachmann

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_EXAMPLE_SCRIPTS=ON
    -D INSTALL_DOCUMENTATION=ON
    -D WITH_GBM=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
    )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -dm755 "$pkgdir/usr/share/zsh/site-functions" \
                 "$pkgdir/usr/share/bash-completion/completions"

  cd "$pkgname-$pkgver"
  install -Dm644 scripts/completions/zsh-completions/* \
    -t "$pkgdir/usr/share/zsh/site-functions/"
  install -Dm644 scripts/completions/bash-completions/* \
    -t "$pkgdir/usr/share/bash-completion/completions"
}
