# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=wl-mirror
pkgver=0.18.3
pkgrel=1
pkgdesc="a simple Wayland output mirror client"
url="https://github.com/Ferdi265/wl-mirror"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  bash
  glibc
  libdrm
  libglvnd
  mesa
  wayland
)
makedepends=(
  cmake
  ninja
  scdoc
)
optdepends=(
  'dmenu: for interactive selection of options and wl-present'
  'pipectl: for wl-present'
  'rofi: for interactively selecting options and wl-present'
  'slurp: for selecting regions and outputs and wl-present'
)
source=($pkgname-$pkgver.tar.gz::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('c3f69310807b73443a7f65012740c5c8b1b2bf955f0fce91bceff24b85b81d244386e853e3acb9e91692f2d0538be65aed748b04db69edc48ffca6103b1a636b')
b2sums=('e580237c2a557cab7181259500baaf3377897e474cc24d1302abe20d4d9a1e376d05e009929d2d62c2b676448fe298373bd49ff688d15cffca6378635228a906')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_EXAMPLE_SCRIPTS=ON
    -D INSTALL_DOCUMENTATION=ON
    -D WITH_GBM=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
    )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -dm755 "$pkgdir/usr/share/zsh/site-functions" \
                 "$pkgdir/usr/share/bash-completion/completions"

  cd "$pkgname-$pkgver"
  install -Dm644 scripts/completions/zsh-completions/* \
    -t "$pkgdir/usr/share/zsh/site-functions/"
  install -Dm644 scripts/completions/bash-completions/* \
    -t "$pkgdir/usr/share/bash-completion/completions"
}
