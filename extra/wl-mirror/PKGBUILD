# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=wl-mirror
pkgver=0.18.0
pkgrel=1
pkgdesc="a simple Wayland output mirror client"
url="https://github.com/Ferdi265/wl-mirror"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  bash
  glibc
  libdrm
  libglvnd
  mesa
  wayland
)
makedepends=(
  cmake
  ninja
  scdoc
)
optdepends=(
  'dmenu: for interactive selection of options and wl-present'
  'pipectl: for wl-present'
  'rofi: for interactively selecting options and wl-present'
  'slurp: for selecting regions and outputs and wl-present'
)
source=($pkgname-$pkgver.tar.gz::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('978e0ceb71be1f83cf769940292c4d45af5dbc7fb7ce334ac09cb4792f85f49446409f388d6e47462105339aa9b19e88f5a88448778ccaa7d27a7488ed05310b')
b2sums=('78b2c40f1f3cca8855114c68eed97b127e4e6b0b2d50b8c413077be71f7e0f549fbb807fd2f9ff68ba8abbce84ce42b86a68b4c15d0b7d78e04387eab5233976')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_EXAMPLE_SCRIPTS=ON
    -D INSTALL_DOCUMENTATION=ON
    -D WITH_GBM=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
    )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
