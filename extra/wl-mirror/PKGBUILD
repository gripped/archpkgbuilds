# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=wl-mirror
pkgver=0.16.4
pkgrel=1
pkgdesc="a simple Wayland output mirror client"
url="https://github.com/Ferdi265/wl-mirror"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  bash
  glibc
  libglvnd
  wayland
)
makedepends=(
  cmake
  ninja
  scdoc
)
optdepends=(
  'dmenu: for interactive selection of options and wl-present'
  'pipectl: for wl-present'
  'rofi: for interactively selecting options and wl-present'
  'slurp: for selecting regions and outputs and wl-present'
)
source=($pkgname-$pkgver.tar.gz::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('e3a9258161dbb944460b7568ad1411326cc9a4cd387ac67bc99687642a8f1114e0929bd8fb778ee26cb1ec5d71ba59a35cd7d3ee80649864301e0b40550e01b8')
b2sums=('cdb7c1d66284cbfc42afe43ca496d3b17250dc83d05ddcb561fd697d4c0621e45f23c7717aede4a5e6bcc9743e798a405ad3b5b0c2b83fa518fa0473fd40e5fe')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_EXAMPLE_SCRIPTS=ON
    -D INSTALL_DOCUMENTATION=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
    )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
