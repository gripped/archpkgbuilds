# Maintainer: David Runge <dvzrv@archlinux.org>

_upstream_name=cyclonedx-rust-cargo
pkgname=cargo-cyclonedx
pkgver=0.5.4
pkgrel=1
pkgdesc="Creates CycloneDX Software Bill of Materials (SBOM) from Rust (Cargo) projects"
arch=(x86_64)
url="https://github.com/CycloneDX/cyclonedx-rust-cargo"
license=(Apache-2.0)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgname-$pkgver.tar.gz
)
sha512sums=('03712e0eb28e04a829e1d4f8f7c30e36d9d16bc32c046bd22924997f3e7d7040cb721b105002c49116fa24e2d5e8ccd1197ff3c7c041a13497c88e8a9568c376')
b2sums=('e99e78fab79c90ec80f94e23173aab16bf94b253222af9202a2ea3d50e418d1c6b785b1d4eb54de92cba34ea4f0035a898348b8efc29044baf15c7eb674757e6')

prepare() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  # NOTE: use different target dir to have reproducible executable
  export CARGO_TARGET_DIR=target_test
  cargo test --frozen --all-features --workspace
}

package() {
  cd $_upstream_name-$pkgname-$pkgver
  install -vDm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
