# Maintainer: David Runge <dvzrv@archlinux.org>

_upstream_name=cyclonedx-rust-cargo
pkgname=cargo-cyclonedx
pkgver=0.5.7
pkgrel=1
pkgdesc="Creates CycloneDX Software Bill of Materials (SBOM) from Rust (Cargo) projects"
arch=(x86_64)
url="https://github.com/CycloneDX/cyclonedx-rust-cargo"
license=(Apache-2.0)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgname-$pkgver.tar.gz
)
sha512sums=('62cf69c7a51903be072f52fe17d2d4fd6f30e26fa9f75436aa3f56997458cc044733a9b54d3f3a68481d7d672a91da64e648ded0d7646535ad07a96cb2e9ca74')
b2sums=('cf725b178e517082c8d6cdaa50fa5857237101275633f3049a1865bf3ea65f18fa0a5ec9335c9b956bf7dd4130847af83b57d9b4515e46f00d1154ee07ceda7d')

prepare() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  # NOTE: use different target dir to have reproducible executable
  export CARGO_TARGET_DIR=target_test
  cargo test --frozen --all-features --workspace
}

package() {
  cd $_upstream_name-$pkgname-$pkgver
  install -vDm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
