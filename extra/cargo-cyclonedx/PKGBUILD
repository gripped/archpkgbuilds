# Maintainer: David Runge <dvzrv@archlinux.org>

_upstream_name=cyclonedx-rust-cargo
pkgname=cargo-cyclonedx
pkgver=0.5.2
pkgrel=1
pkgdesc="Creates CycloneDX Software Bill of Materials (SBOM) from Rust (Cargo) projects"
arch=(x86_64)
url="https://github.com/CycloneDX/cyclonedx-rust-cargo"
license=(Apache-2.0)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgname-$pkgver.tar.gz
  $pkgname-0.5.2-lockfile.patch
)
sha512sums=('c8ff842b515f75805e6718cd03eb18fcbdc34fd40a4cccb15852f38f5503db570c13e024d3eb51ea852db0eb2d848b5d9cd369f72e3b6cc4a1f84ba7c076ec7e'
            '7999ea9435f3186e94c312f035870c4255e9c2e29adb067d2abd6f5a1da2a6bad11b2d50c0fcf04ef45a977ecb37b47ac9d0eddf04c9bb60e8852b6ead81bf00')
b2sums=('b7987dad30fa6c06faa97a360bbbd167fd67d613c44279e4f9c65e6b9661a1627f76ecd05a3f17a3b33ac4447b254dc81cab3cb195c846a0182b5294d095b2f1'
        'eb31ffcf2d47742c39dcf1330fa6e72772494cf109a4be585e935f24d3b63b0d5b8d7c9687d1fc5cb91a0f6063387923c127612c897acd3cae0c7be8bf61af5f')

prepare() {
  cd $_upstream_name-$pkgname-$pkgver
  patch -Np1 -i ../$pkgname-0.5.2-lockfile.patch  # https://github.com/CycloneDX/cyclonedx-rust-cargo/issues/728
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  # NOTE: use different target dir to have reproducible executable
  export CARGO_TARGET_DIR=target_test
  cargo test --frozen --all-features --workspace
}

package() {
  cd $_upstream_name-$pkgname-$pkgver
  install -vDm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
