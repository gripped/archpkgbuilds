# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=shim
pkgver=16.0
pkgrel=1
pkgdesc="EFI preloader (unsigned EFI binaries)"
arch=(any)
url="https://github.com/rhboot/shim"
license=(BSD-3-Clause)
makedepends=(git)
checkdepends=(
  efivar
  xxd
)
source=(
  git+$url?signed#tag=$pkgver
  gnu-efi::git+https://github.com/rhboot/gnu-efi.git
)
sha512sums=('d9d03a39da1e93fe3443eae9e3aafd2b4cfd19d82172186ede27f1c80bbf8a83b40ca5e449fd517372326eb3d8fc093f7c8b4de3fb697e441fce6acbfe6df9f7'
            'SKIP')
b2sums=('c59b35b3fa2cd79e869ee075af462fbd56229b0da9a1fa33799220e73daf22f7b6e7437ff60012cf43b917176a7f424d578463fa93da090f3b600936c0b6c4c6'
        'SKIP')
validpgpkeys=(
  B00B48BC731AA8840FED9FB0EED266B70F4FEF10  # Peter Jones <pjones@redhat.com>
  039A9CEA19DE9508C36875AA2532F9176A95A442  # Robbie Harwood (work) <rharwood@fedoraproject.org>
)

prepare() {
  cd $pkgname

  git submodule init
  git config submodule."gnu-efi".url ../gnu-efi
  git -c protocol.file.allow=always submodule update

  # -Werror, not even once
  sed -e 's/-Werror\b//g' -i Makefile Make.defaults
}

build() {
  # TODO: evaluate setting DEFAULT_LOADER to e.g. loader.efi:
  # this would require changing grub's default, but would allow us to unify
  make EFIDIR=ARCH -C $pkgname
}

check() {
  make test -C $pkgname
}

package() {
  cd $pkgname
  make DATATARGETDIR="/usr/share/$pkgname" DESTDIR="$pkgdir/" install-as-data
  install -vDm 644 COPYRIGHT -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {BUILDING,README.{md,fallback,tpm},TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
