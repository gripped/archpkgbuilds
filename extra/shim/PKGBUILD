# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=shim
pkgver=16.1
pkgrel=1
pkgdesc="EFI preloader (unsigned EFI binaries)"
arch=(any)
url="https://github.com/rhboot/shim"
license=(BSD-3-Clause)
makedepends=(git)
checkdepends=(
  efivar
  xxd
)
source=(
  git+$url?signed#tag=$pkgver
  gnu-efi::git+https://github.com/rhboot/gnu-efi.git
)
sha512sums=('d65e33f5e50868d757edff7e3c1dddf69a4fafff00a7efa2f323d2b98de1da25f1b722ebe31df6997f14871117ff028323568979971ddc918f211716cf1e01e3'
            'SKIP')
b2sums=('aabd720987406db11bac71dd409f1738b754793124531de7b9948cd89f6a26590bc5bb578ef0c0957e83cc2af7aed36d9dc8e2260a6ae17c6c2f917353b7b36c'
        'SKIP')
validpgpkeys=(
  B00B48BC731AA8840FED9FB0EED266B70F4FEF10  # Peter Jones <pjones@redhat.com>
  039A9CEA19DE9508C36875AA2532F9176A95A442  # Robbie Harwood (work) <rharwood@fedoraproject.org>
)

prepare() {
  cd $pkgname

  git submodule init
  git config submodule."gnu-efi".url ../gnu-efi
  git -c protocol.file.allow=always submodule update

  # -Werror, not even once
  sed -e 's/-Werror\b//g' -i Makefile Make.defaults
}

build() {
  # TODO: evaluate setting DEFAULT_LOADER to e.g. loader.efi:
  # this would require changing grub's default, but would allow us to unify
  make EFIDIR=ARCH -C $pkgname
}

check() {
  make test -C $pkgname -j1
}

package() {
  cd $pkgname
  make DATATARGETDIR="/usr/share/$pkgname" DESTDIR="$pkgdir/" install-as-data
  install -vDm 644 COPYRIGHT -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {BUILDING,README.{md,fallback,tpm},TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
