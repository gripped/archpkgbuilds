# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=sqlfluff
pkgver=3.5.0
pkgrel=2
pkgdesc='A dialect-flexible and configurable SQL linter'
arch=('any')
url='https://github.com/sqlfluff/sqlfluff'
license=('MIT')
depends=(
  'python'
  'python-chardet'
  'python-click'
  'python-colorama'
  'python-diff-cover'
  'python-jinja'
  'python-pathspec'
  'python-platformdirs'
  'python-pluggy'
  'python-pytest'
  'python-regex'
  'python-tblib'
  'python-tqdm'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-hypothesis'
  'python-pytest-xdist'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('c2d40e554310f5fabe94f782f9d62ad409f59a22d5770947401d2e541375246abbb8caf04341bfb435c7cc45301349f7699e4adcdaa9f168cb403a8ad4e59965')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest -n=auto \
    --deselect='test/core/linter/discovery_test.py::test__linter__path_from_paths__ignore[test/fixtures/linter/sqlfluffignore]' \
    --deselect='test/core/plugin_test.py::test__plugin_default_config_read[Example_L001-forbidden_columns]' \
    --deselect='test/core/plugin_test.py::test__plugin_example_rules_returned[Rule_Example_L001]' \
    --deselect='test/core/plugin_test.py::test__plugin_manager_registers_example_plugin'
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
