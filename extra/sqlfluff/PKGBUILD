# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=sqlfluff
pkgver=4.0.0
pkgrel=1
pkgdesc='A dialect-flexible and configurable SQL linter'
arch=(any)
url='https://github.com/sqlfluff/sqlfluff'
license=(MIT)
depends=(
  python
  python-chardet
  python-click
  python-colorama
  python-diff-cover
  python-jinja
  python-pathspec
  python-platformdirs
  python-pluggy
  python-pytest
  python-regex
  python-tblib
  python-tqdm
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-pytest-xdist
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('f953f48b399c6f0067d13684a61d43e3b89e530365f09f8ae7598f7c619140326a8a3295961bd949d778cedd99d9800a36258a3df958fe97ddfb5fee716fc6e1')
b2sums=('a6906d4910613bc3e906bcb249371198a148e50c0fb9d18432de87070d83804cf89db5beb9942ea7d5b198008d9d68bca9ef871bffe55915924dfd12e9af5dc7')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest -n=auto \
    --deselect='test/core/linter/discovery_test.py::test__linter__path_from_paths__ignore[test/fixtures/linter/sqlfluffignore]' \
    --deselect='test/core/plugin_test.py::test__plugin_default_config_read[Example_L001-forbidden_columns]' \
    --deselect='test/core/plugin_test.py::test__plugin_example_rules_returned[Rule_Example_L001]' \
    --deselect='test/core/plugin_test.py::test__plugin_manager_registers_example_plugin'
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
