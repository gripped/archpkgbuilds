# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=sqlfluff
pkgver=4.0.4
pkgrel=2
pkgdesc='A dialect-flexible and configurable SQL linter'
arch=(x86_64)
url='https://github.com/sqlfluff/sqlfluff'
license=(MIT)
depends=(
  glibc
  libgcc
  python
  python-chardet
  python-click
  python-colorama
  python-diff-cover
  python-jinja
  python-pathspec
  python-platformdirs
  python-pluggy
  python-pytest
  python-regex
  python-tblib
  python-tqdm
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
  python-maturin
)
checkdepends=(
  python-hypothesis
  python-pytest-xdist
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('ca292c09315fe5f616e57cf039525f8d89094997d659480c39de4746a4daf6e5a701b298f7eff8a1c902e10ed8447cc5d49045c169ad4128fc0ae47a7036866a')
b2sums=('ec7cbdf1228c10415d1af5f0847ae50f456d9e721a69119d8246dd44784d62f7608171c81c9ca3eb89c2849801070f64d7e4d4ce1a812e76a5efc4a6bb6d8b00')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation

  # temporary install
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  # generate dialects
  python utils/rustify.py build

  # build sqlfluffrs
  cd sqlfluffrs
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m installer sqlfluffrs/dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest -n=auto \
    --deselect='test/core/linter/discovery_test.py::test__linter__path_from_paths__ignore[test/fixtures/linter/sqlfluffignore]' \
    --deselect='test/core/plugin_test.py::test__plugin_default_config_read[Example_L001-forbidden_columns]' \
    --deselect='test/core/plugin_test.py::test__plugin_example_rules_returned[Rule_Example_L001]' \
    --deselect='test/core/plugin_test.py::test__plugin_manager_registers_example_plugin' \
    --deselect='test/core/config/loader_test.py::test__config__get_user_config_dir_path[darwin-False-False-~/Library/Application Support/sqlfluff-paths_checked2]'

}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
  python -m installer --destdir="$pkgdir" sqlfluffrs/dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
