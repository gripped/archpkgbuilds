# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=key-rack
pkgver=0.4.0
pkgrel=3
pkgdesc='Password manager for GNOME'
arch=(x86_64)
url='https://gitlab.gnome.org/World/key-rack'
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  git
  meson
  rust
)
source=(
  "git+https://gitlab.gnome.org/World/key-rack.git#tag=$pkgver"
  key-rack-create-window.patch
  key-rack-dbus-activatable.patch
)
b2sums=(
  9f6bba19184eb157e60bd58c1e002d7134f263d54990e5d20a6c679def701d209ad6040d96ab5e6ed9ca5eec4166a2654a689a97c2dcad72357f8a28ca108d6e
  c674adb93105aaf31fa8d2ce4eb7a50705098407cf7bb746359ba37ac48d69f681d5d5ff8e1bcfb71fcba35e48f721f8b246ce09a0301d7a4158e1f810830d82
  af4e520fb105dfb359340a1a2ac32b64990fd72f5747209ddc24057b6446c777c868f98d128ebffc974b9a6a3681e942c7564b12dc9ca96ad5e903f59684e81c
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/key-rack/-/merge_requests/43
  git cherry-pick -n 5084dda8d41b60fb1c28ce1c1ec271913bc8a71d

  # https://gitlab.gnome.org/World/key-rack/-/merge_requests/45
  git apply -3 ../key-rack-create-window.patch

  # https://gitlab.gnome.org/World/key-rack/-/merge_requests/46
  git apply -3 ../key-rack-dbus-activatable.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
