# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=flask-security
pkgname=python-flask-security-too
pkgver=5.5.2
pkgrel=2
pkgdesc='Quick and simple security for Flask applications'
url='https://github.com/pallets-eco/flask-security'
arch=(any)
license=(MIT)
depends=(
  python
  python-flask
  python-flask-login
  python-flask-principal
  python-flask-wtf
  python-wtforms
  python-itsdangerous
  python-markupsafe
  python-passlib
  python-email-validator
  python-importlib_resources
  python-werkzeug
  python-jinja
  python-click
  python-blinker
)
makedepends=(
  python-babel
  python-build
  python-flit-core
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-babel
  python-flask-babel

  python-flask-sqlalchemy
  python-sqlalchemy
  python-sqlalchemy-utils

  python-argon2_cffi
  python-bcrypt
  python-flask-mailman
  python-bleach

  python-cryptography
  python-qrcode
  # phonenumberslite
  # webauthn

  # python-authlib
  python-freezegun
  python-mongoengine
  python-mongomock
  python-peewee
  python-phonenumbers

  python-pytest
  python-zxcvbn

  python-asgiref
)
optdepends=('python-bleach: scrub user input')
provides=('python-flask-security')
conflicts=('python-flask-security')
source=(
  ${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
  fix-subdomain-issue-with-flask-3.1.patch::${url}/commit/7f3977d3557498b9e94333473e5d186b303ba05c.patch
)
sha512sums=('d1e8dca327c1b2e8865a82ab99bbb5573c4f236172c5d77fe6fce0c3f0540b66a3e3974dfd1d39220f1fc1f615e3cc07fe21d0d80721d2cc39505ff5961c8729'
            'e594dbe8faca2c0fe45dc9b72b9616dfe3c6330f6a6c478b136f0166676386d05a12e5101744737b6f04205dae0f44cadcddc1750007062fd007cb6837bc12e9')
b2sums=('d2230e84c84d75302d364a295b3f50f2ba38857e98a88f6d14e5b25b11a36b975fd7603af3c5b344f341e80115ea7baf2ce4a32b28842373efae291211c47dd1'
        '043b8264f084d0953b21031ab4b3a244bdabaea1b33fce7a6998a47fa111a9138cc26a6df7df31705487768ec464a504eb0e0c03da8928dd3c97b36d21e71c07')

prepare() {
  cd ${_pkgname}-${pkgver}
  # Avoid patch conflict
  sed -n '\|^diff --git a/tests/test_basic.py b/tests/test_basic.py|,$p' \
    ../fix-subdomain-issue-with-flask-3.1.patch \
    | patch -Np1 -i -
}

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
  pybabel compile --domain flask_security -d flask_security/translations
}

check() {
  cd ${_pkgname}-${pkgver}
  pytest -v -Wignore --disable-pytest-warnings
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 README.rst CHANGES.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
