# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-flask-security-too
_pkgname=flask-security
pkgver=5.7.1
pkgrel=1
pkgdesc='Quick and simple security for Flask applications'
arch=(any)
url='https://github.com/pallets-eco/flask-security'
license=(MIT)
depends=(
  python
  python-blinker
  python-click
  python-email-validator
  python-flask
  python-flask-login
  python-flask-principal
  python-flask-wtf
  python-importlib_resources
  python-itsdangerous
  python-jinja
  python-markupsafe
  python-passlib
  python-requests
  python-werkzeug
  python-wtforms
)
makedepends=(
  python-babel
  python-build
  python-flit-core
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-babel
  python-flask-babel

  python-flask-sqlalchemy
  python-sqlalchemy
  python-sqlalchemy-utils

  python-argon2_cffi
  python-bcrypt
  python-flask-mail
  python-bleach

  python-cryptography
  python-qrcode
  # phonenumberslite
  # webauthn

  # python-authlib
  python-freezegun
  python-mongoengine
  python-mongomock
  python-peewee
  python-phonenumbers

  python-pytest
  python-zxcvbn

  python-asgiref
)
optdepends=('python-bleach: scrub user input')
provides=('python-flask-security')
conflicts=('python-flask-security')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c2de9cc84f6e90e8c5f99d0f64b9a0730c1475777cf9b081e3dbf204de0a6915945705f39dee29da6c339ebf8f9bf81b8e27c91963eab4993737e1e0761d2e80')
b2sums=('b0cc7099f694becf9a0267885aed951d13c46648bf21abc8456b53a4fff119d541a02699c8a3079c13e3ba346d00639958dab0a13c4aac1f8040486a2e624f5a')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
  pybabel compile --domain flask_security -d flask_security/translations
}

check() {
  cd $_pkgname-$pkgver
  # ignore incompatible bcrypt tests
  # https://github.com/pallets-eco/flask-security/commit/568d81219fc8408e39cc19acc57a682ac0bb9136
  pytest -v -W=ignore::DeprecationWarning -W=ignore::UserWarning \
    --deselect=tests/test_hashing.py::test_login_with_bcrypt_enabled \
    --deselect=tests/test_misc.py::test_password_unicode_password_salt
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst CHANGES.rst
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
