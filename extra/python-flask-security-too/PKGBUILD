# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

_pkgname=flask-security
pkgname=python-flask-security-too
pkgver=5.6.2
pkgrel=2
pkgdesc='Quick and simple security for Flask applications'
url='https://github.com/pallets-eco/flask-security'
arch=(any)
license=(MIT)
depends=(
  python
  python-blinker
  python-click
  python-email-validator
  python-flask
  python-flask-login
  python-flask-principal
  python-flask-wtf
  python-importlib_resources
  python-itsdangerous
  python-jinja
  python-markupsafe
  python-passlib
  python-requests
  python-werkzeug
  python-wtforms
)
makedepends=(
  python-babel
  python-build
  python-flit-core
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-babel
  python-flask-babel

  python-flask-sqlalchemy
  python-sqlalchemy
  python-sqlalchemy-utils

  python-argon2_cffi
  python-bcrypt
  python-flask-mailman
  python-bleach

  python-cryptography
  python-qrcode
  # phonenumberslite
  # webauthn

  # python-authlib
  python-freezegun
  python-mongoengine
  python-mongomock
  python-peewee
  python-phonenumbers

  python-pytest
  python-zxcvbn

  python-asgiref
)
optdepends=('python-bleach: scrub user input')
provides=('python-flask-security')
conflicts=('python-flask-security')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0d80a8642d3523161b9744411ddeb075c00c75d287020a7874ad9db92f8080f49a4fc3486f292369b5ff3b7427c757731d4a006297671bd55c07a803602cd1b7')
b2sums=('1f8d84172c91ca1704f8074f8cf5e80373c50c399f623d798ea0f078275633dfb3182a0292aa8acfec5dda08ed6792ccac147ffe24044c5323a340e96967619f')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
  pybabel compile --domain flask_security -d flask_security/translations
}

check() {
  cd $_pkgname-$pkgver
  pytest -v -W=ignore::UserWarning
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst CHANGES.rst
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}

# vim: ts=2 sw=2 et:
