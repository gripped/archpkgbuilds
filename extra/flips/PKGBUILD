# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=flips
pkgver=198
pkgrel=1
pkgdesc='A patcher for IPS and BPS files'
arch=(x86_64)
url="https://github.com/Alcaro/Flips"
license=(GPL-3.0-or-later)
depends=(
  at-spi2-core
  cairo
  gcc-libs
  gdk-pixbuf2
  glibc
  glib2
  gtk3
  harfbuzz
  hicolor-icon-theme
  pango
  wayland
  zlib
)
makedepends=(git)
_tag=359d414cff73b3e0400871d3c288a59b36564834
source=(git+https://github.com/Alcaro/Flips.git#tag=${_tag})
b2sums=('bcb84e240f7819b00e6076aa7927e754f39a4639d889ba5c4163b5d32419a6885cc0482981e316ffc998c5ad15e8c7af1fa4a8d18dc1ca745eb8c13f0ebc4d9e')

pkgver() {
  cd Flips
  git describe --tags | sed 's/^v//'
}

build() {
  make CFLAGS="$CFLAGS $LDFLAGS" -C Flips
}

package() {
  make DESTDIR="${pkgdir}" -C Flips install
}

# vim: ts=2 sw=2 et:
