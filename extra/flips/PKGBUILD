# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=flips
pkgver=196
pkgrel=1
pkgdesc='A patcher for IPS and BPS files'
arch=(x86_64)
url="https://github.com/Alcaro/Flips"
license=(GPL-3.0-or-later)
depends=(
  at-spi2-core
  cairo
  gcc-libs
  gdk-pixbuf2
  glibc
  glib2
  gtk3
  harfbuzz
  hicolor-icon-theme
  pango
  wayland
  zlib
)
makedepends=(git)
_tag=b4a265b5489c30937e089c7c1d08a16c44d7ef86
source=(git+https://github.com/Alcaro/Flips.git#tag=${_tag})
b2sums=('2cfeb512cae1c1b2d2edd02b78dc59e84375f8c8f5ecae7016aef5f33ce152d777324a08724d12a1a992ebeba1a9e060e6bd048de627fee58a9495fd00995ee6')

pkgver() {
  cd Flips
  git describe --tags | sed 's/^v//'
}

build() {
  make CFLAGS="$CFLAGS $LDFLAGS" -C Flips
}

package() {
  make DESTDIR="${pkgdir}" -C Flips install
}

# vim: ts=2 sw=2 et:
