# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=flips
pkgver=183
pkgrel=1
pkgdesc='A patcher for IPS and BPS files'
arch=(x86_64)
url="https://github.com/Alcaro/Flips"
license=(GPL-3.0-or-later)
depends=(
  at-spi2-core
  cairo
  gcc-libs
  gdk-pixbuf2
  glibc
  glib2
  gtk3
  harfbuzz
  hicolor-icon-theme
  pango
  wayland
  zlib
)
makedepends=(git)
_tag=07162cac69114bdb4b8a2302d131604c29df853b
source=(git+https://github.com/Alcaro/Flips.git#tag=${_tag})
b2sums=('cdf89d07b2e79a373184efb2069c33f22515d5456d86abe417c93dd97d16dcd1ef0c4915e6acff75eb94048c7337d42b351364c2e5523294f47adbe35ad7eaba')

pkgver() {
  cd Flips
  git describe --tags | sed 's/^v//'
}

build() {
  make CFLAGS="$CFLAGS $LDFLAGS" -C Flips
}

package() {
  make DESTDIR="${pkgdir}" -C Flips install
}

# vim: ts=2 sw=2 et:
