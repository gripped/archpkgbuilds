# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=numba-cuda
pkgname=python-$_name
pkgver=0.26.0
pkgrel=1
pkgdesc="The CUDA target for Numba"
url="https://github.com/NVIDIA/numba-cuda"
arch=(x86_64)
license=(BSD-2-Clause)
depends=(
  gcc-libs
  glibc
  python
  python-cuda-bindings
  python-cuda-core
  python-llvmlite
  python-numba
  python-numpy
  python-packaging
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('80e94270d72bf61b4b48441c3b03272f9ec902700a3ff533d2c5befdf3bb212a40ec4021a2a7bac843cff94c55f3a042592a5a9cdff65cf9dae88a4c46fad44e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE LICENSE.numba -t "$pkgdir"/usr/share/licenses/$pkgname/
  # Remove tests
  rm -rv "$pkgdir"$(python -c "import site; print(site.getsitepackages()[0])")/numba_cuda/numba/cuda/{tests/,testing.py}
}
