# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=numba-cuda
pkgname=python-$_name
pkgver=0.24.0
pkgrel=4
pkgdesc="The CUDA target for Numba"
url="https://github.com/NVIDIA/numba-cuda"
arch=(x86_64)
license=(BSD-2-Clause)
depends=(
  gcc-libs
  glibc
  python
  python-cuda-bindings
  python-cuda-core
  python-llvmlite
  python-numba
  python-numpy
  python-packaging
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
        numpy-2.4.patch)
b2sums=('0ed3953a3ae5f731f9c17f17cc49f6e8cdec352ac89bd537fa285233cf439099a3e8f360cb06b45e45d5c5058297281f1f343092e23bb5d3e0b01914df3f894a'
        '5abd9fccd970720c603dd9705c8b9b857d5aebed18cffa6a977001dff105b7f73f71adb2f559fe3dcb4be6904e3a69224417917517d360ec99685899de298d93')

prepare() {
  cd $_name-$pkgver
  # fix compatibility with numpy 2.4 https://github.com/NVIDIA/numba-cuda/pull/739
  patch -p1 < ../numpy-2.4.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE LICENSE.numba -t "$pkgdir"/usr/share/licenses/$pkgname/
  # Remove tests
  rm -rv "$pkgdir"$(python -c "import site; print(site.getsitepackages()[0])")/numba_cuda/numba/cuda/{tests/,testing.py}
}
