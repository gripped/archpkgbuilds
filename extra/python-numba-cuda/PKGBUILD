# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=numba-cuda
pkgname=python-$_name
pkgver=0.27.0
pkgrel=2
pkgdesc="The CUDA target for Numba"
url="https://github.com/NVIDIA/numba-cuda"
arch=(x86_64)
license=(BSD-2-Clause)
depends=(
  glibc
  libgcc
  libstdc++
  python
  python-cuda-bindings
  python-cuda-core
  python-llvmlite
  python-numba
  python-numpy
  python-packaging
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('6829ef66f131ea4ea341ec64acbe72e2f8ebaf9f11b8de1b4e877819d94c037fb439d51c9afbfeb0f500a3e23b2e6c2dfed5c14b7dc4772442cbdd370a1cbfbd')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE LICENSE.numba -t "$pkgdir"/usr/share/licenses/$pkgname/
  # Remove tests
  rm -rv "$pkgdir"$(python -c "import site; print(site.getsitepackages()[0])")/numba_cuda/numba/cuda/{tests/,testing.py}
}
