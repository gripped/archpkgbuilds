# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=m4ri
pkgver=20250128
pkgrel=1
pkgdesc='Algorithms for linear algebra over F_2'
arch=(x86_64)
url='https://bitbucket.org/malb/m4ri'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         libpng)
makedepends=(git)
source=(git+https://github.com/malb/m4ri#tag=$pkgver)
sha256sums=('fc9c7378343c24a0dcbbdd3f41a8c52737677d954e998d6b6f94fcc714f62939')

prepare() {
  cd $pkgname
  sed -e '/AX_EXT/d' -i configure.ac # Disable CPU autodetection
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-openmp \
    --enable-thread-safe \
    --disable-static
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
