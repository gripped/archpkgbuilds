# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=m4ri
pkgver=20251207
pkgrel=1
pkgdesc='Algorithms for linear algebra over F_2'
arch=(x86_64)
url='https://bitbucket.org/malb/m4ri'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         libpng)
makedepends=(git)
source=(git+https://github.com/malb/m4ri#tag=$pkgver)
sha256sums=('4c7ce2b3f3ca53689a22d3f212994a13ed23c5e2fb8c31a89ea088719b0defb0')

prepare() {
  cd $pkgname
  sed -e '/AX_EXT/d' -i configure.ac # Disable CPU autodetection
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-openmp \
    --enable-thread-safe \
    --disable-static
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
