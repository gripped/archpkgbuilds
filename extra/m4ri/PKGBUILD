# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=m4ri
pkgver=20251206
pkgrel=1
pkgdesc='Algorithms for linear algebra over F_2'
arch=(x86_64)
url='https://bitbucket.org/malb/m4ri'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         libpng)
makedepends=(git)
source=(git+https://github.com/malb/m4ri#tag=$pkgver)
sha256sums=('40f064be13af6910c53de0954ef17a0bea47762f9ac02f580b1f88f4d2c1fd7f')

prepare() {
  cd $pkgname
  sed -e '/AX_EXT/d' -i configure.ac # Disable CPU autodetection
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-openmp \
    --enable-thread-safe \
    --disable-static
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
