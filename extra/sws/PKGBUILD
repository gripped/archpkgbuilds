# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: andre.menrath@posteo.de

pkgname=sws
pkgver=2.14.0.6
pkgrel=1
pkgdesc='A collection of features that seamlessly integrate into REAPER'
arch=('x86_64')
url='https://www.sws-extension.org/'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'reaper'
  'taglib'
)
makedepends=(
  'git'
  'cmake'
  'php'
  'perl'
  'gtk3'
)
install='sws.install'
source=(
  "git+https://github.com/reaper-oss/sws.git#tag=v$pkgver"
  'github.com-justinfrankel-wdl::git+https://github.com/justinfrankel/WDL'
  'github.com-justinfrankel-reaper-sdk::git+https://github.com/justinfrankel/reaper-sdk'
)
sha512sums=('c488252f3b658f91cd05d77a1e7868d4c86b2aea3c2746ef645f483b7f7047382dda7510a95f6a62564d202bbd3a76c3f282ba6d610af7782f30d8a2c3b30ecb'
            'SKIP'
            'SKIP')
b2sums=('a849a6a80a09af3d0d602c241ce927b19744b70730e7739b03312c39c2ddb8530144bf229980372c3e7877e525ad4a59b10918dd866ec755ba5c8cea223af6dc'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  # note: vendor/taglib submodule is skipped because we use system taglib
  git submodule init vendor/WDL vendor/reaper-sdk
  git config submodule.vendor/WDL.url "$srcdir/github.com-justinfrankel-wdl"
  git config submodule.vendor/reaper-sdk.url "$srcdir/github.com-justinfrankel-reaper-sdk"
  git -c protocol.file.allow=always submodule update vendor/WDL vendor/reaper-sdk
}

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_TAGLIB=YES

  cmake --build build
}

package() {
  # plugin
  install -vDm755 -t "$pkgdir/usr/lib/sws" "build/reaper_sws-$CARCH.so"
  install -vDm644 -t "$pkgdir/usr/lib/sws" build/sws_python64.py

  cd "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md whatsnew.txt

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
