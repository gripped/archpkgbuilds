# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: andre.menrath@posteo.de

pkgname=sws
pkgver=2.14.0.3
pkgrel=1
pkgdesc='A collection of features that seamlessly integrate into REAPER'
arch=('x86_64')
url='https://www.sws-extension.org/'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'reaper'
  'taglib'
)
makedepends=(
  'git'
  'cmake'
  'php'
  'perl'
  'gtk3'
)
install='sws.install'
source=(
  "git+https://github.com/reaper-oss/sws.git#tag=v$pkgver"
  'github.com-justinfrankel-wdl::git+https://github.com/justinfrankel/WDL'
  'github.com-justinfrankel-reaper-sdk::git+https://github.com/justinfrankel/reaper-sdk'
)
sha512sums=('899b667048fccd3fca6f7b6db56742e98ed06d0c79d420eb3859bb7c2df4cbb253d66b7a0d301e54c24b531d299f1aa52dc1b2ddc174a4e32b65007c311bcacd'
            'SKIP'
            'SKIP')
b2sums=('39b1687ebebfa9258193a0cd8c606f1432e6b37ae946438c3b8d835dd8cb3c8584cc0751e2da8704bdc0794ca3899e7bdd8309c4b13446f26e450dae22aa151c'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  # note: vendor/taglib submodule is skipped because we use system taglib
  git submodule init vendor/WDL vendor/reaper-sdk
  git config submodule.vendor/WDL.url "$srcdir/github.com-justinfrankel-wdl"
  git config submodule.vendor/reaper-sdk.url "$srcdir/github.com-justinfrankel-reaper-sdk"
  git -c protocol.file.allow=always submodule update vendor/WDL vendor/reaper-sdk
}

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_TAGLIB=YES

  cmake --build build
}

package() {
  # plugin
  install -vDm755 -t "$pkgdir/usr/lib/sws" "build/reaper_sws-$CARCH.so"
  install -vDm644 -t "$pkgdir/usr/lib/sws" build/sws_python64.py

  cd "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md whatsnew.txt

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
