# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gurk
pkgver=0.6.0
pkgrel=1
pkgdesc="Signal Messenger client for terminal"
url='https://github.com/boxdot/gurk-rs'
arch=('x86_64')
license=('AGPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'sqlcipher'
)
makedepends=(
  'cargo'
  'protobuf'
)
options=(!lto)
source=(https://github.com/boxdot/${pkgname}-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('63302a65e4f832797911651f688e1f056f049da99b8d396d114f002629d03f2d')
b2sums=('1c3b25e3fbed2b209c02c74e715e67c434018349505fecb3d01080363fb5a162b8af980c5712404601527f1e80b24b2e78a4ce4ca358265343caac4042927876')

prepare() {
  cd "${pkgname}-rs-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

check() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo test --frozen
  target/release/gurk --help
}

package() {
  cd "${pkgname}-rs-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gurk
}

# vim: ts=2 sw=2 et:
