# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=gurk
pkgver=0.7.2
pkgrel=1
pkgdesc="Signal Messenger client for terminal"
url='https://github.com/boxdot/gurk-rs'
arch=('x86_64')
license=('AGPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'sqlcipher'
)
makedepends=(
  'cargo'
  'protobuf'
)
replaces=('gurk-bin')
options=(!lto)
source=(https://github.com/boxdot/${pkgname}-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1b349555a7fc5bc0e8e5a417b1cc49e4107360e3de6d1eb9cbf8f254bc57f9b8')
b2sums=('9a03332f34bf9057e625ac5212877ad7e5e98fde31083491fda88937dc36ed97a738f839c56f20eae28f992d0ffa2dcd34656b25f8852cd5fec31d6976e1b4cf')

prepare() {
  cd "${pkgname}-rs-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

check() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo test --frozen
  target/release/gurk --help
}

package() {
  cd "${pkgname}-rs-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gurk
}

# vim: ts=2 sw=2 et:
