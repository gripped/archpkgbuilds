# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gurk
pkgver=0.6.1
pkgrel=1
pkgdesc="Signal Messenger client for terminal"
url='https://github.com/boxdot/gurk-rs'
arch=('x86_64')
license=('AGPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'sqlcipher'
)
makedepends=(
  'cargo'
  'protobuf'
)
options=(!lto)
source=(https://github.com/boxdot/${pkgname}-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c6e972ae1c40ebc0cc245dd88b3dbbfb0e8afe5bc2cfcb7c7318790a1cc7038f')
b2sums=('720c467f731dfdebbd26d30457840f85c683683c314c3ca9b3f878b00991b6cf4b8f9a79dd645331e917a803d7702241acd4b00fc0da593a18f74d2af574303d')

prepare() {
  cd "${pkgname}-rs-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

check() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo test --frozen
  target/release/gurk --help
}

package() {
  cd "${pkgname}-rs-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gurk
}

# vim: ts=2 sw=2 et:
