# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=gurk
pkgver=0.6.4
pkgrel=1
pkgdesc="Signal Messenger client for terminal"
url='https://github.com/boxdot/gurk-rs'
arch=('x86_64')
license=('AGPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'sqlcipher'
)
makedepends=(
  'cargo'
  'protobuf'
)
replaces=('gurk-bin')
options=(!lto)
source=(https://github.com/boxdot/${pkgname}-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6e1ea43aad916e5c7f1fbbb852b06b28d9a5546d1bf80d93a50c3d6d9d3c42a4')
b2sums=('51ba326e750ea79fc45ef2aee2994d7cb1ca8bdfad784b4f41d5a36af392e869a0e2c899d77aec05cea00a865fb97603dc9bd8db8caeb7d05cb2b1fa761a12bf')

prepare() {
  cd "${pkgname}-rs-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

check() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo test --frozen
  target/release/gurk --help
}

package() {
  cd "${pkgname}-rs-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gurk
}

# vim: ts=2 sw=2 et:
