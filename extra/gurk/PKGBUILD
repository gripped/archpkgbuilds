# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=gurk
pkgver=0.6.3
pkgrel=3
pkgdesc="Signal Messenger client for terminal"
url='https://github.com/boxdot/gurk-rs'
arch=('x86_64')
license=('AGPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'sqlcipher'
)
makedepends=(
  'cargo'
  'protobuf'
)
replaces=('gurk-bin')
options=(!lto)
source=(https://github.com/boxdot/${pkgname}-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('b3d2a3c87e7cb4ceaad42ed329423abd03ef54fb0ca5502f815de0f81c86368b')
b2sums=('5f1488436884fb8f693eefad9f81f510d0196e905d455901a4027e84157378c5b4439d820e0d206fa8479998996e4f0c40149c4aa88b784854468d7367cba1dd')

prepare() {
  cd "${pkgname}-rs-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

check() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo test --frozen
  target/release/gurk --help
}

package() {
  cd "${pkgname}-rs-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gurk
}

# vim: ts=2 sw=2 et:
