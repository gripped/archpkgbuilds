# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=gurk
pkgver=0.8.1
pkgrel=2
pkgdesc="Signal Messenger client for terminal"
url='https://github.com/boxdot/gurk-rs'
arch=('x86_64')
license=('AGPL-3.0-only')
depends=(
  glibc # libc.so libm.so
  libgcc libgcc_s.so
  sqlcipher # libsqlcipher.so
)
makedepends=(
  'cargo'
  'protobuf'
)
replaces=('gurk-bin')
options=(!lto)
source=(https://github.com/boxdot/${pkgname}-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('08077c44f4cee3b5f2f31a0cc90d978e052a51677ed73af61419093ca154163f')
b2sums=('ffdfe87b7569c0e46bdf491191f15b6b055386ce24ba4805a4490a560e4f5a09faa3771c8c13a8af6914fabb5a168bf590a1b64903de155dc65d2259870fe72b')

prepare() {
  cd "${pkgname}-rs-${pkgver}"
  cargo fetch --locked --target host-tuple
}

build() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

check() {
  cd "${pkgname}-rs-${pkgver}"
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo test --frozen
  target/release/gurk --help
}

package() {
  cd "${pkgname}-rs-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gurk
}

# vim: ts=2 sw=2 et:
