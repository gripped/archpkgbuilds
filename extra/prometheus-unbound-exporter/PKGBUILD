# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=prometheus-unbound-exporter
pkgver=0.4.6
pkgrel=1
pkgdesc='Prometheus exporter for Unbound'
arch=('x86_64')
url='https://github.com/letsencrypt/unbound_exporter'
license=('Apache-2.0')
groups=()
depends=('glibc')
makedepends=('git' 'go')
optdepends=('unbound: for local instance')
options=('!lto')
source=("${url}/archive/v${pkgver}/unbound_exporter-v${pkgver}.tar.gz" 'prometheus-unbound-exporter.service')

sha256sums=('6ab5933f67a87025fb744dec4a3ae328a4c9fada623222aa28df8b85105e5311'
            'ab4bfb97e16db7dd389a29e6b7d32b05748af7bf78c2a3ae18f75f0c5645a5aa')

prepare() {
  cd "unbound_exporter-${pkgver}"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod download
}

build() {
  cd "unbound_exporter-${pkgver}"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -v\
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o "build/${pkgname}" \
    .
}

check() {
  cd "unbound_exporter-${pkgver}"

  go test -v ./...
}

package() {
  cd "unbound_exporter-${pkgver}"

  # binary
  install -vDm755 -t "${pkgdir}/usr/bin" "build/${pkgname}"

  # service
  install -vDm0644 "${srcdir}/prometheus-unbound-exporter.service" "${pkgdir}/usr/lib/systemd/system/prometheus-unbound-exporter.service"

  # documentation + license
  install -vDm644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.md
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
