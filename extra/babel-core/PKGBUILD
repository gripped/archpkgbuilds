# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=babel-core
pkgver=7.27.7
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs' 'semver')
makedepends=('npm')
source=("$pkgname-$pkgver.tgz::https://registry.npmjs.org/@babel/core/-/core-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('054d9ff6d94a439080b6188c220a73021e1e0d32d6a359aa8bd8ea1363b1306d04fce335f7d549b76abc073b53c699d25b48b5ca67702d74499d8cef0cce6bdb')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  tar xf $pkgname-$pkgver.tgz package/LICENSE
  install -Dm644 package/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -r "$pkgdir"/usr/lib/node_modules/@babel/core/node_modules/{,.bin/}semver

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
