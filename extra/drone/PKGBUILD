# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Khorne <khorne AT khorne DOT me>

pkgbase=drone
pkgname=(
  'drone'
  'drone-oss'
)
pkgver=2.26.0
pkgrel=1
pkgdesc='Continuous Delivery platform built on container technology'
arch=('x86_64')
url='https://drone.io'
makedepends=('git' 'go')
options=('!lto')
source=(
  "$pkgbase::git+https://github.com/harness/harness.git#tag=v$pkgver"
  'systemd.service'
  'tmpfiles.conf'
  'sysusers.conf'
)
sha512sums=('1aff04ba85e9e6cb7662a8092ec0ca9d96536fd1bb5266eec5ece48b1dee73d5bad22e9a26c8f93ce7a7568c6c3fd85c7ea264c8918bc3d943cdfb22bedd4784'
            '44b29c1388ea6cd0a064c1384f3224d164a37f9ca85fdb956ffafa1ddbcb11f3f01f91701d3b8ce341a593cd85a52cbf102f6549d3c722d6ff8f8eb9cc572ab1'
            'dfdc522b0e17f6b68d86f6599020bc75328de84aa1a393c53a0edc5ed229fb6ef99a0b544eebca6f9eee1b9f2ce04906bcca105792bccf2e49cdd51f10026886'
            'ffb6063109948dbf05d89ceb5e7686f8e9acce549a842c99e40b8966f862de0c90637fb0c3fb25b3ed9a6119181eabefec716c4a44fb57707ac1e42c001e07fb')
b2sums=('9c8e0cc38e86a2637b309b825c03258f1bb833b35e16ae28da6c808b6d9945aa0b3abff6286304c4ee86d0170cc9fca060ad0d371285ba1acdc31b4e580fbd71'
        'ed1d5aebd207997ae2a523786eab0c6a3500080a294864ed5eb6e6251a827ef85a8b2c3bbfdc0ce41bb2f67e14dc65ae627c595688eabb4d822df2f4c971a449'
        '9c1bf8e4a382190f2d1bd560ef5c4a9fadad8c38dcd3cf3b79af42f6d0c16de533ba2d5f6cf676003e4ca6b745833e5e100baf4328111cf540ac5f43f67c6145'
        '098f67ddf919adf039c37deac0b78e22a2b631497683602b484955bb410475d41694fe4c285355af7747d60c3aa4a92ccb18a32e76d71f41fdcfad3b8c527a90')

prepare() {
  cd "$pkgbase"

  # create directory for build output
  mkdir build build-oss

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgbase"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"
  local COMMON_FLAGS=(
    -v
    -buildmode=pie
    -mod=readonly
    -modcacherw
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'"
  )

  # enterprise version
  go build \
    "${COMMON_FLAGS[@]}" \
    -o build \
    ./cmd/...

  # community version
  go build \
    "${COMMON_FLAGS[@]}" \
    -tags "oss nolimit" \
    -o build-oss \
    ./cmd/...
}

check() {
  cd "$pkgbase"
  go test -v ./...
}

package_drone() {
  pkgdesc+=" (Enterprise Edition)"
  license=('LicenseRef-Drone-Non-Commercial')
  # ref: https://discourse.drone.io/t/exec-runner-404-error-on-ping/7922
  depends=('glibc')
  conflicts=('drone-oss')

  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/*

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE NOTICE
}

package_drone-oss() {
  pkgdesc+=" (Community Edition)"
  license=('Apache-2.0')
  depends=('glibc' 'docker')
  conflicts=('drone')

  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build-oss/*
}
