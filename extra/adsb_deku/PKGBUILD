# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=adsb_deku
pkgver=2025.05.03
pkgrel=1
pkgdesc="TUI radar application"
arch=('x86_64')
url="https://github.com/rsadsb/adsb_deku"
license=('MIT')
depends=('gcc-libs' 'dump1090_rs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c9ebc7ad3111ac128d7d164adb075570543448a3f9fa030d9240f84341e9f8c2ef833b29e095566e82a117d481161bf71618c3326dacd3c900b3cc1a47162c9e')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/radar" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
