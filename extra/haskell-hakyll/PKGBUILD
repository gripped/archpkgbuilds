# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hakyll
pkgname=haskell-hakyll
pkgver=4.16.2.2
pkgrel=14
pkgdesc="A static website compiler library"
url="https://jaspervdj.be/hakyll"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-blaze-html' 'haskell-blaze-markup'
         'haskell-data-default' 'haskell-file-embed' 'haskell-fsnotify' 'haskell-hashable'
         'haskell-http-conduit' 'haskell-http-types' 'haskell-lrucache' 'haskell-network-uri'
         'haskell-optparse-applicative' 'haskell-pandoc' 'haskell-random' 'haskell-regex-tdfa'
         'haskell-resourcet' 'haskell-scientific' 'haskell-tagsoup' 'haskell-time-locale-compat'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-wai' 'haskell-wai-app-static'
         'haskell-warp' 'haskell-yaml')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-tasty' 'haskell-tasty-golden'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('dd0c03cea49ef65aaf045bb6cf0128dce2ee4c091dd94652e935fd4a679a1b2e5049729dfa1da941f5c342db32c3a403e9bd807015773f66041e841d79a563f8')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u pandoc
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fcheckexternal -fwatchserver -fpreviewserver -fusepandoc
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
