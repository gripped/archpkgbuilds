# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Johannes Schleifenbaum <johannes [at] js-webcoding [dot] de>

pkgname=testssl.sh
_pkgver=3.2.1
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc='Testing TLS/SSL encryption'
url=https://github.com/testssl/testssl.sh
arch=(any)
license=(GPL-2.0-only)
depends=(
  bash
  bind-tools
  coreutils
  openssl
  procps-ng
  util-linux
)
makedepends=(
  perl
  perl-data-dump
  perl-json
  perl-test-base
)
source=(
  "https://github.com/drwetter/testssl.sh/archive/v${_pkgver}/${pkgname}-${pkgver}.tar.gz"
  set-install-dir.patch
)
sha512sums=('d8dfe4ca6157668d2fecc72fa15dde77e557ae3230698068454aea957ff01b94c751c5f267da8b97d702b35718274726f1d520b88c5357e6ff4c9298f955ffb8'
            '3272f29564325ff08cd9b28077969fcef8c7e5c72e3cd50ba3002b52f6dca0ce5933434a8f3052e0c94face96e7e4fd2af9b6da39c6f3f58565aa01e182fd9c1')
b2sums=('ecbc5481b60c02ce51ccd6669880c1d755896fc67edead72e39b12046048b44799f0449267e77266c4b25fcc6ab547be6f9d715f10dd6c6704f5c9187ab02418'
        'c4f51614e4f7d029f6b27d08a507fe7ac8a04e3dac2a7cc50705c008c1900e7ac4db5d0a3f29e2dac2834ab022b6c6bae26907d9b388fc73ad78dfa59278b963')

prepare() {
  cd ${pkgname}-${_pkgver}
  patch -Np1 < "${srcdir}/set-install-dir.patch"
  rm bin/openssl.Linux*
}

#check() {
#  cd ${pkgname}-${_pkgver}
#  # the bundled openssl segfaults in the tests
#  TESTSSL_INSTALL_DIR="$(pwd)" prove -v
#}

package() {
  cd ${pkgname}-${_pkgver}
  install -Dm 755 testssl.sh "${pkgdir}/usr/bin/testssl"
  install -Dm 644 etc/* -t "${pkgdir}/usr/share/testssl.sh/etc"
  install -Dm 644 Readme.md doc/testssl.1.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/testssl.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
