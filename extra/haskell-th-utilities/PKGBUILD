# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-utilities
pkgname=haskell-th-utilities
pkgver=0.2.5.2
pkgrel=91
pkgdesc="Collection of useful functions for use with Template Haskell"
url="https://github.com/fpco/th-utilities"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-primitive' 'haskell-syb' 'haskell-th-abstraction' 'haskell-th-orphans')
makedepends=('ghc' 'haskell-hspec' 'haskell-th-lift' 'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('a2cf4728885d9e46e51ecd5010626f9ab19190582c97586baecfb00c2724301affdf38b2b3a444c7e5e7284423b85b5f0bae59d4f968ce3941ca502eca50bce5')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
