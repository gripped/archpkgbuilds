# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='bcrypt_pbkdf'
pkgname="ruby-${_gemname}"
pkgver=1.1.1
pkgrel=3
pkgdesc="OpenBSD's bcrypt_pdkfd (a variant of PBKDF2 with bcrypt-based PRF)"
arch=('x86_64')
url='https://github.com/net-ssh/bcrypt_pbkdf-ruby'
license=('MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-bundler' 'ruby-rdoc' 'ruby-rake-compiler' 'ruby-rake-compiler-dock')
checkdepends=('ruby-minitest')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('d0ec5ba137ae4d7f3b35138cd3ce9915365046653f458c39c9fa2f909f90cb79f493d13fbf6e81ae59b3b86b5ce5eab4bfd4fcd0550f3db593e16b16dcd4cbb6')

prepare() {
  cd "${srcdir}/${_gemname}-ruby-${pkgver}"

  sed -E 's|~>|>=|g' --in-place "${_gemname}.gemspec"
  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"
  sed --in-place '/rake-compiler-dock/d' bcrypt_pbkdf.gemspec
  sed --in-place '/has_rdoc/d' bcrypt_pbkdf.gemspec
  rm -rf .github
}

build() {
  cd "${srcdir}/${_gemname}-ruby-${pkgver}"
  rake compile
  rake build
}

check() {
  cd "${srcdir}/${_gemname}-ruby-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${_gemname}-ruby-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install --install-dir "${pkgdir}${_gemdir}" --bindir "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rf ${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/{ext,tmp}
  rm -rf "${pkgdir}/${_gemdir}/cache"
  rm -rf ${pkgdir}/${_gemdir}/extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
  rm -rf "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext"
}

# vim: ts=2 sw=2 et:
