# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=ipycanvas
pkgname=python-$_pyname
pkgver=0.14.2
pkgrel=1
pkgdesc="Interactive widgets library exposing the browser's Canvas API"
url='https://ipycanvas.readthedocs.io/en/latest/'
arch=(any)
license=(BSD-3-Clause)
source=(git+https://github.com/jupyter-widgets-contrib/ipycanvas#tag=v$pkgver)
depends=(ipython
         python
         python-ipywidgets
         python-numpy
         python-pillow
         python-traitlets)
makedepends=(corepack
             git
             jupyterlab
             python-build
             python-hatch-jupyter-builder
             python-hatchling
             python-installer)
sha256sums=('202b70cf4ec1ae6a526877a93e80260456d9fdf8cfea28f5d817e9c24850145d')

prepare() {
  cd $_pyname
# ipycanvas requires yarn>=3
  mkdir bin
  corepack enable --install-directory bin yarn
  yes | bin/yarn set version stable
  bin/yarn install
}
  	
build() {
  cd $_pyname
  PATH="$PWD/bin:$PATH" \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  mv "$pkgdir"/{usr/,}etc
}
