# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: BrinkerVII <brinkervii@gmail.com>

pkgname=luau
pkgver=0.709
pkgrel=1
epoch=1
pkgdesc='A fast, small, safe, gradually typed embeddable scripting language derived from Lua'
arch=(x86_64)
url="https://github.com/luau-lang/$pkgname"
license=(MIT)
depends=(
	gcc-libs
	glibc
)
makedepends=(cmake)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('f02efdf4030e9c2bce4a1f46d04dffee2769d0eb2891e0e317405ade1a36393caa7936811893bd2582a02080fd29c4cd150f30d434c7a46624a1cfbf9871bcbf')

prepare() {
	cd $pkgname-$pkgver
	[ ${pkgver:0:2} = "0." ] || {
		echo "Possibly misstagged version, please verify"
		return 1
	}
}

build() {
	cd $pkgname-$pkgver
	local cmake_options=(
		-D CMAKE_BUILD_TYPE=Release
		-D CMAKE_INSTALL_PREFIX=/usr
		-D CMAKE_CXX_FLAGS='-Wstringop-overread'
		-D LUAU_BUILD_TESTS=On
		-W no-dev
	)
	cmake -S . -B build "${cmake_options[@]}"
	cmake --build build
}

check() {
	cd $pkgname-$pkgver
	./build/Luau.Conformance
	./build/Luau.UnitTest
}

package() {
	cd $pkgname-$pkgver
	# DESTDIR="$pkgdir" cmake --install build
	local executables=(
		luau
		luau-analyze
		luau-ast
		luau-bytecode
		luau-compile
		luau-reduce
	)
	install -vDm0755 -t "$pkgdir/usr/bin/" "${executables[@]/#/build/}"
	install -vDm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
	install -vDm0644 extern/isocline/LICENSE \
		"$pkgdir/usr/share/licenses/$pkgname/isocline-LICENSE.txt"
}

# vim: noet
