# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.9.15
pkgrel=1
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'qt5-base' 'dtkcore' 'dtkgui' 'dtkwidget' 'deepin-image-editor'
         'gcc-libs' 'libraw' 'dtkdeclarative')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
optdepends=('deepin-manual: for help manual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-image-viewer.git#tag=$pkgver")
sha512sums=('c827eba4f7761003d65f3d18f1062dc1d069f528c95fec60b25ca33617d31d5d26fe064d49b8901a41887bfab01f47962bfb8a026e9c04a6d80c10fe646cb132')

prepare() {
  cd deepin-image-viewer
  sed -i -e 's|qt5/plugins|qt/plugins|' -e 's|${Qt5Core_DIR}|/usr/lib/cmake/Qt5Core|' qimage-plugins/libraw/CMakeLists.txt
  git cherry-pick -n b0a7efaf1afb752bd2cbe96250db1e97e4f4da6f
}

build() {
  cd deepin-image-viewer
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  ninja
}

package() {
  cd deepin-image-viewer
  DESTDIR="$pkgdir" ninja install
}
