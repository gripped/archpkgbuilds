# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.9.20
pkgrel=2
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL-3.0-or-later')
depends=('deepin-qt5integration' 'qt5-base' 'dtkcore' 'dtkgui' 'dtkwidget' 'deepin-image-editor'
         'gcc-libs' 'libraw' 'dtkdeclarative')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
optdepends=('deepin-manual: for help manual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-image-viewer.git#tag=$pkgver")
sha512sums=('fba0c3a98a18903fd45b0b2aeded8fef97a4c0f6039fda7a0aa2fa9a7e37738ed3bd9b229dc0cf062826fd8f2f49a61dae17464d3fa4e51a49611ccc89712ff4')

prepare() {
  cd deepin-image-viewer
  sed -i -e 's|qt5/plugins|qt/plugins|' -e 's|${Qt5Core_DIR}|/usr/lib/cmake/Qt5Core|' qimage-plugins/libraw/CMakeLists.txt
}

build() {
  cd deepin-image-viewer
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  ninja
}

package() {
  cd deepin-image-viewer
  DESTDIR="$pkgdir" ninja install
}
