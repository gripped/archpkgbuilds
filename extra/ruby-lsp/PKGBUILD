# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='lsp'
pkgname="ruby-${_gemname}"
pkgver=0.26.6
pkgrel=1
pkgdesc='An opinionated language server for Ruby'
arch=('any')
url="https://github.com/Shopify/ruby-${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-language_server-protocol
  ruby-rbs
  ruby-sorbet-runtime
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/ruby-${pkgname}-${pkgver}.tar.gz")
sha512sums=('7e41550d08513815d1162a7cf58733a1e2af4db449e222fcd314b2e342023ad176e00fccd00efea6426d91bb5c8cbba374c10338b8acba2c22e0d28689c197de')
b2sums=('e7688a99ec23a475e9d63a2da5b881647eae2d7f46800a45d27b951c4ed85f8cf4af1afa6d8fe4f6aa453bc29b4b7748608e2c4f7da531213b343517394c5312')

prepare() {
  cd "${pkgname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${pkgname}.gemspec"
}

build() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${pkgname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${pkgname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${pkgname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${pkgname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${pkgname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
