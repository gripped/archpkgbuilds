# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Dobroslaw Kijowski [dobo] <dobo90_at_gmail.com>

pkgname=python-intervaltree
_pyname=intervaltree
pkgver=3.2.0
pkgrel=1
pkgdesc='Library providing a mutable, self-balancing interval tree for Python'
url="https://github.com/chaimleib/intervaltree"
arch=('any')
license=('Apache-2.0')
depends=(
  'python-sortedcontainers'
)
makedepends=(
  'python-build'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/chaimleib/${_pyname}/archive/${pkgver}.tar.gz")
sha512sums=('7cf567f8a093b198144e2cffda8f70e937d5edbb045e947042b7a62b3771098ccb7dc1911f031de35cd33c6cac96b85d9326a7d237348c7a64379290086360d4')
b2sums=('7de0ecb6fe45aa030f4f604f6d7dd048f419b73b1fbbfc80ad6576891ffd489a3c2d9e46754f9bdc11562a22258d0f69aec77383d70feab3bc2dcab96485db25')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
