# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-power
pkgver=8.0.1
pkgrel=1
pkgdesc='Switchboard Power Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-power
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  dbus
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
_tag=425c966233e41ba50a50454dfff318f12df619a7
source=(git+https://github.com/elementary/switchboard-plug-power.git#tag=${_tag})
b2sums=('0d6331dee68bf1dd55db3d1dbf6cc5e92a9435686d618c1695f885542c6fd1ab8a2648f3fbb60129d30c663215b68e66b6d251c9185f58225f331d718a59b627')

pkgver() {
  cd switchboard-plug-power
  git describe --tags
}

build() {
  arch-meson switchboard-plug-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
