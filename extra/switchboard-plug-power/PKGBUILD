# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-power
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Power Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-power
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  dbus
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
_tag=84ac26fa9c234f793a26d5aa3e20c759085a15d4
source=(git+https://github.com/elementary/switchboard-plug-power.git#tag=${_tag})
b2sums=('51f374ef4cf4ae11e202143cc2bb0dfef9bbc0216473394ee537a6a53008afc80ac1cd84461a073d90eee86666dc97a9a443f3c4b0ec7106922bb43595081ebb')

pkgver() {
  cd switchboard-plug-power
  git describe --tags
}

build() {
  arch-meson switchboard-plug-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
