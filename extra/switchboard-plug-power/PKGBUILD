# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-power
pkgver=8.1.0
pkgrel=1
pkgdesc='Switchboard Power Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-power
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  dbus
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
_tag=4d12d0f7135102bba39250955111b0b190c7af6b
source=(git+https://github.com/elementary/switchboard-plug-power.git#tag=${_tag})
b2sums=('af3f593fe7031cefbfa9b3d7d61aebe49556d3b33d82ad3ff7b37a5bef33bacca573cc02dcce5fd475abb1c035e5e24585c2c8574b7dc8be1ebe19189100ddeb')

pkgver() {
  cd switchboard-plug-power
  git describe --tags
}

build() {
  arch-meson switchboard-plug-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
