# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=klipperscreen
pkgver=0.4.4
pkgrel=2
pkgdesc='GUI for Klipper'
arch=(any)
url=https://github.com/KlipperScreen/KlipperScreen
license=(AGPL-3.0-only)
depends=(
  bash
  cairo
  dbus-glib
  glib2
  gnu-free-fonts
  gdk-pixbuf2
  gtk3
  librsvg
  mpv
  openjpeg2
  pango
  python
  python-cairo
  python-gobject
  python-jinja
  python-mpv
  python-netifaces
  python-numpy
  python-requests
  python-six
  python-websocket-client
  xorg-xinit
  xorg-server
)
makedepends=(git)
_tag=bdf03e8d6f69307d965e70a639318b244971dbac
source=(
  git+https://github.com/jordanruthe/KlipperScreen.git#tag=${_tag}
  klipperscreen.service
)
b2sums=('fff1a70b9e700217353a1da8297157116270f835c572c98c29b6b70969eb60beb0242580447fc81466d99c87411f835d2392d3916d997dba28fb63bf36ea7592'
        '1657a79da1e37a0494e021b19e3b95cd1979a3d8465de7f38b95bda77a3a59a382706bbcb61c60e9d1bb3c84590e74fcb58f36b2e835e0ef4df50d07beab01be')

pkgver() {
  cd KlipperScreen
  git describe --tags | sed 's/^v//'
}

build() {
  python -m compileall KlipperScreen
}

package() {
  install -dm 755 "${pkgdir}"/opt/klipperscreen
  cp -dr --no-preserve=ownership KlipperScreen/{docs,ks_includes,panels,scripts,styles,screen.py} "${pkgdir}"/opt/klipperscreen/
  install -Dm 644 klipperscreen.service -t "${pkgdir}"/usr/lib/systemd/system/
}

# vim: ts=2 sw=2 et:
