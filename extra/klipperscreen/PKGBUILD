# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=klipperscreen
pkgver=0.4.6
pkgrel=3
pkgdesc='GUI for Klipper'
arch=(any)
url=https://github.com/KlipperScreen/KlipperScreen
license=(AGPL-3.0-only)
depends=(
  bash
  cairo
  dbus-glib
  glib2
  gnu-free-fonts
  gdk-pixbuf2
  gtk3
  librsvg
  mpv
  openjpeg2
  pango
  python
  python-cairo
  python-gobject
  python-jinja
  python-mpv
  python-netifaces
  python-numpy
  python-requests
  python-websocket-client
  xorg-xinit
  xorg-server
)
makedepends=(git)
_tag=a04af7cb7951236fc6e08a79d5b5b95415707e0c
source=(
  git+https://github.com/jordanruthe/KlipperScreen.git#tag=${_tag}
  klipperscreen.service
)
b2sums=('dbee8d4e1d0142cc244f0644973392e51ccfac9f86face23bab8e8e387c1e934cc7633c996782bac4481c4b085a635a7211caed59fecfce716bd3bf902cfa00e'
        '1657a79da1e37a0494e021b19e3b95cd1979a3d8465de7f38b95bda77a3a59a382706bbcb61c60e9d1bb3c84590e74fcb58f36b2e835e0ef4df50d07beab01be')

pkgver() {
  cd KlipperScreen
  git describe --tags | sed 's/^v//'
}

build() {
  python -m compileall KlipperScreen
}

package() {
  install -dm 755 "${pkgdir}"/opt/klipperscreen
  cp -dr --no-preserve=ownership KlipperScreen/{docs,ks_includes,panels,scripts,styles,screen.py} "${pkgdir}"/opt/klipperscreen/
  install -Dm 644 klipperscreen.service -t "${pkgdir}"/usr/lib/systemd/system/
}

# vim: ts=2 sw=2 et:
