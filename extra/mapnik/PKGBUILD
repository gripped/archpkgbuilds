# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Dent <thewinch@gmail.com>
# Contributor: orbisvicis <orbisvicis@gmail.com>

pkgname=mapnik
pkgver=4.0.7
pkgrel=1
pkgdesc="Open source toolkit for developing mapping applications"
arch=('x86_64')
url="https://mapnik.org/"
license=('LGPL-2.1-only')
depends=(
  'boost-libs'
  'cairo'
  'freetype2'
  'gcc-libs'
  'gdal'
  'glibc'
  'harfbuzz'
  'icu'
  'libjpeg-turbo'
  'libpng'
  'libtiff'
  'libwebp'
  'libxml2'
  'postgresql-libs'
  'proj'
  'sqlite'
  'zlib'
)
makedepends=(
  'boost'
  'scons'
)
source=("https://github.com/$pkgname/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.bz2")
sha256sums=('8970857ebb0e80f09f7842b85c42da1c75b48510f69185febb21795b9147fa6d')

build() {
  cd $pkgname-v$pkgver
  scons configure  FAST=True \
    PREFIX="/usr" \
    INPUT_PLUGINS=all \
    XMLPARSER=libxml2 \
    DESTDIR="$pkgdir" \
    CUSTOM_CXXFLAGS="$CXXFLAGS -ffat-lto-objects" \
    CUSTOM_LDFLAGS="$LDFLAGS" \
    CUSTOM_DEFINES="-DACCEPT_USE_OF_DEPRECATED_PROJ_API_H=1"
  scons "$(expr "$MAKEFLAGS" : '.*\(\-j[0-9]\+\)')"
}

package(){
  cd $pkgname-v$pkgver
  scons install
}
