# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=sg-323
pkgname=(
  'sg-323-clap'
  'sg-323-vst3'
)
pkgver=1.0.0
pkgrel=1
pkgdesc='Emulation of the Ursa Major Stargate 323 digital reverb'
arch=('x86_64')
url='https://github.com/greyboxaudio/SG-323'
license=('GPL-3.0-only')
groups=('pro-audio')
_common_depends=(
  'glibc'
  'gcc-libs'
  'alsa-lib'
  'freetype2'
)
makedepends=(
  "${_common_depends[@]}"
  'git'
  'cmake'
  'curl'
  'gendesk'
  'jack'
  'libpng'
  'libx11'
  'libxrandr'
  'libxinerama'
  'libxcursor'
  'webkit2gtk'
)
source=(
  "$pkgbase::git+$url#tag=$pkgver"
  'github.com-juce-framework-JUCE::git+https://github.com/juce-framework/JUCE'
  'github.com-free-audio-clap-juce-extensions::git+https://github.com/free-audio/clap-juce-extensions'
  'github.com-free-audio-clap::git+https://github.com/free-audio/clap'
  'github.com-free-audio-clap-helpers::git+https://github.com/free-audio/clap-helpers'
)
sha512sums=('4bd3fcfd7e08435bbe61c46f0fe90a1dd89caea6835755c05332e38ff647568eabacd3a66e9531e87c4e637c9c16d57c29d335178e5611cc14f95126705ce8e6'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
b2sums=('7da2f16048bb6cb866a69d68206e3f736c410824659ce85af1614dfe7c88bddb5e1e47d896068882ac9a04b8745593333d5a19e004a315cb1cd0a88dded02c1d'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgbase"

  # prepare git submodule(s)
  git submodule init
  git config submodule.libs/JUCE.url "$srcdir/github.com-juce-framework-JUCE"
  git config submodule.libs/clap-juce-extensions.url "$srcdir/github.com-free-audio-clap-juce-extensions"
  git -c protocol.file.allow=always submodule update --init

  pushd libs/clap-juce-extensions
  git submodule init
  git config submodule.clap-libs/clap.url "$srcdir/github.com-free-audio-clap"
  git config submodule.clap-libs/clap-helpers.url "$srcdir/github.com-free-audio-clap-helpers"
  git -c protocol.file.allow=always submodule update --init
  popd
}

build() {
  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  cmake \
    -B build \
    -S "$pkgbase"

  cmake --build build
}

package_sg-323-clap() {
  pkgdesc+=' - CLAP plugin'
  groups+=('clap-plugins')
  depends=(
    "${_common_depends[@]}"
    'clap-host'
  )

  install -vDm644 -t "$pkgdir/usr/lib/clap" build/SG323_artefacts/CLAP/SG-323.clap
}

package_sg-323-vst3() {
  pkgdesc+=' - VST3 plugin'
  groups+=('vst3-plugins')
  depends=(
    "${_common_depends[@]}"
    'vst3-host'
  )

  install -vd "$pkgdir/usr/lib/vst3"
  cp -vr build/SG323_artefacts/VST3/SG-323.vst3 "$pkgdir/usr/lib/vst3"
}
