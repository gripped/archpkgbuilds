# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=sg-323
pkgname=(sg-323-{clap,vst3,lv2})
pkgver=1.1.0
pkgrel=2
pkgdesc='Emulation of the Ursa Major Stargate 323 digital reverb'
arch=(x86_64)
url='https://github.com/greyboxaudio/SG-323'
license=(GPL-3.0-only)
groups=(pro-audio)
_common_depends=(
  glibc
  libgcc
  libstdc++
  alsa-lib
  fontconfig
  freetype2
)
makedepends=(
  "${_common_depends[@]}"
  git
  cmake
  libx11
  libxrandr
  libxinerama
  libxcursor
)
source=(
  "$pkgbase::git+$url#tag=$pkgver"
  'github.com-juce-framework-JUCE::git+https://github.com/juce-framework/JUCE'
  'github.com-free-audio-clap-juce-extensions::git+https://github.com/free-audio/clap-juce-extensions'
  'github.com-free-audio-clap::git+https://github.com/free-audio/clap'
  'github.com-free-audio-clap-helpers::git+https://github.com/free-audio/clap-helpers'
)
sha512sums=('ec134779902f95724681eee8ba2a62456751df3d72cac7624e50471a8f951030f44970aafdc63e60f664b6caaaa1d9035742299d3d7733bea32601f2d4578479'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
b2sums=('836bdfe033a2b15c8c4a009d25e061290d94b8ec0f3eed944e5b8c2e762afe15035b87326d579c3716ad68974b82f2e99b7c1c0c046463a466b32824d6a36433'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgbase"

  # prepare git submodule(s)
  git submodule init
  git config submodule.libs/JUCE.url "$srcdir/github.com-juce-framework-JUCE"
  git config submodule.libs/clap-juce-extensions.url "$srcdir/github.com-free-audio-clap-juce-extensions"
  git -c protocol.file.allow=always submodule update --init

  pushd libs/clap-juce-extensions
  git submodule init
  git config submodule.clap-libs/clap.url "$srcdir/github.com-free-audio-clap"
  git config submodule.clap-libs/clap-helpers.url "$srcdir/github.com-free-audio-clap-helpers"
  git -c protocol.file.allow=always submodule update --init
  popd
}

build() {
  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  cmake \
    -B build \
    -S "$pkgbase"

  cmake --build build
}

package_sg-323-clap() {
  pkgdesc+=' - CLAP plugin'
  groups+=(clap-plugins)
  depends=(
    "${_common_depends[@]}"
    clap-host
  )

  install -vDm644 -t "$pkgdir/usr/lib/clap" build/SG323_artefacts/CLAP/SG-323.clap
}

package_sg-323-vst3() {
  pkgdesc+=' - VST3 plugin'
  groups+=(vst3-plugins)
  depends=(
    "${_common_depends[@]}"
    vst3-host
  )

  install -vd "$pkgdir/usr/lib/vst3"
  cp -vr build/SG323_artefacts/VST3/SG-323.vst3 "$pkgdir/usr/lib/vst3"
}

package_sg-323-lv2() {
  pkgdesc+=' - LV2 plugin'
  groups+=(lv2-plugins)
  depends=(
    "${_common_depends[@]}"
    lv2-host
  )

  install -vd "$pkgdir/usr/lib/lv2"
  cp -vr build/SG323_artefacts/LV2/* "$pkgdir/usr/lib/lv2"
}
