# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Achilleas Pipinellis <axilleas at archlinux dot gr>

pkgname=markdownlint-cli
pkgver=0.42.0
pkgrel=1
pkgdesc="Command-line interface for MarkdownLint"
arch=(any)
url="https://github.com/igorshubovych/markdownlint-cli"
license=(MIT)
depends=(nodejs)
makedepends=(npm)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('47e918b2f82deee662fa087fbdab591fb5e50c5fbc7a8fff2b73471cc865c2e1')

prepare() {
  cd $pkgname-$pkgver
  npm clean-install
}

check() {
  cd $pkgname-$pkgver
  npm test
}

package() {
  cd $pkgname-$pkgver
  npm install \
    --global \
    --install-links \
    --prefix "$pkgdir/usr"

  # npm gives ownership of ALL FILES to build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
