# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Achilleas Pipinellis <axilleas at archlinux dot gr>

pkgname=markdownlint-cli
pkgver=0.46.0
pkgrel=1
pkgdesc="Command-line interface for MarkdownLint"
arch=(any)
url="https://github.com/igorshubovych/markdownlint-cli"
license=(MIT)
depends=(nodejs)
makedepends=(npm)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('b8a2521c59bb18037f5433ae19f14cce71c770d7bce50521182700fcbf43b4d6')

prepare() {
  cd $pkgname-$pkgver
  npm clean-install
}

check() {
  cd $pkgname-$pkgver
  npm test
}

package() {
  cd $pkgname-$pkgver
  npm install \
    --global \
    --install-links \
    --prefix "$pkgdir/usr"

  # npm gives ownership of ALL FILES to build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
