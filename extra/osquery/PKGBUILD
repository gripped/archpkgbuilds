# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=osquery
pkgver=5.21.0
pkgrel=1
pkgdesc="SQL powered operating system instrumentation, monitoring, and analytics"
arch=(x86_64)
url="https://osquery.io"
license=('Apache-2.0 OR GPL-2.0-only')
depends=(
  audit
  augeas
  aws-c-auth
  aws-c-cal
  aws-c-common
  aws-c-compression
  aws-c-event-stream
  aws-c-http
  aws-c-io
  aws-c-mqtt
  aws-c-s3
  aws-c-sdkutils
  aws-checksums
  aws-crt-cpp
  aws-sdk-cpp-core
  aws-sdk-cpp-ec2
  aws-sdk-cpp-firehose
  aws-sdk-cpp-iam
  aws-sdk-cpp-kinesis
  bash
  boost-libs
  cryptsetup
  dbus
  device-mapper
  file
  gcc-libs
  gflags
  glibc
  google-glog
  iptables
  libarchive
  libcap
  librdkafka
  openssl
  popt
  rocksdb
  rpm-tools
  s2n-tls
  sleuthkit
  sqlite
  systemd-libs
  thrift
  yara
  zlib
  zstd
)
makedepends=(
  boost
  clang
  cmake
  dpkg
  git
  gtest
  libpcap
  libunwind
  python
  rapidjson
)
backup=(
  etc/osquery/osquery.conf
  etc/sysconfig/osqueryd
)
_url=https://github.com/osquery/osquery
source=(
  "git+$_url.git#tag=$pkgver"
  "git+https://github.com/arangodb/linenoise-ng.git"
  "$pkgname-util-linux::git+https://git.kernel.org/pub/scm/utils/util-linux/util-linux.git"
  # Patchset maintained here:
  # https://github.com/carlsmedstad/osquery/tree/build-on-archlinux
  "$pkgname-0001-Use-system-libraries-where-possible.patch::$_url/commit/cfba4b8acdbb4ed805bcd07e473d5cccefa8e3e6.patch"
  "$pkgname-0002-Disable-submodule-fetching.patch::$_url/commit/1613c65e69bdc69cff1aa6121f46200e533eba68.patch"
  "$pkgname-0003-Remove-include-of-non-existing-header-xlocale.h.patch::$_url/commit/3f963b910767aff9b8e8ed180ff5ed8ccba7f163.patch"
  "$pkgname-0004-Fix-remaining-compilation-issues.patch::$_url/commit/fb198049d765f228f06f0feaa491e531bf1c0905.patch"
  "$pkgname-0005-Do-not-build-system_controls-tables.patch::$_url/commit/db441c2c031dc61afcba1e903ed9a7f7161e7d64.patch"
  "$pkgname-0007-Do-not-build-utils-requiring-libdpkg.patch::$_url/commit/4aae30a46a4a3744e308ab0a82653dba79ad79ff.patch"
  "$pkgname-0008-Fix-systemd-service-and-systemctl-script.patch::$_url/commit/4904934ca19902146215f44a8f6d710f6060dbf3.patch"
  "$pkgname-0009-Adapt-installation-to-using-system-packages.patch::$_url/commit/0777b63c0c2c2cee13f78ab2b6e3100ee1399dd3.patch"
  "$pkgname-gcc-15.patch"
  "$pkgname-boost-1.89.0-compatibility.patch"
)
b2sums=('fed075b3e6b6243f9026c53ec1bfcd683219d21fd2c497078e4100ec24970473d1a214bef64a419dab6e6c70763a119c7711a589b3d47a215db551c3e5f05a93'
        'SKIP'
        'SKIP'
        '928ec1b653f8d06e0dce4d0ce6702060981e02e6033585079b1956e5b6b60030c5346f2eee4fc0cb369610e1bacd636ac7fa89fe00619c0cd58ef3f4e2a13e94'
        'bf550d3383630af0ffd944b84b5229d9dfb51624e5c524cbb9e51947fe65e5bcfdb2af8a010e78d8b0b1ffc8dfb43ce7f2592188ab0a47e386b71a9a45ee20ac'
        '14d67ffb3f5187ab61b38df3c29e032a5a1aa508975212c7c41a11256cf44a41de4c6b3758bd8fc3825eef606527e9dd9161c94ed863c80f0b947428bdbe8464'
        '58341793101eefff97a0f90bce023794d5a605eeac3d0a59e4dd3117e6429bc70610f9a2ea93d42ee49f2e69a24915fe5900de3bd1af8d9d13bd34e3890e834f'
        '51407e82870f430e983a829648db57c003a1f10ba3a1227241f8ff0d37827a4a9b916359c5592002174bc0313da5b4b392f645b04251c8ae4182a5a8f9c74c90'
        'ef0968652a14f721606de48551b79a7eaa322b6a9a328e080c96c5f18e1cd2ea6d561fbbd27a6df9e081105791d8caa08a3d42be4820cc6ed7b44d6f4d96816b'
        '3152e03b97f92c09cd108abea0e67418d0f0f2ab280143f7acbfa0d1c579ea0875908cc98c1a752a9d7539ea118277aeaf5c6fbd17f9ff98bbe2719389743dba'
        '0ba26d44e5927e42ac6e576f2efbdd1357d8afa110223681cdab12fa262430c15a5619a6d456eea01ab202a753e5538c53665c331e7efa758a8ecf14ba90f1d4'
        'a62e3dc663d80080a2452a08547a6f7e925c14554dccf4aab78ff582127fb65378d87ed6ec49e409be61791205ce709cf8fc5e204b1b9aa5c5cd756eabdcd748'
        '6388f0dafa87cf54cb2b82662196424042af354b4eded070a6f0be33f4e701f8049c6671497b6663b26ef8a5782dd8477e21f742678ada14c6762cd83f32f522')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.libraries/cmake/source/linenoise-ng/src.url ../linenoise-ng
  git config submodule.libraries/cmake/source/util-linux/src.url ../$pkgname-util-linux
  git -c protocol.file.allow=always submodule update \
    libraries/cmake/source/linenoise-ng \
    libraries/cmake/source/util-linux

  for patch in ../*.patch; do
    patch -Np1 -i "$patch"
  done
}

build() {
  cd $pkgname
  # This flag causes a linker error:
  # dns_resolvers.cpp:(...): undefined reference to `__res_close'
  export LDFLAGS="${LDFLAGS/-Wl,--as-needed/}"
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DCMAKE_C_COMPILER=clang \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DOSQUERY_BUILD_TESTS=ON \
    -DOSQUERY_BUILD_BPF=OFF \
    -DOSQUERY_BUILD_EXPERIMENTS=OFF \
    -DOSQUERY_BUILD_AWS=ON \
    -DOSQUERY_BUILD_DPKG=OFF \
    -DOSQUERY_USE_SYSTEM_LIBRARIES=ON \
    -DOSQUERY_VERSION="$pkgver"
  cmake --build build
}

check() {
  cd $pkgname
  local skipped_tests=(
    osquery_remote_transports_remotetransportstlstests-test
    osquery_tables_events_tests_seccompeventstests-test
    osquery_tables_system_linux_tests-test
    osquery_tables_system_tests_systemtablestests-test
    osquery_utils_json_jsontests-test
    tests_integration_tables-test
    tools_tests_testfschangestable
    tools_tests_testosqueryd
    tools_tests_testosqueryi
    tools_tests_testrelease
  )
  skipped_tests_pattern="${skipped_tests[0]}$(printf '|%s' "${skipped_tests[@]:1}")"
  ctest --test-dir build --output-on-failure -E "${skipped_tests_pattern}"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" cmake --install build
  install -vdm755 "$pkgdir/var/log/osquery"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -vDm644 tools/deployment/osquery.example.conf "$pkgdir/etc/osquery/osquery.conf"
  install -vDm644 tools/deployment/linux_packaging/osqueryd.sysconfig "$pkgdir/etc/sysconfig/osqueryd"
  install -vDm644 tools/deployment/linux_packaging/rpm/osqueryd.service "$pkgdir/usr/lib/systemd/system/osqueryd.service"
}
