# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=osquery
pkgver=5.14.1
pkgrel=4
pkgdesc="SQL powered operating system instrumentation, monitoring, and analytics"
arch=(x86_64)
url="https://osquery.io"
license=('Apache-2.0 OR GPL-2.0-only')
depends=(
  audit
  augeas
  aws-c-auth
  aws-c-cal
  aws-c-common
  aws-c-compression
  aws-c-event-stream
  aws-c-http
  aws-c-io
  aws-c-mqtt
  aws-c-s3
  aws-c-sdkutils
  aws-checksums
  aws-crt-cpp
  aws-sdk-cpp-core
  aws-sdk-cpp-ec2
  aws-sdk-cpp-firehose
  aws-sdk-cpp-iam
  aws-sdk-cpp-kinesis
  bash
  boost-libs
  cryptsetup
  dbus
  device-mapper
  file
  gcc-libs
  gflags
  glibc
  google-glog
  iptables
  libarchive
  libcap
  librdkafka
  openssl
  popt
  rocksdb
  rpm-tools
  s2n-tls
  sleuthkit
  sqlite
  systemd-libs
  thrift
  yara
  zlib
  zstd
)
makedepends=(
  boost
  clang
  cmake
  dpkg
  git
  gtest
  libpcap
  libunwind
  python
  rapidjson
)
backup=(
  etc/osquery/osquery.conf
  etc/sysconfig/osqueryd
)
_url=https://github.com/osquery/osquery
source=(
  "git+$_url.git#tag=$pkgver"
  "git+https://github.com/arangodb/linenoise-ng.git"
  "git+https://git.kernel.org/pub/scm/utils/util-linux/util-linux.git"
  # Patchset maintained here:
  # https://github.com/carlsmedstad/osquery/tree/build-on-archlinux
  "0001-Use-system-libraries-where-possible.patch::$_url/commit/dd08b4e2f1db5d19003e1728ed6122474060ef91.patch"
  "0002-Disable-submodule-fetching.patch::$_url/commit/e6e3b683fe8ae9131bcbeb4aeb4ae0a4549a4aeb.patch"
  "0003-Remove-include-of-non-existing-header-xlocale.h.patch::$_url/commit/6528b181ebdf75316d145a2819cffa8dcb3a4ec9.patch"
  # https://github.com/osquery/osquery/pull/8401
  "0004-Replace-usage-of-libaudit-function-removed-in-v3.0.7.patch::$_url/commit/8c6c589a2e966f8e231f866cb152bd15832abf22.patch"
  "0005-Fix-remaining-compilation-issues.patch::$_url/commit/a0d28a211afb7291c2ebed321cd91e2c7a686a21.patch"
  "0006-Do-not-build-system_controls-tables.patch::$_url/commit/c52b0ae5a9717ef1da45c05f91f8af4884e6aecd.patch"
  "0007-Do-not-build-block_devices-tables.patch::$_url/commit/c80a3a215eb155c570e76888e74bc81e17ccbeb1.patch"
  "0008-Do-not-build-utils-requiring-libdpkg.patch::$_url/commit/8ae3260cd2bfc7fa108c5bb345a3227b58ba5d7e.patch"
  "0009-Fix-systemd-service-and-systemctl-script.patch::$_url/commit/d792974525c271cfb9984f42d0ee157ed6444705.patch"
  "0010-Adapt-installation-to-using-system-packages.patch::$_url/commit/63d89a012efcd9b4e773abbc63be0e445a58675d.patch"
)
sha256sums=('66eb91a24f544cd13e8053afb1e0dcbd74814dc949270aba184ece6d78fcf53e'
            'SKIP'
            'SKIP'
            'e9f39c6e9a0279311b0034656126141d0ce82d93c02e3f9f98d652ae022f044e'
            'db1bb8553cfae7ef95100950192dd346511fcd3c32b6b60e122c8d56e4e1afe2'
            'b7c4833b19d367a56e3c1dfb6928dbc175d59b22cf0e03f7251907358f9329e7'
            '14440ac27b84daa539f0a3bf88a7d5d195217f8627f113703e25508acb6433e2'
            '6c1f7dbc3139200933991059eda9d1a13cf3e953143d673b3a32b0f916fdedf1'
            '55797a7612ddf1f651c851733ca3cebc76a8047306cf85644f8e809dd566251d'
            'f59ab7a52e6c6ffbf278ca202544f2ab74d02515412f17f9d9e82368c2fa1d72'
            '925be32deb7261c28b03436c3acc0220e1d461291c85e25df58bcc643ec3f46d'
            '79516bce1332b5513687eef3137445d66118f27e79db36be76b86c80c850ac1a'
            '0b4ed3b0c69275a040e2c17b1f25e112da6b7b18c3a48c849bfcabf8e66f422c')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.libraries/cmake/source/linenoise-ng/src.url "$srcdir/linenoise-ng"
  git config submodule.libraries/cmake/source/util-linux/src.url "$srcdir/util-linux"
  git -c protocol.file.allow=always submodule update \
    libraries/cmake/source/linenoise-ng \
    libraries/cmake/source/util-linux

  for patch in "$srcdir"/*.patch; do
    patch -Np1 -i "$patch"
  done
}

build() {
  cd $pkgname
  # This flag causes a linker error:
  # dns_resolvers.cpp:(...): undefined reference to `__res_close'
  export LDFLAGS="${LDFLAGS/-Wl,--as-needed/}"
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DCMAKE_C_COMPILER=clang \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DOSQUERY_BUILD_TESTS=ON \
    -DOSQUERY_BUILD_BPF=OFF \
    -DOSQUERY_BUILD_EXPERIMENTS=OFF \
    -DOSQUERY_BUILD_AWS=ON \
    -DOSQUERY_BUILD_DPKG=OFF \
    -DOSQUERY_USE_SYSTEM_LIBRARIES=ON \
    -DOSQUERY_VERSION="$pkgver"
  cmake --build build
}

check() {
  cd $pkgname
  local skipped_tests=(
    osquery_events_tests_linuxtests-test
    osquery_filesystem_filesystemtests-test
    osquery_remote_transports_remotetransportstlstests-test
    osquery_tables_events_tests_seccompeventstests-test
    osquery_tables_system_linux_tests-test
    osquery_tables_system_tests_systemtablestests-test
    osquery_utils_json_jsontests-test
    tests_integration_tables-test
    tools_tests_testfschangestable
    tools_tests_testosqueryd
    tools_tests_testosqueryi
    tools_tests_testrelease
  )
  skipped_tests_pattern="${skipped_tests[0]}$(printf '|%s' "${skipped_tests[@]:1}")"
  ctest --test-dir build --output-on-failure -E "${skipped_tests_pattern}"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" cmake --install build
  install -vdm755 "$pkgdir/var/log/osquery"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -vDm644 tools/deployment/osquery.example.conf "$pkgdir/etc/osquery/osquery.conf"
  install -vDm644 tools/deployment/linux_packaging/osqueryd.sysconfig "$pkgdir/etc/sysconfig/osqueryd"
  install -vDm644 tools/deployment/linux_packaging/rpm/osqueryd.service "$pkgdir/usr/lib/systemd/system/osqueryd.service"
}
