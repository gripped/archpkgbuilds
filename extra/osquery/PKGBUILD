# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=osquery
pkgver=5.20.0
pkgrel=2
pkgdesc="SQL powered operating system instrumentation, monitoring, and analytics"
arch=(x86_64)
url="https://osquery.io"
license=('Apache-2.0 OR GPL-2.0-only')
depends=(
  audit
  augeas
  aws-c-auth
  aws-c-cal
  aws-c-common
  aws-c-compression
  aws-c-event-stream
  aws-c-http
  aws-c-io
  aws-c-mqtt
  aws-c-s3
  aws-c-sdkutils
  aws-checksums
  aws-crt-cpp
  aws-sdk-cpp-core
  aws-sdk-cpp-ec2
  aws-sdk-cpp-firehose
  aws-sdk-cpp-iam
  aws-sdk-cpp-kinesis
  bash
  boost-libs
  cryptsetup
  dbus
  device-mapper
  file
  gcc-libs
  gflags
  glibc
  google-glog
  iptables
  libarchive
  libcap
  librdkafka
  openssl
  popt
  rocksdb
  rpm-tools
  s2n-tls
  sleuthkit
  sqlite
  systemd-libs
  thrift
  yara
  zlib
  zstd
)
makedepends=(
  boost
  clang
  cmake
  dpkg
  git
  gtest
  libpcap
  libunwind
  python
  rapidjson
)
backup=(
  etc/osquery/osquery.conf
  etc/sysconfig/osqueryd
)
_url=https://github.com/osquery/osquery
source=(
  "git+$_url.git#tag=$pkgver"
  "git+https://github.com/arangodb/linenoise-ng.git"
  "$pkgname-util-linux::git+https://git.kernel.org/pub/scm/utils/util-linux/util-linux.git"
  # Patchset maintained here:
  # https://github.com/carlsmedstad/osquery/tree/build-on-archlinux
  "$pkgname-0001-Use-system-libraries-where-possible.patch::$_url/commit/cfba4b8acdbb4ed805bcd07e473d5cccefa8e3e6.patch"
  "$pkgname-0002-Disable-submodule-fetching.patch::$_url/commit/1613c65e69bdc69cff1aa6121f46200e533eba68.patch"
  "$pkgname-0003-Remove-include-of-non-existing-header-xlocale.h.patch::$_url/commit/3f963b910767aff9b8e8ed180ff5ed8ccba7f163.patch"
  "$pkgname-0004-Fix-remaining-compilation-issues.patch::$_url/commit/fb198049d765f228f06f0feaa491e531bf1c0905.patch"
  "$pkgname-0005-Do-not-build-system_controls-tables.patch::$_url/commit/db441c2c031dc61afcba1e903ed9a7f7161e7d64.patch"
  "$pkgname-0007-Do-not-build-utils-requiring-libdpkg.patch::$_url/commit/4aae30a46a4a3744e308ab0a82653dba79ad79ff.patch"
  "$pkgname-0008-Fix-systemd-service-and-systemctl-script.patch::$_url/commit/4904934ca19902146215f44a8f6d710f6060dbf3.patch"
  "$pkgname-0009-Adapt-installation-to-using-system-packages.patch::$_url/commit/0777b63c0c2c2cee13f78ab2b6e3100ee1399dd3.patch"
  "$pkgname-gcc-15.patch"
  "$pkgname-boost-1.89.0-compatibility.patch"
)
sha256sums=('09173537a0d65d4d931c2c2886b8a05baaaad9dafa3f7b5bf9f5b9108bd7c397'
            'SKIP'
            'SKIP'
            '343191e2dc7f58c4671912151e52fbad98ac03a077b3d36f1e8e27783689c548'
            '1d3dbfaa3d67637f6aab20ba9833d3f0f909cf0506fc1c968a1d26bf115ce8ab'
            '657cfa3e170d49ead14a872317a4d0291afa23ecc60d5830418793996c1c713f'
            '1aeedb7b74a72d5398e5867d3cf845e89edb37d4aac051203db0c3f2c35f788a'
            '3d9f70aa55001bec7005d84e75f302f2e84ae93d1c4b679fcff7d96ebb53e7d7'
            '56ed0eba778aacf96115a4a8ff392e6cd622598595f082db9e7db05229ee6c5f'
            'fb9dee5aa0504d59a24e05a9588888bdf1c0db13c75dad39c6ca9a6adc490696'
            'e50be792bb5e6d0b0cfd32ab5f30043aefc9b40c343bf8124f9bd510709c83aa'
            '21e0bf884039ba7f0c16cefca5ca7d39b5daeee49518c5c0d4a374b15a27e1ae'
            'd12c724315ff4ba492f4e2765c9d17389930d93d7140d053b5c8251b905b39ca')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.libraries/cmake/source/linenoise-ng/src.url ../linenoise-ng
  git config submodule.libraries/cmake/source/util-linux/src.url ../$pkgname-util-linux
  git -c protocol.file.allow=always submodule update \
    libraries/cmake/source/linenoise-ng \
    libraries/cmake/source/util-linux

  for patch in ../*.patch; do
    patch -Np1 -i "$patch"
  done
}

build() {
  cd $pkgname
  # This flag causes a linker error:
  # dns_resolvers.cpp:(...): undefined reference to `__res_close'
  export LDFLAGS="${LDFLAGS/-Wl,--as-needed/}"
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DCMAKE_C_COMPILER=clang \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DOSQUERY_BUILD_TESTS=ON \
    -DOSQUERY_BUILD_BPF=OFF \
    -DOSQUERY_BUILD_EXPERIMENTS=OFF \
    -DOSQUERY_BUILD_AWS=ON \
    -DOSQUERY_BUILD_DPKG=OFF \
    -DOSQUERY_USE_SYSTEM_LIBRARIES=ON \
    -DOSQUERY_VERSION="$pkgver"
  cmake --build build
}

check() {
  cd $pkgname
  local skipped_tests=(
    osquery_remote_transports_remotetransportstlstests-test
    osquery_tables_events_tests_seccompeventstests-test
    osquery_tables_system_linux_tests-test
    osquery_tables_system_tests_systemtablestests-test
    osquery_utils_json_jsontests-test
    tests_integration_tables-test
    tools_tests_testfschangestable
    tools_tests_testosqueryd
    tools_tests_testosqueryi
    tools_tests_testrelease
  )
  skipped_tests_pattern="${skipped_tests[0]}$(printf '|%s' "${skipped_tests[@]:1}")"
  ctest --test-dir build --output-on-failure -E "${skipped_tests_pattern}"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" cmake --install build
  install -vdm755 "$pkgdir/var/log/osquery"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -vDm644 tools/deployment/osquery.example.conf "$pkgdir/etc/osquery/osquery.conf"
  install -vDm644 tools/deployment/linux_packaging/osqueryd.sysconfig "$pkgdir/etc/sysconfig/osqueryd"
  install -vDm644 tools/deployment/linux_packaging/rpm/osqueryd.service "$pkgdir/usr/lib/systemd/system/osqueryd.service"
}
