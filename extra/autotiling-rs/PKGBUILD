# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=autotiling-rs
pkgver=0.1.6
pkgrel=1
pkgdesc='Automatically alternates container layouts between horizontal and vertical'
arch=('x86_64')
url='https://github.com/ammgws/autotiling-rs'
license=('MIT')
makedepends=('rust')
source=("https://github.com/ammgws/autotiling-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('6bb14bb18be739ad46bfff94eda8ed17d063231c1755333c63e53df109f89fe2f81651b3552c4de51c277242f205e4e27c9c29ccc671451431b5c85c4c4d8734')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 target/release/${pkgname} "${pkgdir}"/usr/bin/${pkgname}
}

