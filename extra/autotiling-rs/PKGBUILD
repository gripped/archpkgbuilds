# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=autotiling-rs
pkgver=0.1.4
pkgrel=1
pkgdesc='Automatically alternates container layouts between horizontal and vertical'
arch=('x86_64')
url='https://github.com/ammgws/autotiling-rs'
license=('MIT')
makedepends=('rust')
source=("https://github.com/ammgws/autotiling-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('5d815d66e27208450fa99614c9f18a6f3ffa24d3001b72ef7449a363464b6cca0016f4f7770fc4e5abdd292fedf5f1ee6e3d0ba35e034633bf374cb217d9fec5')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 target/release/${pkgname} "${pkgdir}"/usr/bin/${pkgname}
}

