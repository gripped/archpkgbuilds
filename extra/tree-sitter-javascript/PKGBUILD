# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=tree-sitter-javascript
pkgver=0.25.0
pkgrel=1
pkgdesc='JavaScript grammar for tree-sitter'
arch=('x86_64')
url=https://github.com/tree-sitter/tree-sitter-javascript
license=('MIT')
groups=('tree-sitter-grammars')
makedepends=('git' 'tree-sitter-cli')
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("git+$url.git#tag=v$pkgver")
b2sums=('2c879584ea7ae98160f103c623ab898dded151a9d6dc8ade495ba31335addfae92158e1d1e7632fa6651a9c7444d4a41d45a02d0b4b6c28ab96737e5dc8806b9')

prepare() {
  cd $pkgname
  tree-sitter generate --no-bindings src/grammar.json
}

build() {
  cd $pkgname
  make PREFIX=/usr PARSER_URL="$url"
}

package() {
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
