# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-mtree
_upstream=alpm
pkgver=0.3.2
pkgrel=1
pkgdesc="A CLI for parsing of ALPM MTREE files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-mtree/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('c07dba4f99841c87d986614fb57c987ac575fbbd8362297dbfd6e987f2eafbcbf62913adb64c8e3e0fd092f9ea3074de5ac04d86c41ae905ef238cb348657bcc')
b2sums=('6f07f21fb1ffbbcedd43d8bf98c673baa8f2a70887eeffa899aab26b02ed0e56119c0fe5f52c52289fcfe904ab7424e0342b262a2a7d5db1e01795d5865bc1d4')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --package $pkgname --features cli

  just generate shell_completions $pkgname
  just generate specifications $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
