# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-mtree
_upstream=alpm
pkgver=0.2.3
pkgrel=1
pkgdesc="A CLI for parsing of ALPM MTREE files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-mtree/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('40370fb0bef1fad86897fdec0fd2406a78642ac0a0b771f83d1d13012d95fb31f321eacc14475b6d560031331c2a5f4dada8ec4b061a428f2e8f26543897fe19')
b2sums=('b255434aeecff0f8a4f89c6070976593bb1c1a98cf44c9df59fbcc7121b71c884e50e4db87e94733a147b4329bd664fa415980279c05d61d342e909334a2d63a')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --package $pkgname --features cli

  just generate shell_completions $pkgname
  just generate specifications $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
