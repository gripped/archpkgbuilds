# Maintainer: Morgan <morganamilo@archlinux.org>

pkgname=paccat
pkgver=1.3.1
pkgrel=1
pkgdesc='Print pacman package files'
url='https://github.com/morganamilo/paccat'
source=("$pkgname-$pkgver.tar.gz::https://github.com/Morganamilo/paccat/archive/v$pkgver.tar.gz")
arch=('x86_64')
license=('GPL-3.0-or-later')
makedepends=('cargo')
depends=('libalpm.so')
optdepends=('bat: syntax highlighting')
sha256sums=('637a2956788e07a9824705b8e95fbcc2a6be24a628175e3c2ea2ffced519879e')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build () {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  COMPLETIONS_DIR=target cargo build --frozen --release --target-dir target
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 target/release/paccat -t "$pkgdir/usr/bin/"

  install -Dm644 man/$pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -Dm644 target/$pkgname.bash -t "$pkgdir/usr/share/bash-completion/completions"
  install -Dm644 target/$pkgname.fish -t "$pkgdir/usr/share/fish/completions"
  install -Dm644 target/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions"
}
