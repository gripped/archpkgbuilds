# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=batctl
pkgver=2025.5
_advver=$pkgver
pkgrel=1
pkgdesc='B.A.T.M.A.N. advanced control and management tool'
arch=('x86_64')
url='http://www.open-mesh.net/'
license=('GPL-2.0-only AND MIT')
depends=('libnl')
source=("http://downloads.open-mesh.net/batman/releases/batman-adv-$_advver/batctl-$pkgver.tar.gz"{,.asc})
sha512sums=('dfb97320302efc9ecc0734581154cfbc6e1a5ba4e85151bc3795c6ecfb9ff6e9c3283e33deb3146a59b1fab98e27b9da24a58ae7dbbfb8be5c81907d6f0b4b5d'
            'SKIP')
validpgpkeys=('2DE9541A85CC87D5D9836D5E0C8A47A2ABD72DF9')  # Simon Wunderlich

build(){
  cd batctl-$pkgver
  make
}

package() {
  cd batctl-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr SBINDIR=/usr/bin install
  install -Dm644 LICENSES/preferred/MIT -t $pkgdir/usr/share/licenses/$pkgname/
}
