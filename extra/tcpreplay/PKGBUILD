# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: SÃ©bastien Duquette <ekse.0x@gmail.com>
# Contributor: Dawid Wrobel <cromo@klej.net>

pkgname=tcpreplay
pkgver=4.5.1
pkgrel=1
pkgdesc='Gives the ability to replay previously captured traffic in a libpcap format'
url='https://tcpreplay.appneta.com'
arch=('x86_64')
license=('GPL-3.0-only')
depends=('glibc' 'libpcap' 'libdnet' 'autogen')
makedepends=('tcpdump')
optdepends=('tcpdump: decoding of packets support')
options=('!emptydirs')
source=(https://github.com/appneta/tcpreplay/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('75d6bbdf469d4eab5c5206422ec1dcf8ccda1469b1cf36fa37d65550fc781f91919e315708f2cca6b25ac396a4c2395c67f653b6a11901b5568d450ae4424333'
            'SKIP')
b2sums=('72957f917502e52e463c2ccb22063ba831d96ace40694d7f8bf4064c4aa9e24fb5d94438777a03f9e2c1f38684952c4c0ffd8f644901fe1e7aa8748665630395'
        'SKIP')
validpgpkeys=('84E4FA215C934A7D97DC76D5E9E2149793BDE17E') # Fred Klassen <tcpreplay@appneta.com>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-local-libopts \
    --enable-dynamic-link \
    --enable-shared \
    --with-libdnet \
    --with-tcpdump=/usr/bin/tcpdump \
    ac_cv_header_net_bpf_h=n
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 -C test tcpprep
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
