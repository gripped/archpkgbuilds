# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.9.2
pkgrel=2
pkgdesc='A familiar, modern desktop environment'
arch=('x86_64')
url='https://github.com/BuddiesOfBudgie/budgie-desktop'
license=('GPL-2.0-only AND LGPL-2.1-only')
groups=('budgie')
depends=(
    'accountsservice'
    'budgie-control-center'
    'budgie-screensaver' 
    'budgie-session'
    'gnome-themes-standard'
    'ibus'
    'libgee'
    'libpeas'
    'libwnck3'
    'libxfce4windowing'
    'magpie-wm'
    'xdg-desktop-portal-gtk'
    'zenity'
)
makedepends=(
    'glib2-devel'
    'gobject-introspection'
    'intltool'
    'meson'
    'sassc'
    'vala'
)
optdepends=(
    'budgie-desktop-view: Desktop icons'
    'budgie-backgrounds: Default background set'
    'network-manager-applet: Network management in the panel'
    'power-profiles-daemon: Manage power and performance from the panel'
    'switcheroo-control: Open apps with the secondary GPU'
)
source=("https://github.com/BuddiesOfBudgie/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc}
        "fix_build_with_libxfce4windowing_4.20.patch"
        "30_org.archlinux.budgie-desktop.gschema.override")
validpgpkeys=('0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl <me at joshuastrobl dot com>
b2sums=('f6a91f005d2572d6adf2cb00f24a2317c1851d62e6b4dcc250443b61f20698062a8adccc273a0041172b4608493f75088eb22f4512d2fa620d10dfa828a715fa'
        'SKIP'
        '5b36449fbd9d49dbaebcd37786656981d26afc00f57485a45cf99928840a86ca6baf1c4ff1c228dc0ca0e6a710fcd82ac67b2b941a1fa7cb78f5124b823826a6'
        '8b2e5dce5b93af93b1843f5f81b7496653aca5d536e3b77ae143749e8395a8158b044406f75c923f7c78416348ed4880521a6a5e67668352b8616124ceb08ce3')

prepare() {
    cd "$pkgname-$pkgver"
    # Temporary patch to fix build with libxfce4windowing 4.20
    # See https://github.com/BuddiesOfBudgie/budgie-desktop/commit/a23ff6ab25e9def5810aab68d8901d0525d65864
    patch -Np1 < "$srcdir/fix_build_with_libxfce4windowing_4.20.patch"
}

build() {
    arch-meson "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
    install -Dm 644 30_org.archlinux.budgie-desktop.gschema.override -t "${pkgdir}/usr/share/glib-2.0/schemas"
}
