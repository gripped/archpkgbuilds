# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.9.2
pkgrel=3
pkgdesc='A familiar, modern desktop environment'
arch=('x86_64')
url='https://github.com/BuddiesOfBudgie/budgie-desktop'
license=('GPL-2.0-only AND LGPL-2.1-only')
groups=('budgie')
depends=(
    'accountsservice'
    'budgie-control-center'
    'budgie-screensaver'
    'budgie-session'
    'gnome-themes-standard'
    'ibus'
    'libgee'
    'libpeas'
    'libwnck3'
    'libxfce4windowing'
    'magpie-wm'
    'xdg-desktop-portal-gtk'
    'zenity'
)
makedepends=(
    'glib2-devel'
    'gobject-introspection'
    'intltool'
    'meson'
    'sassc'
    'vala'
)
optdepends=(
    'budgie-desktop-view: Desktop icons'
    'budgie-backgrounds: Default background set'
    'network-manager-applet: Network management in the panel'
    'power-profiles-daemon: Manage power and performance from the panel'
    'switcheroo-control: Open apps with the secondary GPU'
)
source=("https://github.com/BuddiesOfBudgie/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc}
        "30_org.archlinux.budgie-desktop.gschema.override"
        "c24091bb424abe99ebcdd33eedd37068f735ad2a.patch"
        "46c83b1265b4230668da472d9ef6926941678418.patch"
        "ba8170b4f3108f9de28331b6a98a9d92bb0ed4de.patch")
validpgpkeys=('0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl <me at joshuastrobl dot com>
b2sums=('f6a91f005d2572d6adf2cb00f24a2317c1851d62e6b4dcc250443b61f20698062a8adccc273a0041172b4608493f75088eb22f4512d2fa620d10dfa828a715fa'
        'SKIP'
        '8b2e5dce5b93af93b1843f5f81b7496653aca5d536e3b77ae143749e8395a8158b044406f75c923f7c78416348ed4880521a6a5e67668352b8616124ceb08ce3'
        'd370bf9f0bc804875e17a55d46f62598b8334af61ecb0ec0fe933750765cde1a464ae799ab94d50dee76c646558fb97f9f53d175b21ec3279b7baf909528c5fb'
        'b577c84bbfc4a8a2c71e41db311af9063ab9388c0b7fad72264682116c3bb707a4ae0e2bee2a2979b89fc068373cdfeb5ad274567f59e76acf7b8557081200c1'
        '423756a6bb2e57e2b978e9fd026c320b9d09adee97cf92668006d48f5d6a4a6114de17a4c1da27a741e06a9d420fcba5ea1048cbed26911f677436ca70cd4b7d')

prepare() {
    cd "$pkgname-$pkgver"

    patch -Np1 < "$srcdir/c24091bb424abe99ebcdd33eedd37068f735ad2a.patch"
    patch -Np1 < "$srcdir/46c83b1265b4230668da472d9ef6926941678418.patch"
    patch -Np1 < "$srcdir/ba8170b4f3108f9de28331b6a98a9d92bb0ed4de.patch"
}

build() {
    arch-meson "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
    install -Dm 644 30_org.archlinux.budgie-desktop.gschema.override -t "${pkgdir}/usr/share/glib-2.0/schemas"
}
