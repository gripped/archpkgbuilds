# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.7.2
pkgrel=6
pkgdesc='A familiar, modern desktop environment'
arch=('x86_64')
url='https://github.com/BuddiesOfBudgie/budgie-desktop'
license=('GPL2' 'LGPL')
groups=('budgie')
depends=('budgie-screensaver' 'budgie-control-center' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard'
         'libgee' 'libpeas' 'libwnck3' 'magpie-wm' 'accountsservice' 'ibus' 'zenity')
makedepends=('gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('budgie-desktop-view: Desktop icons'
            'budgie-backgrounds: Default background set'
            'network-manager-applet: Network management in the panel'
            'switcheroo-control: Open apps with the secondary GPU')
source=("https://github.com/BuddiesOfBudgie/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc}
        "30_org.archlinux.budgie-desktop.gschema.override"
        "387.patch")
validpgpkeys=('0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl <me at joshuastrobl dot com>
b2sums=('4457867794189e1d127d69366c0f7b70889f01ce01b50915a291440ffb1a66c33973725503df375eb508a0e06d2cca6322a4dec63ad73ff55fcdba7988a6ddf1'
        'SKIP'
        '8b2e5dce5b93af93b1843f5f81b7496653aca5d536e3b77ae143749e8395a8158b044406f75c923f7c78416348ed4880521a6a5e67668352b8616124ceb08ce3'
        '5efaf9079804d77739675c5bcf75731a0e8547eca0d82f0c3128d679fc6bca721548fda1d66660927a8fa2c59fc53da0d911da5918776c9353dc561e7253f120')

prepare() {
    cd "$pkgname-$pkgver"
    patch -Np1 < "${srcdir}/387.patch"
}

build() {
    arch-meson "$pkgname-$pkgver" build -Duse-old-zenity=true
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
    install -Dm 644 30_org.archlinux.budgie-desktop.gschema.override -t "${pkgdir}/usr/share/glib-2.0/schemas"
}
