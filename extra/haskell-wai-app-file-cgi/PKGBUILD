# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=wai-app-file-cgi
pkgname=haskell-wai-app-file-cgi
pkgver=3.1.11
pkgrel=13
pkgdesc="File/CGI/Rev Proxy App of WAI"
url="http://www.mew.org/~kazu/proj/mighttpd/"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-case-insensitive' 'haskell-conduit'
         'haskell-conduit-extra' 'haskell-data-default-class'  'haskell-http-client'
         'haskell-http-conduit' 'haskell-http-date' 'haskell-http-types' 'haskell-mime-types'
         'haskell-network' 'haskell-sockaddr' 'haskell-static-hash' 'haskell-wai'
         'haskell-wai-conduit' 'haskell-warp' 'haskell-word8')
makedepends=('ghc' 'haskell-http' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('93d820c78f7068adf00abd803701e12d5b757d12372ce1a9dc3e7479cd71492d')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # https://github.com/kazu-yamamoto/wai-app-file-cgi/issues/20
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
