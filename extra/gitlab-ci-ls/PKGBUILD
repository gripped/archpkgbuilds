# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=gitlab-ci-ls
pkgver=1.1.11
pkgrel=1
pkgdesc='Language server for Gitlab CI'
arch=(x86_64)
url=https://github.com/alesbrelih/gitlab-ci-ls
license=(MIT)
depends=(
  gcc-libs
  openssl
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+$url.git#tag=$pkgver")
b2sums=('1bc57404fde92435cbe37e0c7af4822911781449283510f47e2d89565b3c5ac289192bc83f265ae508fddebba034c573936f14f4761a419b5bef45e7ff2010b9')

prepare() {
  cd $pkgname
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname \
    vscode-extension/LICENSE.txt
}
