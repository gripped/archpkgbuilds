# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=gitlab-ci-ls
pkgver=1.1.9
pkgrel=1
pkgdesc='Language server for Gitlab CI'
arch=(x86_64)
url=https://github.com/alesbrelih/gitlab-ci-ls
license=(MIT)
depends=(
  gcc-libs
  openssl
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+$url.git#tag=$pkgver")
b2sums=('43012cfeb4c36c3a13b2bb28b84d791b5de27cab5219eb76e2f480e9945d9c7018252c1c3164af1baef9fd8011d57cc7e72d622614e08feedc4a93798781d298')

prepare() {
  cd $pkgname
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname \
    vscode-extension/LICENSE.txt
}
