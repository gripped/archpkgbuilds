# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=gitlab-ci-ls
pkgver=1.1.8
pkgrel=1
pkgdesc='Language server for Gitlab CI'
arch=(x86_64)
url=https://github.com/alesbrelih/gitlab-ci-ls
license=(MIT)
depends=(
  gcc-libs
  openssl
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+$url.git#tag=$pkgver")
b2sums=('6904b31a19b81395cc436fe1af132d7d4a115f5a87692fdb8d43e946e4c06f16b3304c4c73587ea2c3c38c26cfb8327a5b06b1558abfead2c33d5a76265ff60b')

prepare() {
  cd $pkgname
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname \
    vscode-extension/LICENSE.txt
}
