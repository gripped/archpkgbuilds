# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=gitlab-ci-ls
pkgver=1.2.2
pkgrel=1
pkgdesc='Language server for Gitlab CI'
arch=(x86_64)
url=https://github.com/alesbrelih/gitlab-ci-ls
license=(MIT)
depends=(
  gcc-libs
  openssl
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+$url.git#tag=$pkgver")
b2sums=('f366ff32e8aaa8a43472dc3ef204aa5d7fb32a3f44fde212dbb175112c175d73aa9f257f109a0f89483a504217bad805f780a46b26d9157aa433dbe9256657dd')

prepare() {
  cd $pkgname
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname \
    vscode-extension/LICENSE.txt
}
