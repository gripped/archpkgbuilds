# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=pipe-rename
pkgver=1.6.6
pkgrel=3
pkgdesc='Rename your files using your favorite text editor'
arch=(x86_64)
url='https://github.com/marcusbuffett/pipe-rename'
license=(MIT)
depends=(glibc libgcc)
makedepends=(git rust)
options=(!lto)
source=("$pkgname::git+$url.git#tag=$pkgver")
sha512sums=('bfdf0428bfc6dcd3128acb24007f9ead452a4113f7811c39c91459e4644e0060498b2dbc8db8d60102b780b67eb55dcc45cc1ac577908d35f4aa068a3b790e64')
b2sums=('28377d41f593bfb6c3caa3eab5ce9d758d7432a72f865f128db1b3e9028aef37b67f58e9b60cb350731daf16e0ada227368a3be45fa8c3f98326214e0592d709')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/renamer

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
