# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=pipe-rename
pkgver=1.6.6
pkgrel=1
pkgdesc='Rename your files using your favorite text editor'
arch=(x86_64)
url='https://github.com/marcusbuffett/pipe-rename'
license=(MIT)
depends=(gcc-libs glibc)
makedepends=(git rust)
options=(!lto)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('e1aef55bfcdbcf373b97a13a28e3dffd8043259d056848c4f6cc7acc7f5f3f52ecfbd30fb4ae367b78c7d52d853a47e0b73c130987806aa5bb87acbf961107bd')
b2sums=('1630b02b1eeb934aeab2c5943cd8ec35214e29ba2f849aa31ac95bcef77a12d2990b4b64a5c0931a4ceb0ba927fe310f54480e3ce3adb805e69aa8bd13f9b7af')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/renamer

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
