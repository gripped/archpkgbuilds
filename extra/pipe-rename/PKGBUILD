# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=pipe-rename
pkgver=1.6.5
pkgrel=3
pkgdesc='Rename your files using your favorite text editor'
arch=('x86_64')
url='https://github.com/marcusbuffett/pipe-rename'
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cargo' 'git')
options=('!lto')
source=("git+$url#tag=$pkgver"
        ${pkgname}-license::https://raw.githubusercontent.com/marcusbuffett/pipe-rename/833bd82800ba772e30b495f65ad073c6f5305c82/LICENSE)
sha256sums=('7f19f03aae18641ca8b52528eba6779ef11dc59b0948d5c6386d59b9a4417128'
            'cc7ce819e6fe29512855a738274da29acf1d78a3ab9afde619154581a19ccdef')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/renamer

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir"/${pkgname}-license
}
