# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=virt-firmware
pkgver=25.9
pkgrel=1
pkgdesc="Collection of tools for edk2 firmware images"
arch=(any)
url="https://gitlab.com/kraxel/virt-firmware"
license=(GPL-2.0-or-later)
depends=(
  python
  python-crc32c  # undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/4
  python-cryptography
  python-pefile
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  systemd  # tests require systemd-detect-virt(?!)
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('e0d630cb95a2c314d065d1748ba473717822aecb78685f9eeac90b41e8d12b4bd960bc2fb05b5c8600cf6d63faf5dc11d3f174532c5bf03b5b5474454fb7b177')
b2sums=('616c3b0a70d2bb941ceaadcfccbf2c4e62b65dd51d76870235f578c065bb82acd3085842436dc59871e2bf6b1d2a2c2ce9cbb10c9ab53d5d1898cd0b65edb94d')

build() {
  cd $pkgname-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-v$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest -vv tests/tests.py
}

package() {
  cd $pkgname-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 man/virt-*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
