# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=virt-firmware
pkgver=25.12
pkgrel=2
pkgdesc="Collection of tools for edk2 firmware images"
arch=(any)
url="https://gitlab.com/kraxel/virt-firmware"
license=(GPL-2.0-or-later)
depends=(
  python
  python-crc32c  # undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/4
  python-cryptography
  python-pefile
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  systemd  # tests require systemd-detect-virt(?!)
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('978c493bed3c908d9d176e16239f8a7e674abf5f9df43e369c67c5fb2ab73a9966db5632ceffc4d34b4bc4709cd6cda2913f40e50c513a67c6ab0ff2dd79f171')
b2sums=('853367ed2bec725dcb7273c2fbd16311167b9a3beccb06c9ead211fb2d1f4f91d9826d60038ca3136cfe02d6da1166e0d14da8cf61b9456fbd9001842a8c1179')

build() {
  cd $pkgname-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-v$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest -vv tests/tests.py
}

package() {
  cd $pkgname-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 man/virt-*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
