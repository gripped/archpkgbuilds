# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=virt-firmware
pkgver=24.11
pkgrel=1
pkgdesc="Collection of tools for edk2 firmware images"
arch=(any)
url="https://gitlab.com/kraxel/virt-firmware"
license=(GPL-2.0-or-later)
depends=(
  python
  python-crc32c  # undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/4
  python-cryptography
  python-pefile
  python-setuptools
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
  systemd  # tests require systemd-detect-virt(?!)
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('f433aa1115fbd61fbde912850e7abd0c615ccc9dd0ea6a6bf96a3ed64189cac65aa185c605239e7f13569e4827a85a232aa903c20a24140ea9944099a5516c20')
b2sums=('118fa687758117796082e17d74144c8631f896603dcca1fce423ef747c5273788449f1037bd414f17be8c0d59dd694230dbcfcb34eedb96f199fc6e35af0ab1f')

build() {
  cd $pkgname-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-v$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest -vv tests/tests.py
}

package() {
  cd $pkgname-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 man/virt-*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
