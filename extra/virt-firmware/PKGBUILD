# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=virt-firmware
pkgver=25.4.1
pkgrel=1
pkgdesc="Collection of tools for edk2 firmware images"
arch=(any)
url="https://gitlab.com/kraxel/virt-firmware"
license=(GPL-2.0-or-later)
depends=(
  python
  python-crc32c  # undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/4
  python-cryptography
  python-pefile
  python-setuptools
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
  systemd  # tests require systemd-detect-virt(?!)
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('6af62f87afbeb948b39bbed09589ec0b21f28e87532f24a2a5db25bd18bddef5cc0d20b3143e6332385d6f22bdd5049ee06e4d599a3021779ca6b12699c69c08')
b2sums=('130e113e95f4c38f18b2269f3beb9bf518b47a98943fe3737dc2bca8515fd5a107b697ac5c3b10f58f330c39c53abdc2555bd5996c97351070cb6c646d8ba221')

build() {
  cd $pkgname-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-v$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest -vv tests/tests.py
}

package() {
  cd $pkgname-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 man/virt-*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
