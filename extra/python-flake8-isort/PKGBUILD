# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: neodarz <neodarz at neodarz dot net>

pkgname=python-flake8-isort
pkgver=7.0.0
pkgrel=2
pkgdesc='Flake8 plugin to validate annotations complexity'
arch=(any)
url='https://github.com/gforcada/flake8-isort'
license=(GPL-2.0-only)
depends=(
  python
  flake8
  python-isort
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-hatchling
)
checkdepends=(python-pytest python-toml)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('9c0a38d4356a70ccdbdd1854ffab5af147dd8a206cbd28195d983267b1aa29b724175f2a072a566b996e02c0f5f4610d173b64ba795651aeb26de9d580d46881')
b2sums=('d45a2c6d72a2c994cec1f0fae8b996e56b1f08d66c18ac029b5db82bfd485853e0b6c9a4cd28b2d010555b9268b37cb2f60ad55df41a78fac9360118b61654b8')

build() {
  cd "$pkgname"

  
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest run_tests.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
