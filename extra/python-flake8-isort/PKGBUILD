# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: neodarz <neodarz at neodarz dot net>

pkgname=python-flake8-isort
pkgver=6.1.2
pkgrel=1
pkgdesc='Flake8 plugin to validate annotations complexity'
arch=(any)
url='https://github.com/gforcada/flake8-isort'
license=(GPL-2.0-only)
depends=(
  python
  flake8
  python-isort
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-hatchling
)
checkdepends=(python-pytest python-toml)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('c83f15fc753fea8a05b4c536aec6cc8cdc2582bdd733a60cd2d693d98a1a4264d76d815dc6ce6750dd2590dc60a03129f87f564ed04a74bfdbf54bfd02908456')
b2sums=('59898002f7650d9e364370f823cb7a278bb7e49f39f8502be97536ab8d85644f1c440d5ef90829c05acc1e281324df4baf037e44c13fb45f709041c7802d2d44')

build() {
  cd "$pkgname"

  
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest run_tests.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
