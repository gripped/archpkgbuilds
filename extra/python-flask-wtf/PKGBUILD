# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Serge Victor <arch@random.re>

pkgname=python-flask-wtf
pkgver=1.2.2
pkgrel=2
pkgdesc='Simple integration of Flask and WTForms'
arch=('any')
url='https://github.com/pallets-eco/flask-wtf'
license=('BSD-3-Clause')
depends=(
  'python'
  'python-babel'
  'python-flask'
  'python-flask-babel'
  'python-itsdangerous'
  'python-markupsafe'
  'python-werkzeug'
  'python-wtforms'
)
makedepends=(
  'python-build'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('381672150e20c0a3615349beccddc1364df1973b6c6da930bbe60444973f6ee2a318013c4e6ed7eb34c8cdc185b36e12d4f9ef13277060dc1398802a0243fec3')

prepare() {
  cd ${pkgname#python-}-$pkgver
  sed -i "s/^release, version =/& '$pkgver', '${pkgver%.*}.x' #/" docs/conf.py
}

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.rst
}

# vim: ts=2 sw=2 et:
