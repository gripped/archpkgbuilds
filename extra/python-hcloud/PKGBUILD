# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=python-hcloud
pkgver=2.11.1
pkgrel=1
pkgdesc="Official Hetzner Cloud python library"
arch=('any')
url="https://github.com/hetznercloud/hcloud-python"
license=('MIT')
replaces=('hcloud-python')
provides=('hcloud-python')
depends=('python-requests' 'python-dateutil')
makedepends=('python-setuptools')
source=("$pkgname"-$pkgver.tar.gz::https://github.com/hetznercloud/hcloud-python/archive/v${pkgver}.tar.gz)
sha512sums=('9e01ba3d6d1cdb6d00d816a3da1272f6bdc4ca5e0b935127da4448191df727fe1f7cebf2c3872a2ad883f814783b563088bb4e993f13ec5666d29ab90fbed47b')

build() {
  cd "$srcdir/hcloud-python-$pkgver"

  python setup.py build
}

package() {
  cd "$srcdir/hcloud-python-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
