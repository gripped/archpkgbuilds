# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=python-hcloud
pkgver=2.11.0
pkgrel=1
pkgdesc="Official Hetzner Cloud python library"
arch=('any')
url="https://github.com/hetznercloud/hcloud-python"
license=('MIT')
replaces=('hcloud-python')
provides=('hcloud-python')
depends=('python-requests' 'python-dateutil')
makedepends=('python-setuptools')
source=("$pkgname"-$pkgver.tar.gz::https://github.com/hetznercloud/hcloud-python/archive/v${pkgver}.tar.gz)
sha512sums=('12a2dcea61f587909494b65766b2e9f0f20a6140b8b91122faefa7b8fd35139cd7460f179fe4bea3989ae3d0e4512f152b415d1d1495dafdce335d3ea81a781d')

build() {
  cd "$srcdir/hcloud-python-$pkgver"

  python setup.py build
}

package() {
  cd "$srcdir/hcloud-python-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
