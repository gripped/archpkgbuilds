# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=python-hcloud
pkgver=2.9.0
pkgrel=1
pkgdesc="Official Hetzner Cloud python library"
arch=('any')
url="https://github.com/hetznercloud/hcloud-python"
license=('MIT')
replaces=('hcloud-python')
provides=('hcloud-python')
depends=('python-requests' 'python-dateutil')
makedepends=('python-setuptools')
source=("$pkgname"-$pkgver.tar.gz::https://github.com/hetznercloud/hcloud-python/archive/v${pkgver}.tar.gz)
sha512sums=('f416993b49ac7bf082a6bcd5635595e60cb79643b9b5baab0a5b2e12d10ea7ecfbd95272a690747e86b7510120bf2cdb7decb51072a847f013cd9bdec8001c2b')

build() {
  cd "$srcdir/hcloud-python-$pkgver"

  python setup.py build
}

package() {
  cd "$srcdir/hcloud-python-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
