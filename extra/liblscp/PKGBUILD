# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=liblscp
pkgver=1.0.1
pkgrel=1
pkgdesc="LinuxSampler Control Protocol C++ library"
arch=(x86_64)
url="https://github.com/rncbc/liblscp"
license=(LGPL-2.1-or-later)
depends=(glibc)
makedepends=(cmake)
provides=(liblscp.so)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('dbe7ca90f31b438ce119fa6e070777caa33f7d820c93eb03bf5ac60a183a1eed5cf7eba5fc0c11f000225e73cc7a5e35a1078bac8bf3fad11bc436095549c732')
b2sums=('0d5c4eeea81ea0c13602edac08d51a9271ad116e1bf02e9d9985de117341dfb5e721a2cb796046b10d5b049f5e3934beb676e205f2a597018dcc866840e85375')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

