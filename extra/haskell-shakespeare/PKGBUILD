# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shakespeare
pkgname=haskell-shakespeare
pkgver=2.1.4
pkgrel=16
pkgdesc="A toolkit for making compile-time interpolated templates"
url="https://www.yesodweb.com/book/shakespearean-templates"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-file-embed'
         'haskell-scientific' 'haskell-th-lift' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        $pkgname-$pkgver-en.msg::https://raw.githubusercontent.com/yesodweb/shakespeare/refs/tags/v2.1.3/other-messages/en.msg)
sha512sums=('d4994cf4477a45170cda3b18c4d523e4fa3189f33a36a25b63488dcdb4e62629d38db4be56fff0714ff545806e9cee74bf2b70c2d39d5b70fb7c1bad58a2fd24'
            'd5ff4a61c254743d350448a166b6371e27c414c05e805ebe9be3dac8d2cbc5e922d6844c0297ec5a278bf9b3c868dc7ee8aba4ce09cee6e0a2953f61ead8d998')

prepare() {
    cd $_hkgname-$pkgver
    mkdir other-messages
    mv ../$pkgname-$pkgver-en.msg other-messages/en.msg
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-test_roy -f-test_coffee -f-test_export
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
