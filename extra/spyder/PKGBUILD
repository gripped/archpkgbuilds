# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Muflone http://www.muflone.com/contacts/english/
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=6.0.3
pkgrel=1
pkgdesc="The Scientific Python Development Environment"
arch=(any)
url="https://www.spyder-ide.org/"
license=(MIT)
depends=(
  autopep8
  flake8
  hicolor-icon-theme
  ipython
  jupyter-nbconvert
  jupyter-nbformat
  python
  python-aiohttp
  python-asyncssh
  python-atomicwrites
  python-chardet
  python-cloudpickle
  python-cookiecutter
  python-dateutil
  python-diff-match-patch
  python-docutils
  python-intervaltree
  python-jedi
  python-jellyfish
  python-jinja
  python-jsonschema
  python-jupyter-client
  python-jupyter-core
  python-keyring
  python-lsp-black
  python-lsp-server
  python-numpydoc
  python-packaging
  python-parso
  python-pexpect
  python-pickleshare
  python-pillow
  python-psutil
  python-pycodestyle
  python-pydocstyle
  python-pyflakes
  python-pygithub
  python-pygments
  python-pylint
  python-pylint-venv
  python-pyls-spyder
  python-pyqt5
  python-pyqt5-webengine
  python-pyuca
  python-pyxdg
  python-pyzmq
  python-qdarkstyle
  python-qstylizer
  python-qtawesome
  python-qtconsole
  python-qtpy
  python-requests
  python-rope
  python-rtree
  python-setuptools
  python-sphinx
  python-spyder-kernels
  python-superqt
  python-textdistance
  python-three-merge
  python-tornado
  python-traitlets
  python-typing_extensions
  python-watchdog
  python-whatthepatch
  python-yarl
  qt5-svg
  yapf
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  cython
  python-flaky
  python-matplotlib
  python-pandas
  python-pillow
  python-pytest
  python-pytest-lazy-fixture
  python-pytest-mock
  python-pytest-order
  python-pytest-qt
  python-pytest-timeout
  python-scipy
  python-sympy
  tk
  xorg-server-xvfb
)
optdepends=(
  'cython: run Cython files in the IPython Console'
  'python-matplotlib: 2D/3D plotting in the IPython Console'
  'python-numpy: support for N-dimensional arrays in the Variable Explorer'
  'python-pandas: support for DataFrames and Series in the Variable Explorer'
  'python-scipy: support for Matlab workspace in the Variable Explorer'
  'python-sympy: symbolic mathematics in the IPython Console'
)
source=(
  "git+https://github.com/spyder-ide/spyder#tag=v$pkgver"
  "remove-module-level-qt-code.patch"
)
sha256sums=('9fdc55c3a43f7bdbc3b8176fd3e15a3837b3ada2bfaf8d16d539d517556fc5e5'
            'e898320f7576faa923d96b952bc7b62d7f3b53b17941d3a40a9beac4d0d42826')

prepare() {
  cd $pkgname
  patch -Np1 -i ../remove-module-level-qt-code.patch
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  local pytest_args=(
    # pytestqt.exceptions.TimeoutError
    --deselect='spyder/plugins/completion/providers/languageserver/tests/test_client.py'
    --deselect='spyder/plugins/completion/tests/test_plugin.py'
    --deselect='spyder/plugins/editor/widgets/codeeditor/'
    --deselect='spyder/plugins/editor/widgets/editorstack'
    --deselect='spyder/plugins/editor/widgets/tests/test_editorsplitter.py'
    --deselect='spyder/plugins/editor/widgets/tests/test_formatting.py'
    --deselect='spyder/plugins/ipythonconsole/tests/test_ipythonconsole.py'
    --deselect='spyder/app/tests/test_mainwindow.py'

    # Requires keyring access
    --deselect='spyder/config/tests/test_manager.py::test_secure_options'
    --deselect='spyder/plugins/ipythonconsole/widgets/tests/test_kernelconnect.py::test_connection_dialog_remembers_input_with_password'
    --deselect='spyder/plugins/ipythonconsole/widgets/tests/test_kernelconnect.py::test_connection_dialog_remembers_input_with_ssh_passphrase'
    --deselect='spyder/widgets/github/tests/test_github_backend.py::test_store_user_credentials'

    # Python error: Segmentation fault
    --deselect='spyder/plugins/plots/tests/test_plugin.py'

    # assert 102 < 100
    --deselect='spyder/plugins/plots/widgets/tests/test_plots_widgets.py::test_zoom_figure_viewer[image/svg+xml]'

    # Failed: DID NOT RAISE <class 'spyder.utils.vcs.ActionToolNotFound'>
    --deselect='spyder/utils/tests/test_vcs.py::test_vcs_tool'

    # Fails - not sure why
    --deselect='spyder/plugins/updatemanager/tests/test_update_manager.py::test_updates'

    # AssertionError
    --deselect='spyder/plugins/variableexplorer/widgets/objectexplorer/tests/test_objectexplorer.py::test_objectexplorer_types'
    --deselect='spyder/plugins/variableexplorer/widgets/objectexplorer/tests/test_objectexplorer.py::test_objectexplorer_collection_types'

    # Requires pytest-docker
    --ignore='spyder/plugins/remoteclient/tests'
  )
  PYTEST_QT_API="pyqt5" xvfb-run pytest "${pytest_args[@]}"
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
  install -vDm644 -t "$pkgdir/usr/share/icons/hicolor/scalable/apps/" \
    spyder/images/spyder.svg
  rm -f "$pkgdir/usr/bin/spyder_win_post_install.py"
}
