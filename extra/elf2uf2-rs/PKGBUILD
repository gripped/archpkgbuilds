# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=elf2uf2-rs
pkgver=2.1.1
pkgrel=3
pkgdesc="Convert ELF files to UF2 for USB Flashing Bootloaders"
url='https://github.com/JoNil/elf2uf2-rs'
arch=('x86_64')
license=('0BSD')
depends=(
  'gcc-libs'
  'glibc'
  'libudev.so'
)
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/${pkgname}/${pkgver}/download"
        # https://github.com/JoNil/elf2uf2-rs/pull/41
        "new-abi-compat.diff::https://github.com/JoNil/elf2uf2-rs/commit/c1639b9e8bcaaaab653f9fa0e358fed0e8a7ce76.diff")
sha256sums=('a10b1662b8b09059d0f582a7b316a7aae8a47c7298c3b622e8eda823e414d662'
            '9bd8f027032418d025f8dcd0a0545100227a2dd26f4f43e1b84094e51168bcd3')
b2sums=('d91c2ce514a7650bae004472219a67ebf095628be8ae9d80f49ad7ce591c0f26a48b517d4ba49e8ad8ce218d5ffbe6596b97cbaf601ecb00e0b8d41df9d62d12'
        'b719e743a6fc1a48d6394d7e2078ffebfea7d39e606d479a706ef16d303f21f5ed0f75f0d5dfd483a539373e302e7990e5d820656bf084d1744697c01dcbc5b7')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  patch -Np1 -i ../new-abi-compat.diff
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/elf2uf2-rs

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
