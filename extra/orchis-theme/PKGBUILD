# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Alan Sartorio

pkgname=orchis-theme
_pkgname="${pkgname^}"
pkgver=2024_11_03
_pkgver="${pkgver//_/-}"
pkgrel=3
pkgdesc="A Material Design theme for GNOME/GTK based desktop environments"
url="https://github.com/vinceliuice/orchis-theme"
arch=('any')
license=('GPL-3.0-or-later')
optdepends=('gtk-engine-murrine: for gtk2 themes'
            'gnome-themes-extra: for gtk2 themes'
            'tela-circle-icon-theme: recommended icon theme'
            'vimix-cursors: recommended cursors theme')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${_pkgver}.tar.gz")
sha256sums=('0089ab3a17b054309b32904c5b2ebf3b432f28ede6f456132c47495012038ee9')
options=(!strip)

package() {
	cd "${_pkgname}-${_pkgver}"
	
	install -dm 755 "${pkgdir}/usr/share/themes"
	./install.sh --dest "${pkgdir}/usr/share/themes"
}
