# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sus
pkgver=0.35.2
pkgrel=1
pkgdesc='A fast and scalable test runner'
arch=(any)
url='https://github.com/socketry/sus'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
)
options=(!emptydirs)
source=(git+https://github.com/socketry/sus.git#tag=v$pkgver)
sha512sums=('664cf3c97ed039d94bd1ded30ccbcf5f8cef890ddd406e2cd8e74533f1f6d48fa7b0ed99033ca9c905a059962fde7ad50b01b3059943258cf8ab38d09ee63033')
b2sums=('5d48ffe3d06c7df3bce0ff69f39e6116d4dfe6bd068944c741d62a395eb8e6b8048febf084ed88f395dab2d8e9835d8ddb7efd3019a13c0d169f600f418dc764')

prepare() {
  cd sus

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i sus.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/agent-context/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sus
  gem build sus.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sus-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sus
  PATH="$PWD/tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd sus
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
