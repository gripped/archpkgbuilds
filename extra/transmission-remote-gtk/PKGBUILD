# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: SÃ©bastien LEBEAU <sebcbi1 at gmail dot com>
# Contributor: Patrick Griffis <tingping@fedoraproject.org>

pkgname=transmission-remote-gtk
pkgver=1.6.0
pkgrel=1
pkgdesc="GTK remote control for the Transmission BitTorrent client"
arch=(x86_64)
url="https://github.com/transmission-remote-gtk/transmission-remote-gtk"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  gdk-pixbuf2
  geoip
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  json-glib
  libappindicator-gtk3
  libsoup3
  pango
)
makedepends=(meson)
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b090844f6a482e6f3588070ff3fdd54b79e8f85df02b39853cfb01fccee10cac')

build() {
  arch-meson ${pkgname}-${pkgver} build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
