# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: SÃ©bastien LEBEAU <sebcbi1 at gmail dot com>
# Contributor: Patrick Griffis <tingping@fedoraproject.org>

pkgname=transmission-remote-gtk
pkgver=1.7.0
pkgrel=1
pkgdesc="GTK remote control for the Transmission BitTorrent client"
arch=(x86_64)
url="https://github.com/transmission-remote-gtk/transmission-remote-gtk"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  json-glib
  libappindicator-gtk3
  libsoup3
  pango
)
makedepends=(
  meson
  python-docutils
)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('7eb29ca2a4ac56f8543fe0911639dd118022eec9bb4ed0eb6a648a7b126129a62495f087ae9e9cb4b2436648071db31d9aeedbe1a0e41f03cc690f6051f7f402')

build() {
  arch-meson ${pkgname}-${pkgver} build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
