# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=warp
pkgname=haskell-warp
pkgver=3.4.0
pkgrel=1
pkgdesc="A fast, light-weight web server for WAI applications."
url="https://github.com/yesodweb/wai"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-auto-update' 'haskell-bsb-http-chunked' 'haskell-case-insensitive'
         'haskell-crypton-x509' 'haskell-hashable' 'haskell-http-date' 'haskell-http-types'
         'haskell-http2' 'haskell-iproute' 'haskell-network' 'haskell-recv'
         'haskell-simple-sendfile' 'haskell-streaming-commons' 'haskell-time-manager'
         'haskell-unliftio' 'haskell-vault' 'haskell-wai' 'haskell-word8')
makedepends=('ghc' 'haskell-doctest' 'haskell-http-client' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e6f5d55cdb18103b3fcf8a60f1e3f87592bca45a894674fd064bccf61fe6033efa619a86e6fc33e645a17d734c09a0a397f6b153c20a79baf4d08fd6e182828a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-warp-debug -fallow-sendfilefd -f-network-bytestring
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
