# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.4.14
pkgrel=1
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('x86_64')
url="https://github.com/sailfishos/fingerterm"
license=('GPL-2.0-only')
depends=('qt5-feedback' 'nemo-qml-plugin-notifications')
makedepends=('git' 'qt5-tools')
source=("git+https://github.com/sailfishos/fingerterm.git#tag=$pkgver")
sha512sums=('c5088bc1034d81d6e9a5af9c646d310d378876c778f703ff5c98159e422354430351f96a751fbc8197f320abd4daad99a056a04292d3b422f0e80fe56f3bbd59')

build() {
  cd fingerterm

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm

  make INSTALL_ROOT="$pkgdir" install
}
