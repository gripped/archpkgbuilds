# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.4.13
pkgrel=1
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('x86_64')
url="https://github.com/sailfishos/fingerterm"
license=('GPL-2.0-only')
depends=('qt5-feedback' 'nemo-qml-plugin-notifications')
makedepends=('git' 'qt5-tools')
source=("git+https://github.com/sailfishos/fingerterm.git#tag=$pkgver")
sha512sums=('5c60b4f368911c0acf41537d2cd06655780efd5295c415a190f694463d689566d939fe351deadfb1448f96d57ad63b1e9804e11cf036db1f715eb201ca733d90')

build() {
  cd fingerterm

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm

  make INSTALL_ROOT="$pkgdir" install
}
