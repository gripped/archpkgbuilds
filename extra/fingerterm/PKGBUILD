# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.4.11
pkgrel=1
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('x86_64')
url="https://github.com/sailfishos/fingerterm"
license=('GPL-2.0-only')
depends=('qt5-feedback' 'nemo-qml-plugin-notifications')
makedepends=('git' 'qt5-tools')
source=("git+https://github.com/sailfishos/fingerterm.git#tag=$pkgver")
sha512sums=('7805aff4eb10e50ea13b80abf6f99becd014f3b06a42b4ddd68144766ad90cae245c6714221fa609ac933b2be3c92d51f5c98857d281d079b42dcc7108a00b50')

build() {
  cd fingerterm

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm

  make INSTALL_ROOT="$pkgdir" install
}
