# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=espflash
pkgver=3.3.0
pkgrel=1
pkgdesc="Cargo subcommand for flashing Espressif devices over serial"
url='https://github.com/esp-rs/espflash'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
  'libcurl.so'
  'libudev.so'
  'libz.so'
)
makedepends=(
  'cargo'
)
provides=('cargo-espflash')
conflicts=('cargo-espflash')
replaces=('cargo-espflash')
options=(!lto)
source=(https://github.com/esp-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/esp-rs/espflash/commit/85d8f3190b3d60fc5cabc1fee3ecff6d640a65cb.diff)
sha256sums=('5f641653d2112904a02d4c67e1fba6564a7fc23fbb34def2df8ce6ac953a4163'
            'd1e3e2bf31c0d0bf74fc1855e538a35ee3b5f616de548e9f486eacc5a9775a29')
b2sums=('964c99566c5d5d1e162bcc316b0d3325c5b2fdae12420f5810e8f47b39b314005da9c699a8d79897b7173da0b866013c016b1163d2e01e5d7132876883dd7ed8'
        '1fcdc7400d1f5fee9eefc9a2ee2a276823345ff62ba8707e8394ec2313dd75288cf9e20184eb98b6ae9b053af09fb3e8a5e35fb11de39ade4af45be8e3b82e48')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../85d8f3190b3d60fc5cabc1fee3ecff6d640a65cb.diff
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen --workspace
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/espflash \
    target/release/cargo-espflash

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"

  # licenses
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-*
}

# vim: ts=2 sw=2 et:
