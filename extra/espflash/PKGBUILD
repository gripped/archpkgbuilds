# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=espflash
pkgver=4.3.0
pkgrel=1
pkgdesc="Cargo subcommand for flashing Espressif devices over serial"
url='https://github.com/esp-rs/espflash'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
provides=('cargo-espflash')
conflicts=('cargo-espflash')
replaces=('cargo-espflash')
options=(!lto)
source=(https://github.com/esp-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ea038015c548421c4aba4a3ed7391ab831d7003d09944f1ac247c77dfe090341')
b2sums=('15adf73a35ce990cd23c1b33130720c7730bd530c6be47bcd021717f9881973850c9af8a28cce21e9628fb237ea15480e2a973aa86e634916003030817ac3613')

prepare() {
  cd ${pkgname}-${pkgver}

  cargo fetch --target "$(rustc --print host-tuple)" --locked
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen --workspace
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/espflash \
    target/release/cargo-espflash

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
    "${pkgdir}/usr/share/zsh/site-functions" \
    "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash >"${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh >"${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish >"${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"

  # licenses
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-*
}

# vim: ts=2 sw=2 et:
