# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gosop
pkgver=1.1.0
pkgrel=1
pkgdesc="Stateless CLI for GopenPGP"
arch=(x86_64)
url="https://github.com/ProtonMail/gosop"
license=(MIT)
groups=(stateless-openpgp)
depends=(glibc)
makedepends=(go)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('bc0d42c307c6a6ebb671f1572e1d6a4eb0ddc44dddb4fc5b3ca8f25b553a5f50bc246e404849cc0aa7a68aa5131024fede7d77f09221c68f559f9c95a18ae43b')
b2sums=('9d0cb6a3781df06395233863d54ddb0598ba608a6452ec8bf9a547c59055a0fc91cb210f871f22f12e430a22fdb96cd203a7a11c3b5d4ad139b89ac0624263a7')

prepare() {
  cd $pkgname-$pkgver
  mkdir -p build
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"

  cd $pkgname-$pkgver
  go build -o build -ldflags "-compressdwarf=false -linkmode external" .
}

check() {
  cd $pkgname-$pkgver
  go test ./...
}

package() {
  install -vDm 755 $pkgname-$pkgver/build/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname-$pkgver/man/$pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
