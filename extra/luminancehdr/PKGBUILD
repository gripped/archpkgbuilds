# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=luminancehdr
pkgver=2.6.1.1
pkgrel=1
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL-2.0-or-later')
depends=('exiv2' 'fftw' 'gsl' 'lcms2' 'libraw' 'openexr' 'qt5-webengine' 'qt5-svg'
         'ccfits' 'desktop-file-utils' 'hicolor-icon-theme' 'boost-libs')
makedepends=('cmake' 'boost' 'gtest' 'qt5-tools' 'eigen')
optdepends=('hugin: align image stack functionality')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(https://sourceforge.net/projects/qtpfsgui/files/luminance/$pkgver/luminance-hdr-$pkgver.tar.bz2
        luminancehdr-openexr3.patch
        fix-headers.patch
        exiv2-0.28.patch
        https://github.com/LuminanceHDR/LuminanceHDR/commit/33b364f7.patch)
sha512sums=('9006339037aa3a0b7332cf71e1cf143d9e700eaae1102dfb8eccea8a9d97a5bcb6331202684adf76542116927dd9a69169882518af6ebb25c85d08057fdc552e'
            '78da713154042f125511ddc7ad184f961c92fa679ff0aa5d33700b619a0eed973722ea9f51c44c58595043ea308f4f3c44f6613b88fbb41e5159849bc0f8741e'
            '6d7ae3df647ffb67a27ef2729c8e8fd04e2b400bd960744dd0172406bc27773156a2b0bd67a7b71157cc59d0f528fbe00882085baa42f503dd506baa2826d0e9'
            '711e64209cba59b218b09715451ddcca3084b020da9bd1baa7decc71e5d3ad7375cba5703bc042e2371e9465c01436a67d0162731718dbef158e207de5266615'
            'eb4d32d279eab7658e38007ae9e8680fdce36abb6e0f3c1864de2163dcb5ec2dbba9c19341f23aebd799124dc64338f34570b041ac995249941b2ac4ba718e30')

prepare() {
  patch -d luminance-hdr-$pkgver -p1 < luminancehdr-openexr3.patch
  patch -d luminance-hdr-$pkgver -p1 < fix-headers.patch
  patch -d luminance-hdr-$pkgver -p1 < exiv2-0.28.patch # Fix build with exiv2 0.28
  patch -d luminance-hdr-$pkgver -p1 < 33b364f7.patch # Fix build with boost 1.85
}

build() {
  cd "${srcdir}"/luminance-hdr-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/luminance-hdr-$pkgver

  make DESTDIR="${pkgdir}" install

  #https://sourceforge.net/p/qtpfsgui/feature-requests/51/
  mv "${pkgdir}"/usr/share/appdata/ "${pkgdir}"/usr/share/metainfo/
}
