# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libcerf
epoch=1
pkgver=2.5
pkgrel=1
pkgdesc='Self-contained numeric library that provides an efficient and accurate implementation of complex error functions'
arch=(x86_64)
url='https://jugit.fz-juelich.de/mlz/libcerf/'
license=(MIT)
depends=(glibc)
makedepends=(cmake
             git)
source=(git+https://jugit.fz-juelich.de/mlz/libcerf#tag=v$pkgver)
sha256sums=('9ec7468f362918796e03f093e6e05c1b83b28557a9e7100f04313ec3f942416c')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
