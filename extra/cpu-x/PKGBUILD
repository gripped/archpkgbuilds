# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: UnicornDarkness

pkgname=cpu-x
pkgver=5.3.1
pkgrel=1
pkgdesc="A Free software that gathers information on CPU, motherboard and more"
arch=(x86_64)
url="https://thetumultuousunicornofdarkness.github.io/CPU-X"
license=(GPL-3.0-only)
depends=(
  cairomm
  dconf
  gcc-libs
  glib2
  glibc
  glibmm
  gtkmm3
  libcpuid
  libglvnd
  libsigc++
  ncurses
  opencl-icd-loader
  pangomm
  pciutils
  procps-ng
  vulkan-icd-loader
)
makedepends=(
  cmake
  git
  nasm
  ninja
  opencl-headers
  polkit
  vulkan-headers
)
optdepends=('opengl-driver: packaged openGL driver'
            'vulkan-driver: packaged Vulkan driver'
            'opencl-driver: packaged OpenCL driver')
source=("cpu-x::git+https://github.com/TheTumultuousUnicornOfDarkness/CPU-X.git#tag=v${pkgver}")
sha512sums=('2958d548cb3368ce3da6f61394929deb953032d7e34d666faa13bc590d797b460cb8b8dd6ac09afb7b770ef65d0f80974c0f07652d70aa152a3d2a02df71334b')

build() {
	cmake -S "$pkgname" -B build -GNinja -DCMAKE_BUILD_TYPE=None -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR="lib/cpu-x"
	cmake --build build
}

check() {
	ninja -C build test
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}
