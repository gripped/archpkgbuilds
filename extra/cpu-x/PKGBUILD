# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: UnicornDarkness

pkgname=cpu-x
pkgver=5.2.0
pkgrel=1
pkgdesc="A Free software that gathers information on CPU, motherboard and more"
arch=(x86_64)
url="https://thetumultuousunicornofdarkness.github.io/CPU-X"
license=(GPL-3.0-only)
depends=(
  glibc
  libsigc++
  glib2
  dconf
  glibmm
  pangomm
  gcc-libs
  cairomm
  gtkmm3
  ncurses
  libcpuid
  pciutils
  libglvnd
  glfw
  vulkan-icd-loader
  procps-ng
)
makedepends=(
  git
  cmake
  ninja
  nasm
  vulkan-headers
  polkit
)
optdepends=('opengl-driver: packaged openGL driver'
            'vulkan-driver: packaged Vulkan driver')
source=("cpu-x::git+https://github.com/TheTumultuousUnicornOfDarkness/CPU-X.git#tag=v${pkgver}")
sha512sums=('9153b2dfb1ac8d15ea4ad1f953d345077a13dbc698179c1831047c150f3e13477ba1ff4ef18d160ad925d98564b400909438e7224a4e3bb37f15169c3e220594')

build() {
	cmake -S "$pkgname" -B build -GNinja -DCMAKE_BUILD_TYPE=None -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR="lib/cpu-x"
	cmake --build build
}

check() {
	ninja -C build test
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}
