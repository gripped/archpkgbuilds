# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: UnicornDarkness

pkgname=cpu-x
pkgver=5.1.3
pkgrel=1
pkgdesc="A Free software that gathers information on CPU, motherboard and more"
arch=(x86_64)
url="https://thetumultuousunicornofdarkness.github.io/CPU-X"
license=(GPL-3.0-only)
depends=(
  glibc
  libsigc++
  glib2
  dconf
  glibmm
  pangomm
  gcc-libs
  cairomm
  gtkmm3
  ncurses
  libcpuid
  pciutils
  libglvnd
  glfw
  vulkan-icd-loader
  procps-ng
)
makedepends=(
  git
  cmake
  ninja
  nasm
  vulkan-headers
  polkit
)
optdepends=('opengl-driver: packaged openGL driver'
            'vulkan-driver: packaged Vulkan driver')
source=("cpu-x::git+https://github.com/TheTumultuousUnicornOfDarkness/CPU-X.git#tag=v${pkgver}")
sha512sums=('03c46c450d794a9e99988993c0def32bbfb02fb381fb709eeca24f75fd30617bbce3a9e4187c8aa6d03b82eea7d1186932a604aadb592d68f1f765167b9777c5')

build() {
	cmake -S "$pkgname" -B build -GNinja -DCMAKE_BUILD_TYPE=None -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR="lib/cpu-x"
	cmake --build build
}

check() {
	ninja -C build test
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}
