# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=git-grab
pkgver=4.0.0
pkgrel=1
pkgdesc='A tool to clone git repositories to a standard location organised by domain and path'
arch=(x86_64)
url='https://github.com/wezm/git-grab'
license=('MIT OR Apache-2.0')
depends=(git gcc-libs)
makedepends=(rust)
options=(!lto)
source=("$pkgname::git+$url.git#tag=$pkgver")
sha512sums=('1f418e8470608375e575c42a86bfc93b31972093f69ff3227361681ff6480a71cef7b2894e22219fa7ec837380bd8e565e5293e612266114c04eb4b02ca74be5')
b2sums=('3e2e37165862a473715e1ac5ec6a0fedc89f2f95f51bf5d80fdd07cfa1993b4d3ca37e65b4f44a977f8ccbf930baede99f6b7d35504b33a17d076ef1bbeb7a60')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/git-grab

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
  sed -n '/^Licence/,/^at your option./p' \
    README.md \
    > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
