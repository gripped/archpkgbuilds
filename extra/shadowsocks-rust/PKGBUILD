# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: BigmenPixel <bigmen dot pixel at tuta dot io>
# Contributor: rustemb <rustemb at systemli dot org>

pkgname=shadowsocks-rust
pkgver=1.18.4
pkgrel=1
pkgdesc='A Rust port of shadowsocks'
arch=(x86_64)
url='https://github.com/shadowsocks/shadowsocks-rust'
license=('MIT')
depends=('glibc' 'gcc-libs' 'openssl')
makedepends=('git' 'cargo')
options=(!lto)
source=("git+https://github.com/shadowsocks/shadowsocks-rust.git#tag=v$pkgver"
        'shadowsocks-rust@.service'
        'shadowsocks-rust-server@.service')
sha512sums=('ebf585866868d15a44a4bfab802192c433b4f648941fe0c0b6f624cf97695cfeaebbf8111edb5191ade19d1b89e2dca579aed4b87ae4d067a8186c8b87fa6d7e'
            '90ee8735104795b5c50bc855ad11ee9c741fa1695409d72de7c69b89b0aa80c9596459edbb3a2c1d49e9414e3d06bc55328126c0062f1e28cac141ea202e455d'
            'ef5a348dfbbfd0bad733da217824dd55851aff3490c63685e9f1bc2b393998fea1bdad864c16a98b8af07264851bb0a11326959529a669008c5e62e94b26b209')

prepare() {
  cd shadowsocks-rust
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd shadowsocks-rust
  cargo build --release --frozen --features local-redir,local-tun,local-dns,local-http-native-tls
}

package() {
  cd shadowsocks-rust
  install -Dm755 target/release/{sslocal,ssserver,ssurl,ssmanager,ssservice} -t "$pkgdir"/usr/bin/
  install -Dm644 "$srcdir"/$pkgname{@,-server@}.service -t "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 examples/config_ext.json "$pkgdir"/etc/$pkgname/config_ext_rust.json.example
  install -Dm644 examples/config.json "$pkgdir"/etc/$pkgname/config_rust.json.example
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
