# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: BigmenPixel <bigmen dot pixel at tuta dot io>
# Contributor: rustemb <rustemb at systemli dot org>

pkgname=shadowsocks-rust
pkgver=1.18.1
pkgrel=1
pkgdesc='A Rust port of shadowsocks'
arch=(x86_64)
url='https://github.com/shadowsocks/shadowsocks-rust'
license=('MIT')
depends=('glibc' 'gcc-libs' 'openssl')
makedepends=('cargo')
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        'shadowsocks-rust@.service'
        'shadowsocks-rust-server@.service')
sha512sums=('511d054da9f78b928508a8d048e633fcc290ea7fc28be24e8c47cb95b5f0bf82fcadd4e7b365015c7eba25ab99c851a4f37c2e5f8202686b087b94ae923f16f2'
            '90ee8735104795b5c50bc855ad11ee9c741fa1695409d72de7c69b89b0aa80c9596459edbb3a2c1d49e9414e3d06bc55328126c0062f1e28cac141ea202e455d'
            'ef5a348dfbbfd0bad733da217824dd55851aff3490c63685e9f1bc2b393998fea1bdad864c16a98b8af07264851bb0a11326959529a669008c5e62e94b26b209')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --features local-redir,local-tun,local-dns,local-http-native-tls
}

package() {
    cd $pkgname-$pkgver
    install -Dm755 target/release/{sslocal,ssserver,ssurl,ssmanager,ssservice} -t "$pkgdir"/usr/bin/
    install -Dm644 "$srcdir"/$pkgname{@,-server@}.service -t "$pkgdir"/usr/lib/systemd/system/
    install -Dm644 examples/config_ext.json "$pkgdir"/etc/$pkgname/config_ext_rust.json.example
    install -Dm644 examples/config.json "$pkgdir"/etc/$pkgname/config_rust.json.example
    install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
