# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: BigmenPixel <bigmen dot pixel at tuta dot io>
# Contributor: rustemb <rustemb at systemli dot org>

pkgname=shadowsocks-rust
pkgver=1.23.2
pkgrel=1
pkgdesc='A Rust port of shadowsocks'
arch=(x86_64)
url='https://github.com/shadowsocks/shadowsocks-rust'
license=('MIT')
depends=('glibc' 'gcc-libs' 'openssl')
makedepends=('git' 'cargo' 'clang')
options=(!lto)
source=("git+https://github.com/shadowsocks/shadowsocks-rust.git#tag=v$pkgver"
        "git+https://github.com/rust-rocksdb/rust-rocksdb.git#commit=392e1dc059f6c4865ab68711ae4f06e0f05362a0" # 0.17.1+9.9.3
        'rocksdb-gcc15-add-missing-includes.patch::https://github.com/facebook/rocksdb/pull/13437.diff'
        'shadowsocks-rust@.service'
        'shadowsocks-rust-server@.service')
sha512sums=('c2a64b06970a2cf7a663eb7a0d4c6243f03cf867f18792975a7f6c8bb18ac6a414567f66d2a9e432a5ddcab062cdb77ce8121ed9f6fb88d7e350e572f89cc0dc'
            '1f4cb37811982ccc82532c9548038b9db2757ca6f6026b63e4a2c081d4917463864ad2bba2bb4af72b985dc976f90ef9f83df9c718d3c599e66371b53f9eea03'
            '2bbac1496330116d2fb8e0514315c23ca6b2f312f0534a98ce0220d537518e97241f7602999ac46274f3af01723f4a9247ff3ccf7211e53e50b64bb027e1ea6e'
            '90ee8735104795b5c50bc855ad11ee9c741fa1695409d72de7c69b89b0aa80c9596459edbb3a2c1d49e9414e3d06bc55328126c0062f1e28cac141ea202e455d'
            'ef5a348dfbbfd0bad733da217824dd55851aff3490c63685e9f1bc2b393998fea1bdad864c16a98b8af07264851bb0a11326959529a669008c5e62e94b26b209')

prepare() {
  # Patch rocksdb to fix GCC 15 build
  pushd rust-rocksdb
  git submodule update --init --recursive
  cd librocksdb-sys/rocksdb
  patch -Np1 -i "$srcdir"/rocksdb-gcc15-add-missing-includes.patch
  popd
  
  cd shadowsocks-rust
  echo -e "\n[patch.crates-io]\nlibrocksdb-sys = { path = '../rust-rocksdb/librocksdb-sys' }" >> Cargo.toml
  cargo update -p librocksdb-sys
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd shadowsocks-rust
  cargo build --release --frozen --features full-extra
}

package() {
  cd shadowsocks-rust
  install -Dm755 target/release/{sslocal,ssserver,ssurl,ssmanager,ssservice} -t "$pkgdir"/usr/bin/
  install -Dm644 "$srcdir"/$pkgname{@,-server@}.service -t "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 examples/config_ext.json "$pkgdir"/etc/$pkgname/config_ext_rust.json.example
  install -Dm644 examples/config.json "$pkgdir"/etc/$pkgname/config_rust.json.example
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
