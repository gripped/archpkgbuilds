# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-polkit-agent
pkgver=6.0.12
pkgrel=1
pkgdesc='Deepin Polkit Agent'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-polkit-agent"
license=('GPL3')
depends=('deepin-shell' 'dtk6core' 'dtk6widget' 'polkit-qt6' 'qt6-base' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-polkit-agent.git#tag=$pkgver")
sha512sums=('e812b1ce7d8c1e1ad1b04bd4e04a112075cd2cdd5159e919177b9285718c8d81c5925c25129f7282241364b391bc680a1bdffef4b8547e3055370e774c7b641d')

build() {
  cd dde-polkit-agent
  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_DEEPIN_POLKIT=OFF
  ninja
}

package() {
  cd dde-polkit-agent
  DESTDIR="$pkgdir" ninja install
}
