# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-polkit-agent
pkgver=6.0.10
pkgrel=1
pkgdesc='Deepin Polkit Agent'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-polkit-agent"
license=('GPL3')
depends=('deepin-shell' 'dtk6core' 'dtk6widget' 'polkit-qt6' 'qt6-base' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-polkit-agent.git#tag=$pkgver")
sha512sums=('614b08b8f871bff1294ea49d10c766b8aad6858e4fbb15b0f22da2917a2bd4a4ed4c30e83d39ca7d9763a7bf0febca4ed4fb5c3d877a329658595f1eb74dbe76')

build() {
  cd dde-polkit-agent
  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_DEEPIN_POLKIT=OFF
  ninja
}

package() {
  cd dde-polkit-agent
  DESTDIR="$pkgdir" ninja install
}
