# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-polkit-agent
pkgver=6.0.9
pkgrel=1
pkgdesc='Deepin Polkit Agent'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-polkit-agent"
license=('GPL3')
depends=('dtk6core' 'dtk6widget' 'polkit-qt6' 'qt6-base' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-polkit-agent.git#tag=$pkgver")
sha512sums=('5bf52ef671df72c8e4ee36208406d0e16f071c85cbe4997523e05408ca8949976c0997f7100af7b281e7225c8a6b39f63ad66c10c9a51c778be416f81ac9214d')

build() {
  cd dde-polkit-agent
  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_DEEPIN_POLKIT=OFF
  ninja
}

package() {
  cd dde-polkit-agent
  DESTDIR="$pkgdir" ninja install
}
