# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-polkit-agent
pkgver=6.0.16
pkgrel=1
pkgdesc='Deepin Polkit Agent'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-polkit-agent"
license=('GPL-3.0-or-later')
depends=('deepin-shell' 'dtk6core' 'dtk6widget' 'polkit-qt6' 'qt6-base' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-polkit-agent.git#tag=$pkgver")
sha512sums=('ea057436f6840de80c5fcf182053dfe01301b75496d7a443ecd0be32729c3c3797b93a76ea09955a2d9aa71c72c674c3a2ca44b90fc54d515876bc6481dc9c74')

build() {
  cd dde-polkit-agent
  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_DEEPIN_POLKIT=OFF
  ninja
}

package() {
  cd dde-polkit-agent
  DESTDIR="$pkgdir" ninja install
}
