# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alan <alan1world@googlemail.com>

pkgname=python-prefixed
pkgver=0.9.0
pkgrel=3
pkgdesc='Prefixed alternative numeric library'
arch=(any)
url=https://github.com/Rockhopper-Technologies/prefixed
license=(MPL2)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=9367d1d72d119a19cc4b15c4c41af820bbe3bd8b
source=(git+https://github.com/Rockhopper-Technologies/prefixed.git#tag=${_tag})
b2sums=('e66fc7f4facc53c1e085b48955975c7926e0aa4f085b62726a2c7747ec7e41ba53a12b31cd39f81f70d9a9ccc52234fcc7d0cfeb710b54383eae1a14ecb4ee14')

pkgver() {
  cd prefixed
  git describe --tags
}

build() {
  cd prefixed
  python -m build --wheel --no-isolation
}

package(){
  cd prefixed
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-prefixed/
}

# vim: ts=2 sw=2 et:
