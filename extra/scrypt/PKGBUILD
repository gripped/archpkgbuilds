# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Oliver Herold <oliver - at - akephalos - dot - de>

pkgname=scrypt
pkgver=1.3.3
pkgrel=1
pkgdesc='key derivation function and encryption utility'
arch=('x86_64')
depends=('glibc' 'openssl')
url='https://www.tarsnap.com/scrypt.html'
license=('BSD')
source=(https://www.tarsnap.com/scrypt/scrypt-${pkgver}.tgz)
sha256sums=('1c2710517e998eaac2e97db11f092e37139e69886b21a1b2661f64e130215ae9')
b2sums=('4cd625c8fae44b0526f6cca4a9f4573f76dbc67b3a3f917c0d7615916f10f8f492c8b5289c7eecda7ecd482a23f13e5143eeac49450988c72c8bae1aa62edf86')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
