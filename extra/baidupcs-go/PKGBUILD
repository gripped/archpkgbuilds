# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.9.8
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/qjfoidnh/BaiduPCS-Go"
license=('Apache-2.0')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("git+https://github.com/qjfoidnh/BaiduPCS-Go.git#tag=v$pkgver")
sha512sums=('479b297639bcbff33b8ecf5ae85b22dcdb9aff027c5f4b867d5085cd9330122befe224468894d50ad2dd1361c1a45916a3daefb0a5e7212575423d142ea3f2e2')

build() {
  cd BaiduPCS-Go
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
