# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.9.9
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/qjfoidnh/BaiduPCS-Go"
license=('Apache-2.0')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("git+https://github.com/qjfoidnh/BaiduPCS-Go.git#tag=v$pkgver")
sha512sums=('4fd1e0bda84d7820d74100a9eb3e7a582ea83688a5148be2b0cceb16b70c6eba1da97d941a88503e32ffd8d1118bc3b4a0bfaa30681dcbd5f91b83249ff51f6d')

build() {
  cd BaiduPCS-Go
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
