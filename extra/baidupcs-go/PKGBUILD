# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=4.0.0
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/qjfoidnh/BaiduPCS-Go"
license=('Apache-2.0')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("git+https://github.com/qjfoidnh/BaiduPCS-Go.git#tag=v$pkgver")
sha512sums=('a2f0c77e09415289fb58492d2134ee56a338e78d59ff9ce4f402df93f34679c83cd2ab80f4949f4ca010c91e89ad968993424a4a2b875002b2593dc39ef9a617')

prepare() {
  cd BaiduPCS-Go
  export GOPATH="${srcdir}"
  go mod download -modcacherw -x
}

build() {
  cd BaiduPCS-Go
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
