# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.4.2
pkgrel=4
pkgdesc='A general-purpose subtitle editor with ASS/SSA support'
arch=(x86_64)
url=http://www.aegisub.org
license=(BSD-3-Clause)
depends=(
  alsa-lib
  boost-libs
  curl
  fftw
  fontconfig
  gcc-libs
  glibc
  hicolor-icon-theme
  hunspell
  icu
  libass.so
  libffms2.so
  libgl
  libpulse
  uchardet
  wxwidgets-common
  wxwidgets-gtk3
  zlib
)
makedepends=(
  boost
  cmake
  git
  mesa
  meson
)
_tag=f437ba4f547fdb0e02432f1c45a4da25e51035f8
source=(git+https://github.com/TypesettingTools/Aegisub.git#tag=${_tag})
#validpgpkeys=(6DD9508BCB1CE31AF295FF9B6A889F50A8B00C08) # Thomas Goyne <plorkyeran@aegisub.org>
b2sums=('9d46d4c296e1d52ffb1abbd2e3ac48b3329618706fad4e76928e733b15d61e3d352b50d0514426b0ab62dadc5ceaf234028a31dec65e10e5b41b0086882ca25b')

prepare() {
  cd Aegisub
  meson subprojects download luajit
  meson subprojects packagefiles --apply luajit
  sed "/subdir('tests')/d" -i meson.build
  echo "#define BUILD_GIT_VERSION_NUMBER ${pkgver/./}" >> git_version.h
  echo "#define BUILD_GIT_VERSION_STRING \"${pkgver}\"" >> git_version.h
  echo '#define TAGGED_RELEASE 0' >> git_version.h
  echo '#define INSTALLER_VERSION "0.0.0"' >> git_version.h
  echo '#define RESOURCE_BASE_VERSION 0, 0, 0' >> git_version.h
}

pkgver() {
  cd Aegisub
  git describe --tags | sed 's/^v//'
}

build() {
  export CXXFLAGS+=" -fpermissive"
  arch-meson Aegisub build \
    -Dopenal=disabled \
    -Dportaudio=disabled
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  install -dm 755 "${pkgdir}"/usr/share/aegisub/automation/include
  cp -dr --no-preserve=ownership Aegisub/automation/{autoload,demos} "${pkgdir}"/usr/share/aegisub/automation/
  cp -dr --no-preserve=ownership Aegisub/automation/include/{aegisub,*.lua} "${pkgdir}"/usr/share/aegisub/automation/include/
  install -Dm 644 Aegisub/LICENCE -t "${pkgdir}"/usr/share/licenses/aegisub/
}

# vim: ts=2 sw=2 et:
