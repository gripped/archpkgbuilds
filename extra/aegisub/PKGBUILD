# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.4.1
pkgrel=1
pkgdesc='A general-purpose subtitle editor with ASS/SSA support'
arch=(x86_64)
url=http://www.aegisub.org
license=(BSD-3-Clause)
depends=(
  alsa-lib
  boost-libs
  curl
  fftw
  fontconfig
  gcc-libs
  glibc
  hicolor-icon-theme
  hunspell
  icu
  libass.so
  libffms2.so
  libgl
  libpulse
  uchardet
  wxwidgets-common
  wxwidgets-gtk3
  zlib
)
makedepends=(
  boost
  cmake
  git
  mesa
  meson
)
_tag=a81794872968ee25ad46a5eebdfe1ce297668a4d
source=(git+https://github.com/TypesettingTools/Aegisub.git#tag=${_tag})
#validpgpkeys=(6DD9508BCB1CE31AF295FF9B6A889F50A8B00C08) # Thomas Goyne <plorkyeran@aegisub.org>
b2sums=('cd1ad877f36a9b1a403f1c8d789a60aaef9d719e160c0aa40c9cc9b29bbaa7e84e8bdd4264bceae342c2b7fc41005007315cb04302d31322da4c4c084e2ac988')

prepare() {
  cd Aegisub
  meson subprojects download luajit
  meson subprojects packagefiles --apply luajit
  sed "/subdir('tests')/d" -i meson.build
}

pkgver() {
  cd Aegisub
  git describe --tags | sed 's/^v//'
}

build() {
  export CXXFLAGS+=" -fpermissive"
  arch-meson Aegisub build \
    -Dopenal=disabled \
    -Dportaudio=disabled
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  install -dm 755 "${pkgdir}"/usr/share/aegisub/automation/include
  cp -dr --no-preserve=ownership Aegisub/automation/{autoload,demos} "${pkgdir}"/usr/share/aegisub/automation/
  cp -dr --no-preserve=ownership Aegisub/automation/include/{aegisub,*.lua} "${pkgdir}"/usr/share/aegisub/automation/include/
  install -Dm 644 Aegisub/LICENCE -t "${pkgdir}"/usr/share/licenses/aegisub/
}

# vim: ts=2 sw=2 et:
