# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Peter Mattern <pmattern at arcor dot de>

pkgname=feathernotes
pkgver=1.4.0
pkgrel=1
pkgdesc="Lightweight Qt hierarchical notes-manager"
arch=('x86_64')
url="https://github.com/tsujan/FeatherNotes"
license=('GPL-3.0-or-later')
depends=('hicolor-icon-theme' 'hunspell' 'qt6-svg')
makedepends=('cmake' 'qt6-tools')
source=("${url}/releases/download/V${pkgver}/FeatherNotes-${pkgver}.tar.xz"{,.asc})
sha256sums=('09a046b66a314c92ea1e89902f396a1fdc180c74ecce34b539a8f85ddbde555c'
            'SKIP')
validpgpkeys=('19DFDF3A579BD509DBB572D8BE793007AD22DF7E')

build() {
	cmake -S "FeatherNotes-${pkgver}" \
		-B build \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
