# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-supply-chain
pkgver=0.3.4
pkgrel=1
pkgdesc='Gather author, contributor and publisher data on crates in your dependency graph'
arch=(x86_64)
url='https://github.com/rust-secure-code/cargo-supply-chain'
license=(Apache-2.0 MIT Zlib)
depends=(glibc gcc-libs)
makedepends=(git rust)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('4aca6973549aeb27382e81bdb91934eb3028d386d4bc0b051ce31e8bb90e673f0313bcecee8ef0072679a3c5115d6422efa465f2723e389fc4d06009dd9482d2')
b2sums=('d013d26e1f91307d5c104df9a0002692e68fb719b7d51b7433978cf225632eb794adca4c5ff391308dd678c2dc9b5e37c642555a336c479cc5a8ab419887fde1')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
