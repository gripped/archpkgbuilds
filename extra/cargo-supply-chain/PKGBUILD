# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-supply-chain
pkgver=0.3.7
pkgrel=1
pkgdesc='Gather author, contributor and publisher data on crates in your dependency graph'
arch=(x86_64)
url='https://github.com/rust-secure-code/cargo-supply-chain'
license=(Apache-2.0 MIT Zlib)
depends=(glibc gcc-libs)
makedepends=(git rust)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('9769a1cc04e3459efa74f09dda7d2e58ec869b5bd4368beac566e51cb63c9333a423f74c3b3810c14c63cd65a39eeba1a2e2d43358b47a9c355d782192f32000')
b2sums=('ee730aa22b99f47114d5b9cdf51b9988b969b39049940101979a4711daf7a0bc5dcd7768515353c7d770f82725868e23f238ca356bacd62c43be0269d0872d13')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
