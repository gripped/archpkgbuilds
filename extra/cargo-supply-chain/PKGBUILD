# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cargo-supply-chain
pkgver=0.3.6
pkgrel=1
pkgdesc='Gather author, contributor and publisher data on crates in your dependency graph'
arch=(x86_64)
url='https://github.com/rust-secure-code/cargo-supply-chain'
license=(Apache-2.0 MIT Zlib)
depends=(glibc gcc-libs)
makedepends=(git rust)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('4b3f09dc4a7639f25cb361096599154d6dc6a2a4d752ada24ee3c0c6b100868e7365f9abb831c7d76d7ccb12bb357f34cb475168eedf1877b9d53e86b413f442')
b2sums=('7b0869b6df97f6a502e671697d00b862c6b098a9cbd2541964171c49568afaf4c482759c2d7abb780c7669f5d94a3209e1837f4ae8b6abac15113428b1552f96')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
