# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=ailment
pkgname=python-${_pyname}
pkgver=9.2.123
pkgrel=1
pkgdesc='The angr intermediate language'
url='https://github.com/angr/ailment'
license=('custom:BSD2')
arch=('any')
depends=('python' 'python-claripy' 'python-pyvex' 'python-pypcode')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-archinfo')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0cd5a19620c6e61af09b48c525bb553a353019bec60c775c2a27764f242beafd5b6c2c5d5632ff428c142bb27e9583e2aa1142a86032eefe6d4b31af44c91435')
b2sums=('3d88a532d953a71adacb51537a3198dc3fa5b0ba1f3f4402117aaead285409acc10727a8e2bbfb363a12b8a2a000c4e7152f5e24e4a78f20de1a9e0989b1c55d')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  # TODO: needed for angr (hard dep)
  # PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
