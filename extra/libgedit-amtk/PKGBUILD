# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libgedit-amtk
pkgver=5.9.1
pkgrel=1
pkgdesc='Actions, Menus and Toolbars Kit for GTK applications'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/libgedit-amtk'
license=(LGPL-3.0-or-later)
depends=(
  glib2
  glibc
  gtk3
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
replaces=(amtk)
provides=(libgedit-amtk-5.so)
conflicts=(amtk)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-amtk.git?signed#tag=$pkgver")
b2sums=(76f991d178890994fc41711afb75853b6e79025905bb435000ca32341f83beb2184e37c910315ed4ffbee47c869348e883d980b986caa25cebb5c3b1c157cdbb)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
