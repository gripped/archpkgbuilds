# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Michael Beaumont <mjboamail@gmail.com>

pkgname=sops
pkgver=3.9.4
pkgrel=1
pkgdesc='Editor of encrypted files that supports YAML, JSON, ENV, INI and BINARY formats'
arch=('x86_64')
url='https://github.com/getsops/sops'
license=('MPL-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("git+https://github.com/getsops/sops.git?signed#tag=v${pkgver}"
        'bash_autocomplete'
        'zsh_autocomplete')
sha256sums=('d8911ee8090a2fd0947ee319857e2cb4a9e39fe518c933a01b7d7b68f2eeb176'
            '761c67ffd48431e60ea5dc48e5656b4480eca7534d4b715eb65bd75680f5e975'
            'd98d34aeaa7b0f98220bcb21fc9f2bb58efaed45478a7229eb58e5795b57b5a7')
validpgpkeys=('1CBD23A64DD703A808AF66E71E60B8598833B166') # Felix Fontein

prepare() {
  cd ${pkgname}

  go mod download
}

build() {
  cd ${pkgname}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPROXY=off
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode=external -extldflags \"${LDFLAGS}\"" \
    ./cmd/sops
}

check() {
  cd ${pkgname}

  go test
}

package() {
  install -Dm644 bash_autocomplete "${pkgdir}/usr/share/bash-completion/completions/sops"
  install -Dm644 zsh_autocomplete "$pkgdir/usr/share/zsh/site-functions/_sops"

  cd ${pkgname}

  install -Dm755 sops "${pkgdir}/usr/bin/sops"
  install -Dm644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}
