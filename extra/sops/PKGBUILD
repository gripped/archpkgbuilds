# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Michael Beaumont <mjboamail@gmail.com>

pkgname=sops
pkgver=3.10.2
pkgrel=2
pkgdesc='Editor of encrypted files that supports YAML, JSON, ENV, INI and BINARY formats'
arch=('x86_64')
url='https://github.com/getsops/sops'
license=('MPL-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("git+https://github.com/getsops/sops.git?signed#tag=v${pkgver}"
        'bash_autocomplete'
        'zsh_autocomplete')
sha512sums=('1058973d19af463a607e87db26bc4361b17f1e7c151cb6a3444a908fc359fd0ee79a7ad0e6f18682db3e26df0377724dbe3c30e309f855b374cb7075fb6225a7'
            '95d62564dc6ceb449b0b498a40e383bc15b342856ee6862bb4424f4aa6a168fe797d9c5cafb945a8111ac97958f03c6033dccd1fda49efeecb5ab46d22ae9be0'
            '3293fa87e9a2acb0d290241f0586292bc79f502c246c4b853edb3dc3852014b9fdd0d8f149db4e5e88ca65dea32e34c8bde7191b859ad027a3acbce2cb2f1af9')
b2sums=('740e3db5ba0d186d222420692ddb09a41eb340f89322d17151fa86987ac0fe8683fe5b8bbfede4319b3b31fb56e68dcbf16d597668e2b3772da4e188f4e25c49'
        'b0bbd3a5ffa49c4383b5c3a9fa7c911051735ef828452727dfd970e020a47d999a2dbfe8311547812587a56490a3f3bd1ec022c52ed2753acf3282924f47d37f'
        'a34a13a107899fa60739ebb146264ca6ea9cd5bc359a29006b1ea0ab0e1202ca82bf7c343141517b4def18e206082b2d01f69392f52f1b52c5f1345fa017be6e')
validpgpkeys=('1CBD23A64DD703A808AF66E71E60B8598833B166') # Felix Fontein

prepare() {
  cd ${pkgname}

  go mod download
}

build() {
  cd ${pkgname}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPROXY=off
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode=external -extldflags \"${LDFLAGS}\"" \
    ./cmd/sops
}

check() {
  cd ${pkgname}

  go test
}

package() {
  install -Dm644 bash_autocomplete "${pkgdir}/usr/share/bash-completion/completions/sops"
  install -Dm644 zsh_autocomplete "$pkgdir/usr/share/zsh/site-functions/_sops"

  cd ${pkgname}

  install -Dm755 sops "${pkgdir}/usr/bin/sops"
  install -Dm644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}
