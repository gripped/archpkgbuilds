# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Michael Beaumont <mjboamail@gmail.com>

pkgname=sops
pkgver=3.10.1
pkgrel=1
pkgdesc='Editor of encrypted files that supports YAML, JSON, ENV, INI and BINARY formats'
arch=('x86_64')
url='https://github.com/getsops/sops'
license=('MPL-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("git+https://github.com/getsops/sops.git?signed#tag=v${pkgver}"
        'bash_autocomplete'
        'zsh_autocomplete')
sha512sums=('9a646d85cc182c65622f41d89710367cd29c02d4e5bcef5e87b4e2dc6823177471b8af377c327cdb0fd215850a4a3f53efac8acc7e2b9324b8e68eeca3f4d944'
            '95d62564dc6ceb449b0b498a40e383bc15b342856ee6862bb4424f4aa6a168fe797d9c5cafb945a8111ac97958f03c6033dccd1fda49efeecb5ab46d22ae9be0'
            'ce945bdab2232c331cbaa45f2afbd940980925cb87717e375a4a6801207f4617ac8903835d0da993bced542d623a71c430e3d89b2ec010731ab3930b4e4608b6')
b2sums=('a75b07f2ae6c2796412e1df586381fbd23740ff2662b608eb00740e6974c32a5ff396a25275944f12ebb24d458aad0eaa2d69f4b9d99a27aef7ddc1b4cbf49c7'
        'b0bbd3a5ffa49c4383b5c3a9fa7c911051735ef828452727dfd970e020a47d999a2dbfe8311547812587a56490a3f3bd1ec022c52ed2753acf3282924f47d37f'
        '115f01ef87c4227f3ef07e55c5865e1ccb253af5fa85c2d445d938331228f12b5fd948138975f5dc4abc70aaeef6d661382ccef539ec7c293ab85705419dc1e1')
validpgpkeys=('1CBD23A64DD703A808AF66E71E60B8598833B166') # Felix Fontein

prepare() {
  cd ${pkgname}

  go mod download
}

build() {
  cd ${pkgname}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPROXY=off
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode=external -extldflags \"${LDFLAGS}\"" \
    ./cmd/sops
}

check() {
  cd ${pkgname}

  go test
}

package() {
  install -Dm644 bash_autocomplete "${pkgdir}/usr/share/bash-completion/completions/sops"
  install -Dm644 zsh_autocomplete "$pkgdir/usr/share/zsh/site-functions/_sops"

  cd ${pkgname}

  install -Dm755 sops "${pkgdir}/usr/bin/sops"
  install -Dm644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}
