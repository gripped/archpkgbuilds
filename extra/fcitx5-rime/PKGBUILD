# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-rime
pkgver=5.1.10
pkgrel=1
pkgdesc="RIME support for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-rime"
license=('GPL')
depends=('fcitx5' 'librime')
makedepends=('git' 'extra-cmake-modules' 'ninja')
source=("git+https://github.com/fcitx/fcitx5-rime.git#tag=$pkgver?signed")
sha512sums=('a27e31e09f27b5b6a274c0c3cb13ef246d39c522a13428c4eb21b9d702171008cbbfb143c9153b15e3d811854e0a0fca7f94f81b1e629ab3157e4efdafe1160f')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
