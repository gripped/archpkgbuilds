# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>
pkgname=geogram
pkgver=1.9.7
pkgrel=1
pkgdesc="Library of geometric algorithms"
arch=('x86_64')
url="https://github.com/BrunoLevy/geogram"
license=('BSD-3-Clause')
depends=('gcc-libs' 'glu' 'glfw-x11')
makedepends=('cmake' 'ninja')
source=("https://github.com/BrunoLevy/geogram/releases/download/v${pkgver}/geogram_${pkgver}.tar.gz")
sha256sums=('b6c99fbdbefd633f03443c9c6359dfb6eb26cda0a54202793a633cce34cff6bf')

build() {
  cd ${pkgname}_${pkgver}

  sed -i "s|cmake/modules|cmake/geogram|" CMakeLists.txt

  CFLAGS+=" -Wno-incompatible-pointer-types -std=gnu99" \
  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DGEOGRAM_USE_SYSTEM_GLFW3=ON

  ninja -C build
}

package() {
  cd ${pkgname}_${pkgver}

  DESTDIR=${pkgdir} ninja -C build install
  install -Dm755 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
