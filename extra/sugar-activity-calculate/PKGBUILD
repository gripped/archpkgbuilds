# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-calculate
_pkgname=calculate-activity
pkgver=47
pkgrel=5
pkgdesc='Graphing calculator for Sugar'
arch=(any)
url='https://github.com/sugarlabs/calculate-activity'
license=(GPL-2.0-or-later)
groups=(sugar-fructose)
depends=(
  glib2
  gtk3
  librsvg
  pango
  python
  python-dbus
  python-gobject
  sugar-toolkit-gtk3
  telepathy-glib
)
optdepends=('python-matplotlib: alternative plotting backend')
makedepends=(git)
source=("git+https://github.com/sugarlabs/$_pkgname.git#tag=v$pkgver")
b2sums=(e95b55a946267c7585ac244934cbebff6dc244da193040b8ca126efe296051c89dfd576d41fa7feb639f1eac2127bd7a64966e65c81056a05fd9b90681a44da2)

prepare() {
  cd $_pkgname

  # Set buttons to vertically expand to fill grid
  git cherry-pick -n b5067c32bdd4fae9cc7166ce26bc8de62993d165
  git cherry-pick -n f3bedcf9c4ed967c65f1bd7e1ac18c299c81dff5
}

package() {
  cd $_pkgname
  python setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
