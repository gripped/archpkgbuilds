# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Rob Til Freedmen <rob.til.freedman@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=hydrogen
pkgver=1.2.6
pkgrel=1
pkgdesc="An advanced drum machine"
arch=(x86_64)
url="http://www.hydrogen-music.org/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt5-base
  qt5-svg
  qt5-xmlpatterns
)
makedepends=(
  alsa-lib
  cmake
  jack
  ladspa
  libarchive
  liblo
  liblrdf
  libpulse
  libsndfile
  portaudio
  portmidi
  python
  qt5-tools
)
checkdepends=(cppunit)
optdepends=('new-session-manager: for session management')
source=(
  https://github.com/$pkgname-music/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('ddf0c7183ce6721cb29d8e9ab83f0ecd9e2f598316fc7a903c54e78a1f9e6e842d120b65554b74f8ede75cc8fe8042f0865ba62c66624ffce330b3e61c34ca57')
b2sums=('00c8a4ef0f2df0a5e98275bb6aa8c968a814f34f4d97f381bf97fd1c27fa41b757fa7a5634b855239e7901e4cffd059d1d77ca17d879042645218fc525bb2f9a')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D WANT_DEBUG=OFF
    -D WANT_CPPUNIT=ON
    -D WANT_LRDF=ON
    -D WANT_PORTAUDIO=ON
    -D WANT_PORTMIDI=ON
    -D WANT_PULSEAUDIO=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
  # update translations
  (
    cd $pkgname-$pkgver/data/i18n
    ./updateTranslations.sh
  )
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    alsa-lib libasound.so
    jack libjack.so
    libarchive libarchive.so
    liblo liblo.so
    liblrdf liblrdf.so
    libpulse libpulse.so
    libsndfile libsndfile.so
    portaudio libportaudio.so
    portmidi libportmidi.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,INSTALL,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
