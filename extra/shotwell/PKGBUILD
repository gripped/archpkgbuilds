# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
epoch=2
pkgver=0.32.14
_basever=${pkgver%.*}
pkgrel=2
pkgdesc="A digital photo organizer designed for the GNOME desktop environment (odd versions are unstable!)"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Shotwell"
license=('LGPL2.1')
depends=('libgee' 'libgexiv2' 'libraw' 'libsoup3'
	 'dconf' 'hicolor-icon-theme' 'json-glib'
	 'gst-plugins-base' 'libgphoto2' 'libgudev'
	 'gcr' 'libportal-gtk3')
makedepends=('intltool' 'vala>=0.17.2' 'itstool' 'meson' 'ninja' 'cmake')
optdepends=('gst-libav: encoders, decoders, muxers, and demuxers provided by FFmpeg')
# odd versions are unstable!
source=("https://download.gnome.org/sources/shotwell/${_basever}/shotwell-$pkgver.tar.xz")
sha256sums=('41b122f55d245a4b68d687085fd92398d25f0bbca5483a98b2b7932be4f289da')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix string format in translation. It uses cyrillic-`e` instead of `e`.
  sed -i 's#%ะต#%e#g' po/ru.po
}

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
