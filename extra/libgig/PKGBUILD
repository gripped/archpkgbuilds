# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=libgig
pkgver=4.5.0
pkgrel=1
pkgdesc="C++ library for loading, modifying and creating .gig, .ksf, .kmp, .sf2 and DLS files"
arch=(x86_64)
url="https://www.linuxsampler.org/libgig/"
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  gcc-libs
  glibc
  util-linux-libs
)
makedepends=(
  libsndfile
  util-linux
)
provides=(libgig.so)
source=(
  https://download.linuxsampler.org/packages/$pkgname-$pkgver.tar.bz2
  $pkgname-4.3.0-libdir.patch
)
sha512sums=('921bddf06b572d7336cb0055a62b6509901bc07156ea487b0941ae84a86ae545c54772ef113f34be40bd17894a1364480f4e5e1b93ef2635ed2b430f052d789e'
            '7bc339ff8fddc7e35ab8a58a591038bc06b8fb3acc895b471968e8731608e1524d74c3e58f6e5cff06ca1a4fd0c09de85e6ffedff0336df31270aeaf5aef6f81')
b2sums=('13122abdb6b2013bd60be06a3eabdecab2eb882b156dd8c86e0196245f98e7d2702e1818867a0bfac382a31da9116fab52c79aca8bf7760e3ee915bcf228e69a'
        '67790271997b476b4fdd6eb82c69d889de3a2d0f419d5338fa552d8ebdaef37f12596307fc1831af3e2bae1e270e714f41358a71f518d912bed2200917957c74')

prepare() {
  # install libgig.so as a globally visible shared object
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-4.3.0-libdir.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(
    libsndfile libsndfile.so
  )
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
