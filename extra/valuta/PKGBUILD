# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=valuta
pkgver=1.3.2
pkgrel=4
pkgdesc='Simple application for converting currencies'
arch=(any)
url='https://apps.gnome.org/Valuta/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
  python
  python-babel
  python-gobject
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/ideveCore/Valuta.git#tag=v$pkgver"
  valuta-activate.patch
  valuta-dbus-activatable.patch
  valuta-search-provider.patch
)
b2sums=(
  fd86776631bea7467775e4b4da1a2abd839296868485ddffe50b38a0027b8402e6558bb71257230be8d76760acdd1d50b08a0ad0998f1ac6a6ad48107d68f316
  6043f18f562198efa87b7c6a1e6d12f36e86e0968430e69a4e4d50ae8b6d5e1e17a2d71ff209a97b9e08f2203228e9c2007c7b73f0a93664f23e9cef76187209
  86da7ea669bd24a4872fe3baca92dc401a3e64fa87c4d165933bf8db9b6f6b34316e9d9c654553b7b187ba9418a1b4c372c7af4c826e9142d232f784b099458d
  413ca9d623041a359cb7d97b6bbba1335c7802688ecf0354764b8af4e363e802b55be903385fc33c0649c31ddc9e0b7a23f91e18bbf2161bb404f79cb3bb8174
)

prepare() {
  cd Valuta

  # Various fixes from upstream
  git cherry-pick -n -m 1 v1.3.2..8dfbf5ef226dce9618e00943e8c444aaddd6c368

  # https://github.com/ideveCore/Valuta/pull/59
  git apply -3 ../valuta-activate.patch

  # https://github.com/ideveCore/Valuta/pull/60
  git apply -3 ../valuta-dbus-activatable.patch

  # https://github.com/ideveCore/Valuta/pull/61
  git apply -3 ../valuta-search-provider.patch
}

build() {
  arch-meson Valuta build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  find "$pkgdir/usr/share/" -type f -name "*.blp" -delete

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
