# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=valuta
pkgver=1.3.2
pkgrel=3
pkgdesc='Simple application for converting currencies'
arch=(any)
url='https://apps.gnome.org/Valuta/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
  python
  python-babel
  python-gobject
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/ideveCore/Valuta.git#tag=v$pkgver"
  valuta-about-dialog.patch
  valuta-regex.patch
  valuta-metainfo.patch
  valuta-window-icon.patch
  valuta-about-license.patch
  valuta-icons.patch
)
b2sums=(
  fd86776631bea7467775e4b4da1a2abd839296868485ddffe50b38a0027b8402e6558bb71257230be8d76760acdd1d50b08a0ad0998f1ac6a6ad48107d68f316
  4bb9fba5222a2d5c09bf753cccbcb5a649d25b2212825dd643dca186d24a9a8293b7a87dacdd3c5aa679e342978e08165e5c9f785dd4ee47a47c72a0b5211f6f
  e8272b8e3310e23e6d2005adde785b7e79acfedb42028de98a24effee96cee9031f3c3a919d0d2b05d72d25040fd29e8eef64dfe279e4097b5f97e058912d5ed
  fed7978d2d37b61f3c34012cae58fc945811327549f956436188df234beeb7d7deb5f89d73073d1a862f328aee2eebe63e39fc2e36e48de71449c76f91dca64b
  7cc3cbc4c1b9bc21858afcf2a82d0edd7b356d8014212bff718741f904761ccb6ed8ca5e5cdea2fcc7f9ac11f113e71d36358d9dd279559fc039ad46141e0bb3
  1828a0581a4883115cd40e8f986347d439809a5a988e5aae5cf240a0f10bf7b4dd0fa2a0d5d785edf0fd559839acf19a1ca936d0a6e5c156b86a5a373e214b57
  SKIP
)

prepare() {
  cd Valuta

  # https://github.com/ideveCore/Valuta/pull/45
  git apply -3 ../valuta-about-dialog.patch

  # https://github.com/ideveCore/Valuta/pull/46
  git apply -3 ../valuta-regex.patch

  # https://github.com/ideveCore/Valuta/pull/47
  git apply -3 ../valuta-metainfo.patch

  # https://github.com/ideveCore/Valuta/pull/48
  git apply -3 ../valuta-window-icon.patch

  # https://github.com/ideveCore/Valuta/pull/49
  git apply -3 ../valuta-about-license.patch

  # https://github.com/ideveCore/Valuta/pull/50
  git apply -3 ../valuta-icons.patch
}

build() {
  arch-meson Valuta build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  find "$pkgdir/usr/share/" -type f -name "*.blp" -delete

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
