# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=valuta
pkgver=1.4.2
pkgrel=2
pkgdesc='Simple application for converting currencies'
arch=(any)
url='https://apps.gnome.org/Valuta/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
  python
  python-babel
  python-gobject
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://github.com/ideveCore/Valuta.git#tag=v$pkgver")
b2sums=(83b9e67759d7cc3fe46a6a27c0a555847490dd78134197ebb22cfd008a735ff026e97a2099f4455102196bd38b8da3f4920f6a98a8481ccfd2c40bc4f36f347c)

prepare() {
  cd Valuta
}

build() {
  arch-meson Valuta build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  find "$pkgdir/usr/share/" -type f -name "*.blp" -delete

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
