# Maintainer: Dan Printzell <wild@archlinux.org>

pkgname=('dfmt')
pkgver=0.15.2
pkgrel=1
_pkgcommit=d8e43e23eca0aa32f064fe7efe8e74a9efa8018e
pkgdesc="Dfmt is a formatter for D source code"
arch=('x86_64')
url="https://github.com/dlang-community/dfmt"
license=("BSL-1.0")
groups=('dlang')
makedepends=('ldc' 'git')
depends=('liblphobos')

source=(
	"git+https://github.com/dlang-community/dfmt#commit=$_pkgcommit"
	"git+https://github.com/dlang-community/libdparse"
	"git+https://github.com/sociomantic-tsunami/makd"
	"git+https://github.com/sociomantic-tsunami/beaver"
	"git+https://github.com/dlang-community/stdx-allocator"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
	cd "$srcdir/dfmt"

	git submodule init
	git config submodule.libdparse.url "$srcdir/libdparse"
	git config submodule.makd.url "$srcdir/makd"
	git config submodule.beaver.url "$srcdir/beaver"
	git config submodule.stdx-allocator.url "$srcdir/stdx-allocator"
	git -c protocol.file.allow=always submodule update

	_ldFlags=$(echo -ne $LDFLAGS | cut -d\" -f2 | tail -c+4 | sed -e "s/-Wl,/-L=/g" -e "s/,/ -L=/g" -e "s/-flto=auto//")
	_extraFlags="-link-defaultlib-shared=false $_ldFlags"
	sed -i "/LDC_FLAGS \:=/ a override LDC_FLAGS += $_extraFlags" makefile
}

build() {
	cd "$srcdir/dfmt"
	make ldc
}

package() {
	# binaries
	install -Dm755 "$srcdir/dfmt/bin/dfmt" "$pkgdir/usr/bin/dfmt"

	# license
	install -Dm644 "$srcdir/dfmt/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
