# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: daskol (Daniel Bershatsky) < bepshatsky at yandex dot ru >
# Contributor: trougnouf (Benoit Brummer) < trougnouf at gmail dot com >

_name=datasets
pkgname=python-$_name
pkgver=4.1.1
pkgrel=1
pkgdesc="Ready-to-use datasets for ML models with fast, easy-to-use and efficient data manipulation tools"
arch=(any)
url="https://github.com/huggingface/datasets"
license=(Apache-2.0)
groups=(huggingface)
depends=(
  python
  python-aiohttp
  python-dill
  python-filelock
  python-fsspec
  python-h5py
  python-huggingface-hub
  python-multiprocess
  python-numpy
  python-packaging
  python-pandas
  python-pyarrow
  python-requests
  python-tqdm
  python-xxhash
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-absl
  python-decorator
  python-librosa
  python-pytest
  python-pytest-datadir
  python-pytest-xdist
  python-pytorch
  python-soxr
  python-sqlalchemy
  python-torchcodec
  python-zstandard
)
optdepends=(
  'python-librosa: Audio datasets'
  'python-soxr: Audio datasets'
  'python-torchcodec: Audio datasets'
  'python-pillow: Vision datasets'
  'python-tensorflow: TensorFlow support'
  'python-pytorch: PyTorch support'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
b2sums=('6f40a4523f305761cad40a00557f09e4846ed0c8f2063e109e0b81f534aef493fed0ba31a4d32adf171df50287bbe2009b9baa2d68785c74f213004f1ebb875a')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    # distribute tests across multiple CPUs
    -n auto
    # python-pyspark is not packaged, causes error when collecting tests
    --ignore tests/packaged_modules/test_spark.py
    # ModuleNotFoundError: No module named 'aifc'
    --deselect tests/packaged_modules/test_audiofolder.py::test_data_files_with_metadata_and_archives
    # AssertionError failures
    --deselect tests/test_arrow_dataset.py::BaseDatasetTest::test_filter_caching_on_disk
    --deselect tests/test_arrow_dataset.py::BaseDatasetTest::test_map_caching_on_disk
    --deselect tests/test_arrow_dataset.py::BaseDatasetTest::test_map_caching_partial_remap_on_disk
    --deselect tests/test_arrow_dataset.py::BaseDatasetTest::test_map_caching_reuses_cache_with_different_num_proc_on_disk
    --deselect tests/test_file_utils.py::TestxPath::test_xpath_glob
    --deselect tests/test_file_utils.py::TestxPath::test_xpath_rglob
    --deselect tests/test_fingerprint.py::RecurseHashTest
    # NameError: name 'log' is not defined
    --deselect tests/test_fingerprint.py::HashingTest
    --deselect tests/test_iterable_dataset.py::test_iterable_dataset_from_file
    # subprocess.CalledProcessError
    --deselect tests/test_fingerprint.py::test_move_script_doesnt_change_hash
    # ImportError: Install s3fs to access S3
    --deselect tests/test_arrow_dataset.py::test_build_local_temp_path[s3://bucket/relative/path]
    # TypeError: Passing coroutines is forbidden, use tasks explicitly.
    --deselect tests/test_distributed.py::test_torch_distributed_run
    --deselect tests/test_distributed.py::test_torch_distributed_run_streaming_with_num_workers
    # FileNotFoundError: Couldn't find a dataset script at
    # /build/python-datasets/src/datasets-3.5.0/patrickvonplaten/librispeech_asr_dummy/librispeech_asr_dummy.py
    # or any data file in the same directory. Couldn't find
    # 'patrickvonplaten/librispeech_asr_dummy' on the Hugging Face Hub either
    --deselect tests/features/test_audio.py::test_dataset_with_audio_feature_loaded_from_cache
    # often fails due to 429 Client Error: Too Many Requests
    --deselect tests/test_inspect.py::test_get_dataset_split_names
    --deselect tests/test_inspect.py::test_get_dataset_info
    # FileNotFoundError
    --deselect tests/test_load.py::test_load_dataset_private_zipped_images[True]
    # Hanging tests
    --deselect tests/test_upstream_hub.py::TestPushToHub::test_push_dataset_dict_to_hub_num_proc
    --deselect tests/test_upstream_hub.py::TestPushToHub::test_push_dataset_dict_to_hub_iterable_num_proc
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
