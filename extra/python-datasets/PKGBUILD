# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: daskol (Daniel Bershatsky) < bepshatsky at yandex dot ru >
# Contributor: trougnouf (Benoit Brummer) < trougnouf at gmail dot com >

_name=datasets
pkgname=python-$_name
pkgver=4.4.2
pkgrel=1
pkgdesc="Ready-to-use datasets for ML models with fast, easy-to-use and efficient data manipulation tools"
arch=(any)
url="https://github.com/huggingface/datasets"
license=(Apache-2.0)
groups=(huggingface)
depends=(
  python
  python-aiohttp
  python-dill
  python-filelock
  python-fsspec
  python-h5py
  python-httpx
  python-huggingface-hub
  python-multiprocess
  python-numpy
  python-packaging
  python-pandas
  python-pyarrow
  python-requests
  python-tqdm
  python-xxhash
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-absl
  python-decorator
  python-librosa
  python-pytest
  python-pytest-datadir
  python-pytest-rerunfailures
  python-pytest-xdist
  python-pytorch
  python-soxr
  python-sqlalchemy
  python-torchcodec
  python-zstandard
)
optdepends=(
  'python-librosa: Audio datasets'
  'python-soxr: Audio datasets'
  'python-torchcodec: Audio datasets'
  'python-pillow: Vision datasets'
  'python-tensorflow: TensorFlow support'
  'python-pytorch: PyTorch support'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
b2sums=('205f9bad54a01830fe4f324e5ae7f1d55732f6ce0532a9828b78e5732c8e30b65e1590cdeb81ddd4d26ff882fbe6c184c33e1af13fc13fd80a753794b91c5688')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    # distribute tests across multiple CPUs
    -n auto
    # re-run failed tests
    --reruns 2
    # python-pyspark is not packaged, causes error when collecting tests
    --ignore tests/packaged_modules/test_spark.py
    # ModuleNotFoundError: No module named 'aifc'
    --deselect tests/packaged_modules/test_audiofolder.py::test_data_files_with_metadata_and_archives
    # subprocess.CalledProcessError
    --deselect tests/test_fingerprint.py::test_move_script_doesnt_change_hash
    # ImportError: Install s3fs to access S3
    --deselect tests/test_arrow_dataset.py::test_build_local_temp_path[s3://bucket/relative/path]
    # TypeError: Passing coroutines is forbidden, use tasks explicitly.
    --deselect tests/test_distributed.py::test_torch_distributed_run
    --deselect tests/test_distributed.py::test_torch_distributed_run_streaming_with_num_workers
    # skip test modules that cause many failures due to rate-limiting (429 Too Many Requests)
    --deselect tests/test_inspect.py
    --deselect tests/test_load.py
    --deselect tests/test_upstream_hub.py
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
