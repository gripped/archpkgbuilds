# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=dialect
pkgver=2.5.0
pkgrel=3
pkgdesc='Translation app for GNOME based on Google Translate and other online services'
arch=(any)
url='https://apps.gnome.org/Dialect/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gst-plugins-base
  gst-plugins-good
  gst-python
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libsecret
  libsoup3
  python
  python-beautifulsoup4
  python-gobject
  python-gtts
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/dialect-app/dialect.git#tag=$pkgver"
  dialect-po::git+https://github.com/dialect-app/po
)
b2sums=(
  80bf66868466ea4e23d73734bff48b655be8a9e5f78f359f3c08e3da24d42c02d94fdbeff30f557c251ad723ac323ea19a52098ca705ac48d5bde08c30383902
  SKIP
)

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url po "$srcdir/dialect-po"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  find "$pkgdir/usr/share/" -type f -name "*.blp" -delete

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
