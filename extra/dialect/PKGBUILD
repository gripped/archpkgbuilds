# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=dialect
pkgver=2.6.0
pkgrel=1
pkgdesc='Translation app for GNOME based on Google Translate and other online services'
arch=(any)
url='https://apps.gnome.org/Dialect/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gst-plugins-base
  gst-plugins-good
  gst-python
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libsecret
  libsoup3
  python
  python-beautifulsoup4
  python-gobject
  python-gtts
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/dialect-app/dialect.git#tag=$pkgver"
  dialect-po::git+https://github.com/dialect-app/po
  dialect-dbus-activatable.patch
  dialect-fix-search-provider.patch
)
b2sums=(
  80dea07d8359bceb5939214970e8442e0c3248e0cb6986b44e34dabb0d5dcf06fa3c899aaed6bec11f750a37962d62665f451052db04bc73bd91ff5b31d2f33e
  SKIP
  24dd584e65474dcee062b3bcef06141288f6d85e8951f6fafa1f2fadd895b477ce94b1f59b8abcbd0ece1b0c0c8407b6bf8e557f338587bb223163deccd165c5
  6edd960ffc2ae188955bd96b3b86f3c099a0d1d98171b5f7bc4aa36b14976d6dd1c87473715d2352c7609c9e445ffd7c99246f3281f77dab49afe02f172ebffb
)

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url po "$srcdir/dialect-po"
  git -c protocol.file.allow=always submodule update

  # https://github.com/dialect-app/dialect/pull/431
  git apply -3 ../dialect-dbus-activatable.patch

  # https://github.com/dialect-app/dialect/pull/437
  git apply -3 ../dialect-fix-search-provider.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  find "$pkgdir/usr/share/" -type f -name "*.blp" -delete

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
