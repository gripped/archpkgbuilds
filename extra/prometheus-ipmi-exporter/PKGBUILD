# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgname=prometheus-ipmi-exporter
pkgver=1.10.1
pkgrel=1
pkgdesc='Prometheus exporter for IPMI metrics'
arch=(x86_64)
url='https://github.com/prometheus-community/ipmi_exporter'
license=('MIT')
depends=('glibc' 'freeipmi')
makedepends=('git' 'go')
backup=(
  'etc/prometheus/ipmi-exporter.yml'
  'etc/prometheus/ipmi-exporter.env'
)
options=('!lto')
source=(
  "$pkgname::git+$url.git#tag=v$pkgver"
  'systemd.service'
  'sysusers.conf'
  'sudoers.conf'
  'config.yml'
  'config.env'
)
sha512sums=('b7713c329d4488e1d615e8a941132d6d8ded6be595ea3bad11a1bea26538ba5bf8ec3ed17c90f6a61552be93e6dafedaa7134c5e3063c024b4b0da20dce79463'
            '420dbe6629a904979988ef9d28a7c09de14e88828375397af92e49469962dc9593ee236b503a2cb0f0b140e49bbc79076e7c3ea1f6e8bc2cc58d4ab1194c785f'
            'bf654d4dad35349c23f4ef91f53b585f899c44f4806b7d9c8de62945711ebf8c751918c3cda0fff106c39b2873a11d2520b829977e4b632070d60e584aed56fd'
            '7be2811049c75f572956ceeba3fc68a5ae52643a09e0275c33e1d8d12acf47e61595db1738cee98dd0437a71927ed254ad99ba5d1fc09a5178bcb18473f43b4a'
            'a26a7d379d8577e0f9237218b7d63df25643ae37c9f1a504b4a4ac5421e01cd9ac1a72435bacf16cce25e08a2394367e0d629ac1e877c1831f3c570ffbfafcff'
            '0ce0a1f01a9f7b03df172a1f61bb0903bc4e1fdfd6422c8d39f6b33672906073e4da234277a1fdc8f552d30cb772998b321fe9814c63c2cebadf64493667791f')
b2sums=('19ea1be82f92e6d4bc1ac8473221bdfbd300250f4bb8c439a3c8ee1dbee7dfff5054d95ad5da1a9167c2f32e5b2069ecbd88d3431352f6857352e9f9932742b6'
        '50295e5e9b56a3fbe2d2093f15c3582637cfea1665a9a100da010b1fb435ad3a79babd9c73746c48f86ac4e36af526f7a9da83d272e0fbcd3b779db7d42fb052'
        '5a207a39efa97117e28f356793d1001957309d2912c3be2c6ebe242789a7ce4cd45114526ee7b796704bf3209a35961bc83cc1b29ecf1b2635ebf1c0414c3ec1'
        '7289646126dbdd22c9183fa9831d11b5492116daa0bd175ab2b20180cebdfb601edc086e9d7e689ca496f8992926f807d1c45b06d1ead6b85d8ce15c710caaba'
        'b4390aa6796cfbd2a4595e1aa5ffd250c997def77887cfb48e4e7bba9e044acb51533b5e9c2375aa274eeff27aba31c5df81abdce13228173b9c6da66db4e622'
        '042b25709b320603f3440fd0592bc159b75c26991d4e608540ac78b3cb74b501750e592b938898218872310b16b7ab0d66a888a02f105ff5c1110be0f27101d6')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # reproducible: use commit date for build date
  local _commit_date=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d-%H:%M:%S)

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/prometheus/common/version.Version=$pkgver \
    -X github.com/prometheus/common/version.Revision=$(git rev-parse HEAD) \
    -X github.com/prometheus/common/version.Branch=main \
    -X github.com/prometheus/common/version.BuildUser=archlinux \
    -X github.com/prometheus/common/version.BuildDate=$_commit_date" \
    -o build \
    .
}

package() {
  # systemd integration
  # taken from upstream's contrib/rpm directory
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm640 config.yml "$pkgdir/etc/prometheus/${pkgname#prometheus-}.yml"
  install -vDm640 config.env "${pkgdir}/etc/prometheus/${pkgname#prometheus-}.env"

  # sudoers configuration
  install -vdm750 "$pkgdir/etc/sudoers.d"
  install -vDm440 sudoers.conf "$pkgdir/etc/sudoers.d/$pkgname"

  cd "$pkgname"

  # binary
  install -vDm755 build/ipmi_exporter "$pkgdir/usr/bin/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" \
    ipmi*.yml docs/*.md README.md
}
