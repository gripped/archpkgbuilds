# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Achmad Fathoni<fathoni.id(at)gmail.com>
# Contributor: Michał Wojdyła < micwoj9292 at gmail dot com >

pkgname=python-trio-websocket
pkgver=0.12.2
pkgrel=1
pkgdesc="WebSocket library for Trio"
arch=('any')
url='https://github.com/HyperionGray/trio-websocket'
license=(MIT)
depends=(python "python-"{trio,wsproto})
makedepends=("python-"{build,installer,setuptools,wheel})
source=(https://github.com/HyperionGray/trio-websocket/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('4784c823dbe98e4dc3909b560857c64f5786bca01d00d16f2e3ae37a32a54174')

build() {
    cd ${pkgname#*-}-${pkgver}
    python -m build --wheel --no-isolation
}

package() {
    cd ${pkgname#*-}-${pkgver}
    python -m installer --destdir="$pkgdir" dist/*.whl
}
