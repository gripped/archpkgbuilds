# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Achmad Fathoni<fathoni.id(at)gmail.com>
# Contributor: Michał Wojdyła < micwoj9292 at gmail dot com >

pkgname=python-trio-websocket
pkgver=0.12.1
pkgrel=1
pkgdesc="WebSocket library for Trio"
arch=('any')
url='https://github.com/HyperionGray/trio-websocket'
license=(MIT)
depends=(python "python-"{trio,wsproto})
makedepends=("python-"{build,installer,setuptools,wheel})
source=(https://github.com/HyperionGray/trio-websocket/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('f1a60e5957ef8f12083f5b135f7878c7dfc15a3c942f8c67f0cf08ced30cbca8')

build() {
    cd ${pkgname#*-}-${pkgver}
    python -m build --wheel --no-isolation
}

package() {
    cd ${pkgname#*-}-${pkgver}
    python -m installer --destdir="$pkgdir" dist/*.whl
}
