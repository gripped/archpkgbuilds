# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Achmad Fathoni<fathoni.id(at)gmail.com>
# Contributor: Michał Wojdyła < micwoj9292 at gmail dot com >

pkgname=python-trio-websocket
pkgver=0.12.0
pkgrel=1
pkgdesc="WebSocket library for Trio"
arch=('any')
url='https://github.com/HyperionGray/trio-websocket'
license=(MIT)
depends=(python "python-"{trio,wsproto})
makedepends=("python-"{build,installer,setuptools,wheel})
source=(https://github.com/HyperionGray/trio-websocket/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('eb0aabdc22a25585257473172336b3d9e1661f6a672541134e5d6de7020102cf')

build() {
    cd ${pkgname#*-}-${pkgver}
    python -m build --wheel --no-isolation
}

package() {
    cd ${pkgname#*-}-${pkgver}
    python -m installer --destdir="$pkgdir" dist/*.whl
}
