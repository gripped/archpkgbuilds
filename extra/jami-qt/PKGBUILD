# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=jami-qt
pkgver=20240823.0
pkgrel=1
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (Qt client)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(jami-daemon glib2 gdk-pixbuf2 libnm libnotify qrencode
         qt6-declarative qt6-multimedia qt6-svg qt6-5compat qt6-webengine
         qt6-webchannel qt6-shadertools)
makedepends=(git cmake python qt6-networkauth qt6-tools)
replaces=(jami-gnome jami-libclient)
conflicts=(jami-gnome jami-libclient)
_sfpmcommit=a2a0c72e4db38e1c5478bd3e0f67ff99fae11f00
_md4ccommit=ad8d41127b94e2f0633ad14b3787f0bc4613a689
_tidycommit=d08ddc2860aa95ba8e301343a30837f157977cba
source=(git+https://git.jami.net/savoirfairelinux/jami-client-qt.git#tag=stable/$pkgver
        git+https://github.com/atraczyk/SortFilterProxyModel#commit=${_sfpmcommit}
        git+https://github.com/mity/md4c#commit=${_md4ccommit}
        git+https://github.com/htacg/tidy-html5#commit=${_tidycommit}
        drop-qt-version-check.patch
        qt-6.6.patch)
sha256sums=('ab4b4a2b3d2a57afb8aca2adc8a883464d127ee1658f7d46175f1cb3b120143b'
            '47e134be55b75996e1c38deba860a4171fc54b45b01b193d14f9fc5af1db8ee6'
            'fd2217ffb1756b017439a032bf6c80bc13f6a0900287a9874870dcd314ca3614'
            'd756abef2fe8277cec1fe513e6af85362a173d9e046967494e68f76c2f6bbc60'
            'e64eb0e5abf1be8245aea7eb705659d225b0c711c286166e28541fc66532a220'
            '61d7ca804ed18650274f233cd60a811518859b4c6739ecc246414c35c4b8d906')

#pkgver() {
#  cd jami-client-qt
#  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
#}

prepare() {
  # Drop strict Qt version check. Qt is ABI stable and jami is not using any private API
  patch -p1 -d jami-client-qt < drop-qt-version-check.patch
  # Workaround for running with Qt 6.6 https://git.jami.net/savoirfairelinux/jami-client-qt/-/issues/1397
  patch -p1 -d jami-client-qt < qt-6.6.patch

  cd jami-client-qt
  rm -r 3rdparty/{SortFilterProxyModel,md4c,tidy-html5}
  mv "$srcdir"/{SortFilterProxyModel,md4c,tidy-html5} 3rdparty
}

build() {
  cmake -B build -S jami-client-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_LIBWRAP=ON \
    -DWITH_DAEMON_SUBMODULE=OFF \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
