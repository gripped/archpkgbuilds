# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=python-aiostream
_name=${pkgname#python-}
pkgver=0.5.2
pkgrel=2
pkgdesc='Generator-based operators for asynchronous iteration'
arch=('any')
url=https://github.com/vxgmichel/aiostream
license=('GPL3')
depends=('python-typing_extensions')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("git+$url.git#tag=v$pkgver")
b2sums=('42d1eaefde15c5fd7a0426921fd8e49a7c8192ec8b1c7fe826655f249b28206945356590bd18bfcb46c21ad2018fc33c65968c2afa67439023ad90ee140da486')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  # Override addopts as they invoke coverage testing
  PYTHONPATH="$PWD/$_name:$PYTHONPATH" pytest --override-ini="addopts=" tests --strict-markers
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
