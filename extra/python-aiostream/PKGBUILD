# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=python-aiostream
_name=${pkgname#python-}
pkgver=0.6.4
pkgrel=2
pkgdesc='Generator-based operators for asynchronous iteration'
arch=(any)
url=https://github.com/vxgmichel/aiostream
license=(GPL-3.0-only)
depends=(python-typing_extensions)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('dd46f7c5be5205bd8036020e2ae9a006488c9c8cff808515800fc1318b153cdfd2add7fe30837202ef780969f54ed28abc0f093ea3b596509b4879c245fef9da')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  # Override addopts as they invoke coverage testing
  PYTHONPATH="$PWD/$_name:$PYTHONPATH" pytest --override-ini="addopts=" tests --strict-markers
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
