# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-term-image
pkgver=0.7.2
pkgrel=4
pkgdesc='Display images in the terminal with Python'
arch=(any)
url='https://github.com/AnonymouX47/term-image'
license=(MIT)
depends=(
  python
  python-pillow
  python-requests
  python-typing_extensions
  python-urwid
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest libwebp)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('11b244ab554385a697d14734757a031acbbe0c424f7bc824f25716ef98404882830c5d322e80582e5f7d4abdfef3d268956ad09da189d515d689242d1e3990e7')
b2sums=('20935e58bf75783c4538722929cfea9656c103d0cadbb991786ee0ec247771155b42bd3008763c55bf8b141f1e832cb7388755917dc82ce6565917f60be4b486')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
