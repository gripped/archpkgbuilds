# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: George Woodall <georgewoodall82@gmail.com>

pkgname=tgpt
pkgver=2.9.1
pkgrel=1
pkgdesc="AI Chatbots in terminal without needing API keys"
arch=(x86_64)
url="https://github.com/aandrew-me/tgpt"
license=(GPL-3.0-or-later)
depends=(glibc)
makedepends=(go)
options=(!lto)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  "$pkgname-fix-fmt-typo.patch::$url/commit/4d8377123442a4550907c3de53a0b1ab4fa90388.patch"
)
sha256sums=('05c2d2009789679fe1d744474783a853abc79d3dad6d14871402ee933397fe00'
            '1b49dee6b37feebaa9d9ead9778dfa50ad96ca8c3e692367f2bd7536ea44f050')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../$pkgname-fix-fmt-typo.patch

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags="-compressdwarf=false -linkmode=external"
  go build -v -ldflags "$ld_flags"
}

check() {
  cd $pkgname-$pkgver
  # OpenGPT tests failing due to 'Response: Service Unavailable'.
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/aandrew-me/tgpt/v2/providers/opengpts
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" tgpt
}
