# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Stéphane Graber <stgraber AT ubuntu DOT com>

pkgname=distrobuilder
pkgver=3.1
pkgrel=1
pkgdesc='System container image builder for Incus and LXC'
arch=('x86_64')
url='https://linuxcontainers.org/distrobuilder/'
license=('Apache-2.0')
depends=(
  'rsync'
  'squashfs-tools'
  'gnupg'
  'debootstrap'
  'dosfstools'
  'gptfdisk'
  'qemu-img'
  'e2fsprogs'
  'btrfs-progs'
)
makedepends=('go' 'git')
optdepends=(
  'cdrtools: for repack-windows command'
  'hivex: for repack-windows command'
  'wimlib: for repack-windows command'
)
options=('!lto')
source=("https://linuxcontainers.org/downloads/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('5b0853a5f9a31b635f2d5160e918af8b7a66812570a1e50e70a515ef581bffe3904f6cf2487fa3d6691ed76ff34f97cd24b645b5f5ba146fec2127c3d9f21558'
            'SKIP')
b2sums=('2cb88f90c4459f71a2878824f7cb8f1d982f960ef498251561f58a57948d4dca35e67c85b15fff584db9ba369da07055d4854689f6d01a8f0f22dff97e813424'
        'SKIP')
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67') # Stéphane Graber <stgraber@stgraber.org>

prepare() {
  cd "$pkgname-$pkgver"

  # create folder for build output
  mkdir build
}

build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # verify modules
  go mod verify

  go build -v \
    -buildmode=pie \
    -mod=vendor \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./...
}

# failing due to /tmp issues
#check() {
#  cd "$pkgname-$pkgver"
#  go test -v ./...
#  go vet -v ./...
#}

package() {
  install -vDm755 -t "$pkgdir/usr/bin" "$pkgname-$pkgver/build/$pkgname"
}
