# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Stéphane Graber <stgraber AT ubuntu DOT com>

pkgname=distrobuilder
pkgver=3.2
pkgrel=1
pkgdesc='System container image builder for Incus and LXC'
arch=(x86_64)
url='https://linuxcontainers.org/distrobuilder/'
license=(Apache-2.0)
depends=(
  rsync
  squashfs-tools
  gnupg
  debootstrap
  dosfstools
  gptfdisk
  qemu-img
  e2fsprogs
  btrfs-progs
)
makedepends=(go git)
optdepends=(
  'cdrtools: for repack-windows command'
  'hivex: for repack-windows command'
  'wimlib: for repack-windows command'
)
source=("https://linuxcontainers.org/downloads/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('9ed62c8c7307d0df4f06d8cf77e936cc4a2c6e7c058441abf675ac86bb387a0fc8e8598ff03df0776b549092ebc8e64e766d32122f79f0e2f8ec9ee595493383'
            'SKIP')
b2sums=('a46048800419d9a6f71957472db984d27f33d2fdb9ae47e758495167673faf02ec0cc384dba83c08a0acf86719121157daaf77b6ddf4722b59aade7391ca0c03'
        'SKIP')
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67') # Stéphane Graber <stgraber@stgraber.org>

prepare() {
  cd "$pkgname-$pkgver"

  # create folder for build output
  mkdir build
}

build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # verify modules
  go mod verify

  go build -v \
    -buildmode=pie \
    -mod=vendor \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./...
}

# failing due to /tmp issues
#check() {
#  cd "$pkgname-$pkgver"
#  go test -v ./...
#  go vet -v ./...
#}

package() {
  install -vDm755 -t "$pkgdir/usr/bin" "$pkgname-$pkgver/build/$pkgname"
}
