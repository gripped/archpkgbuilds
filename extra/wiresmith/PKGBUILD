# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=wiresmith
pkgver=0.4.4
pkgrel=2
pkgdesc="Auto-config WireGuard clients into a mesh"
arch=('x86_64')
url="https://github.com/svenstaro/wiresmith"
license=('MIT')
depends=('libgcc' 'glibc')
makedepends=('rust')
options=('!lto')
source=($pkgname-$pkgver.tar.gz::https://github.com/svenstaro/wiresmith/archive/v${pkgver}.tar.gz)
sha512sums=('41d62fc4770c9d27b71efd40aa07777dedb8fabf1a991953d3666f3c6b0cd5e16dc48638c9c89842461a7978ec9438de2ab50032753c4cc0d90825278cc897ac')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/wiresmith "$pkgdir"/usr/bin/wiresmith
  install -Dm644 packaging/wiresmith.service "$pkgdir"/usr/lib/systemd/system/wiresmith.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
