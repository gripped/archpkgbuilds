# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=wiresmith
pkgver=0.4.1
pkgrel=1
pkgdesc="Auto-config WireGuard clients into a mesh"
arch=('x86_64')
url="https://github.com/svenstaro/wiresmith"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('rust')
options=('!lto')
source=($pkgname-$pkgver.tar.gz::https://github.com/svenstaro/wiresmith/archive/v${pkgver}.tar.gz)
sha512sums=('7a1718389402ecb8012ab0b93a915cd54b106e76d909611245b9a32780394137a9f0130a7634cb37f43b0123212fc9937a93fbb452055c73f4d70ef56fe49811')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/wiresmith "$pkgdir"/usr/bin/wiresmith
  install -Dm644 packaging/wiresmith.service "$pkgdir"/usr/lib/systemd/system/wiresmith.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
