# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=wiresmith
pkgver=0.3.0
pkgrel=1
pkgdesc="Auto-config WireGuard clients into a mesh"
arch=('x86_64')
url="https://github.com/svenstaro/wiresmith"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
options=('!lto')
source=($pkgname-$pkgver.tar.gz::https://github.com/svenstaro/wiresmith/archive/v${pkgver}.tar.gz)
sha512sums=('448d701943b45d8d3bf62477532f14115de877bc07e0bfc3dbdf8c1a83c51a05fcd315a2b26c9703401e324269d0b05c6ed2035d334a75aa39921798e9e1738e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/wiresmith "$pkgdir"/usr/bin/wiresmith
  install -Dm644 packaging/wiresmith.service "$pkgdir"/usr/lib/systemd/system/wiresmith.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
