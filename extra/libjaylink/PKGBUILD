# Maintainer: Felix Yan <felixonmars at archlinux dot org>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: glatavento <glatavento at outlook dot com>

pkgname=libjaylink
pkgver=0.4.0
pkgrel=1
pkgdesc='Library for accessing SEGGER J-Link and compatible devices'
arch=('x86_64')
url='https://gitlab.zapb.de/libjaylink/libjaylink'
license=('GPL')
depends=('libusb')
provides=("$pkgname.so")
makedepends=('git' 'meson')
source=("git+https://gitlab.zapb.de/libjaylink/libjaylink.git#tag=$pkgver")
sha256sums=('01083f298381c3d0f7031cdca1c47885038f2142a484073f554e4b0309b0b7a2')

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
