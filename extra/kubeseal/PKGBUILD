# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: sseneca <me at ssene dot ca>
# Contributor: ml <ml@visu.li>

pkgname=kubeseal
_pkgname=sealed-secrets
pkgver=0.32.2
pkgrel=1
pkgdesc="A Kubernetes controller and tool for one-way encrypted Secrets"
arch=('x86_64')
url="https://github.com/bitnami-labs/sealed-secrets"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9ce7ce11bd3f82c3aa02e1e90130c8cd6812d84997c16c7b7d08beb1e99dc8c4a5b306f3baf8dfe68f609d9f54fa3790978d2d7c7b91840f49d4a9365c3aa871')
options=('!lto')

build() {
  cd "${_pkgname}-${pkgver}/cmd/kubeseal"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  go build -ldflags="-linkmode=external -X=main.VERSION=v${pkgver}" .
}

check() {
  cd "${_pkgname}-${pkgver}"

  go test ./cmd/kubeseal/... ./pkg/...
}

package() {
  cd "${_pkgname}-${pkgver}/cmd/kubeseal/"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
}
