# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-stroke
pkgver=0.0.0.20250920
_commit=a4dc0423c578bf5e15bea66103e082ee187a8737
pkgrel=1
pkgdesc="Stroke input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-stroke"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-luna-pinyin')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-stroke.git#commit=$_commit")
sha512sums=('8ef2ebe9bbe84dca6ecd9d32f5fdfb41d15941defa8b70f0778a6bd224a1d37039455f94409b3e5493998c70b6761642fe190523381aaab49229eb77de1309ae')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
