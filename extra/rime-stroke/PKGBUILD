# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-stroke
pkgver=0.0.0.20250118
_commit=7c9874c6b2e0b94947653e9a7de6f99623ff27e4
pkgrel=1
pkgdesc="Stroke input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-stroke"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-luna-pinyin')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-stroke.git#commit=$_commit")
sha512sums=('bd1a987e1a07e4b8e9287c3c61c3faa717b944ebe7fbfb13b1f5b46463b3d784bbac51ed93f5238be7bc2d73d1061d856d57c576aff0aff1cb2a8437ca571be0')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
