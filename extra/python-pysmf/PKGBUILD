# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-pysmf
pkgver=0.1.2
pkgrel=1
pkgdesc='Python module for reading & writing Standard MIDI Files'
arch=(x86_64)
url='https://github.com/mididings/pysmf'
license=(BSD-2-Clause)
depends=(
  python
  libsmf
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
  cython
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('270b9b04bdeb9eba21d56011d4c61ad10292434c3f838d70b1c12e7988daee2a610dafac35f984ae35c7cdf9c631f655498c0f97dd7326fe5bea3e8b14909780')
b2sums=('0a776d8f3f6c67492791d2b04483c3396ae44d6c81f703032a5d259e69645a73991da1ffe82cc5858d15d9b98541bd3c9fdb1af00d2c0b002ac3b172f73a1255')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" NEWS README.md
  cp -vr examples "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
