# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=systing
pkgver=r485.3a4e5bb
pkgrel=1
pkgdesc="A libbpf based tracer to help figure out what an application is doing"
arch=(x86_64)
url="https://github.com/josefbacik/systing"
depends=(
  bpf
  libelf
  libbpf
  gcc-libs
  glibc
  fmt
  libcap
  re2
  zlib
)
makedepends=(
  clang
  git
  rust
  linux
)
license=(MIT)
_commit="3a4e5bb2defc754d55f04474ad30615c41ac9831"
source=(
  "${pkgname}::git+https://github.com/josefbacik/systing.git#commit=$_commit"
  "${pkgname}-strobelight-libs::git+https://github.com/facebookincubator/strobelight-libs.git"
)
sha512sums=('04e5aa33dedbe5cb05e04c69de4b336f46ab68ebe279e9a547ef74baa50749105fc4205b4b8c631c14a629e64f4195b027246e620e18ccdda156924302062a25'
            'SKIP')
b2sums=('14390bdfcb3819d03ac2e1c31bf79bd2270a5910e2af5d3d5f8469d93243ce422c1f94a1951417247a558ffc0e251cec32a1c1b617a9726fae7564abe1958739'
        'SKIP')

# needed to workaround linking error with BPF lib
options=(!lto)

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd $pkgname

  git submodule init
  git config submodule."strobelight-libs".url "${srcdir}/${pkgname}"-strobelight-libs
  git -c protocol.file.allow=always submodule update --init

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname

  cargo build --frozen --release
}

check() {
  cd $pkgname

  cargo test -p $pkgname --frozen
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
