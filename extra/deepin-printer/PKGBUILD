# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-printer
pkgver=0.9.21
pkgrel=1
pkgdesc='Printer configuration project for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-printer"
license=('GPL3')
# smbclient: dlopen
depends=('cups' 'dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'libusb' 'qt5-base'
         'hicolor-icon-theme' 'xdg-utils' 'smbclient')
makedepends=('git' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/dde-printer.git#tag=$pkgver")
sha512sums=('bb71e2a87391b20464024a6612f7b081f92ff8945c309cec3a927a25db4f5dfdb74d10dbd9b7bd2ab74ae2ba03a7e05b2203ad37a8f12d64e9c3e50b2737438a')

prepare() {
  cd dde-printer
  sed -i '$aOnlyShowIn=Deepin' src/Printer/platform/linux/dde-printer.desktop
}

build() {
  cd dde-printer
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-printer
  make INSTALL_ROOT="$pkgdir" install
}
