# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Campbell Jones <serebit at archlinux dot org>

pkgname=budgie-desktop-view
pkgver=1.3
pkgrel=3
pkgdesc='Desktop icons implementation for the Budgie Desktop'
arch=('x86_64')
url='https://github.com/BuddiesOfBudgie/budgie-desktop-view'
license=('Apache-2.0')
groups=('budgie')
depends=('gtk3')
makedepends=('intltool' 'meson' 'vala')
source=("https://github.com/BuddiesOfBudgie/budgie-desktop-view/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc}
        'budgie-desktop-view-fix-build.patch::https://github.com/BuddiesOfBudgie/budgie-desktop-view/commit/05a9822a.patch')
validpgpkeys=('96B4A0291094A86A2B7E3367DD672FE9A2BE5892'
              '0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl (Personal)
b2sums=('b3d4b4bfb96b82b4cf6d957e4718600d6ce8bd09bb86e6df76b353e60bd265f18628a2f37fc38395d62319d2e9c75e2cce7e22e6ba731deb6741c83a163a444b'
        'SKIP'
        '8fd7114539f67b8eaaa63f4b6c1d8ce9844ae4948845153703245f5f6d6cc37dfc8f52c52413d8c45459ef4bd7917110d70d2971b25112736e379c3a5b661c24')

prepare() {
  cd $pkgname-$pkgver

  # Fix build
  patch -Np1 -i ../budgie-desktop-view-fix-build.patch
}

build() {
    arch-meson "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
