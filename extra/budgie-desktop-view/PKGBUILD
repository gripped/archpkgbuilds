# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop-view
pkgver=10.10.1
pkgrel=1
pkgdesc='Desktop icons implementation for the Budgie Desktop'
arch=(x86_64)
url='https://github.com/BuddiesOfBudgie/budgie-desktop-view'
license=(Apache-2.0)
groups=(budgie)
depends=(
	dconf
	gdk-pixbuf2
	glib2
	glibc
	gtk3
	gtk-layer-shell
	libxfce4windowing
)
makedepends=(
	git
	intltool
	meson
	vala
)
source=("git+https://github.com/BuddiesOfBudgie/budgie-desktop-view.git#tag=v$pkgver")
b2sums=('64e2cf3093515e7c36ff5e298cc6d6891cd819766d4b860ebf3eef64055f98ba4c7351da646f46116d6b8ba52658b54fba2bfafc7c9e1638579cb5001020375c')
validpgpkeys=(
  96B4A0291094A86A2B7E3367DD672FE9A2BE5892
  0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B # Joshua Strobl
)

build() {
	arch-meson $pkgname build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
