# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop-view
pkgver=1.3
pkgrel=5
pkgdesc='Desktop icons implementation for the Budgie Desktop'
arch=(x86_64)
url='https://github.com/BuddiesOfBudgie/budgie-desktop-view'
license=(Apache-2.0)
groups=(budgie)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
)
makedepends=(
  git
  intltool
  meson
  vala
)
source=("git+https://github.com/BuddiesOfBudgie/budgie-desktop-view.git#tag=v$pkgver")
b2sums=(595ca31a143b723de0d15c7457820cc892f94059fe3849c4c03472c13a3d39c342e5d8ce70b5fd77ea6cf2d147776267970d4248144ade10cc77baf619ae3d06)
validpgpkeys=(
  96B4A0291094A86A2B7E3367DD672FE9A2BE5892
  0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B # Joshua Strobl
)

prepare() {
  cd $pkgname

  # Fix build
  git cherry-pick -n 05a9822a465a023968efb28c620e9448c82634db

  # Add MATE Terminal and GNOME Console to supported terminals
  # https://github.com/BuddiesOfBudgie/budgie-desktop-view/pull/2
  git cherry-pick -n 14a62f08fae7855118769a27dd27a92896dbce66
  # https://github.com/BuddiesOfBudgie/budgie-desktop-view/pull/35
  git cherry-pick -n c2a95e0f1e8fce170d90ff8d556f14612c6eacd3
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
