# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=ssh-tpm-agent
pkgver=0.7.0
pkgrel=1
pkgdesc="ssh-agent for TPMs"
arch=("x86_64")
url="https://github.com/Foxboron/ssh-tpm-agent"
license=("MIT")
depends=("glibc")
makedepends=("go" "git" "systemd")
source=("$pkgname-$pkgver.tar.gz::https://github.com/Foxboron/$pkgname/archive/v${pkgver}.tar.gz")
sha256sums=('8ed282f4fbac9240248f6311054a40a7d8f483fe3f3dbe67f32ca8ae2b248231')

build(){
    cd "${pkgname}-${pkgver}"
    export GOFLAGS="-buildmode=pie -modcacherw"
    make
}

package(){
    cd "${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" PREFIX="/usr" install
}
