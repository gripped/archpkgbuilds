# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=ssh-tpm-agent
pkgver=0.3.0
pkgrel=1
pkgdesc="ssh-agent for TPMs"
arch=("x86_64")
url="https://github.com/Foxboron/ssh-tpm-agent"
license=("MIT")
depends=("glibc")
makedepends=("go" "git" "systemd")
source=("$pkgname-$pkgver.tar.gz::https://github.com/Foxboron/$pkgname/archive/v${pkgver}.tar.gz")
sha256sums=('a8aa46d433b370b217ae7fb9ec0873cf39d0e1b15de88dc7204406b008bbb218')

build(){
    cd "${pkgname}-${pkgver}"
    export GOFLAGS="-buildmode=pie -modcacherw"
    make
}

package(){
    cd "${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" PREFIX="/usr" install
}
