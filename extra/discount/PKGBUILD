# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Hyacinthe Cartiaux <hyacinthe.cartiaux at free.fr>
# Contributor: Bartlomiej Piotrowski <nospam at bpiotrowski dot pl>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=discount
pkgver=2.2.7.d
_pkgver=2.2.7d
pkgrel=1
pkgdesc='A Markdown implementation written in C'
arch=(x86_64)
url='https://www.pell.portland.or.us/~orc/Code/discount/'
license=('custom:BSD')
source=(https://github.com/Orc/discount/archive/v$_pkgver/$pkgname-$pkgver.tar.gz
        discount-ldflags.patch::https://github.com/Orc/discount/commit/55cea9c5.patch)
sha256sums=('eb9f6629dadddb44c3111267b82e51d5fb8364e228cc3533fe937d8a99e0155b'
            '7eced2472fdbe25badc13c35dfd6920b68b5a208889ee031ef0c060c093cc89b')
depends=(glibc)

prepare() {
  patch -d $pkgname-$_pkgver -p1 -i ../discount-ldflags.patch # Honor system ldflags
}

build() {
  cd $pkgname-$_pkgver
  ./configure.sh \
    --prefix=/usr \
    --enable-all-features \
    --with-fenced-code \
    --github-checkbox \
    --shared
  make
}

package() {
  cd $pkgname-$_pkgver
  # fix ldconfig call
  sed -e 's|/sbin/ldconfig|/sbin/ldconfig -n|' -i librarian.sh

  make DESTDIR="$pkgdir" install.everything
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
