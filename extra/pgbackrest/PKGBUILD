# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Shalygin Konstantin <k0ste@k0ste.ru>
# Contributor: xiretza <xiretza+aur@xiretza.xyz>

pkgname=pgbackrest
pkgver=2.56.0
pkgrel=1
pkgdesc='Reliable PostgreSQL backup and restore'
arch=(x86_64)
url="https://github.com/pgbackrest/pgbackrest"
license=(MIT)
depends=(bzip2 glibc libssh2 libxml2 lz4 openssl postgresql-libs zlib zstd)
makedepends=(libyaml meson)
source=("$pkgname-$pkgver.tar.gz::$url/archive/release/$pkgver.tar.gz")
b2sums=('c388049769a40742520769820c707bd11728f77ec11fa1cdb297cbb4163910390ebf62ea4fbf0fd6b0ef8fbf4e85e1ea97ebf125f8e3c947cbce7a0e922487f9')
backup=(etc/$pkgname/$pkgname.conf)

prepare() {
  echo '# Placeholder config. See https://pgbackrest.org/configuration.html for more info.' > example.conf
}

build() {
  arch-meson $pkgname-release-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 example.conf "$pkgdir/etc/$pkgname/$pkgname.conf"
  install -Dm644 $pkgname-release-$pkgver/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
