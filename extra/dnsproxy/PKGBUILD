# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Bin Jin <bjin@protonmail.com>
# Contributor: Whyme Lyu <callme5long@gmail.com>

pkgname=dnsproxy
pkgver=0.76.1
pkgrel=2
pkgdesc="Simple DNS proxy with DoH, DoT, DoQ and DNSCrypt support"
url="https://github.com/AdguardTeam/dnsproxy"
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'go'
)
backup=(etc/dnsproxy/dnsproxy.yaml)
source=("https://github.com/AdguardTeam/dnsproxy/archive/v${pkgver}/dnsproxy-${pkgver}.tar.gz"
        dnsproxy.service)
sha256sums=('e11c0b67aa8784a9d1e8baf2724fe438dfe06a12f928eba8f1530b7aeb47f7db'
            '002deb38e7d69beb8848c57a7ba0d00437c542589baccc6dab074767ffa64b75')
b2sums=('60fd1a95b70cf2701dc129d814ec5a14789c42d9f3c5f45ca5fb8c8504558863f0d7527fc89ab82e97235719fae5be905a415f29c24ca00720ec80ecb016ade2'
        '6cea74df1b6bd914e7845635a5a4b72bd6bd71f8135d900a744bc156dd256e97b9e179e47958cf135cd77726ce2a22a19c71343f49dfc1f34ba8958e3a76e5e8')

build() {
  cd "${pkgname}-${pkgver}"

  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X github.com/AdguardTeam/dnsproxy/internal/version.version=${pkgver}"
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dnsproxy -t "${pkgdir}/usr/bin"
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/dnsproxy"
  install -Dm644 config.yaml.dist "${pkgdir}/etc/dnsproxy/dnsproxy.yaml"
  install -Dm644 ../dnsproxy.service -t "${pkgdir}/usr/lib/systemd/system"
}

# vim: ts=2 sw=2 et:
