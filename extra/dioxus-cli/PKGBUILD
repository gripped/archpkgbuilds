# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Adrian Groh <adrian[dot]groh[at]t[dash]online[dot]de>

pkgname=dioxus-cli
pkgver=0.7.3
pkgrel=1
pkgdesc="CLI tool for developing, testing, and publishing Dioxus apps"
url="https://github.com/DioxusLabs/dioxus"
license=('MIT' 'Apache-2.0')
arch=("x86_64")
depends=('gcc-libs' 'bzip2' 'zlib' 'openssl' 'xz')
makedepends=('cargo')
provides=('dx')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('af0f7735763059749540b547bbb7ad6dbb89314a0966955ad7973fa3caeb95aa11ac7ad12f8bcdcd03f62c2742a40d170ceb6616cbe8aa12665e17ccd94afc5e')
options=('!lto')

prepare() {
  cd "${pkgname%-cli}-$pkgver"
  cargo fetch --target "$(rustc --print host-tuple)" # --locked
}

build() {
  cd "${pkgname%-cli}-$pkgver"
  cargo build -p "$pkgname" --release --frozen
}

check() {
  cd "${pkgname%-cli}-$pkgver"
  cargo test -p "$pkgname" --frozen -- --skip "test_harnesses::run_harness"
}

package() {
  cd "${pkgname%-cli}-$pkgver"
  install -Dm 755 "target/release/dx" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
