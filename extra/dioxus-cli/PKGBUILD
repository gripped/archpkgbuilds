# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Adrian Groh <adrian[dot]groh[at]t[dash]online[dot]de>

pkgname=dioxus-cli
pkgver=0.7.2
pkgrel=1
pkgdesc="CLI tool for developing, testing, and publishing Dioxus apps"
url="https://github.com/DioxusLabs/dioxus"
license=('MIT' 'Apache-2.0')
arch=("x86_64")
depends=('gcc-libs' 'bzip2' 'zlib' 'openssl' 'xz')
makedepends=('cargo')
provides=('dx')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('043493d7a3fb28779618231229a42a211ff9802e06d297e7fbf4908f9fa53335f040a3de000035188bb425d1f2531e162373a4cbfbc16447731771f54d7becec')
options=('!lto')

prepare() {
  cd "${pkgname%-cli}-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" # --locked
}

build() {
  cd "${pkgname%-cli}-$pkgver"
  cargo build -p "$pkgname" --release --frozen
}

check() {
  cd "${pkgname%-cli}-$pkgver"
  cargo test -p "$pkgname" --frozen -- --skip "test_harnesses::run_harness"
}

package() {
  cd "${pkgname%-cli}-$pkgver"
  install -Dm 755 "target/release/dx" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
