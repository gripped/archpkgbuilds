# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Adrian Groh <adrian[dot]groh[at]t[dash]online[dot]de>

pkgname=dioxus-cli
pkgver=0.7.0
pkgrel=1
pkgdesc="CLI tool for developing, testing, and publishing Dioxus apps"
url="https://github.com/DioxusLabs/dioxus"
license=('MIT' 'Apache-2.0')
arch=("x86_64")
depends=('gcc-libs' 'bzip2' 'zlib' 'openssl' 'xz')
makedepends=('cargo')
provides=('dx')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('20aa43abe0ea23957b2a55a614907ee53d5c1cd7fc96ec33aed5b23f017ffc547794f76d8536c963f0d3cd3a05c008d2ab73f11be18932bc1e1334e808872ef2')
options=('!lto')

prepare() {
  cd "${pkgname%-cli}-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" # --locked
}

build() {
  cd "${pkgname%-cli}-$pkgver"
  cargo build -p "$pkgname" --release --frozen
}

check() {
  cd "${pkgname%-cli}-$pkgver"
  cargo test -p "$pkgname" --frozen -- --skip "test_harnesses::run_harness"
}

package() {
  cd "${pkgname%-cli}-$pkgver"
  install -Dm 755 "target/release/dx" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
