# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Adrian Groh <adrian[dot]groh[at]t[dash]online[dot]de>

pkgname=dioxus-cli
pkgver=0.6.3
pkgrel=1
pkgdesc="CLI tool for developing, testing, and publishing Dioxus apps"
url="https://github.com/DioxusLabs/dioxus"
license=('MIT' 'Apache-2.0')
arch=("x86_64")
depends=('gcc-libs' 'bzip2' 'zlib' 'openssl' 'xz')
makedepends=('cargo')
provides=('dx')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('f95e8819aaeb801ffe084c3dd83f831fd772a4ef3da145e3480465abee0bf340301a7156cba0b7e18f3b0c605d6ba268740c2acdfb9330439e0feb3e99e0d6ca')
options=('!lto')

prepare() {
  cd "${pkgname%-cli}-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" # --locked
}

build() {
  cd "${pkgname%-cli}-$pkgver"
  cargo build -p "$pkgname" --release --frozen
}

check() {
  cd "${pkgname%-cli}-$pkgver"
  cargo test -p "$pkgname" --frozen
}

package() {
  cd "${pkgname%-cli}-$pkgver"
  install -Dm 755 "target/release/dx" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
