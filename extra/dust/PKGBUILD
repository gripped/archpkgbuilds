# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=dust
pkgver=1.2.4
pkgrel=1
pkgdesc='A more intuitive version of du in rust'
arch=(x86_64)
url='https://github.com/bootandy/dust'
license=(Apache-2.0)
depends=(gcc-libs glibc)
makedepends=(rust git)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0a6dcf9ba0668afbeb39dfe288ece1723d0caa6012511d17c93513ddd1edfb6242f743bec39e9419c9780c7961c51c54614490cca36739254b4fb45dbccea344')
b2sums=('648fff66583dc0a6c6e0d555615fa40d5ff395ba8699dd13c8254b1e0e4bfb1740b51ae7bb00615948b6363cb00e1bac3353a2fb3c3a6ffc92d81dab8d4051d3')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # shell completions
  install -vDm644 "completions/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 -t "$pkgdir/usr/share/elvish/lib/" "completions/$pkgname.elv"
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "completions/$pkgname.fish"
  install -vDm644 -t "$pkgdir/usr/share/zsh/site-functions/" "completions/_$pkgname"

  # manpage
  install -vDm644 -t "$pkgdir/usr/share/man/man1/" man-page/$pkgname.1
}

# vim:set ts=2 sw=2 et:
