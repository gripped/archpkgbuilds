# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=dust
pkgver=0.9.0
pkgrel=2
pkgdesc='A more intuitive version of du in rust'
arch=('x86_64')
url='https://github.com/bootandy/dust'
license=('Apache-2.0')
depends=('gcc-libs' 'glibc')
makedepends=('cargo' 'git')
source=("$pkgname::git+$url#tag=v$pkgver")
sha256sums=('a7abd4d9f10beb72dbb6b45ca7fe7be1d3bc7cf276d9cf71c0eb4d7e96e63538')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim:set ts=2 sw=2 et:
