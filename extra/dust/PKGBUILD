# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=dust
pkgver=1.1.0
pkgrel=1
pkgdesc='A more intuitive version of du in rust'
arch=('x86_64')
url='https://github.com/bootandy/dust'
license=('Apache-2.0')
depends=('gcc-libs' 'glibc')
makedepends=('cargo' 'git')
source=("$pkgname::git+$url#tag=v$pkgver")
sha256sums=('e330374b1f572e21cf0feaba1f2370360e39b1f17f4f4898366f1d84f8c774a0')

prepare() {
  cd "$pkgname"

  # Upstream lockfile out of sync, https://github.com/bootandy/dust/issues/418
  sed -i -e '306s/1.0.0/1.1.0/' Cargo.lock
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # shell completions
  install -vDm644 "completions/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 -t "$pkgdir/usr/share/elvish/lib/" "completions/$pkgname.elv"
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "completions/$pkgname.fish"
  install -vDm644 -t "$pkgdir/usr/share/zsh/site-functions/" "completions/_$pkgname"

  # manpage
  install -vDm644 -t "$pkgdir/usr/share/man/man1/" man-page/$pkgname.1
}

# vim:set ts=2 sw=2 et:
