# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: JakobDev <jakobdev at gmx dot de>
# Contributor: entriphy <t4ils.dev@gmail.com>

pkgname=python-pillow-avif-plugin
_pkgname=pillow-avif-plugin
pkgver=1.5.2
pkgrel=1
pkgdesc="A Python Pillow plugin that adds AVIF support via libavif"
url="https://github.com/fdintino/pillow-avif-plugin"
arch=('x86_64')
license=('BSD-2-Clause')
depends=('python' 'python-pillow' 'libavif')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-pytest' 'python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('ca224a3ba77cc2ccc5a4e3a7e081c2c0914ea1481fdeb4c4c007e04d8675c5fe')

build() {
	cd "${_pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
