# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgbase=gdcm
pkgname=(gdcm gdcm-docs)
pkgver=3.2.2
pkgrel=2
pkgdesc="C++ library for DICOM medical files"
arch=(x86_64)
url="https://github.com/malaterre/GDCM"
license=(BSD-3-Clause)
makedepends=(
  cmake
  ninja
  python
  swig
  doxygen
  graphviz
  # depends
  expat
  json-c
  libxml2
  openjpeg2
  openssl
  util-linux-libs
  zlib
)
source=($pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
b2sums=('28740451af675b400cee28e9939577a0ad05b952a8b058fec91b0975dd33bc2ada27f24ea380acd01d9014a41c13ed9bfd016a287d4ed8233ab76bd54ceca05e')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local cmake_options=(
    -B build
    -S ${pkgbase^^}-$pkgver
    -G Ninja
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DGDCM_BUILD_APPLICATIONS=ON
    -DGDCM_BUILD_SHARED_LIBS=ON
    -DGDCM_BUILD_TESTING=ON
    -DGDCM_DOCUMENTATION=ON
    -DGDCM_PDF_DOCUMENTATION=OFF  # does not work - latex error Bad register code (32866).
    -DGDCM_USE_SYSTEM_CHARLS=OFF  # not packaged
    -DGDCM_USE_SYSTEM_EXPAT=ON
    -DGDCM_USE_SYSTEM_JSON=ON
    -DGDCM_USE_SYSTEM_LIBXML2=ON
    -DGDCM_USE_SYSTEM_LJPEG=OFF   # not packaged
    -DGDCM_USE_SYSTEM_OPENJPEG=ON
    -DGDCM_USE_SYSTEM_OPENSSL=ON
    -DGDCM_USE_SYSTEM_PAPYRUS3=OFF  # not packaged
    -DGDCM_USE_SYSTEM_POPPLER=OFF   # not compatible with the Arch version
    -DGDCM_USE_SYSTEM_SOCKETXX=OFF  # not packaged
    -DGDCM_USE_SYSTEM_UUID=ON
    -DGDCM_USE_SYSTEM_ZLIB=ON
    -DGDCM_USE_VTK=OFF
    -DGDCM_WRAP_PYTHON=ON
    -DGDCM_INSTALL_PYTHONMODULE_DIR="$site_packages"
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  local excluded_tests=""
  excluded_tests+="|TestTransferSyntax" #(Failed)
  excluded_tests+="|TestUIDGeneratorPython" #(Failed)
  excluded_tests+="|TestSCUValidation" #(Failed)
  excluded_tests+="|TestEcho" #(Subprocess aborted)
  excluded_tests+="|TestFind" #(Subprocess aborted)
  excluded_tests+="|TestWrapPython" #(Failed)
  excluded_tests+="|gdcmscu-echo-dicomserver" #(Subprocess aborted)
  excluded_tests+="|gdcmscu-find-dicomserver" #(Subprocess aborted)
  ctest --test-dir build --output-on-failure --exclude-regex "$excluded_tests"
}

package_gdcm() {
  depends=(
    expat libexpat.so
    gcc-libs
    glibc
    json-c libjson-c.so
    libxml2 libxml2.so
    openjpeg2
    openssl libcrypto.so
    util-linux-libs libuuid.so
    zlib libz.so
  )
  optdepends=(
    'gdcm-docs: GDCM documentation'
    'python: Python bindings'
  )
  provides=(
    libgdcmCommon.so
    libgdcmDICT.so
    libgdcmDSED.so
    libgdcmIOD.so
    libgdcmMEXD.so
    libgdcmMSFF.so
  )

  DESTDIR="$pkgdir" cmake --install build
  (
    cd "$pkgdir"
    _pick $pkgbase-docs usr/share/doc
  )
  install -vDm 644 ${pkgbase^^}-$pkgver/Copyright.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_gdcm-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 ${pkgbase^^}-$pkgver/Copyright.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
