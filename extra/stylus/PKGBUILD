# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=stylus
pkgdesc='Robust, expressive, and feature-rich CSS superset'
pkgver=0.61.0
pkgrel=1
arch=('any')
url='https://github.com/stylus/stylus'
license=('MIT')
depends=('nodejs')
makedepends=('npm' 'git')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('a1a57d4f8b1106241f0a15c4d1abfd4ab2c3fa8bc4750896ccf27993021e62f3218d40c99d9b5db9b006ea54926da478b029e84fab30e35d483a5e4091c86de1')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
