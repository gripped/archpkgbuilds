# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=vpl-gpu-rt
pkgver=25.1.4
pkgrel=1
pkgdesc='Intel VPL runtime implementation for Intel GPUs (Tiger Lake and newer)'
arch=('x86_64')
url='https://github.com/intel/vpl-gpu-rt/'
license=('MIT')
depends=('intel-media-driver' 'libdrm' 'libva')
makedepends=('cmake')
provides=('onevpl-intel-gpu' 'vpl-runtime')
conflicts=('onevpl-intel-gpu')
replaces=('onevpl-intel-gpu')
source=("https://github.com/intel/vpl-gpu-rt/archive/intel-onevpl-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-vpl-gpu-rt-disable-verbose-makefile.patch')
sha256sums=('2ee59a47c281f402cd77118eed1ec7cef703d25432e9a71b4be6b91e8c304d72'
            'c6b17b7026d16d02c3cab7b5081366c24021a86fa173eaee5db310691ce76879')

prepare() {
    patch -d "vpl-gpu-rt-intel-onevpl-${pkgver}" -Np1 -i "${srcdir}/010-vpl-gpu-rt-disable-verbose-makefile.patch"
}

build() {
    export CFLAGS+=' -DNDEBUG'
    export CXXFLAGS+=' -DNDEBUG'
    cmake -B build -S "vpl-gpu-rt-intel-onevpl-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_TESTS:BOOL='OFF' \
        -DMFX_ENABLE_AENC:BOOL='ON' \
        -Wno-dev
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -D -m644 "vpl-gpu-rt-intel-onevpl-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
