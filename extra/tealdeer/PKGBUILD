# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Danilo Bargen <aur at dbrgn dot ch>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=tealdeer
pkgver=1.7.1
pkgrel=1
pkgdesc='A fast tldr client in Rust'
arch=('x86_64')
url='https://github.com/tealdeer-rs/tealdeer'
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('git' 'rust')
provides=('tldr')
conflicts=('tldr')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('f5164777a8c7c4a932533fc20250510890cee9ba3c58a10654962a4515cd0263d4326779ecac7cf9168a92e0b7c9a329696e69ceaf3f3f23053dd20554b59684')
b2sums=('a19cadea9e6d40b232670b1ab170d241f95cb565f272ebf10784ffa313f6c19c67c6ec34da51fbfd37126540830381d26cfb1eb04224ac298c4e4d3b1024e57f')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen
}

check() {
  cd "$pkgname"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/tldr"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  cd completion
  install -vDm644 bash_tealdeer "$pkgdir/usr/share/bash-completion/completions/tldr"
  install -vDm644 fish_tealdeer "$pkgdir/usr/share/fish/vendor_completions.d/tldr.fish"
  install -vDm644 zsh_tealdeer "$pkgdir/usr/share/zsh/site-functions/_tldr"
}

# vim:set ts=2 sw=2 et:
