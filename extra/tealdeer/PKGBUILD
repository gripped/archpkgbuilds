# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Danilo Bargen <aur at dbrgn dot ch>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=tealdeer
pkgver=1.7.0
pkgrel=1
pkgdesc='A fast tldr client in Rust'
arch=('x86_64')
url='https://github.com/tealdeer-rs/tealdeer'
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('git' 'rust')
provides=('tldr')
conflicts=('tldr')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('5b595b0392fdea64220866642a78349800e7950d20e731234974f0840a4d471b362716a93f068b0a271e365beca39cc15c2f24490056301b6292601897e94f1b')
b2sums=('de35cc360d73737e2edcd549da6a6cec6f9c3d099e58f0d23424c210a4348d72ec2337ba8f49ee41ab388fb3dda3ccac2b78ab7b527dad0c6b8ba300004e0a58')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen
}

check() {
  cd "$pkgname"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/tldr"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  cd completion
  install -vDm644 bash_tealdeer "$pkgdir/usr/share/bash-completion/completions/tldr"
  install -vDm644 fish_tealdeer "$pkgdir/usr/share/fish/vendor_completions.d/tldr.fish"
  install -vDm644 zsh_tealdeer "$pkgdir/usr/share/zsh/site-functions/_tldr"
}

# vim:set ts=2 sw=2 et:
