# Maintainer: Alad Wenter <alad@archlinux.org>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Lorenzo Nizzi Grifi Gargiolli <lorenzo.nizzi.grifi@gmail.com>
# Contributor: marc0s <marc0s@fsfe.org>

pkgname=pcsc-tools
pkgver=1.7.4
pkgrel=1
pkgdesc='PC/SC Architecture smartcard tools'
arch=('x86_64')
url='https://pcsc-tools.apdu.fr/'
license=('GPL')
depends=('pcsclite' 'pcsc-perl' 'glib-perl')
makedepends=('pkg-config')
optdepends=('perl-gtk3: gscriptor'
            'perl-libintl-perl: gscriptor')
validpgpkeys=('F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E') # Ludovic Rousseau <rousseau@debian.org>
source=("https://pcsc-tools.apdu.fr/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('59a79f15a231ebe905c9013fcf1432c8b785babf453cf0a657e34cd87b00517dd30c21f4f0633714b6988aa78eb73494bfc3a02102c7d1de2c5338e2ed79836a'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
