# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mattias Andrée <`base64 -d`(bWFhbmRyZWUK)@kth.se>

pkgname=sha3sum
pkgver=1.2.3.1
pkgrel=1
pkgdesc="Keccak-family checksum calculator, including SHA-3"
arch=('x86_64')
url='https://codeberg.org/maandree/sha3sum'
license=('custom:ISC')
depends=('libkeccak>=1.4')
makedepends=('git')
source=("git+https://codeberg.org/maandree/sha3sum.git#tag=${pkgver}?signed")
b2sums=('82c48b3b5ae39d37a926129f73318b0d0052fe2e4c953befb6b8199442e30f19869156636ae391cd5460b50ed8f1294cf6a9638b85dad2dc2871d33cc05333b2')
validpgpkeys=(
  'BC61893EC01FDBDEE061BEF67BE0C42240089FE0' # attias Andrée <maandree@kth.se>
)

prepare() {
  cd ${pkgname}
  # inject our flags into the build
  sed -i -r 's#^(.*)FLAGS\s+=(.*)$#\1FLAGS :=\2 \$(\1FLAGS)#g' config.mk
}

build() {
  cd ${pkgname}
  make PREFIX=/usr
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}
