# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mattias Andrée <`base64 -d`(bWFhbmRyZWUK)@kth.se>

pkgname=sha3sum
pkgver=1.2.4
pkgrel=1
pkgdesc="Keccak-family checksum calculator, including SHA-3"
arch=('x86_64')
url='https://codeberg.org/maandree/sha3sum'
license=('custom:ISC')
depends=('libkeccak>=1.4')
makedepends=('git')
source=("git+https://codeberg.org/maandree/sha3sum.git#tag=${pkgver}?signed")
b2sums=('56b5944be9477cffc26b6dbcebf1b61bb9f62b4105815a4261b39a1e6f99b0886beea8033f69814430b20fce71f0f41f73d396706ed29868d655ed8a5d4f7853')
validpgpkeys=(
  '3683C4B70CFA859F0173F2CCE0DD13EBFC7D5E3E' # Mattias Andrée <maandree@kth.se>
)

prepare() {
  cd ${pkgname}
  # inject our flags into the build
  sed -i -r 's#^(.*)FLAGS\s+=(.*)$#\1FLAGS :=\2 \$(\1FLAGS)#g' config.mk
}

build() {
  cd ${pkgname}
  make PREFIX=/usr
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}
