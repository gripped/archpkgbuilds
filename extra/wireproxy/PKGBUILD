# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Ivan <vantu5z@mail.ru>

pkgname=wireproxy
pkgver=1.0.10
pkgrel=4
arch=(x86_64)
license=(ISC)
url="https://github.com/whyvl/wireproxy"
pkgdesc="Wireguard client that exposes itself as a socks5 proxy"
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles"
        "${pkgname}.service"
        "${pkgname}@.service"
        "${pkgname}-user.service"
        "${pkgname}-user@.service")
sha256sums=('3e08e79e3c1b66b7e129426dc6e84b93a1fb3827edde96d85bbf863a5a66c8ff'
            '2fa16441c6913154ec8f026420bb603fec463d0627aee4277aca30ca95b534df'
            'ab9a59d0f200371aee18abc93653cf8f9504063d14ae08e289b90f711e559735'
            '570b9777af70938f0f0ceec7feb46698966e7f96b96506af2a564ffca870214b'
            '2e8e57908b54d1a91f0838488a78eb5304ffe0aaecd10f1131ad9e7f44794622'
            '7ab95b432cc3a4ea5be7d60e925c87eee132445320f2178d99d4e74e62bf4a85'
            '24c5cbf76615b1be5b21370e5abc34182d1d477158b2b10306f2f0395e5dc07b')

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"

  go build -v \
    -buildmode=pie \
    -trimpath \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode=external -s -w -buildid='' \
      -X main.version=${pkgver}" \
    -o "${pkgname}" \
    "./cmd/${pkgname}"
}

package() {
  install -Dm0644 "${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm0644 "${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm0644 "${pkgname}.service" "${pkgname}@.service" -t "${pkgdir}/usr/lib/systemd/system/"
  install -Dm0644 "${pkgname}-user.service" "${pkgdir}/usr/lib/systemd/user/${pkgname}.service"
  install -Dm0644 "${pkgname}-user@.service" "${pkgdir}/usr/lib/systemd/user/${pkgname}@.service"

  cd "${pkgname}-${pkgver}"
  install -Dm0755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
