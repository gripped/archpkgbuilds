# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor : bartus <arch-user-repoá˜“bartus.33mail.com>
pkgname=popsift
pkgver=1.0.0.git4
_commit=e34fdc66c3055315c16879f2062a88d1db9fe501
pkgrel=2
pkgdesc="Realtime GPU implementation of SIFT"
arch=('x86_64')
url="https://github.com/alicevision/popsift"
license=('MPL-2.0')
depends=('boost-libs')
makedepends=('boost' 'cmake' 'ninja' 'git' 'cuda')
options=('staticlibs' '!lto')
source=("git+https://github.com/alicevision/popsift.git#commit=$_commit"
         boost-1.89.patch)
sha256sums=('e76fea8c4b263e6a2f7b9cacb3d77d83df7bb84f4dca955a0241b8b0ec503146'
            '606ba3d19d1472090c15ad0766ef5f66dabaf40b8ff4f1da20a0b8bd0d56b991')

prepare() {
  cd popsift
  patch -p1 -i ../boost-1.89.patch
}

build() {
  cd popsift
  local cuda_archs="75;80;86;87;88;89;90;100;103;110;120;121;121-virtual"

  cmake \
    -Bbuild_static \
    -GNinja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=OFF \
    -DCMAKE_CUDA_ARCHITECTURES="$cuda_archs"
  ninja -C build_static

  cmake \
    -Bbuild_shared \
    -GNinja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_CUDA_ARCHITECTURES="$cuda_archs"
  ninja -C build_shared
}

package() {
  cd popsift
  DESTDIR="${pkgdir}" ninja -C build_static install
  DESTDIR="${pkgdir}" ninja -C build_shared install
}

# vim:set ts=2 sw=2 et:
