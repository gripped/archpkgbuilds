# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=cosign
pkgver=3.0.4
pkgrel=1
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'go'
  'git'
)
checkdepends=('go-tools')
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('23f2b6b4bb194fd3877c4037f8a62db9358f18731aff1e6f59d06e122e3ff3ec9aecd1975a4e5ed65e45f29b1c0157495ea00c95b604266a5c65a575d7c7d2b5')
b2sums=('645706d4e054208d055a6785979d043fee6c54c327864ae9994b5c8d85683b9962600043c9cb7fde713136b069bc47642383db1431aa3d567acf64756b6ca556')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -X sigs.k8s.io/release-utils/version.gitVersion=v$pkgver \
    -compressdwarf=false  \
    -linkmode=external \
  "
  go build -v -ldflags="$ld_flags" ./cmd/cosign
}

check() {
  cd $pkgname
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/attest \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/bundle \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/fulcio \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/sign \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/verify \
      | grep -v github.com/sigstore/cosign/v3/internal/pkg/cosign/tsa \
      | grep -v github.com/sigstore/cosign/v3/pkg/cosign
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" cosign
  "$pkgdir/usr/bin/cosign" completion bash \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/cosign"
  "$pkgdir/usr/bin/cosign" completion zsh \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_cosign"
  "$pkgdir/usr/bin/cosign" completion fish \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/cosign.fish"
}
