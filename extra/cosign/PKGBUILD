# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=cosign
pkgver=2.5.2
pkgrel=1
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'go'
  'git'
)
checkdepends=('go-tools')
options=('!lto')
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('234d44ad63c59c0901d9908adfbb1cb5ccd2c573cb17aad432af5df976e550155ad3d3c1b0a6d3b8b26c75e5fadeeaccb8eecee29766ead424cdb7d11967580e')
b2sums=('eab97599f4d0bd9c2c5f3168a237a12ce478c56d3a3aa819c01593f4f6ade33208d259742120c66eb4cbda327535018bb4d058fc3e7943cdd53772720c29cdc3')

prepare() {
  cd "${pkgname}"
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build(){
  cd "${pkgname}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -X sigs.k8s.io/release-utils/version.gitVersion=v${pkgver} \
    -compressdwarf=false  \
    -linkmode=external \
  "
  go build -v -ldflags="${ld_flags}" ./cmd/cosign
}

check() {
 cd "${pkgname}"
 go test -v ./...
}

package() {
  cd "${pkgname}"
  install -vDm755 -t "${pkgdir}/usr/bin" cosign
  "$pkgdir"/usr/bin/cosign completion bash \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/cosign"
  "$pkgdir"/usr/bin/cosign completion zsh \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_cosign"
  "$pkgdir"/usr/bin/cosign completion fish \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/cosign.fish"
}
