# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=cosign
pkgver=2.6.1
pkgrel=1
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'go'
  'git'
)
checkdepends=('go-tools')
options=('!lto')
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('482ea30011887ecd97cc161f33e2b20f0cf9e576af31812d4c74cc73b94155df216122817c4ae52124115912b3998382e3cbb55620be79b9c8847a69a8662bee')
b2sums=('dee1438a9eb2708161667957c402adc707a8d6fb859c50166d222de39b9213178eaa31e54cbade85c5cb072c666bf57f10f0ec80cc7ee328b29b66e4d473f587')

prepare() {
  cd "${pkgname}"
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build(){
  cd "${pkgname}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -X sigs.k8s.io/release-utils/version.gitVersion=v${pkgver} \
    -compressdwarf=false  \
    -linkmode=external \
  "
  go build -v -ldflags="${ld_flags}" ./cmd/cosign
}

check() {
 cd "${pkgname}"
 go test -v ./...
}

package() {
  cd "${pkgname}"
  install -vDm755 -t "${pkgdir}/usr/bin" cosign
  "$pkgdir"/usr/bin/cosign completion bash \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/cosign"
  "$pkgdir"/usr/bin/cosign completion zsh \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_cosign"
  "$pkgdir"/usr/bin/cosign completion fish \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/cosign.fish"
}
