# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=cosign
pkgver=3.0.3
pkgrel=1
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'go'
  'git'
)
checkdepends=('go-tools')
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('1be65c38227186b2bf741fadadceea8a2611bcb76c4a4573a05ec54daa24b7ea98bcdb3a4a6e3773cdce9231bde243d3e5f99d0e5c58511be77e78577ccdcb59')
b2sums=('a34c160e801bc1a900a12920ef56b5ba5c3d82fe003221764bbd9b68b73fe66fbb4acc170cdaf299b4e1e2383e2cb9f8c62e809eb1b034d71a5f319674f07658')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -X sigs.k8s.io/release-utils/version.gitVersion=v$pkgver \
    -compressdwarf=false  \
    -linkmode=external \
  "
  go build -v -ldflags="$ld_flags" ./cmd/cosign
}

check() {
  cd $pkgname
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/attest \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/bundle \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/fulcio \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/sign \
      | grep -v github.com/sigstore/cosign/v3/cmd/cosign/cli/verify \
      | grep -v github.com/sigstore/cosign/v3/internal/pkg/cosign/tsa \
      | grep -v github.com/sigstore/cosign/v3/pkg/cosign
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" cosign
  "$pkgdir/usr/bin/cosign" completion bash \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/cosign"
  "$pkgdir/usr/bin/cosign" completion zsh \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_cosign"
  "$pkgdir/usr/bin/cosign" completion fish \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/cosign.fish"
}
