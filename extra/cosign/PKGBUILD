# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=cosign
pkgver=2.5.0
pkgrel=1
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'go'
  'git'
)
checkdepends=('go-tools')
options=('!lto')
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('021e89cb3ff4aa084a40eab00f8e0cebb3f0849e7e82a9c837ad6d5470fa033caea008a06f09bf9ecc30da6b4fd672f464327306811c3f3055743fcef27b95ab')
b2sums=('c3989dbbbd7b34ec5288fd77e58ca8cd4b620113e364bcf2b8d2985da27c1506311ba53eead79273179abb5f7380218dab72cf592ea082a46dc66a5ae417f9c2')

prepare() {
  cd "${pkgname}"
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build(){
  cd "${pkgname}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -X sigs.k8s.io/release-utils/version.gitVersion=v${pkgver} \
    -compressdwarf=false  \
    -linkmode=external \
  "
  go build -v -ldflags="${ld_flags}" ./cmd/cosign
}

check() {
 cd "${pkgname}"
 go test -v ./...
}

package() {
  cd "${pkgname}"
  install -vDm755 -t "${pkgdir}/usr/bin" cosign
  "$pkgdir"/usr/bin/cosign completion bash \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/cosign"
  "$pkgdir"/usr/bin/cosign completion zsh \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_cosign"
  "$pkgdir"/usr/bin/cosign completion fish \
    | install -vDm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/cosign.fish"
}
