# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=bitsery
pkgver=5.2.4
pkgrel=1
pkgdesc="Header only C++ binary serialization library"
arch=(any)
url="https://github.com/fraillt/bitsery"
license=(MIT)
makedepends=(
  cmake
  gtest
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('26e525d799d1777e182753c6c970765be8695a557e0fef35224ab8f4629a094c04fd8d7e456da369938d74acb0ca84084f394f212ae1343fa62a27256dba971f')
b2sums=('e6fe7f4b71eefddffa6580749bb8f04265c31ddb414994ed5c170b25d4a8bceb895ec89fd8cecad18b11760c034f2f3b185c53244253f0f14bb93862505733fb')

build() {
  local cmake_options=(
    -B build
    -D BITSERY_BUILD_TESTS=ON
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build/tests
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
