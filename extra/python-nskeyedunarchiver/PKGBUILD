# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

_pyname=NSKeyedUnArchiver
pkgname=python-nskeyedunarchiver
pkgver=1.5.1
pkgrel=1
pkgdesc="Library for decoding Apple's text or binary plist files"
arch=(any)
url='https://github.com/avibrazil/NSKeyedUnArchiver'
license=('LGPL')
depends=('python')
makedepends=('python-'{build,installer,wheel}
             'python-setuptools')
_archive="${_pyname,,}-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha512sums=('45a7d542f169d89899cc1c78240c4567b1eda9248d60a3755fc092851641b8fd53aa055be51a71435792ed9de097c68ff6840080cabe34fc3f9ac0fb24bb667c')

build() {
  cd "$_archive"
  python -m build -wn
}

package() {
  cd "$_archive"
  python -m installer -d "$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
