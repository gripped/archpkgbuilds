# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-markdown2
pkgver=2.5.4
pkgrel=2
pkgdesc='A fast and complete implementation of Markdown in Python'
arch=(any)
url='https://github.com/trentm/python-markdown2'
license=(MIT)
depends=(python python-pygments)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('97b274da0ab64f42a9d07c79791b774c82a83b6da0e4e85b55c94ad674dd5926cf313eae92e85939bbd936c5c5c2a225513e25c73262faef7d95e13b2c08a50d')
b2sums=('206d8beb5ebd81cb503df219f7519d828f3f408ab24907f8c3d1e25c99a7ac6338d0136449022121e8c979c6bbd73734c6dbe81b6f3309377d53de0d077bf140')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname/test"

  python testall.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
# vim:set ts=2 sw=2 et:
