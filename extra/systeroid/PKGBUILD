# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=systeroid
pkgver=0.4.3
pkgrel=1
pkgdesc="A more powerful alternative to sysctl"
arch=('x86_64')
url="https://github.com/orhun/systeroid"
license=('MIT' 'Apache')
depends=('libxcb' 'libxkbcommon' 'linux-docs')
makedepends=('cargo' 'python')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c998aa62d32a2a53c6e125cc1827d63020608058ef4858a3c5bb9145012453541d94844243d5f40a4a71d4f0625e66db9386c34cc0de324611ab79f49c5c7d82')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  NO_COLOR=1 cargo test --frozen --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/$pkgname-tui" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man8/$pkgname.8" -t "$pkgdir/usr/share/man/man8"
  install -Dm 644 "man8/$pkgname-tui.8" -t "$pkgdir/usr/share/man/man8"
}

# vim: ts=2 sw=2 et:
