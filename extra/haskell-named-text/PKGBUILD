# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=named-text
pkgname=haskell-named-text
pkgver=1.2.1.0
pkgrel=46
pkgdesc="A parameterized named text type and associated functionality."
url="https://github.com/kquick/named-text"
license=("ISC")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-hashable' 'haskell-prettyprinter' 'haskell-sayable')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-parameterized-utils' 'haskell-tasty'
             'haskell-tasty-ant-xml' 'haskell-tasty-checklist' 'haskell-tasty-hspec'
             'haskell-unordered-containers')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('f4e50f0e21cb9c72a5f04970ebce9747c549bc4573ead617857135bea1a7361d')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fwith-json

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
