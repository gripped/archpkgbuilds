# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=crane
_pkgname=go-containerregistry
pkgver=0.21.0
pkgrel=1
pkgdesc="Interacting with remote container images and registries."
arch=('x86_64')
url="https://github.com/google/go-containerregistry"
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'go'
)
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/google/${_pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('fa3472a765b031636ba79efdd1068012794a5a83679364136d456fcde0519fa3')
b2sums=('d6cac43152dcd5a43f226b0dbd6a7fab19a660d44f712da80d13d08149f322b27b54568d4bb900da9250f4ac6ae05c8ff87e8c36b06c538e1991c2404a1b42d6')

prepare() {
  cd ${_pkgname}-${pkgver}
  go mod download
  cd cmd/krane
  go mod download
}

build() {
  cd ${_pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  local go_options=(
    -buildmode=pie
    -mod=readonly
    -modcacherw
    -ldflags '-compressdwarf=false -linkmode=external'
  )

  go build "${go_options[@]}" -o build/ ./cmd/{crane,gcrane}
  cd cmd/krane
  go build "${go_options[@]}" -o ../../build/ .
}

check() {
  cd ${_pkgname}-${pkgver}
  ./build/crane --help
  ./build/gcrane --help
  ./build/krane --help
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    build/{crane,gcrane,krane}

  # setup completions
  install -dm 755 "${pkgdir}/usr/share/bash-completion/completions" \
                  "${pkgdir}/usr/share/zsh/site-functions" \
                 "${pkgdir}/usr/share/fish/vendor_completions.d"
  for bin in crane gcrane krane; do
    "${pkgdir}/usr/bin/${bin}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${bin}"
    "${pkgdir}/usr/bin/${bin}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${bin}"
    "${pkgdir}/usr/bin/${bin}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${bin}.fish"
  done

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
