# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-dbus-fast
pkgver=2.24.0
pkgrel=1
pkgdesc='A faster version of dbus-next'
arch=(x86_64)
url=https://github.com/bluetooth-devices/dbus-fast
license=(MIT)
depends=(
  cython
  dbus
  glib2
  glibc
  python
)
makedepends=(
  git
  python-async-timeout
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-cov
  python-pytest-timeout
)
_tag=31ff222f82ce3bab7ef68be74e484244af3a3b20
source=(git+https://github.com/Bluetooth-Devices/dbus-fast.git#tag=${_tag})
b2sums=('0aed62c852f05c283931d1ed86c6ee9dc09250d265df45468c71cafa3c4e057cb38b64f06de508e6f0f6e75d6c8e883a6bb74b521f2023e550aa1d26b2ed6652')

pkgver() {
  cd dbus-fast
  git describe --tags | sed 's/^v//'
}

build() {
  cd dbus-fast
  python -m build --wheel --no-isolation
}

check() {
  cd dbus-fast
  PYTHONPATH="$PWD" \
  dbus-run-session -- pytest -v
}

package() {
  python -m installer --destdir="${pkgdir}" dbus-fast/dist/*.whl
  install -Dm 644 dbus-fast/LICENSE -t "${pkgdir}"/usr/share/licenses/python-dbus-fast/
}

# vim: ts=2 sw=2 et:
