# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-dbus-fast
pkgver=2.44.1
pkgrel=1
pkgdesc='A faster version of dbus-next'
arch=(x86_64)
url=https://github.com/bluetooth-devices/dbus-fast
license=(MIT)
depends=(
  cython
  dbus
  glib2
  glibc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-codspeed
  python-pytest-timeout
)
_tag=d8c108f73de9d4045c5308cf8eb77370c8d5a02d
source=(git+https://github.com/Bluetooth-Devices/dbus-fast.git#tag=${_tag})
b2sums=('b403b99ef211084547b315f1e52d54c3f84047a3da01409879edfe8354fd547a7a872bedc773ef8500bccfd1e680103dbd3b756d487540c4158470991965e483')

pkgver() {
  cd dbus-fast
  git describe --tags | sed 's/^v//'
}

build() {
  cd dbus-fast
  python -m build --wheel --no-isolation
}

check() {
  cd dbus-fast
  PYTHONPATH="$PWD" \
  dbus-run-session -- pytest -v --override-ini="addopts="
}

package() {
  python -m installer --destdir="${pkgdir}" dbus-fast/dist/*.whl
  install -Dm 644 dbus-fast/LICENSE -t "${pkgdir}"/usr/share/licenses/python-dbus-fast/
}

# vim: ts=2 sw=2 et:
