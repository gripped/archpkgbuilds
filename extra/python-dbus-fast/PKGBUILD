# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-dbus-fast
pkgver=2.24.3
pkgrel=2
pkgdesc='A faster version of dbus-next'
arch=(x86_64)
url=https://github.com/bluetooth-devices/dbus-fast
license=(MIT)
depends=(
  cython
  dbus
  glib2
  glibc
  python
)
makedepends=(
  git
  python-async-timeout
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-timeout
)
_tag=4c838a7bfc7db8be0b6b63457b7c014d3bb663ab
source=(git+https://github.com/Bluetooth-Devices/dbus-fast.git#tag=${_tag})
b2sums=('bace3d1884ee47324dcc1af4c708b97053e544d523e3e037a1af0bc64b8133c6cad7eefa5096746672c7ef52cc2bd2641a3cca1fc9d580f19f2b7dd2eda1335e')

pkgver() {
  cd dbus-fast
  git describe --tags | sed 's/^v//'
}

build() {
  cd dbus-fast
  python -m build --wheel --no-isolation
}

check() {
  cd dbus-fast
  PYTHONPATH="$PWD" \
  dbus-run-session -- pytest -v --override-ini="addopts="
}

package() {
  python -m installer --destdir="${pkgdir}" dbus-fast/dist/*.whl
  install -Dm 644 dbus-fast/LICENSE -t "${pkgdir}"/usr/share/licenses/python-dbus-fast/
}

# vim: ts=2 sw=2 et:
