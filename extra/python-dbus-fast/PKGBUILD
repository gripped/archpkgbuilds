# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-dbus-fast
pkgver=2.44.2
pkgrel=1
pkgdesc='A faster version of dbus-next'
arch=(x86_64)
url=https://github.com/bluetooth-devices/dbus-fast
license=(MIT)
depends=(
  cython
  dbus
  glib2
  glibc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-codspeed
  python-pytest-timeout
)
_tag=bd8406f20e97b072e58580b1f63532a8e416cc21
source=(git+https://github.com/Bluetooth-Devices/dbus-fast.git#tag=${_tag})
b2sums=('5e05e7cc9dc0c31e3ae70cdf006bccc46a6362196784e62d948fe04e4ec2f5d8cd04052c2d62c1e1a1a0ffc5544abad93f32d4c1f214cebf244501292d107209')

prepare() {
  sed 's/Cython>=3,<3.1.0/Cython>=3,<3.2.0/' -i dbus-fast/pyproject.toml
}

pkgver() {
  cd dbus-fast
  git describe --tags | sed 's/^v//'
}

build() {
  cd dbus-fast
  python -m build --wheel --no-isolation
}

check() {
  cd dbus-fast
  PYTHONPATH="$PWD" \
  dbus-run-session -- pytest -v --override-ini="addopts="
}

package() {
  python -m installer --destdir="${pkgdir}" dbus-fast/dist/*.whl
  install -Dm 644 dbus-fast/LICENSE -t "${pkgdir}"/usr/share/licenses/python-dbus-fast/
}

# vim: ts=2 sw=2 et:
