# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-dbus-fast
pkgver=3.1.2
pkgrel=2
pkgdesc='A faster version of dbus-next'
arch=(x86_64)
url=https://github.com/bluetooth-devices/dbus-fast
license=(MIT)
depends=(
  cython
  dbus
  glib2
  glibc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-codspeed
  python-pytest-timeout
)
_tag=b19f299ca62b7bad1d2808841b39078f9aaa8978
source=(git+https://github.com/Bluetooth-Devices/dbus-fast.git#tag=${_tag})
b2sums=('50a5a2c40c6e820d0978b28820a152fd584ffd505cd9656fd701041ed0b911d9a8603fdabcd0281119b6f5af366c9deada1ea0c777947fa1f09787b3705046d4')

prepare() {
  sed 's/Cython>=3,<3.1.0/Cython>=3/' -i dbus-fast/pyproject.toml
}

pkgver() {
  cd dbus-fast
  git describe --tags | sed 's/^v//'
}

build() {
  cd dbus-fast
  python -m build --wheel --no-isolation
}

check() {
  cd dbus-fast
  PYTHONPATH="$PWD" \
  dbus-run-session -- pytest -v --override-ini="addopts="
}

package() {
  python -m installer --destdir="${pkgdir}" dbus-fast/dist/*.whl
  install -Dm 644 dbus-fast/LICENSE -t "${pkgdir}"/usr/share/licenses/python-dbus-fast/
}

# vim: ts=2 sw=2 et:
