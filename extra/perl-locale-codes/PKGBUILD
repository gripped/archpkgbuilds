# Maintainer: Konstantin Gizdov <arch at kge dot pw>
pkgname=perl-locale-codes
pkgver=3.86
pkgrel=1
pkgdesc='Distribution of modules to handle locale codes'
_dist=Locale-Codes
arch=('any')
url="https://metacpan.org/release/${_dist}"
license=('GPL' 'PerlArtistic')
depends=(perl)
checkdepends=('perl-test-inter')
options=('!emptydirs' purge)
source=("https://search.cpan.org/CPAN/authors/id/SBECK/${_dist}-${pkgver}.tar.gz")
b2sums=('994f274700bd5f3342b43bf930c48a7593ff3e4625a629ecca7884072db2ef4751442e52e69ade33d8967415ced9b608780fa90739a96a08fc92bc7a50202e60')

build() {
  cd "${srcdir}/${_dist}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/${_dist}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${srcdir}/${_dist}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
}
