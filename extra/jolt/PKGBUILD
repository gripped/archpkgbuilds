# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Anas Elgarhy <anas.elgarhy.dev@gmail.com>

pkgname=jolt
pkgver=1.2.0
pkgrel=2
pkgdesc='TUI-based battery and energy monitor'
arch=(x86_64 aarch64)
url="https://github.com/jordond/$pkgname"
license=(MIT)
makedepends=(cargo)
options=(!lto)
install="$pkgname.install"
_archive="$pkgname-$pkgver"
source=("$url/archive/refs/tags/$pkgver/$_archive.tar.gz")
sha256sums=('c6756b84349a6f253d81eb9ad6074f9b94461043c053b1b7ce5f86c2e1bed04d')

prepare() {
	cd "$_archive"
	# https://github.com/jordond/jolt/issues/159
	cargo update
	cargo fetch --locked --target host-tuple
}

build() {
	cd "$_archive"
	cargo build --frozen --release
}

package() {
	cd "$_archive"
	install -Dm0755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 -t "${pkgdir}/usr/lib/systemd/user/" "assets/$pkgname.service"
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}
