# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apko
pkgver=0.30.34
pkgrel=1
pkgdesc="Declarative APK-based container building tool with support for sigstore signatures"
url="https://github.com/chainguard-dev/apko"
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'alpine-keyring'
  'apk-tools'
  'glibc'
)
makedepends=(
  'go'
)
source=("https://github.com/chainguard-dev/apko/archive/v${pkgver}/apko-${pkgver}.tar.gz")
sha256sums=('5ff2e903667148dfab6b43982a254fa51cb9ae63332bb8f9e566a83e4d363a30')
b2sums=('11f26b9c2d779b11c7f98c8d6644927cde2e2fb8616512c27bcc1eaf2b60b69faff41a252675d1dad7e3e876a9977e6c0c5528bd0e7ad63a9d3122e21455f3e4')

build() {
  cd "${pkgname}-${pkgver}"
  mkdir build

  go build \
      -o build/ \
      -tags=pivkey,pkcs11key \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
      ./...

  for i in bash fish zsh; do
    build/apko completion ${i} > ./apko.${i}
  done
}

check() {
  cd "${pkgname}-${pkgver}"
  # skip TestPublish: https://github.com/chainguard-dev/apko/issues/881
  go test ./... -skip 'TestPublish|TestBuild'
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/apko "${pkgdir}"/usr/bin/apko
  install -Dm644 apko.bash "${pkgdir}"/usr/share/bash-completion/completions/apko
  install -Dm644 apko.fish "${pkgdir}"/usr/share/fish/vendor_completions.d/apko.fish
  install -Dm644 apko.zsh "${pkgdir}"/usr/share/zsh/site-functions/_apko
}

# vim: ts=2 sw=2 et:
