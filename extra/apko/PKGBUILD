# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apko
pkgver=0.25.5
pkgrel=1
pkgdesc="Declarative APK-based container building tool with support for sigstore signatures"
url="https://github.com/chainguard-dev/apko"
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'alpine-keyring'
  'apk-tools'
  'glibc'
)
makedepends=(
  'go'
)
source=("https://github.com/chainguard-dev/apko/archive/v${pkgver}/apko-${pkgver}.tar.gz")
sha256sums=('02aba5d22e45feabfebb0a21a7dcbbe98b6e0a42efe3ecfda31e2702bfe56be2')
b2sums=('25cfa45a3c688c0dfc571d4f5773fc7ac68eb749d6569f16178fe463e5310c08293e49d2117920d279e53f87e03ee5476f662a47a7277b0c97406079f0221c58')

build() {
  cd "${pkgname}-${pkgver}"
  mkdir build

  go build \
      -o build/ \
      -tags=pivkey,pkcs11key \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
      ./...

  for i in bash fish zsh; do
    build/apko completion ${i} > ./apko.${i}
  done
}

check() {
  cd "${pkgname}-${pkgver}"
  # skip TestPublish: https://github.com/chainguard-dev/apko/issues/881
  go test ./... -skip 'TestPublish|TestBuild'
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/apko "${pkgdir}"/usr/bin/apko
  install -Dm644 apko.bash "${pkgdir}"/usr/share/bash-completion/completions/apko
  install -Dm644 apko.fish "${pkgdir}"/usr/share/fish/completions/apko.fish
  install -Dm644 apko.zsh "${pkgdir}"/usr/share/zsh/site-functions/_apko
}

# vim: ts=2 sw=2 et:
