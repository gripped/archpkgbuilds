# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apko
pkgver=0.12.0
pkgrel=1
pkgdesc="Declarative APK-based container building tool with support for sigstore signatures"
url="https://github.com/chainguard-dev/apko"
arch=('x86_64')
license=('Apache-2.0')
depends=('apk-tools' 'alpine-keyring')
makedepends=('go')
source=("https://github.com/chainguard-dev/apko/archive/v${pkgver}/apko-${pkgver}.tar.gz")
sha256sums=('3b1911d16ecdb76d1678602e6ae0cd859043bbe9904bdad10d44d517882b2155')
b2sums=('6d52bcbd97fea6998c7c379f4898675d767f841d752c9117008f8bacc2cd529dea87f95a73d4337cf36a976289fa86bf7c560f744d8d8f6465c7c830a0a7f1c6')

build() {
  cd "${pkgname}-${pkgver}"
  mkdir build
  CGO_LDFLAGS="${LDFLAGS}" \
    GOFLAGS="-trimpath -buildmode=pie -mod=readonly" \
    go build -o build/ -tags -tags=pivkey,pkcs11key ./...

  for i in bash fish zsh; do
    build/apko completion ${i} > ./apko.${i}
  done
}

check() {
  cd "${pkgname}-${pkgver}"
  # skip TestPublish: https://github.com/chainguard-dev/apko/issues/881
  go test ./... -skip TestPublish
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/apko "${pkgdir}"/usr/bin/apko
  install -Dm644 apko.bash "${pkgdir}"/usr/share/bash-completion/completions/apko
  install -Dm644 apko.fish "${pkgdir}"/usr/share/fish/completions/apko.fish
  install -Dm644 apko.zsh "${pkgdir}"/usr/share/zsh/site-functions/_apko
}

# vim: ts=2 sw=2 et:
