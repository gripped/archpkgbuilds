# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apko
pkgver=1.0.1
pkgrel=1
pkgdesc="Declarative APK-based container building tool with support for sigstore signatures"
url="https://github.com/chainguard-dev/apko"
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'alpine-keyring'
  'apk-tools'
  'glibc'
)
makedepends=(
  'go'
)
source=("https://github.com/chainguard-dev/apko/archive/v${pkgver}/apko-${pkgver}.tar.gz")
sha256sums=('0e8c3c6847a8dae13d13de3c5b1e1e84cf8ce0b31120aed984971896ef58237f')
b2sums=('d85b1e1f5ea27018bcdbe3bd95f7c3392745be3e12e242d732417ffc29188381d7797ce185e797a771cfa6bd242fefb6f32590f51a060ab83d466ebb2b48e00c')

build() {
  cd "${pkgname}-${pkgver}"
  mkdir build

  go build \
      -o build/ \
      -tags=pivkey,pkcs11key \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
      ./...

  for i in bash fish zsh; do
    build/apko completion ${i} > ./apko.${i}
  done
}

check() {
  cd "${pkgname}-${pkgver}"
  # skip TestPublish: https://github.com/chainguard-dev/apko/issues/881
  go test ./... -skip 'TestPublish|TestBuild'
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/apko "${pkgdir}"/usr/bin/apko
  install -Dm644 apko.bash "${pkgdir}"/usr/share/bash-completion/completions/apko
  install -Dm644 apko.fish "${pkgdir}"/usr/share/fish/vendor_completions.d/apko.fish
  install -Dm644 apko.zsh "${pkgdir}"/usr/share/zsh/site-functions/_apko
}

# vim: ts=2 sw=2 et:
