# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apko
pkgver=0.25.6
pkgrel=1
pkgdesc="Declarative APK-based container building tool with support for sigstore signatures"
url="https://github.com/chainguard-dev/apko"
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'alpine-keyring'
  'apk-tools'
  'glibc'
)
makedepends=(
  'go'
)
source=("https://github.com/chainguard-dev/apko/archive/v${pkgver}/apko-${pkgver}.tar.gz")
sha256sums=('bfd5187f39d4de73d2a53207d94bde96d69681644ba8ede1ec9f913958821db2')
b2sums=('7ec6db9a9f81d4f222bdb9511bd24fd063eeda995d35bc878849b2b0eb1656ed2296eb172ddea6b9a57891d900cd403e680a8e232e1ba6278674332b807668cc')

build() {
  cd "${pkgname}-${pkgver}"
  mkdir build

  go build \
      -o build/ \
      -tags=pivkey,pkcs11key \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
      ./...

  for i in bash fish zsh; do
    build/apko completion ${i} > ./apko.${i}
  done
}

check() {
  cd "${pkgname}-${pkgver}"
  # skip TestPublish: https://github.com/chainguard-dev/apko/issues/881
  go test ./... -skip 'TestPublish|TestBuild'
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/apko "${pkgdir}"/usr/bin/apko
  install -Dm644 apko.bash "${pkgdir}"/usr/share/bash-completion/completions/apko
  install -Dm644 apko.fish "${pkgdir}"/usr/share/fish/completions/apko.fish
  install -Dm644 apko.zsh "${pkgdir}"/usr/share/zsh/site-functions/_apko
}

# vim: ts=2 sw=2 et:
