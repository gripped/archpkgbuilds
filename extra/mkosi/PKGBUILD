# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Lucas Werkmeister <mail@lucaswerkmeister.de>
# Contributor: Dave Reisner <dreisner@archlinux.org>

pkgname=mkosi
pkgver=15.1
pkgrel=1
pkgdesc="Build Legacy-Free OS Images"
arch=(any)
url="https://github.com/systemd/mkosi"
license=(LGPL-2.1-or-later)
depends=(
  arch-install-scripts
  python
  python-pexpect
)
makedepends=(
  pandoc-cli
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-pexpect
  python-pytest
  systemd
)
optdepends=(
  'btrfs-progs: raw_btrfs and subvolume output formats'
  'cryptsetup: add dm-verity partitions'
  'debian-archive-keyring: build Debian images'
  'debootstrap: build Debian or Ubuntu images'
  'dosfstools: build bootable images'
  'gnupg: sign images'
  'python-pefile: for bootable images'
  'qemu: run bootable images using QEMU'
  'sbsigntools: sign EFI binaries for UEFI SecureBoot'
  'squashfs-tools: for creating custom scripts to output as squashfs'
  'tar: tar output format'
  'ubuntu-keyring: build Ubuntu images'
  'xz: compress images with xz'
  'zstd: compress images with zstd'
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
)
sha512sums=('3b65c6b964aa2bde12117b7b2c3e4be0ac72036a92a115f1e93e7be0b3b0aa0bd3adb753ee49865723a9bc677bc4492416caafff4fec3ffd942de78357c11608')
b2sums=('36171c08c18dd3ba3c2e54c8a3374fad2a9b008f8253e6cd57cedf7b7e8216b6610b30f9f46aa634de62ebf257d1e8eff94a83b07c027708e4875e24bc0a8305')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  pandoc -t man -s -o $pkgname/resources/$pkgname.1 $pkgname/resources/$pkgname.md
}

check() {
  cd $pkgname-$pkgver
  pytest -vv
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 $pkgname/resources/$pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 docs/*.md {NEWS,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
