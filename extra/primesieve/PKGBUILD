# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=12.1
pkgrel=1
pkgdesc='Fast prime number generator'
arch=(x86_64)
url='https://github.com/kimwalisch/primesieve'
license=(BSD-2-Clause)
depends=(gcc-libs
         glibc)
makedepends=(cmake)
source=(https://github.com/kimwalisch/primesieve/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('64e7f5d3ce680262b865c7aec1381ffb13d13f75dd985b964ce33806e1bbf386')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
