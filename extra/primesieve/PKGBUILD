# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=12.5
pkgrel=1
pkgdesc='Fast prime number generator'
arch=(x86_64)
url='https://github.com/kimwalisch/primesieve'
license=(BSD-2-Clause)
depends=(gcc-libs
         glibc)
makedepends=(cmake
             git)
source=(git+https://github.com/kimwalisch/primesieve#tag=v$pkgver)
sha256sums=('602373e73ed2963f48c2d1feabf0454ad9baf336a67c6247ff230522bdaaac73')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
