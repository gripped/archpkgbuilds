# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=12.8
pkgrel=1
pkgdesc='Fast prime number generator'
arch=(x86_64)
url='https://github.com/kimwalisch/primesieve'
license=(BSD-2-Clause)
depends=(gcc-libs
         glibc)
makedepends=(cmake
             git)
source=(git+https://github.com/kimwalisch/primesieve#tag=v$pkgver)
sha256sums=('11ccf5c722683118274fe44f75c57192ba4ea4c417757a959b081f3c86af4740')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
