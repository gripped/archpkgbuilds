# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-shortcut-viewer
pkgver=5.5.5
pkgrel=1
pkgdesc='Deepin Shortcut Viewer'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-shortcut-viewer"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gcc-libs' 'glibc' 'qt6-base')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/deepin-shortcut-viewer.git#tag=$pkgver")
sha512sums=('bb2c831f4bc4d824de366b8102a32f84ef9ec9c2ed914e14a065aa68d3f065ba9c7ebfcda3cc61f624a180444ff6b7b346af074816a8312f26a0aaad6e715413')

build(){
  cmake -B build -S deepin-shortcut-viewer -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
