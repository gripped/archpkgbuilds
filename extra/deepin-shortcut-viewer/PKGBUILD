# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-shortcut-viewer
pkgver=5.5.4
pkgrel=1
pkgdesc='Deepin Shortcut Viewer'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-shortcut-viewer"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gcc-libs' 'glibc' 'qt6-base')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/deepin-shortcut-viewer.git#tag=$pkgver")
sha512sums=('f3c3e4ccc65078362d9a3a27b7f732198308026a8709cd3165549f046ae9b7c05db1acf9e29442975ce0e229337fbdbf11613001382e7380b111022533cdb06c')

build(){
  cmake -B build -S deepin-shortcut-viewer -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
