# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-shortcut-viewer
pkgver=5.5.2
pkgrel=1
pkgdesc='Deepin Shortcut Viewer'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-shortcut-viewer"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gcc-libs' 'glibc' 'qt6-base')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/deepin-shortcut-viewer.git#tag=$pkgver")
sha512sums=('fa6294eb60f53a7285b730f74d834f0d445a371c2555479e5d873cd49682c8a6a3d3c7f7f46db6208d17242549703dadd0b71bd75c2f7cc40d533db1922ea7e9')

build(){
  cmake -B build -S deepin-shortcut-viewer -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
