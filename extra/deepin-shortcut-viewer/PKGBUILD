# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-shortcut-viewer
pkgver=5.5.0
pkgrel=1
pkgdesc='Deepin Shortcut Viewer'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-shortcut-viewer"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'gcc-libs' 'glibc' 'qt6-base')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/deepin-shortcut-viewer.git#tag=$pkgver")
sha512sums=('7f0e8a101f4fe70766f5317d39a49cae2505f03564780b194d6d337b45b65fbd830e5c427aa7d3eebe872de5512b102db628982a61104dcb835d1c8533d39e60')

build(){
  cmake -B build -S deepin-shortcut-viewer -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
