# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=hyprlauncher
pkgver=0.1.4
pkgrel=3
pkgdesc='A multipurpose and versatile launcher / picker for Hyprland'
arch=(x86_64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD-3-Clause)
depends=(gcc-libs # libgcc_s.so libstdc++.so
         glibc # libc.so libm.so
         icu libicuuc.so
         hyprlang libhyprlang.so
         hyprutils libhyprutils.so
         libqalculate # libqalculate.so
         hyprwire libhyprwire.so
         # libdrm # libdrm.so
         hyprtoolkit libhyprtoolkit.so
         wayland # libwayland-cursor.so libwayland-client.so
         wl-clipboard)
makedepends=(cmake)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('f3ae0d0b34451db90fce9631f6040097d5207e0fffb72bb77a5a61f514379f91')

build() {
	cd "$_archive"
	local cmake_options=(
		-D CMAKE_BUILD_TYPE=None
		-D CMAKE_INSTALL_PREFIX=/usr
	)
	cmake -B build -W no-dev ${cmake_options[@]}
	cmake --build build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
