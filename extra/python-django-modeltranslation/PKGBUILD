# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-django-modeltranslation
pkgver=0.19.9
pkgrel=3
pkgdesc='Translates Django models using a registration approach'
arch=(any)
url=https://github.com/deschler/django-modeltranslation
license=(BSD)
depends=(python-django)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=9652b66d0daa05a190b9ab77db6a19d4b375cd2a
source=(git+https://github.com/deschler/django-modeltranslation.git#tag=${_tag})
sha256sums=('d30458c32f42671df2727efcbc10a17697cdee7c7202b13f192aa9c026fdb957')

pkgver() {
  cd django-modeltranslation
  git describe --tags | sed 's/^v//'
}

build() {
  cd django-modeltranslation
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" django-modeltranslation/dist/*.whl
  install -Dm 644 django-modeltranslation/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/python-django-modeltranslation/
}

# vim: ts=2 sw=2 et:
