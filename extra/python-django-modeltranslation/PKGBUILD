# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-django-modeltranslation
pkgver=0.19.13
pkgrel=1
pkgdesc='Translates Django models using a registration approach'
arch=(any)
url=https://github.com/deschler/django-modeltranslation
license=(BSD)
depends=(python-django)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=3f966d4ed2e1b9590ce05ac0ddf1cc174c65524d
source=(git+https://github.com/deschler/django-modeltranslation.git#tag=${_tag})
sha256sums=('13053d982d904ac7eb9f74a59571a150a10d47a60f4a762c73b8390828ea9f13')

pkgver() {
  cd django-modeltranslation
  git describe --tags | sed 's/^v//'
}

build() {
  cd django-modeltranslation
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" django-modeltranslation/dist/*.whl
  install -Dm 644 django-modeltranslation/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/python-django-modeltranslation/
}

# vim: ts=2 sw=2 et:
