# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-django-modeltranslation
pkgver=0.19.18
pkgrel=2
pkgdesc='Translates Django models using a registration approach'
arch=(any)
url=https://github.com/deschler/django-modeltranslation
license=(BSD-3-Clause)
depends=(python-django)
makedepends=(
  git
  python-build
  python-hatchling
  python-hatch-vcs
  python-installer
  python-setuptools
  python-wheel
)
_tag=eaf426f249f3f7cd42c374f01e55cca1d10580ab
source=(git+https://github.com/deschler/django-modeltranslation.git#tag=${_tag})
sha256sums=('4956a52ea57093db3dc3ded5892718abcccfced023b34b6ed4a3c758c9714661')

pkgver() {
  cd django-modeltranslation
  git describe --tags | sed 's/^v//'
}

build() {
  cd django-modeltranslation
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" django-modeltranslation/dist/*.whl
  install -Dm 644 django-modeltranslation/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/python-django-modeltranslation/
}

# vim: ts=2 sw=2 et:
