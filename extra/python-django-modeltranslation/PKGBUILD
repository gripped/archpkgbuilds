# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-django-modeltranslation
pkgver=0.19.12
pkgrel=2
pkgdesc='Translates Django models using a registration approach'
arch=(any)
url=https://github.com/deschler/django-modeltranslation
license=(BSD)
depends=(python-django)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=a0528bf6f2fafbc8099cd4cea7502c6118d42eaa
source=(git+https://github.com/deschler/django-modeltranslation.git#tag=${_tag})
sha256sums=('c00a94f645e4693f67685f8760594b7b6ac0c9dbb0f934e136997e6466b5f905')

pkgver() {
  cd django-modeltranslation
  git describe --tags | sed 's/^v//'
}

build() {
  cd django-modeltranslation
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" django-modeltranslation/dist/*.whl
  install -Dm 644 django-modeltranslation/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/python-django-modeltranslation/
}

# vim: ts=2 sw=2 et:
