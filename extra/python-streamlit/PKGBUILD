# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=streamlit
pkgname=python-$_name
pkgver=1.44.1
pkgrel=1
pkgdesc="A faster way to build and share data apps"
arch=(any)
url="https://streamlit.io"
license=(Apache-2.0)
depends=(
  python
  python-altair
  python-blinker
  python-cachetools
  python-click
  python-numpy
  python-packaging
  python-pandas
  python-pillow
  python-protobuf
  python-pyarrow
  python-requests
  python-tenacity
  python-toml
  python-tornado
  python-typing_extensions
  python-watchdog
)
makedepends=(
  git
  nodejs
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(
  # https://github.com/streamlit/streamlit/blob/develop/lib/test-requirements.txt
  python-authlib
  python-graphviz
  python-hypothesis
  python-matplotlib
  python-parameterized
  python-plotly
  python-pytest
  python-requests-mock
  python-rich
  python-seaborn
  python-testfixtures
  # additional optdepends
  python-gitpython
)
optdepends=(
  'python-authlib: for user authentication'
  'python-gitpython: for obtaining information from git repositories'
  'python-graphviz: for graphviz_chart'
  'python-matplotlib: for pyplot figures'
  'python-plotly: for plotly_chart'
  'python-polars: for using Polars dataframes'
  'python-pydeck: for pydeck_chart'
  'python-rich: for syntax highlighting in error logs'
  'python-sqlalchemy: for connecting to SQL databases'
  'python-xarray: for using Xarray datasets'
)
source=(
  # build requires git in frontend/protobuf/scripts/generate-proto.js
  git+https://github.com/streamlit/streamlit#tag=$pkgver
)
b2sums=('9b9759413abc16f185a940acee6dfe2bae2dbdebda06352db50b8e7df00480661295663db6b27b47b0a0fa159c0d2e3e3327faec65722deb3f5d2072060dce18')

# based on https://github.com/streamlit/streamlit/blob/develop/Makefile
build() {
  # initialize corepack
  mkdir corepack-install
  corepack enable --install-directory corepack-install
  cd "$srcdir"/$_name/frontend
  corepack install
  export PATH="$PATH:$srcdir/corepack-install"

  # install frontend dependencies
  cd "$srcdir"/$_name/frontend
  yarn install --immutable

  # recompile protobuf files for Python and the frontend
  cd "$srcdir"/$_name
  protoc \
    --proto_path=proto \
    --python_out=lib \
    proto/streamlit/proto/*.proto
  # TODO: protoc-gen-mypy: program not found or is not executable
  #  --mypy_out=lib \

  # JS/TS protobuf generation
  cd "$srcdir"/$_name/frontend
  yarn workspace @streamlit/protobuf run generate-protobuf

  # build frontend into static files
  yarn workspaces foreach --all --topological run build

  # copy the static files into the Python tree
  cd "$srcdir"/$_name
  cp -ar frontend/app/build/ lib/streamlit/static/

  # build the Python module
  cd "$srcdir"/$_name/lib
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    -m "not performance and not slow"
    # override addopts as they invoke coverage testing
    --override-ini="addopts="
    # ignore tests that require pydeck and cause error during collection
    --ignore tests/streamlit/delta_generator_test.py
    --ignore tests/streamlit/elements/pydeck_test.py
    --ignore tests/streamlit/runtime/caching/cache_data_api_test.py
    --ignore tests/streamlit/runtime/caching/cache_resource_api_test.py
    --ignore tests/streamlit/runtime/fragment_test.py
    --ignore tests/streamlit/runtime/state/widgets_test.py
    --ignore tests/streamlit/streamlit_test.py
    # sqlite3.OperationalError: table movie already exists
    --deselect tests/streamlit/dataframe_util_test.py::DataframeUtilTest::test_verify_sqlite3_integration
    # AssertionError: assert 'text/xml' == 'application/xml'
    --deselect tests/streamlit/web/server/app_static_file_handler_test.py::AppStaticFileHandlerTest::test_static_files_with_safe_extensions_200_05_xml
    # AssertionError: assert 'application/octet-stream' == 'font/woff2'  (etc.)
    --deselect tests/streamlit/web/server/app_static_file_handler_test.py::AppStaticFileHandlerTest::test_static_files_with_safe_extensions_200_06_woff2
    --deselect tests/streamlit/web/server/app_static_file_handler_test.py::AppStaticFileHandlerTest::test_static_files_with_safe_extensions_200_07_woff
    --deselect tests/streamlit/web/server/app_static_file_handler_test.py::AppStaticFileHandlerTest::test_static_files_with_safe_extensions_200_08_ttf
    --deselect tests/streamlit/web/server/app_static_file_handler_test.py::AppStaticFileHandlerTest::test_static_files_with_safe_extensions_200_09_otf
  )

  cd $_name/lib
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name/lib
  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -vf "$pkgdir"/usr/bin/streamlit.cmd
}
