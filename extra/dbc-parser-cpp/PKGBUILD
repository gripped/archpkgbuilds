# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=dbc-parser-cpp
pkgver=0.3.0
pkgrel=1
pkgdesc='A DBC file parsing library written in C++'
arch=(x86_64)
license=(MIT)
url='https://github.com/LinuxDevon/dbc_parser_cpp'
depends=()
makedepends=(cmake
             fast_float)
source=(https://github.com/LinuxDevon/dbc_parser_cpp/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('ef95837de4aadb21be18ff76d3be68832da86a328fbfadd88606bc876bfef323')

build() {
  cmake -B build -S ${pkgname//-/_}-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_INCLUDEDIR=include \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -DDBC_ENABLE_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 ${pkgname//-/_}-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
