# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2024-12-16
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc='Rust compiler front-end for IDEs'
arch=(x86_64)
url=https://rust-analyzer.github.io/
license=('MIT OR Apache-2.0')
depends=(
  gcc-libs
  rust-src
)
makedepends=(git)
source=("git+https://github.com/rust-lang/$pkgname.git#tag=$_pkgver")
b2sums=('4c3622f192cc1452cf87e4d442456699715a47bfc8a30ae698ac6d803a680f7a866f67482f40ee05db7fd05317a22549be00946d90d2db7149f555bcce3de696')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  cd $pkgname
  CFG_RELEASE=1 cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
