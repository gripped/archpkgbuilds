# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2026-02-23
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc='Rust compiler front-end for IDEs'
arch=(x86_64)
url=https://rust-analyzer.github.io/
license=('MIT OR Apache-2.0')
depends=(
  gcc-libs
  rust-src
)
makedepends=(git)
source=("git+https://github.com/rust-lang/$pkgname.git#tag=$_pkgver")
b2sums=('521daf795095fb75f17c6c9c20026bef964a3c06af92734041289690d9a1379e2748ad81bb388d115ee2d910ca5ae0a342e4c1125e212b2bb134ac910f9315c5')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  cd $pkgname
  CFG_RELEASE=1 cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
