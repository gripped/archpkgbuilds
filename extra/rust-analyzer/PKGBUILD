# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2026-01-26
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc='Rust compiler front-end for IDEs'
arch=(x86_64)
url=https://rust-analyzer.github.io/
license=('MIT OR Apache-2.0')
depends=(
  gcc-libs
  rust-src
)
makedepends=(git)
source=("git+https://github.com/rust-lang/$pkgname.git#tag=$_pkgver")
b2sums=('b62010e23c17850868b2e622a25805388ed56aa7afad6636f4f3bbaa5e1b3d9d2adc702a5ae6e7e01bba14d8ac20ee95d1e82dfb57ed1b54260b73f336c3284b')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  cd $pkgname
  CFG_RELEASE=1 cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
