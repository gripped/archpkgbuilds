# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2025-03-17
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc='Rust compiler front-end for IDEs'
arch=(x86_64)
url=https://rust-analyzer.github.io/
license=('MIT OR Apache-2.0')
depends=(
  gcc-libs
  rust-src
)
makedepends=(git)
source=("git+https://github.com/rust-lang/$pkgname.git#tag=$_pkgver")
b2sums=('636bd0339540ce331192ce94bd76616ece85716ec28c4444d736f223401c0b6aef04e86cfc5083511d7fbf9e3f3d2c74b57a698a7d02ce2ba8c5b092245a4dca')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  cd $pkgname
  CFG_RELEASE=1 cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
