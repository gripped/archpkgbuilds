# Maintainer: George Rawlinson <george@rawlinson.net.nz>

_pkgname=timescaledb
pkgname=timescaledb-old-upgrade
_current_pg_version=18
_old_pg_version=$(( _current_pg_version - 1 ))
# limit to last 5 releases
declare -gA _commits=(
  [2.21.2]=0ce1bf56e0f3d8e46cbf2aae16067d054dcaa9fe
  [2.21.3]=d4bb1e51d0c0db3beeeee86c8bfbb392fcf272e0
  [2.22.0]=bf4cea89afa9e0a7d3c119e4341435b5dfa97701
  [2.22.1]=687331da33dce1be390d277cbaa0eef62e6ede9d
  [2.23.0]=ba5dcfd1c69f3529d0cc9de3028e1a12a36042ba
)
pkgver=2.23.0
pkgrel=2
pkgdesc='TimescaleDB build for migrating between major versions of PostgreSQL'
arch=('x86_64')
url='https://www.timescale.com/'
license=('Apache-2.0' 'LicenseRef-Timescale')
depends=(
  'glibc'
  'openssl'
  "postgresql-old-upgrade<$_current_pg_version"
  "postgresql-old-upgrade>=$_old_pg_version"
  "timescaledb=$pkgver"
)
makedepends=('gcc' 'cmake' 'git')
source=("$_pkgname::git+https://github.com/timescale/timescaledb#commit=${_commits[$pkgver]}")
sha512sums=('34595c725a95a8d0ae136da4a147f3b0f3d5d359ed9d47b9d47b4e5c045ec8ad5d40f2616b7eb3a1a3dfd77289386b7873afd18c39b0004b4b08a483a0f91242')
b2sums=('6004e7ecc7bd5ab65379e1d9a53126c549a116b03615ad23d5a69c6181a5013d436aa5cd347eccd44b9c03ad21fcaaf8053846034b586c655dac53c55482545e')

prepare() {
  mkdir -p build
}

build() {
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/$_pkgname"

    git checkout "${_commits[$version]}"

    BUILD_DIR="$srcdir/build/$version" ./bootstrap \
      -DWARNINGS_AS_ERRORS=OFF \
      -DREGRESS_CHECKS=OFF \
      -DTAP_CHECKS=OFF \
      -DPG_PATH="/opt/pgsql-${_old_pg_version}"

    # build package or past shared library
    cd "$srcdir/build/$version"

    # ensure reproducible builds (value from `lsb_release -r`)
    # TODO: resolve https://github.com/timescale/timescaledb/issues/3480
    sed \
      -e "s:BUILD_OS_VERSION \".*\"$:BUILD_OS_VERSION \"rolling\":" \
      -i src/config.h

    if [[ $version == $pkgver ]]; then
      make
    else
      make timescaledb timescaledb-tsl sqlfile
    fi
  done
}

package() {
  cd "$_pkgname"
  # install licenses from latest version
  git checkout "${_commits[$pkgver]}"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE LICENSE-APACHE tsl/LICENSE-TIMESCALE

  # install package or past shared library
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/build/$version"
    if [[ $version == $pkgver ]]; then
      make DESTDIR="$pkgdir/" install
    elif (( $(vercmp $version 2.23.0) >= 0 )); then
      install -vDm755 -t "$pkgdir/opt/pgsql-${_old_pg_version}/lib" \
        "src/$_pkgname-$version.so" \
        "tsl/src/$_pkgname-tsl-$version.so" \
        "tsl/src/continuous_aggs/$_pkgname-invalidations-$version.so"
      install -Dm644 -t "$pkgdir/opt/pgsql-${_old_pg_version}/share/extension" \
        "sql/timescaledb--$version.sql"
    else
      install -vDm755 -t "$pkgdir/opt/pgsql-${_old_pg_version}/lib" \
        "src/$_pkgname-$version.so" \
        "tsl/src/$_pkgname-tsl-$version.so"
      install -Dm644 -t "$pkgdir/opt/pgsql-${_old_pg_version}/share/extension" \
        "sql/timescaledb--$version.sql"
    fi
  done
}
