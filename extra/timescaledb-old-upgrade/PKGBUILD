# Maintainer: George Rawlinson <george@rawlinson.net.nz>

_pkgname=timescaledb
pkgname=timescaledb-old-upgrade
_current_pg_version=16
_old_pg_version=$(( _current_pg_version - 1 ))
# limit to last 5 releases
declare -gA _commits=(
  [2.15.0]=19f731d174820dfc0f33e073235c99368b3ad98c
  [2.15.1]=6a8b31f866308362b76287da47473a13d279d4a6
  [2.15.2]=903847e4d69938b8bed8d1d13cb84053e243e262
  [2.15.3]=777f7266e16ae0f3bc2ac5f4ec9d420b5d2ea110
  [2.16.0]=4fd2a878df1dbb807a9e99d01cf5614ebddc02b4
  [2.16.1]=7c78574c4d42ec7ceb840bd9d55652182221f251
)
pkgver=2.16.1
pkgrel=1
pkgdesc='TimescaleDB build for migrating between major versions of PostgreSQL'
arch=('x86_64')
url='https://www.timescale.com/'
license=('Apache-2.0' 'custom:Timescale')
depends=(
  "postgresql-old-upgrade<$_current_pg_version"
  "postgresql-old-upgrade>=$_old_pg_version"
  "timescaledb=$pkgver"
)
makedepends=('gcc' 'cmake' 'git')
source=("$_pkgname::git+https://github.com/timescale/timescaledb#commit=${_commits[$pkgver]}")
sha512sums=('bb93dfe99b8f200d13745e7ac58f76a3393b68ff6cef3168b13de81f6000454f16babe83d14d5466bf8369c6de41b9081dc43c4671a622278fc0fc65b75d45b5')
b2sums=('9243bda4d403e94554f94ba6bc3a5c298dc49b6d0ddc1305edcfcce66d7c4d1ca5ebf59fb1576271c47f9e972007b4cc99e9ae7b9854c25a85b84105d358d8d3')

prepare() {
  mkdir -p build
}

build() {
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/$_pkgname"

    git checkout "${_commits[$version]}"

    BUILD_DIR="$srcdir/build/$version" ./bootstrap \
      -DWARNINGS_AS_ERRORS=OFF \
      -DREGRESS_CHECKS=OFF \
      -DPG_PATH="/opt/pgsql-${_old_pg_version}"

    # build package or past shared library
    cd "$srcdir/build/$version"

    # ensure reproducible builds (value from `lsb_release -r`)
    # TODO: resolve https://github.com/timescale/timescaledb/issues/3480
    sed \
      -e "s:BUILD_OS_VERSION \".*\"$:BUILD_OS_VERSION \"rolling\":" \
      -i src/config.h

    if [[ $version == $pkgver ]]; then
      make
    else
      make timescaledb timescaledb-tsl sqlfile
    fi
  done
}

package() {
  cd "$_pkgname"
  # install licenses from latest version
  git checkout "${_commits[$pkgver]}"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE LICENSE-APACHE tsl/LICENSE-TIMESCALE

  # install package or past shared library
  local version
  for version in "${!_commits[@]}"; do
    cd "$srcdir/build/$version"
    if [[ $version == $pkgver ]]; then
      make DESTDIR="$pkgdir/" install
    else
      install -vDm755 -t "$pkgdir/opt/pgsql-${_old_pg_version}/lib" \
        "src/$_pkgname-$version.so" \
        "tsl/src/$_pkgname-tsl-$version.so"
      install -Dm644 -t "$pkgdir/opt/pgsql-${_old_pg_version}/share/extension" \
        "sql/timescaledb--$version.sql"
    fi
  done
}
