# Maintainer: Christian Hesse <mail@eworm.de>

pkgbase=vis
pkgname=(vis vis-syntax-highlighting)
_commit='43f321d18256842c9aae29faeafa6815c579a9be'
pkgver=0.9.r367.g43f321d1
pkgrel=1
pkgdesc='modern, legacy free, simple yet efficient vim-like editor'
arch=('x86_64')
url='https://github.com/martanne/vis#vis-a-vim-like-text-editor'
depends=('acl' 'libacl.so' 'bash' 'ncurses' 'libncursesw.so' 'libtermkey'
         'libtermkey.so' 'lua' 'tre' 'file')
makedepends=('git')
checkdepends=('vim' 'lua-lpeg')
license=('custom:ISC')
validpgpkeys=('54A47F12462D26A7D078A932C83A52CD1962562C') # Marc Andr√© Tanner <mat@brain-dump.org>
# TODO: can we have signed tag back?
source=("git+https://github.com/martanne/vis.git#commit=${_commit}")
sha256sums=('277955a652701c204e90f1b3dabae91eb505e4dae305bbd2306252a72f117ca3')

_backports=(
)

_reverts=(
)

pkgver() {
  cd vis/

  git describe | sed 's|^v||; s|-|.r|; s|-|.|'
}

prepare() {
  cd vis/

  local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done
}

build() {
  cd vis/

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd vis/

  make -C test/
}

package_vis() {
  optdepends=('wl-clipboard: wayland clipboard support'
              'xclip: X11 clipboard support'
              'xsel: X11 clipboard support'
              'vis-syntax-highlighting: syntax highlighting')
  backup=('etc/vis/visrc.lua')

  cd vis/

  make DESTDIR="${pkgdir}" install

  # move for vis-syntax-highlighting
  mv "${pkgdir}"/usr/share/vis/lexer{s,.lua} "${srcdir}"/

  install -D -m0644 lua/visrc.lua "${pkgdir}"/etc/vis/visrc.lua

  install -D -m0644 'LICENSE' "${pkgdir}/usr/share/licenses/vis/LICENSE"
}

package_vis-syntax-highlighting() {
  pkgdesc='syntax highlighting for vis editor'
  depends=('vis' 'lua-lpeg')

  install -d -m0755 "${pkgdir}"/usr/share/vis/
  mv "${srcdir}"/lexer{s,.lua} "${pkgdir}"/usr/share/vis/
}
