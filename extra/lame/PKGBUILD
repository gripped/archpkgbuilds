# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=lame
pkgver=3.101.r6531
pkgrel=1
pkgdesc='A high quality MPEG Audio Layer III (MP3) encoder'
arch=(x86_64)
url=http://lame.sourceforge.net/
license=(LGPL-2.0-only)
depends=(
  glibc
  mpg123
  ncurses
)
makedepends=(
  nasm
  subversion
)
provides=(libmp3lame.so)
_revision=6531
source=(
  svn+https://svn.code.sf.net/p/lame/svn/trunk/lame#revision=6531
  lame-symbols.patch
  lame-cbr-abr-quality-settings-clamp.patch
)
b2sums=('SKIP'
        'c8cc00f560515231dba1bf9393d056ecb11c2268bafae4923e8c2594b92de473b94e6e5cbef1e52b4843ddd36bb4e06ea6b24ac4a18aa682b2ab7c5ef6deb73c'
        '217cf6611f13cb91997856c3af9c30b7c23fa3744126d27f71768001b6799a4a100c9c0baec34beb656fee1c8c519c86b1a9c549e2ea6befe197eb930f70caaa')

prepare() {
  cd lame
  patch -Np1 -i ../lame-symbols.patch
  patch -Np1 -i ../lame-cbr-abr-quality-settings-clamp.patch
}

pkgver() {
  echo "3.101.r${_revision}"
}

build() {
  cd lame
  export CFLAGS+=" -Wno-implicit-function-declaration -Wno-incompatible-pointer-types"
  ./configure \
    --prefix=/usr \
    --enable-nasm \
    --enable-mp3rtp \
    --enable-shared
  make
}

check() {
  make test -C lame
}

package() {
  make DESTDIR="${pkgdir}" install -C lame
}

# vim: ts=2 sw=2 et:
