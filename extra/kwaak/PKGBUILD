# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=kwaak
pkgver=0.17.0
pkgrel=1
pkgdesc="Run a team of autonomous AI agents from your terminal"
arch=('x86_64')
url="https://github.com/bosun-ai/kwaak"
license=('MIT')
depends=('gcc-libs' 'openssl')
makedepends=('cargo' 'protobuf')
checkdepends=('git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('409919c685e7c399af7cefa5dfbf456c35ffed48e1469fb91b85317e48fd306b2f98cf6cd349e34ad79d7096998a6a0a1c59f19dce986295d3217414688bd04a')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib -- --skip "config::defaults"
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
