# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=kwaak
pkgver=0.16.2
pkgrel=1
pkgdesc="Run a team of autonomous AI agents from your terminal"
arch=('x86_64')
url="https://github.com/bosun-ai/kwaak"
license=('MIT')
depends=('gcc-libs' 'openssl')
makedepends=('cargo' 'protobuf')
checkdepends=('git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('836e8b67ce9bb12d70c7ebc63d6eb735b67a047b32344826e3b37d03431da5e1bf11646206a36afaeeca2cf62d5b6cfa16ccd2da613d5c69512aca1635a2d082')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib -- --skip "config::defaults"
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
