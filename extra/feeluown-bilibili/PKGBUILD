# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-bilibili
pkgver=0.5.3
pkgrel=2
pkgdesc="Bilibili provider for FeelUOwn"
arch=('any')
url="https://github.com/feeluown/feeluown-bilibili"
license=('GPL-3.0-only')
depends=('feeluown' 'python-cachetools' 'python-beautifulsoup4' 'python-pycryptodome')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-bilibili.git#tag=v$pkgver")
sha512sums=('f3874f746d488e37e049ef28bdcd9584f8e783de0c9239a36b6fca7baf1a8f19e799897d179dd4aa8518c8fc703ae3c2c53e294216b812a07ac2940df37bd355')

build() {
  cd feeluown-bilibili
  python -m build -nw
}

package() {
  cd feeluown-bilibili
  python -m installer --destdir="$pkgdir" dist/*.whl
}
