# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-bilibili
pkgver=0.5.1
pkgrel=1
pkgdesc="Bilibili provider for FeelUOwn"
arch=('any')
url="https://github.com/feeluown/feeluown-bilibili"
license=('GPL3')
depends=('feeluown' 'python-cachetools' 'python-beautifulsoup4' 'python-pycryptodome')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-bilibili.git#tag=v$pkgver")
sha512sums=('f7341d1e023a30d73f19a6ee487355203876aa8a2e8d816a1bcfb6c90687517be9b4b24405cfdb781299ada000987756ef2133f1268a03a1cb1351bb9e2d522f')

build() {
  cd feeluown-bilibili
  python -m build -nw
}

package() {
  cd feeluown-bilibili
  python -m installer --destdir="$pkgdir" dist/*.whl
}
