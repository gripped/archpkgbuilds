# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackdocstheme
pkgver=3.3.0
pkgrel=2
pkgdesc="Open Infrastructure Projects Sphinx Themes"
arch=('any')
url="https://opendev.org/openstack/openstackdocstheme"
license=('Apache')
depends=('python-pbr' 'python-dulwich' 'python-sphinx')
makedepends=('git' 'python-setuptools')
options=('!emptydirs')
source=("git+https://github.com/openstack/openstackdocstheme.git#tag=$pkgver")
sha512sums=('5ac92167dbf8e6a9402a7793ce346b156f75ef5e444d84d357a1aa53acc72e368f2eadd461710d652b4263f0df5fd2f355a3194fe63271604d2a4df66f795095')

build() {
  cd openstackdocstheme
  python setup.py build
  PYTHONPATH="$PWD/build/lib" sphinx-build -W -b man doc/source doc/build/man
}

package() {
  cd openstackdocstheme
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 doc/build/man/*.1 -t "$pkgdir"/usr/share/man/man1/
}
