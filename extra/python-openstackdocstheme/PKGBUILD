# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackdocstheme
pkgver=3.5.0
pkgrel=2
pkgdesc="Open Infrastructure Projects Sphinx Themes"
arch=('any')
url="https://opendev.org/openstack/openstackdocstheme"
license=('Apache')
depends=('python-pbr' 'python-dulwich' 'python-sphinx')
makedepends=('git' 'python-setuptools')
options=('!emptydirs')
source=("git+https://github.com/openstack/openstackdocstheme.git#tag=$pkgver")
sha512sums=('1b552da5bd13d5b9501348d1659225a35a9b5de2223f724345a687775ad97c98dbe12fb0498ec52c4b3a2529d788357abacfa4abf620e63b024018b5c6053537')

build() {
  cd openstackdocstheme
  python setup.py build
  PYTHONPATH="$PWD/build/lib" sphinx-build -W -b man doc/source doc/build/man
}

package() {
  cd openstackdocstheme
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 doc/build/man/*.1 -t "$pkgdir"/usr/share/man/man1/
}
