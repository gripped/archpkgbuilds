# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackdocstheme
pkgver=3.4.1
pkgrel=1
pkgdesc="Open Infrastructure Projects Sphinx Themes"
arch=('any')
url="https://opendev.org/openstack/openstackdocstheme"
license=('Apache')
depends=('python-pbr' 'python-dulwich' 'python-sphinx')
makedepends=('git' 'python-setuptools')
options=('!emptydirs')
source=("git+https://github.com/openstack/openstackdocstheme.git#tag=$pkgver")
sha512sums=('5ba64c5f4d02c22a1795de576ae440eb35c690e3a62875dfa554892d874f03ad6f3f41e7b95866fdf02b2025e03298d1cf5ba24a07709f27c61e62c2ad63e41c')

build() {
  cd openstackdocstheme
  python setup.py build
  PYTHONPATH="$PWD/build/lib" sphinx-build -W -b man doc/source doc/build/man
}

package() {
  cd openstackdocstheme
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 doc/build/man/*.1 -t "$pkgdir"/usr/share/man/man1/
}
