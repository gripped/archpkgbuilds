# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Hoàng Văn Khải <hvksmr1996@gmail.com>

pkgname=parallel-disk-usage
_pkgname=pdu
pkgver=0.21.1
pkgrel=2
pkgdesc='Summarize disk usage of the set of files, recursively for directories'
arch=('x86_64')
url='https://github.com/KSXGitHub/parallel-disk-usage'
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('da4b81d7bc34f3c7dff26e7408003a0cce8f43481e9933cc81ce64f39b95b26d8b3ea184ec138a27c8f16b9b7f566f01636dd6435e13ee096505dcbd2f55e060')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --bin "$_pkgname"
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 exports/completion.bash "$pkgdir/usr/share/bash-completion/completions/pdu"
  install -Dm644 exports/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/pdu.fish"
  install -Dm644 exports/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_pdu"
}

# vim: ts=2 sw=2 et:
