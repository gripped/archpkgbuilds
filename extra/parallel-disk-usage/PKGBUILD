# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Hoàng Văn Khải <hvksmr1996@gmail.com>

pkgname=parallel-disk-usage
_pkgname=pdu
pkgver=0.11.1
pkgrel=1
pkgdesc='Summarize disk usage of the set of files, recursively for directories'
arch=('x86_64')
url='https://github.com/KSXGitHub/parallel-disk-usage'
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('a1328d10f99225172b0c00dc00a43cd3cdd262036f457400f41f09c2ba65e452b09258d6ec8aa3e6bb487a11cebc2e3aa25455a8ddc6d18c7c747bd707733288')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --bin "$_pkgname"
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 exports/completion.bash "$pkgdir/usr/share/bash-completion/completions/pdu"
  install -Dm644 exports/completion.fish "$pkgdir/usr/share/fish/completions/pdu.fish"
  install -Dm644 exports/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_pdu"
}

# vim: ts=2 sw=2 et:
