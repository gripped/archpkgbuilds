# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Hoàng Văn Khải <hvksmr1996@gmail.com>

pkgname=parallel-disk-usage
_pkgname=pdu
pkgver=0.12.0
pkgrel=1
pkgdesc='Summarize disk usage of the set of files, recursively for directories'
arch=('x86_64')
url='https://github.com/KSXGitHub/parallel-disk-usage'
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('c9fadf258183e3f167b8d73c24c57ea86aecbfe2128a05625755f7b66845829cba99a84461f0c3739868f2afcb56952ef9f02e4d1435d6141faf1b36ecd68b6d')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --bin "$_pkgname"
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 exports/completion.bash "$pkgdir/usr/share/bash-completion/completions/pdu"
  install -Dm644 exports/completion.fish "$pkgdir/usr/share/fish/completions/pdu.fish"
  install -Dm644 exports/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_pdu"
}

# vim: ts=2 sw=2 et:
