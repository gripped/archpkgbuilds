# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.6.4
pkgrel=4
pkgdesc="An e-mail client for GNOME"
arch=('x86_64')
url='https://pawsa.fedorapeople.org/balsa/'
license=('GPL-2.0-or-later')
depends=('gmime3' 'gpgme' 'gtksourceview4' 'libcanberra' 'libical' 'webkit2gtk-4.1')
makedepends=('meson' 'yelp-tools')
changelog=$pkgname.changelog
source=(https://pawsa.fedorapeople.org/balsa/$pkgname-$pkgver.tar.xz
        balsa-webkit2gtk-4.1.patch
        balsa-i18n.patch
        balsa-gettext.patch)
sha256sums=('befa5984511db33d41f2b1ecbfc99e22a15d45d08efe5d737b5174a1a6ac8fc1'
            'd4442696152979a13a93a5c8e4d54bda2f2f2e796a749e759b279d05809a307b'
            'a319211f4b3da8368b6f4a629a158716d43f7142c81325d052709e37bb11c25d'
            'cd08d6b5bd442a4d75200f4bda93f0644f85119290a3f4911fd838ba1d0eb8bf')

prepare() {
  cd $pkgname-$pkgver

  # Build with webkit2gtk-4.1
  patch -Np1 -i ../balsa-webkit2gtk-4.1.patch

  # Fix native language support
  patch -Np1 -i ../balsa-i18n.patch

  # Migrate from Intltool to Gettext (fixes metainfo file)
  patch -Np1 -i ../balsa-gettext.patch

  # Add missing file
  echo 'cs de el es fr sl' > doc/LINGUAS

  # Don't try to run gtk-update-icon-cache during packaging
  sed -i 's/gtk_update_icon_cache_program.found()/false/' images/meson.build
}

build() {
  arch-meson $pkgname-$pkgver build \
    -D canberra=true \
    -D gss=true \
    -D gtksourceview=true \
    -D ldap=true \
    -D rubrica=true \
    -D sqlite=true \
    -D help-files=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
