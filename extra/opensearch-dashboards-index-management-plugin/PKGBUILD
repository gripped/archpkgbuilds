# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-index-management-plugin'
_pluginname='index-management-dashboards-plugin'
pkgver=2.18.0.0
_dashboardsver=2.18.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Index Management Plugin'
url='https://opensearch.org/docs/latest/im-plugin/index/'
arch=('x86_64')
license=('Apache-2.0')
depends=("opensearch-dashboards=${_dashboardsver}" "opensearch-dashboards-notifications-plugin")
makedepends=('yarn' 'npm' 'python' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/${_pluginname}.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
)
sha256sums=('f01ec4aec67bac73b258dc57c13bc2507a1e868df4807823c00390db9ca17a27'
            '047ea8d8669958d1cd16ee4c6c417088b7295dcf83a7efe855e9b32903eb02e1')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" \
    "${_pluginname}/package.json" \
    "OpenSearch-Dashboards/package.json"
}

build() {
  mv "${_pluginname}" "OpenSearch-Dashboards/plugins"
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn osd bootstrap
  yarn plugin-helpers build --skip-archive # `yarn build` will always create the ZIP
}

check() {
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn test:jest -u
}

package() {
  cd "${srcdir}/OpenSearch-Dashboards/plugins/${_pluginname}"

  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
