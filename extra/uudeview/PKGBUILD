# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: kmaster <kmaster12@sasktel.net>

pkgname=uudeview
pkgver=20230502
pkgrel=2
_commit=186f9543e6c7d3fa17ac13906b130cbab8ee9aea
pkgdesc="UUDeview helps transmitting & receiving binary files using mail or newsgroups. Includes library files."
arch=('x86_64')
url="https://github.com/hannob/uudeview"
license=('GPL')
makedepends=('git')
optdepends=('tk: for GUI')
options=('staticlibs' '!strip')
source=("git+https://github.com/hannob/uudeview.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
  cd "$srcdir"/$pkgname/uulib
  autoreconf
  ./configure --prefix=/usr
  make

  cd "$srcdir"/$pkgname
  autoreconf
  ./configure --prefix=/usr --mandir=/usr/share/man CFLAGS="-ffat-lto-objects"
  make
}

package() {
  cd "$srcdir"/$pkgname
  make DESTDIR="$pkgdir" install
  strip "$pkgdir"/usr/bin/*

  cd "$srcdir"/$pkgname/uulib
  install -d "$pkgdir"/usr/include/uudeview
  cp -v *.h "$pkgdir"/usr/include/uudeview
  install -d "$pkgdir"/usr/lib
  cp -v *.a "$pkgdir"/usr/lib/
}
