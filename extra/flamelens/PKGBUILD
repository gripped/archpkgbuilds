# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=flamelens
pkgver=0.4.0
pkgrel=1
pkgdesc="Flamegraph viewer in the terminal "
arch=('x86_64')
url="https://github.com/YS-L/flamelens"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('142a945a1f132b533ceaea77409098b63ab01508b2df63d92974611c596ba82f524825e28c56bb335bc2255c6574401ddfc8850b2cfd6ee971f0307894ee08fe')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --no-default-features
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
