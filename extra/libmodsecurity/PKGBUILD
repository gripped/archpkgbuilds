# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libmodsecurity
pkgver=3.0.14
pkgrel=2
epoch=1

pkgdesc='ModSecurity v3 library component'
arch=('x86_64')
depends=('yajl' 'curl' 'lmdb' 'libxml2' 'pcre2' 'geoip' 'libmaxminddb' 'ssdeep' 'luajit')
makedepends=('doxygen')
url="https://github.com/owasp-modsecurity/ModSecurity/tree/v3/master"
license=('Apache-2.0')
source=("https://github.com/owasp-modsecurity/ModSecurity/releases/download/v$pkgver/modsecurity-v$pkgver.tar.gz"{,.sig}
        libxml-includes.patch)
sha256sums=('f7599057b35e67ab61764265daddf9ab03c35cee1e55527547afb073ce8f04e8'
            'SKIP'
            '7ee0adbe5b164ca512c49e51e30ffd41e29244156a695e619dcf1d0387e69aef')
validpgpkeys=('0B2BA1924065B44691202A2AD286E022149F0F6E') # OWASP ModSecurity <modsecurity@owasp.org>

prepare() {
	cd "$srcdir"/modsecurity-v$pkgver
	patch -p1 -i ../libxml-includes.patch
}

build() {
	cd "$srcdir"/modsecurity-v$pkgver
	sed -e 's/luajit-2.0/luajit-2.1/g' \
		-e 's/LUA_POSSIBLE_LIB_NAMES="/LUA_POSSIBLE_LIB_NAMES="luajit /g' \
		-i build/lua.m4
	./build.sh
	./configure \
		--prefix=/usr \
		--with-lmdb \
		--with-libxml \
		--with-lua \
		--with-pcre2 \
		--with-ssdeep \
		--disable-examples
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd "$srcdir"/modsecurity-v$pkgver
	make DESTDIR="$pkgdir" install
}
