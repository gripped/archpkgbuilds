# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=d2
pkgver=0.7.1
pkgrel=1
pkgdesc='A modern diagram scripting language that turns text to diagrams'
arch=('x86_64')
url='https://d2lang.com'
license=('MPL-2.0')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/terrastruct/d2#tag=v$pkgver")
sha512sums=('f7a61ef8d96b553a503c87f1872bf01ce729181e10215d071759fb6cb0e05153b1001dd3567502a0c924ff928d949dd758fec53e616f52c7e03d00870bb67b84')
b2sums=('ccbadf5b07479692174a12c7a1918094e3572bba940b5106ef01baf2de589b6c82acf1dcd3170f7e8fe54da672f303d573001b0ef5727c6c2d61e1fb0edc6992')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X oss.terrastruct.com/d2/lib/version.Version=$pkgver" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/d2

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" ci/release/template/man/d2.1

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
