# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=egl-wayland2
pkgver=1.0.0.rc.r51.gada1c37
pkgrel=1
pkgdesc="EGLStream-based Wayland external platform (2)"
url="https://github.com/NVIDIA/egl-wayland2"
arch=(x86_64)
license=(Apache-2.0)
depends=(
  eglexternalplatform
  mesa
  glibc
  libdrm
  wayland
)
makedepends=(
  git
  'libglvnd>=1.3.4'
  meson
  wayland-protocols
)
provides=(libnvidia-egl-wayland2.so)
_commit=ada1c37041002f4f009a33d6d285eae39bf9ad02
source=("git+$url#tag=$_commit")
b2sums=('29e1c8ada3a06b0d62a9328b833a7a8b382f77bcbe81bcdaacccbda81a48cc1cfbb75318e5288ea18b1cd452abca1878410c4ff7227bf63845380fbace7bbd53')

pkgver() {
  cd $pkgname
  git describe --long --tags | sed -E 's/^v//;s/-0-g[0-9a-f]+$//;s/-([0-9]+)-g/.r\1.g/;s/-/./g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/LICENSE
}

# vim:set sw=2 sts=-1 et:
