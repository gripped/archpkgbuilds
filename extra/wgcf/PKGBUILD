# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: w0rty <mawo97 at gmail.com>

pkgname=wgcf
pkgver=2.2.18
pkgrel=2
pkgdesc='Generate WireGuard profile from Cloudflare Warp account'
arch=('x86_64')
url="https://github.com/ViRb3/wgcf"
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/ViRb3/wgcf.git#tag=v$pkgver")
sha256sums=('1482b74bc97383f7be5a2879aee9580f6cfeec1d796642073fb9c9fe60417b54')

prepare(){
  cd $pkgname
  mkdir -p build/
}

build() {
  export GOPATH="$srcdir"/gopath
  cd $pkgname
  # Buildflags according to https://wiki.archlinux.org/index.php/Go_package_guidelines#Flags_and_build_options
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  go build -o build -trimpath -buildmode=pie -ldflags "-linkmode=external -extldflags \"${LDFLAGS}\" -s -w" -modcacherw

  build/$pkgname completion bash > build/$pkgname.bash
  build/$pkgname completion zsh > build/$pkgname.zsh
  build/$pkgname completion fish > build/$pkgname.fish
}

package() {
  cd $pkgname
  install -Dm755 build/$pkgname -t "$pkgdir"/usr/bin/
  install -Dm644 build/$pkgname.bash "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  install -Dm644 build/$pkgname.zsh "$pkgdir"/usr/share/zsh/site-functions/_$pkgname
  install -Dm644 build/$pkgname.fish "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgname.fish
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
