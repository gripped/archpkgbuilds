# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=nvshmem
pkgver=3.5.19
_cudaver=13
pkgrel=1
pkgdesc="NVIDIA parallel programming interface based on OpenSHMEM"
arch=(x86_64)
url="https://developer.nvidia.com/nvshmem"
license=(LicenseRef-NVIDIA-NVSHMEM)
depends=(
  cuda
  gcc-libs
  glibc
  hwloc
)
optdepends=(
  'libfabric: For OpenFabrics transport plugin'
  'openmpi: For MPI and OpenSHMEM bootstrap plugins'
  'openpmix: For PMI bootstrap plugins'
  'openucx: For UCX transport plugin'
  'rdma-core: For IBGDA transport plugin'
)
options=(!strip)
source=(https://developer.download.nvidia.com/compute/nvshmem/redist/libnvshmem/linux-x86_64/lib${pkgname}-linux-x86_64-${pkgver}_cuda${_cudaver}-archive.tar.xz)
b2sums=('18701fb9f5cd230f84fc93bf87b9f93dc0f66892ad352e014c02c64c9fae5493adef5ea646f586b6b6d62ca2b5979f605d3478dbe1cd990bf6588b9f4bfdfab2')

package() {
  cd libnvshmem-linux-x86_64-${pkgver}_cuda${_cudaver}-archive

  install -vdm 755 "$pkgdir"/usr/
  cp -arv bin lib "$pkgdir"/usr/

  # remove static libs
  rm "$pkgdir"/usr/lib/*.a

  # move bin subdirs into "namespaced" directory
  install -vdm 755 "$pkgdir"/usr/bin/$pkgname/
  mv -v "$pkgdir"/usr/bin/{examples,perftest} "$pkgdir"/usr/bin/$pkgname/

  # install headers into "namespaced" directory
  install -vdm 755 "$pkgdir"/usr/include/
  cp -arv include "$pkgdir"/usr/include/$pkgname

  # fix INCLUDE_DIRS in cmake config file
  sed -i 's|${PACKAGE_PREFIX_DIR}/include|${PACKAGE_PREFIX_DIR}/include/'"$pkgname"'|' "$pkgdir"/usr/lib/cmake/nvshmem/NVSHMEMConfig.cmake

  # install sources
  install -vdm 755 "$pkgdir"/usr/share/$pkgname/
  cp -arv share/src/ "$pkgdir"/usr/share/$pkgname/

  # install license
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
