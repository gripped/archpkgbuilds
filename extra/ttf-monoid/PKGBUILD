# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Severen Redwood <severen@shrike.me>

pkgname=ttf-monoid
_pkgname=${pkgname#ttf-}
pkgver=0.61
pkgrel=9
pkgdesc='A customisable coding font'
arch=(any)
url='https://larsenwork.com/monoid'
license=('MIT OR OFL-1.1')
makedepends=(
  python
  fontforge
)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/larsenwork/monoid/archive/$pkgver.tar.gz"
  fontbuilder-python3.patch
)
sha512sums=('2acae11b3d0a86ac1d0588de986fbcf13a83bdcb857b0eb1deaeae55615fdccbfd8f19b5618ee61abd1261c3f203cadcaa7b37f44f535a3fe462240602ab51dc'
            '03f6bcc996673472eb72a2134dd77297f65f2dc1f89af27ae763e7c47a4b4dfa118ab08dba2171c351df95bd1b145980c837e8c211ef94a9b2308ce64dccad4b')
b2sums=('d9e014dcb3787958e2c9cd2e5429ad6f680e41465f4a278ded2b13e578473fd9363e0c295119ca5a907b4d8d8c579b1536a641ad4e8922dedac0528090a42883'
        'c27c9272cee283c0a717c72444486622594228ff4b56a478cebf3fff089a1d8ad42dfa86f53c7dfcdd617654acbb5a280c1e674a13501e679f2f752ea3c63baf')


prepare () {
  cd "$_pkgname-$pkgver"

  # extract LICENSE from README
  sed -n '/Monoid is dual licensed/,/OTHER DEALINGS IN THE FONT SOFTWARE./p' \
    Readme.md > \
    LICENSE

  # patch fontbuilder script
  patch -d Scripts -p1 -i "$srcdir/fontbuilder-python3.patch"
}

build () {
  cd "$_pkgname-$pkgver"

  # ensure python can find fontbuilder module
  export PYTHONPATH="$(pwd)/Scripts"

  # list of fonts to build
  # note: Monoid-Diacritic variants are buggy, so they are skipped
  local FONT_FILES=(
    "Source/Monoid.sfdir"
    "Source/Monoid-Bold.sfdir"
    "Source/Monoid-Italic.sfdir"
    "Source/Monoid-Retina.sfdir"
    "Monoisome/Monoisome.sfdir"
  )

  # build fonts
  for FONT_FILE in "${FONT_FILES[@]}"; do
    python3 -c "import fontbuilder; fontbuilder.build('_build', '"${FONT_FILE}"');"
  done
}

package () {
  cd "$_pkgname-$pkgver"

  # fonts
  install -vDm644 -t "$pkgdir/usr/share/fonts/TTF" _build/*.ttf

  # license
  install -vDm644 -t  "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
