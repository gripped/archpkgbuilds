# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mahjongg
pkgver=47.0
pkgrel=2
pkgdesc="Disassemble a pile of tiles by removing matching pairs"
url="https://wiki.gnome.org/Apps/Mahjongg"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-mahjongg.git?signed#tag=$pkgver")
b2sums=('bb22efa2e3d0d8b3ced38eb538623c92c627dc80f54f8a01111c50a726c4c0f609b7484452bdc030391781854a8f37a8611e8b7217050330594575eaf71fbafb')
validpgpkeys=(
  FAE80BEA85C476AF1BACC2A77B274B850A9296D4 # Mat <mail@mathias.is>
)

prepare() {
  cd $pkgname

  # Don't create new game when activating existing instance
  git cherry-pick -n 74d35400bcf98d50646d67f737389859fbf1204d
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  # NOTE: appstreamcli validate fails due to duplicated tags caused by a regression in gettext 0.23
  # https://gitlab.archlinux.org/archlinux/packaging/packages/gettext/-/issues/4
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
