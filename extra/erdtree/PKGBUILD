# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=erdtree
pkgver=3.1.2
pkgrel=3
pkgdesc='A multi-threaded filesystem and disk-usage analysis tool'
arch=(x86_64)
url='https://github.com/solidiquis/erdtree'
license=(MIT)
depends=(glibc libgcc)
makedepends=(git rust)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('e70d1c5ed8803427032e888e41fa947a1412bb0ab963cde26bfdd1318841de435164c5818b94d84549c0bd4de076d5d3ead9cacfd8c13bc602f493c9d28550dd')
b2sums=('79acefbcc07f014708e33a22153614f16e3ea751bc7521ef27cef07924d034f77ffec027afb0786600e74bf381dc74ba3eae9112752e24266d755e5ecc2a3a7c')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features

  # completions
  cd target/release
  for shell in bash fish zsh; do
    ./erd --completions "$shell" > "$shell-completion"
  done

}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/erd

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr assets "$pkgdir/usr/share/doc/$pkgname"

  # completions
  pushd target/release
  install -vDm644 bash-completion "$pkgdir/usr/share/bash-completion/completions/erd"
  install -vDm644 fish-completion "$pkgdir/usr/share/fish/vendor_completions.d/erd.fish"
  install -vDm644 zsh-completion "$pkgdir/usr/share/zsh/site-functions/_erd"
  popd

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
