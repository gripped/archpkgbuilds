# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor:  Konstantin Gorodinskii <mail@konstantin.io>

pkgname=wabt
pkgdesc="The WebAssembly Binary Toolkit is a suite of tools for WebAssembly"
url="https://github.com/WebAssembly/wabt"
pkgver=1.0.36
pkgrel=1
arch=('x86_64')
license=('APACHE-2.0')
depends=(glibc gcc-libs)
makedepends=('cmake' 'git' 'gcc' 'clang' 'python-ply' 'gtest')
source=("git+https://github.com/WebAssembly/wabt#tag=$pkgver"
        "git+https://github.com/WebAssembly/testsuite"
        "git+https://github.com/google/googletest"
        "git+https://github.com/dabeaz/ply")
validpgpkeys=('5DE3E0509C47EA3CF04A42D34AEE18F83AFDEB23')
sha512sums=('2c7a31932a9a30614f0ceedb136dfc87f4399d7f80b2a7d5a9dfcd19d5efa9aff3dd5fb469661d309ae32426b2232b5d0799b44d7301758d9660e635bcfe5d66'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd ${pkgname}

  git submodule init
  git config submodule."external/testsuite".url "${srcdir}/testsuite"
  git config submodule."external/googletest".url "${srcdir}/googletest"
  git config submodule."external/ply".url "${srcdir}/ply"
  git submodule update --recursive

  mkdir build
}

build() {
  cd ${pkgname}/build
  export CFLAGS+=" -ffat-lto-objects"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..

  make
}

#check() {
#  cd ${pkgname}
#  make test
#}

package() {
  cd ${pkgname}/build
  make DESTDIR="${pkgdir}" install
}
