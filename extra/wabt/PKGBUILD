# Maintainer: Quentin Michaud <mh4ckt3mh4ckt1c4s@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>
# Contributor:  Konstantin Gorodinskii <mail@konstantin.io>

pkgname=wabt
pkgdesc="The WebAssembly Binary Toolkit: a suite of tools for WebAssembly"
url="https://github.com/WebAssembly/wabt"
pkgver=1.0.39
pkgrel=1
arch=('x86_64')
license=('Apache-2.0')
depends=(glibc gcc-libs)
makedepends=('cmake' 'git' 'gcc' 'clang' 'python-ply' 'gtest' 'ninja')
source=("git+https://github.com/WebAssembly/wabt#tag=$pkgver"
        "git+https://github.com/WebAssembly/testsuite"
        "git+https://github.com/google/googletest"
        "git+https://github.com/dabeaz/ply")
validpgpkeys=('5DE3E0509C47EA3CF04A42D34AEE18F83AFDEB23')
sha512sums=('c11f6b1306c0390b79d596535eb1a5bed3126647f5aa12568a18777731e140ba26a1927bd3803c730afa81d5461b128c7946edd3dcc1616b0ccd142ae8f63d4a'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd ${pkgname}

  git submodule init
  git config submodule."external/testsuite".url "${srcdir}/testsuite"
  git config submodule."external/googletest".url "${srcdir}/googletest"
  git config submodule."external/ply".url "${srcdir}/ply"
  git submodule update --recursive

  mkdir build
}

build() {
  cd ${pkgname}/build
  export CFLAGS+=" -ffat-lto-objects"
  cmake \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF \
    ..

  ninja
}

#check() {
#  cd ${pkgname}
#  make test
#}

package() {
  cd ${pkgname}/build
  DESTDIR="${pkgdir}" ninja install
}
