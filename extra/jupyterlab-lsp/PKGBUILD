# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Specter119 <spcter119 AT gmail.com>

pkgname=jupyterlab-lsp
pkgver=5.1.1
pkgrel=1
pkgdesc="Coding assistance for JupyterLab with Language Server Protocol."
arch=(any)
url="https://github.com/jupyter-lsp/jupyterlab-lsp"
license=(BSD-3-Clause)
depends=(
  python
  jupyter-lsp
  jupyterlab
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
  npm
  yarn
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('be92f103271a030648c39357a93e0a2dd11c28454b06bc55c80d97ea52275d0ffac18e4adf692c266ecaa5d348392ea4c513d90846cfe6b737b9e030f893a6e4')

build() {
  cd $pkgname-$pkgver/python_packages/jupyterlab_lsp
  jlpm install
  jlpm build
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver/python_packages/jupyterlab_lsp
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
