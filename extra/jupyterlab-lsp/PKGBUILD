# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Specter119 <spcter119 AT gmail.com>

pkgname=jupyterlab-lsp
pkgver=5.2.0
pkgrel=2
pkgdesc="Coding assistance for JupyterLab with Language Server Protocol."
arch=(any)
url="https://github.com/jupyter-lsp/jupyterlab-lsp"
license=(BSD-3-Clause)
depends=(
  python
  jupyter-lsp
  jupyterlab
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
  npm
  yarn
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('26cfa09e278847f08cd8451b07b65d8529f9c30e7f0e2d5ca5f31daacdc9fbadbd7256c57f850529df1efcf7435f32d9e561ba2e9cc2542166092c730aa3597f')

build() {
  cd $pkgname-$pkgver/python_packages/jupyterlab_lsp
  jlpm install
  jlpm build
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver/python_packages/jupyterlab_lsp
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
