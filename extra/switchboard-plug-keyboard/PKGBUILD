# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-keyboard
pkgver=8.0.1
pkgrel=2
pkgdesc='Switchboard Keyboard Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-keyboard
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  libibus
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
  libxml2
)
makedepends=(
  git
  meson
  vala
)
_tag=6e63405a36c2e1dc56d231624bf2501ba4503c74
source=(git+https://github.com/elementary/switchboard-plug-keyboard.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-keyboard
  git describe --tags
}

build() {
  arch-meson switchboard-plug-keyboard build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
