# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-keyboard
pkgver=8.1.0
pkgrel=1
pkgdesc='Switchboard Keyboard Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-keyboard
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  libibus
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
  libxml2
)
makedepends=(
  git
  meson
  vala
)
_tag=39d602893c061d54740b9565e43f467acd67b351
source=(git+https://github.com/elementary/switchboard-plug-keyboard.git#tag=${_tag})
b2sums=('576d1f2707ab781352820324d8ee79e8a20ffa59d3a3d74d6ef3a0b7c39511042bc7b3463d87297af61af816dfd1b25de2df42f1f9d049e829a4300999bc2dc3')

pkgver() {
  cd switchboard-plug-keyboard
  git describe --tags
}

build() {
  arch-meson switchboard-plug-keyboard build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
