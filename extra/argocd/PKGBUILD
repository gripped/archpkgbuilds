# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=argocd
pkgver=3.0.12
# find matching argo-ui commit via ui/yarn.lock
# commit id also needs to be updated in argo-ui.patch
_argoui_commit=5cf36101733ce43eed57242a12389f2a7e40bd2b
pkgrel=1
pkgdesc="Declarative continuous deployment for Kubernetes"
arch=('x86_64')
url='https://argo-cd.readthedocs.io/en/stable/'
license=('Apache-2.0')
makedepends=('go' 'python' 'yarn')
depends=('glibc')
provides=('argocd-cli')
replaces=('argocd-cli')
source=("argo-cd-${pkgver}.tar.gz::https://github.com/argoproj/argo-cd/archive/v${pkgver}.tar.gz"
        "argo-ui-${_argoui_commit}.tar.gz::https://github.com/argoproj/argo-ui/archive/${_argoui_commit}.tar.gz"
        "argo-ui.patch")
sha512sums=('50182e71ec3cf79203427fbc135fb5292d788fb28e63cf07beb8f10e9042861740f9eea3ca2e7629a71fcfc9646e82de67891139366bad91766ee80393c9674e'
            '81fa3cca8c20d57330867a3d7ba0fb579a23fb898be53a8947d6466f3aa909579c75b0c70dd0beb21993e2f7ccc05c62248b20a4334111066801ff094742e20d'
            '4bf8c4b4d7b9b828968cfa501b2ab50747d0aaf9d3909538b25ede0c85d778aa4e8fe06fa12f8b94316fe1161aab531a6b26a2dab249c5cad97737172336a259')

prepare() {
  cd "argo-cd-$pkgver"

  patch -p0 -i ../argo-ui.patch

  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "argo-cd-$pkgver"

  (
    export YARN_CACHE_FOLDER="${srcdir}/yarn-cache"
    cd ui
    yarn install
    ARGO_VERSION="$pkgver" NODE_ENV='production' NODE_ONLINE_ENV='online' yarn run build
  )

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  export GOPROXY=off
  go build -o argocd \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-X github.com/argoproj/argo-cd/v3/common.version=${pkgver} \
              -X github.com/argoproj/argo-cd/v3/common.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
              -X github.com/argoproj/argo-cd/v3/common.gitTag=${pkgver} \
              -compressdwarf=false \
              -linkmode=external \
              -extldflags \"${LDFLAGS}\"" \
    ./cmd

  ./argocd completion bash > argocd.bash
  ./argocd completion zsh > argocd.zsh
}

package() {
  cd "argo-cd-$pkgver"

  install -D argocd "$pkgdir/usr/bin/argocd"
  ln -s argocd "$pkgdir/usr/bin/argocd-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-application-controller"
  ln -s argocd "$pkgdir/usr/bin/argocd-repo-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-cmp-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-dex"
  ln -s argocd "$pkgdir/usr/bin/argocd-notifications"
  ln -s argocd "$pkgdir/usr/bin/argocd-git-ask-pass"

  install -Dm644 argocd.bash "$pkgdir/usr/share/bash-completion/completions/argocd"
  install -Dm644 argocd.zsh "$pkgdir/usr/share/zsh/site-functions/_argocd"
}
