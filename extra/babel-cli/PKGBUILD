# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

pkgname=babel-cli
pkgver=7.27.2
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs' 'babel-core' 'semver')
makedepends=('npm')
provides=('nodejs-babel-cli')
conflicts=('nodejs-babel-cli' 'openbabel<3')
replaces=('nodejs-babel-cli')
source=("$pkgname-$pkgver.tgz::https://registry.npmjs.org/@babel/cli/-/cli-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('71f77b0e71a5847e8e232b8f4928f7bdc7c87676d7ba4840c8a63c35a66b15a742ee95f22fd98e2f95a08dca6d88424b8b99378fc698bcb2150b37b3ad64da88')

package() {
  npm install -g --legacy-peer-deps --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  tar xf $pkgname-$pkgver.tgz package/LICENSE
  install -Dm644 package/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -r "$pkgdir"/usr/lib/node_modules/@babel/cli/node_modules/{,.bin/}semver

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
