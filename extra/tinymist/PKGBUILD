# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=tinymist
pkgver=0.14.8
pkgrel=1
epoch=1
pkgdesc='an integrated language service for Typst'
arch=(i686 x86_64)
url="https://github.com/Myriad-Dreamin/$pkgname"
license=(Apache-2.0)
depends=(gcc-libs # libgcc_s.so
         glibc # libc.so libm.so
         openssl libcrypto.so)
makedepends=(cargo)
replaces=("$pkgname-bin")
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('22d4d682df5ad56496da6965e74f0a89f00133c22a30c1c07f1bb45acd841aa7')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
}

pkgver() {
	# https://github.com/Myriad-Dreamin/tinymist#versioning-and-release-cycle
	if [[ $pkgver =~ ^.*[13579]$ ]]; then
		echo 'Odd-numbered releases target nightly typst, not stable.' >&2
		exit 1
	else
		echo "$pkgver"
	fi
}

_srcenv() {
	cd "$_archive"
	export OPENSSL_NO_VENDOR=true
}

build() {
	_srcenv
	CFLAGS+=' -ffat-lto-objects'
	cargo build --frozen --release
}

check() {
	_srcenv
	local skipped=(
		e2e
	)
	cargo test --frozen -- ${skipped[@]/#/--skip }
}

package() {
	cd "$_archive"
	local _target="target/release/$pkgname"
	install -Dm0755 -t "$pkgdir/usr/bin/" "$_target"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	install -Dm0644 <($_target completion bash) "$pkgdir/usr/share/bash-completion/completions/$pkgname"
	install -Dm0644 <($_target completion fish) "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
	install -Dm0644 <($_target completion zsh) "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
