# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=tinymist
pkgver=0.12.8
pkgrel=1
pkgdesc='an integrated language service for Typst'
arch=(i686 x86_64)
url="https://github.com/Myriad-Dreamin/$pkgname"
license=(Apache-2.0)
depends=(gcc-libs # libgcc_s.so
         glibc # libc.so libm.so
         openssl libcrypto.so)
makedepends=(cargo)
replaces=("$pkgname-bin")
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('69755c0b6561d18517361a9d90c636ba95f18e32bb23cd752f5658657189c340')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_srcenv() {
	cd "$_archive"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	export OPENSSL_NO_VENDOR=true
}

build() {
	_srcenv
	CFLAGS+=' -ffat-lto-objects'
	cargo build --frozen --release
}

check() {
	_srcenv
	local skipped=(
		e2e
	)
	cargo test --frozen -- ${skipped[@]/#/--skip }
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
