# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pimsync
pkgver=0.5.5
pkgrel=1
pkgdesc='Synchronize calendars and contacts'
arch=(x86_64)
url=https://pimsync.whynothugo.nl/
license=(EUPL-1.2)
depends=(
  gcc-libs
  sqlite
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://git.sr.ht/~whynothugo/pimsync#tag=v$pkgver")
b2sums=('d526c045207cac060977f15ae71c527211a5ec25c80b1f9c2cf7b158ea4bab13a478eefeca2d16411245f2a541f91d79f01222b2f5c4fa570ef4cb2cf0027002')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  cargo build -p $pkgname --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 $pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/man/man5 $pkgname.conf.5
  install -Dm644 -t "$pkgdir"/usr/share/man/man7 $pkgname-migration.7
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENCE
}
