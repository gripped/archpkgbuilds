# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pimsync
pkgver=0.4.3
pkgrel=1
pkgdesc='Synchronize calendars and contacts'
arch=(x86_64)
url=https://pimsync.whynothugo.nl/
license=(EUPL-1.2)
depends=(
  gcc-libs
  sqlite
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://git.sr.ht/~whynothugo/pimsync#tag=v$pkgver")
b2sums=('bf3e20f92668b1f5eba9e64dd3102273c5e8f09c931aed1f7f80236a8f35ef909948e589082fc45b6c53a1d71a8d7af9a8b5bc233b30e20d3db8db7b404f399f')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build -p $pkgname --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 pimsync.1
  install -Dm644 -t "$pkgdir"/usr/share/man/man5 pimsync.conf.5
  install -Dm644 -t "$pkgdir"/usr/share/man/man7 pimsync-migration.7
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENCE
}
