# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pimsync
pkgver=0.5.4
pkgrel=1
pkgdesc='Synchronize calendars and contacts'
arch=(x86_64)
url=https://pimsync.whynothugo.nl/
license=(EUPL-1.2)
depends=(
  gcc-libs
  sqlite
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://git.sr.ht/~whynothugo/pimsync#tag=v$pkgver")
b2sums=('e9e80e47873a42567d30ff21043f3f6fc3696c693059d8c0a3ff30844a29e1e75b2c8f325762ca1232f2d4f795974230aaf76b7e76f9200fe25235b4e91204d2')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build -p $pkgname --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 $pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/man/man5 $pkgname.conf.5
  install -Dm644 -t "$pkgdir"/usr/share/man/man7 $pkgname-migration.7
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENCE
}
