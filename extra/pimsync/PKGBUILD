# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pimsync
pkgver=0.4.4
pkgrel=1
pkgdesc='Synchronize calendars and contacts'
arch=(x86_64)
url=https://pimsync.whynothugo.nl/
license=(EUPL-1.2)
depends=(
  gcc-libs
  sqlite
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("git+https://git.sr.ht/~whynothugo/pimsync#tag=v$pkgver")
b2sums=('985b24946eff10064bdedcfc9be566e86c0f21034949d3a81870bf807180085fdf2f3c4adac7dd0f799ecbca654b7a42e36f288e43edbd647a82624b20b01a85')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build -p $pkgname --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 $pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/man/man5 $pkgname.conf.5
  install -Dm644 -t "$pkgdir"/usr/share/man/man7 $pkgname-migration.7
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENCE
}
