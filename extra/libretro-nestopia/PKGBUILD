# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-nestopia
pkgver=169
pkgrel=1
epoch=1
pkgdesc='Nintendo Entertainment System core'
arch=(x86_64)
url=https://github.com/libretro/nestopia
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=5deada54077fae87e2873f5ad9ef77e3ab7af5e1
source=(libretro-nestopia::git+https://github.com/libretro/nestopia.git#commit=${_commit})
sha256sums=('99ce3527169dc488d0755cccb9098e3cd9a064b371160ee6ec042ade8f0dddc1')

pkgver() {
  cd libretro-nestopia

  git rev-list --count HEAD
}

build() {
  make -C libretro-nestopia/libretro
}

package() {
  install -Dm 644 libretro-nestopia/libretro/nestopia_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
