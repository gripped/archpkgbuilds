# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-nestopia
pkgver=151
pkgrel=1
epoch=1
pkgdesc='Nintendo Entertainment System core'
arch=(x86_64)
url=https://github.com/libretro/nestopia
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=0e82a1642d4acafab4da9ce937954b85d846952b
source=(libretro-nestopia::git+https://github.com/libretro/nestopia.git#commit=${_commit})
sha256sums=('a2824774b024293337e727ef384cf4d54e4b4d6dc255967711fbcd5ff8bc777e')

pkgver() {
  cd libretro-nestopia

  git rev-list --count HEAD
}

build() {
  make -C libretro-nestopia/libretro
}

package() {
  install -Dm 644 libretro-nestopia/libretro/nestopia_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
