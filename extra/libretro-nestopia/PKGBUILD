# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-nestopia
pkgver=136
pkgrel=1
epoch=1
pkgdesc='Nintendo Entertainment System core'
arch=(x86_64)
url=https://github.com/libretro/nestopia
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=5b56b6b98ed5f0d7871be4c957fc9d39a608a7c0
source=(libretro-nestopia::git+https://github.com/libretro/nestopia.git#commit=${_commit})
sha256sums=('febf2cdde03a36a298609789db434bcd24fbd7454e91769880e81272b0621d17')

pkgver() {
  cd libretro-nestopia

  git rev-list --count HEAD
}

build() {
  make -C libretro-nestopia/libretro
}

package() {
  install -Dm 644 libretro-nestopia/libretro/nestopia_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
