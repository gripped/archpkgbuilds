# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-nestopia
pkgver=146
pkgrel=1
epoch=1
pkgdesc='Nintendo Entertainment System core'
arch=(x86_64)
url=https://github.com/libretro/nestopia
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=b3eb368603cd519d54bb4886d2934ee4fd188081
source=(libretro-nestopia::git+https://github.com/libretro/nestopia.git#commit=${_commit})
sha256sums=('de08717549af14681f14d369c933fcfc09480343be6fd8dfb1e94b24db94f0a0')

pkgver() {
  cd libretro-nestopia

  git rev-list --count HEAD
}

build() {
  make -C libretro-nestopia/libretro
}

package() {
  install -Dm 644 libretro-nestopia/libretro/nestopia_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
