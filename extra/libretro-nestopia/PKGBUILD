# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-nestopia
pkgver=140
pkgrel=1
epoch=1
pkgdesc='Nintendo Entertainment System core'
arch=(x86_64)
url=https://github.com/libretro/nestopia
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=5859109fc71b16dea31d1e3c2db43aa214e1c3eb
source=(libretro-nestopia::git+https://github.com/libretro/nestopia.git#commit=${_commit})
sha256sums=('5f20ce79435c8a766856f730b8e336572f18eefbaa838f469c58a376b2a23af5')

pkgver() {
  cd libretro-nestopia

  git rev-list --count HEAD
}

build() {
  make -C libretro-nestopia/libretro
}

package() {
  install -Dm 644 libretro-nestopia/libretro/nestopia_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
