# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-desktop
pkgver=8.2.1
pkgrel=1
pkgdesc='Switchboard Desktop Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-pantheon-shell
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  glibc
  gtk4
  libgee
  libgexiv2
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  ninja
  vala
)
_tag=ced9a2b22c1c641b4a168836318c4fa8a36bbc17
source=(switchboard-plug-desktop::git+https://github.com/elementary/switchboard-plug-pantheon-shell.git#tag=${_tag})
b2sums=('fdb4759d104474b117e3f0bd955fc9fca7abefb2569fd977dee35be75ac02c9b7cb98a69d735921af165ea44145a192d50b41d1873c196eae59d91df0b25a711')

pkgver() {
  cd switchboard-plug-desktop
  git describe --tags
}

build() {
  arch-meson switchboard-plug-desktop build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
