# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-desktop
pkgver=8.1.0
pkgrel=1
pkgdesc='Switchboard Desktop Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-pantheon-shell
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  glibc
  gtk4
  libgee
  libgexiv2
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  ninja
  vala
)
_tag=aec2f501423851ad2a108f5ef51a308687d086b3
source=(switchboard-plug-desktop::git+https://github.com/elementary/switchboard-plug-pantheon-shell.git#tag=${_tag})
b2sums=('86d762ee5c16f92d0c4c32f98f44be28498faf4f2b0a4fd92a928a00d07bdf90cbfb6009e31194863ec417febb96470034f020f2d66c462586bd36e8bff163f1')

pkgver() {
  cd switchboard-plug-desktop
  git describe --tags
}

build() {
  arch-meson switchboard-plug-desktop build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
