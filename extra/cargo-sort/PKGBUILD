# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=cargo-sort
pkgver=2.0.0
pkgrel=1
pkgdesc="Check if tables and items in a .toml file are lexically sorted"
arch=('x86_64')
url="https://github.com/DevinR528/cargo-sort"
license=('Apache-2.0' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('81cab653336109fe8a27bf1febed2f683c622bdecd6f981476864c4b4ff325159424cab8f158fab918a16f1ce1110aad5bed08bc180992fbafd0d8ed34b5a680')
b2sums=('1796d0678e2757dfeae55588929731ee7ac0885674834944f822b4aaed89254a2a780fe29884cdb2a390e926f224154f62431c00c8d9b65d0ba4fe1215a06b2b')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # licenses
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '/^####.*$/,/^<\/sub>$/p' README.md \
    >"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examp "$pkgdir/usr/share/doc/$pkgname/examples"
}
