# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=cargo-sort
pkgver=2.0.2
pkgrel=1
pkgdesc='Check if tables and items in a .toml file are lexically sorted'
arch=(x86_64)
url='https://github.com/DevinR528/cargo-sort'
license=(Apache-2.0 MIT)
depends=(gcc-libs)
makedepends=(git rust)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('09e9590fe06b3d889c4eeb6a9216e0eb0e1374aea96c4d8721b35d911b060613a707dd656234cb7c3e256bf5c75de45f679ad277d65484903fd53d4cc7f09969')
b2sums=('920e1ce0d51309bcf0451706e36726c9a9db3552cb2e46fef0801a5513b6354c406ad0f4389ebb9e21639fbd9ec0c8a8fd266ba16542cd4dae7f737fcab75661')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen
}

check() {
  cd "$pkgname"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # licenses
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '/^####.*$/,/^<\/sub>$/p' README.md \
    >"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examp "$pkgdir/usr/share/doc/$pkgname/examples"
}
