# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=cargo-sort
pkgver=1.1.0
pkgrel=1
pkgdesc="Check if tables and items in a .toml file are lexically sorted"
arch=('x86_64')
url="https://github.com/DevinR528/cargo-sort"
license=('Apache-2.0' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('5ed7e3d1ded56f59fcb13f29b1716b339f8159746263b8b027519032fb80c5f44641d8a43e86e0eff55f7a9aa97cb65e5ee2519772767cdd575cd922f4c819ba')
b2sums=('1d3ed03ede98180a8e51003882ba9f22ebf71b5406c15d69745d4a7b0df8670d208116c8bb961e972e4f0e8f5a4f5a68f126a9ec18dcde32887e4b724a71b4d3')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # licenses
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '/^####.*$/,/^<\/sub>$/p' README.md \
    >"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examp "$pkgdir/usr/share/doc/$pkgname/examples"
}
