# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=r2ghidra
_ghidra_native=21b6dbafee5b8265ee000827dd31f542ad46861d
pkgver=5.9.4
pkgrel=2
pkgdesc='Deep ghidra decompiler integration for radare2 and iaito'
url='https://github.com/radareorg/r2ghidra'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(
  radare2
  gcc-libs
  glibc
)
makedepends=(
  git
)
provides=(radare2-r2ghidra)
replaces=(r2ghidra-dec)
source=("git+https://github.com/radareorg/r2ghidra#tag=${pkgver}"
        "git+https://github.com/radareorg/ghidra-native.git#commit=${_ghidra_native}"
        git+https://github.com/zeux/pugixml.git)
sha512sums=('88f57d5d71eef95ba2545d14650ab99f857433d74d7b986bbc3550b0145dea6339da61d320202825bf73deef55467b84f1314e1fce8eb84dcea8eab7a6a10751'
            'b715599743e675ccf0925734c8fc876a2c3f0477d4b9d44b293ea152b4b3214bd552505e752277068d22be0f2aea79e323ff8b5dafa242423e68f56171db5c11'
            'SKIP')

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git -c protocol.file.allow=always submodule update --recursive
  ln -sf "${srcdir}/ghidra-native" ghidra-native
}

build() {
  cd ${pkgname}
  make -C ghidra-native patch
  ./configure PREFIX=/usr
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="$pkgdir" install
  r2version=$(r2 -v|grep radare2|sed -E 's/radare2 ([0-9\.]+) .*/\1/')
  ln -s /usr/lib/radare2/${r2version} "${pkgdir}/usr/lib/radare2/last"
}

# vim: ts=2 sw=2 et:
