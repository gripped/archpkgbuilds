# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=r2ghidra
_ghidra_native=0.5.0
pkgver=5.9.8
pkgrel=1
pkgdesc='Deep ghidra decompiler integration for radare2 and iaito'
url='https://github.com/radareorg/r2ghidra'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(
  radare2
  gcc-libs
  glibc
  zlib
)
makedepends=(
  git
)
provides=(radare2-r2ghidra)
replaces=(r2ghidra-dec)
source=("git+https://github.com/radareorg/r2ghidra#tag=${pkgver}"
        "git+https://github.com/radareorg/ghidra-native.git#tag=${_ghidra_native}"
        git+https://github.com/zeux/pugixml.git)
sha512sums=('9d34a369d442409b55d7c994a847b5e06c23ca8df0bd213c8f959002b1c7603b60a6ea273b594dcacb0cf4bf8292a4b111fff67086afe15d1acb36ba9e15793a'
            '1ab76f465e8d8a4ead1d60a18ccd0a533492cf7dc974769641e7891bf4273dea7e1035ac4f104374376a2b00a4675de3153f0fc4c8ac4ee899cadc1b34cffafe'
            'SKIP')

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git -c protocol.file.allow=always submodule update --recursive
  ln -sf "${srcdir}/ghidra-native" ghidra-native
}

build() {
  cd ${pkgname}
  make -C ghidra-native patch
  ./configure PREFIX=/usr
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="$pkgdir" install
  r2version=$(r2 -v|grep radare2|sed -E 's/radare2 ([0-9\.]+) .*/\1/')
  ln -s /usr/lib/radare2/${r2version} "${pkgdir}/usr/lib/radare2/last"
}

# vim: ts=2 sw=2 et:
