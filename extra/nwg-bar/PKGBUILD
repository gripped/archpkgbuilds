# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=nwg-bar
pkgver=0.1.6
pkgrel=3
pkgdesc="GTK3-based button bar for sway and other wlroots-based compositors"
url="https://github.com/nwg-piotr/nwg-bar"
arch=('x86_64')
license=('MIT')
depends=('gtk3' 'gtk-layer-shell')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('badc232f9eef1a225776c7f0fe2534b49ac0a957f1a81dc24acb8f341b7719d4')

prepare(){
	cd "${pkgname}-${pkgver}"
	mkdir -p build/
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	go build -ldflags="-s -w -linkmode=external -buildid=''" -o "build/${pkgname}" *.go
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm 755 "build/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 config/* -t "${pkgdir}/usr/share/${pkgname}/"
	install -Dm 644 images/* -t "${pkgdir}/usr/share/${pkgname}/images/"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
