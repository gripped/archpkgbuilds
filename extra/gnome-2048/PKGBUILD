# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-2048
pkgver=50beta
pkgrel=1
pkgdesc="Obtain the 2048 tile"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/gnome-2048"
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libgcc
  pango
)
makedepends=(
  appstream
  desktop-file-utils
  git
  itstool
  meson
  rust
  vala
)
options=(!lto)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-2048.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('dc10a18852862bb7d1ef461c8f2bd68c7710f450dac8a2cbc79b2b3d314f30cb376034723986285aa43969825e766e5237220f28aec35330ef7f2d596bf924b3')

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" cargo fetch --locked --target host-tuple
}


build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
