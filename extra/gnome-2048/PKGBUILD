# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-2048
pkgver=50alpha.2
pkgrel=3
pkgdesc="Obtain the 2048 tile"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/gnome-2048"
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  desktop-file-utils
  git
  itstool
  meson
  rust
  vala
)
options=(!lto)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-2048.git#tag=${pkgver/[a-z]/.&}"
  gnome-2048-app-name.patch
  gnome-2048-build-depends.patch
)
b2sums=(
  77117bde2aae07382a99c102e1113c55646e0300a646d50ad6acd3646ecfd5c922b98debb49ad42e728aee775e2f4a270fd768596d25ce26a8645f647220d743
  840f4c98c273dc50cebe85ef0f6352137aca7a12f7d79272d45abd37843905a7f0e0db68fbe90d7c021f3de9912e0cfc891e2ae3eba2a3ad0addea5d5e606102
  dce8b9ab3e6e89026486f73e97c066e6da45e24f2e20069299ee0dd8c69cbfb890077a5ba7a1425ec78b39ac2d725a7e8f3c6b3bb343e27d3ad3a6a4de648ce0
)

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc --print host-tuple)"

  # Fix application name
  # https://gitlab.gnome.org/GNOME/gnome-2048/-/merge_requests/53
  git apply -3 ../gnome-2048-app-name.patch

  # Specify libgnome_games_support as dependency
  # https://gitlab.gnome.org/GNOME/gnome-2048/-/merge_requests/54
  git apply -3 ../gnome-2048-build-depends.patch
}


build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
