# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-2048
pkgver=50alpha.2
pkgrel=2
pkgdesc="Obtain the 2048 tile"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/gnome-2048"
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  desktop-file-utils
  git
  itstool
  meson
  rust
  vala
)
options=(!lto)
source=("git+https://gitlab.gnome.org/GNOME/gnome-2048.git#tag=${pkgver/[a-z]/.&}")
b2sums=('77117bde2aae07382a99c102e1113c55646e0300a646d50ad6acd3646ecfd5c922b98debb49ad42e728aee775e2f4a270fd768596d25ce26a8645f647220d743')

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc --print host-tuple)"

  # Fix application name
  # https://gitlab.gnome.org/GNOME/gnome-2048/-/merge_requests/53
  git cherry-pick -n 11b613fbe702bc4fbb194630d0099a18954d6100

  # Specify libgnome_games_support as dependency
  # https://gitlab.gnome.org/GNOME/gnome-2048/-/merge_requests/54
  git cherry-pick -n 6b1c41352c8be42fd2d3cb9c2f2be0415db4fe16
}


build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
