# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=retrie
pkgname=haskell-retrie
pkgver=1.2.3
pkgrel=10
pkgdesc="A powerful, easy-to-use codemodding tool for Haskell."
url="https://github.com/facebookincubator/retrie"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-async' 'haskell-data-default'
         'haskell-ghc' 'haskell-ghc-exactprint' 'haskell-ghc-paths' 'haskell-src-exts'
         'haskell-list-t' 'haskell-optparse-applicative' 'haskell-random-shuffle' 'haskell-syb'
         'haskell-unordered-containers')
makedepends=('ghc' 'haskell-hunit' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-temporary')
checkdepends=('git' 'mercurial')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('6380a1f276ac832af0a19979f792134098ff4d93d5cf47284a37ef77b8ddee7cc46d3344c681ad5c77777e7f4263ad2c9ab8ebb812a03c28e80499690c23535b')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
