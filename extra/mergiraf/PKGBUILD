# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: VCalV
pkgname=mergiraf
pkgver=0.5.1
pkgrel=1
pkgdesc="A syntax-aware git merge driver for a growing collection of programming languages and file formats"
arch=('x86_64')
url="https://mergiraf.org/"
license=('GPL-3.0-or-later')
makedepends=('rust')
checkdepends=('git')
depends=('glibc' 'gcc-libs')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://codeberg.org/mergiraf/mergiraf/archive/v$pkgver.tar.gz")
sha512sums=('f2aa3c1610b83fbc322b37a48dff46213b2ed84905e51c5f526cf12bd2d0c46d058291e2c631bfcc901ee031fdff8326900dc3c57c0a4acc8fd6592069308f74')

prepare() {
    cd "$pkgname"

    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname"

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features --bin mergiraf
}

check() {
    cd "$pkgname"

    cargo test --all-features
}


package() {
    cd "$pkgname"

    install -Dm755 target/release/mergiraf "$pkgdir"/usr/bin/mergiraf
    install -Dm444 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm444 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
}
