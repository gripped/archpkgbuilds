# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: VCalV
pkgname=mergiraf
pkgver=0.6.0
pkgrel=1
pkgdesc="A syntax-aware git merge driver for a growing collection of programming languages and file formats"
arch=('x86_64')
url="https://mergiraf.org/"
license=('GPL-3.0-or-later')
makedepends=('rust')
checkdepends=('git')
depends=('glibc' 'gcc-libs')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://codeberg.org/mergiraf/mergiraf/archive/v$pkgver.tar.gz")
sha512sums=('d0a1c7db7fd04402808cc7e0c423ecf1ba539585a376c638a1c371f7d63a8eb5e36b1147754af20b35b953251472e5f36a8453ea59b9ded4abe5fdc6b67c5d0b')

prepare() {
    cd "$pkgname"

    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname"

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features --bin mergiraf
}

check() {
    cd "$pkgname"

    cargo test --all-features
}


package() {
    cd "$pkgname"

    install -Dm755 target/release/mergiraf "$pkgdir"/usr/bin/mergiraf
    install -Dm444 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm444 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
}
