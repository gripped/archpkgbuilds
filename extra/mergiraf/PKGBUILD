# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: VCalV
pkgname=mergiraf
pkgver=0.16.3
pkgrel=1
pkgdesc="A syntax-aware git merge driver for a growing collection of programming languages and file formats"
arch=('x86_64')
url="https://mergiraf.org/"
license=('GPL-3.0-or-later')
makedepends=('rust')
checkdepends=('git')
depends=('glibc' 'gcc-libs')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://codeberg.org/mergiraf/mergiraf/archive/v$pkgver.tar.gz")
sha512sums=('48d46e8968a9c1bd0c9078caea84862b3876f36fef4471d93cdf93b8f33758cbeb47adcbeb61e8d0245020735d316177f50f1a95ebb33e8fd5ef9188155407f4')

prepare() {
    cd "$pkgname"

    cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
    cd "$pkgname"

    cargo build --frozen --release --all-features --bin mergiraf
}

check() {
    cd "$pkgname"

    cargo test --all-features
}


package() {
    cd "$pkgname"

    install -Dm755 target/release/mergiraf "$pkgdir"/usr/bin/mergiraf
    install -Dm444 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm444 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
}
