# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Donald Carr <d@chaos-reins.com>

pkgname=system76-scheduler
pkgver=2.0.2
pkgrel=1
pkgdesc='Auto-configure CFS and process priorities for improved desktop responsiveness'
arch=(x86_64)
url=https://github.com/pop-os/system76-scheduler
license=(MPL-2.0)
depends=(
  bcc-tools
  gcc-libs
  glibc
  libpipewire
)
makedepends=(
  clang
  git
  lld
  rust
  just
  linux-headers
)
backup=(
  etc/system76-scheduler/config.kdl
  etc/system76-scheduler/process-scheduler/pop_os.kdl
)
_tag=c403d0144eba88b952f663c52e2fcdb1a6c2f35f
source=(git+https://github.com/pop-os/system76-scheduler.git#tag=${_tag})
b2sums=('4bf17c797bbd4fc65d8182709bbb21cc991d1233dd33c4b24fbc3b230b17725686c91874f394d2997051f381311268a05b7cd34c73b8c332b7e0edc7d16c763c')

pkgver() {
  cd system76-scheduler
  git describe --tags
}

build() {
  cd system76-scheduler
  export CC=clang
  just execsnoop=/usr/share/bcc/tools/execsnoop build-release
}

package() {
  cd system76-scheduler
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
