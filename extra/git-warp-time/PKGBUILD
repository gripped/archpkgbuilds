# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-warp-time
pkgver=0.7.4
pkgrel=1
pkgdesc='reset timestamps of Git repository files to the time of the last modifying commit'
arch=(x86_64)
url="https://github.com/alerque/$pkgname"
license=(GPL-3.0-only)
depends=(gcc-libs
         glibc
         libgit2 libgit2.so)
makedepends=(cargo
             git
             jq)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.zst"{,.asc})
sha256sums=('a0a9948727005943efb965d8332efc339dbbc27f0ab807bd3060de40914888e6'
            'SKIP')
validpgpkeys=('9F377DDB6D3153A48EB3EB1E63CC496475267693') # Caleb Maclennan <caleb@alerque.com> (@alerque)

prepare() {
	cd "$_archive"
	sed Makefile.am -i \
		-e 's/cargo \(build\|install\|test\)/cargo --offline \1/'
	autoreconf
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
	./configure --prefix /usr --enable-shared
	make
}

check() {
	cd "$_archive"
	make check
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
