# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-warp-time
pkgver=0.8.0
pkgrel=1
pkgdesc='reset timestamps of Git repository files to the time of the last modifying commit'
arch=(x86_64)
url="https://github.com/alerque/$pkgname"
license=(GPL-3.0-only)
depends=(gcc-libs # libgcc_s.so 
         glibc # libc.so libm.so
         libgit2)
makedepends=(cargo
             git
             jq)
options=(!lto)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.zst"{,.asc})
sha256sums=('27e5ab9209d0e010b04b4b75ee07977e573d92c5f7753ca4d32af2497a94c5df'
            'SKIP')
validpgpkeys=('9F377DDB6D3153A48EB3EB1E63CC496475267693') # Caleb Maclennan <caleb@alerque.com> (@alerque)

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	export CARGO_FEATURE_FLAGS==--offline
	export LIBGIT2_NO_VENDOR=1
	export LIBGIT2_SYS_USE_PKG_CONFIG=1
	./configure --prefix /usr --enable-shared
	make
}

check() {
	cd "$_archive"
	export CARGO_FEATURE_FLAGS==--offline
	make check
}

package() {
	depends+=(libgit2.so)
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
