# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-onboarding
pkgver=8.0.2
pkgrel=1
pkgdesc='Onboarding app for new users'
arch=(x86_64)
url=https://github.com/elementary/onboarding
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  libadwaita-1.so
  libgranite-7.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=5dc3a7f3bf04bdcfc5acf5ccc96036ebe29b8791
source=(pantheon-onboarding::git+https://github.com/elementary/onboarding.git#tag=${_tag})
b2sums=('3157b1bcc95b6d31a1d6e79cb574cfca89944d5a975cb04bfe1d97e86fa78ef480994abb20ddcb02fdc21392fbf245fd149bab6cf551c0d1a58e1d647629819d')

pkgver() {
  cd pantheon-onboarding
  git describe --tags
}

build() {
  arch-meson pantheon-onboarding build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
