# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-onboarding
pkgver=8.0.3
pkgrel=1
pkgdesc='Onboarding app for new users'
arch=(x86_64)
url=https://github.com/elementary/onboarding
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgranite-7.so
  pantheon-wayland
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=f59166dab0e20855624a5f7d2ebcb9838ae6dca0
source=(pantheon-onboarding::git+https://github.com/elementary/onboarding.git#tag=${_tag})
b2sums=('48a7aa9980964b683d4b342f96918d4c4a3806ec003abdd7d8ea52edbdb671aeefa0acfcaec31b70785f8e47a3749d9db6154e9260be59a4d423ca64676a02a4')

pkgver() {
  cd pantheon-onboarding
  git describe --tags
}

build() {
  arch-meson pantheon-onboarding build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
