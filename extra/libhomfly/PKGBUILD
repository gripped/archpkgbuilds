# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libhomfly
pkgver=1.03
pkgrel=1
pkgdesc='Library to compute the homfly polynomial of a link'
arch=(x86_64)
url='https://github.com/miguelmarco/libhomfly'
license=(LicenseRef-libhomfly)
depends=(gc
         glibc)
makedepends=(git)
source=(git+https://github.com/miguelmarco/libhomfly#tag=$pkgver)
sha256sums=('64a8691f3bb4259b42624876ff039b138ee52d4c3024e3c8107d31cec86a5311')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
