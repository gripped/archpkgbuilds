# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-qiniu
pkgver=7.14.0
pkgrel=2
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
depends=('python-requests')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest' 'python-freezegun')
source=("git+https://github.com/qiniu/python-sdk.git#tag=v$pkgver")
sha512sums=('87539eef8cedffe46e4f68a057aba9a7df3bfe0c521260a7b25d17e5ccfbcf34ea5c825b76d5722fa5a46aa8b50325c61abcf0b1de954473768c3d0dfd6bda40')

build() {
  cd python-sdk
  python setup.py build
}

check() {
  cd python-sdk
  pytest || echo "Tests failed"
}

package() {
  cd python-sdk
  python setup.py install -O1 --root "$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
