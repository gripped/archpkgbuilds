# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-qiniu
pkgver=7.16.0
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
depends=('python-requests')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest' 'python-freezegun')
source=("git+https://github.com/qiniu/python-sdk.git#tag=v$pkgver")
sha512sums=('1261cc3db6ab637336e38dc86ca56eedf43df9b5ac34f9538090e0710e6b9fb29e326afcf701be68da07654940822e4ca6a83d803241d9667aff2d47dd59d317')

build() {
  cd python-sdk
  python setup.py build
}

check() {
  cd python-sdk
  pytest || echo "Tests failed"
}

package() {
  cd python-sdk
  python setup.py install -O1 --root "$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
