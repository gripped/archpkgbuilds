# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-qiniu
pkgver=7.15.0
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
depends=('python-requests')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest' 'python-freezegun')
source=("git+https://github.com/qiniu/python-sdk.git#tag=v$pkgver")
sha512sums=('c5f9c2b3beb76a2a00c2ed7d781620303dd9b4e8ba2d56a414464f9fa7d80ad88cfb84feaa63c977a715e2fccd208bd0d314639ccede5b4c36bd016ec32bf761')

build() {
  cd python-sdk
  python setup.py build
}

check() {
  cd python-sdk
  pytest || echo "Tests failed"
}

package() {
  cd python-sdk
  python setup.py install -O1 --root "$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
