# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=quicklisp
pkgver=20150128
pkgrel=2
pkgdesc='A library manager for Common Lisp'
arch=('any')
url='https://www.quicklisp.org'
license=('MIT')
install=quicklisp.install
source=(
  'https://beta.quicklisp.org/quicklisp.lisp'{,.asc}
  'LICENSE'
  'README.md'
  'FAQ.md'
)
b2sums=('57d0d29e08d77176fea4f01e644fc11bdb96e9efbd132cbb9720192d3b47599f5ce3c6e0307b44c33add06bd70c28004b8efb8968f1d9deb881a2db4ac615772'
        'SKIP'
        'e7a64d5d86ea97cd694c9d72c602036003657b471c278e0116cc55aa72c817190446e542abf359f7424962b65bd33f4c9c8aa168de0870b9f9fc11e14801577f'
        'eb3532d8aaf1bb8b85491da18aa279f043d35d8de85d3d213ffaee4fee5f5a4e064dd3b27ad9eb774ca6a8d177570fcb1769b618d7dc152fa7fa0c5d9b5bfef1'
        'b6d9417fed625884ed9fa7d1991b5d5d66d5ae5878da85368b27cf6b4946a42236193adee3b339eea4f91b15d0e728aaa02fe9e695312a69ddba0ff5cedd998f')
validpgpkeys=('D7A3489DDEFE32B7D0E7CC61307965AB028B5FF7') # release@quicklisp.org

pkgver() {
  grep 'defvar qlqs-info' quicklisp.lisp | sed -e 's/.*"\(.*\)".*/\1/' -e 's/-//g'
}

package() {
  # library
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" quicklisp.lisp

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
