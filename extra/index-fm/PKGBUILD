# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=index-fm
pkgver=4.0.2
pkgrel=1
pkgdesc='Multi-platform file manager'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         kio
         mauikit
         mauikit-archiver
         mauikit-documents
         mauikit-filebrowsing
         mauikit-terminal
         mauikit-texteditor
         qt6-base
         qt6-declarative
         qt6-multimedia)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/index/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bb761ead52693efd7408302027b733116dcd7d0997d25c7c8af9e0a72c71c3d5'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
