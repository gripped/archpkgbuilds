# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=scip
pkgver=9.2.3
pkgrel=1
pkgdesc='Solving Constraint Integer Programs'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(Apache-2.0)
depends=(gcc-libs
         glibc
         gmp
         mpfr
         readline
         tbb
         zlib)
makedepends=(boost
             cmake
             git
             papilo
             soplex)
source=(git+https://github.com/scipopt/scip#tag=v${pkgver//./})
sha256sums=('91f2f293fbf0acee546a0b934f9a25cc18655f3c51d333022279a7ecd24dc00f')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DAUTOBUILD=ON \
    -DSYM=bliss
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
