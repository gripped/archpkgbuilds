# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Benoit Brummer < trougnouf at gmail dot com >
# Contributor: Carl George < arch at cgtx dot us >

pkgname=gnome-shell-extension-dash-to-panel
_pkgname=dash-to-panel
pkgver=61
pkgrel=2
pkgdesc='Extension for GNOME Shell to combine the dash and main panel'
arch=('any')
url='https://github.com/home-sweet-gnome/dash-to-panel'
license=('GPL-2.0-or-later')
depends=('gnome-shell')
source=("https://github.com/home-sweet-gnome/$_pkgname/archive/v$pkgver/$_pkgname-$pkgver.tar.gz"
        'gnome-shell-extension-dash-to-panel-system-install-paths.patch'
        'gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch'
        'gnome-shell-extension-dash-to-panel-terminal-command.patch')
sha256sums=('33cc4dad5d13f90d3683f0812d39c29528131763fee526dfbcd9db43c9d7fe22'
            'b9c247fc8ab7f1ee18ce2c98f8312f86da0340622d4c5daadd3a4579f4213fc5'
            'd772d9aa2995602e5b11fa9624586d7a1c485fde9b21f046f2cf52e5fb099735'
            'b3127999d68a56363d0aa39b380fec7f6a4ba4af6b345b67464a36cc12cee703')

prepare() {
  cd $_pkgname-$pkgver
  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1948
  patch -Np1 -i ../gnome-shell-extension-dash-to-panel-system-install-paths.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1969
  patch -Np1 -i ../gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/2062
  patch -Np1 -i ../gnome-shell-extension-dash-to-panel-terminal-command.patch
}

build() {
  cd $_pkgname-$pkgver
  make
}

package() {
  cd $_pkgname-$pkgver
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
