# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Benoit Brummer < trougnouf at gmail dot com >
# Contributor: Carl George < arch at cgtx dot us >

pkgname=gnome-shell-extension-dash-to-panel
_pkgname=dash-to-panel
pkgver=62
pkgrel=3
pkgdesc='Extension for GNOME Shell to combine the dash and main panel'
arch=(any)
url='https://github.com/home-sweet-gnome/dash-to-panel'
license=(GPL-2.0-or-later)
depends=(gnome-shell)
makedepends=(git)
source=(
  "git+https://github.com/home-sweet-gnome/dash-to-panel.git#tag=v$pkgver"
  gnome-shell-extension-dash-to-panel-system-install-paths.patch
  gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch
  gnome-shell-extension-dash-to-panel-fix-crash-on-shutdown.patch
  gnome-shell-extension-dash-to-panel-gnome47.patch
)
b2sums=(
  6ff7ed4d7a1c4b15458a00b81835f0e901af921be155b8245fad29189ccf6768671d96710e4db1ae5fb72fb55c183f8ad0c17edd2018c1ac6ef60e3f70f36349
  710fc53e3f8a8f783261707ec2d9281ab2f8544f4def5fd373738999195c9a76f810cdce1b57711e5f76540d29bd115412ccfb15c072cb11c63d95ee3e9efe69
  d899985e4a704196463e33808880dccb1227cfb6a5ef0ec3ca1dc8d392299d13842338f7e90ad1f3a89670cacbb7083c7b3b5c429ea245ac6545853c574a6e4d
  4cdf88aa2a67d6ef86d9311fab2f26b52d53fa92c53562c93cb7633b7803b9e9fdff5c5ea2a1958f91390f184ee3501c56ac4576b517149dae5324e54e4e248e
  37ff35a7f804675717599c81fd3eb4f16616542acb85aec1dfc02ed088c1d9d4195315db0d12a73d8e455077e2d742abd3a65c089cdd84bf2570683890c50572
)

prepare() {
  cd $_pkgname

  # Various fixes from upstream
  git cherry-pick -n -m 1 v62..9d0d870b89fbbaba369b59e81ec0e6015f9ce6f2

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1948
  git apply -3 ../gnome-shell-extension-dash-to-panel-system-install-paths.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1969
  git apply -3 ../gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/2134
  git apply -3 ../gnome-shell-extension-dash-to-panel-fix-crash-on-shutdown.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/2154
  git apply -3 ../gnome-shell-extension-dash-to-panel-gnome47.patch
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
