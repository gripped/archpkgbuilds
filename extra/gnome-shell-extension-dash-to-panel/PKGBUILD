# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Benoit Brummer < trougnouf at gmail dot com >
# Contributor: Carl George < arch at cgtx dot us >

pkgname=gnome-shell-extension-dash-to-panel
_pkgname=dash-to-panel
pkgver=64
pkgrel=2
pkgdesc='Extension for GNOME Shell to combine the dash and main panel'
arch=(any)
url='https://github.com/home-sweet-gnome/dash-to-panel'
license=(GPL-2.0-or-later)
depends=(gnome-shell)
makedepends=(git)
source=(
  "git+https://github.com/home-sweet-gnome/dash-to-panel.git#tag=v$pkgver"
  gnome-shell-extension-dash-to-panel-system-install-paths.patch
  gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch
  gnome-shell-extension-dash-to-panel-fix-crash-on-shutdown.patch
)
b2sums=('1df4c413601f5f9e26ccab9e380059cd75230a95ce0c30375c5c109d05a86f142b0d5cf9825d792903b721d79c52ab9c546684069fc8161e4294e6f9d049dcaf'
        '710fc53e3f8a8f783261707ec2d9281ab2f8544f4def5fd373738999195c9a76f810cdce1b57711e5f76540d29bd115412ccfb15c072cb11c63d95ee3e9efe69'
        'd899985e4a704196463e33808880dccb1227cfb6a5ef0ec3ca1dc8d392299d13842338f7e90ad1f3a89670cacbb7083c7b3b5c429ea245ac6545853c574a6e4d'
        '4cdf88aa2a67d6ef86d9311fab2f26b52d53fa92c53562c93cb7633b7803b9e9fdff5c5ea2a1958f91390f184ee3501c56ac4576b517149dae5324e54e4e248e')

prepare() {
  cd $_pkgname

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1948
  git apply -3 ../gnome-shell-extension-dash-to-panel-system-install-paths.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1969
  git apply -3 ../gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/2134
  git apply -3 ../gnome-shell-extension-dash-to-panel-fix-crash-on-shutdown.patch

  # Update Extensions app executable name
  sed -i 's/gnome-shell-extension-prefs/gnome-extensions-app/' appIcons.js
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
