# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Benoit Brummer < trougnouf at gmail dot com >
# Contributor: Carl George < arch at cgtx dot us >

pkgname=gnome-shell-extension-dash-to-panel
_pkgname=dash-to-panel
pkgver=70
pkgrel=2
pkgdesc='Extension for GNOME Shell to combine the dash and main panel'
arch=(any)
url='https://github.com/home-sweet-gnome/dash-to-panel'
license=(GPL-2.0-or-later)
depends=(gnome-shell)
makedepends=(git)
source=(
  "git+https://github.com/home-sweet-gnome/dash-to-panel.git#tag=v$pkgver"
  gnome-shell-extension-dash-to-panel-system-install-paths.patch
  gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch
  gnome-shell-extension-dash-to-panel-fix-crash-on-shutdown.patch
)
b2sums=(
  e4bd53730bee66bc7e742e69df34e7db05389ed135d235a26e68a78a835488984987486da300592083823f74e67f6f6a90f6d35e3436d2a23dcade43c6bd32b1
  710fc53e3f8a8f783261707ec2d9281ab2f8544f4def5fd373738999195c9a76f810cdce1b57711e5f76540d29bd115412ccfb15c072cb11c63d95ee3e9efe69
  45ffabd2e1a44635ded780c99809202d423713b7977b6bf9f354214605944a4bb032cfd342601ed1578ed7e25c140fcc178cc3c4723fb0f5eb787454a1815149
  abdefe9de059329ffbb9ddcbc1064e63de47a89c732eb7537c9e4b16ec884bedb850a1b3e3ffbbf969b1d5891dcc095841a29c99cf8d6e00c1eb82dbb90dcfab
)

prepare() {
  cd $_pkgname

  # Apply fixes from upstream
  git cherry-pick -m 1 -n v70..47e0be962aa25080675347713a200231f687ed82

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1948
  git apply -3 ../gnome-shell-extension-dash-to-panel-system-install-paths.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1969
  git apply -3 ../gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/2134
  git apply -3 ../gnome-shell-extension-dash-to-panel-fix-crash-on-shutdown.patch
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
