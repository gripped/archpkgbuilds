# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-bg
pkgver=1.0.0.beta.1.1
pkgrel=1
pkgdesc='COSMIC session service which applies backgrounds to displays'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-bg
license=(MPL-2.0)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  clang
  cargo
  git
  just
  lld
  nasm
)
_tag=40254a7101b52b482f06d35a4d2eba8245814b2c
source=(
  git+https://github.com/pop-os/cosmic-bg.git#tag=${_tag}
  cosmic-bg-lto.patch
)
b2sums=('45e3625b5616569af79d1943fac9c70ffee97a1630d37ca0b66011848271e450afcc4ad3cf95bebe01ae7834d23ea48bb7397ad69251a85a6910cb8617aeed1e'
        '425c8b386f69d6b4b7883235f822089bb8b596043b1ef5616ff13768ab7afa5a0db55b0cff9c64f264abc60dd6c11f137069c0c8273135b8203054db3ff87c83')

prepare() {
  cd cosmic-bg
  patch -Np1 -i ../cosmic-bg-lto.patch
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-bg
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-bg
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-bg
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
