# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-bg
pkgver=1.0.0.alpha.1
pkgrel=1
pkgdesc='COSMIC session service which applies backgrounds to displays'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-bg
license=(MPL-2.0)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
  nasm
)
_tag=e5637fcc83abeb4e9a675b04a9b98ae4a5c186fa
source=(git+https://github.com/pop-os/cosmic-bg.git#tag=${_tag})
b2sums=('80757e5fd9db7e16c4f72e08708099b5f6fe369e1a6135061ba3f35489d70a0b88fe9a03f0cef99efdd101d8eb33183af113260f2b93a58dd9b517c2d7e83a0a')

prepare() {
  cd cosmic-bg
  cargo fetch --locked
}

pkgver() {
  cd cosmic-bg
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-bg
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-bg
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
