# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-bg
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='COSMIC session service which applies backgrounds to displays'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-bg
license=(MPL-2.0)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
  nasm
)
_tag=b44439fcd5ba85fec1a5d35cc163877160d31d0d
source=(
  git+https://github.com/pop-os/cosmic-bg.git#tag=${_tag}
  cosmic-bg-lto.patch
)
b2sums=('e2454e06eae350a0ef0d7cce198b48501b59e3eb2db9f912b1a170752d019b4c7f825187a70355250749c13f8171bba84a715020e12172d508c16cfbbfbb1d0d'
        'ccf50f22e306236727decc55d6895adeb6d82b8786bd8f14d34c48ecbf857931a52ac4be2a2b5dcb8175d872c2b8e45e056662770414815e436b77594fd25794')

prepare() {
  cd cosmic-bg
  patch -Np1 -i ../cosmic-bg-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-bg
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-bg
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-bg
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
