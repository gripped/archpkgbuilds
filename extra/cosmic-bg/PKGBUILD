# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-bg
pkgver=1.0.0.alpha.5.1
pkgrel=1
pkgdesc='COSMIC session service which applies backgrounds to displays'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-bg
license=(MPL-2.0)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
  nasm
)
_tag=fd44edf79b5ffbced27540fd222e37aa05daeeba
source=(
  git+https://github.com/pop-os/cosmic-bg.git#tag=${_tag}
  cosmic-bg-lto.patch
)
b2sums=('c23bf7f47980127d9161bec206f80cab052d01167efc0f9e4be9435c5f19069907b1ea29ead8dd3a02e973e802d510a62b976fe9f8be2ee4872a11829f6eaffe'
        'ccf50f22e306236727decc55d6895adeb6d82b8786bd8f14d34c48ecbf857931a52ac4be2a2b5dcb8175d872c2b8e45e056662770414815e436b77594fd25794')

prepare() {
  cd cosmic-bg
  patch -Np1 -i ../cosmic-bg-lto.patch
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-bg
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-bg
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-bg
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
