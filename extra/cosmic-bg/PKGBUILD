# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-bg
pkgver=1.0.0.alpha.6
pkgrel=1
pkgdesc='COSMIC session service which applies backgrounds to displays'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-bg
license=(MPL-2.0)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
  nasm
)
_tag=b6adf25075383c0e606658a7309919a9b092ee54
source=(
  git+https://github.com/pop-os/cosmic-bg.git#tag=${_tag}
  cosmic-bg-lto.patch
)
b2sums=('618d8b3857e889a4eb8b545e0ce787f007da679f7d34c738ab424b59743433d15acb1be26a602edab48e0245eb77996f6369acbe21682c49a516dc4881434568'
        'ccf50f22e306236727decc55d6895adeb6d82b8786bd8f14d34c48ecbf857931a52ac4be2a2b5dcb8175d872c2b8e45e056662770414815e436b77594fd25794')

prepare() {
  cd cosmic-bg
  patch -Np1 -i ../cosmic-bg-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-bg
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-bg
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-bg
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
