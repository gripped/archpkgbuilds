# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-runner
pkgver=0.121
pkgrel=1
pkgdesc='Scripts to run Sugar'
arch=(x86_64)
url='https://github.com/sugarlabs/sugar-runner'
license=(GPL-2.0-or-later)
depends=(
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libx11
  libxrandr
  python
  python-gobject
  sugar
  xorg-server-xephyr
  xorg-xinit
  xorg-xrandr
)
makedepends=(
  git
  gobject-introspection
)
source=("git+https://github.com/sugarlabs/$pkgname.git#tag=v$pkgver")
b2sums=(b8d713266a6207fadf015bbf69f62b71dc9a873628a1769655e2bd9069c237f4fa5a4c74ee629a30239c42a1eaf837191a363c4ff28e9ca49de53c449515d865)

prepare() {
  cd $pkgname

  # Fix launch from the application menu
  echo 'Terminal=true' >>data/sugar-runner.desktop

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
