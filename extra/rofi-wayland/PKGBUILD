# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Jeremy Gust <jeremy AT plasticsoup DOT net>
# Contributor: Carson Rueter <swurl at swurl dot xyz>
# Contributor: tinywrkb <tinywrkb@gmail.com>
# Contributor: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maxim Baz <rofi at maximbaz dot com>
# Contributor: Anatol Pomozov
# Contributor: Benjamin Chr√©tien <chretien + aur [at] lirmm [dot] fr>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Rasi <rasi@xssn.at>
# Contributor: Sean Pringle <sean.pringle@gmail.com>
# Contributor: SanskritFritz (gmail)

pkgname=rofi-wayland
_pkgname=rofi
pkgver=1.7.7.1
_pkgver="${pkgver%.*}+wayland${pkgver##*.}"
pkgrel=1
pkgdesc="A window switcher, run dialog and dmenu replacement - fork with wayland support"
url="https://github.com/lbonn/rofi"
arch=('x86_64')
license=('MIT')
depends=('bash' 'cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'hicolor-icon-theme' 'libxcb' 'libxkbcommon' 'libxkbcommon-x11'
         'pango' 'startup-notification' 'wayland' 'xcb-imdkit' 'xcb-util' 'xcb-util-cursor' 'xcb-util-keysyms' 'xcb-util-wm')
makedepends=('check' 'git' 'meson' 'pandoc' 'wayland-protocols')
provides=('rofi')
conflicts=('rofi')
source=("git+${url}.git#tag=${_pkgver}"
        'git+https://github.com/sardemff7/libgwater.git'
        'git+https://github.com/sardemff7/libnkutils.git'
        'fix_xfce4-terminal_binary_name.patch')
sha256sums=('26c57bfa62768628824407d1360983756b992d3898db69b11e9f797fdcd45e80'
            'SKIP'
            'SKIP'
            'a1305667c93119a916df4b5438b5c7a737b66bf304fff68f038bf6c9ac44651f')

prepare() {
	cd "${_pkgname}"
	git submodule init
	git config submodule.subprojects/libgwater.url "${srcdir}/libgwater"
	git config submodule.subprojects/libnkutils.url "${srcdir}/libnkutils"
	git -c protocol.file.allow=always submodule update

	# xfce4-terminal.wrapper is a Debian specific script that Arch does not provide
	patch -Np1 -i "${srcdir}/fix_xfce4-terminal_binary_name.patch"
}

build() {
	arch-meson "${_pkgname}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir "${pkgdir}"

	cd "${_pkgname}"
	install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
	install -Dm 755 Examples/*.sh -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
