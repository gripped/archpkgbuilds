# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=6.0.13
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-icon-theme' 'deepin-sound-theme')
makedepends=('git' 'python' 'go')
conflicts=('deepin-artwork-themes')
replaces=('deepin-artwork-themes')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-desktop-schemas.git#tag=$pkgver")
sha512sums=('364f2fda4e853eb291d0c094bf1303c6e6abdab24387f912183388a763139ec418a0dee724676c278f93eb0b5f64f41e31bc57749796f7bce16bc295a76e11b7')

prepare() {
  cd deepin-desktop-schemas
  # fix default background url
  sed -i "s#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#" \
    overrides/common/com.deepin.wrap.gnome.desktop.override schemas/com.deepin.dde.appearance.gschema.xml
  # fix network checker url
  sed -i "s#'http://detect.uniontech.com', 'http://detectportal.deepin.com'#'http://ping.archlinux.org/nm-check.txt'#" schemas/com.deepin.dde.network-utils.gschema.xml
  grep uniontech schemas/com.deepin.dde.network-utils.gschema.xml && exit 1 || :
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd deepin-desktop-schemas
  make
}

check() {
  cd deepin-desktop-schemas
  make test
}

package() {
  cd deepin-desktop-schemas
  make DESTDIR="$pkgdir" install
}
