# Maintainer: Martin Rys <https://rys.rs/contact>
# Contributor: a821 at (nospam) mail de
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: John Hamelink <me@johnhame.link>

pkgname=python-tidalapi
_pkgname=${pkgname#python-}
pkgver=0.8.10
pkgrel=2
pkgdesc="Unofficial API for TIDAL music streaming service"
arch=("any")
url="https://github.com/tamland/python-tidal"
license=("LGPL-3.0-or-later")
depends=(
	"python"
	"python-requests"
	"python-dateutil"
	"python-isodate"
	"python-mpegdash"
	"python-pyaes"
	"python-typing_extensions"
)
makedepends=("python-poetry")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/EbbLabs/python-tidal/releases/download/v${pkgver}/tidalapi-${pkgver}.tar.gz")
sha256sums=("f5b7d6ab19cfd40907941200a91d491f16d1b68b0323af9b0660bb739e0aab80")

build() {
	cd "${_pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

check() {
	# Tests require an active Tidal session
	# Just check that we can import tidalapi
	cd "${_pkgname}-${pkgver}"
	python -c "import tidalapi"
}

package() {
	export PYTHONHASHSEED=0
	cd "${_pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}/" dist/*.whl
	install -Dm644 README.rst HISTORY.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
