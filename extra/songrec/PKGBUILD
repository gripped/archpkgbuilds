# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

_pkgname=SongRec
pkgname=songrec
pkgver=0.6.1
pkgrel=1
pkgdesc='An open-source, unofficial Shazam client for Linux'
arch=('x86_64')
url='https://github.com/marin-m/SongRec'
license=('GPL-3.0-only')
depends=('gtk4' 'alsa-lib' 'openssl' 'ffmpeg' 'libpulse' 'pulse-native-provider'
         'glib2' 'pango' 'cairo' 'glibc' 'dbus' 'gcc-libs' 'hicolor-icon-theme'
         'gdk-pixbuf2' 'libadwaita')
makedepends=('cargo' 'pkgconf' 'git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('89f9a13755f8f35e40088f6e249811b7b6869664c5eaf49426a673b5b1dd9535')
options=('!lto')

prepare() {
  cd "$_pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$_pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm 755 "target/release/songrec" "$pkgdir/usr/bin/songrec"

  install -Dm 644 "packaging/rootfs/usr/share/applications/re.fossplant.songrec.desktop" \
                    "$pkgdir/usr/share/applications/re.fossplant.songrec.desktop"

  install -Dm 644 "packaging/rootfs/usr/share/icons/hicolor/scalable/apps/re.fossplant.songrec.svg" \
                    "$pkgdir/usr/share/icons/hicolor/scalable/apps/re.fossplant.songrec.svg"

  install -Dm 644 "packaging/rootfs/usr/share/metainfo/re.fossplant.songrec.metainfo.xml" \
                    "$pkgdir/usr/share/metainfo/re.fossplant.songrec.metainfo.xml"

  cp -r translations/*/ -t "$pkgdir/usr/share"

  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
