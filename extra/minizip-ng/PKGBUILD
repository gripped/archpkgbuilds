# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=minizip-ng
pkgname=(minizip-ng
         minizip-ng-compat)
pkgver=4.0.7
pkgrel=3
pkgdesc='Fork of the popular zip manipulation library found in the zlib distribution'
arch=(x86_64)
url='https://github.com/zlib-ng/minizip-ng'
license=(Zlib)
depends=(bzip2
         glibc
         openssl
         xz
         zlib-ng
         zstd)
makedepends=(cmake
             git)
source=(git+https://github.com/zlib-ng/minizip-ng#tag=$pkgver)
sha256sums=('f4c388042d4911e625f02ac6a7bef0bf5b2fca2227c6ed7ec64c65ade98e33d0')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMZ_LIB_SUFFIX="-ng" \
    -DBUILD_SHARED_LIBS=ON \
    -DMZ_COMPAT=OFF
  cmake --build build

  cmake -B build-compat -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build-compat
}

package_minizip-ng() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_minizip-ng-compat() {
  provides=(minizip)
  conflicts=(minizip)
  DESTDIR="$pkgdir" cmake --install build-compat
  install -Dm644 $pkgbase/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
