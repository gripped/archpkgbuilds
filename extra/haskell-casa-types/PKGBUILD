# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=casa-types
pkgname=haskell-casa-types
pkgver=0.0.3
pkgrel=17
pkgdesc="Content Addressable Storage Archive types"
url="https://github.com/commercialhaskell/casa"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base16-bytestring'
         'haskell-hashable' 'haskell-path-pieces' 'haskell-persistent')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('4aab5db507a4f9ad12e0b7ce34e6e384dd1a6d7878fb2a72c42bd072f7e44d321e560294777e32686a6d076ebcc08d1c709437ac6654b1491bbd8ab6aab533e8')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
