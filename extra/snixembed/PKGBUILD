pkgname=snixembed
pkgver=0.3.3
pkgrel=2
pkgdesc="proxy StatusNotifierItems as XEmbedded systemtray-spec icons"
url="https://git.sr.ht/~steef/snixembed"
arch=(x86_64)
license=(ISC)
depends=(
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libdbusmenu-gtk3
)
makedepends=(
  git
  vala
)
source=(
  "git+https://git.sr.ht/~steef/snixembed#tag=${pkgver}"
  avoid-massive-overlinking-by-using-gcc.patch
)
b2sums=('52d6990c0c57e67ad0a07f7ef725b185bd0de969210c077659779bdef82b800bc5c582b530dfe9f3be2d8d76697c3f020a4d436364f89feee56010a966e9e2d5'
        'b872b06388dedfe78fb1569b8ebf9e9d830873b9066592a085d687ab0d32cec65d2bd61df84cc63035e42a7be25f84bc64c418a6024d5c95c84089b66b3fe196')

prepare() {
	cd ${pkgname}
  # makefile: respect LDFLAGS
  git cherry-pick -n 1803c381b2a34a14ed36e28f886e6f17e7e68ba9
  # Handle non-standard icon sizes and resizing
  git cherry-pick -n 135406b7e698b8ae36eb7521953891b4def95a83
  patch -Np1 < ../avoid-massive-overlinking-by-using-gcc.patch
}

build() {
	cd ${pkgname}
	make
}

package() {
	cd ${pkgname}
	make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
