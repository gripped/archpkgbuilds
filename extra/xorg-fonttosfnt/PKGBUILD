# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Andreas Bosch (progandy) <linux+aur@progandy.de>

pkgname=xorg-fonttosfnt
_pkgname=fonttosfnt
pkgver=1.2.5
pkgrel=1
pkgdesc="Convert a set of bdf or pcf fonts in a bitmap only sfnt (otb)."
arch=('x86_64')
url="https://gitlab.freedesktop.org/xorg/app/fonttosfnt"
license=('MIT')
depends=( 'freetype2' 'libfontenc' 'glibc')
makedepends=('xorg-util-macros' 'xorgproto')
provides=('fonttosfnt')
conflicts=('fonttosfnt')
replaces=('fonttosfnt')
source=(https://xorg.freedesktop.org/archive/individual/app/$_pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('3be2a625234175dfc8a637b13f2d273afc82126ccd624a38afcc7ef3e13814dac0e55c8ae0a0a713725cf52078f8b45f3e86d7b357b1a19a55def55f969d0640'
            'SKIP')
# validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys=('3AB285232C46AE43D8E192F4DAB0F78EA6E7E2D2') # Alan Coopersmith <alan.coopersmith@oracle.com>                                                                                   

build() {
    cd "$_pkgname"-$pkgver
    ./configure --prefix=/usr
    make
}

check() {
    cd "$_pkgname"-$pkgver
    make check
}

package() {
    cd "$_pkgname"-$pkgver
    make DESTDIR="$pkgdir" install
    install -Dm 644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
