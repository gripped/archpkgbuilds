# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=fourmolu
pkgname=haskell-fourmolu
pkgver=0.11.0.0
pkgrel=49
pkgdesc="A formatter for Haskell source code"
url="https://github.com/parsonsmatt/fourmolu"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-dlist'
         'haskell-file-embed' 'haskell-ghc-lib-parser' 'haskell-megaparsec' 'haskell-memotrie'
         'haskell-optparse-applicative' 'haskell-syb' 'haskell-th-env' 'haskell-yaml')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-hspec-megaparsec' 'haskell-path' 'haskell-path-io' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('b9c581f07d2183eb0b673f3e1212cb144a6126e2fe62be34991f2b2df22047c3')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u ansi-terminal -u optparse-applicative
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  PATH="$PWD/dist/build/$_hkgname:$PATH" LD_LIBRARY_PATH="$PWD/dist/build" \
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
