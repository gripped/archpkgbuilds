# Contributor: Shengyun Wang <leo_wsy@outlook.com>
# Contributor: noureddinex
# Contributor: envolution
# Contributor: Aaron Coach <aur@awc.id.au>
# shellcheck shell=bash disable=SC2034,SC2154

pkgname=qwen-code
pkgver=0.11.0
pkgrel=1
pkgdesc='Open-source AI agent for the terminal, optimized for Qwen-Coder'
url='https://github.com/QwenLM/qwen-code'
arch=(x86_64)
license=(Apache-2.0)
depends=(
  libgcc
  libstdc++
  glibc
  nodejs
  ripgrep
)
makedepends=(
  git
  jq
  npm
)
source=("git+https://github.com/QwenLM/qwen-code.git#tag=v${pkgver}")
sha256sums=('7d6272d481b38661e2e7e3bca6197e881acb775329dc9160bc4115c04ed6c9d5')
b2sums=('79e1a4e7b7e3f573349a735c9381731f797c8dc86bc7419bf78b6edf79b37910bc2d583d941729bd34169e29da08c0fea7f4dbf933a3b333bb22811cfea75e03')

prepare() {
  cd $pkgname
  npm clean-install --ignore-scripts
}

build() {
  cd $pkgname
  npm run build
  npm run bundle
}

check() {
  cd $pkgname
  npm test
}

package() {
  cd $pkgname
  install -vDm755 dist/cli.js "$pkgdir/usr/bin/qwen"

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
