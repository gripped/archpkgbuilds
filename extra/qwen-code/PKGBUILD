# Contributor: Shengyun Wang <leo_wsy@outlook.com>
# Contributor: noureddinex
# Contributor: envolution
# Contributor: Aaron Coach <aur@awc.id.au>
# shellcheck shell=bash disable=SC2034,SC2154

pkgname=qwen-code
pkgver=0.10.6
pkgrel=1
pkgdesc='Open-source AI agent for the terminal, optimized for Qwen-Coder'
url='https://github.com/QwenLM/qwen-code'
arch=(x86_64)
license=(Apache-2.0)
depends=(
  libgcc
  libstdc++
  glibc
  nodejs
  ripgrep
)
makedepends=(
  git
  jq
  npm
)
source=("git+https://github.com/QwenLM/qwen-code.git#tag=v${pkgver}")
sha256sums=('1cdb77132a6d3ddf0ba8d1ecfc28c23eb09cfbbb12246f3041a64a35990c2ac5')
b2sums=('6ff41887201a5c9ba39648cc6d928021202ae06fef8da26637896f484d52bb02dc11d70f2240deff851b2da61ac57ab939c46b0ef08f195f9f1f95d04fb9e915')

prepare() {
  cd $pkgname
  npm clean-install --ignore-scripts
}

build() {
  cd $pkgname
  npm run bundle
}

check() {
  cd $pkgname
  npm run build
  npm test
}

package() {
  cd $pkgname
  install -vDm755 dist/cli.js "$pkgdir/usr/bin/qwen"

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
