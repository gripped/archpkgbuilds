# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Leewings Ng <osideal@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-table-chinese
pkgver=1.8.14
pkgrel=1
pkgdesc="Some table-based input method of tables engines for IBus"
arch=('any')
url="https://github.com/mike-fabian/ibus-table-chinese"
license=('GPL-3.0-only')
depends=('ibus-table')
makedepends=('git' 'cmake')
source=("git+https://github.com/mike-fabian/ibus-table-chinese.git#tag=$pkgver")
sha512sums=('b002198668d64c8c63488802b8d2ac2b17077787b9a813f3e46b842e3de61f58d96ba61c9d93cb718db5948ac6a9501a202ff7f93fcc993926de097c72dbd96e')

build() {
  cd $pkgname
  cmake . -DDATA_DIR=/usr/share -DPRJ_DOC_DIR=/usr/share/doc/$pkgname -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
