# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gpsbabel
pkgver=1.10.0
pkgrel=2
pkgdesc="Read, write and manipulate GPS waypoints in a variety of formats"
arch=(x86_64)
url="https://www.gpsbabel.org/"
license=('GPL-2.0-or-later')
depends=(hicolor-icon-theme libusb shapelib qt6-serialport qt6-5compat)
makedepends=(cmake libicns qt6-tools)
source=(https://github.com/gpsbabel/$pkgname/archive/${pkgname}_${pkgver//./_}.tar.gz
        gpsbabel.appdata.xml)
sha256sums=('a89756fb988a54f5c5f371413845b9aecb66628a594cd83bd529c0f18382c968'
            '7b4759c49316203fce76650312b4aa8c8d570471ccff1e8fcf258a2aa656c8e2')

prepare() {
  cd $pkgname-${pkgname}_${pkgver//./_}

  # Disable version check on startup
  # Disable usage data reporting
  # Disable donation dialog on close
  sed -i gui/babeldata.h \
    -e 's/startupVersionCheck_{true}/startupVersionCheck_{false}/' \
    -e 's/reportStatistics_{true}/reportStatistics_{false}/' \
    -e 's/disableDonateDialog_{false}/disableDonateDialog_{true}/'

  icns2png -x gui/images/appicon.icns
}

build() {
  cmake -B build -S $pkgname-${pkgname}_${pkgver//./_} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGPSBABEL_WITH_ZLIB=pkgconfig \
    -DGPSBABEL_WITH_SHAPELIB=pkgconfig \
    -DGPSBABEL_WITH_LIBUSB=pkgconfig \
    -DGPSBABEL_MAPPREVIEW=OFF
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  install -Dm755 build/gpsbabel build/gui/GPSBabelFE/gpsbabelfe -t "$pkgdir"/usr/bin
  install -Dm644 $pkgname-${pkgname}_${pkgver//./_}/gui/gpsbabel.desktop -t "$pkgdir"/usr/share/applications
  install -Dm644 $pkgname.appdata.xml -t "$pkgdir/usr/share/metainfo"

  for res in 16 32 128 256 512; do
    install -Dm644 $pkgname-${pkgname}_${pkgver//./_}/appicon_${res}x${res}x32.png \
      "$pkgdir/usr/share/icons/hicolor/${res}x${res}/apps/$pkgname.png"
  done
}
