# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=sentry-native
pkgver=0.10.1
pkgrel=1
pkgdesc="Sentry SDK for C, C++ and native applications"
arch=(x86_64)
url="https://github.com/getsentry/sentry-native"
license=(MIT)
depends=(
  curl
  gcc-libs
  glibc
  zlib
)
makedepends=(
  cmake
  git
)
checkdepends=(
  dotnet-sdk
  python-msgpack
  python-pytest
  python-pytest-httpserver
)
provides=(libsentry.so)
source=(
  "git+$url.git#tag=$pkgver"
  "$pkgname-libunwindstack-ndk::git+https://github.com/getsentry/libunwindstack-ndk.git"
  "$pkgname-breakpad::git+https://github.com/getsentry/breakpad.git"
  "$pkgname-linux-syscall-support::git+https://chromium.googlesource.com/linux-syscall-support.git"
  "$pkgname-crashpad::git+https://github.com/getsentry/crashpad.git"
  "$pkgname-mini_chromium::git+https://github.com/getsentry/mini_chromium.git"
  "$pkgname-zlib::git+https://chromium.googlesource.com/chromium/src/third_party/zlib.git"
)
sha256sums=('01cf4a1e2ae3efc8d8f40e7bf6cfdac091d58e3a18341b3565f4aaedf7eee62c'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.external/libunwindstack-ndk.url "$srcdir/$pkgname-libunwindstack-ndk"
  git config submodule.external/breakpad.url "$srcdir/$pkgname-breakpad"
  git config submodule.external/third_party/lss.url "$srcdir/$pkgname-linux-syscall-support"
  git config submodule.external/crashpad.url "$srcdir/$pkgname-crashpad"
  git -c protocol.file.allow=always submodule update

  cd external/crashpad
  git submodule init
  git config submodule.third_party/mini_chromium/mini_chromium.url "$srcdir/$pkgname-mini_chromium"
  git config submodule.third_party/zlib/zlib.url "$srcdir/$pkgname-zlib"
  git config submodule.third_party/lss/lss.url "$srcdir/$pkgname-linux-syscall-support"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd $pkgname
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cd $pkgname
  # Ignore Windows tests & deselect test requiring .NET 8.0.
  pytest \
    --deselect tests/test_dotnet_signals.py::test_dotnet_signals_inproc \
    --ignore external/crashpad/snapshot/win \
    --ignore external/crashpad/test/win
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
