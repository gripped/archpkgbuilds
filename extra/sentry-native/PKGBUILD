# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=sentry-native
pkgver=0.7.18
pkgrel=1
pkgdesc="Sentry SDK for C, C++ and native applications"
arch=(x86_64)
url="https://github.com/getsentry/sentry-native"
license=(MIT)
depends=(
  curl
  gcc-libs
  glibc
  zlib
)
makedepends=(
  cmake
  git
)
checkdepends=(
  dotnet-sdk
  python-msgpack
  python-pytest
  python-pytest-httpserver
)
provides=(libsentry.so)
source=(
  "git+$url.git#tag=$pkgver"
  "git+https://github.com/getsentry/libunwindstack-ndk.git"
  "git+https://github.com/getsentry/breakpad.git"
  "git+https://chromium.googlesource.com/linux-syscall-support.git"
  "git+https://github.com/getsentry/crashpad.git"
  "git+https://github.com/getsentry/mini_chromium.git"
  "git+https://chromium.googlesource.com/chromium/src/third_party/zlib.git"
)
sha256sums=('8b6341d950959707938f02891f2051a8e98ab24f763f88b44fc87c95cec958f6'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.external/libunwindstack-ndk.url "$srcdir/libunwindstack-ndk"
  git config submodule.external/breakpad.url "$srcdir/breakpad"
  git config submodule.external/third_party/lss.url "$srcdir/linux-syscall-support"
  git config submodule.external/crashpad.url "$srcdir/crashpad"
  git -c protocol.file.allow=always submodule update

  cd external/crashpad
  git submodule init
  git config submodule.third_party/mini_chromium/mini_chromium.url "$srcdir/mini_chromium"
  git config submodule.third_party/zlib/zlib.url "$srcdir/zlib"
  git config submodule.third_party/lss/lss.url "$srcdir/linux-syscall-support"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd $pkgname
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cd $pkgname
  # Ignore Windows tests.
  pytest \
    --ignore external/crashpad/snapshot/win \
    --ignore external/crashpad/test/win
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
