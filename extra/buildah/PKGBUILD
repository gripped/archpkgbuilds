# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=buildah
pkgver=1.39.4
pkgrel=1
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/containers/buildah"
license=(Apache-2.0)
depends=(oci-runtime skopeo passt libseccomp gpgme)
makedepends=(go git bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man skopeo systemd)
options=(!lto !debug)
source=(git+https://github.com/containers/buildah.git#tag=v$pkgver)
sha512sums=('348bc1beab20d23c60512b910bd8ab1055cca185c519ba63c3cdcf579ec6e2a6866721e655eb213992c5454b881c43f83371c8a80862f16fd75443fc0aa92b04')

build() {
  cd $pkgname
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  make PREFIX=/usr BUILDTAGS="containers_image_ostree_stub seccomp"
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=usr install install.completions
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
