# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=buildah
pkgver=1.40.0
pkgrel=2
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/containers/buildah"
license=(Apache-2.0)
depends=(oci-runtime skopeo passt libseccomp gpgme)
makedepends=(go git bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man skopeo systemd)
options=(!lto !debug)
source=(git+https://github.com/containers/buildah.git#tag=v$pkgver)
sha512sums=('6feaba937afd4b1646e5124c29d4a5da09057163e5c00f490b9a195b49c6c4dd3466bde1e8537448d5d2a9f86e3822eb54606f03fde6625e833dfc44ca1b4f8a')

build() {
  cd $pkgname
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  make PREFIX=/usr BUILDTAGS="containers_image_ostree_stub seccomp"
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=usr install install.completions
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
