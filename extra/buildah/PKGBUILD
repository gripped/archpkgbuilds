# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=buildah
pkgver=1.35.1
pkgrel=1
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/containers/buildah"
license=(Apache)
depends=(crun skopeo slirp4netns)
optdepends=('passt: for rootless network support')
makedepends=(go git bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man runc skopeo systemd)
options=(!lto !debug)
source=(git+https://github.com/containers/buildah.git#tag=v$pkgver)
sha512sums=('d224e5d33cc193e3e23954a85ea494b846b7b6d856689c4f844a4c967adc51af4b9bde508b470b349c955b35536c872f084caf0e464c255c89e271b8f01c64bc')

pkgver() {
  cd buildah
  git describe --tags | sed 's/^[vV]//;s/-/+/g'
}

build() {
  cd $pkgname
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  make PREFIX=/usr BUILDTAGS="containers_image_ostree_stub seccomp"
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=usr install install.completions
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
