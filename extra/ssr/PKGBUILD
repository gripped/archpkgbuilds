# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=ssr
pkgver=0.6.1
pkgrel=1
pkgdesc="A tool for real-time spatial audio reproduction"
arch=(x86_64)
url="http://spatialaudio.net/ssr/"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(bash fmt gcc-libs glibc glu hicolor-icon-theme libmysofa libxml2 qt5-base)
makedepends=(asio ecasound fftw gendesk help2man jack libglvnd libsndfile qt5-tools websocketpp)
source=(
  https://github.com/SoundScapeRenderer/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz
  reproducible.patch
  asio134.patch
)
sha512sums=('51418c983ce61178417466ed0fbddc46b00079a4d2329023ffe6b699edae5eea04b23737065bb864f50166f7eb3b975193287fc973d6a466ca6075fb3faa76fe'
            'bb86b74d09963e51dac59bacc0ec2bdac9442898e9ade99c51bce9c4c4e039263c107d53f17019b8c8538aca6fe6155f805afff194deb275b45c52a76401f05a'
            '3d9b04a68817b60f1742d30b675c32c912fcd98538e07144e492a010f6609965df2d65d2ba2c1ae949614e45a104fab98b1a5050ee528cc34a42e55154aa514e')
_name="SoundScape Renderer"

prepare() {
  local name
  declare -A _descriptions=(
    ["${pkgname}"]="${_name}: ${pkgdesc}"
    ["${pkgname}-aap"]="${_name} (Ambisonics Amplitude Panner): ${pkgdesc}"
    ["${pkgname}-binaural"]="${_name} (Binaural): ${pkgdesc}"
    ["${pkgname}-brs"]="${_name} (Binaural Room Synthesis): ${pkgdesc}"
    ["${pkgname}-dca"]="${_name} (Distance-coded Ambisonics): ${pkgdesc}"
    ["${pkgname}-generic"]="${_name} (Generic): ${pkgdesc}"
    ["${pkgname}-vbap"]="${_name} (Vector Based Amplitude Panning): ${pkgdesc}"
    ["${pkgname}-wfs"]="${_name} (Wave Field Synthesis): ${pkgdesc}"
  )

  patch -Np1 -d $pkgname-$pkgver < reproducible.patch
  patch -Np1 -d $pkgname-$pkgver < asio134.patch

  for name in "${!_descriptions[@]}"; do
    gendesk -n \
            --pkgname $name \
            --name $name \
            --icon $pkgname \
            --pkgdesc "${_descriptions[$name]}" \
            --genericname "Spatial Audio Renderer" \
            --categories "AudioVideo;Audio"
  done

  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-vrpn --disable-dynamic-asdf --disable-intersense
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    fftw libfftw3f.so
    jack libjack.so
    libglvnd libGL.so
    libsndfile libsndfile.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications/"

  # add icon so that XDG desktop integration can pick it up
  install -vDm 644 "$pkgdir/usr/share/$pkgname/images/${pkgname}_logo.png" "$pkgdir/usr/share/icons/hicolor/64x64/apps/$pkgname.png"
}
