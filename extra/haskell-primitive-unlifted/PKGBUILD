# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=primitive-unlifted
pkgname=haskell-primitive-unlifted
pkgver=2.1.0.0
pkgrel=19
pkgdesc="Primitive GHC types with unlifted types inside"
url="https://github.com/haskell-primitive/primitive-unlifted"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-primitive' 'haskell-text-short')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-classes-base' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        $pkgname-primitive-0.9.patch::https://github.com/haskell-primitive/primitive-unlifted/commit/26922952ef20c4771d857f3e96c9e710cb3c2df9.patch)
sha256sums=('613421078a2f4c012a99233dbc7a8ff0a17a0cb5abfd9baf544f4695751a3d1e'
            '97fb0bcf87264a9bd5529305b8dd57f3d05c3375bcf07512b82b1dc1785b5283')

prepare() {
  cd $_hkgname-$pkgver
  patch -p1 -i ../$pkgname-primitive-0.9.patch
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
