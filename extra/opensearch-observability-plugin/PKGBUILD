# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-observability-plugin
pkgver=2.18.0.0
pkgrel=1
_opensearchver=2.18.0
_jdkver=11
pkgdesc="OpenSearch Observability Plugin"
arch=('x86_64')
url="https://github.com/opensearch-project/observability"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/observability/archive/${pkgver}.tar.gz"
)
sha256sums=('3c46f09a5bedb1dd48bef8aacc0682d6d4f0f69d94e460c58fd1051e24520609')

build() {
  cd "observability-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-observability"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-observability"
  unzip "${srcdir}/observability-${pkgver}/build/distributions/opensearch-observability-${pkgver}.zip"

  install -Dm644 "${srcdir}/observability-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
