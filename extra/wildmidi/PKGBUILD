# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Panagiotis Papadopoulos

pkgname=wildmidi
pkgver=0.4.6
pkgrel=1
pkgdesc='Simple software MIDI player which has a core softsynth library'
arch=(x86_64)
url='https://github.com/Mindwerks/wildmidi'
license=('GPL-3.0-or-later AND LGPL-3.0-or-later')
depends=(
  alsa-lib
  glibc
)
makedepends=(
  cmake
  git
  ninja
)
source=("git+https://github.com/Mindwerks/wildmidi.git#tag=$pkgname-$pkgver")
b2sums=(33100dfb9a7df1ee516083f90d7a2600e1c79117093dba568df540fd602da408cfe8373bea7363efbc893b1db943c681887e44b00f0861c1b29ac037d4b019f7)

build() {
  cmake -S $pkgname -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  ln -s wildmidi.pc "$pkgdir/usr/lib/pkgconfig/WildMIDI.pc"
}
