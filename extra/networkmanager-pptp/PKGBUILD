# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=1.2.12
pkgrel=6
pkgdesc="NetworkManager VPN plugin for PPTP"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=(x86_64)
license=(GPL)
depends=(
  libnm
  libsecret
  ppp
  pptpclient
)
makedepends=(
  expac
  git
  glib2-devel
  libnma
  libnma-gtk4
  python
)
optdepends=(
  'libnma-gtk4: GUI support (GTK 4)'
  'libnma: GUI support (GTK 3)'
)
_commit=ad5a2951d5f281eeea482404058ace8d15e23f30  # tags/1.2.12^0
source=("git+https://gitlab.gnome.org/GNOME/NetworkManager-pptp.git#commit=$_commit")
b2sums=('1dbd40c92f0f93ffe050822ce4187f71b4f808bf9cb3f05f2549957315771c7bb70b5b2b44e261a7481edfb85c9a33c61a5e94a4df735f80c2eb1a56efb897e0')

pkgver() {
  cd NetworkManager-pptp
  git describe --tags | sed 's/-dev/dev/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd NetworkManager-pptp
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --disable-static
    --with-gtk4
    --with-pppd-plugin-dir=/usr/lib/pppd/$(expac %v ppp | cut -d - -f 1)
  )

  cd NetworkManager-pptp
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd NetworkManager-pptp
  make DESTDIR="$pkgdir" install dbusservicedir=/usr/share/dbus-1/system.d
}

# vim:set sw=2 sts=-1 et:
