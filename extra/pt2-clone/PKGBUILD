# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pt2-clone
pkgver=1.77
pkgrel=1
pkgdesc='A highly accurate clone of the classic ProTracker 2.3D software for Amiga'
arch=('x86_64') # note: not compatible with big-endian CPUs
url='https://16-bits.org/pt2.php'
license=('BSD-3-Clause')
depends=('glibc' 'alsa-lib' 'sdl2' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
install='pt2-clone.install'
source=("$pkgname::git+https://github.com/8bitbubsy/pt2-clone#tag=v$pkgver")
sha512sums=('0d04915a4f4f5aac9a1bf9bf6766ff9ac7a19ecc6d03536570a2b7fcc98968acdeb26a3bd4df9e82b23e7231b63aad4863daf031b0aed43cd2dc36097640155c')
b2sums=('fb22e5d6f0e201564fc81d906374706b865093de0a91667a7563d8574ecdea07994d11ee492f1eae7e3e83a210fb76ec2bdfb12e2a019a1982d6da5647638cd6')

prepare() {
  cd "$pkgname"

  # modify desktop file
  sed \
    -e 's/Icon.*/Icon=pt2-clone/' \
    -e 's/Comment.*/Comment = Music Tracker/' \
    -i "release/other/Freedesktop.org Resources/ProTracker 2 clone.desktop"
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # desktop file
  install -vDm644 \
    "release/other/Freedesktop.org Resources/ProTracker 2 clone.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # application icon
  install -vDm644 \
    "release/other/Freedesktop.org Resources/ProTracker 2 clone.png" \
    "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md release/*.txt release/other/protracker.ini

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
  install -vDm644 "release/other/Freedesktop.org Resources/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.freedesktop-resources"
}
