# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pt2-clone
pkgver=1.76
pkgrel=1
pkgdesc='A highly accurate clone of the classic ProTracker 2.3D software for Amiga'
arch=('x86_64') # note: not compatible with big-endian CPUs
url='https://16-bits.org/pt2.php'
license=('BSD-3-Clause')
depends=('glibc' 'alsa-lib' 'sdl2' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
install='pt2-clone.install'
source=("$pkgname::git+https://github.com/8bitbubsy/pt2-clone#tag=v$pkgver")
sha512sums=('3b61cf81a37b3d3cd8153416246a6dc6a36ec79523294433b219361e97dcb56d4da62a526f59fe7233962339a7829f7e379a2fc6999250e0e199b4e35a8992d5')
b2sums=('6224c092becfd3c901fbb9010daaa989f8f75348c28713f8f3bc50ce3f926b470ea20cd6ecbdcba7a2896f540d918d665a476cd6f128d3cec83023c111230eee')

prepare() {
  cd "$pkgname"

  # modify desktop file
  sed \
    -e 's/Icon.*/Icon=pt2-clone/' \
    -e 's/Comment.*/Comment = Music Tracker/' \
    -i "release/other/Freedesktop.org Resources/ProTracker 2 clone.desktop"
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # desktop file
  install -vDm644 \
    "release/other/Freedesktop.org Resources/ProTracker 2 clone.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # application icon
  install -vDm644 \
    "release/other/Freedesktop.org Resources/ProTracker 2 clone.png" \
    "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md release/*.txt release/other/protracker.ini

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
  install -vDm644 "release/other/Freedesktop.org Resources/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.freedesktop-resources"
}
