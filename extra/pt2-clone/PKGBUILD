# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pt2-clone
pkgver=1.80
pkgrel=1
pkgdesc='A highly accurate clone of the classic ProTracker 2.3D software for Amiga'
arch=(x86_64) # note: not compatible with big-endian CPUs
url='https://16-bits.org/pt2.php'
license=(BSD-3-Clause)
depends=(glibc alsa-lib sdl2 hicolor-icon-theme)
makedepends=(git cmake)
install=pt2-clone.install
source=("$pkgname::git+https://github.com/8bitbubsy/pt2-clone#tag=v$pkgver")
sha512sums=('cdb707e71f8aacb9829a0c83bf07c7ac53147e465dab93a589c8ad827ef69eff44f3bbaa7214c3b8279ae7e4c4a952a56977279bd7489077cb346c373f91ca4c')
b2sums=('7f5f90088e883c6ca9fece5b90bcd2999dc87a783ca829b688525b14316996fef63c962d8143f51f414d4938827d730c34237436c1247c7445df06ac61d32a39')

prepare() {
  cd "$pkgname"

  # modify desktop file
  sed \
    -e 's/Icon.*/Icon=pt2-clone/' \
    -e 's/Comment.*/Comment = Music Tracker/' \
    -i "release/other/Freedesktop.org Resources/ProTracker 2 clone.desktop"
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # desktop file
  install -vDm644 \
    "release/other/Freedesktop.org Resources/ProTracker 2 clone.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # application icon
  install -vDm644 \
    "release/other/Freedesktop.org Resources/ProTracker 2 clone.png" \
    "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md release/*.txt release/other/protracker.ini

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
  install -vDm644 "release/other/Freedesktop.org Resources/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.freedesktop-resources"
}
