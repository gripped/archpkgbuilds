# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pt2-clone
pkgver=1.78
pkgrel=1
pkgdesc='A highly accurate clone of the classic ProTracker 2.3D software for Amiga'
arch=('x86_64') # note: not compatible with big-endian CPUs
url='https://16-bits.org/pt2.php'
license=('BSD-3-Clause')
depends=('glibc' 'alsa-lib' 'sdl2' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
install='pt2-clone.install'
source=("$pkgname::git+https://github.com/8bitbubsy/pt2-clone#tag=v$pkgver")
sha512sums=('df334968e1c3afee22af74340058ed614718365718fa188779107e7a103f80e19e6f8e57bb0ac8dec817d77f630bc27aed59a4d87db69a55cb1e6672a9b28506')
b2sums=('7394154244071397443c0815c3702d27b1f9bd3d838b650664751e99c741544c7fb7b98d2257552816cbadab67a0f72e0229109618bd55e9ff2bf23368e3eceb')

prepare() {
  cd "$pkgname"

  # modify desktop file
  sed \
    -e 's/Icon.*/Icon=pt2-clone/' \
    -e 's/Comment.*/Comment = Music Tracker/' \
    -i "release/other/Freedesktop.org Resources/ProTracker 2 clone.desktop"
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # desktop file
  install -vDm644 \
    "release/other/Freedesktop.org Resources/ProTracker 2 clone.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # application icon
  install -vDm644 \
    "release/other/Freedesktop.org Resources/ProTracker 2 clone.png" \
    "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md release/*.txt release/other/protracker.ini

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
  install -vDm644 "release/other/Freedesktop.org Resources/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.freedesktop-resources"
}
