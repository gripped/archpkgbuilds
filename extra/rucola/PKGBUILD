# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: linus.mussmaecher@gmail.com

pkgname=rucola
pkgver=0.5.0
pkgrel=1
pkgdesc="Terminal-based markdown note manager"
arch=('x86_64')
url="https://github.com/Linus-Mussmaecher/rucola"
license=("GPL-3.0-only")
depends=('gcc-libs')
makedepends=('cargo')
replaces=('rucola-notes')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('7c5fe6e47751486763c823d51c6d46870cb7f4413f1e4de95f1b15e617081225')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" # --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
