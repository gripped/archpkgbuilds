# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: linus.mussmaecher@gmail.com

pkgname=rucola
pkgver=0.8.0
pkgrel=1
pkgdesc="Terminal-based markdown note manager"
arch=('x86_64')
url="https://github.com/Linus-Mussmaecher/rucola"
license=("GPL-3.0-only")
depends=('gcc-libs')
makedepends=('cargo')
replaces=('rucola-notes')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('f05f473bc84fdb85af0b3507f0958114cd28b90eca68078c45b8e4e42d2a2490')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" # --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 default-config/*.{css,toml} -t "$pkgdir/usr/share/doc/$pkgname/default-config"
}

# vim: ts=2 sw=2 et:
