# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Aoibhinn Nic Aoidh <oibindnicaoidh@gmail.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: J. W. Birdsong <jwbirdsong@gmail.com>

pkgname=luakit
pkgver=2.3.6
pkgrel=2
pkgdesc='Fast, small, webkit based browser framework extensible by Lua'
arch=(x86_64)
url=https://luakit.github.io/
license=(GPL3)
depends=(
  lua51-filesystem
  luajit
  webkit2gtk-4.1
)
makedepends=(
  git
  help2man
)
backup=(
  etc/xdg/luakit/rc.lua
  etc/xdg/luakit/theme.lua
)
_tag=a7e0cddaa590d44625adcaa7423bb68798d9a087
source=(git+https://github.com/luakit/luakit.git#tag=${_tag}
        do_not_compress_man_pages_in_Makefile.patch)
b2sums=('83644caf27236c062ddb82aa0b39d7307305a790f26bc77c66000204a11713748d86f88867271db2f5e3f697e29c5b0d7055abd59ac99d4bc1a3b65a3ef672d1'
        '3083f478c6c81ec2869f1fe31ba7e258801d4d24bdbe5d647a54ae93d71de96d83734ad1b37418d56136da8f632b6b54b7971ce1101b5c4a81722a242e69918c')

pkgver() {
  cd luakit
  git describe --tags
}

prepare() {
  cd luakit
  # Backport upstream patch to not compress man pages in the Makefile
  # This makes the package reproducible as a side benefit
  # See https://github.com/luakit/luakit/commit/6b00226d922f75f376d1f60a46901de460954a8b
  patch -Np1 -i "${srcdir}/do_not_compress_man_pages_in_Makefile.patch"
}

build() {
  make DEVELOPMENT_PATHS=0 PREFIX=/usr USE_LUAJIT=1 -C luakit
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr -C luakit install
}

# vim: ts=2 sw=2 et:
