# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=metadata-cleaner
pkgver=2.5.6
pkgrel=5
pkgdesc='Python GTK application to view and clean metadata in files, using mat2'
arch=(any)
url='https://gitlab.com/rmnvgr/metadata-cleaner'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  mat2
  python
  python-gobject
)
makedepends=(
  appstream
  git
  itstool
  meson
)
source=(
  "git+https://gitlab.com/rmnvgr/metadata-cleaner.git?signed#tag=v$pkgver"
  metadata-cleaner-new-window.patch
)
b2sums=(
  c9131565149088f890f8d49395bcaa761928522fac2cbe2c1cf0063f65fb3fec78ee044828a48022ea9e19d78afd603ff3080223d9e87f9f6469bf3564aba52c
  82cc6763153bf2a36eb332be729579f908a370552c5e75990a5d9bf9c3d20cae41f21a00a0ce15ac53f7fc746f667f1ae195b5f86d92ce44cc217a8ea0285270
)
validpgpkeys=(8E6737F5943A27EECEBE14528C66D1CA798D512D) # Romain Vigier <romain@romainvigier.fr>

prepare() {
  cd $pkgname

  # Add "new-window" command-line option
  git apply -3 ../metadata-cleaner-new-window.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
