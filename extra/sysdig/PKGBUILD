# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.40.1
pkgrel=1

pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('Apache-2.0')
depends=('luajit' 'yaml-cpp' 'nlohmann-json' 'falcosecurity-libs' 'ncurses' 'scap-dkms')
makedepends=('cmake' 'pandoc' 'gtest' 'clang' 'git' 'ninja')
options=('!lto')
source=("git+https://github.com/draios/sysdig.git#tag=$pkgver"
        "bashcomp-location.patch"
        "falcosecurity-libs-nodownload.patch")
sha256sums=('4bfc7db6ad98fecef3ebd28c1068cfbb99d74eadcc0e78d991b661dfb144a5ea'
            '3b659326176c314eee9115adac39a249dc4b9530511b344ea6a2b23236bb8386'
            '3392204c265ef46c2a1378fc2acbb74b2b440585de4c9127a007f97ce10f0cfa')

prepare() {
  cd "$srcdir/sysdig"
  mkdir -p build
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/falcosecurity-libs-nodownload.patch
}

build() {
  cd "$srcdir"/$pkgbase/build
  cmake .. \
    -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations -I/usr/include/falcosecurity/driver/ -I/usr/include/falcosecurity/libsinsp/ -I/usr/include/falcosecurity/libscap/" \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
    -DCMAKE_CXX_STANDARD=17 \
    -DCMAKE_CXX_EXTENSIONS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSDIG_VERSION=$pkgver \
    -DUSE_BUNDLED_FALCOSECURITY_LIBS=OFF \
    -DUSE_BUNDLED_DEPS=OFF \
    -GNinja
  cmake --build .
}

package() {
  cd "$srcdir"/$pkgbase/build
  DESTDIR="$pkgdir" cmake --install .

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 "$srcdir"/$pkgname/{NOTICES,COPYING} "$pkgdir"/usr/share/licenses/$pkgname
}
