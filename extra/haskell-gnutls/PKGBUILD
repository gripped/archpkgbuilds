# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=gnutls
pkgname=haskell-gnutls
pkgver=0.3.1
pkgrel=1
pkgdesc="Bindings for GNU libgnutls"
url="https://git.singpolyma.net/haskell-gnutls"
license=('GPL-3.0-only')
arch=('x86_64')
depends=('ghc-libs' "gnutls" "haskell-unexceptionalio-trans")
makedepends=('ghc' 'c2hs')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2f848b5c52c9075ecbba3c81cdbc1e19b7b2bc845a7ffa8aca172807fa860044401e7651d399ad9f2bf06dc269c543db1d8eea6c4aa73a7f7cc56fb04577436a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
