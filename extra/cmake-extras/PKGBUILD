# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Robert Tari <robert at tari dot in>

pkgname=cmake-extras
pkgver=1.9
pkgrel=1
pkgdesc="A collection of add-ons for the CMake build tool"
arch=("any")
url="https://gitlab.com/ubports/development/core/${pkgname}"
license=('LGPL-3.0-or-later')
depends=('cmake' 'python' 'bash')
makedepends=('git' 'ninja')
source=("git+https://gitlab.com/ubports/development/core/cmake-extras.git#tag=$pkgver")
sha512sums=('ed2ee373e8619fb30771ae1ac3e4ebc17c7d099fc96ff1bc41aaca4f38943d855c1e51bc0ff4155854a32b0709d1e0d45dd7c4cbdc334e57a6cc7df5763986f9')

build() {
  cd cmake-extras
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  ninja
}

package() {
  cd cmake-extras
  DESTDIR="$pkgdir" ninja install
}
