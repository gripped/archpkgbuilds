# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=eartag
pkgver=1.0.2
pkgrel=3
pkgdesc='Simple audio file tag editor'
arch=(any)
url='https://apps.gnome.org/EarTag/'
license=(MIT)
groups=(gnome-circle)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  python
  python-aiofiles
  python-aiohttp
  python-aiohttp-retry
  python-filetype
  python-gobject
  python-mutagen
  python-pillow
  python-pyacoustid
  python-xxhash
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
source=("git+https://gitlab.gnome.org/World/eartag.git#tag=$pkgver")
b2sums=(4bb90a6318b5da577d68a1e5c40faaa1f447fcbb664440c3376f8dfbb126fc3cc8f8db76d136f47e0fe2ddcd569de51c6b1c92a723f57db197d0120fe0643101)

prepare() {
  # fix python-pytest-asyncio error: '"function"' is not a valid asyncio_default_fixture_loop_scope. Valid scopes are: function, class, module, package, session.
  sed -i 's|asyncio_default_fixture_loop_scope = "function"|asyncio_default_fixture_loop_scope = function|' $pkgname/pytest.ini
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname/COPYING

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
