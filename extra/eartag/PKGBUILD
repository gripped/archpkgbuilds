# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=eartag
pkgver=1.0.1
pkgrel=1
pkgdesc='Simple audio file tag editor'
arch=(any)
url='https://apps.gnome.org/EarTag/'
license=(MIT)
groups=(gnome-circle)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  python
  python-aiofiles
  python-aiohttp
  python-aiohttp-retry
  python-filetype
  python-gobject
  python-mutagen
  python-pillow
  python-pyacoustid
  python-xxhash
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
source=("git+https://gitlab.gnome.org/World/eartag.git#tag=$pkgver")
b2sums=(292ca873422d15621b59c489e612e19364da501f8d9f76924a0c876a794b34c2474338fdf19dff82b634c3d2c001d404fc84cbd1ec573628fbbbaf820f1939f8)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname/COPYING

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
