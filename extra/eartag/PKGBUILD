# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=eartag
pkgver=0.6.3
pkgrel=2
pkgdesc='Simple audio file tag editor'
arch=(any)
url='https://apps.gnome.org/EarTag/'
license=(MIT)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  python
  python-gobject
  python-magic
  python-mutagen
  python-pillow
  python-pyacoustid
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
checkdepends=(python-pytest)
source=("git+https://gitlab.gnome.org/World/eartag.git#tag=$pkgver")
b2sums=(f86dc42b34981908948240f2f18a07a2a83ad90de79164484f8495598a3b7b2a90c221710cdb51fd5f229937643b1120ff066a63dad22b81b545e2cea427df3f)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname/COPYING

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
