# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Luna Jernberg <lunajernberg@gnome.org>

pkgname=tuba
pkgver=0.10.0
pkgrel=1
pkgdesc='Browse the Fediverse'
arch=(x86_64)
url='https://apps.gnome.org/Tuba/'
license=('GPL-3.0-only')
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gst-libav
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  icu
  json-glib
  libadwaita
  libclapper
  libclapper-gtk
  libgee
  libgexiv2
  libsecret
  libsoup3
  libspelling-1.so
  libxml2
  org.freedesktop.secrets
  pango
  webkitgtk-6.0
  webp-pixbuf-loader
)
makedepends=(
  git
  meson
  vala
)
groups=(gnome-circle)
source=("git+https://github.com/GeopJr/Tuba.git#tag=v$pkgver")
b2sums=('76f02ef9f8c4962a9357ba3fc7808ea3b22baa0c0b8d78fba7401c7ad454ed3d4083ef36dd36ac4816dcbc20e78d4dede63e4a5b7c1d8161156ce70a21989218')

prepare() {
  cd Tuba
}

build() {
  local meson_options=(
    -D clapper=true
  )

  arch-meson Tuba build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -sr "$pkgdir"/usr/bin/{dev.geopjr.Tuba,tuba}
}
