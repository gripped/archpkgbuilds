# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Luna Jernberg <lunajernberg@gnome.org>

pkgname=tuba
pkgver=0.9.0
pkgrel=1
pkgdesc='Browse the Fediverse'
arch=(x86_64)
url='https://apps.gnome.org/Tuba/'
license=('GPL-3.0-only')
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gst-libav
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  icu
  json-glib
  libadwaita
  libclapper
  libclapper-gtk
  libgee
  libsecret
  libsoup3
  libspelling-1.so
  libxml2
  org.freedesktop.secrets
  pango
  webp-pixbuf-loader
)
makedepends=(
  git
  meson
  vala
)
source=("git+https://github.com/GeopJr/Tuba.git#tag=v$pkgver")
b2sums=('fc1eba164c6daeb2b4f8a2888593d796c657ec8eec6e0c90db5882c144a47a8598b476404c24b53aaf40bd80226eaebfbc93383841e076030b05a3a2f305dcb2')

prepare() {
  cd Tuba
}

build() {
  local meson_options=(
    -D clapper=true
  )

  arch-meson Tuba build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -sr "$pkgdir"/usr/bin/{dev.geopjr.Tuba,tuba}
}
