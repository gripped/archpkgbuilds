# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Luna Jernberg <lunajernberg@gnome.org>

pkgname=tuba
pkgver=0.10.3
pkgrel=3
pkgdesc='Browse the Fediverse'
arch=(x86_64)
url='https://apps.gnome.org/Tuba/'
license=('GPL-3.0-only')
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gst-libav
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  icu
  json-glib
  libadwaita
  libclapper
  libclapper-gtk
  libgee
  libgexiv2
  libsecret
  libsoup3
  libspelling-1.so
  libxml2
  org.freedesktop.secrets
  pango
  webkitgtk-6.0
)
makedepends=(
  git
  meson
  vala
)
groups=(gnome-circle)
source=("git+https://github.com/GeopJr/Tuba.git#tag=v$pkgver")
b2sums=('b5538b04eab54fb621bbccbb3cf5bdeb457487ca28e4de51951c37c31968348f67a430bdf327a197509a5b9c0d645484129618329a90360c0f0c5ab1d79271fe')

prepare() {
  cd Tuba
}

build() {
  local meson_options=(
    -D clapper=true
  )

  arch-meson Tuba build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -sr "$pkgdir"/usr/bin/{dev.geopjr.Tuba,tuba}
}
