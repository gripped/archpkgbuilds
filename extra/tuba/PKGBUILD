# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Luna Jernberg <lunajernberg@gnome.org>

pkgname=tuba
pkgver=0.9.1
pkgrel=1
pkgdesc='Browse the Fediverse'
arch=(x86_64)
url='https://apps.gnome.org/Tuba/'
license=('GPL-3.0-only')
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gst-libav
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  icu
  json-glib
  libadwaita
  libclapper
  libclapper-gtk
  libgee
  libsecret
  libsoup3
  libspelling-1.so
  libxml2
  org.freedesktop.secrets
  pango
  webp-pixbuf-loader
)
makedepends=(
  git
  meson
  vala
)
source=("git+https://github.com/GeopJr/Tuba.git#tag=v$pkgver")
b2sums=('d50d971d36447c1fdc160648a1bc4240bb49d355c12391b5afd7cc11832581bf8ee928a8addc0c5d70d7ed04f5ae9c7c270feca7eaf9f20a1c2f8749a965cce3')

prepare() {
  cd Tuba
}

build() {
  local meson_options=(
    -D clapper=true
  )

  arch-meson Tuba build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -sr "$pkgdir"/usr/bin/{dev.geopjr.Tuba,tuba}
}
