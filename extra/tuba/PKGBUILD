# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Luna Jernberg <lunajernberg@gnome.org>

pkgname=tuba
pkgver=0.8.3
pkgrel=1
pkgdesc='Browse the Fediverse'
arch=(x86_64)
url='https://tuba.geopjr.dev/'
license=('GPL-3.0-only')
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  gtksourceview5
  hicolor-icon-theme
  icu
  json-glib
  libadwaita
  libgee
  libsecret
  libsoup3
  libspelling
  libxml2
  org.freedesktop.secrets
  pango
  webp-pixbuf-loader
)
makedepends=(
  git
  meson
  vala
)

source=("git+https://github.com/GeopJr/Tuba.git#tag=v$pkgver")
b2sums=('6c96614351cad169260ab13576938f0eedaf49dd24381bc109fa10902a00475c9cff474e23e46c179239bb0a3544ff5456d1445d0b8f127b09e153aa1cd486cb')

prepare() {
  cd Tuba
}

build() {
  arch-meson Tuba build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s "/usr/bin/dev.geopjr.Tuba" "$pkgdir/usr/bin/tuba" 
}
