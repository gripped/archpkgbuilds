# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Luna Jernberg <lunajernberg@gnome.org>

pkgname=tuba
pkgver=0.9.2
pkgrel=2
pkgdesc='Browse the Fediverse'
arch=(x86_64)
url='https://apps.gnome.org/Tuba/'
license=('GPL-3.0-only')
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gst-libav
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  icu
  json-glib
  libadwaita
  libclapper
  libclapper-gtk
  libgee
  libsecret
  libsoup3
  libspelling-1.so
  libxml2
  org.freedesktop.secrets
  pango
  webp-pixbuf-loader
)
makedepends=(
  git
  meson
  vala
)
source=("git+https://github.com/GeopJr/Tuba.git#tag=v$pkgver")
b2sums=('12c0e16d7b56662e455d8b96e33afe9536086a2364521389ac770ae20785983d0bca130922b5d4c0188890911a7aa41baf5daeaa9b3eca336fce84c8787d7553')

prepare() {
  cd Tuba
}

build() {
  local meson_options=(
    -D clapper=true
  )

  arch-meson Tuba build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -sr "$pkgdir"/usr/bin/{dev.geopjr.Tuba,tuba}
}
