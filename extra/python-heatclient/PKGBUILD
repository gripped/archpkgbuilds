# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-heatclient
pkgver=4.1.0
pkgrel=1
pkgdesc="OpenStack Orchestration API Client Library"
arch=('any')
url="https://docs.openstack.org/python-heatclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cliff' 'python-iso8601' 'python-osc-lib' 'python-prettytable'
         'python-oslo-i18n' 'python-oslo-serialization' 'python-oslo-utils' 'python-keystoneauth1'
         'python-swiftclient' 'python-yaml' 'python-requests' 'python-six')
makedepends=('git')
checkdepends=('python-stestr' 'python-testscenarios' 'python-requests-mock')
source=("git+https://github.com/openstack/python-heatclient.git#tag=$pkgver")
sha512sums=('1563536150cb06738de4d9f660553223c3f657118b041c7ede81403baaebb95ab3d12d81b91ae43361642dc283a541e69834492c5218b08f2589c6762648c2f2')

build() {
  cd python-heatclient
  python setup.py build
}

check() {
  cd python-heatclient
  stestr run
}

package() {
  cd python-heatclient
  python setup.py install --root="$pkgdir" --optimize=1
}
