# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-heatclient
pkgver=4.2.0
pkgrel=1
pkgdesc="OpenStack Orchestration API Client Library"
arch=('any')
url="https://docs.openstack.org/python-heatclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cliff' 'python-iso8601' 'python-osc-lib' 'python-prettytable'
         'python-oslo-i18n' 'python-oslo-serialization' 'python-oslo-utils' 'python-keystoneauth1'
         'python-swiftclient' 'python-yaml' 'python-requests' 'python-six')
makedepends=('git' 'python-build' 'python-installer')
checkdepends=('python-stestr' 'python-testscenarios' 'python-requests-mock')
source=("git+https://github.com/openstack/python-heatclient.git#tag=$pkgver")
sha512sums=('6134f9be995783a6b676ead8842da6cfec59df09cabb5308af8e86f124a08e49c7c8e219db90392f15886a3d30d5aaa84e07c7862a558f905493fcafd5f72355')

build() {
  cd python-heatclient
  python -m build -nw
}

check() {
  cd python-heatclient
  stestr run
}

package() {
  cd python-heatclient
  python -m installer --destdir="$pkgdir" dist/*.whl
}
