# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-heatclient
pkgver=5.0.0
pkgrel=1
pkgdesc="OpenStack Orchestration API Client Library"
arch=('any')
url="https://docs.openstack.org/python-heatclient/latest/"
license=('Apache-2.0')
depends=('python-pbr' 'python-cliff' 'python-iso8601' 'python-osc-lib' 'python-prettytable'
         'python-oslo-i18n' 'python-oslo-serialization' 'python-oslo-utils' 'python-keystoneauth1'
         'python-swiftclient' 'python-yaml' 'python-requests' 'python-openstackclient')
makedepends=('git' 'python-build' 'python-installer')
checkdepends=('python-stestr' 'python-testscenarios' 'python-requests-mock')
source=(
  "git+https://github.com/openstack/python-heatclient.git#tag=$pkgver"
  "$pkgname-python-3.14.patch"
)
sha512sums=('a0b1b2f2668fdf3b322bde55697bf00f8c13f9019e36ad33ba003315339cdf8ff90e3bb785615a5fa6363353842b1c72e2bb44143881d8ae1e1c7de2e8f11380'
            'a87ed82b0708a35c348729553d4a5ee43f5b88f35a55932152f5059c61bc8bd0be0384259474f05cd58248c03102df885f21ebc8acf532d59d35d6fa80adc7c3')

prepare() {
  cd python-heatclient
  patch -Np1 < ../$pkgname-python-3.14.patch
}

build() {
  cd python-heatclient
  python -m build -nw
}

check() {
  cd python-heatclient
  stestr run
}

package() {
  cd python-heatclient
  python -m installer --destdir="$pkgdir" dist/*.whl
}
