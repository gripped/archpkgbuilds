# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=semialign
pkgname=haskell-semialign
pkgver=1.3.1
pkgrel=39
pkgdesc="Align and Zip type-classes from the common Semialign ancestor."
url="https://github.com/haskellari/these"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-indexed-traversable'
         'haskell-indexed-traversable-instances' 'haskell-semigroupoids' 'haskell-tagged'
         'haskell-these' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('049100871cf65d36f9b0f3746cf7375ad7e0a32070df50a977eae3243795b5641b40c0e48d33d531104e8c6647fca83172725d3562de23f2e33f8a21ed9f66c8')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
