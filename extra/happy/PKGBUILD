# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=happy
pkgver=2.1.6
pkgrel=2
pkgdesc="The Parser Generator for Haskell"
url="https://hackage.haskell.org/package/happy"
arch=('x86_64')
license=("BSD-2-Clause")
depends=('ghc-libs' 'haskell-happy-lib')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a00dd06645dd7658f8675d68365e96460479e50760d395707f688d8d4a157d114c6a0228ff16068fb584ab9cb15149def111140539387795c6385e72362ac30a')

build() {
  cd $pkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'
  runhaskell Setup build $MAKEFLAGS
}

check() {
  cd $pkgname-$pkgver
  PATH="$PWD/dist/build/happy:$PATH" HC="ghc -dynamic" runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  rm -r "$pkgdir/usr/share/doc"
}
