# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thibaut PÃ©rami <thibaut.perami@cl.cam.ac.uk>

pkgname=zydis
pkgver=4.1.1
pkgrel=1
pkgdesc='Fast and lightweight x86/x86-64 disassembler and code generation library'
arch=('x86_64')
url='https://zydis.re'
license=('MIT')
depends=('gcc-libs' 'zycore-c')
makedepends=('git' 'cmake' 'ruby-ronn-ng' 'doxygen')
checkdepends=('python')
provides=('libZydis.so')
source=("git+https://github.com/zyantific/zydis.git#tag=v$pkgver")
sha512sums=('7ca4fc08ad5074f7ae7115f8eb511ad53b84edcf6770395d43512bee73e38ffad0e1b59850e7732454abdf880266307cf5fd396ac516ef1f9489d4fb592a455b')
b2sums=('a596fe2649f4f9d5644b3abe7df3fa1d921ec378324691f04e17df60e3ebec39f3877ecb68519b71fde1a0a8144e48b35556febb241fa3cc631794c7e866f8d5')

build() {
  # https://archlinux.org/todo/lto-fat-objects/
  export CFLAGS+=" -ffat-lto-objects"

  # https://github.com/zyantific/zydis#-fpic-for-shared-library-builds
  cmake \
    -S "$pkgname" \
    -B build \
    -W no-dev \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_POSITION_INDEPENDENT_CODE=ON \
    -D ZYDIS_BUILD_SHARED_LIB=ON \
    -D ZYDIS_BUILD_TESTS=ON \
    -D ZYAN_SYSTEM_ZYCORE=ON \
    -D ZYDIS_BUILD_MAN=ON

  cmake --build build
}

check() {
  # test suite extracted from .github/workflows/main.yml
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/Zydis" README.md
  cp -vr examples "$pkgdir/usr/share/doc/Zydis"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
