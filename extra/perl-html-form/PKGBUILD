# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Justin "juster" Davis <jrcd83@gmail.com>

pkgname=perl-html-form
pkgver=6.13
pkgrel=2
pkgdesc="Class that represents an HTML form element"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.8' 'perl-html-parser' 'perl-http-message' 'perl-uri>=1.10')
makedepends=('perl-test-warnings')
conflicts=('perl-libwww<6')
url=https://search.cpan.org/dist/HTML-Form
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/HTML-Form-$pkgver.tar.gz")
sha256sums=('ae5ad0f6fe70b1a382789d5e83a9b669cc541ee9d459e1bfa89b43ae0c014cdd')

build() {
  cd "${srcdir}/HTML-Form-${pkgver}"
  PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
    PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'" \
    PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
    MODULEBUILDRC=/dev/null perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/HTML-Form-${pkgver}"
  PERL_MM_USE_DEFAULT=1 PERL5LIB="" make test
}

package() {
  cd "${srcdir}/HTML-Form-${pkgver}"
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
