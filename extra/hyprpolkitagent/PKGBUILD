# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Adam Perkowski <adas1per@protonmail.com>

pkgname=hyprpolkitagent
pkgver=0.1.2
pkgrel=2
pkgdesc='Simple polkit authentication agent for Hyprland, written in QT/QML'
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD-3-Clause)
depends=(gcc-libs # libgcc_s.so libstdc++.so
         glibc # libc.so libm.so
         hyprutils libhyprutils.so
         polkit-qt6 # libpolkit-qt6-core-1.so libpolkit-qt6-agent-1.so
         qt6-base # libQt6Widgets.so libQt6Gui.so libQt6Qml.so libQt6Core.so
         qt6-declarative) # libQt6QuickControls2.so
makedepends=(cmake
             hyprland-qt-support)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('2aa642a55aab000ac340c9209063a3068fda5b419ad83116f3c87532f06b0a79')

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=Release
	cmake --build build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}


