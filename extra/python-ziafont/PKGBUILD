# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziafont
pkgver=0.10
pkgrel=1
pkgdesc="Convert TTF/OTF font glyphs to SVG paths"
url="https://ziafont.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-nbval' 'python-pytest')
source=("git+https://github.com/cdelker/ziafont.git#tag=$pkgver")
sha512sums=('857e19b12bf5ffd6b8a28297c58b940f39dc89c7989b2905094fd44492c08d7bfdb1f1cabb8c3f3f7d3d9586a06fcccf78bac09a27b4dc8704cc44c3d7e585f8')

build() {
  cd ziafont
  python -m build -nw
}

check() {
  cd ziafont
  PYTHONPATH="$PWD" pytest --nbval || echo "Tests failed"
}

package() {
  cd ziafont
  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
