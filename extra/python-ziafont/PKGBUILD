# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziafont
pkgver=0.9
pkgrel=1
pkgdesc="Convert TTF/OTF font glyphs to SVG paths"
url="https://ziafont.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-nbval' 'python-pytest')
source=("git+https://github.com/cdelker/ziafont.git#tag=$pkgver")
sha512sums=('259bddacea5ab259f978df52611467ef3e414b2e347cc5bbac0f26e7fda840e0ce114b548a667729117ede0275bce2856b043543a9d89f5d8a8b2c47aa220d63')

build() {
  cd ziafont
  python -m build -nw
}

check() {
  cd ziafont
  PYTHONPATH="$PWD" pytest --nbval || echo "Tests failed"
}

package() {
  cd ziafont
  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
