# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr

pkgname=vaultwarden
pkgver=1.34.1
pkgrel=2
pkgdesc='Unofficial Bitwarden compatible server written in Rust'
arch=(x86_64)
url=https://github.com/dani-garcia/vaultwarden
license=(AGPL-3.0-only)
depends=(
  gcc-libs
  glibc
  openssl
  sqlite
)
depends_aarch64=(
  mariadb-libs
  postgresql-libs
)
depends_x86_64=(
  mariadb-libs
  postgresql-libs
)
makedepends=(
  git
  rust
)
optdepends=('vaultwarden-web: for the web app')
backup=(etc/vaultwarden.env)
options=(!lto)
install=$pkgname.install
source=(
  "git+$url.git#tag=$pkgver"
  systemd.service
  sysusers.conf
  tmpfiles.conf
  env-overrides.patch
)
sha512sums=('6d9895f401913ca00161eec2c88566fe931dc4e60e5512d397e273a9e80bcecda9d1a899c55963db1eaa4fa129a6058d7732914a8c2a6a99db173a5ce385257c'
            'a12e993bbb568370b9a19f1634085eceda33c1f6bc98ea2c4fa2686a91676f3e9aafc681ab83450bf90b52e553770aabec871c311746faf2098e589cb67c7436'
            'd133aec7f75cc5653e847e81bd11c36374eb20da62b2e197bbdcd306ef9c5d4d1de9fc41b7afe88645bebecc1397f64089bfd57c22eb2c9fb83b1da7b44c23aa'
            'efbe316f8d22f14c88b5020ea163b58a42e9a0de1028d04af6e439276a55eefc7f7631dd9c4aee5cf5ca2fb642e4fa5b31eedef4370d6d785e106474c0940afe'
            'bcd7fcd50f8939601f142e18a0f0e548183fce732f1c9648c9ec43f3a9a4a06731e82340498f45f676642aee8ee5df889bc2a13d3dddf8de594198c2b8e37d07')
b2sums=('5e6a6f7c1b736fd0c22557aa22edff39143079605ae27b7c81ef003467d3ef350b71f49d7181939e8cc3efb799db6a90a71371ff079e3f1d4108d39f96a0d04c'
        'ff8e896f820b88f079d3d4f4c2eb394362569c921f53693e25d3badaf9fdca841e4d7665862e09d22421f544802f3a64014696fd92d7f0b923faf41f8d99e472'
        'bfda155679ece4df7fa364eaa0d8dbe2f08701a83f5e438282ec7e6ab56afe741c9820ae6fe2da5c9d7691c647fb4c862c4bed588eee248f0de943cec155f392'
        '9cdcd6c997c884eb3c1ad0c9b8cd91e68ec6762a27f9b80a7eb6c0cb1ffa4e500a654da6f17e9f985cc67add154efe737761f74e4e5f72892fc0e9d457314973'
        '464b78a7cf336b745b64e351bdebbc7ba0b329b3be5890ce8a2f8c7811082856f8fc327583f551e7bee8dec35c2604c082abb051885c02c2a76afd1cb7833474')

prepare() {
  cd $pkgname
  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  patch --forward --strip=1 --input=../env-overrides.patch
}

build() {
  cd $pkgname

  export VW_VERSION=$pkgver
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    # workaround for 32-bit systems
    # https://github.com/dani-garcia/vaultwarden/issues/4320
    cargo build --profile release-low --frozen --features sqlite
  else
    cargo build --release --frozen --features sqlite,mysql,postgresql
  fi
}

check() {
  cd $pkgname

  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    cargo test --frozen --features sqlite,postgresql
  else
    cargo test --frozen --features sqlite,mysql,postgresql
  fi
}

package() {
  # systemd integration
  install -vDm644 systemd.service \
    "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -vDm644 sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -vDm644 tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  cd $pkgname

  # configuration
  install -Dm640 .env.template "$pkgdir"/etc/$pkgname.env

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release*/$pkgname
}
