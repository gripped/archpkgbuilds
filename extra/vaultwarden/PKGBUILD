# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr

pkgname=vaultwarden
pkgver=1.35.3
pkgrel=1
pkgdesc='Unofficial Bitwarden compatible server written in Rust'
arch=(x86_64)
url=https://github.com/dani-garcia/vaultwarden
license=(AGPL-3.0-only)
depends=(
  gcc-libs
  glibc
  openssl
  sqlite
)
depends_aarch64=(
  mariadb-libs
  postgresql-libs
)
depends_x86_64=(
  mariadb-libs
  postgresql-libs
)
makedepends=(
  git
  rust
)
optdepends=('vaultwarden-web: for the web app')
backup=(etc/vaultwarden.env)
options=(!lto)
install=$pkgname.install
source=(
  "git+$url.git#tag=$pkgver"
  systemd.service
  sysusers.conf
  tmpfiles.conf
  env-overrides.patch
)
sha512sums=('a4214a00f3dfa1dad401d5959d403333aaf8c7159a0c620f26ffa9929a0e77d170fc26aafddab269155e4cb5407379245f2a20f8df7a2268cc9d5163289f0592'
            'a12e993bbb568370b9a19f1634085eceda33c1f6bc98ea2c4fa2686a91676f3e9aafc681ab83450bf90b52e553770aabec871c311746faf2098e589cb67c7436'
            'd133aec7f75cc5653e847e81bd11c36374eb20da62b2e197bbdcd306ef9c5d4d1de9fc41b7afe88645bebecc1397f64089bfd57c22eb2c9fb83b1da7b44c23aa'
            'efbe316f8d22f14c88b5020ea163b58a42e9a0de1028d04af6e439276a55eefc7f7631dd9c4aee5cf5ca2fb642e4fa5b31eedef4370d6d785e106474c0940afe'
            '0094aeea62c204080b6ba6f476840ef8b40a8d799d4dce1cfe025cce03a145d91468aade7140af7764c00cde5d5bc418aef3a79ef507531e5a0b8695dcc8c639')
b2sums=('a9080884ca0762aba64768d72ee0cdf0f33fe1d1092ce67f1397da82ea0de2621d3d8571dd2a681a331deec26c42eaa4effae2bfc2d505b2819eb27606d9a729'
        'ff8e896f820b88f079d3d4f4c2eb394362569c921f53693e25d3badaf9fdca841e4d7665862e09d22421f544802f3a64014696fd92d7f0b923faf41f8d99e472'
        'bfda155679ece4df7fa364eaa0d8dbe2f08701a83f5e438282ec7e6ab56afe741c9820ae6fe2da5c9d7691c647fb4c862c4bed588eee248f0de943cec155f392'
        '9cdcd6c997c884eb3c1ad0c9b8cd91e68ec6762a27f9b80a7eb6c0cb1ffa4e500a654da6f17e9f985cc67add154efe737761f74e4e5f72892fc0e9d457314973'
        'dff624472c0a64485bdece7cc2997919b145f556cbb40766df001495bafe64ae0a23b541c563ff6f395ae777ab934341a86e2c77cd7953793cc15d065df2794a')

prepare() {
  cd $pkgname
  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"

  patch --forward --strip=1 --input=../env-overrides.patch
}

build() {
  cd $pkgname

  export VW_VERSION=$pkgver
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    # workaround for 32-bit systems
    # https://github.com/dani-garcia/vaultwarden/issues/4320
    cargo build --profile release-low --frozen --features sqlite
  else
    cargo build --release --frozen --features mysql,postgresql,sqlite,s3
  fi
}

check() {
  cd $pkgname

  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    cargo test --frozen --features sqlite
  else
    cargo test --frozen --features mysql,postgresql,sqlite,s3
  fi
}

package() {
  # systemd integration
  install -vDm644 systemd.service \
    "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -vDm644 sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -vDm644 tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  cd $pkgname

  # configuration
  install -Dm640 .env.template "$pkgdir"/etc/$pkgname.env

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release*/$pkgname
}
