# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr

pkgname=vaultwarden
pkgver=1.32.7
pkgrel=1
pkgdesc='Unofficial Bitwarden compatible server written in Rust'
arch=(x86_64)
url=https://github.com/dani-garcia/vaultwarden
license=(AGPL-3.0-only)
depends=(
  gcc-libs
  glibc
  openssl
  postgresql-libs
  sqlite
)
depends_x86_64=(mariadb-libs)
makedepends=(
  git
  rust
)
optdepends=('vaultwarden-web: for the web app')
backup=(etc/vaultwarden.env)
options=(!lto)
install=$pkgname.install
source=(
  "git+$url.git#tag=$pkgver"
  systemd.service
  sysusers.conf
  tmpfiles.conf
)
sha512sums=('e1887090f75541418233e807a1072e9164f8103922f9f1bbaa663b50694b4a6a88e5bbdfae21da88b79ba31e704860c7e20c80afafedf8ce132dea2d31cac065'
            'a12e993bbb568370b9a19f1634085eceda33c1f6bc98ea2c4fa2686a91676f3e9aafc681ab83450bf90b52e553770aabec871c311746faf2098e589cb67c7436'
            '497d82d6aaab94a1f45fb9f3e5f8c624fdf44810e758b2def23ede03c8174740dc0b5d6859f7e1944af55e3b93bd97e833b256f783ed2586d18adf54899a11ef'
            'efbe316f8d22f14c88b5020ea163b58a42e9a0de1028d04af6e439276a55eefc7f7631dd9c4aee5cf5ca2fb642e4fa5b31eedef4370d6d785e106474c0940afe')
b2sums=('cf9249e0c6dfde0a90f6e2e7a43f50050284142de43d5126cc983b754b19c2ae25b0d026b930d05d9193c7f79244a8381bcd7c5b35ac972ede303147bd678019'
        'ff8e896f820b88f079d3d4f4c2eb394362569c921f53693e25d3badaf9fdca841e4d7665862e09d22421f544802f3a64014696fd92d7f0b923faf41f8d99e472'
        '6580cf4031f262638abf11e529e8dfc489197afd6cd616c6a7810741e03c004706a5f9358f825aac6644c0ab6b251627ff988ed68f8264120b3575a655972390'
        '9cdcd6c997c884eb3c1ad0c9b8cd91e68ec6762a27f9b80a7eb6c0cb1ffa4e500a654da6f17e9f985cc67add154efe737761f74e4e5f72892fc0e9d457314973')

prepare() {
  cd $pkgname

  # change various defaults
  sed -i "s,# DATA_FOLDER=data,DATA_FOLDER=/var/lib/$pkgname,
  s,web-vault/,/usr/share/webapps/$pkgname-web,
  s,# WEB_VAULT_ENABLED=true,WEB_VAULT_ENABLED=false,
  s,/path/to/log,/var/log/$pkgname.log,
  /^# ROCKET_TLS/a ROCKET_LIMITS={json=10485760}" .env.template

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname

  export VW_VERSION=$pkgver
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    # workaround for 32-bit systems
    # https://github.com/dani-garcia/vaultwarden/issues/4320
    cargo build --profile release-low --frozen --features sqlite,postgresql
  else
    cargo build --release --frozen --features sqlite,mysql,postgresql
  fi
}

check() {
  cd $pkgname

  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    cargo test --frozen --features sqlite,postgresql
  else
    cargo test --frozen --features sqlite,mysql,postgresql
  fi
}

package() {
  # systemd integration
  install -vDm644 systemd.service \
    "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -vDm644 sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -vDm644 tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  cd $pkgname

  # configuration
  install -Dm640 .env.template "$pkgdir"/etc/$pkgname.env

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release*/$pkgname
}
