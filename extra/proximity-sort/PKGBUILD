# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Alec Mev <alec@mev.earth>

pkgname=proximity-sort
pkgver=1.3.0
pkgrel=1
pkgdesc='Simple command-line utility for sorting inputs by proximity to a path argument'
arch=('x86_64')
url=https://github.com/jonhoo/proximity-sort
license=('MIT' 'APACHE')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jonhoo/proximity-sort/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('2642b896af81fbb62902d45f12bca19f8c1827d5f6b152d4eaaf9bc1e3e0166d')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" target/release/proximity-sort
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.md
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-APACHE
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-MIT
}
