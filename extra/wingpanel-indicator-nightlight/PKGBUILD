# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-nightlight
pkgver=2.1.3
pkgrel=2
pkgdesc='Wingpanel Nightlight Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-nightlight
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk3
  libwingpanel.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=70922bc5986efde49b97a8ba8e7301c7bb4c1a67
source=(git+https://github.com/elementary/wingpanel-indicator-nightlight.git#tag=${_tag})
b2sums=('69f311292e547d816db3ed5ecfbe92c9a0c46f1b93fc0a77ad07646df80d722719b829563da988dc62a4349d6bce120c6b5429f592713784e4a8d22f13e06689')

pkgver() {
  cd wingpanel-indicator-nightlight
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-nightlight build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
