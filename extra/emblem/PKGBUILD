# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=emblem
pkgver=1.6.0
pkgrel=1
pkgdesc='Generate projects avatars for your Matrix rooms and git forges from a symbolic icon'
arch=(x86_64)
url='https://apps.gnome.org/Emblem/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/design/emblem.git#tag=$pkgver")
b2sums=(73860cc1fbe8195f655bdb29af1b09f50ca5ad9da65282f097219c2fe9ecd34b446e13287da5bbe8561d0682265ca7d49b9b273a2e72359247fedd2f1ea332a9)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
