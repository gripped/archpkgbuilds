# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.10.0
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=(x86_64)
url="https://drumstick.sourceforge.io/"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-5compat
)
makedepends=(
  alsa-lib
  cmake
  doxygen
  docbook-xsl
  fluidsynth
  graphviz
  libpulse
  libpipewire
  qt6-tools
  sonivox
)
provides=(
  libdrumstick-alsa.so
  libdrumstick-file.so
  libdrumstick-rt.so
  libdrumstick-widgets.so
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('db5f9f523716518e7dffa869799dee87533dfb4a387c1f6bc2196639e3676e0ad763432fd7735116b31ef05b0bcc79b017f692e5ba1c152092b5825b2f1d1990')
b2sums=('991274f1b12e6f5c54c7eda933b200529cde24b10414710454ec7f7af49c644b4ed0fbf5ad65d4853ad0dc1bbb17f1fb9336d2874955bfa5405ce8a584be36f5')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  # some tests fail due to insufficient system privileges, hence only run those
  # that make sense in the build environment
  # https://sourceforge.net/p/drumstick/bugs/24/
  make test -C build/tests/fileTest1
  make test -C build/tests/fileTest2
}

package() {
  depends+=(
    alsa-lib libasound.so
    fluidsynth libfluidsynth.so
    libpulse libpulse.so libpulse-simple.so
    libpipewire libpipewire-0.3.so
    sonivox libsonivox.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,readme.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
