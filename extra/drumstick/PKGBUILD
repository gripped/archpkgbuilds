# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.11.0
pkgrel=1
pkgdesc="MIDI libraries for Qt6/C++"
arch=(x86_64)
url="https://drumstick.sourceforge.io/"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-5compat
)
makedepends=(
  alsa-lib
  cmake
  doxygen
  docbook-xsl
  fluidsynth
  graphviz
  libpulse
  libpipewire
  qt6-tools
  sonivox
)
provides=(
  libdrumstick-alsa.so
  libdrumstick-file.so
  libdrumstick-rt.so
  libdrumstick-widgets.so
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('1a49c31fb98093097b18beeb0f506a859247c9facfd6bb822e151dc6cc5467d74c3fa15d8c3860e90f289fd8b15f2489e8eb55561d3d79a8c9deeb9c0c5b69f4')
b2sums=('98561b4dfa3076693928b8004b7c3033e8c43f534833e6aece9e4712f849fef6bd1194d5f4c795fd168bbc14cd8e7c2b64d80b3f2c10974b9a0c24201e3c93d6')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  # some tests fail due to insufficient system privileges, hence only run those
  # that make sense in the build environment
  # https://sourceforge.net/p/drumstick/bugs/24/
  make test -C build/tests/fileTest1
  make test -C build/tests/fileTest2
}

package() {
  depends+=(
    alsa-lib libasound.so
    fluidsynth libfluidsynth.so
    libpulse libpulse.so libpulse-simple.so
    libpipewire libpipewire-0.3.so
    sonivox libsonivox.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,readme.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
