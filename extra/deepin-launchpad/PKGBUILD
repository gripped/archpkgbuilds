# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=0.4.1
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL3')
# dtkdeclarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-quickcontrols2' 'qt6-svg' 'dtk6core' 'dtk6gui'
         'glib2' 'appstream-qt' 'dtk6declarative' 'glibc' 'gcc-libs')
makedepends=('cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("https://github.com/linuxdeepin/dde-launchpad/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('47a890e53cd09de3800b090acedfe44a40cbf1f4b15453d5013054d8f7aacd5b63ee5a3a228c2ca5c291103925e34a7d1b5ef472616a3d00c1fe9827df77f58c')

build() {
  cd dde-launchpad-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DPREFER_QT_5=OFF
  ninja
}

package() {
  cd dde-launchpad-$pkgver
  DESTDIR="$pkgdir" ninja install
}
