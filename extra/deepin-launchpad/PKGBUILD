# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=1.0.8
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL-3.0-or-later')
# dtk6declarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'qt6-wayland' 'dtk6core' 'dtk6gui' 'dtk6declarative'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver")
sha512sums=('102c5eaa8cd508a5fbad1c3195bb1ad66d035b056f55b03d546f53ac3f2922bbe0e168f33874b44e260d4b321c4e5c4cef1e69d709d48361c41a1c4cffde464c')

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
