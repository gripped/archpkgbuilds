# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=0.6.11
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL3')
# dtkdeclarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'dtk6core' 'dtk6gui'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'dtk6declarative' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver")
sha512sums=('5910b067c69bd876d19f0d8b64b72b2004fcb00cdb1b293b62ceff641f611dd46f4c098297358745aab23819a6a80f5693e8ea16795b4a088c6eb74c655f1dfd')

prepare() {
  cd dde-launchpad

  # https://github.com/linuxdeepin/dde-launchpad/pull/193
  git cherry-pick -n b7e0dd5c441d31927fe8fc9f26517703464103b6
}

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
