# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=0.6.6
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL3')
# dtkdeclarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'dtk6core' 'dtk6gui'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'dtk6declarative' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver")
sha512sums=('3191562c53f5c0014b599e0c405258d737077f07496b99b48ac4eb570d03bd307fba8e9a19e6decea83f15dff23420bf50f1452aad240478086a53562d31b6ca')

prepare() {
  cd dde-launchpad

  # https://github.com/linuxdeepin/dde-launchpad/pull/193
  git cherry-pick -n b7e0dd5c441d31927fe8fc9f26517703464103b6
}

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
