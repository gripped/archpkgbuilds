# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=1.0.0
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL-3.0-or-later')
# dtk6declarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'qt6-wayland' 'dtk6core' 'dtk6gui' 'dtk6declarative'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver")
sha512sums=('130af1725083c01837d470b3198ab4f6f7f037a61765cedc4f0a3bf9927f24a73137c9ebb07a0237e5516ee0b1b332acc8aa1ee858d0fe240b69b243462e7312')

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
