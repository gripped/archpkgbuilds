# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=0.7.4
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL3')
# dtkdeclarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'dtk6core' 'dtk6gui'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'dtk6declarative' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver")
sha512sums=('b3a910f4ebe048ef847e4ea5d0dc02461e59ad04bffdea360005d079622831323f37836db067019ad27ee63ae8a38f00621d9c7bbd0f414ab2f802c5519a7e00')

prepare() {
  cd dde-launchpad

  # https://github.com/linuxdeepin/dde-launchpad/pull/193
  git cherry-pick -n b7e0dd5c441d31927fe8fc9f26517703464103b6
}

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
