# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=2.0.2
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL-3.0-or-later')
# dtk6declarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'qt6-wayland' 'dtk6core' 'dtk6gui' 'dtk6declarative'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver")
sha512sums=('c66dd69868c88167fc745f795a3a5e3ef3ed4bf122a2c4418b7acb29498e6a227c8f10cb83fa9c3abddad07a72d963b2b4e58b911f5ae86c5aa61500459b5e6f')

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
