# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=2.0.9
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL-3.0-or-later')
# dtk6declarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'qt6-wayland' 'dtk6core' 'dtk6gui' 'dtk6declarative'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('e963f28a4f04c53f910dbcc71a267ec3c648f563a0beb02853684b566b6cef373138b3baa3990c97fc34d25eb9581ceba6c673a1c4caa0500661e69a90292481'
            'faee74fc12718a37f443eb8ea7aa161b49780326e62fa000aba6c94270f6ef474b2fd9164fa002437fc1101389c1a19236feda72f1c481435986386d1fb1c19a')

prepare() {
  cd dde-launchpad
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
