# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=0.8.2
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL3')
# dtkdeclarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'dtk6core' 'dtk6gui'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'dtk6declarative' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver")
sha512sums=('78ab49eafd8bbfd30a31cfc5eddafc9a7fc8a3076e80df6f54f4d84bcd63b73b6dc8e73f68dd125b7ba11661f46316f805bda5120355a8ecdfa8f1fa371a0285')

prepare() {
  cd dde-launchpad

  # https://github.com/linuxdeepin/dde-launchpad/pull/193
  git cherry-pick -n b7e0dd5c441d31927fe8fc9f26517703464103b6
}

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
