# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=2.0.10
pkgrel=2
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL-3.0-or-later')
# dtk6declarative: qml: org.deepin.dtk
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'dtk6core' 'dtk6gui' 'dtk6declarative'
         'glib2' 'appstream-qt' 'deepin-application-manager' 'deepin-shell' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'gtest' 'qt6-tools' 'clang')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("git+https://github.com/linuxdeepin/dde-launchpad.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('3145fbf0c8bf14244b9b9d7cadb24c57c3a2b2cf08caeb6803d2dea2259fc523020fcc7b06b2e6ebd1f805992e0d3a6da2fdc74057410ae8d2ec4ec4a7881499'
            'faee74fc12718a37f443eb8ea7aa161b49780326e62fa000aba6c94270f6ef474b2fd9164fa002437fc1101389c1a19236feda72f1c481435986386d1fb1c19a')

prepare() {
  cd dde-launchpad
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd dde-launchpad
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad
  DESTDIR="$pkgdir" ninja install
}
