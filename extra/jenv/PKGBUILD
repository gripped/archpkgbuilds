# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gerard Ribugent <ribugent@gmail.com>

pkgname=jenv
pkgver=0.6.0
pkgrel=1
pkgdesc='Manage your Java environment'
arch=(any)
url=https://github.com/jenv/jenv
license=(MIT)
depends=(bash)
makedepends=(git)
optdepends=(
  'bash-completion: Bash completion support'
  'fish: Fish support'
  'zsh: Zsh support'
)
_tag=a3db63d1e2f138acd3882858cd34fa9b0b88cd2c
source=(git+https://github.com/jenv/jenv.git#tag=${_tag})
b2sums=('117f63d980afa0233023b93942516ec3267af4d7c60c021a205ea3c1b449ee5f8459cc90a52dcce4572fafebddfaeabdac08bd067ea26e9df13134bca8b9eb1d')

pkgver() {
  cd jenv
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

package() {
  install -dm 755 "${pkgdir}"/usr/{bin,share/{fish,jenv}}
  cp -dr --no-preserve=ownership jenv/{available-plugins,libexec} "$pkgdir"/usr/share/jenv/
  cp -dr --no-preserve=ownership jenv/fish "${pkgdir}"/usr/share/fish/vendor_functions.d
  ln -s /usr/share/jenv/libexec/jenv "${pkgdir}"/usr/bin/
  install -Dm 644 jenv/completions/jenv.bash "${pkgdir}"/usr/share/bash-completion/completions/jenv
  install -Dm 644 jenv/completions/jenv.fish -t "${pkgdir}"/usr/share/fish/vendor_completions.d/
  install -Dm 644 jenv/completions/jenv.zsh "${pkgdir}"/usr/share/zsh/site-functions/_jenv
  install -Dm 644 jenv/LICENSE -t "${pkgdir}"/usr/share/licenses/jenv/
}

# vim: ts=2 sw=2 et:
