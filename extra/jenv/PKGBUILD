# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gerard Ribugent <ribugent@gmail.com>

pkgname=jenv
pkgver=0.5.9
pkgrel=1
pkgdesc='Manage your Java environment'
arch=(any)
url=https://github.com/jenv/jenv
license=(MIT)
depends=(bash)
makedepends=(git)
optdepends=(
  'bash-completion: Bash completion support'
  'fish: Fish support'
  'zsh: Zsh support'
)
_tag=fdc4b046640140a77cf9ea3ec340a5406e32b84a
source=(git+https://github.com/jenv/jenv.git#tag=${_tag})
b2sums=('2da295c87c5b5d1724e048481df3d1bf38d9e99f6354fb9fce4687bf883b131a51ede28da101fe428c77903e4466d0f71f9941b4c2c71b089ae4be996a0f6f6f')

pkgver() {
  cd jenv
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

package() {
  install -dm 755 "${pkgdir}"/usr/{bin,share/{fish,jenv}}
  cp -dr --no-preserve=ownership jenv/{available-plugins,libexec} "$pkgdir"/usr/share/jenv/
  cp -dr --no-preserve=ownership jenv/fish "${pkgdir}"/usr/share/fish/vendor_functions.d
  ln -s /usr/share/jenv/libexec/jenv "${pkgdir}"/usr/bin/
  install -Dm 644 jenv/completions/jenv.bash "${pkgdir}"/usr/share/bash-completion/completions/jenv
  install -Dm 644 jenv/completions/jenv.fish -t "${pkgdir}"/usr/share/fish/vendor_completions.d/
  install -Dm 644 jenv/completions/jenv.zsh "${pkgdir}"/usr/share/zsh/site-functions/_jenv
  install -Dm 644 jenv/LICENSE -t "${pkgdir}"/usr/share/licenses/jenv/
}

# vim: ts=2 sw=2 et:
