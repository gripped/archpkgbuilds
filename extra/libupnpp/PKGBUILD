# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Tristelune <tristelune@archlinux.info>

pkgname=libupnpp
pkgver=1.0.2
pkgrel=1
pkgdesc="A C++ wrapper for libupnp - used with upmpdcli or upplay"
url="https://www.lesbonscomptes.com//upmpdcli/libupnpp-refdoc/libupnpp-ctl.html"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(glibc gcc-libs curl expat libnpupnp)
makedepends=('meson')
provides=(libupnpp.so)
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/${pkgname}-${pkgver}.tar.gz"
        "0.26.4-include-fix.patch")
sha256sums=('f4b1cb69645de1868a9c794063b0a01bd114e9852d7ef6852e843e2870fd1fb2'
            '31ade4ca2151267b226a368125f55ec599c780e40c3d8cff39456610279e83b6')

prepare(){
  cd $pkgname-$pkgver
  patch -p1 -i ../0.26.4-include-fix.patch
}

build(){
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package(){
  depends+=(libnpupnp.so)
  meson install -C build --destdir "$pkgdir"
  install -Dm644 "$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
