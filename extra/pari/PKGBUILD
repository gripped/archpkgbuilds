# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari
pkgver=2.17.1
pkgrel=1
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='https://pari.math.u-bordeaux.fr/'
license=(GPL-2.0-or-later)
arch=(x86_64)
depends=(gcc-libs
         glibc
         gmp
         libx11
         readline
         sh)
makedepends=(chrpath
             perl
             texlive-latex)
optdepends=('perl: gphelp, tex2mail'
            'pari-elldata: J. Cremona elliptic curve database'
            'pari-galdata: to compute Galois groups in degrees 8 through 11'
            'pari-seadata: needed by ellap for large primes'
            'pari-galpol: GALPOL database of polynomials defining Galois extensions of the rationals')
checkdepends=(pari-elldata pari-galdata pari-seadata pari-galpol)
source=(https://pari.math.u-bordeaux.fr/pub/pari/unix/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('67ba6f3071233725258541e4f174b5efbc64c65ae5115bade9edfc45f1fde5dc'
            'SKIP')
validpgpkeys=('42028EA404A2E9D80AC453148F0E7C2B4522E387')

prepare() {  
  cd $pkgname-$pkgver
  sed -e 's|DLLDFLAGS  = \$DLLDFLAGS|DLLDFLAGS  = $DLLDFLAGS $LDFLAGS|' -i config/Makefile.SH # Honor system LDFLAGS
}

build() {
  cd $pkgname-$pkgver

  ./Configure \
    --prefix=/usr \
    --with-readline \
    --mt=pthread \
    --with-gmp
  make all
  make -C Olinux-x86_64 gp-sta
}

check() {
  cd $pkgname-$pkgver
  make test-all
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C Olinux-x86_64 install-bin-sta
  ln -sf gp.1.gz "$pkgdir"/usr/share/man/man1/pari.1
  chrpath -d "$pkgdir"/usr/bin/gp-*
}
