# Maintainer: Florian Pritz <bluewind@xinu.at>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=grafana-zabbix
pkgver=4.5.3
pkgrel=1
pkgdesc="Zabbix plugin for Grafana dashboard"
arch=('x86_64')
url="https://github.com/grafana/grafana-zabbix"
license=('Apache-2.0')
depends=('grafana')
makedepends=('yarn' 'libfaketime' 'go' 'git' 'nodejs-lts-iron' 'mage')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        fix_panic_with_go_1.23.patch::https://github.com/grafana/grafana-zabbix/commit/f20c89003393eff94780f5f20f6d591521886128.patch)
sha256sums=('8e5e797693297afdba7e344076c6e2db4dcb1e32fd77ec2ac308f0223937fa9f'
            '26bd56abd3b3339ccc85f13c4f2da882afd462dc3cbaee59b4abeb6d41a5a827')

prepare() {
	cd "${pkgname}-${pkgver}"
	# Fix tests not working without git clone
	sed -i 's#jest --watch --onlyChanged#jest#' package.json
	# Remove lint related dependency (we don't care about linting tests at our level)
	sed -i '/GO111MODULE=off go get -u golang.org\/x\/lint\/golint/d' Makefile
	# Temporary patch to fix panic in tests with go 1.23
	# See https://github.com/grafana/grafana-zabbix/pull/1875
	patch -Np1 < "${srcdir}/fix_panic_with_go_1.23.patch"
	go mod tidy
}

build() {
	cd "${pkgname}-${pkgver}"
	make install
	make build
	make dist-frontend
	make dist-backend-linux
}

check() {
	cd "${pkgname}-${pkgver}"
	# Force UTC timezone so that tests pass, even after a DST change
	PATH="${PATH}:/build/go/bin" TZ=UTC make test
}

package() {
	cd "${pkgname}-${pkgver}"
	install -dm 755 "${pkgdir}/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	cp -rv dist/* "${pkgdir}/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	rm -rf "${pkgdir}/var/lib/grafana/plugins/alexanderzobnin-zabbix-app/node_modules"
}
