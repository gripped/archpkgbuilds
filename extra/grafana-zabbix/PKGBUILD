# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=grafana-zabbix
pkgver=6.1.1
pkgrel=1
pkgdesc="Zabbix plugin for Grafana dashboard"
arch=('x86_64')
url="https://github.com/grafana/grafana-zabbix"
license=('Apache-2.0')
depends=('grafana')
makedepends=('yarn' 'npm' 'libfaketime' 'go' 'git' 'nodejs-lts-iron' 'mage')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        'update_flag_for_yarnV2.patch')
sha256sums=('09517a9ff4e47d595fecb1df92b92e275563c0ea48ca600eac391b86ad4ea3d2'
            '42fa6bfeb808ef6bedcb5202cd1c069e6db04c79e6541eb9498e4da4a7ab85d4')

prepare() {
	cd "${pkgname}-${pkgver}"
	# Fix tests not working without git clone
	sed -i 's#jest --watch --onlyChanged#jest#' package.json
	# Remove lint related dependency (we don't care about linting tests at our level)
	sed -i '/GO111MODULE=off go get -u golang.org\/x\/lint\/golint/d' Makefile
	# Replace `--pure-lockfile` with `--immutable` for Yarn V2 compatibility
	patch -Np1 -i "${srcdir}/update_flag_for_yarnV2.patch"
}

build() {
	cd "${pkgname}-${pkgver}"
	make install
	make build
	make dist-frontend
	make dist-backend-linux
}

check() {
	cd "${pkgname}-${pkgver}"
	# Force UTC timezone so that tests pass, even after a DST change
	PATH="${PATH}:/build/go/bin" TZ=UTC make test
}

package() {
	cd "${pkgname}-${pkgver}"
	install -dm 755 "${pkgdir}/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	cp -rv dist/* "${pkgdir}/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	rm -rf "${pkgdir}/var/lib/grafana/plugins/alexanderzobnin-zabbix-app/node_modules"
}
