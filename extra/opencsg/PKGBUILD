# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Xyne <ca dot archlinux at xyne, backwards>
# Contributor: nikor

pkgname=opencsg
pkgver=1.8.2
pkgrel=1
pkgdesc='Library for image-based CSG rendering using OpenGL'
arch=(x86_64)
url='https://www.opencsg.org'
license=(GPL-2.0-or-later)
depends=(glibc gcc-libs)
makedepends=(git cmake ninja glu)
source=("$pkgname::git+https://github.com/floriankirsch/OpenCSG#tag=$pkgname-${pkgver//./-}-release")
sha512sums=('11e80ba45b531121047aa49fd776b261420cf295d856f40c00dca5e62668c0e44c89036dcbff36657c7d16846f6cb5617e7ab56ec6946e3e8be026295349d9b9')
b2sums=('90dc35394b3d076a049db0a6cf43300b287d65cbbe9a5d00372c1f3ea3c24ffc0829aec7b510f729efccdb26bbf8edaec468bdf05064a942705e6eefe6ec1271')

build() {
    local cmake_options=(
    -B build
    -S "$pkgname"
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_EXAMPLE=OFF
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
