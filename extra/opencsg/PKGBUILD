# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Xyne <ca dot archlinux at xyne, backwards>
# Contributor: nikor

pkgname=opencsg
pkgver=1.8.1
pkgrel=1
pkgdesc="Library for image-based CSG rendering using OpenGL"
url="https://www.opencsg.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'glu')
source=("$pkgname::git+https://github.com/floriankirsch/OpenCSG#tag=$pkgname-${pkgver//./-}-release")
sha512sums=('21df4710db2c634cff095038e3902880a7b487540d518002916971a3e6cb41c14ee6fa764bca45231d76d6708183d8ca7a5778a531f3c5f721799e54f9b533aa')
b2sums=('06770b5eeb7706b3ed623441a71992ca67ce1c6d7f5da4fb2468cdc40375061d881680af2488a52b60bce111af055585bf14cef8f86734fc17615239f6dfd723')

build() {
    local cmake_options=(
    -B build
    -S "$pkgname"
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_EXAMPLE=OFF
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
