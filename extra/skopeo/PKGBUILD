# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=skopeo
pkgver=1.21.0
pkgrel=1
pkgdesc='A command line utility for various operations on container images and image repositories.'
arch=(x86_64)
url='https://github.com/containers/skopeo'
license=(Apache-2.0)
depends=(gpgme device-mapper containers-common)
makedepends=(go go-md2man btrfs-progs git systemd)
source=(git+$url#tag=v$pkgver)
sha256sums=('9cb0876964d7b41a61883641e9ac73dc38e4afc70cf7c0be7e905002f653799b')

prepare(){
  cd ${pkgname}
  sed -i 's|.PHONY: bin/skopeo||' Makefile
  sed -i 's|-gcflags "$(GOGCFLAGS)"||' Makefile
}

build() {
  cd ${pkgname}
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  make bin/skopeo GO_DYN_FLAGS="-buildmode=pie -trimpath" BUILDTAGS='containers_image_ostree_stub'
  make docs
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  rm -rf "${pkgdir}/etc/containers/registries.d"
  rm "${pkgdir}/etc/containers/policy.json"
}
