# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Attenuation <ouyangjun1999@gmail.com>

pkgname=hexyl
pkgver=0.15.0
pkgrel=1
pkgdesc='Colored command-line hex viewer'
url='https://github.com/sharkdp/hexyl'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=('gcc-libs')
makedepends=('cargo' 'pandoc')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('db1e0d96c655be1cb0cabb93daef7bd4176517a87a4f7de6c1cd00bcbadf725c297294b41009597eb1cf9891a0f29a3977f5fe1032a58e82d1c8792a3eaadbc4')
b2sums=('55cd03aa51c2dbfacb5c9a262f800a8da77c44a721da8a0fe24ef468488a8608b8eb78fc26cbcaf4aa2b85eb366c3f151dfdf5d2c8d1f3e5c382c8a1e2249bac')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --frozen
  pandoc -s -f markdown -t man -o "doc/${pkgname}.1" "doc/${pkgname}.1.md"
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/${pkgname}"
  install -Dm 644 "doc/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
