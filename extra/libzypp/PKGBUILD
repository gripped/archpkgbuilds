# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=libzypp
pkgver=17.36.3
pkgrel=1
pkgdesc="ZYpp Package Management library"
arch=(x86_64)
url="https://github.com/openSUSE/libzypp"
license=('GPL-2.0-or-later')
depends=(
    boost-libs
    curl
    gcc-libs
    glib2
    glibc
    gpgme
    libsigc++
    libsolv
    libxml2
    openssl
    rpm-tools
    systemd-libs
    yaml-cpp
    zlib
)
makedepends=(
    asciidoctor
    boost
    cmake
    doxygen
    fcgi
    git
    libproxy
    nginx
    vsftpd
)
# just needed for the mediabackend tests which don't seem to work in nspawn
# checkdepends=(
#     nginx
#     vsftpd
# )
source=("git+https://github.com/openSUSE/libzypp.git#tag=${pkgver}")
sha256sums=('9569fbcb886b7619da938cf2bf15c71dfe193c735dd327080465309e71200f82')

prepare() {
    cd $pkgname
    git cherry-pick -n 35d728621dbea40c481cce5a6c82ce05d383ff3f
}

build() {
    local cmake_options=(
        -B build
        -S $pkgname
        -W no-dev
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX=/usr
        -D ENABLE_BUILD_TESTS=ON
        -D ENABLE_BUILD_DOCS=ON
        -D ENABLE_BUILD_TRANS=ON
        -D ENABLE_ZSTD_COMPRESSION=ON
        -D DISABLE_MEDIABACKEND_TESTS=ON
        -D ZYPP_LIBEXEC_INSTALL_DIR=/usr/lib/zypp
    )
    cmake "${cmake_options[@]}"
    cmake --build build
}

check() {
    # https://github.com/openSUSE/libzypp/issues/622
    local excluded_tests=""
    local ctest_flags=(
        --test-dir build
        # show the stdout and stderr when the test fails
        --output-on-failure
        # execute tests in parallel
        --parallel $(nproc)
        # exclude problematic tests
        --exclude-regex "${excluded_tests[@]}"
    )
    ctest "${ctest_flags[@]}"
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    # remove reference to /usr/libexec
    mv -v "$pkgdir/usr/libexec/zypp/" "$pkgdir/usr/lib/"
    rm -vr "$pkgdir/usr/libexec/"
}
