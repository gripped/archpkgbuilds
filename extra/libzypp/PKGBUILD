# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgbase=libzypp
pkgname=(
    libzypp
    libzypp-docs
)
pkgver=17.37.0
pkgrel=1
pkgdesc="ZYpp Package Management library"
arch=(x86_64)
url="https://github.com/openSUSE/libzypp"
license=('GPL-2.0-or-later')
makedepends=(
    asciidoctor
    boost
    boost-libs
    cmake
    curl
    doxygen
    fcgi
    gcc-libs
    git
    glib2
    glibc
    gpgme
    libproxy
    libsigc++
    libsolv
    libxml2
    nginx
    openssl
    rpm-tools
    systemd-libs
    vsftpd
    yaml-cpp
    zlib
)
# just needed for the mediabackend tests which don't seem to work in nspawn
# checkdepends=(
#     nginx
#     vsftpd
# )
source=("git+https://github.com/openSUSE/libzypp.git#tag=${pkgver}")
sha256sums=('0ed7ba5603ec9ca2aeb3431cdf3012cfe7f6053d74f40ba188dc2a69330621ff')

prepare() {
  # Fix compat with libxml 2.14
  # https://github.com/openSUSE/libzypp/issues/635
  git -C $pkgname cherry-pick -n 7001aa82291d95c687ae923552b88779a71264df
}

build() {
    local cmake_options=(
        -B build
        -S $pkgname
        -W no-dev
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX=/usr
        -D ENABLE_BUILD_TESTS=ON
        -D ENABLE_BUILD_DOCS=ON
        -D ENABLE_BUILD_TRANS=ON
        -D ENABLE_ZSTD_COMPRESSION=ON
        -D DISABLE_MEDIABACKEND_TESTS=ON
        -D ZYPP_LIBEXEC_INSTALL_DIR=/usr/lib/zypp
    )
    cmake "${cmake_options[@]}"
    cmake --build build
}

check() {
    # https://github.com/openSUSE/libzypp/issues/622
    local excluded_tests=""
    local ctest_flags=(
        --test-dir build
        # show the stdout and stderr when the test fails
        --output-on-failure
        # execute tests in parallel
        --parallel $(nproc)
        # exclude problematic tests
        --exclude-regex "${excluded_tests[@]}"
    )
    ctest "${ctest_flags[@]}"
}

package_libzypp() {
    depends=(
        boost-libs
        curl
        gcc-libs
        glib2
        glibc
        gpgme
        libsigc++
        libsolv
        libxml2
        openssl
        rpm-tools
        systemd-libs
        yaml-cpp
        zlib
    )
    optdepends=(
        'libzypp-docs: for documentation'
    )


    DESTDIR="$pkgdir" cmake --install build

    # remove reference to /usr/libexec and remove docs
    mv -v "$pkgdir/usr/libexec/zypp/" "$pkgdir/usr/lib/"
    rm -vr "$pkgdir/usr/libexec/" "$pkgdir/usr/share/doc/"
}

package_libzypp-docs() {
    pkgdesc+=" - documentation"

    install -vdm 755 "$pkgdir/usr/share/doc/$pkgbase/html/"
    cp -av build/doc/* "$pkgdir/usr/share/doc/$pkgbase/html/"
}
