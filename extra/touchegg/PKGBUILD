# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AwesomeHaircut <jesusbalbastro@gmail.com>
# Contributor: jbouter <aur@kn0x.org>

pkgname=touchegg
pkgver=2.0.18
pkgrel=1
pkgdesc="Linux multi-touch gesture recognizer"
url="https://github.com/JoseExposito/touchegg"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('libinput' 'cairo' 'systemd-libs' 'libx11' 'libxi' 'libxrandr' 'libxtst' 'pugixml' 'gtk3' 'glib2')
makedepends=('cmake')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('79c806dc21305a6a1ebe5ad8db3c7b632876ca24627cef02e7cd89b0ea4ab34ddd9ccc9f631bba34f19fa25508b1193307f2a79fbc875e52f29be2356c84e4ec')

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
	    -DCMAKE_BUILD_TYPE='None' \
	    -DCMAKE_INSTALL_PREFIX='/usr' \
	    -DCMAKE_POLICY_VERSION_MINIMUM='3.5' \
	    -Wno-dev
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
