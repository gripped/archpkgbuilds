# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-cli_helpers
pkgver=2.9.0
pkgrel=1
pkgdesc='Python helpers for common CLI tasks'
arch=(any)
url='https://github.com/dbcli/cli_helpers'
license=(BSD-3-Clause)
depends=(
  python
  python-tabulate
  python-configobj
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(python-pytest python-pygments)
optdepends=('python-pygments: for styles extras')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('484476cfa1e133ec3141d5cc26b12998961ff79dc90f6aebccb608640ad2ff1f2267646dbb865ddf7637c7ac6d47f73f0d4048a3b8d1b8d07002fe8feed5d3b6')
b2sums=('7a239b54c06e148cdc469c412645f3235a8862f2785129110eeb9b331f2856a070427716ff2b37bf79f8de27a3dab6a37ac78c624cbada5e511eee5160c95090')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_tabulate_wrapper \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_style_output_table
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
