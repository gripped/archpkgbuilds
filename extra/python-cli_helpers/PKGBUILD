# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-cli_helpers
pkgver=2.7.0
pkgrel=2
pkgdesc='Python helpers for common CLI tasks'
arch=(any)
url='https://github.com/dbcli/cli_helpers'
license=(BSD-3-Clause)
depends=(
  python
  python-tabulate
  python-configobj
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(python-pytest python-pygments)
optdepends=('python-pygments: for styles extras')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('ad290a13eae44c26067b2ddef41d823921046246624c96f465169f4a0ca10dbbeea2c4ed181aea869057a53455a2713b83fdd31f1ea3fc93b871aadbbca3791e')
b2sums=('ab3915551c641fe3993baef13c107e630219976008f6e9f656220a9a06c359735788497630e1c7bae398e2332b6952dfc262bc5e049fb4e7bf7e3f779836c639')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_tabulate_wrapper \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_style_output_table
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
