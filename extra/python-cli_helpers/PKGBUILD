# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-cli_helpers
pkgver=2.8.1
pkgrel=1
pkgdesc='Python helpers for common CLI tasks'
arch=(any)
url='https://github.com/dbcli/cli_helpers'
license=(BSD-3-Clause)
depends=(
  python
  python-tabulate
  python-configobj
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(python-pytest python-pygments)
optdepends=('python-pygments: for styles extras')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('eeb6a6df3cfdf2a660cf4687cd216063f98531e7adc9147b42f6dde6496f422b9163d99cb136cf09c739af8f8d5ae6389321481126be10cbac5698ca1462a052')
b2sums=('40b4b809b504d76bd1ee3e4f2768800c59f55407915b9dfc96ccd5bbc3741f771309022f3cfef25be3a860566535d7939a3c85d8844abb5fe72ad2f8878a1d6f')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_tabulate_wrapper \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_style_output_table
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
