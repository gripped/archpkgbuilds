# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-cli_helpers
pkgver=2.10.0
pkgrel=1
pkgdesc='Python helpers for common CLI tasks'
arch=(any)
url='https://github.com/dbcli/cli_helpers'
license=(BSD-3-Clause)
depends=(
  python
  python-tabulate
  python-configobj
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(python-pytest python-pygments)
optdepends=('python-pygments: for styles extras')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('89328a133be8077e4f1810b15a4df0cd6e9784f36f25b8575eefe6daac2c7b00e761357baeed3dbee08276e16f136abf29e876ceb7e5d4e91d7ea75eb8a9ef4f')
b2sums=('6c3f866b9c2e5f400a4801b9e7b3062ceedf21cbf9e4e8315fbde0fe72636124544fecd01e05b3317ae23c7856f4703d8ec75619592281a1b1dc4287de2a229f')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_tabulate_wrapper \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_style_output_table
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
