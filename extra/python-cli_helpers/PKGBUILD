# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-cli_helpers
pkgver=2.10.1
pkgrel=1
pkgdesc='Python helpers for common CLI tasks'
arch=(any)
url='https://github.com/dbcli/cli_helpers'
license=(BSD-3-Clause)
depends=(
  python
  python-tabulate
  python-configobj
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(python-pytest python-pygments)
optdepends=('python-pygments: for styles extras')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('70dc5c32237f1b6cdcdc17088e04e4f3799c0b44328d4f18f09ca00d49b1434a9a8e678229d14d1cdb63733088e45397a3862a32840eb1951f3222574360be15')
b2sums=('9094cc51764c30ff0b5d212be9d1179c4a86ab8547fec6356b06335da6894adf4f34e691345b35120f345534ad1c03ed19e20f6334c2aee757355aa825e93b21')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_tabulate_wrapper \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_style_output_table
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
