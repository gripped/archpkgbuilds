# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-cli_helpers
pkgver=2.4.0
pkgrel=1
pkgdesc='Python helpers for common CLI tasks'
arch=('any')
url='https://github.com/dbcli/cli_helpers'
license=('BSD-3-Clause')
depends=(
  'python'
  'python-tabulate'
  'python-configobj'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-pytest')
optdepends=('python-pygments: for styles extras')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('94213200f24c098e9d63b86f1b4c1a3ec76523115187d603368e03d0e63584461ef9d6db33222f21647d634ded53ecffbd31fe0a11568ba8f82ea7bd02e14e36')
b2sums=('17326cf7e76b9a421fd60aa0085b69f626a04d415585e3c514b6c35e8f22defca016cbcb76fae0563be4aec0e175c44b4028e6b438b4e1bc6b1f1f5a03c20636')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_tabulate_wrapper
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
