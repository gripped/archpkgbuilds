# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=mmc-utils
pkgver=1.0
pkgrel=1
pkgdesc="Userspace tools for MMC/SD devices"
url="https://mmc-utils.readthedocs.io"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('glibc')
makedepends=('git' 'sparse')
source=("git+https://git.kernel.org/pub/scm/utils/mmc/mmc-utils.git#tag=v${pkgver}"
	'0001-makefile-gcc-include-fix.patch')
sha256sums=('201e123099fd31edd32fb2b6c95025b963a8c9e502c9a510666f3845b2745bb0'
            '20281124858d2739ad1b70753624f599f896e3f3326987ba5d6e1d32a8ea8b75')

prepare() {
        cd "${pkgname}"
        patch -Np1 -i "${srcdir}/0001-makefile-gcc-include-fix.patch"
}

build() {
	cd "${pkgname}"
	make
}

package() {
	cd "${pkgname}"
	make prefix=/usr DESTDIR="${pkgdir}" install
}
