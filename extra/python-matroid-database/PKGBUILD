# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=matroid-database
pkgname=python-$_pyname
pkgver=0.2
pkgrel=1
pkgdesc='Python interface to matroid database'
arch=(any)
url='https://github.com/gmou3/matroid-database'
license=(GPL-3.0-or-later)
depends=(python)
makedepends=(python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://pypi.org/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('662d836b5d2152641b84758dc7669a66db51840d674533cb46496f2023c3c54c')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
