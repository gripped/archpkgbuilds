# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=quodlibet
pkgver=4.7.1
pkgrel=2
pkgdesc="Music player and music library manager"
arch=(any)
url="https://quodlibet.readthedocs.io/"
license=(GPL-2.0-or-later)
depends=(
  dbus-python
  gdk-pixbuf2
  glib2
  gobject-introspection-runtime
  gst-plugins-base
  gst-plugins-base-libs
  gstreamer
  gtk3
  hicolor-icon-theme
  libsoup3
  pango
  python
  python-cairo
  python-feedparser
  python-gobject
  python-mutagen
  python-requests
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx_rtd_theme
  python-wheel
)
# python-raven python-senf are currently vendored
checkdepends=(
  gst-plugins-bad
  gst-plugins-good
  gst-plugins-ugly
  # python-polib  # TODO: package python-polib
  python-pytest
  python-pyvirtualdisplay
  xine-lib
  xorg-server-xvfb
)
optdepends=(
  'gst-libav: Extra media codecs for the GStreamer backend'
  'gst-plugins-bad: Extra media codecs for the GStreamer backend; Audio Pitch / Speed, Crossfeed, Submit Acoustic Fingerprints plugins'
  'gst-plugins-good: Extra media codecs, PulseAudio support for the GStreamer backend; Audio Compressor, Karaoke, Replay Gain plugins'
  'gst-plugins-ugly: Extra media codecs for the GStreamer backend'
  'gtksourceview3: Undo and redo support in multiline text fields'
  'kakasi: Kana/Kanji Simple Inverter plugin'
  'libappindicator-gtk3: Appindicator support in the Tray Icon plugin'
  'libkeybinder3: Multimedia key support under non Gnome setups'
  'libmodplug: MOD support'
  'python-musicbrainzngs: MusicBrainz Lookup, MusicBrainz Sync plugins'
  'python-paho-mqtt: MQTT Publisher plugin'
  'python-pyinotify: Automatic Library Update plugin'
  'rygel: UPnP AV Media Server plugin'
  'xine-lib: Alternative audio backend'
)
source=(
  https://github.com/$pkgname/$pkgname/releases/download/release-$pkgver/$pkgname-$pkgver.tar.gz{,.sig}
  $pkgname-fix-executable-conflict.patch
)
sha512sums=('edfc7fadd8cb062d812d5604974b28eceb7605da881af9c3cbf972c2bae29c64c1ffd31a2dc20d1ba5b44b6f1f22fba3bd88c4466479e567d43616d1024550f6'
            'SKIP'
            '3aa13cde5ee5dcbad5492afc082bff4d280a7b87f1ad26abc26cdef7ec549e53582ccce1d1e331d504fa2ff9a3ee3234394b21f04fb2e42cab866f10d96e2892')
b2sums=('2244529122c109c53a35159a4ae69d3db593f3779d6ac0b87a6ec5aa46a5e6f84bdc062faf0b8635c5c9d52774332bc3969cf4faa1e0955df876b7b75a41cfcf'
        'SKIP'
        '44566c652ac54278aa6ee13083d435848b5d86f1eb4ccc7e32ec52e50f8a027868c94e7a197f380dc1bf7c12620e5c41db1fd837a41b1e5e76714a2c563ba997')
validpgpkeys=(0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC) # Christoph Reiter <reiter.christoph@gmail.com>

prepare() {
  cd $pkgname-$pkgver
  # Fix zsh completions dir
  sed -e 's|vendor-completions|site-functions|' -i gdist/zsh_completions.py
  patch -Np1 < ../$pkgname-fix-executable-conflict.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -k='not TFlake8'  # do not run linter checks
    --ignore=tests/quality  # we are not interested in code quality tests
    # we do not care about missing tests for translations
    --deselect=tests/test_po.py::TPOTFILESIN::test_missing
    # certainly do not want to do a telnet connection during test
    --deselect='tests/plugin/test_prefs.py::TestPluginPrefs::test_plugin_pref[SqueezeboxSyncPlugin]'
    # certainly do not want to do a telnet connection during test
    --deselect='tests/plugin/test_prefs.py::TestPluginPrefs::test_plugin_pref[SqueezeboxPlaylistPlugin]'
    # https://github.com/quodlibet/quodlibet/issues/3966
    --deselect=tests/test_operon.py::TOperonEdit::test_remove_all
    # TODO: fails with Python 3.14
    --deselect=tests/test_formats_xiph.py::TVCCoverOgg::test_get_images
    --deselect=tests/test_formats_xiph.py::TVCCoverOgg::test_handle_old_coverart
    --deselect=tests/test_formats_xiph.py::TVCCoverOgg::test_set_image
    --deselect=tests/test_formats_xiph.py::TVCCoverFlac::test_get_images
    --deselect=tests/test_formats_xiph.py::TVCCoverFlac::test_handle_old_coverart
    --deselect=tests/test_formats_xiph.py::TVCCoverFlac::test_set_image
  )

  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" {README,NEWS}.rst
}
