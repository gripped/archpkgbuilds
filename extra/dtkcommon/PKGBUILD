# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.7.9
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('LGPL3')
depends=('dconf')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtkcommon.git#tag=$pkgver")
sha512sums=('ea55fc65bbc56de0534499c663fd3ca53ec2c22672e79feb94cd5c6f9ceec6826d574a2e22aa338d58bc1420d52345bfb805bb69ad8060b84fb7e1709f458d0d')

build() {
  cd dtkcommon
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr  \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd dtkcommon
  DESTDIR="$pkgdir" ninja install
}
