# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.6.16
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('LGPL3')
depends=('dconf')
makedepends=('cmake' 'ninja')
source=("https://github.com/linuxdeepin/dtkcommon/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0fa5236b803393bce6919d15b8ce373748f03499cdbc2190d22c800d19905765c32493631f64bc8bfd8f964094aeaeb90734e979d5426bef85707977bbeaa960')

build() {
  cd dtkcommon-$pkgver
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr  \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd dtkcommon-$pkgver
  DESTDIR="$pkgdir" ninja install
}
