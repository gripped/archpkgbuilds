# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.6.29
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('LGPL3')
depends=('dconf')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtkcommon.git#tag=$pkgver")
sha512sums=('33e3898d3e5246d54ddc492b98eca1d9ccbe31575d0b51fbf359cc00ee584a19f16cf96718d872dbfb6b6ffa2a57d9da9b867e9bccb9b78b26b61727fe18065b')

build() {
  cd dtkcommon
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr  \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd dtkcommon
  DESTDIR="$pkgdir" ninja install
}
