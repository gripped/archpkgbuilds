# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.7.28
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('BSD-3-Clause')
depends=('dconf')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtkcommon.git#tag=$pkgver")
sha512sums=('d0d7da7e76809ff95238a911b5d703ee5ae0ecd396e27852fb36c17badcce37c69d3b0d1c7d3a85204240fad2c9e53e1e21c9813ca1b43e84f281f34f366e2f6')

build() {
  cd dtkcommon
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkcommon
  DESTDIR="$pkgdir" ninja install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
