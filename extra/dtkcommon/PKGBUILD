# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.7.20
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('BSD-3-Clause')
depends=('dconf')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtkcommon.git#tag=$pkgver")
sha512sums=('67391f77ef2dc1cf5e4dd69e58decadb051af1da18ec223fbf22ab96f363cb9c475f84f844e0605c2a1977c1ae04e636d1c4490de336b99366905c09e65a7191')

build() {
  cd dtkcommon
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkcommon
  DESTDIR="$pkgdir" ninja install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
