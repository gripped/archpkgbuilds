# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.7.23
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('BSD-3-Clause')
depends=('dconf')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtkcommon.git#tag=$pkgver")
sha512sums=('cc2632102c2c61375a9a2c2164611a06649bb3b5aa7da9eff42c20cccc354b9f807177fea9abfe0a8e3570d91d5f91fb11aa274f3b1c9684acbcd7faabac63cf')

build() {
  cd dtkcommon
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkcommon
  DESTDIR="$pkgdir" ninja install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
