# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.7.4
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('LGPL3')
depends=('dconf')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtkcommon.git#tag=$pkgver")
sha512sums=('55286ac52db66c0d5e0d28b46859927d9adbdcc9a4a4a4de7f7ed4581534324f22ce76e39a096da7197570c2de4f7f3182ce29b659198a8bd3fb8801828ebdc7')

build() {
  cd dtkcommon
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr  \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd dtkcommon
  DESTDIR="$pkgdir" ninja install
}
