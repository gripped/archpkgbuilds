# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.7.22
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('BSD-3-Clause')
depends=('dconf')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtkcommon.git#tag=$pkgver")
sha512sums=('03b39403f202a3baf909178c39bdb3c469940f32a27956ca2c536a9ec1a0c6cbdc624c557f0ff4cb7bb3ca5be6f0cf0a800368c6d5631040f4b9a56170682161')

build() {
  cd dtkcommon
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkcommon
  DESTDIR="$pkgdir" ninja install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
