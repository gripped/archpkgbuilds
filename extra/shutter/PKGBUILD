# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: PhotonX <photon89 [at] gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Shanto <shanto@hotmail.com>
# Contributor: Athurg <athurg@gooth.cn>
# Contributor: TDY <tdy@gmx.com>

pkgname=shutter
pkgver=0.99.6
pkgrel=1
pkgdesc="The feature-rich screenshot tool"
arch=('any')
url="https://shutter-project.org/"
license=('GPL-3.0-only')
depends=('xdg-utils' 'imagemagick' 'procps' 'librsvg' 'libwnck3'
         'perl-carp-always'
         'perl-file-basedir'
         'perl-file-copy-recursive'
         'perl-file-which'
         'perl-glib-object-introspection'
         'perl-goocanvas2'
         'perl-goocanvas2-cairotypes'
         'perl-gtk3'
         'perl-gtk3-imageview'
         'perl-json'
         'perl-json-maybexs'
         'perl-locale-gettext'
         'perl-moo'
         'perl-net-dbus'
         'perl-number-bytes-human'
         'perl-proc-processtable'
         'perl-proc-simple'
         'perl-readonly'
         'perl-sort-naturally'
         'perl-www-mechanize'
         'perl-x11-protocol'
         'perl-xml-simple'
         'perl-xml-simple'
         'pango-perl'
         'xdg-desktop-portal')
optdepends=('bc: 3D Rotate and 3D Reflection plugins support'
            'libappindicator-gtk3: display the tray icon in some environments (like GNOME or Plasma Wayland)'
            'libavif: AVIF format support'
            'perl-image-exiftool: read and write EXIF data'
            'perl-lwp-protocol-https: Imgur upload support'
            'perl-net-oauth: Imgur upload support'
            'perl-path-class: Imgur upload support'
            'webp-pixbuf-loader: WebP format support')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/shutter-project/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('3c75d7cd7f472411eccb7d0e1e4f67412842463923f83d979b57d1e3ebe05b36792d5e5bb54f59dbc22ffedb8e18b569f3fbab5006fb6d31490c4805d4856439')

package() {
  cd "${pkgname}-${pkgver}"
  make install prefix="${pkgdir}/usr"
}
