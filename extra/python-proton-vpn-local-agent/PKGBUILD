# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>
# Contributor: Self Denial <selfdenial at pm dot me>
# Contributor: antermin <github dot com slash antermin>

pkgname=python-proton-vpn-local-agent
pkgver=1.5.0
pkgrel=1
_pyver=3.13
pkgdesc="Proton VPN local agent written in Rust"
arch=(x86_64)
url="https://github.com/ProtonVPN/local-agent-rs"
license=(GPL-3.0-or-later)
options=(!lto !debug)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  git
  cargo
)
# Proton did not do the tag properly
#source=("git+https://github.com/ProtonVPN/local-agent-rs.git#tag=${pkgver}")
source=("git+https://github.com/ProtonVPN/local-agent-rs.git#commit=0f7a7fa240f3d539896bbf7cdc3539f4daa3e1de")
sha256sums=('e37902dd39ca9cbee37095ccce841e3e9f744f8b86f5935fa68f833dcd37d0ed')

prepare() {
    cd "${srcdir}"/local-agent-rs/python-proton-vpn-local-agent
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${srcdir}"/local-agent-rs/python-proton-vpn-local-agent
    cargo build --frozen --release --all-features
}

check() {
    cd "${srcdir}"/local-agent-rs/python-proton-vpn-local-agent
    cargo test --frozen --all-features
}

package() {
    mkdir -p "${pkgdir}"/usr/lib/python"${_pyver}"/site-packages/proton/vpn
    install -Dm755 "${srcdir}"/local-agent-rs/python-proton-vpn-local-agent/target/release/libpython_proton_vpn_local_agent.so "${pkgdir}"/usr/lib/python"${_pyver}"/site-packages/proton/vpn/local_agent.abi3.so
}
