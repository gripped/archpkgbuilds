# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=kubo
pkgver=0.32.1
pkgrel=1
pkgdesc='IPFS implementation in Go'
url='https://github.com/ipfs/kubo'
arch=(x86_64)
license=(MIT)
depends=(glibc)
makedepends=(
  git
  go
)
optdepends=('fuse2: for mounting/advanced use')
provides=('go-ipfs')
replaces=('go-ipfs')
conflicts=('go-ipfs')
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/ipfs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ipfs.service
        ipfs@.service)
sha256sums=('d95b6b0ea3702854128a2617dac137dcb591dfcd0bca11d04d07774b7fc72cec'
            'b9cee7b118c5dbb16c182340bb37150c5f864501e1d649995ff0229c577026b3'
            '69cb69f638b97b5578d635147e4d78b29ad707ed17cb7968cd4588fe266c92ab')
b2sums=('3962042d22eb364f6e0d28e96f39a6263ba2ce71b0b8e0b7087f551bf39b5bb5aa7fcfda6d9c2e58a580abc4033bb700bd5f789f432c0882d66bba7a08c44586'
        'b0c0e8ede6c872104fd55e4edc127ba3e2c3dacbbe74178337effe6ae149a12246b0dc662c10c420ddadf1bdb889f2d747601f807bd2bc5e90101e1ffce595a4'
        '88f7b58b11519da87ba2e50a493ae62e57ab1c1cbbca7d5d8a3f6705738a7a09636ec614e3f905be20f3442214f9f12eedfc5050a92ec763031e27595f42c0d6')

prepare() {
  cd "${pkgname}-${pkgver}"
  go mod download
}

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_REQUIRED="1"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw -ldflags '-compressdwarf=false -linkmode=external'"

  make build
  cmd/ipfs/ipfs commands completion bash > "$srcdir/ipfs-completion.bash"
  cmd/ipfs/ipfs commands completion fish > "$srcdir/ipfs-completion.fish"
  cmd/ipfs/ipfs commands completion zsh > "$srcdir/ipfs-completion.zsh"
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644 "$srcdir"/ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 "$srcdir"/ipfs@.service "$pkgdir/usr/lib/systemd/system/ipfs@.service"

  install -Dm 644 "$srcdir"/ipfs-completion.bash "$pkgdir/usr/share/bash-completion/completions/ipfs"
  install -Dm 644 "$srcdir"/ipfs-completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/ipfs.fish"
  install -Dm 644 "$srcdir"/ipfs-completion.zsh "$pkgdir/usr/share/zsh/site-functions/_ipfs"

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/MIT" LICENSE-MIT
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/APACHE" LICENSE-APACHE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim: ts=2 sw=2 et:
