# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=kubo
pkgver=0.27.0
pkgrel=1
pkgdesc='IPFS implementation in Go'
url='https://github.com/ipfs/kubo'
arch=(x86_64)
license=(MIT)
depends=(glibc)
makedepends=(go git)
optdepends=('fuse2: for mounting/advanced use')
provides=('go-ipfs')
replaces=('go-ipfs')
conflicts=('go-ipfs')
source=(https://github.com/ipfs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ipfs.service
        ipfs@.service)
sha256sums=('d5bba110154d73700733cacbf9abf2639ed9b8ba11556befc664ec10f3d8d78f'
            'b9cee7b118c5dbb16c182340bb37150c5f864501e1d649995ff0229c577026b3'
            '69cb69f638b97b5578d635147e4d78b29ad707ed17cb7968cd4588fe266c92ab')
b2sums=('168f15b74c0143b9bc799c1bb485e90acac26b033f8d274b9e33ce0c19accddfcad134a7eecdd09baeb1c9f3610d0543a254b2b7914cebeb10604e75c3e8d565'
        'b0c0e8ede6c872104fd55e4edc127ba3e2c3dacbbe74178337effe6ae149a12246b0dc662c10c420ddadf1bdb889f2d747601f807bd2bc5e90101e1ffce595a4'
        '88f7b58b11519da87ba2e50a493ae62e57ab1c1cbbca7d5d8a3f6705738a7a09636ec614e3f905be20f3442214f9f12eedfc5050a92ec763031e27595f42c0d6')

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
  make build
  cmd/ipfs/ipfs commands completion bash > "$srcdir/ipfs-completion.bash"
  cmd/ipfs/ipfs commands completion fish > "$srcdir/ipfs-completion.fish"
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644 "$srcdir"/ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 "$srcdir"/ipfs@.service "$pkgdir/usr/lib/systemd/system/ipfs@.service"

  install -Dm 644 "$srcdir"/ipfs-completion.bash "$pkgdir/usr/share/bash-completion/completions/ipfs"
  install -Dm 644 "$srcdir"/ipfs-completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/ipfs.fish"

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/MIT" LICENSE-MIT
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/APACHE" LICENSE-APACHE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim: ts=2 sw=2 et:
