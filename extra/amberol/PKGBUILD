# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=amberol
pkgver=2024.1
pkgrel=1
pkgdesc='Small and simple sound and music player that is well integrated with GNOME'
arch=(x86_64)
url='https://apps.gnome.org/Amberol/'
license=(GPL-3.0-or-later)
depends=(
  dbus
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  git
  meson
  rust
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
source=(
  "git+https://gitlab.gnome.org/World/amberol.git?signed#tag=$pkgver"
  amberol-appdata-path.patch
)
b2sums=(
  3128a6138bba76f009808d148446e65eda396dbb4ca54c0c7cab7f17c4e1f20df80fcbf4ceb06f58d4641b576b5ebff8488403f076e1c61de719c819bebaa519
  483689d0ef4989df72c026dd96e0f4447798982a18b15e8cfebab953e2b00ae0a072c38c800ec6ea359b67e2e884d5f8421fae3ee242112ae7ca6fa4506f26e7
)
validpgpkeys=(53EF3DC3B63E2899271BD26322E8091EEA11BBB7) # Emmanuele Bassi (GNOME) <ebassi@gnome.org>

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/amberol/-/merge_requests/126
  git apply -3 ../amberol-appdata-path.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
