# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=angle-grinder
pkgver=0.19.6
pkgrel=1
pkgdesc='Slice and dice logs on the command line'
arch=('x86_64')
url='https://github.com/rcoh/angle-grinder'
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('8a2be3b63fc502b27ec4a454e10ccfd6f4255e40b1b90b44539b1e0be4973cd9e3db934320809ca1c1833360ec3ef23d2cbd9667a0c7a8e4428b751ffdc0c24a')
b2sums=('6cc2a58ee9b1fbdb65d8ab864dc4e825beefa3dfdb292ad6a00b74a455464dd6cd6262146ba7ea5ce26a036877b87e984a55cc50e76c12dd775ca6f25c3ec490')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

check() {
  cd "$pkgname"

  cargo test --frozen --release
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/agrind

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
