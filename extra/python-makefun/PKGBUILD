# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-makefun
pkgver=1.16.0
pkgrel=2
pkgdesc="Dynamically create Python functions with a proper signature"
arch=('any')
url="https://github.com/smarie/python-makefun"
license=('BSD-3-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-asyncio'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('0ba44a034f7e994335bb5a54565dea4b7738734baddff47b8001e58d3e3021ba8e9cd8103968d2579d30e52abc65b3460d20a2a89890c472437f31d46a131055')

build() {
  cd $pkgname-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest --asyncio-mode=auto
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
