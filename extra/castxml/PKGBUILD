# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org>

pkgname=castxml
_pkgname=CastXML
pkgdesc="C-family abstract syntax tree XML output tool"
pkgver=0.6.13
pkgrel=1
arch=(x86_64)
url="https://github.com/CastXML/CastXML"
license=(Apache-2.0)
depends=(
  clang
  gcc-libs
  glibc
  llvm-libs
)
makedepends=(
  cmake
  llvm
  python-sphinx
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('df954886464fe624887411e5f4e2a7db00da3d64a48f142d3aff973e2097e2d6')

build() {
  cd $_pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None \
    -Wno-dev \
    -DCLANG_LINK_CLANG_DYLIB=ON \
    -DSPHINX_HTML=ON \
    -DSPHINX_MAN=ON \
    -DCastXML_INSTALL_MAN_DIR=share/man
  cmake --build build
}

check() {
  cd $_pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package(){
  cd $_pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
}
