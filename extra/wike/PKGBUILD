# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=wike
pkgver=3.1.3
pkgrel=1
pkgdesc='Wikipedia reader for the GNOME desktop'
arch=(any)
url='https://apps.gnome.org/Wike/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
  pango
  python
  python-gobject
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
source=(
  "git+https://github.com/hugolabe/Wike.git#tag=$pkgver"
  wike-new-tab.patch
  wike-dbus-activatable.patch
)
b2sums=(
  f4c73e1f0c4519c91f06603a4f8b5d8a18ed4c642bf89855c8a9d29193511743107bab8617f4f638045607c5a6b930bcdf30866f2ce3266ddb5585cc567db360
  13619b237284607377e89f3b5d1a0d98ab8ee0e4066df705a2e121f321fec56a55ce151756b43596002836afc2636dd898b54d59566fbabb8384a31a3d6788e6
  3ea100f29aa3d50c27fad0218446d351bac43be4431f9a270e9e37302acaac40fdadf0fbba4981275b4f55742abd14d20e69f3c10f2084f8b5b09cd99bfccd72
)

prepare() {
  cd Wike

  # https://github.com/hugolabe/Wike/pull/220
  git apply -3 ../wike-new-tab.patch

  # https://github.com/hugolabe/Wike/pull/221
  git apply -3 ../wike-dbus-activatable.patch
}

build() {
  arch-meson Wike build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
