# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=wike
pkgver=3.2.0
pkgrel=2
pkgdesc='Wikipedia reader for the GNOME desktop'
arch=(any)
url='https://apps.gnome.org/Wike/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
  pango
  python
  python-gobject
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://github.com/hugolabe/Wike.git#tag=$pkgver")
b2sums=(bf74df8e75e05f35368213ae114fbb79f913d2c2c2c93c7c530e686cf77acb038ed400cc4f471fdb8f1db715af7cfffe54c4dd9fdfac9c2091a2e1d95aba7db3)

prepare() {
  cd Wike
}

build() {
  arch-meson Wike build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
