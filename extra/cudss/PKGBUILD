# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=cudss
pkgver=0.7.1.4
_cudaver=13
pkgrel=2
pkgdesc="A high-performance CUDA library for Direct Sparse Solvers"
arch=(x86_64)
url="https://docs.nvidia.com/cuda/cudss/"
license=(LicenseRef-NVIDIA-cuDSS)
depends=(
  "cuda>=$_cudaver"
  glibc
  libgcc
  libgomp
  libstdc++
)
optdepends=(
  'nccl: for the NCCL multi-GPU communication layer'
  'openmpi: for the OpenMPI multi-GPU communication layer'
)
options=(!strip)
source=(https://developer.download.nvidia.com/compute/$pkgname/redist/lib$pkgname/linux-x86_64/lib$pkgname-linux-x86_64-${pkgver}_cuda$_cudaver-archive.tar.xz)
b2sums=('ebefc3133becb22145b8f86acee33e8279060f2c3d69f913f8f262fdf5d2ede56e12958b83865bc7bf99ab77da35f40f3e0d2e37d5848a8f1279a7908477cd2c')

package() {
  cd lib$pkgname-linux-x86_64-${pkgver}_cuda${_cudaver}-archive

  install -vdm 755 "$pkgdir"/usr/
  cp -r lib include "$pkgdir"/usr/

  # remove static libs
  rm "$pkgdir"/usr/lib/*.a
  rm "$pkgdir"/usr/lib/cmake/cudss/cudss-static-targets*.cmake

  install -vdm 755 "$pkgdir"/usr/share/doc/$pkgname/
  cp -r examples src "$pkgdir"/usr/share/doc/$pkgname/

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
