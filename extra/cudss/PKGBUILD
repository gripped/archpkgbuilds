# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=cudss
pkgver=0.6.0.5
_cudaver=12
pkgrel=1
pkgdesc="A high-performance CUDA library for Direct Sparse Solvers"
arch=(x86_64)
url="https://docs.nvidia.com/cuda/cudss/"
license=(LicenseRef-NVIDIA-cuDSS)
depends=(
  "cuda>=$_cudaver"
  gcc-libs
  glibc
)
optdepends=(
  'nccl: for the NCCL multi-GPU communication layer'
  'openmpi: for the OpenMPI multi-GPU communication layer'
)
options=(!strip staticlibs)
source=(https://developer.download.nvidia.com/compute/cudss/redist/libcudss/linux-x86_64/libcudss-linux-x86_64-${pkgver}_cuda$_cudaver-archive.tar.xz)
b2sums=('b22bb464a01e47d32f4de2a5b4afbd5354f3c93a4e4ad1f1c7c21157f3366c8fe5d2573d0a780d0b7d1191bce72145fe8d88fae943ff9d8d100f4e0417873659')

package() {
  cd libcudss-linux-x86_64-${pkgver}_cuda${_cudaver}-archive

  install -vdm 755 "$pkgdir"/usr/
  cp -r lib include "$pkgdir"/usr/

  install -vdm 755 "$pkgdir"/usr/share/doc/$pkgname/
  cp -r examples src "$pkgdir"/usr/share/doc/$pkgname/

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
