# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=toot
pkgver=0.47.1
pkgrel=1
pkgdesc='A Mastodon CLI client'
arch=('any')
url='https://github.com/ihabunek/toot'
license=('GPL-3.0-only')
depends=(
  'python'
  'python-requests'
  'python-beautifulsoup4'
  'python-wcwidth'
  'python-urwid'
  'python-tomlkit'
  'python-click'
  'python-typing_extensions'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('3fb1a685b5e814c7093af4c1df7c389b097a6aa9890d52db8a7b897d109e2ef8a62cd694065b3319beefc35a8e352dd96a5982f96beee85479e60b069be7fb87')
b2sums=('7d448776ee97b7f378c6afd60aac11228a83f9aee2fcf079f86c0af9b7038c1d601ec00ffce91db22baa67237f43292872443930bdeaa6c7645e15b02d7783e4')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation

  # generate shell completions
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$(pwd)/tmp/usr/bin:$PATH"
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  for shell in bash fish zsh; do
  _TOOT_COMPLETE=${shell}_source toot > "$shell.completion"
  done
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # shell completion
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/toot"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/vendor_completions.d/toot.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_toot"
}
