# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=toot
pkgver=0.48.1
pkgrel=1
pkgdesc='A Mastodon CLI client'
arch=('any')
url='https://github.com/ihabunek/toot'
license=('GPL-3.0-only')
depends=(
  'python'
  'python-requests'
  'python-beautifulsoup4'
  'python-wcwidth'
  'python-urwid'
  'python-tomlkit'
  'python-click'
  'python-typing_extensions'
  'python-dateutil'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
optdepends=(
  'python-term-image: image support'
  'python-urwidgets: rich text support'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('04c2062e8f31d256ed301ea0143c0a91089d56fee92f6dc81666e99da9015c8062d2106af3c32b04f708d90863e091ab55bfa4d3cf50da9fd45dc2e1ba542072')
b2sums=('8df641c2e602eac85d748753a36b5014a54fd50094aae4421988cbe936b871677846f73574a13e25a670e562aa14296ebdf5ff2ed6a1dad42803aa98761cd4a1')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation

  # generate shell completions
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$(pwd)/tmp/usr/bin:$PATH"
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  for shell in bash fish zsh; do
  _TOOT_COMPLETE=${shell}_source toot > "$shell.completion"
  done
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # shell completion
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/toot"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/vendor_completions.d/toot.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_toot"
}
