# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=toot
pkgver=0.51.1
pkgrel=1
pkgdesc='A Mastodon CLI client'
arch=(any)
url='https://github.com/ihabunek/toot'
license=(GPL-3.0-only)
depends=(
  python
  python-requests
  python-beautifulsoup4
  python-wcwidth
  python-urwid
  python-urwidgets
  python-tomlkit
  python-click
  python-typing_extensions
  python-dateutil
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
optdepends=(
  'python-term-image: image support'
  'python-pillow: image support'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('9575579a9fa045db6f8a0c2f8fada760b00c9038936b6d0889b100af9637dbb66290060a56e82ea6b25d29ddac85274c37b8297b0bb0e0dcea3ecc1341e5c785')
b2sums=('6cebe63f1cdea8684af3f16f12033fff05dd8e4b8c4ad1a0612ab22caac454b376972dd5bc075dffac3788412136ac181328d2026bae6f5f89dcb4a53f325d23')

build() {
  cd "$pkgname"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation

  # generate shell completions
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$(pwd)/tmp/usr/bin:$PATH"
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  for shell in bash fish zsh; do
  _TOOT_COMPLETE=${shell}_source toot > "$shell.completion"
  done
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # shell completion
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/toot"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/vendor_completions.d/toot.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_toot"
}
