# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=toot
pkgver=0.50.1
pkgrel=1
pkgdesc='A Mastodon CLI client'
arch=(any)
url='https://github.com/ihabunek/toot'
license=(GPL-3.0-only)
depends=(
  python
  python-requests
  python-beautifulsoup4
  python-wcwidth
  python-urwid
  python-urwidgets
  python-tomlkit
  python-click
  python-typing_extensions
  python-dateutil
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
optdepends=(
  'python-term-image: image support'
  'python-pillow: image support'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('f48aabbf638a601ce253ebff2abcac6b558fc6b4ffe0fe0e268201cd3fb5ab4ef4751aec3433d7f86ca4e154592935a324b936abdeddc2f8a79f66a02532a211')
b2sums=('bd706c84eb98089ee24e42db64cdf3db1d110fe53d82c71cbe468b56e147619429eaffd4b5a3118011def2d4b27dc6eec724f9f8ae87735a78c2c94a464c1f95')

build() {
  cd "$pkgname"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation

  # generate shell completions
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$(pwd)/tmp/usr/bin:$PATH"
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  for shell in bash fish zsh; do
  _TOOT_COMPLETE=${shell}_source toot > "$shell.completion"
  done
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # shell completion
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/toot"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/vendor_completions.d/toot.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_toot"
}
