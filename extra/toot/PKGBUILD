# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=toot
pkgver=0.50.0
pkgrel=1
pkgdesc='A Mastodon CLI client'
arch=('any')
url='https://github.com/ihabunek/toot'
license=('GPL-3.0-only')
depends=(
  'python'
  'python-requests'
  'python-beautifulsoup4'
  'python-wcwidth'
  'python-urwid'
  'python-urwidgets'
  'python-tomlkit'
  'python-click'
  'python-typing_extensions'
  'python-dateutil'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
optdepends=(
  'python-term-image: image support'
  'python-pillow: image support'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('b85ce17388846ff7ab7792241f893b3eb721aceb437f75af80e8e22fb0caa3952c416dd1350ac7c284bebf9a822b07a65f01fbbdf3f779d9337fc5162bd467dc')
b2sums=('582e911953b2940befe64337dfbe1498d80153f61b91ff88d697edf11b3581bc26ca837113a0c310d456dcef26917a9a3c728d771f78795124fccf484cbdbc46')

prepare() {
  cd "$pkgname"

  git revert --no-commit \
    3c20a82ac1813eba6c69d2ea9b083ddc9cd47270 \
    cf0f1868f1afbb17e088e4405175f78246bf9d26
}

build() {
  cd "$pkgname"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation

  # generate shell completions
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$(pwd)/tmp/usr/bin:$PATH"
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  for shell in bash fish zsh; do
  _TOOT_COMPLETE=${shell}_source toot > "$shell.completion"
  done
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # shell completion
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/toot"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/vendor_completions.d/toot.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_toot"
}
