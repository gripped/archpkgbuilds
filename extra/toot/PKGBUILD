# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=toot
pkgver=0.51.0
pkgrel=1
pkgdesc='A Mastodon CLI client'
arch=(any)
url='https://github.com/ihabunek/toot'
license=(GPL-3.0-only)
depends=(
  python
  python-requests
  python-beautifulsoup4
  python-wcwidth
  python-urwid
  python-urwidgets
  python-tomlkit
  python-click
  python-typing_extensions
  python-dateutil
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
optdepends=(
  'python-term-image: image support'
  'python-pillow: image support'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('c43799d0e1f84308c2c6d021265ac323f44feac20987b12c34b87ed82c2ed0dd5dc32dd7089ea8ba9b3b3209a43a58d4548dd5782c5009b35f93ce89ed679069')
b2sums=('1b9d4b044df5d626cb1ae29597bc0de0d34c460c14f1b266289d84b2acc91d5c9d2830dcfc09548990aa89007733f2acee06c9f5f8ac322e254765a01b49e3cf')

build() {
  cd "$pkgname"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation

  # generate shell completions
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$(pwd)/tmp/usr/bin:$PATH"
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  for shell in bash fish zsh; do
  _TOOT_COMPLETE=${shell}_source toot > "$shell.completion"
  done
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # shell completion
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/toot"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/vendor_completions.d/toot.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_toot"
}
