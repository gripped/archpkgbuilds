# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: NOGISAKA Sadata <ngsksdt@gmail.com>
# Contributor: Bennett Piater <bennett at piater dot name>

pkgname=doctl
pkgver=1.101.0
pkgrel=1
pkgdesc='The official command line interface for the DigitalOcean API'
url='https://github.com/digitalocean/doctl'
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go')
source=(https://github.com/digitalocean/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('58d0215da9085c063ec34202abcad31c6d3f6c8330265247dc6415f6bc92d92547860e9541c77ae2542c49ffcbad733c86145591283599fc8ae8da0324489ba8')
b2sums=('b5eb8ca7fca7f4558f1d74886865e75d21241deb10ef4cae497f31cff18a67767c9dec1d145b9e3113415f115df48318a9339c8d85e0a22d5fdc05e2501c2171')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"
      -X github.com/digitalocean/doctl.Major=$(echo $pkgver | cut -d. -f1)
      -X github.com/digitalocean/doctl.Minor=$(echo $pkgver | cut -d. -f2)
      -X github.com/digitalocean/doctl.Patch=$(echo $pkgver | cut -d. -f3)
      -X github.com/digitalocean/doctl.Label=release
    " \
    ./cmd/...
}

check() {
  cd ${pkgname}-${pkgver}
  ./doctl --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "${pkgname}" -t "${pkgdir}/usr/bin"

  # setup completions
  install -dm 755 "${pkgdir}/usr/share/bash-completion/completions" \
                  "${pkgdir}/usr/share/zsh/site-functions" \
                 "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
