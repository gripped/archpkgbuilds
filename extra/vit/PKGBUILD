# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>

pkgname=vit
pkgver=2.3.3
pkgrel=2
pkgdesc='Terminal interface for Taskwarrior with Vim key bindings and colorization'
url='https://github.com/vit-project/vit'
arch=('any')
license=('MIT')
depends=('task' 'python' 'python-urwid' 'python-tasklib')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('9a1b1165fd77713df6f7a4cc8c1f7b26f439e529e477f0acd0748b09b1bd81de')
b2sums=('86d2e0ee3b3a690c94df9d40e1d3f0047406625b947e96f2389dd6687aaf59955fb7f971663a5e67793c1ca4376c7aae371c978188d2a885edeb974acf291600')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}
  # TODO: requires python-terminal
  # ./run-tests.sh
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 scripts/bash/vit.bash_completion "${pkgdir}/usr/share/bash-completion/completions/vit"
  install -Dm 644 *.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
