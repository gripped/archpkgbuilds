# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: soloturn <soloturn@gmail.com>

pkgname=cosmic-text-editor
pkgver=1.0.0.alpha.6
pkgrel=1
pkgdesc='Text editor for the COSMIC desktop'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-edit
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  oniguruma
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=4b75c32bffecf735f8393af9ddb32a89a237d3c2
source=(
  git+https://github.com/pop-os/cosmic-edit.git#tag=${_tag}
  cosmic-text-editor-lto.patch
)
b2sums=('a05435e80a4fbb3a47b5be447240ac700154a570c9a5b82ba7f92131d5c56ce96044153170c4771151754bcb89c2f4f788a96914972d6571abcd74ff40b955b8'
        'd0ac6a0caa849a4445812f574787c5f99e42e73dbc53dcbf0c0a212cc97382dcd3cdbce58f6203fb2d920fc45d4a0a4b5d086308bcb5e2a92b86f1bebf1dd6da')

prepare() {
  cd cosmic-edit
  patch -Np1 -i ../cosmic-text-editor-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-edit
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-edit
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld -C link-arg=-lonig"
  just build-release --frozen
}

package() {
  cd cosmic-edit
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
