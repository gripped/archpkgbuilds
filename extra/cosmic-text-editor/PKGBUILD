# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: soloturn <soloturn@gmail.com>

pkgname=cosmic-text-editor
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='Text editor for the COSMIC desktop'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-edit
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  oniguruma
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=d4294713d8fc5c44ed7c9b1957aa6db7ee16a4d4
source=(
  git+https://github.com/pop-os/cosmic-edit.git#tag=${_tag}
  cosmic-text-editor-lto.patch
)
b2sums=('2a2c436bb277aaf8b558ac5ff3074dec67f0ed5a04626a5c6079254998a74c9f0df7ad2e672abe5c14152fb2b35006546db05fcb67e0c539b1ee97ff6371e58c'
        'd0ac6a0caa849a4445812f574787c5f99e42e73dbc53dcbf0c0a212cc97382dcd3cdbce58f6203fb2d920fc45d4a0a4b5d086308bcb5e2a92b86f1bebf1dd6da')

prepare() {
  cd cosmic-edit
  patch -Np1 -i ../cosmic-text-editor-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-edit
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-edit
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld -C link-arg=-lonig"
  just build-release --frozen
}

package() {
  cd cosmic-edit
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
