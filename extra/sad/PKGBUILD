# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joost Bremmer <contact at madeofmagicandwires dot online>
# Contributor: Eliezio Oliveira <eliezio@pm.me>
# Contributor: Matthew Gamble <internet@matthewgamble.net>

pkgname=sad
pkgver=0.4.32
pkgrel=3
pkgdesc='Space Age seD'
arch=(x86_64)
url='https://github.com/ms-jpq/sad'
license=(MIT)
depends=(
  glibc
  libgcc
)
makedepends=(
  cargo
  git
  python
)
optdepends=(
  'fzf: for tui usage'
  'diff-so-fancy: for diff colourizer'
  'git-delta: for diff colourizer'
)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('224150e979c3dab38e4ca8f39b1108a8828b79caee394d595ab112dc5d03270492e87065be09ba79380ccd3c1c648f2de20b8e2017d0c0ed721a51917b1d334c')
b2sums=('e4f3d04f63f4fb85e021046091e66754edc413552ea819fd6adf3ed74093c903fba169a5faa709af32c9f854aa25dd58be107205ce40bc1738da3acd332fccbf')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sad

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim: set ts=2 sts=2 sw=2 et :
