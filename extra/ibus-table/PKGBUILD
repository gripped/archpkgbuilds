# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.17.15
pkgrel=1
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL-2.1-or-later')
makedepends=('git' 'docbook2x' 'docbook-sgml' 'docbook-utils')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("git+https://github.com/kaio/ibus-table.git#tag=$pkgver")
sha512sums=('b9de336859d429ddf89578f871570bb3d95e26fda4158d5ff4625116b76c2025a49e4b9a0e6929906c6dbb4d361c0ab2e2869ea3c873d73b8233bd0237a449bd')

build() {
  cd $pkgname

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
