# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.17.16
pkgrel=2
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL-2.1-or-later')
makedepends=('git' 'docbook2x' 'docbook-sgml' 'docbook-utils')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("git+https://github.com/kaio/ibus-table.git#tag=$pkgver")
sha512sums=('66c29d8f40a893ea6d1fb030d2e838ce5dc8bc8041d91975a25846552f5c977a4a5b5efc43513ef0be7a431f8828e69d1b29b68b0129c2ac90de48b2bbc9ad50')

build() {
  cd $pkgname

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
