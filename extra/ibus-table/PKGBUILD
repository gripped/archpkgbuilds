# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.17.13
pkgrel=1
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL-2.1-or-later')
makedepends=('git' 'docbook2x' 'docbook-sgml' 'docbook-utils')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("git+https://github.com/kaio/ibus-table.git#tag=$pkgver")
sha512sums=('cbad9a17caf88177426fcc9a3519d6eaab51f52e5cbff3701b4560628d5daa3f999fb4ce9b7961f326a4fb64a9218e80bb55c6c4b340149b8ce65994be9fed4c')

build() {
  cd $pkgname

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
