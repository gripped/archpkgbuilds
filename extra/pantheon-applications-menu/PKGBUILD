# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-applications-menu
pkgver=8.0.1
pkgrel=2
pkgdesc='The Pantheon Application Menu'
arch=(x86_64)
url=https://github.com/elementary/applications-menu
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  json-glib
  libgee
  libgranite.so
  libhandy-1.so
  libswitchboard-3.so
  wingpanel
  plank
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=251f790abc982e04db68d940024acb8f36211ffd
source=(pantheon-applications-menu::git+https://github.com/elementary/applications-menu.git#tag=${_tag})
b2sums=('8d47affef5b5f0e5979255aa13b7cbbfdbabcd3f6b87355f068cce8da01301e3e099ccf8c98c9a712551817309071f16a369127d376f20582d3759917a92d9fd')

pkgver() {
  cd pantheon-applications-menu
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson pantheon-applications-menu build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
