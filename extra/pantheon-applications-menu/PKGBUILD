# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-applications-menu
pkgver=2.11.1.r145.5e26f5ee
pkgrel=1
pkgdesc='The Pantheon Application Menu'
arch=(x86_64)
url=https://github.com/elementary/applications-menu
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  json-glib
  libgee
  libgranite.so
  libhandy-1.so
  libsoup
  libswitchboard-3.so
  wingpanel
  plank
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_commit=5e26f5ee08b37d99cfc9a52983aa763c85783d40
source=(pantheon-applications-menu::git+https://github.com/elementary/applications-menu.git#commit=${_commit})
b2sums=('88aee42ffca8e69e0ebc4c7707faf2aeca1779dc9173d0983e7f0d5172125f84b85791cbb71495dd765528d4b2301f895725f1fd6d54bc22231fb2c4b3bba85c')

pkgver() {
  cd pantheon-applications-menu
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson pantheon-applications-menu build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
