# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-applications-menu
pkgver=8.0.2
pkgrel=1
pkgdesc='The Pantheon Application Menu'
arch=(x86_64)
url=https://github.com/elementary/applications-menu
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  json-glib
  libgee
  libgranite.so
  libhandy-1.so
  libswitchboard-3.so
  wingpanel
  plank
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=b3d8c61ec2b3282ac2f21408834540e9173b72cc
source=(pantheon-applications-menu::git+https://github.com/elementary/applications-menu.git#tag=${_tag})
b2sums=('2d0d811967be9815a7261951b87913b88cd315c17197fc1c581d68b188f1e3ea31b01809a2a3917799d73f503cf339285775952b90d3f05315508aba455bd0b0')

pkgver() {
  cd pantheon-applications-menu
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson pantheon-applications-menu build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
