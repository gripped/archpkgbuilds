# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-applications-menu
pkgver=8.0.0
pkgrel=1
pkgdesc='The Pantheon Application Menu'
arch=(x86_64)
url=https://github.com/elementary/applications-menu
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  json-glib
  libgee
  libgranite.so
  libhandy-1.so
  libsoup
  libswitchboard-3.so
  wingpanel
  plank
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=697ed0a63f16a564d79055ef5641ab655c31defe
source=(pantheon-applications-menu::git+https://github.com/elementary/applications-menu.git#tag=${_tag})
b2sums=('4c49e624064d20effd91f33834ba65225a1d12c03e5fcf3d3a5d070b8f2a56171e524a6f854cf1165417c896219e37bf38a6b7582fce4a4140ff865b351120cf')

pkgver() {
  cd pantheon-applications-menu
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson pantheon-applications-menu build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
