# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>

pkgname=amdsmi
pkgver=6.4.3
pkgrel=1
pkgdesc='AMD System Management Interface library'
arch=('x86_64')
url='https://rocm.docs.amd.com/projects/amdsmi/en/latest/'
license=('MIT' 'NCSA')
depends=('rocm-core' 'glibc' 'gcc-libs' 'libdrm' 'python' 'python-yaml')
makedepends=('cmake' 'python-wheel' 'python-setuptools')
_git='https://github.com/ROCm/amdsmi'
_esmi='https://github.com/amd/esmi_ib_library'
_esmi_ver=4.1.2
source=(
  "$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz"
  "$pkgname-esmi-$pkgver.tar.gz::$_esmi/archive/refs/tags/esmi_pkg_ver-$_esmi_ver.tar.gz"
  "gcc15-missing-include.patch"
)
sha256sums=('a850125bf33402cad6e57d2130e32d8b37bfc315a6dcfddd90fb593fea1f0e46'
            'd95f12c1500c60dc01b500d8928864a276757bd3bbb4a6daa84fb8c028e2978b'
            'af704f2d65613e81719fa109488001ab5b2e5e7a529c18c84e695c002c055bce')
options=(!lto)
_dirname="$(basename "$_git")-$(basename "${source[0]}" .tar.gz)"
_esminame="$(basename "$_esmi")-$(basename "${source[1]}" .tar.gz)"

prepare() {
  mv "$_esminame" "$_dirname/esmi_ib_library"

  cd $_dirname

  # HACK from Fedora's spec: Replace git tag with version of our tar archive
  sed -i "s/NOT latest_esmi_tag/NOT esmi_pkg_ver-$_esmi_ver/" CMakeLists.txt

  patch -Np1 -i "$srcdir"/gcc15-missing-include.patch
}

build() {
  local cmake_args=(
    -Wno-dev
    -S "$_dirname"
    -B build
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
    -D CMAKE_BUILD_TYPE=None
    -D BUILD_TESTS=OFF
  )
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir/$_dirname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/$_dirname/esmi_ib_library/License.txt" "$pkgdir/usr/share/licenses/$pkgname/esmi_ib_LICENSE"
}
