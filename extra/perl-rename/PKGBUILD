# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florian Pritz <flo@xinu.at>

pkgname=perl-rename
pkgver=1.16
pkgrel=1
pkgdesc='Renames multiple files using Perl regular expressions'
arch=('any')
url='https://search.cpan.org/~pederst/rename/'
license=(
  'GPL-1.0-or-later'
  'Artistic-1.0-Perl'
)
depends=('perl')
options=('!emptydirs')
source=(
  "$pkgname-$pkgver.tar.gz::https://search.cpan.org/CPAN/authors/id/P/PE/PEDERST/rename-$pkgver.tar.gz"
)
sha512sums=('8be459c0173dd100b13dd3e78b22c0ba78eaac5c464ff493b7e8979d2e132aef12f7e6e861061bef0c034840fdd2b4778afceb448f85eb18f3c5208eb4c898cf')
b2sums=('3f3bd18bfab5f3758d8b532a54783f1949466dcf9d6b9471e918657681ec2e6af551a2b9be0bb923b16a666cc40008ccbb775a64ad10e9bf305be12eaa913afb')

build() {
  cd "rename-$pkgver"

  perl Makefile.PL PREFIX=/usr INSTALLDIRS=vendor

  make
}

package() {
  cd "rename-$pkgver"

  make DESTDIR="$pkgdir" install

  # create directories
  install -vd "$pkgdir/usr/bin"

  # symlink binary
  ln -s vendor_perl/rename "$pkgdir/usr/bin/perl-rename"

  # symlink man page
  ln -s /usr/share/man/man1/rename.1p.gz "$pkgdir/usr/share/man/man1/perl-rename.1p.gz"
}
