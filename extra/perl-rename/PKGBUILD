# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florian Pritz <flo@xinu.at>

pkgname=perl-rename
pkgver=1.15
pkgrel=1
pkgdesc='Renames multiple files using Perl regular expressions'
arch=('any')
url='https://search.cpan.org/~pederst/rename/'
license=(
  'GPL-1.0-or-later'
  'Artistic-1.0-Perl'
)
depends=('perl')
options=('!emptydirs')
source=(
  "$pkgname-$pkgver.tar.gz::https://search.cpan.org/CPAN/authors/id/P/PE/PEDERST/rename-$pkgver.tar.gz"
)
sha512sums=('698946cba7106e53667967d27b350256a23930e7f86045ee5f6cd915555b1c2f3137539929dc91282bb3a6ea2628d47321184a8663257c4258406e911cbace79')
b2sums=('3502ded5f630aea241638259074ec6cc4ccf04b5bb7c18163bf6380917850f7dceda558add456f6d4f83f8a69935ee8cfcc329db8d00ef40d2f6b0f6163b23e5')

build() {
  cd "rename-$pkgver"

  perl Makefile.PL PREFIX=/usr INSTALLDIRS=vendor

  make
}

package() {
  cd "rename-$pkgver"

  make DESTDIR="$pkgdir" install

  # create directories
  install -vd "$pkgdir/usr/bin"

  # symlink binary
  ln -s vendor_perl/rename "$pkgdir/usr/bin/perl-rename"

  # symlink man page
  ln -s /usr/share/man/man1/rename.1p.gz "$pkgdir/usr/share/man/man1/perl-rename.1p.gz"
}
