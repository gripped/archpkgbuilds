# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Gabriel Magno <gabrielmagno1@gmail.com>
# Contributor: Michał Pałubicki <maln0ir@gmx.com>

_pyname=agate-excel
pkgname=python-$_pyname
pkgver=0.4.2
pkgrel=2
pkgdesc='Adds read support for Excel files (xls and xlsx) to agate'
arch=(any)
url="https://$_pyname.readthedocs.org"
license=(MIT)
_pydeps=(agate
         olefile
         openpyxl
         xlrd)
depends=(python
         "${_pydeps[@]/#/python-}")
makedepends=(python-setuptools
             python-sphinx
             python-sphinx-furo)
_archive="$_pyname-$pkgver"
source=("$_archive.tar.gz::https://github.com/wireservice/$_pyname/archive/$pkgver.tar.gz")
sha256sums=('1c3df325257c6f5047dc1ce7475caa109565f0ae98379e39e1bd83503617153e')

build() {
	cd "$_archive"
	python setup.py build
	make -C docs html
	local _rtd_theme_path="$(python -c 'import sphinx_rtd_theme; print(sphinx_rtd_theme.get_html_theme_path())')"
	rm -rvf docs/_build/html/_static
	ln -svf "$_rtd_theme_path/sphinx_rtd_theme/static" docs/_build/html/_static
}

check() {
	cd "$_archive"
	python -m unittest discover -vs .
}

package() {
	cd "$_archive"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -dm0755 "$pkgdir/usr/share/doc/"
	cp -rv docs/_build/html "$pkgdir/usr/share/doc/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING
}
