# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=ttysvr
pkgver=0.3.2
pkgrel=1
pkgdesc="Screen saver for your terminal"
arch=('x86_64')
url="https://github.com/cxreiff/ttysvr"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'alsa-lib' 'systemd-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('cbf40de5f8c67dc7283651e8e87f8b857776a4aa5804b220f03ae4bf4134e792c73c33435af2c5d396cfaa5b7a9822ef4ec46674d39e230475ca4b8fa954490d')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --no-default-features
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
