# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=ttysvr
pkgver=0.3.4
pkgrel=1
pkgdesc="Screen saver for your terminal"
arch=('x86_64')
url="https://github.com/cxreiff/ttysvr"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'alsa-lib' 'systemd-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('74bab2615698d5064afe802756f3aca66ff949f2b2b653cbd48789a30462413aca4e574905745dbff77e678cbdb12426e15177030b14bf563383dae333a91cd5')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --no-default-features
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
