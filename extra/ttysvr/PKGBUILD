# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=ttysvr
pkgver=0.3.1
pkgrel=1
pkgdesc="Screen saver for your terminal"
arch=('x86_64')
url="https://github.com/cxreiff/ttysvr"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'alsa-lib' 'systemd-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('abc235354a44d76184dee338f48bd6861e2403500d0aee00a2fea6ecc0be13a4bb3d366a45dd560b5e10ac3ddcfc93db9cf4fc28e350edea92e939f462619f06')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --no-default-features
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
