# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=6.0.26
pkgrel=1
pkgdesc='The dock of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('LGPL3')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
# deepin-session-shell: org.deepin.dde.LockFront1
depends=('deepin-menu' 'deepin-daemon' 'deepin-launchpad' 'deepin-qt5integration'
         'deepin-network-core' 'deepin-control-center' 'dwayland' 'wayland' 'dtkcore' 'dtkgui' 'dtkwidget'
         'qt5-base' 'qt5-svg' 'qt5-x11extras' 'qt5-wayland' 'glib2' 'gsettings-qt' 'libdbusmenu-qt5'
         'libx11' 'libxext' 'libglvnd' 'libxtst' 'libxcursor' 'libxres' 'libxcb' 'libxkbcommon'
         'xcb-util-wm' 'xcb-util-image' 'gcc-libs' 'dconf' 'deepin-application-manager'
         'deepin-appearance' 'deepin-widgets' 'deepin-wloutput-daemon' 'deepin-session-shell')
makedepends=('cmake' 'ninja' 'qt5-tools' 'extra-cmake-modules')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-dock/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5618412f465a26091205e2e7844c4608bb0d4e1cd5f201e2cdb7860fc24769eb57650fed479029baab1d90fd7c0fdde5f1cce86848f9973ffcd1b5e497bc948c')

build() {
  cd dde-dock-$pkgver
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

check() {
  cd dde-dock-$pkgver
  # ninja test
}

package() {
  cd dde-dock-$pkgver
  DESTDIR="$pkgdir" ninja install
}
