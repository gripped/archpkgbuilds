# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=6.0.27
pkgrel=1
pkgdesc='The dock of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('LGPL3')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
# deepin-session-shell: org.deepin.dde.LockFront1
depends=('deepin-menu' 'deepin-daemon' 'deepin-launchpad' 'deepin-qt5integration'
         'deepin-network-core' 'deepin-control-center' 'dwayland' 'wayland' 'dtkcore' 'dtkgui' 'dtkwidget'
         'qt5-base' 'qt5-svg' 'qt5-x11extras' 'qt5-wayland' 'glib2' 'gsettings-qt' 'libdbusmenu-qt5'
         'libx11' 'libxext' 'libglvnd' 'libxtst' 'libxcursor' 'libxres' 'libxcb' 'libxkbcommon'
         'xcb-util-wm' 'xcb-util-image' 'gcc-libs' 'dconf' 'deepin-application-manager'
         'deepin-appearance' 'deepin-widgets' 'deepin-wloutput-daemon' 'deepin-session-shell')
makedepends=('cmake' 'ninja' 'qt5-tools' 'extra-cmake-modules')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-dock/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('06166da33fe9189ccd71a2bb88c49351a52fc305791039df934f0aed91d69d3588e7f32c5481fc1b8450f106cb84289d3b354bbbaa70fd0558a3457780a0b91f')

build() {
  cd dde-dock-$pkgver
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

check() {
  cd dde-dock-$pkgver
  # ninja test
}

package() {
  cd dde-dock-$pkgver
  DESTDIR="$pkgdir" ninja install
}
