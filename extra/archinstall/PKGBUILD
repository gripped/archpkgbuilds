# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=3.0.7
pkgrel=3
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL-3.0-only)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  dosfstools
  e2fsprogs
  glibc
  kbd
  libcrypt.so
  libxcrypt
  pciutils
  procps-ng
  python
  python-cryptography
  python-pydantic
  python-pyparted
  systemd
  util-linux
  xfsprogs
  lvm2
  f2fs-tools
  ntfs-3g
)
makedepends=(
  ruff
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
  python-sphinx_rtd_theme
  python-pylint
  python-pylint-pydantic
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.sig::$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz.sig
)
sha512sums=('2e6e2ffdba097a004ae2ac94178c09a47084a67d778d6ca522aa1b5109647d425b58da8841249f1c3989a0fbe8631d9ab27d0b1b7fa3e7806667544aeb129f55'
            'SKIP')
b2sums=('405216a1635d42e5629815973acbf2455d28483f4d6e9f080052799f7d672af146a297606453638363647637b7b1b08151863d31f32c4ed51e1cfca8525c2b2d'
        'SKIP')
validpgpkeys=('8AA2213C8464C82D879C8127D4B58E897A929F2E') # torxed@archlinux.org

check() {
  cd $pkgname-$pkgver
#  pylint .
  ruff check
}

build() {
  cd $pkgname-$pkgver
  
  # Applying patch https://github.com/archlinux/archinstall/pull/3540/files
  # This wil be include in the next version, but the next version is blocked
  # due to breaking backwards compatability, so patching here for now:
  sed -i 's/grub-btrfs.service/grub-btrfsd.service/g' archinstall/lib/installer.py

  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
