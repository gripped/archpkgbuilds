# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=3.0.13
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL-3.0-only)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  dosfstools
  e2fsprogs
  glibc
  kbd
  libcrypt.so
  libxcrypt
  pciutils
  procps-ng
  python
  python-cryptography
  python-pydantic
  python-pyparted
  python-textual
  systemd
  util-linux
  xfsprogs
  lvm2
  f2fs-tools
  ntfs-3g
)
makedepends=(
  ruff
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
  python-sphinx_rtd_theme
  python-pylint
  python-pylint-pydantic
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.sig::$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz.sig
)
sha512sums=('581a0267f076ba148e3b56db73806416b990211b6defd0a1bf006b8ffb0b2d1037d5faf2a369a64b05b6751150bb755671af967b8884ae227dc775d3ca0daa17'
            'SKIP')
b2sums=('6ffac0f6bac30645068777f342f862c50467d8d109f5033bd58985f4f42bd59a17438fbc90cb377ff6a4a2b183fb672fdbeaae8bed7e20b8ac0fa004815eff8f'
        'SKIP')
validpgpkeys=('8AA2213C8464C82D879C8127D4B58E897A929F2E') # torxed@archlinux.org

check() {
  cd $pkgname-$pkgver
#  pylint .
  ruff check
}

build() {
  cd $pkgname-$pkgver
  
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
