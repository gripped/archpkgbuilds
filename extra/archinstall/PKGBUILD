# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=3.0.6
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL-3.0-only)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  dosfstools
  e2fsprogs
  glibc
  kbd
  libcrypt.so
  libxcrypt
  pciutils
  procps-ng
  python
  python-pydantic
  python-pyparted
  systemd
  util-linux
  xfsprogs
  lvm2
  f2fs-tools
  ntfs-3g
)
makedepends=(
  ruff
  python-build
  python-cryptography
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
  python-sphinx_rtd_theme
  python-pylint
  python-pylint-pydantic
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.sig::$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz.sig
)
sha512sums=('8d880ee47349dfe1f15f05a3e00d773a316cd299e321212253dd2d6366e09b9bcbb0aeec1288aaee39586c9c9f9c840e3544683c46501d23397a04ca7c4d0268'
            'SKIP')
b2sums=('0a73a8eeb6b192ff568e2551ad711020c7645d2369b7de7091afe936445720b98a3dd888a81cecb17437309ffa3efe773df7ca86195808e86471a22f6a89a368'
        'SKIP')
validpgpkeys=('8AA2213C8464C82D879C8127D4B58E897A929F2E') # torxed@archlinux.org

check() {
  cd $pkgname-$pkgver
#  pylint .
  ruff check
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
