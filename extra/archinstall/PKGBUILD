# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=3.0.14
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL-3.0-only)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  dosfstools
  e2fsprogs
  glibc
  kbd
  libcrypt.so
  libxcrypt
  pciutils
  procps-ng
  python
  python-cryptography
  python-pydantic
  python-pyparted
  python-textual
  systemd
  util-linux
  xfsprogs
  lvm2
  f2fs-tools
  ntfs-3g
)
makedepends=(
  ruff
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
  python-sphinx_rtd_theme
  python-pylint
  python-pylint-pydantic
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.sig::$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz.sig
)
sha512sums=('6fcf49f06a97a5062cc08fb42810eaeea677a69f32a81e040136949477e95cb2889dd461cfc8821d1552dce7d646983d06db6ffc4b9a47baa468353199e665b9'
            'SKIP')
b2sums=('adb2e47c1db2e5292ccdc989e0292fe9e70b67e9e599ed756751cb4130814690fc28a1f101917d615a9dd7e8e95139b7366784f2271bb0c8cd49503850bc0e23'
        'SKIP')
validpgpkeys=('8AA2213C8464C82D879C8127D4B58E897A929F2E') # torxed@archlinux.org

check() {
  cd $pkgname-$pkgver
#  pylint .
  ruff check
}

build() {
  cd $pkgname-$pkgver
  
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
