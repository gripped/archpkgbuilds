# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.6.1
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  e2fsprogs
  glibc
  kbd
  pciutils
  procps-ng
  python
  python-pyparted
  python-simple-term-menu
  systemd
  util-linux
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-v$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-v$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
)
sha512sums=('f0f460b9407fc57ce96ae9f60e353b8e37247c344c8bbf1d3baf4a902a8890d4a92e7016f00e404b8f667926ddaa12da08fe5d37b82fb5c805baad2eda380f4c'
            'SKIP')
b2sums=('92317dfa92ff4b145d7478f22c170557c06fe74f445ec415b2f2097ff5b62e35a1161773a94da56d563c81905f78d99991fd6363e931e73bd30e8226006a5566'
        'SKIP')
validpgpkeys=('8AA2213C8464C82D879C8127D4B58E897A929F2E') # torxed@archlinux.org

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
