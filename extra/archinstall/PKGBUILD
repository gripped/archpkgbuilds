# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=3.0.0
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL-3.0-only)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  dosfstools
  e2fsprogs
  glibc
  kbd
  pciutils
  procps-ng
  python
  python-pydantic
  python-pyparted
  systemd
  util-linux
  xfsprogs
  lvm2
  f2fs-tools
  ntfs-3g
  reiserfsprogs
)
makedepends=(
  ruff
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
  python-sphinx_rtd_theme
  python-pylint
  python-pylint-pydantic
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-v$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-v$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
)
sha512sums=('c0194fea6ad7fb1c4c9adb3cf365d584d464758c096fd5d9a472ed8dba0b57b4d831dceb9c2d60e2515e1507ea5faaed0a5a51121de066233591ac98a936dcce'
            'SKIP')
b2sums=('72fec314b3c1adb7b4f234c48f476d1f2e764c5813041d66ba0487ea3c8bfbf7afa5bec6af0c6aa4587e93d2fa742b525d39746cd09c13a3eb8a421863749003'
        'SKIP')
validpgpkeys=('8AA2213C8464C82D879C8127D4B58E897A929F2E') # torxed@archlinux.org

check() {
  cd $pkgname-$pkgver
  pylint .
  ruff check
}

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
