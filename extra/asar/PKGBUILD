# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Nicola Squartini <tensor5@gmail.com>

pkgname=asar
pkgver=3.3.1
pkgrel=2
pkgdesc='Creating Electron app packages'
arch=('any')
url='https://github.com/electron/asar'
license=('MIT')
depends=('nodejs')
makedepends=('npm' 'typescript')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/electron/asar/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('b7c14bf2ef429acdf2304271428082d7e35235a5d9e5c6b025bd82fa57da1e46bdb2b794b5bd984c9b1b211e9976296e3f278aed39146a1c0f011d7aa036a873')

build() {
  cd asar-$pkgver
  # electron binary is unnecessary for building this package.
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  npm i
  npm run build
}

package() {
  npm install --install-links -g --prefix "${pkgdir}/usr" ./asar-${pkgver}
  chown -R root:root "${pkgdir}"

  install -Dm644 "${pkgdir}/usr/lib/node_modules/@electron/asar/LICENSE.md" "${pkgdir}/usr/share/licenses/asar/LICENSE.md"
}
