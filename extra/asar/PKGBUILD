# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Nicola Squartini <tensor5@gmail.com>

pkgname=asar
pkgver=4.0.1
pkgrel=1
pkgdesc='Creating Electron app packages'
arch=('any')
url='https://github.com/electron/asar'
license=('MIT')
depends=('nodejs')
makedepends=('npm' 'typescript')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/electron/asar/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('69ec66715734fe02c7ff874bb78f89072f42a19f4f119a1d30dbc6e10b92f9337af464d0bb084d19fac8a0bfb7902bf53b41282eb29066bc47c0e7ddde04f688')

build() {
  cd asar-$pkgver
  # electron binary is unnecessary for building this package.
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  npm i --force
  npm run build
}

package() {
  npm install --install-links -g --prefix "${pkgdir}/usr" ./asar-${pkgver}
  chown -R root:root "${pkgdir}"

  install -Dm644 "${pkgdir}/usr/lib/node_modules/@electron/asar/LICENSE.md" "${pkgdir}/usr/share/licenses/asar/LICENSE.md"
}
