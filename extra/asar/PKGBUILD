# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Nicola Squartini <tensor5@gmail.com>

pkgname=asar
pkgver=3.4.0
pkgrel=1
pkgdesc='Creating Electron app packages'
arch=('any')
url='https://github.com/electron/asar'
license=('MIT')
depends=('nodejs')
makedepends=('npm' 'typescript')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/electron/asar/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('6d1ed1590ddbf359a194d63c0f4cd3ad64c4f9e0e13808624276d2fbe9bb7f9ef03f8c13c2d0c3ea07219b76e7819816158318d28c75ed65a957e41d0e61458c')

build() {
  cd asar-$pkgver
  # electron binary is unnecessary for building this package.
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  npm i
  npm run build
}

package() {
  npm install --install-links -g --prefix "${pkgdir}/usr" ./asar-${pkgver}
  chown -R root:root "${pkgdir}"

  install -Dm644 "${pkgdir}/usr/lib/node_modules/@electron/asar/LICENSE.md" "${pkgdir}/usr/share/licenses/asar/LICENSE.md"
}
