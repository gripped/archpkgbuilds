# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Nicola Squartini <tensor5@gmail.com>

pkgname=asar
pkgver=3.4.1
pkgrel=1
pkgdesc='Creating Electron app packages'
arch=('any')
url='https://github.com/electron/asar'
license=('MIT')
depends=('nodejs')
makedepends=('npm' 'typescript')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/electron/asar/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('4b0aa3cbd187042db24f1d0e1e599158f4e29aea2cb8d8dec32cae271d073017ba334ebb95c303b5679d20b0ca5c01b8dba93a2edde278453ccf56e25df1c321')

build() {
  cd asar-$pkgver
  # electron binary is unnecessary for building this package.
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  npm i
  npm run build
}

package() {
  npm install --install-links -g --prefix "${pkgdir}/usr" ./asar-${pkgver}
  chown -R root:root "${pkgdir}"

  install -Dm644 "${pkgdir}/usr/lib/node_modules/@electron/asar/LICENSE.md" "${pkgdir}/usr/share/licenses/asar/LICENSE.md"
}
