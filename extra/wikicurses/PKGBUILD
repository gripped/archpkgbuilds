# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Ian D, Scott <ian@perebruin.com>

pkgname=wikicurses
pkgver=1.4
pkgrel=12
pkgdesc='A simple curses interface for accessing Wikipedia'
arch=(any)
url='https://github.com/ids1024/wikicurses'
license=(MIT)
depends=(
  python
  python-urwid
  python-beautifulsoup4
  python-lxml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
backup=(etc/wikicurses.conf)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  "$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.asc"
  pyproject.toml.patch
)
sha512sums=('9473c01e58b70f4ea92b9615ed4f437139a2463a5b712209e2091425e9c5ee967f23f6703d7d2548162e94ccace99273cec41ed139a4d49c8e7063ff303ab063'
            'SKIP'
            'f538f068ee49a88995dc92927851e91b027cecca23f65525f58c2a70f7d25ad4b8258255a798ae8deac0f1128a148c044e6db1a4032b5778de2b58d9196379d5')
b2sums=('d9ca4feefbfae42dd1431ad2cc48a149186e1351676a826e70c878f0f04f50107962c80b4a6eaa4c9449df80321a1023049ff1c7a9527fa12ecc3054e2bfc45e'
        'SKIP'
        'f373674a68009fb2452ecb3cc55d92f8ab4aadea913c47713726102fb195ef428fd3170b30b4892392bf5a9bcff1bd53c795204f3dc033eabf40f4e91684a208')
validpgpkeys=('098BF569A360E234CE2B861A4924E10E199B5959') # Ian D. Scott <ian@perebruin.com>

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i "$srcdir/pyproject.toml.patch"
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # configuration
  install -vDm644 -t "$pkgdir/etc" wikicurses.conf

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" wikicurses.1
  install -vDm644 -t "$pkgdir/usr/share/man/man5" wikicurses.conf.5

  # zsh completions
  install -vDm644 -t "$pkgdir/usr/share/zsh/site-functions" _wikicurses

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
