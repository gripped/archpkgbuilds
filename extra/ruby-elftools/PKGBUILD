# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bet4 <0xbet4@gmail.com>

_gemname=elftools
pkgname=ruby-${_gemname}
pkgver=1.3.1
pkgrel=1
pkgdesc='ELF parser library implemented in pure Ruby'
url='https://github.com/david942j/rbelftools/'
arch=(any)
license=(MIT)
depends=(
  ruby
  ruby-bindata
)
makedepends=(
  ruby-bundler
  ruby-rake
  ruby-rdoc
)
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha512sums=('5a0e0528dff675cc1d6630c0af55ca0643d4a592ccb781b39cb8bc5edcefc1b12e3a0109803669c8634b970ed3493eea9745c54a8bde7d0cf59eba4e8adcbbba')
b2sums=('a60e22639bf678f023dd6abdddf165f9e4c61d0e6a810f5faf67261dda4de7f981e1b4b75fad4605aa8920ad3cfbd963dd0889a55cb8d05fb087c4e0ac3229fa')

prepare() {
  cd rbelftools-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd rbelftools-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd rbelftools-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so,man} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
