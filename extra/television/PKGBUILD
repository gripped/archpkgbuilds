# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=television
_pkgname=tv
pkgver=0.8.2
pkgrel=1
pkgdesc="A general purpose fuzzy finder for your terminal"
arch=('x86_64')
url="https://github.com/alexpasmantier/television"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('701353e97a52f0ef0bb99f3a838631abd74b0027e1ab9fc3def0a99f58978750d7ee14728ec6ba75cc99bf5242cc0853b3bb822cea2b843a315336457b1f262d')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
