# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=television
_pkgname=tv
pkgver=0.5.2
pkgrel=1
pkgdesc="A general purpose fuzzy finder for your terminal"
arch=('x86_64')
url="https://github.com/alexpasmantier/television"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('f0631d4a07d1ae559c7e2bec5cff7a4e9b45d5bda9b4a086354c228a62a682ebf86c95b8357bddaa7be0d369849f3434611d813ed730f3793d6f52a923bd6271')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
