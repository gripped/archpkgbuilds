# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

_pkgname=triton
pkgname=python-${_pkgname}
pkgver=3.5.0
pkgrel=1
pkgdesc="Language and compiler for writing highly efficient custom Deep-Learning primitives"
url="https://triton-lang.org/main/index.html"
license=(MIT)
arch=(x86_64)
depends=(
	glibc
	gcc-libs
	python
)
makedepends=(
	git
	cmake
	ninja
	pybind11
	python-setuptools
	python-build
	python-wheel
	python-installer
)
_git="https://github.com/triton-lang/triton"
_llvm="https://github.com/triton-lang/llvm-project"
# https://github.com/triton-lang/triton/blob/v3.5.0/cmake/llvm-hash.txt
_commit=7d5de3033187c8a3bb4d2e322f5462cdaf49808f
source=(
	"${pkgname}-${pkgver}.tar.gz::${_git}/archive/refs/tags/v${pkgver}.tar.gz"
	"${pkgname}-llvm-${pkgver}::git+${_llvm}#commit=${_commit}"
	)
sha256sums=('426cefe3752fcce2d3ea31af4252f3fa8358b112fe1e3e112434411935644259'
            '77b77b6eafa9ebc175922705ebb054067a42849ecc84f64f8a42d9d595a7683b')

prepare() {
	cd "${_pkgname}-${pkgver}"
	# We bring our own pybind11, cmake and ninja
	sed -i '/requires = \[/s/.*/requires = \["setuptools"\]/' pyproject.toml
	# Werror breaks build with current gcc
	sed -i 's/-Werror//' CMakeLists.txt
}

build() {
	local llvm_args=(
		-G Ninja
		-S "${pkgname}-llvm-${pkgver}/llvm"
		-B build-llvm
		-DCMAKE_BUILD_TYPE=Release
		-DLLVM_ENABLE_ASSERTIONS=ON
		-DLLVM_ENABLE_PROJECTS="mlir;llvm;lld"
		-DLLVM_TARGETS_TO_BUILD="X86;NVPTX;AMDGPU"
	)
	cmake "${llvm_args[@]}"
	cmake --build build-llvm

	cd "${_pkgname}-${pkgver}"
	export LLVM_BUILD_DIR="${srcdir}"/build-llvm
	LLVM_INCLUDE_DIRS="${LLVM_BUILD_DIR}"/include \
	LLVM_LIBRARY_DIR="${LLVM_BUILD_DIR}"/lib \
	LLVM_SYSPATH="${LLVM_BUILD_DIR}" \
	python -m build --wheel --no-isolation
}

package() {
	cd "${_pkgname}-${pkgver}"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
