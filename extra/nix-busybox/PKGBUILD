# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=nix-busybox
_pkgname="${pkgname#nix-}"
pkgver=1.36.1
pkgrel=2
pkgdesc='Slimmed down busybox for Nix sandbox builds'
arch=('x86_64')
url='https://www.busybox.net'
license=('GPL-2.0-only')
makedepends=(
  'ncurses'
  'musl'
  'kernel-headers-musl'
)
source=(
  "$url/downloads/$_pkgname-$pkgver.tar.bz2"{,.sig}
  'config'
)
sha512sums=('8c0c754c9ae04b5e6b23596283a7d3a4ef96225fe179f92d6f6a99c69c0caa95b1aa56c267f52d7c807f6cc69e1f0b7dd29a8ac624098f601738f8c0c57980d4'
            'SKIP'
            'fba9f58d38f22739f42a597c62325b4a813ec6b9fbc50493deb9712864d9387091f6415b25a19ca5bd9ef85a18a6c53145bdc8cd97fba59181af6c5febf820c0')
b2sums=('e515825cb3ab1c520e16b9c2512e9fc72947366a72a0466bff59b507fdffbc78fc9d16b44a26116175fc7a429d849ad944b1bc379d36c6d3a0eb20969997336e'
        'SKIP'
        '0ce60a047b2c4b88dc774dff85ca9a8559e3478e8c4deb0b768b46d3bba953c30e3b51bdcdf3bfca8941c0c9aff8ca9bb85dd8b4373985f40e8fc3a8230b9738')
validpgpkeys=('C9E9416F76E610DBD09D040F47B70C55ACC9965B') # Denis Vlasenko <vda.linux@googlemail.com>

build() {
  cd "$_pkgname-$pkgver"

  cp "$srcdir/config" .config

  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make CC=musl-gcc
}

package() {
  cd "$_pkgname-$pkgver"

  install -vDm755 -t "$pkgdir/usr/lib/nix" busybox
}
