# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=nix-busybox
_pkgname="${pkgname#nix-}"
pkgver=1.36.1
pkgrel=1
pkgdesc='Slimmed down busybox for Nix sandbox builds'
arch=('x86_64')
url='https://www.busybox.net'
license=('GPL-2.0-only')
makedepends=(
  'ncurses'
  'musl'
  'kernel-headers-musl'
)
source=(
  "$url/downloads/$_pkgname-$pkgver.tar.bz2"{,.sig}
  'config'
)
sha512sums=('8c0c754c9ae04b5e6b23596283a7d3a4ef96225fe179f92d6f6a99c69c0caa95b1aa56c267f52d7c807f6cc69e1f0b7dd29a8ac624098f601738f8c0c57980d4'
            'SKIP'
            '7f9b5269ebcfc94e3bf9f8b2a6225a7f81392e99e884bb5447dc741bdedc7fd02eeed3cd83efe7ca336e62c843146ce995e1ccf3bb87b6c4fd90558f59d00280')
b2sums=('e515825cb3ab1c520e16b9c2512e9fc72947366a72a0466bff59b507fdffbc78fc9d16b44a26116175fc7a429d849ad944b1bc379d36c6d3a0eb20969997336e'
        'SKIP'
        '7d4759036db05d7e0c06571e8fd24db488edf87a9b82a5382eee3fb43fc5318bbd75398e43c9b30c88b78630e910ee9a5316aaa824b5ab26f93fc1b3f834f07d')
validpgpkeys=('C9E9416F76E610DBD09D040F47B70C55ACC9965B') # Denis Vlasenko <vda.linux@googlemail.com>

build() {
  cd "$_pkgname-$pkgver"

  cp "$srcdir/config" .config

  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make CC=musl-gcc
}

package() {
  cd "$_pkgname-$pkgver"

  install -vDm755 -t "$pkgdir/usr/lib/nix" busybox
}
