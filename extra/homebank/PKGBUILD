# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=homebank
pkgver=5.9.5
pkgrel=1
pkgdesc="Free, easy, personal accounting for everyone"
arch=('x86_64')
url="https://www.gethomebank.org/"
license=('GPL-2.0-or-later')
depends=('desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'libofx'
         'librsvg' 'perl-xml-parser' 'shared-mime-info' 'libsoup3')
makedepends=('intltool')
changelog=$pkgname.changelog
source=(https://www.gethomebank.org/public/sources/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ff6c8b0f6da411133e29b848e91f48fdb88de40ad02cba20a8840930a3e7ed43')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
