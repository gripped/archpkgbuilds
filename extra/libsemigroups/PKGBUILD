# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=2.7.4
pkgrel=1
pkgdesc='C++ library for semigroups and monoids'
url='https://libsemigroups.github.io'
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(fmt
         gcc-libs
         glibc)
makedepends=(eigen
             git)
source=(git+https://github.com/libsemigroups/libsemigroups#tag=v$pkgver)
sha256sums=('4c680b41875d6676bf25c4080ad1dc6e5ee76ff992daebffd7ce323593075d25')

prepare() {
  cd $pkgname
  autoreconf -vif
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
              --disable-hpcombi \
              --enable-fmt \
              --with-external-fmt \
              --with-external-eigen
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
