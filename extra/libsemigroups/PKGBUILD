# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=2.7.2
pkgrel=2
pkgdesc='C++ library for semigroups and monoids'
url='https://libsemigroups.github.io'
arch=(x86_64)
license=(GPL3)
depends=(fmt
         gcc-libs
         glibc)
makedepends=(eigen)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('c89e020ce7857e9eee058eee47e385fdc49dce7f6c22f4304854637c023e44cd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --disable-hpcombi \
              --enable-fmt \
              --with-external-fmt \
              --with-external-eigen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
