# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=spdx
pkgname=haskell-spdx
pkgver=1.0.0.3
pkgrel=81
pkgdesc="SPDX license expression language, Extras"
url="https://github.com/phadej/spdx"
license=("BSD-3-Clause")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-base-compat' 'haskell-tasty' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        "$pkgname-cabal-3.8.patch::https://github.com/phadej/spdx/commit/545dc69f433225c837375fba4cbbdb7f9cc7b09b.patch")
sha512sums=('16c7e3126d77b9284fc3ef27dd94e551cae5496595df32b47e08722ff03288766770a0ef17a69f0a70ebdead08c54cb385471351c8a33b60a3578fe2303c19ac'
            '255bc9be638ed955f7aa56bec590e737207f2f513151e79eae97dfc7dcd8d08d1d920dd885cdd42f97258d18c7142d000adcdffe5b53a8557c6335d8f5e43390')

prepare() {
    cd $_hkgname-$pkgver
    patch -Np1 -i ../$pkgname-cabal-3.8.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
