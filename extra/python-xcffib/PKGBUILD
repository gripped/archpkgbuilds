# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgname=python-xcffib
pkgver=1.6.0
pkgrel=1
pkgdesc="xcffib is the XCB binding for python"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache-2.0')
depends=('python-cffi' 'libxcb')
makedepends=('git' 'python-setuptools' 'haskell-xcffib')
checkdepends=('python-pytest' 'xorg-server-xvfb' 'xorg-xeyes')
source=("git+https://github.com/tych0/xcffib.git#tag=v$pkgver")
sha512sums=('25fe693cd486da5395b75ccf918cce916c5b889123a6e43ab1538c630708356a16198c97a412bf8cd52447908e00a44e9dfe1cd81f505305d5caf59cc1563ed8')

prepare() {
  cd xcffib
  # Remove redundant six import
  git cherry-pick -n 96eb6d4a6fa0b1404bf4ba072d0bb8f9e58af306
}

build() {
  cd xcffib
  make xcffib GEN=xcffibgen CABAL=true
  python setup.py build
}

check() {
  cd xcffib
  xvfb-run pytest
}

package() {
  cd xcffib
  python setup.py install --root="$pkgdir/" --optimize=1
}
