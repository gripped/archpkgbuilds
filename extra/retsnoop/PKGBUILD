# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: David Vernet <void@manifault.com>

pkgname=retsnoop
pkgver=0.10
pkgrel=2
pkgdesc="A tool for investigating kernel error call stacks"
url="https://github.com/anakryiko/retsnoop"
arch=('x86_64')
license=('BSD-2-Clause')
depends=('glibc' 'libelf' 'zlib')
makedepends=('git' 'clang' 'cargo' 'llvm')
source=("git+${url}.git#tag=v${pkgver}"
        git+https://github.com/libbpf/libbpf.git
        git+https://github.com/libbpf/bpftool.git)
sha256sums=('0af94acab7a7ab8967e4ac8c4598b1c1d267b0f024481ec209d342d9d556f1f0'
            'SKIP'
            'SKIP')

prepare() {
	cd "${pkgname}"
	git submodule init
	git config submodule.libbpf.url "${srcdir}/libbpf"
	git config submodule.bpftool.url "${srcdir}/bpftool"
	git -c protocol.file.allow=always submodule update
}

build() {
	cd "${pkgname}"
	make -C src
}

package() {
	cd "${pkgname}"
	install -Dm 755 "src/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
