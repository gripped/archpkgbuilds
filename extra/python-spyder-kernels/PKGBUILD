# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=spyder-kernels
pkgname=python-${_pkg}
pkgver=3.0.5
pkgrel=1
pkgdesc="Jupyter Kernels for the Spyder console"
arch=(any)
url="https://github.com/spyder-ide/spyder-kernels/"
license=(MIT)
depends=(
  cython
  ipython
  python
  python-cloudpickle
  python-dateutil
  python-ipykernel
  python-jupyter-client
  python-matplotlib
  python-matplotlib-inline
  python-numpy
  python-pandas
  python-pillow
  python-pygments
  python-pyzmq
  python-traitlets
  python-wurlitzer
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-dask
  python-distributed
  python-django
  python-flaky
  python-h5py
  python-numpy
  python-pillow
  python-pydicom
  python-pydicom
  python-pytest
  python-scipy
  python-xarray
)
optdepends=('python-pydicom: DICOM format import')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('cfe9483893f03a751a9233d7477ed69596d9c7b0b16fc78502c87f7ca3ec712c')

build() {
  cd ${_pkg}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkg}-${pkgver}
  pytest -vv --color=yes
}

package() {
  cd ${_pkg}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.txt
}
