# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=spyder-kernels
pkgname=python-${_pkg}
pkgver=3.0.0
pkgrel=2
pkgdesc="Jupyter Kernels for the Spyder console"
arch=(any)
url="https://github.com/spyder-ide/spyder-kernels/"
license=(MIT)
makedepends=(python python-setuptools)
depends=(
    cython
    ipython
    python-cloudpickle
    python-ipykernel
    python-jupyter_client
    python-matplotlib
    python-pandas
    python-pyzmq
    python-wurlitzer
)
checkdepends=(
    python-dask
    python-pydicom
    python-distributed
    python-django
    python-flaky
    python-h5py
    python-numpy
    python-pillow
    python-pytest
    python-scipy
    python-xarray
)
optdepends=('python-pydicom: DICOM format import')
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2e5c7be41712ce17b1c0e0f1259326c45ee22ddfc796a3c1fb8e9ba0ad13ee89')

prepare() {
  cd ${_pkg}-${pkgver}
  sed -i 's|ipykernel>=6.16.1,<7|ipykernel>=6.16.1|' setup.py
  sed -i 's|ipython>=7.31.1,<9|ipython>=7.31.1|' setup.py
  sed -i 's|jupyter-client>=7.4.9,<8|jupyter-client>=7.4.9|' setup.py
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest -vv --color=yes spyder_kernels \
    -k 'not test_load_dicom_files' # required many optional dependencies
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
