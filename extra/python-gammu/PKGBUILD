# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgname=python-gammu
pkgver=3.2.4
pkgrel=8
pkgdesc="Python bindings for Gammu library"
arch=('x86_64')
url="https://wammu.eu/python-gammu/"
license=('GPL-2.0-or-later')
depends=(
  'gammu'
  'glibc'
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('libdbi-drivers')
source=("git+https://github.com/gammu/python-gammu.git?signed#tag=$pkgver")
sha512sums=('3afda6918b662fccab5d2bea0d33942b1e029c21a18b39ae8a1495438d2d5dc915872bd0f4f646a48b3f34a4069300b7ffefa010db7631fdc6d80edc3bdd8d16')
validpgpkeys=('63CB1DF1EF12CF2AC0EE5A329C27B31342B7511D') # Michal Čihař <michal@cihar.com>

build() {
  cd $pkgname
  python -m build --no-isolation
}

check() {
  cd $pkgname
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  # The test suite cannot run in the project root as the uncompiled package
  # there will take precedence over the compiled package installed in
  # tmp_install, regardless of what PYTHONPATH is set to. To work around this,
  # copy the tests to the tmp_install directory and run them from there.
  cp -r test "$PWD/tmp_install/$site_packages"
  # Tests can be flaky on slower hardware due to timing.
  python -m unittest discover -vs "$PWD/tmp_install/$site_packages"
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
