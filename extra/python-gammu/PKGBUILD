# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgname=python-gammu
pkgver=3.2.5
pkgrel=1
pkgdesc="Python bindings for Gammu library"
arch=('x86_64')
url="https://wammu.eu/python-gammu/"
license=('GPL-2.0-or-later')
depends=(
  'gammu'
  'glibc'
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'libdbi-drivers'
)
source=("git+https://github.com/gammu/python-gammu.git?signed#tag=$pkgver")
b2sums=('bf890ce85da5bffbfbd1876f5a3e63f12479bf556612fcf9e03094c9e24bb85910cc871dbc66902f128d30b87e9ead74fffd40313000b1c5237616a14d297e52')
validpgpkeys=('63CB1DF1EF12CF2AC0EE5A329C27B31342B7511D') # Michal Čihař <michal@cihar.com>

build() {
  cd $pkgname
  python -m build --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -Pm pytest
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
