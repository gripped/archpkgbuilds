# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgname=python-gammu
pkgver=3.2.6
pkgrel=1
pkgdesc="Python bindings for Gammu library"
arch=('x86_64')
url="https://wammu.eu/python-gammu/"
license=('GPL-2.0-or-later')
depends=(
  'gammu'
  'glibc'
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'libdbi-drivers'
)
source=("git+https://github.com/gammu/python-gammu.git?signed#tag=$pkgver")
b2sums=('8a15514003ea775ce85f0126aaf1f47aa89b785343382731349bc59bdfdf422c042b3cc2271c2d8015fd36375dea44d091ff80eb21474360b310edb3ea99c7ec')
validpgpkeys=('63CB1DF1EF12CF2AC0EE5A329C27B31342B7511D') # Michal Čihař <michal@cihar.com>

build() {
  cd $pkgname
  export CFLAGS+=" -Wno-error=return-mismatch"
  python -m build --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -Pm pytest
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
