# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Muflone http://www.muflone.com/contacts/english/

pkgname=python-pysmi
pkgver=1.5.11
pkgrel=2
pkgdesc='SNMP/SMI MIB parsing and conversion library designed to turn ASN.1 MIBs into various formats'
arch=('any')
url='https://github.com/lextudio/pysmi'
license=('BSD-2-Clause')
depends=(
  'python'
  'python-jinja'
  'python-ply'
  'python-requests'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-poetry-core'
  'python-wheel'
)
checkdepends=(
  'python-pysnmp'
  'python-pytest'
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2d455710a36e9d32091f8fb743e279de45c1b5502bd388f47622187383c357e1900bb332c44d0ca0c1fb1de13f37e3e246cfb805e12edc3047f2e3525265bfcf')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  pytest \
    --deselect tests/test_typedeclaration_smiv2_pysnmp.py::TypeDeclarationFixedLengthTestCase \
    --deselect tests/test_typedeclaration_smiv2_pysnmp.py::TypeDeclarationTestCase
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr -t "$pkgdir/usr/share/doc/$pkgname" examples

  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.rst
}

# vim: ts=2 sw=2 et:
