# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florijan Hamzic <florijanh at gmail dot com>

pkgname=python-weasyprint
pkgver=61.2
pkgrel=3
pkgdesc='Utility to render HTML and CSS to PDF'
arch=('any')
url='https://weasyprint.org/'
license=('BSD')
depends=(
  'pango'
  'python'
  'python-brotli'
  'python-cffi'
  'python-cssselect2'
  'python-fonttools'
  'python-html5lib'
  'python-pillow'
  'python-pydyf'
  'python-pyphen'
  'python-tinycss2'
  'python-zopfli'
  'ttf-font'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-flit-core'
)
checkdepends=(
  'python-pytest'
  'ghostscript'
  'ttf-dejavu'
)
source=("$pkgname::git+https://github.com/Kozea/WeasyPrint.git#tag=v$pkgver")
sha256sums=('80d52ac97afb02393c5ab363eed543f9e2f8c1f77cc446dd6bc544bd2cbdbe3c')

prepare() {
  cd "$pkgname"

  # thou shalt not lint coding style in integration tests
  sed -i '/^addopts/d' pyproject.toml
}

build(){
  cd "$pkgname"

  python \
    -m build \
    --wheel \
    --no-isolation
}

check() {
  cd "$pkgname"

  # skip failing tests
  python -m pytest \
    --deselect tests/draw/test_gradient.py::test_linear_gradients_5 \
    --deselect tests/draw/test_gradient.py::test_linear_gradients_12
}

package() {
  cd "$pkgname"

  python \
    -m installer \
    --destdir="$pkgdir" \
    dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
