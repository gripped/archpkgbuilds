# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florijan Hamzic <florijanh at gmail dot com>

pkgname=python-weasyprint
pkgver=68.1
pkgrel=1
pkgdesc='Utility to render HTML and CSS to PDF'
arch=(any)
url='https://weasyprint.org/'
license=(BSD-3-Clause)
depends=(
  pango
  python
  python-brotli
  python-cffi
  python-cssselect2
  python-fonttools
  python-html5lib
  python-pillow
  python-pydyf
  python-pyphen
  python-tinycss2
  python-tinyhtml5
  python-zopfli
  ttf-font
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-flit-core
)
checkdepends=(
  python-pytest
  ghostscript
  ttf-dejavu
)
source=("$pkgname::git+https://github.com/Kozea/WeasyPrint.git#tag=v$pkgver")
sha512sums=('3acb07fd38b22d145067afe4dadd2144685205294530b3d13dcfb9c62787900835334b793313dd340068e1fb9e6f7745356822b82baef354878c18a8f1453f68')
b2sums=('bd5676c12281be6a6282980a1d852eccf798758a9a949e8c7335933d409812be7e484348f41f62a3d8c80c03521d12e1e66b0987b15fee67ade130fbf81f6866')

prepare() {
  cd "$pkgname"

  # thou shalt not lint coding style in integration tests
  sed -i '/^addopts/d' pyproject.toml
}

build(){
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  local deselected=(
    tests/draw/test_gradient.py::test_linear_gradients_5
    tests/draw/test_gradient.py::test_linear_gradients_12
    tests/draw/test_column.py::test_column_rule_normal
    tests/draw/test_transform.py::test_2d_transform_1
    tests/draw/test_transform.py::test_2d_transform_2
    tests/draw/test_transform.py::test_2d_transform_3
    tests/draw/test_transform.py::test_2d_transform_4
    tests/draw/test_visibility.py::test_visibility_1
    tests/draw/test_visibility.py::test_visibility_3
    tests/draw/test_visibility.py::test_visibility_4
  )

  pytest ${deselected[@]/#/--deselect }
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
