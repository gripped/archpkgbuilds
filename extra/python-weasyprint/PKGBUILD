# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florijan Hamzic <florijanh at gmail dot com>

pkgname=python-weasyprint
pkgver=68.0
pkgrel=1
pkgdesc='Utility to render HTML and CSS to PDF'
arch=(any)
url='https://weasyprint.org/'
license=(BSD-3-Clause)
depends=(
  pango
  python
  python-brotli
  python-cffi
  python-cssselect2
  python-fonttools
  python-html5lib
  python-pillow
  python-pydyf
  python-pyphen
  python-tinycss2
  python-tinyhtml5
  python-zopfli
  ttf-font
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-flit-core
)
checkdepends=(
  python-pytest
  ghostscript
  ttf-dejavu
)
source=("$pkgname::git+https://github.com/Kozea/WeasyPrint.git#tag=v$pkgver")
sha512sums=('88f737c41ff556e326b437a2439885b04b6b9010e713a9b2c9022e0311fdf7b4055af39446d1d72736348de864d0f7809e07a4b2cd3c81a466c90c389f9cd2b7')
b2sums=('457156a02a6de93c035ca4949705308d130d6e0c13dd9f45e8ea15f58b5be5d2ffff2147d01fd5d1e8b5a687cf17c4c1d73be80157534717e1da2b76fc138411')

prepare() {
  cd "$pkgname"

  # thou shalt not lint coding style in integration tests
  sed -i '/^addopts/d' pyproject.toml
}

build(){
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  local deselected=(
    tests/draw/test_gradient.py::test_linear_gradients_5
    tests/draw/test_gradient.py::test_linear_gradients_12
    tests/draw/test_column.py::test_column_rule_normal
    tests/draw/test_transform.py::test_2d_transform_1
    tests/draw/test_transform.py::test_2d_transform_2
    tests/draw/test_transform.py::test_2d_transform_3
    tests/draw/test_transform.py::test_2d_transform_4
    tests/draw/test_visibility.py::test_visibility_1
    tests/draw/test_visibility.py::test_visibility_3
    tests/draw/test_visibility.py::test_visibility_4
  )

  pytest ${deselected[@]/#/--deselect }
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
