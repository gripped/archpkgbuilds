# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florijan Hamzic <florijanh at gmail dot com>

pkgname=python-weasyprint
pkgver=65.1
pkgrel=1
pkgdesc='Utility to render HTML and CSS to PDF'
arch=(any)
url='https://weasyprint.org/'
license=(BSD-3-Clause)
depends=(
  pango
  python
  python-brotli
  python-cffi
  python-cssselect2
  python-fonttools
  python-html5lib
  python-pillow
  python-pydyf
  python-pyphen
  python-tinycss2
  python-tinyhtml5
  python-zopfli
  ttf-font
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-flit-core
)
checkdepends=(
  python-pytest
  ghostscript
  ttf-dejavu
)
source=("$pkgname::git+https://github.com/Kozea/WeasyPrint.git#tag=v$pkgver")
sha512sums=('5b08764528db4bfb66579d0ddc8b9b3429d7746559b0abb8f549a2ec3affdfcc419e8277b38dc82fb6eccb6933f6d5581c2ed27cc49af3ef7966855f10a4468a')
b2sums=('3b42d30499f6cf4880a2863fc56b343b9a33b3103874ec5db8a1eaa546dc521df4e53783f1b9e423f133d7ad51901a6fd2f917df5f9978b86e63b681f91dcce8')

prepare() {
  cd "$pkgname"

  # thou shalt not lint coding style in integration tests
  sed -i '/^addopts/d' pyproject.toml
}

build(){
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  local deselected=(
    tests/draw/test_gradient.py::test_linear_gradients_5
    tests/draw/test_gradient.py::test_linear_gradients_12
  )

  pytest ${deselected[@]/#/--deselect }
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
