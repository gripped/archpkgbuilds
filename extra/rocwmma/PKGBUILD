# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot com>

pkgname=rocwmma
pkgver=6.4.1
pkgrel=1
pkgdesc='Library for accelerating mixed precision matrix multiplication'
arch=('x86_64')
url='https://rocm.docs.amd.com/projects/rocWMMA/en/latest/'
license=('MIT')
depends=('rocm-core' 'hip-runtime-amd' 'rocblas' 'openmp')
makedepends=('rocm-cmake' 'doxygen')
_git='https://github.com/ROCmSoftwarePlatform/rocWMMA'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz")
sha256sums=('888e9794adff06ca1be811d80018e761b9a9cf84cb88dec9e51bc3a6db7a359a')
_dirname="$(basename "$_git")-$(basename "${source[0]}" .tar.gz)"

build() {
    local cmake_args=(
        -Wno-dev
        -B build
        -S "$_dirname"
        -D CMAKE_POLICY_VERSION_MINIMUM=3.5
        -D CMAKE_BUILD_TYPE=Release
        -D CMAKE_CXX_COMPILER=amdclang++
        -D CMAKE_C_COMPILER=amdclang
        -D CMAKE_INSTALL_PREFIX=/opt/rocm
        -D ROCWMMA_BUILD_TESTS=OFF
        -D ROCWMMA_BUILD_SAMPLES=OFF
    )
    cmake "${cmake_args[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    install -Dm644 "$_dirname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
