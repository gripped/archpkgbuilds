# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=rocwmma
pkgver=7.2.0
pkgrel=1
pkgdesc='Library for accelerating mixed precision matrix multiplication'
arch=('x86_64')
url='https://rocm.docs.amd.com/projects/rocWMMA/en/latest/'
license=('MIT')
depends=(
    'hip-runtime-amd'
    'openmp'
    'rocblas'
    'rocm-core'
)
makedepends=(
    'doxygen'
    'rocm-cmake'
    'rocm-llvm'
    'rocm-toolchain'
)
_git='https://github.com/ROCmSoftwarePlatform/rocWMMA'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz")
sha256sums=('12f056df671c78dad38c38632f7ddf44163387932a092d59bed433b0194da995')
_dirname="$(basename "$_git")-$(basename "${source[0]}" .tar.gz)"
options=(!lto)

build() {
    local cmake_args=(
        -Wno-dev
        -B build
        -S "$_dirname"
        -D CMAKE_POLICY_VERSION_MINIMUM=3.5
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_CXX_COMPILER=amdclang++
        -D CMAKE_INSTALL_PREFIX=/opt/rocm
        -D ROCWMMA_BUILD_TESTS=OFF
        -D ROCWMMA_BUILD_SAMPLES=OFF
        -D ROCM_SYMLINK_LIBS=OFF
        -D HIP_PLATFORM=amd
        -D CMAKE_CXX_FLAGS="${CXXFLAGS} -fcf-protection=none"
        -D GPU_TARGETS=$(rocm-supported-gfx)
    )
    cmake "${cmake_args[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    install -Dm644 "$_dirname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
