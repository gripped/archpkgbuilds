# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=lazyjj
pkgver=0.3.1
pkgrel=1
pkgdesc="TUI for Jujutsu/jj"
arch=('x86_64')
url="https://github.com/Cretezy/lazyjj"
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
checkdepends=('jujutsu')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('4308869b89686341a878177882a4ba127c60e1a5c2dd4d52caa9af66bed43ae113f8df0b63d7fadb706db8e433814e0fe898fada49219c447ad342ea1e3c4d63')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
