# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=lazyjj
pkgver=0.4.0
pkgrel=1
pkgdesc="TUI for Jujutsu/jj"
arch=('x86_64')
url="https://github.com/Cretezy/lazyjj"
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
checkdepends=('jujutsu')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('91f336d2a5c4546de8565ec0343f51ba5bf05ef05d1aa8b5f01807eff8f4eb910b5c35b706cbb4d35fc5455a498d879df04b95e535e263c63dae8e9643332632')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" # --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
