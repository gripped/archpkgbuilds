# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=2.5
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2 glibc libusb)
makedepends=(git meson)
source=("git+$url#tag=v${pkgver}"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('5152299aacf3bb9085c096ac1a2ca9fe91756823e0939fd5d43eb76d1238679e4c4517ea0635e475ab0512002b6561281759048985edc3e59d4f5802b04422b8'
        'a4c8aaab320e07d0c7086238a2f1e842df40ba8a85b7ed3f519f2e6f8511f6d513c80e6693a1db6c69f2c15e71e7d08db3d37ebefa47633efdaae2e706809e1c')

build() {
  mkdir -p build
  arch-meson build $pkgname \
    --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
