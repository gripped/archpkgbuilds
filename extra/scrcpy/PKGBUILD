# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=3.2
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2 glibc libusb)
makedepends=(git meson)
source=("git+$url#tag=v$pkgver"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('b1c242393c6a885c816ea957aa8a59273c7eacf4017be04f51f1fdf8353359f5444b416e02da4e80c54758c955a3ada6f323d675c1c7d31674104f9d4ef599a0'
        'bf65e771a63ef786543f421ad64fddd12cd3c5ab35c49595a0efb58960235fad0989e93c218526d067d49af4d4e1e8a52d68e7936ff854d4ab7239506cb0f958')

build() {
  mkdir -p build
  arch-meson build $pkgname --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
