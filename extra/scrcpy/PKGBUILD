# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=3.3.3
pkgrel=4
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2 glibc libusb)
makedepends=(git meson)
source=("git+$url#tag=v$pkgver"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('fda549bd41c0b8ad9d61f818f9632339044682097963aa3aa2857ee1ce211aea18c0013bb8c4d2ebd71108da5bda31fed47adc992af3a8611225128e0da78cad'
        '662ce33e8957e5db070b5f7c71f83c9dd49d65ca15e105837d3e806b13b351acdeb58741562302d0090b502ebf11ef5457a750a2f7fb041318ce90c411e2457c')

build() {
  mkdir -p build
  arch-meson build $pkgname --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
