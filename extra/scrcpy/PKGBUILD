# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=3.3.1
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2 glibc libusb)
makedepends=(git meson)
source=("git+$url#tag=v$pkgver"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('fba1fb975bac1447bedcf41c626aca04f0a9f7369b1316364358886ce6f981aedf0254bd4fc1ee71d0cf93312c978fa4cfe6edb96b5eca041feb618f22190141'
        'c000aaa1d3f54494bb98c4a90a2086c0af8370381f29b4e6f7efc01f27afccba5bc0116b2394fa53fe1678bbf393accd79f997391c28993f8a54b30374bb5964')

build() {
  mkdir -p build
  arch-meson build $pkgname --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
