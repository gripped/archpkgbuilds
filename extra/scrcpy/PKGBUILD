# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=3.3
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2 glibc libusb)
makedepends=(git meson)
source=("git+$url#tag=v$pkgver"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('8d00904f6c2f90697738a78c96ba1c452937c3e18ec44370e4007288fe22c46dfa5f7cb26e6dd1d5b1508277e8188cc1919a13a16851ef92132771b929313847'
        '202969bc7f2a45a907b93033fdd57498c403bb8ccf7a31e9bcd33ba06fa09a8e14c898f828f1ee2e58a01d50db0ac11c86705a6afeb644cb3e7cfc7b6bddbed3')

build() {
  mkdir -p build
  arch-meson build $pkgname --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
