# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=2.4
pkgrel=3
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2)
makedepends=(git meson)
source=("git+$url#tag=v${pkgver}"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('2e25a17d90f51fa1c9cc51f9695109e4ef83f996017c63ac396db1c01803923daa201ae370fbf5a4ce0e086c4d1ed7dcc1e355c65416347d7953fa4e37da9038'
        'c34d75bb34dcdaf27111366575f46d8e1d54c3d9a0eca8cb569367204a3e51e4d5b05f8a4d2de9868fe48ca4e3d323c583a99da86132e533dbede78314ca447a')

build() {
  mkdir -p build
  arch-meson build $pkgname \
    --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
