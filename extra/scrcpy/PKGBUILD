# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=3.3.2
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2 glibc libusb)
makedepends=(git meson)
source=("git+$url#tag=v$pkgver"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('64bd5d14e39937d07f230813b6b92df5a73829641af3c52becedbe2394760f85d62af329af2e46bae805e8e5330e5e99cd94c81a147663306eb20ccc31c2e9e5'
        'e0a5880fa99fb5af2a48833a93ed20f108442b49937a71ec21095afab90ff12030fcdc24cca260c6c20da2a72c987e8984cf3fa1733a4ec8c3e0c5d8c76a2f51')

build() {
  mkdir -p build
  arch-meson build $pkgname --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
