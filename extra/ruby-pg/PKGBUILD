# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Niels Abspoel <aboe76 (at) Gmail.com>

_gemname=pg
pkgname=ruby-$_gemname
pkgver=1.6.1
pkgrel=1
pkgdesc='Ruby interface to the PostgreSQL RDBMS'
url='https://github.com/ged/ruby-pg'
arch=('x86_64')
license=('BSD-2-Clause')
depends=('glibc' 'ruby' 'ruby-rspec-core' 'postgresql-libs')
makedepends=('git' 'ruby-rdoc')
options=('!emptydirs')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('6fa6059101e02ee50e0a1366930d71a467030b0068763bd0e0f73b4621cbfc5f0e4f23c3d58a3ad9662f6d07a30b6506b06b7e8be72ce3310d1daeb4b9724edf')
b2sums=('0b9cdf6eb0712d8886280c42e3a32e0ba41308570ef5aca863e554968b8bb3bc81fc99e6e37fc826b943146932989785960785783106cee6f08d18a6fb9be991')

prepare() {
  cd "$pkgname"

  sed 's|"ChangeLog".freeze, ||' -i ${_gemname}.gemspec
  sed 's|"lib/pg/deprecated_constants.rb".freeze, ||' -i ${_gemname}.gemspec
  sed "s|^  s.version = .*$|  s.version = '${pkgver}'|" -i ${_gemname}.gemspec
}

build() {
  cd "$pkgname"

  gem build "$_gemname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-document \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"

  # delete unnecessary files & folders
  cd "$pkgdir/$_gemdir"
  find . -type f \
    -name "page-Makefile.ri" -delete \
    -o -name 'gem_make.out' -delete \
    -o -name 'mkmf.log' -delete
  rm -rf cache
  cd "gems/$_gemname-$pkgver"
  find . -type f -name ".*" -delete
  rm -vrf .github certs sample misc spec ext POSTGRES Manifest.txt Rakefile* Gemfile "$_gemname.gemspec"

  # generate reproducible documentation
  install -vd "$pkgdir/$_gemdir/doc/$_gemname-$pkgver"
  cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  rdoc \
    --format ri \
    --output "$pkgdir$_gemdir/doc/$_gemname-$pkgver/ri" \
    ./lib
  # delete unnecessary rdoc metadata file
  rm -f "$pkgdir$_gemdir/doc/$_gemname-$pkgver/ri/created.rid"

  # move documentation
  install -vd "$pkgdir/usr/share/doc/$pkgname"
  mv *.{md,rdoc} "$pkgdir/usr/share/doc/$pkgname"

  # move license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  mv LICENSE BSDL "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
