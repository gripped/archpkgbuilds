# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=pika-backup
pkgver=0.7.5
pkgrel=1
pkgdesc='Easy to use backup tool to keep your data safe'
arch=(x86_64)
url='https://apps.gnome.org/PikaBackup/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  borg
  gcc-libs
  glib2
  glibc
  gtk4
  gvfs
  hicolor-icon-theme
  libadwaita
  openssh
  pango
  python-pyfuse3
)
makedepends=(
  appstream
  git
  itstool
  meson
  rust
)
checkdepends=(xorg-server-xvfb)
source=("git+https://gitlab.gnome.org/World/pika-backup.git#tag=v$pkgver")
b2sums=(34170b9c1d46da73f0e6339acdc3e95ff03e53183eedbf83930fc9afa214f7d2461565225cf4d858a5a26b783ceb0b86985dc0c5ec864e9171967355331ad5b4)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/pika-backup/-/merge_requests/310
  git cherry-pick -n f0f7603dc60c6519dfea2027ab357d72a97e1478
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
