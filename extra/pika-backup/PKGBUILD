# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=pika-backup
pkgver=0.7.4
pkgrel=1
pkgdesc='Easy to use backup tool to keep your data safe'
arch=(x86_64)
url='https://apps.gnome.org/PikaBackup/'
license=(GPL-3.0-only)
depends=(
  borg
  gcc-libs
  glib2
  glibc
  gtk4
  gvfs
  hicolor-icon-theme
  libadwaita
  openssh
  pango
  python-pyfuse3
)
makedepends=(
  appstream
  git
  itstool
  meson
  rust
)
checkdepends=(xorg-server-xvfb)
source=(
  "git+https://gitlab.gnome.org/World/pika-backup.git#tag=v$pkgver"
  pika-backup-outoftree.patch
  pika-backup-window-icon.patch
)
b2sums=(
  ac5e583ef98818d5ab265b64b3660c4b24582c8c3a6a00e41c0979403509f2b6a0cc6465fda487d4c3637f5e56746e635e9ab1af97c384e980e1a54dbb57ec5d
  ed2f520d0aa463826176e7e7edeb0e94f6f03a62d84f6e845c378baf422f6e5d5577eb8ec5f0ef7d8633d2a8e2344e70ef5a0a7345ac298277dd8aa3add3608b
  ae856a2cfe627b9bc7b4b8a0643a134543d4bb52265a8f48fb67a4b41de8a3765d5b73734916cf46ab19ff81c87f6109aa2ea52bfcfdc2cc99e1e78028653372
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/pika-backup/-/merge_requests/310
  git apply -3 ../pika-backup-outoftree.patch

  # https://gitlab.gnome.org/World/pika-backup/-/merge_requests/311
  git apply -3 ../pika-backup-window-icon.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
