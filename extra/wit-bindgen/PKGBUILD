# Maintainer: Frederik Schwan <freswa at archlinux.org>

pkgname=wit-bindgen
pkgver=0.53.0
pkgrel=1
pkgdesc="A language binding generator for WebAssembly interface types "
url="https://github.com/bytecodealliance/wit-bindgen"
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(
  git
)
options=(!lto)
source=("git+https://github.com/bytecodealliance/wit-bindgen.git#tag=v${pkgver}")
sha256sums=('dac0098fc18fcccdca67cdfcc90f8517ca675b8f19146f7b04d85d9d41dcd53c')
b2sums=('f8805717fe6f552ec29c0971370724868cc21dfe86cb47bdeffd53a61c81fb2cf02f16277ccbfd86ce93af284a6971685972e7cc98180bea4330b3d96282ada9')

prepare() {
  cd ${pkgname}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}
  cargo build --release --locked
}

check() {
  cd ${pkgname}
  cargo test --release --locked
}

package() {
  cd ${pkgname}
  install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
