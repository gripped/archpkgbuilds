# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=planarity
pkgver=4.0.0.0
pkgrel=1
pkgdesc='A library for implementing graph algorithms'
arch=(x86_64)
url='https://github.com/graph-algorithms/edge-addition-planarity-suite'
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(git)
source=(git+https://github.com/graph-algorithms/edge-addition-planarity-suite#tag=Version_$pkgver)
sha256sums=('295c521f9ad62e415b71dc2ae0bee74f3552b686e24915483bbc49b283b662a6')

prepare() {
  cd edge-addition-planarity-suite                
  autoreconf -vi
}

build() {
  cd edge-addition-planarity-suite
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd edge-addition-planarity-suite
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
