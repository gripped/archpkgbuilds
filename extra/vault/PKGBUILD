# Maintainer: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: Sebastian Rakel <sebastian@devunit.eu>
# Contributor: Andrew Tyler <assimilat@gmail.com>

pkgname=vault
pkgdesc='A tool for managing secrets'
pkgver=1.21.3
pkgrel=1
url="https://vaultproject.io/"
license=('BUSL-1.1')
arch=('x86_64')
depends=('glibc')
makedepends=('go' 'git' 'yarn' 'nodejs-lts-iron' 'npm')
install=vault.install
backup=('etc/vault.hcl' 'etc/default/vault')
source=("git+https://github.com/hashicorp/vault#tag=v${pkgver}"
  'vault.sysusers'
  'vault.tmpfiles'
  'vault.hcl')
sha512sums=('94b976c06d627b18abd8f0917370b58af7c2e139eb2f88a7014df22a9f9175f58fbb08236bd08dfdeb480d78aeb0feb42fe675fb4de8da42cd88c9b4d65a93b6'
            '598d673a7d86cd4948855adda005c0d4dd4fc494173221306b1c0eebc359bb881b5272f3166ea46ca23b54983ebfba2b1799c6f827ed8dbaa864f03cf06acb00'
            'db327aae6f821ee1ea608abdb3fc82aeeae72ce873d78ada44461644add32afd6c0197019427734498bc28ae187b6f741a02196e40a620caab597e5eef32ca7a'
            '75d654ec4eadfe983f57951d470fff8b9eb953b42c08e7b6b3a1baaa0721fd7a9d5be37480b0e4f4fd8518f375348bdd8394848f0fb27cb1d425279acb67f693')

pkgver() {
  cd vault
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd vault
  sed -i 's|/etc/vault.d/vault.hcl|/etc/vault.hcl|g' .release/linux/package/usr/lib/systemd/system/vault.service
  sed -i 's|/etc/vault.d/vault.env|/etc/default/vault|g' .release/linux/package/usr/lib/systemd/system/vault.service
}

build() {
  mkdir -p "${srcdir}/vault/http/web_ui"
  cd "${srcdir}/vault/ui"
  yarn install
  npm rebuild node-sass
  yarn run build

  cd "${srcdir}/vault"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  mkdir -p bin
  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode=external -compressdwarf=false -X github.com/hashicorp/vault/sdk/version.Version=${pkgver} -X github.com/hashicorp/vault/sdk/version.GitCommit=${_vault_commit} -X github.com/hashicorp/vault/sdk/version.BuildDate=$(date -u '+%Y-%m-%dT%H:%M:%SZ')" \
    -tags ui \
    -o dist/ \
    .
}

package() {
  install -Dm644 "${srcdir}/vault.hcl" "${pkgdir}/etc/vault.hcl"
  install -Dm644 "${srcdir}/vault.sysusers" "${pkgdir}/usr/lib/sysusers.d/vault.conf"
  install -Dm644 "${srcdir}/vault.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/vault.conf"

  cd "${srcdir}/vault"

  install -Dm644 ".release/linux/package/etc/vault.d/vault.env" "${pkgdir}/etc/default/vault"
  install -Dm644 ".release/linux/package/usr/lib/systemd/system/vault.service" "${pkgdir}/usr/lib/systemd/system/vault.service"

  install -Dm755 "dist/vault" "${pkgdir}/usr/bin/vault"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for file in README.md CHANGELOG.md CONTRIBUTING.md; do
    install -Dm644 "${file}" "${pkgdir}/usr/share/doc/${pkgname}/${file}"
  done
}
