# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Ã©clairevoyant
# Contributor: nullgemm <nullgemm@mailbox.org>

pkgname=ly
pkgver=1.1.2
pkgrel=1
pkgdesc="TUI display manager"
arch=(x86_64)
url="https://codeberg.org/AnErrupTion/ly"
license=('WTFPL')
depends=(pam glibc libxcb)
makedepends=(git zig)
optdepends=('xorg-xauth: for X server sessions'
            'libxcb: for X server sessions')
backup=(
    etc/$pkgname/{config.ini,setup.sh}
    etc/pam.d/ly
)
source=("git+$url.git#tag=v${pkgver}")
b2sums=('20464d125d8528f82676839ed21ea2d9b8f688732a19755b245d08e6d1269f15e89ebd26bc895462d66e4a6562cee11ce604ec0fee70e13246619df69f5dffc7')

build() {
    cd "$pkgname"
    zig build -Ddest_directory="$pkgdir" -Dname="ly-dm" -Dcpu=baseline -Doptimize=ReleaseSafe
}

package() {
    cd "$pkgname"
    zig build -Ddest_directory="$pkgdir" -Dname="ly-dm" -Dcpu=baseline -Doptimize=ReleaseSafe installexe

    install -Dm644 license.md "$pkgdir/usr/share/licenses/$pkgname/WTFPL"
}
