# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Ã©clairevoyant
# Contributor: nullgemm <nullgemm@mailbox.org>

pkgname=ly
pkgver=1.1.1
pkgrel=1
pkgdesc="TUI display manager"
arch=(x86_64)
url="https://codeberg.org/AnErrupTion/ly"
license=('WTFPL')
depends=(pam glibc libxcb)
makedepends=(git zig)
optdepends=('xorg-xauth: for X server sessions'
            'libxcb: for X server sessions')
backup=(
    etc/$pkgname/{config.ini,setup.sh}
    etc/pam.d/ly
)
source=("git+$url.git#tag=v${pkgver}")
b2sums=('301f07f506ac1062b22823e048b0457c7990bdee894b250dd0bb17489b75e07ea274ea4fd0e6c450b59543bb6ce65e807398ea9d89b5463f44d19f66beee3727')

build() {
    cd "$pkgname"
    zig build -Ddest_directory="$pkgdir" -Dname="ly-dm" -Dcpu=baseline -Doptimize=ReleaseSafe
}

package() {
    cd "$pkgname"
    zig build -Ddest_directory="$pkgdir" -Dname="ly-dm" -Dcpu=baseline -Doptimize=ReleaseSafe installexe

    install -Dm644 license.md "$pkgdir/usr/share/licenses/$pkgname/WTFPL"
}
