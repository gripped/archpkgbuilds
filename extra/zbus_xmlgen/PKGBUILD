# Maintainer: David Runge <dvzrv@archlinux.org>

_project_name=zbus
_name=zbus-xmlgen
pkgname=zbus_xmlgen
pkgver=5.2.0
pkgrel=1
pkgdesc="D-Bus XML interface code generator"
arch=(x86_64)
url="https://github.com/dbus2/zbus"
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $url/archive/refs/tags/$pkgname-$pkgver.tar.gz
)
sha512sums=('f2b536e840b3a3ffa46551015e2f6fa954fcb2ee426416253a856da61c1cb6caf7cfcc1f180a496c209c36978ce331d1be045f34e175e21c4e4fc181e237a5e7')
b2sums=('d42c2c65d23836916051e89b0ae04d4fe01d95267e53b883fe5ea962313708048e9875af91dff19f1258f77adaf8eb37838ce19803a931c943195b0636e59952')

prepare() {
  cd $_project_name-$pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_project_name-$pkgname-$pkgver
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_project_name-$pkgname-$pkgver
  cargo test --frozen --all-features --package $pkgname
}

package() {
  install -vDm 755 $_project_name-$pkgname-$pkgver/target/release/$_name -t "$pkgdir/usr/bin/"
  install -vDm 644 $_project_name-$pkgname-$pkgver/LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/MIT.txt"
}
