# Maintainer: David Runge <dvzrv@archlinux.org>

_project_name=zbus
_name=zbus-xmlgen
pkgname=zbus_xmlgen
pkgver=5.0.0
pkgrel=1
pkgdesc="D-Bus XML interface code generator"
arch=(x86_64)
url="https://github.com/dbus2/zbus"
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $url/archive/refs/tags/$pkgname-$pkgver.tar.gz
)
sha512sums=('8a9244d953ee62c60226ab1550ccb36da7e00c841dc9377a4ade93991d52dbdee6b9e02ee82fa533e7489c273f171aecb1638ee4fc72db03ee332a4226dbc740')
b2sums=('e1ab3b17f673d98716d12b8960f20c432f1e94b65157e5b8d6c7d6172de6cc1f29ce5542282f9c45fc6bf8d36bd2212df6b351b69adfa1cabddec375999729b3')

prepare() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features --package $pkgname
}

package() {
  install -vDm 755 $_project_name-$pkgname-$pkgver/target/release/$_name -t "$pkgdir/usr/bin/"
  install -vDm 644 $_project_name-$pkgname-$pkgver/LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/MIT.txt"
}
