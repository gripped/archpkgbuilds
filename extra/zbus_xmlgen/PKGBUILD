# Maintainer: David Runge <dvzrv@archlinux.org>

_project_name=zbus
_name=zbus-xmlgen
pkgname=zbus_xmlgen
pkgver=4.0.1
pkgrel=1
pkgdesc="D-Bus XML interface code generator"
arch=(x86_64)
url="https://github.com/dbus2/zbus"
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $url/archive/refs/tags/$pkgname-$pkgver.tar.gz
  Cargo.lock
)
sha512sums=('ac865ac9f1a8c37150404b17ee01a880838ad207bb24fcc86ba29875b2da75ec4f774624e0de5b3eb349d351c4ab274dd9f4b5fc1961912fbc8efbdee19f3b91'
            '68b20f18d9539f0d39205d2bcfc373acf49b37382b9ffa69130f34e7a16a989479acf0dc5c618ede1cfab793ce4db47781c5cc5001fcee10d6937e8c98f6fdf5')
b2sums=('634fe28237dacac2e2c45afb583cd4303039963dcb73025577122ab8e9018acf1c13fb6e93be63c3f180a86986d28b0e83d03636c052278d778f342e479a02af'
        'ee703d17d28faa0379c8c3687cdb0dd77f09f70385ccace800f9563a2451a441b42a0662e07c48c9256f9f947e25490c922ace34c09df08c87de57d21b04d82e')

prepare() {
  cp -v -- Cargo.lock $_project_name-$pkgname-$pkgver/

  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features --package $pkgname
}

package() {
  install -vDm 755 $_project_name-$pkgname-$pkgver/target/release/$_name -t "$pkgdir/usr/bin/"
  install -vDm 644 $_project_name-$pkgname-$pkgver/LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/MIT.txt"
}
