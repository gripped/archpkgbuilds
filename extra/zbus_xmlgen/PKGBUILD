# Maintainer: David Runge <dvzrv@archlinux.org>

_project_name=zbus
_name=zbus-xmlgen
pkgname=zbus_xmlgen
pkgver=5.0.2
pkgrel=1
pkgdesc="D-Bus XML interface code generator"
arch=(x86_64)
url="https://github.com/dbus2/zbus"
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $url/archive/refs/tags/$pkgname-$pkgver.tar.gz
)
sha512sums=('21ee111946c52dd13f8a479b4ecaaf0a70c8fa80ee62f7d3b184a54d1b9766c0525cb8c54fa57ad638f6e3b490712c1b7c407a12d43d454a4025f75f78ebc00b')
b2sums=('26a4c2db2cd3de771b58a79fffecbe00e3e44014347ea85123db95f4c1e539c1320b2d20a9362ed472cbc6771fd7b23afdd3029c2d508f472216f0500b1a1b02')

prepare() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features --package $pkgname
}

package() {
  install -vDm 755 $_project_name-$pkgname-$pkgver/target/release/$_name -t "$pkgdir/usr/bin/"
  install -vDm 644 $_project_name-$pkgname-$pkgver/LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/MIT.txt"
}
