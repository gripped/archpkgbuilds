# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: getzze <getzze at gmail dot com>
pkgbase=python-tensorflow-serving-api
pkgname=("${pkgbase}" "${pkgbase}"-gpu)
_pkgname=tensorflow_serving_api
pkgver=2.18.0
pkgrel=1
pkgdesc="Serving system for machine learning models, designed for production environments"
arch=(any)
url="https://www.tensorflow.org/serving/"
license=('Apache')
depends=('python' 'python-grpcio' 'python-tensorflow' 'python-protobuf')
makedepends=('bazel' 'git' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tensorflow/serving/archive/${pkgver}.tar.gz")
b2sums=('2717c845f5d078cf74981d03ed1f882218ca9eba122cb5545d44ddbe522933f9241d7659aae88bb035d4ba434160677e4ae36756e5ba9e680370578409428c72')

build() {
  cd "${srcdir}/serving-${pkgver}"

  bazel build --enable_bzlmod=no -c opt tensorflow_serving/tools/pip_package:build_pip_package
  sed -i "s|bazel-genfiles/|bazel-out/k8-opt/bin/|g" tensorflow_serving/tools/pip_package/build_pip_package.sh

  mkdir "${srcdir}/dist"
  bazel-bin/tensorflow_serving/tools/pip_package/build_pip_package "${srcdir}/dist"
  ls -lah "${srcdir}/dist"
}

package_python-tensorflow-serving-api() {
  cd "${srcdir}/serving-${pkgver}"

  python -m installer --destdir="${pkgdir}" "${srcdir}/dist/${_pkgname}"-*.whl
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_python-tensorflow-serving-api-gpu() {
  pkgdesc+=" (with GPU support)"
  provides=(python-tensorflow-serving-api)
  conflicts=(python-tensorflow-serving-api)

  cd "${srcdir}/serving-${pkgver}"
  python -m installer --destdir="${pkgdir}" "${srcdir}/dist/${_pkgname}"_gpu-*.whl
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
