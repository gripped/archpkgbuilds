# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Paolo Giangrandi <peoro.noob@gmail.com>

pkgname=impacket
pkgver=0.12.0
pkgrel=1
pkgdesc='Collection of classes for working with network protocols'
url='https://github.com/CoreSecurity/impacket'
arch=(any)
license=(Apache-2.0)
depends=(
  python
  python-flask
  python-ldap3
  python-pcapy
  python-pyasn1
  python-pyasn1-modules
  python-pycryptodome
  python-pycryptodomex
  python-pyopenssl
  python-setuptools  # https://github.com/fortra/impacket/issues/885#issuecomment-1197218746
  python-charset-normalizer
  python-six
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  python-cryptography
  python-pytest
)
source=(https://github.com/CoreSecurity/impacket/archive/impacket_${pkgver//./_}.tar.gz)
sha256sums=('1c41951a29c03a03ff749ad68caa0f050072873850c09d7a7f1bca75c41fd66c')
sha512sums=('3d30209f6ef73dd002fb5619af3366cc403c9a3d8ce7bc47b88f33facb1bc752266c148c8c7389fbd5720a10adf60f0d90334c33687a49a7d9a8051987648e63')

build() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  # Deselected tests are not compatible with Python 3.13, see:
  # https://github.com/fortra/impacket/issues/1824
  pytest -m "not remote" \
    --deselect tests/SMB_RPC/test_smbserver.py
}

package() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 ChangeLog.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  ln -s "/usr/lib/python${PYTHONVERSION}/site-packages/impacket/examples" "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
