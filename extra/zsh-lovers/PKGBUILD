# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>
# Contributor: Patrick Leslie Polzer <polzer@gnu.org>
# Contributor: Aaron 'venisonslurpee' Laursen <venisonslurpee@gmail.com>

pkgname=zsh-lovers
pkgver=0.11.0
pkgrel=1
pkgdesc="A collection of tips, tricks and examples for the Z shell."
arch=('any')
url='https://grml.org/zsh/#zshlovers'
license=('GPL-2.0-only')
source=("$pkgname-$pkgver.tar.gz::https://github.com/grml/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('981b82e216a6c86c87a3f1237b47485e')
sha256sums=('9bd114c5883f43515888129945967816813b93887fb726cce781d76780e94f69')
makedepends=('asciidoc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  a2x -vv --no-xmllint -f manpage zsh-lovers.1.txt
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  install -Dm0644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
