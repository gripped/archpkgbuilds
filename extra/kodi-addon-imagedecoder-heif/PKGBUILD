# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>

pkgname=kodi-addon-imagedecoder-heif
pkgver=21.0.3
_codename=Omega
pkgrel=1
pkgdesc="Kodi heif/heic imagedecoder addon"
arch=('x86_64')
url="https://github.com/xbmc/imagedecoder.heif"
license=('GPL2')
groups=('kodi-addons' 'kodi-addons-imagedecoder')
makedepends=('cmake' 'kodi-dev' 'libheif' 'tinyxml2')
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/imagedecoder.heif/archive/refs/tags/$pkgver-$_codename.tar.gz"
        "0001-Fix-memroy-leak-in-HEIF-image-decoder.patch::https://patch-diff.githubusercontent.com/raw/xbmc/imagedecoder.heif/pull/85.patch"
)
sha512sums=('342ee83dedb0e1f48f7a302227405bbfee0f57b3a249af1154afe646c682a73d433a7d72262c4d7276a63b9dcbf9ee1d4bf656328ba2cd8ff2b84ece778c79c9'
            '61fa10ba190a9ee9fca75228371736813724dda28bbad15f36bddb93d84cfa88afeec2b02bc218c124a8fd092f223489b2e8f8698ba11e04499c5fd13a105dfa')

prepare() {
    cd "imagedecoder.heif-$pkgver-$_codename"
    patch -p1 -i "$srcdir/0001-Fix-memroy-leak-in-HEIF-image-decoder.patch"
}

build() {
    cd "imagedecoder.heif-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'libheif' 'tinyxml2')
    cd "imagedecoder.heif-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}
