# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=psygnal
pkgname=python-$_name
pkgver=0.12.0
pkgrel=1
pkgdesc="Python observer pattern (callback/event system) modeled after Qt Signals & Slots (but independent of Qt)"
arch=(any)
url="https://github.com/pyapp-kit/psygnal"
license=(BSD-3-Clause)
depends=(
  python
)
makedepends=(
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
)
checkdepends=(
  python-pytest
  python-attrs
  python-dask
  python-numpy
  python-pydantic
  python-toolz
  python-wrapt
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('2559aff77c3be9afec1fa4a073a695fc37374e5691a65814fce94011e0cb3c3dc4a8fc5bf5379e62c6301f6c2a222ff3f0e1033d7e1c1e655767dc9c8a36927b')

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
