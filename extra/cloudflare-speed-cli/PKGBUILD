# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cloudflare-speed-cli
pkgver=0.6.0
pkgrel=1
pkgdesc="CLI for internet speed test via Cloudflare "
url="https://github.com/kavehtehrani/cloudflare-speed-cli"
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('GPL-3.0-only')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('1c2c26ca1f227c1964b3bf356c77c6d006d422fbb5f0e24dfdb0f52a43e4964d9a956c226bf5b1e6b6297a6be5ccb9c29c46fcab6759ee79633cd6fd7b68afb9')
options=('!lto')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
