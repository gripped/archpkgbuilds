# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cloudflare-speed-cli
pkgver=0.5.0
pkgrel=1
pkgdesc="CLI for internet speed test via Cloudflare "
url="https://github.com/kavehtehrani/cloudflare-speed-cli"
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('GPL-3.0-only')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d268522fe8510ffb0f5349fcfab0116001433e8cf0d64fcdbd354428f4050635ab0dfc6ed3eb4d0d0129bc0f8f051effe3143f463aa25ec5752e45562c482430')
options=('!lto')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
