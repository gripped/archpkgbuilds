# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cloudflare-speed-cli
pkgver=0.6.5
pkgrel=1
pkgdesc="CLI for internet speed test via Cloudflare "
url="https://github.com/kavehtehrani/cloudflare-speed-cli"
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('GPL-3.0-only')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f9d366cb0977737f4211f3be04c6fa2705ef6fd6139e3ea2a6dd2335081d92a19a1ed6055d3efae2eb36ecb65b2ec6ded82132801a2cdf9a41bac1baf0786b0b')
options=('!lto')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
