# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cloudflare-speed-cli
pkgver=0.6.1
pkgrel=1
pkgdesc="CLI for internet speed test via Cloudflare "
url="https://github.com/kavehtehrani/cloudflare-speed-cli"
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('GPL-3.0-only')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('fa90761176a84df8187600bc90066bf8cd4e10a5a2c4d63d1b108b578d132bd877cd17233e4b2863e3e219928085e0099d09579d8ae8be1ae81ffc6a64515cb4')
options=('!lto')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
