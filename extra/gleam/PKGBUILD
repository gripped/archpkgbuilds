# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Arnaud Berthomier <arnaud@cyprio.net>

pkgname=gleam
pkgver=1.14.0
pkgrel=4
pkgdesc='Friendly programming language for building type-safe and scalable systems'
arch=(x86_64)
url='https://github.com/gleam-lang/gleam'
license=(Apache-2.0)
depends=(erlang-core erlang-eunit glibc)
makedepends=(cargo git rust)
checkdepends=(bun deno nodejs)
optdepends=('rebar3: erlang dependency support'
            'erlang: standard erlang distribution')
provides=(gleam)
options=(!docs !libtool !staticlibs !lto)
source=("git+$url#tag=v$pkgver")
b2sums=('67b66dca4c28ca1909c0d4584a75cad9c65b244565b083000deac7ba4b167ba28c3735c8b523c15c0b3eb3e8a1185b6958f5a62bdf3fcb2b85bccfc781b22e86')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --workspace
}

package() {
  install -vDm755 $pkgname/target/release/gleam "$pkgdir/usr/bin/gleam"
}
