# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>

pkgname=yq
pkgver=3.4.2
pkgrel=2
pkgdesc="Command-line YAML, XML, TOML processor - jq wrapper for YAML/XML/TOML documents"
arch=('any')
url="https://github.com/kislyuk/yq"
license=('Apache')
depends=('jq'
         'python-argcomplete'
         'python-tomlkit'
         'python-xmltodict'
         'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
conflicts=('go-yq')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('f916408e834e96f390ef82a36bfcbf24a555ae5a2fcdaef94a2ed5d260d161ef')
b2sums=('253701053d32279e78f703626432c5e068a58ad8d14286739c07452f1e0eeb8599eb1f2f0ea9479b60ed32d7bf94bd1c046b79ebee920df84371757195268bfb')

prepare() {
  cd $pkgname-$pkgver
  # Unblock current setuptools: https://github.com/kislyuk/yq/issues/190
  sed -i -e '/setuptools_scm/s/\(_scm\).*"/\1"/' setup.py
  # Fix version: https://github.com/pypa/setuptools_scm/issues/1037
  echo "version = '$pkgver'" > "$pkgname/version.py"
}

build() {
  cd $pkgname-$pkgver
  python -m build -wn
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
}
