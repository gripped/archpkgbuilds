# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>

pkgname=yq
pkgver=3.3.1
pkgrel=1
pkgdesc="Command-line YAML, XML, TOML processor - jq wrapper for YAML/XML/TOML documents"
arch=('any')
url="https://github.com/kislyuk/yq"
license=('Apache')
depends=('jq'
         'python-argcomplete'
         'python-tomlkit'
         'python-xmltodict'
         'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
conflicts=('go-yq')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('09c6c56bae8d5a3fe36222aa74fd343625071c511f02699c35346388a47a32a5')
b2sums=('bf341fc128761c2fff3dadcf0e3a00abab649b88b9210d4e9c01f9594ab64dfc375a99e3be180f1f1147c8a1f55dd0cb65953b6ddbc1a24bace7f0e9d324994b')

build() {
  cd $pkgname-$pkgver
  python -m build -wn
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
}
