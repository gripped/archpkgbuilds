# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Matthias Ellmer <ellmer.matthias@gmail.com>
# Contributor: Julien Morin <jmorin@cp.dias.ie>

pkgbase=python-basemap
pkgname=('python-basemap' 'python-basemap-common')
pkgver=2.0.0
pkgrel=2
arch=('x86_64')
url="https://matplotlib.org/basemap/"
license=('MIT')
makedepends=('cython' 'python-setuptools' 'python-matplotlib' 'geos' 'shapelib'
             'python-numpy' 'python-pyproj' 'python-pip')
source=($pkgname-$pkgver.tar.gz::https://github.com/matplotlib/basemap/archive/v${pkgver}.tar.gz)
sha256sums=('192ee1c63291d7d1af98fab798c45239ea27e6ca31041afa8c42c8b697e44596')

prepare() {
  pushd "${srcdir}"/basemap-${pkgver}/
  sed -i "s/lib64/lib/g" setup.py
  popd
}

build() {
  cd "${srcdir}"/basemap-${pkgver}/
  python setup.py config
  python setup.py build
  cd "${srcdir}"/basemap-${pkgver}/data/basemap_data
  python setup.py config
  python setup.py build
}

package_python-basemap() {
  pkgdesc="Toolkit for plotting data on map projections"
  depends=('python-basemap-common' 'python-matplotlib' 'geos' 'shapelib' 'python-numpy' 'python-pyproj')
  optdepends=('python-owslib: needed for the BaseMap.wmsimage function'
              'python-pillow: needed for Basemap warpimage, bluemarble, shadedrelief and etop methods')

  cd "${srcdir}"/basemap-${pkgver}/

  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-basemap-common() {
  pkgdesc="Data files for python-basemap"
  license=('GPL2')

  cd "${srcdir}"/basemap-${pkgver}/data/basemap_data

  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm644 ../../LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
