# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=binary
pkgver=5.3
pkgrel=2
pkgdesc='Simple app to convert between different hexadecimal and binary numbers'
arch=(any)
url='https://apps.gnome.org/Binary/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  python
  python-gobject
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://github.com/fizzyizzy05/binary.git#tag=$pkgver")
b2sums=(0ecd350a815f64074258d9238ea064161f945417f3c1e82cbb8ed685c750a69377a5d70d185720313290ed8bcaa7516f381245cd3bde218b968044cd3fd034d5)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
