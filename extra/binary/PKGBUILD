# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=binary
pkgver=5.2
pkgrel=2
pkgdesc='Simple app to convert between different hexadecimal and binary numbers'
arch=(any)
url='https://apps.gnome.org/Binary/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  python
  python-gobject
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://github.com/fizzyizzy05/binary.git#tag=$pkgver")
b2sums=(2e4c8611e51d8634eac953183328a65bc689f5111fc158672417df585eb54b267dbf5bb6c13e912ef46f769dd3d70995cffac76e046fea7f6e55c88f70938893)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
