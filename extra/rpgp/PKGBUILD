# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rpgp
_name="${pkgname}ie"
pkgver=0.9.0
pkgrel=1
pkgdesc="An experimental OpenPGP CLI tool"
arch=(x86_64)
url="https://codeberg.org/heiko/rpgpie"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('54f8f630db88247440eef47514a9c046ddf9ed3df9f9b8be3142a1d56627da4089d64268b47a0e12301d893a4d7545328ce5ba0d44efbb752a4641a0a73c19d4')
b2sums=('87f3f32c69540ef846d036dd827729c2fe08743d9b0990603f81e7f40363e424fae2bfe8eb2589575cd3e9c95ae373c9ef2e429c791acd9e36f6a535deb0f08e')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  export CARGO_TARGET_DIR=target

  cd $_name
  cargo build --frozen --release --features=cli,draft-pqc
}

check() {
  cd $_name
  cargo test --frozen --features=cli
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
