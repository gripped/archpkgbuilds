# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rpgp
_name="${pkgname}ie"
pkgver=0.7.2
pkgrel=1
pkgdesc="An experimental OpenPGP CLI tool"
arch=(x86_64)
url="https://codeberg.org/heiko/rpgpie"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('e10a8e9472b7d2f48f7bbc60893fa11d20b1044b41707154369a3a991d678af7c25649d5f522bcf2176095a89dc7cdcc1d9acf7637b694e920316fece1c0d718')
b2sums=('640dc96423195e20d61a808039e7a3eea8a8b713c90ab6586394fedae0802ebbffcf6f32ac34d08e18a1d605882779a30dd77110fbb55268eb753bd8e4437cfb')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export CARGO_TARGET_DIR=target

  cd $_name
  cargo build --frozen --release --features=cli
}

check() {
  cd $_name
  cargo test --frozen --features=cli
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
