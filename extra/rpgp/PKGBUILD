# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rpgp
_name="${pkgname}ie"
pkgver=0.6.6
pkgrel=1
pkgdesc="An experimental OpenPGP CLI tool"
arch=(x86_64)
url="https://codeberg.org/heiko/rpgpie"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('9e8e9924e798fc6bb28acee53d340e03d684a4070d1cd80e4a095158f85997b0eeb744a0a0106603f9ce8fdee1390a15c0594f7d76c1fbb2d6b0299521a2587c')
b2sums=('2336dc08d5973dcad411f512acb455595768d489a3d94be492bd9d5f570bfe7e8d6fdbb0a9e61ab9c852edb60f938977f39a85ad000704bb2ce0d50db4250b45')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export CARGO_TARGET_DIR=target

  cd $_name
  cargo build --frozen --release --features=cli
}

check() {
  cd $_name
  cargo test --frozen --features=cli
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
