# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rpgp
_name="${pkgname}ie"
pkgver=0.7.5
pkgrel=1
pkgdesc="An experimental OpenPGP CLI tool"
arch=(x86_64)
url="https://codeberg.org/heiko/rpgpie"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('261483ba337348b9b6344f22d47c0ab06b09c706557f607a0de70042a2e997c2bc2d860302aa1dcbbfff4f8c95e5f38ff8cedec24623a63d1ac29911becc728a')
b2sums=('6d3f1790e64d720f0db254f1f5b5978782f468eb1098a45b936f08cb2c1f66223a9d9cb095706c7863068d4174318fed588de66839fe6d58933de13ae9f6a055')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export CARGO_TARGET_DIR=target

  cd $_name
  cargo build --frozen --release --features=cli,draft-pqc
}

check() {
  cd $_name
  cargo test --frozen --features=cli
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
