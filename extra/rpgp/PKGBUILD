# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rpgp
_name="${pkgname}ie"
pkgver=0.8.1
pkgrel=1
pkgdesc="An experimental OpenPGP CLI tool"
arch=(x86_64)
url="https://codeberg.org/heiko/rpgpie"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('3ac425ada3e153d8dfa82e992bf2924e898e0fd6e9f7d6bb7c21f6e500963a523a1be768347ebd53f37e8176e2126726f5449ebfbbbdb0821f844ddc3975ac01')
b2sums=('40f7c53303caf42aa9c1e8e7de2f5fb0813513caec001cb513bc5a044b5c86839fda211f1170a7e2aa78e848e84895e29076796c287bc473ed74b97e7a9b669f')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export CARGO_TARGET_DIR=target

  cd $_name
  cargo build --frozen --release --features=cli,draft-pqc
}

check() {
  cd $_name
  cargo test --frozen --features=cli
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
