# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rpgp
_name="${pkgname}ie"
pkgver=0.8.2
pkgrel=1
pkgdesc="An experimental OpenPGP CLI tool"
arch=(x86_64)
url="https://codeberg.org/heiko/rpgpie"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('e99d867ab7c80488a61a74c83bf1b5a2726a6ba41395d4658204f097894a574957653cb472f260f02fee3d6f812160940c030d013c179beceefcd160de6b70ab')
b2sums=('c4af51494d647b4488fc7a1d957ed82dbe740ba4623da231162a50c435227fda1cf2ca8de5006867c10a4ba645eb33fd22d95d09720d07bb2e8b92580483dd78')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  export CARGO_TARGET_DIR=target

  cd $_name
  cargo build --frozen --release --features=cli,draft-pqc
}

check() {
  cd $_name
  cargo test --frozen --features=cli
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
