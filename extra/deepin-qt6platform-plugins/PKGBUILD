# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6platform-plugins
pkgver=6.0.29
pkgrel=1
pkgdesc='Qt 6 platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6platform-plugins"
license=('LGPL3')
depends=('qt6-base' 'libx11' 'libxi' 'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'libglvnd' 'gtest' 'qt6-xcb-private-headers'
             'extra-cmake-modules' 'dtkcommon' 'cairo')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6platform-plugins.git#tag=$pkgver")
sha512sums=('fea00e0f824e488eb5f60b790ad9af0adaff6bf60a3b22ba2b977871b72a03f25506356b55e3a5ea1ceec3f527ee9104734ae84269589e388bbc6de220378a55')

prepare() {
  cd qt6platform-plugins
  git cherry-pick -n d5ede4e9e5bf8f018c8178e481ef479eab9ce493 # Fix for Qt 6.9
}

build() {
  cd qt6platform-plugins
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qt6xcb-private \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6platform-plugins
  DESTDIR="$pkgdir" ninja install
}
