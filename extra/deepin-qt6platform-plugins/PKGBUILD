# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6platform-plugins
pkgver=6.0.43
pkgrel=2
pkgdesc='Qt 6 platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6platform-plugins"
license=('LGPL-3.0-or-later')
depends=('qt6-base' 'libx11' 'libxi' 'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'libglvnd' 'gtest' 'qt6-xcb-private-headers'
             'extra-cmake-modules' 'dtkcommon' 'cairo')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6platform-plugins.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('f0d1fdec11a4e9a0a8b2d2ed1b0272ada49293cbdba248f7ddbac6beb2bf632840871b58fd062b04e1ac98a64356cc35b4f90f72fed6080121761325e674c204'
            '8f8d60fd3eec50d4b1edc6ba911666b689d5419387155d5ea05a2ab46dd5e1038f66b25ae090aa3c729e3ac9ebed381064d5df1b15477a0395e22a6e1c70f4e1')

prepare() {
  cd qt6platform-plugins
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd qt6platform-plugins
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qt6xcb-private
  ninja
}

package() {
  cd qt6platform-plugins
  DESTDIR="$pkgdir" ninja install
}
