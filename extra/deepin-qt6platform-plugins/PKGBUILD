# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6platform-plugins
pkgver=6.0.47
pkgrel=2
pkgdesc='Qt 6 platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6platform-plugins"
license=('LGPL-3.0-or-later')
depends=('qt6-base' 'libx11' 'libxi' 'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'libglvnd' 'gtest' 'qt6-xcb-private-headers'
             'extra-cmake-modules' 'dtkcommon' 'cairo')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6platform-plugins.git#tag=$pkgver")
sha512sums=('ac5ca4226540b5090d0cd7039b1646e9ee2d927db5a0a626730f75580227646acec44316fc4c909387612bc9870447c3871505b957bcd938431c6e0dca041b98')

build() {
  cd qt6platform-plugins
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qt6xcb-private
  ninja
}

package() {
  cd qt6platform-plugins
  DESTDIR="$pkgdir" ninja install
}
