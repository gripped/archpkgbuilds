# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6platform-plugins
pkgver=6.0.23
pkgrel=1
pkgdesc='Qt 6 platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6platform-plugins"
license=('LGPL3')
depends=('qt6-base' 'libx11' 'libxi' 'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'libglvnd' 'gtest' 'qt6-xcb-private-headers'
             'extra-cmake-modules' 'dtkcommon' 'cairo')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6platform-plugins.git#tag=$pkgver")
sha512sums=('81c5b18d2006e23eca0db1bd67c56dff93e7f3026c0dcfcfad5b5f5d52699801c6985cc592c7bd1078ee4940437835089f55a4232a6653898d71470cced76f16')

build() {
  cd qt6platform-plugins
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qt6xcb-private \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6platform-plugins
  DESTDIR="$pkgdir" ninja install
}
