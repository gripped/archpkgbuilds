# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.2.6
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('bd81f15e003352a458e21923cda8347e81c225d3864099d373d69d02fd865e1b2d773856bc902baeec8a1bd9c26542fcaf1100a698de79154db3a15f0c95bbd8')
b2sums=('257994a73f546dd316c1611b49682b1d5b749f7f1147a5a534f7d3f4d6ac55b19f34faeb28b69f98d4e224eeb20ed28faeda8081411605c432682d6e2b1f5ec6')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
