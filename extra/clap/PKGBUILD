# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.2.3
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('867585d86a8a5a5b758aca1d78f73c0af098b61c5d57fab61674e4afed8bd80aa03bb435223f6cd1d152820f33112006e40c26210e9f012a4c28645a1a0a4297')
b2sums=('371bb5bfff5992e20874fb854fdbe47c85451c7a0ce69ba598496b6cd0374bc83cd7a6ab314a9f941250db89b8866e59ffc608cc9fe3b164b283e04cfcae2c28')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
