# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.2.2
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('bb927a53d10ea7680f43a4139db6bcd293a051e9cab4293612cba29858ec18760d97de331da8eaa948844fd4ce38e895ee0b731654c42e3adbdc9000d1727884')
b2sums=('25c813cb54278f71d0066972854d0fdaec58d8699f923ade137f05674236d4d6ed1bc8f189c2e82755a43e0e60f5ec0e32d6e00bfef6fde9918310c8de5dc3be')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
