# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.1.9
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('7fdfa781488cb8443eb0d04ec4ed92b0b2fc937965fdf1e3b6b36c9d3df296a8faf44fd16e53ad5641106697f85c7ee818154d82a5544ed34a1e86365bd717ba')
b2sums=('dc1667a967b75b41cd7cba51674d5ed096b56ae5e11fc2b8b2f0e399b24e4b68d5eb23d50ea87034040f121c10cf80a78ea31eada2f1ff9efbaee0232dc89cf0')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
