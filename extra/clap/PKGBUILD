# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.2.5
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('275142d95dcb62ce2b3f96c7ee9c325e126b521b8edd25ae52fc3e5a3916c4f1b6a1ac1ace8c3ae79d4e7ea0df35df064d080f782abcc317a7dd1e267098a501')
b2sums=('47880b22c3f03d7a61e149c8a9fa6cb0b869a770aff66620c73252ab5b9d206e8a057c9b7ce5615cfce1e3348d74083d9be33b87efa94fd381aa8bf59ab6747c')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
