# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-openconnect
pkgver=1.2.10
pkgrel=3
pkgdesc="NetworkManager VPN plugin for OpenConnect"
url="https://networkmanager.dev/docs/vpn/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libnm
  libsecret
  libxml2
  openconnect
)
makedepends=(
  gcr
  git
  intltool
  libnma
  libnma-gtk4
  python
  webkit2gtk-4.1
)
optdepends=(
  "gcr: GUI support"
  "libnma-gtk4: GUI support (GTK 4)"
  "libnma: GUI support (GTK 3)"
  "webkit2gtk-4.1: GUI support"
)
source=("git+https://gitlab.gnome.org/GNOME/NetworkManager-openconnect.git?signed#tag=$pkgver")
b2sums=('2a2a92181f28c2c1bd50559c9430f8262bb6ebe73b2ee77b5fcc1af456e1b5124a4888285e31d86b3e0f820290b25c8b2b86d797389c3021d9a9ca27aacf8149')
validpgpkeys=(
  BE07D9FD54809AB2C4B0FF5F63762CDA67E2F359 # David Woodhouse <dwmw2@infradead.org>
)

prepare() {
  cd NetworkManager-openconnect
  intltoolize --automake --copy
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --disable-static
    --with-gtk4
  )

  cd NetworkManager-openconnect
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(libopenconnect.so)

  cd NetworkManager-openconnect
  make DESTDIR="$pkgdir" install dbusservicedir=/usr/share/dbus-1/system.d
  echo 'u nm-openconnect - "NetworkManager OpenConnect"' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}

# vim:set sw=2 sts=-1 et:
