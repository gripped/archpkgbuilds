# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-flamegraph
pkgver=0.6.10
pkgrel=1
pkgdesc='Easy flamegraphs for Rust projects and everything else'
url="https://github.com/flamegraph-rs/flamegraph"
depends=(
  'cargo'
  'gcc-libs'
  'glibc'
  'perf'
)
arch=('x86_64')
license=('MIT' 'Apache-2.0')
source=(https://github.com/flamegraph-rs/flamegraph/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('36dcd88cf872b6d0206dd2b684b3d4b99158d2960f5aa4f84d97677ea5ef0ae9')
b2sums=('03b161366bea6d2c0fb1d212c75a3931b73f495748722ebf57a66dfff228cfc244bb4f0c828310026d15d031fbc431c56c58c73000da63c97d28a57a9c350c9c')

build() {
  cd "flamegraph-${pkgver}"
  cargo build --release --locked

  # Generate completion scripts
  target/release/flamegraph --completions bash > flamegraph
  target/release/flamegraph --completions zsh > _flamegraph
  target/release/flamegraph --completions fish > flamegraph.fish
}

check() {
  cd "flamegraph-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "flamegraph-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    target/release/cargo-flamegraph \
    target/release/flamegraph
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 flamegraph -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm644 _flamegraph -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm644 flamegraph.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"
}

# vim:set ts=2 sw=2 et:
