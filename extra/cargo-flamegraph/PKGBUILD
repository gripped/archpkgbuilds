# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-flamegraph
pkgver=0.6.9
pkgrel=1
pkgdesc='Easy flamegraphs for Rust projects and everything else'
url="https://github.com/flamegraph-rs/flamegraph"
depends=(
  'cargo'
  'gcc-libs'
  'glibc'
  'perf'
)
arch=('x86_64')
license=('MIT' 'Apache-2.0')
source=(https://github.com/flamegraph-rs/flamegraph/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('dd9b83affab5f9c1eaad0de9034037780e634c96fef4dce8fa83900e58cc240c')
b2sums=('55ec65b5770fab714eb4a0861331b04f2bc567ab1b3566f27e8c75930a8944e4841654e4461e3fee177dd0dc4b023f8691a7cdc9e511cbd3a8c5ad73501c94ef')

build() {
  cd "flamegraph-${pkgver}"
  cargo build --release --locked

  # Generate completion scripts
  target/release/flamegraph --completions bash > flamegraph
  target/release/flamegraph --completions zsh > _flamegraph
  target/release/flamegraph --completions fish > flamegraph.fish
}

check() {
  cd "flamegraph-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "flamegraph-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    target/release/cargo-flamegraph \
    target/release/flamegraph
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 flamegraph -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm644 _flamegraph -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm644 flamegraph.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"
}

# vim:set ts=2 sw=2 et:
