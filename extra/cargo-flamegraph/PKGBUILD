# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-flamegraph
pkgver=0.6.11
pkgrel=1
pkgdesc='Easy flamegraphs for Rust projects and everything else'
url="https://github.com/flamegraph-rs/flamegraph"
depends=(
  'cargo'
  'gcc-libs'
  'glibc'
  'perf'
)
arch=('x86_64')
license=('MIT' 'Apache-2.0')
source=(https://github.com/flamegraph-rs/flamegraph/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('371f81e674abe70cdaf3478d7d6ab04cee0b5af174767a858f45c5c42206089e')
b2sums=('05f3998b90c3d52e4f8325fdc41d4c8bbd5ce7d07e016b2786323ae4c8e4bd89e238c301c043fb96c717266e66142e61a1d02f623036492ef525ef74e056fcee')

build() {
  cd "flamegraph-${pkgver}"
  cargo build --release --locked

  # Generate completion scripts
  target/release/flamegraph --completions bash > flamegraph
  target/release/flamegraph --completions zsh > _flamegraph
  target/release/flamegraph --completions fish > flamegraph.fish
}

check() {
  cd "flamegraph-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "flamegraph-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    target/release/cargo-flamegraph \
    target/release/flamegraph
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 flamegraph -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm644 _flamegraph -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm644 flamegraph.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"
}

# vim:set ts=2 sw=2 et:
