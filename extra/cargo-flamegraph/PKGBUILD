# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-flamegraph
pkgver=0.6.7
pkgrel=2
pkgdesc='Easy flamegraphs for Rust projects and everything else'
url="https://github.com/flamegraph-rs/flamegraph"
depends=(
  'cargo'
  'gcc-libs'
  'glibc'
  'perf'
)
arch=('x86_64')
license=('MIT' 'Apache-2.0')
source=(https://github.com/flamegraph-rs/flamegraph/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('d7fa901673f4ece09226aeda416b98f919b7d946541ec948f1ef682bd6eec23b')
b2sums=('c6aef00e91a3c8d8018b1da9bc7a186b37ef5570be016656c0502555820aa94f2429fe3577721ab3069e30ecfdb5c6a8c0fff382aecfa6604b4d0ae982e3edb7')

build() {
  cd "flamegraph-${pkgver}"
  cargo build --release --locked

  # Generate completion scripts
  target/release/flamegraph --completions bash > flamegraph
  target/release/flamegraph --completions zsh > _flamegraph
  target/release/flamegraph --completions fish > flamegraph.fish
}

check() {
  cd "flamegraph-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "flamegraph-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    target/release/cargo-flamegraph \
    target/release/flamegraph
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 flamegraph -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm644 _flamegraph -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm644 flamegraph.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"
}

# vim:set ts=2 sw=2 et:
