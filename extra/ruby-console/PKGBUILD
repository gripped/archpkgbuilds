# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-console
pkgver=1.34.3
pkgrel=1
pkgdesc='Beautiful logging for Ruby'
arch=(any)
url='https://github.com/socketry/console'
license=(MIT)
depends=(
  ruby
  ruby-fiber-annotation
  ruby-fiber-local
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-console
)
options=(!emptydirs)
source=("git+${url}.git#tag=v$pkgver")
sha512sums=('0f7a3fb0f94baae82106d61b0fc1225ea893fa6e20570b2dbb57e22209cfa04184aa6e3327e4a43692f5e32ee73bcd46d0dfad4d5b8fdf85f06b8f737074e7c2')
b2sums=('8f554d894525a64e235075446f0a44ee9045624ddd9f322858cc0345fc0e8face7fa0219ad93db6f4f28a5dc434f227d002b082857e9a897dbc4078e3e31b7a0')

prepare() {
  cd console

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i console.gemspec
  sed -i \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd console
  gem build console.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    console-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd console
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd console
  cp -a tmp_install/* "$pkgdir"/
}
