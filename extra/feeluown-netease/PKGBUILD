# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-netease
pkgver=1.0.7
pkgrel=1
pkgdesc="feeluown netease plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-netease"
license=('GPL3')
depends=('feeluown' 'python-beautifulsoup4' 'python-pycryptodome' 'python-marshmallow'
         'python-requests' 'python-mutagen')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-netease.git#tag=v$pkgver")
sha512sums=('40e4cd076cf814c98a171ca646e6430eea5aaab67e2a6f7e0739120c35833d62f5a3ca22ec28a88cddde3d53a2fad50be604903e2ee670d6477bc072fa15b914')

build() {
  cd feeluown-netease
  python -m build --wheel --no-isolation
}

check() {
  cd feeluown-netease
  pytest
}

package() {
  cd feeluown-netease
  python -m installer --destdir="$pkgdir" dist/*.whl
}
