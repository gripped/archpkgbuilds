# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-netease
pkgver=1.0.6
pkgrel=2
pkgdesc="feeluown netease plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-netease"
license=('GPL3')
depends=('feeluown' 'python-beautifulsoup4' 'python-pycryptodome' 'python-marshmallow'
         'python-requests' 'python-mutagen')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-netease.git#tag=v$pkgver")
sha512sums=('5756dcc164c8861d9838ef5e7b6505283bbd4bbf60d3afb57bb9f909b4471fe6be70f911137b5da6f007d2f57dd3fcb8e245d4e9ca8a8e2976963c1fbd8d4e92')

build() {
  cd feeluown-netease
  python setup.py build
}

check() {
  cd feeluown-netease
  pytest
}

package() {
  cd feeluown-netease
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
