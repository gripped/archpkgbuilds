# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=toml-bombadil
pkgver=4.1.0
pkgrel=1
pkgdesc="Adotfile manager with templating"
arch=('x86_64')
url="https://github.com/oknozor/toml-bombadil"
license=('MIT')
depends=('gcc-libs' 'zlib')
makedepends=('rust')
options=(!lto)
source=($pkgname-$pkgver.tar.gz::https://github.com/oknozor/toml-bombadil/archive/${pkgver}.tar.gz)
sha512sums=('a22a064cfcc8c839a11b5e94350d81fc604de1182febe6c62f8d6dae115638c4de5adb11ca3cdce2ac00c06aab57b04a97757df2840caff2ea385b0d77378933')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/bombadil "$pkgdir"/usr/bin/bombadil

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/bombadil generate-completions bash > "$pkgdir"/usr/share/bash-completion/completions/bombadil
  target/release/bombadil generate-completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_bombadil
  target/release/bombadil generate-completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/bombadil.fish

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
