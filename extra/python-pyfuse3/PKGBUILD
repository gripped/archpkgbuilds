# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-pyfuse3
pkgver=3.4.2
pkgrel=1
pkgdesc='Python bindings for libfuse 3 with asynchronous API'
arch=(x86_64)
url='https://github.com/libfuse/pyfuse3'
license=(LGPL-2.0-or-later)
depends=(
  fuse3
  glibc
  python
  python-trio
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(python-pytest)
source=("git+https://github.com/libfuse/pyfuse3.git#tag=v$pkgver")
b2sums=(a1eac4735bc3f242f16725a52f7fc2f861ecfb916db95020db4b190ed92eb703452a06cb3f3e49e1a6612bc7267e1104f3a00a40702dfed42300cf92adf43e26)
validpgpkeys=(6D5BEF9ADD2075805747B70F9F88FB52FAF7B393) # Thomas Waldmann <tw@waldmann-edv.de>

build() {
  cd pyfuse3
  python -m build --wheel --no-isolation
}

check() {
  cd pyfuse3
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')" \
    pytest
}

package() {
  cd pyfuse3
  python -m installer --destdir="$pkgdir" dist/*.whl
}
