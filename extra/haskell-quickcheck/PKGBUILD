# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=QuickCheck
pkgname=haskell-quickcheck
pkgver=2.15.0.1
pkgrel=95
pkgdesc='Automatic testing of Haskell programs'
url='https://hackage.haskell.org/package/QuickCheck'
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-erf' 'haskell-random' 'haskell-splitmix')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('8fbcd49b5c87b1731cc9f11de9122b34a7cec3d0d936df3f43c29d2461f9b912f651e3d36f834c50627dc889d86893752a78c1973a003ad64ab86f0b160b4ad7')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure \
    -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir="/usr/share/doc/$pkgname" --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    -ftemplateHaskell

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script

  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -D -m744 unregister.sh "$pkgdir/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
