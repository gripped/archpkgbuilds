# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gnuchess
pkgver=6.2.11
pkgrel=2
pkgdesc="Play chess against the computer on a terminal and an engine for graphical chess frontends"
arch=('x86_64')
url="https://www.gnu.org/software/chess/chess.html"
license=('GPL3')
depends=('bash')
optdepends=('xboard: graphical frontend')
source=(https://ftp.gnu.org/pub/gnu/chess/$pkgname-$pkgver.tar.gz
        https://ftp.gnu.org/pub/gnu/chess/$pkgname-$pkgver.tar.gz.sig)
validpgpkeys=('34319C9B50B8D2176F5556C543AC7FF93CED5A6B')
md5sums=('784dc06371a98bd2d8e91ab5fb291b3c'
         'SKIP')
sha256sums=('d81140eea5c69d14b0cfb63816d4b4c9e18fba51f5267de5b1539f468939e9bd'
            'SKIP')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr

  make  
}

package() {
  cd $pkgname-$pkgver

  make prefix="$pkgdir"/usr install
}
