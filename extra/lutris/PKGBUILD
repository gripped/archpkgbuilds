# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>

pkgname=lutris
pkgver=0.5.16
pkgrel=2
pkgdesc='Open Gaming Platform'
arch=(any)
url=https://lutris.net
license=(GPL-3.0-only)
depends=(
  cabextract
  curl
  gdk-pixbuf2
  glib2
  gnome-desktop
  gobject-introspection-runtime
  gtk3
  hicolor-icon-theme
  mesa-utils
  p7zip
  pango
  psmisc
  python
  python-cairo
  python-certifi
  python-dbus
  python-distro
  python-evdev
  python-gobject
  python-lxml
  python-moddb
  python-pillow
  python-requests
  python-urllib3
  python-yaml
  unzip
  webkit2gtk-4.1
  xdg-desktop-portal-impl
  xorg-xrandr
)
makedepends=(
  git
  meson
  ninja
  python-poetry-core
)
checkdepends=(
  python-nose-cover3
  xorg-server-xvfb
  xterm
  fluidsynth
  vulkan-tools
  pciutils
)
optdepends=(
  'gamemode: Allows games to request a temporary set of optimisations'
  'gvfs: GVFS backend'
  'innoextract: Extract Inno Setup installers'
  'lib32-gamemode: Allows games to request a temporary set of optimisations'
  'lib32-vkd3d: DirectX 12 support'
  'lib32-vulkan-icd-loader: Vulkan support'
  'python-protobuf: Battle.net support'
  'vkd3d: DirectX 12 support'
  'vulkan-icd-loader: Vulkan support'
  'vulkan-tools: Vulkan support'
  'wine: Windows support'
  'xorg-xgamma: Restore gamma on game exit'
)
source=("git+https://github.com/lutris/lutris.git#tag=v${pkgver}"
        fix_refresh_steam_library.patch::https://github.com/lutris/lutris/commit/d6faf75fb370a004b80539466a7cd0beedcf5e5c.patch)
sha256sums=('eb675cd7c1f172ad93b8230fa270c800a0402b17bfcad1a6565d4dd95c91d4fd'
            'f6564979af07a343aebb475b304fad54f3fd512ba367667d334b1288bbf5dc91')

prepare() {
  cd lutris
  # Temporary patch to fix an issue when trying to refresh Steam library
  # See https://github.com/lutris/lutris/issues/5248
  patch -Np1 < "${srcdir}/fix_refresh_steam_library.patch"
}

build() {
  arch-meson lutris build
  ninja -C build
}

check() {
  cd lutris
  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
