# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>

pkgname=lutris
pkgver=0.5.18
pkgrel=2
pkgdesc='Open Gaming Platform'
arch=(any)
url=https://lutris.net
license=(GPL-3.0-only)
depends=(
  cabextract
  curl
  gdk-pixbuf2
  glib2
  gnome-desktop
  gobject-introspection-runtime
  gtk3
  hicolor-icon-theme
  mesa-utils
  p7zip
  pango
  psmisc
  python
  python-cairo
  python-certifi
  python-dbus
  python-distro
  python-evdev
  python-gobject
  python-lxml
  python-moddb
  python-pillow
  python-requests
  python-urllib3
  python-yaml
  unzip
  webkit2gtk-4.1
  xdg-desktop-portal-impl
  xorg-xrandr
)
makedepends=(
  git
  meson
  ninja
  protobuf
  python-poetry-core
)
checkdepends=(
  python-nose
  xorg-server-xvfb
  xterm
  fluidsynth
  vulkan-tools
  pciutils
)
optdepends=(
  'fluidsynth: For games that have MIDI music'
  'gamemode: Allows games to request a temporary set of optimisations'
  'gvfs: GVFS backend'
  'innoextract: Extract Inno Setup installers'
  'libayatana-appindicator: tray icon support'
  'lib32-gamemode: Allows games to request a temporary set of optimisations'
  'lib32-vkd3d: DirectX 12 support'
  'lib32-vulkan-icd-loader: Vulkan support'
  'python-protobuf: Battle.net support'
  'vkd3d: DirectX 12 support'
  'vulkan-icd-loader: Vulkan support'
  'vulkan-tools: Vulkan support'
  'wine: Windows support'
  'xorg-xgamma: Restore gamma on game exit'
  'umu-launcher: For running games through Proton'
)
source=("git+https://github.com/lutris/lutris.git#tag=v${pkgver}"
        "git+https://github.com/bartok765/galaxy_blizzard_plugin.git")
sha256sums=('8ef52446984b43fbd52378aeb21828f7763193553a25b8d8631e8b147dfb4dda'
            'SKIP')

prepare() {
  # Regenerate protos to fix BattleNet plugin
  # See https://gitlab.archlinux.org/archlinux/packaging/packages/lutris/-/issues/4
  # And https://github.com/lutris/lutris/issues/5659
  protoc --proto_path=galaxy_blizzard_plugin/src --python_out=. product_db.proto
  cp -vf product_db_pb2.py lutris/lutris/util/battlenet/product_db_pb2.py
}

build() {
  arch-meson lutris build
  ninja -C build
}

check() {
  cd lutris
  xvfb-run nosetests
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
