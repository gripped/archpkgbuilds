# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Renato Budinich <rennabh at gmail dot com>

pkgname=wah-plugins
_name=WAH-plugins
pkgver=0.1.0
pkgrel=8
pkgdesc="LADSPA Wah filter plugin"
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio/ladspa/"
license=(GPL-2.0-or-later)
groups=(
  ladspa-plugins
  pro-audio
)
depends=(
  gcc-libs
  glibc
)
makedepends=(ladspa)
source=(
  https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$_name-$pkgver.tar.bz2
  $pkgname-0.1.0-makefile.patch
)
sha512sums=('e3063115d2e635bee30ac4792d0c4f3536de79f25c153d790a1721c0f636636feeef5f7dd739c35443a68be058943ca29b4825dceef9bc56f862dbd7c7958ada'
            '6e814c11cb25862bdc00b68df7b2bd9e79ed6cebc00b9b22a87ba78f6aacaed78115a22c5d30318cc91b9d5030a1718541d76592e78ffb09df2cb055bd8a1a3d')
b2sums=('be526282af8752ad82917ad8d96856d7665d3ed0f13fcada86ab400ca03471641721dfb8209967430fcb270c8bf4c7120e34d89f6078d9323b8f0e5743690c98'
        'af765f569f97446acf127257b148c6d4666d585065e38624fba19537fc57e4aa13ad35cf80db68b32f42729392098c691498fa527e8849d2d6226424892052d2')

prepare() {
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-0.1.0-makefile.patch
}

build() {
  make -C $_name-$pkgver
}

package() {
  cd $_name-$pkgver

  # plugin
  install -vDm 755 autowah.so "$pkgdir/usr/lib/ladspa/autowah.so"
  # docs
  install -vDm 644 {AUTHORS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
