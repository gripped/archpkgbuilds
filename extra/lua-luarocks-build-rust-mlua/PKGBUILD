# Maintainer: Caleb Maclennan <caleb@alerque.com>

_rockname=luarocks-build-rust-mlua
pkgname=("lua-$_rockname" "lua53-$_rockname" "lua52-$_rockname" "lua51-$_rockname")
pkgver=0.2.3
_rockrel=1
pkgrel=1
pkgdesc='A LuaRocks build backend for Lua modules written in Rust using mlua'
arch=(any)
url="https://github.com/khvzak/$_rockname"
license=(MIT)
depends=(cargo)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$_rockname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('6f48d9073a88b43a708e025c99014c2a39eeaf04fabdb7433ed06dca1d512a1b')

_package() {
	cd "$_archive"
	depends+=("${pkgname%%-*}")
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		make --deps-mode none --no-manifest "$_archive-$_rockrel.rockspec"
}

package_lua-luarocks-build-rust-mlua() {
	_package 5.4
}

package_lua51-luarocks-build-rust-mlua() {
	_package 5.1
}

package_lua52-luarocks-build-rust-mlua() {
	_package 5.2
}

package_lua53-luarocks-build-rust-mlua() {
	_package 5.3
}
