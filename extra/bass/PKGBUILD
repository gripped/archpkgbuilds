# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: rabyte <rabyte__gmail>

pkgname=bass
pkgver=1.2
pkgrel=16
# Note: Including full name here so people can find it using the long name
pkgdesc="Beneath A Steel Sky is a 2D point-and-click science fiction thriller set in a bleak vision of the future"
arch=('any')
url="http://revolution.co.uk/games/beneath-a-steel-sky/"
license=('LicenseRef-Freeware')
depends=('scummvm>=0.5')
source=(https://downloads.sourceforge.net/scummvm/$pkgname-cd-$pkgver.zip
        $pkgname.sh
        $pkgname.desktop
        $pkgname.svg
        $pkgname.xpm)
sha512sums=('ff896e723b4dbf2cc52851ee1260c1006aa285b768a727974719d63509bdac1c65be9ea565eb037d5e9e1c8fd8fd76a8c04776bcde46616ca9ccce18ea498d64'
            '89a12d3fa558ddd548bf06a9c3c1fb5dcefcd32256ae9cbaa440dcebf46fb5b3fc3d7b9196b7d9bb0dd85825fccfe0d5f559a94266eaa6b3f7bcb32657aebad8'
            '45c4189a4a646be408047769614dd000068b16754100cf24d10026974d2ee6bfb6898549c6192cb7377ec771efe13af11b196813e12c04e9b98ead24d25c1bd8'
            'c2c6a0e9898b08c445263dc42b02e215248e0897fec2b3c2b9ed7c730d1244cc9e805d341a76ad95357d49bdf2f176528757782a84e13f62d806d8a1ae5f5c3e'
            '23613aa817acc5dc8d48755fd8c5e70d070e3e1d967647a5d255f36201eeaed27c82477b5c567c3baac79eccc20fc68bb398abac2c5903759742ce633940bcbd')

package() {
  cd $pkgname-cd-$pkgver

  install -d "$pkgdir"/usr/share/{licenses/,}bass
  install -m 644 readme.txt "$pkgdir"/usr/share/licenses/bass/
  install -m 644 sky.{cpt,dnr,dsk} "$pkgdir"/usr/share/bass/
  install -Dm 755 "$srcdir"/bass.sh "$pkgdir"/usr/bin/bass

  # Desktop entry and icons
  install -Dm644 "$srcdir"/bass.desktop "$pkgdir"/usr/share/applications/bass.desktop
  install -Dm644 "$srcdir"/bass.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/bass.svg
  install -Dm644 "$srcdir"/bass.xpm "$pkgdir"/usr/share/pixmaps/bass.xpm
}
