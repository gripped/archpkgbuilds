# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Mikhail Swift <mikhail.swift@gmail.com>

pkgname=lazydocker
pkgver=0.24.2
pkgrel=1
pkgdesc='A simple terminal UI for docker and docker-compose, written in Go with the gocui library.'
arch=('1686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url='https://github.com/jesseduffield/lazydocker'
license=('MIT')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jesseduffield/lazydocker/archive/v${pkgver}.tar.gz")
b2sums=('89bb12c4200eb8e168ddbd992c447c71c08828f79e0ffacd367a1a1f479b30a0e7229e9d9d6081d50cc5f2ab8ce9300639329682d1e31d1965a2800480088772')

build() {
	cd "${pkgname}-${pkgver}"

	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	go build -o ${pkgname} \
		-ldflags "-extldflags \"${LDFLAGS}\" -s -w -X main.version=${pkgver}" main.go
}

package() {
	cd "${pkgname}-${pkgver}"

	install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
