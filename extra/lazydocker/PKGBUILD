# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Mikhail Swift <mikhail.swift@gmail.com>

pkgname=lazydocker
pkgver=0.24.1
pkgrel=2
pkgdesc='A simple terminal UI for docker and docker-compose, written in Go with the gocui library.'
arch=('1686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url='https://github.com/jesseduffield/lazydocker'
license=('MIT')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jesseduffield/lazydocker/archive/v${pkgver}.tar.gz")
b2sums=('3931aace12de32c6c9319fe48c1f74892293bfeeb9f207dfb451efdab236c5e38517b5ec65a02331a06dd4ac93fcf17dcd0997a78c7e6053e056eacdccbc9331')

build() {
	cd "${pkgname}-${pkgver}"

	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	go build -o ${pkgname} \
		-ldflags "-extldflags \"${LDFLAGS}\" -s -w -X main.version=${pkgver}" main.go
}

package() {
	cd "${pkgname}-${pkgver}"

	install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
