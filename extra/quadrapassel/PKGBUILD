# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=quadrapassel
pkgver=40.2
pkgrel=3
pkgdesc="Fit falling blocks together (Tetris-like game for GNOME)"
url="https://wiki.gnome.org/Apps/Quadrapassel"
arch=(x86_64)
license=(GPL)
depends=(clutter-gtk gsound librsvg libmanette)
makedepends=(meson gobject-introspection yelp-tools appstream-glib git vala)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/quadrapassel.git#tag=$pkgver")
b2sums=('9a3e80d1cc1e973e068a547e86fa6b572554dfb5274e8d932305b7d797f8175db35a3dbf45fcba11545e6b17002e8d18ddad44eabc3cc41196bbc68313303b28')

prepare() {
  cd $pkgname

  # Fix segmentation fault when press any key just after application started
  git cherry-pick -n 8704e0fbcd994df45f2492c58f6ac261bfeb382f
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
