# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=quadrapassel
pkgver=49.2.1
pkgrel=1
pkgdesc="Fit falling blocks together (Tetris-like game for GNOME)"
url="https://wiki.gnome.org/Apps/Quadrapassel"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gsound
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libmanette
  librsvg
  pango
)
makedepends=(
  blueprint-compiler
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/quadrapassel.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('6c4e2ce084bfcce42af8b2a0952bb6faead9339d2e27f499bbf7b774c3af48626c485fe07f024ba02cc7b7010ed5ecd5b4ed8e3dcf793a1af0d82e4803695f4d')
validpgpkeys=(
  161DEEB19F942B2CE00B67ADC1C19D5530553963 # John Ward <john@johnward.net>
  1DB827B47C4E2494A1D733586458908EC334A207 # Will Warner <willwwarner@proton.me>
  C6043B3C11E1C16FCA1FBEE48D0B2EC86F801EA9 # Will Warner <willwwarner@proton.me>
)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
