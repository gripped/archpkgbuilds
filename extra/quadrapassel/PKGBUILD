# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=quadrapassel
pkgver=49.2
pkgrel=1
pkgdesc="Fit falling blocks together (Tetris-like game for GNOME)"
url="https://wiki.gnome.org/Apps/Quadrapassel"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gsound
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libmanette
  librsvg
  pango
)
makedepends=(
  blueprint-compiler
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/quadrapassel.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('f41cda0a83de273a1552a8e8a9dde3137a05a8d4af79f89b26075c9bb5db5387a1578b90e6d3f8ff0c02652d904d28d89405511f4051de31cf719968dd7b30bb')
validpgpkeys=(
  161DEEB19F942B2CE00B67ADC1C19D5530553963 # John Ward <john@johnward.net>
  1DB827B47C4E2494A1D733586458908EC334A207 # Will Warner <willwwarner@proton.me>
  C6043B3C11E1C16FCA1FBEE48D0B2EC86F801EA9 # Will Warner <willwwarner@proton.me>
)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
