# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-curl_cffi
pkgver=0.14.0
_curl_version=curl-8_15_0
_curl_impersonate_version=1.3.1
pkgrel=2
pkgdesc='Python FFI binding for curl-impersonate'
arch=(x86_64)
url='https://github.com/lexiforest/curl_cffi'
license=(MIT)
depends=(
  glibc
  gcc-libs
	"curl-impersonate=${_curl_impersonate_version}"
	python
	python-certifi
	python-cffi
	python-eventlet
	python-gevent
	python-typing_extensions
)
makedepends=(
  git
	python-build
	python-installer
	python-setuptools
	python-wheel
	python-python-multipart
	unzip
)
optdepends=(
  'python-orjson: speed and memory optimized JSON parsing'
  'python-markdownify'
  'python-lxml-html-clean'
  'python-readability-lxml'
)
source=(
	"$pkgname::git+$url#tag=v$pkgver"
  "${_curl_version}.tar.gz::https://github.com/curl/curl/archive/${_curl_version}.tar.gz"
  "curl-impersonate::git+https://github.com/lexiforest/curl-impersonate#tag=v${_curl_impersonate_version}"
	use-system-curl-impersonate.patch
  no-download.patch
)
sha512sums=('0cc2e0917973bddf24c72db5c9775fe4433601257f4ff333ccc3cec625fc043e713118cd52d9ed550285adce4dd263c64f6eccf113f01bd99e7a8f88421b2183'
            'd4a560e225d0110133f44ed57cf5394c1710530c5fec395d02baafaac9ea2186dd543047ae27fd7542894b8744070760516ae611602105b1b40605abbf84e684'
            '5ae2d97a521287fcf73488c5a220d8e7996cdcac429ad8238b0357122ee110edc09cfbb6a1bc27357d4b61d4155710181391f26cf9f7d58f362f6bf3e52cad87'
            '8faa57148e8079323c005485d8430146f23fbdc168197a70adc81cb0c810574b6f1390537235e8859517e9b4d29fa3151e8b93bc44ead2144c315383c66f77ed'
            'bec73973ec2aff8605a56a6d7761a0f4232a7c94f46c7a1dfe33c8007352cf956958f25469260aac2e9bd4c468de406c3350117c35916687bf0b239e5a261533')
b2sums=('a190f12d669557b304a6c8c4b38da392f803aa7c821fef6a4522c7551e697db07db9153ef762327205dd8abf5d1e3fec21e8eb08bcd4551c88fb67ed163c8b6a'
        '5f2d2e5f498495744e3b28ea375596f3e4213f32b5eb45ea9942c0339ad1541d0d98b1d4a774bfa4ce431fe23d81f860f4c150f325610d61286f2aea0a93f770'
        '32327bc55f71f773a80044df86ef928434518cdcd2062aad6a92a235706f5128e07a2eaf3a24012b22d03486f4bbf0f24e1e6c5a9f3852b582469545db92e6ea'
        'd6c6113ac618dfa992fc7134cde695c5f92429f4431b5c4eab56175d287c497aca812aa6c8f97d9d98df7b2c2280866df02ee7b09795d58230c74031b62e558b'
        'b99ecb48a74c9b1da2815ebb437e2b50d20a12da6c92398fa8165a453aaa212a215bc54877fb185bb765d9cafd90471db441a9ead4a68b28587ab4e9921c259a')

prepare() {
  cd "$pkgname"

  # use system curl-impersonate
  patch -p1 -i "$srcdir/use-system-curl-impersonate.patch"

  # shuffle around dependencies to match folder structure expected in Makefile
  mv "$srcdir/curl-${_curl_version}" "${_curl_version}"
  mv "$srcdir/curl-impersonate" "curl-impersonate-${_curl_impersonate_version}"

  # patch Makefile for file verification
  patch -p1 -i "$srcdir/no-download.patch"
  sed \
    -e "s/@CURL_IMPERSONATE_VERSION@/${_curl_impersonate_version}/" \
    -e "s/@CURL_VERSION@/${_curl_version}/" \
    -i Makefile \
    -i scripts/build.py

  make preprocess
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
