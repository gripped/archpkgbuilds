# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-polkit-agent
pkgver=8.0.2
pkgrel=1
pkgdesc='Pantheon Polkit Agent'
arch=(x86_64)
url=https://github.com/elementary/pantheon-agent-polkit
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgranite-7.so
  pantheon-wayland
  polkit
)
makedepends=(
  cmake
  git
  meson
  intltool
  vala
)
_tag=9e93b53ebb3d7784eba4908243f168662da434da
source=(pantheon-polkit-agent::git+https://github.com/elementary/pantheon-agent-polkit.git#tag=${_tag})
b2sums=('0162c18b3b34baba28ebc3047e5b6c8c0ecb43616b644677c1dcb3cecc038ddfdb19e8339657e72b3896b284224cae96b21e1fd6f426779c896446cd3529d185')

pkgver() {
  cd pantheon-polkit-agent
  git describe --tags
}

build() {
  arch-meson pantheon-polkit-agent build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
