# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-polkit-agent
pkgver=8.0.1
pkgrel=1
pkgdesc='Pantheon Polkit Agent'
arch=(x86_64)
url=https://github.com/elementary/pantheon-agent-polkit
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgranite-7.so
  pantheon-wayland
  polkit
)
makedepends=(
  cmake
  git
  meson
  intltool
  vala
)
_tag=1866e51f2dd21b5280c5c2cc16d83ef5ef26fcf4
source=(pantheon-polkit-agent::git+https://github.com/elementary/pantheon-agent-polkit.git#tag=${_tag})
b2sums=('b6a9555b6dd61f09c7ac624ec457c2b870d51b8fac7f9a23d5cdd2cf57638ea6f7c296c6330698860e0737c84ef37df8e56d88443ffd29224d1a3f91673f868c')

pkgver() {
  cd pantheon-polkit-agent
  git describe --tags
}

build() {
  arch-meson pantheon-polkit-agent build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
