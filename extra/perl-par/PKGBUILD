# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-par
pkgver=1.021
pkgrel=1
pkgdesc='Perl Archive Toolkit'
arch=('any')
url='https://metacpan.org/release/PAR'
license=('GPL')
depends=('perl-archive-zip' 'perl-par-dist')
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/R/RS/RSCHUPP/PAR-${pkgver}.tar.gz")
b2sums=('17a57e577cd886a8486b4a3766383fbd7812edd49f9da1ca8c643918536337c62d0e52b54008aae9a0275b1f61d038bc63a6daabaaf25fdc7db08b62669ab9c0')

build() {
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}
check(){
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}
package() {
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
}
