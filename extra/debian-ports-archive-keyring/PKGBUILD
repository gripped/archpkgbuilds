# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=debian-ports-archive-keyring
pkgver=2025.01.01
pkgrel=1
pkgdesc="GnuPG archive keys of the debian-ports archive"
arch=('any')
url="https://salsa.debian.org/aurel32/debian-ports-archive-keyring"
license=('GPL-2.0-or-later')
depends=('gnupg')
makedepends=('git')
source=("git+https://salsa.debian.org/aurel32/debian-ports-archive-keyring.git#tag=debian/$pkgver")
sha512sums=('0182add7ee92039ea05dd7b6b8073ac9315995811ff82dc529745eb0653a216ed4732118822e66e7342db3f7f65a7921268a46958607f07e443a059c4dd1ec80')

build() {
  cd $pkgname
  # Translated from debian/rules

  # Build keyrings
  mkdir -p build/keyrings
  gpg --no-keyring --import-options import-export --import active-keys/* > build/keyrings/debian-ports-archive-keyring.gpg
  gpg --no-keyring --import-options import-export --import removed-keys/* > build/keyrings/debian-ports-archive-keyring-removed.gpg
  
  # Build fragment files
  mkdir -p build/trusted.gpg.d
  for k in $(ls active-keys); do
    gpg --armor --no-keyring --import-options import-export --import active-keys/$k > build/trusted.gpg.d/${k%.key}.asc
  done
}

package() {
  cd $pkgname
  install -Dm644 build/trusted.gpg.d/*asc -t "$pkgdir"/etc/apt/trusted.gpg.d/
  install -Dm644 build/keyrings/*gpg -t "$pkgdir"/usr/share/keyrings/
}

# vim:set ts=2 sw=2 et:
