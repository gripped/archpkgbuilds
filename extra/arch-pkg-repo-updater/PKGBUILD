# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=arch-pkg-repo-updater
pkgver=0.0.3
pkgrel=1
pkgdesc='Arch Linux packaging Git repository updater'
url='https://gitlab.archlinux.org/archlinux/buildbtw/-/tree/main/arch-pkg-repo-updater'
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libgit2
  openssh
  openssl
)
makedepends=(
  cargo
  git
  sqlite
)
options=(!lto)
source=(buildbtw::"git+https://gitlab.archlinux.org/archlinux/buildbtw.git#tag=${pkgname}/v${pkgver}?signed")
sha256sums=('71a007a82b006c0cdcf8ebdf1b67b726ce3212ebfb01b71c0151d1785b156f47')
b2sums=('b760b3696ec0ca3b3ae8f33b01e54f2f21de0b9704721f91b85a1c7e50627c6498b8f60b1eff552e732b49d20b7fda89833e5d4763ce4f35d4e354301dd0b1ca')
validpgpkeys=(
  E240B57E2C4630BA768E2F26FC1B547C8D8172C8 # Levente Polyak <anthraxx@archlinux.org>
  AAE2599CE2DFD58B8E884726B4EFE6DC59FAE118 # Rafael Eppl√©e <raffomania@archlinux.org>
  8FC15A064950A99DD1BD14DD39E4B877E62EB915 # Sven-Hendrik Haase <svenstaro@archlinux.org>
  F00B96D15228013FFC9C9D0393B11DAA4C197E3D # Christian Heusel <gromit@archlinux.org>
)

prepare() {
  cd buildbtw/${pkgname}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd buildbtw/${pkgname}
  cargo build --package ${pkgname} --frozen --release
}

check() {
  cd buildbtw/${pkgname}
  cargo test --package ${pkgname} --frozen
}

package() {
  cd buildbtw
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
