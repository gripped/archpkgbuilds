# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=arch-pkg-repo-updater
_pkgname=repo-updater
pkgver=0.0.4
pkgrel=1
pkgdesc='Arch Linux packaging Git repository updater'
url='https://gitlab.archlinux.org/archlinux/buildbtw/-/tree/main/src/bin/repo-updater'
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libgit2
  openssh
  openssl
)
makedepends=(
  cargo
  git
  sqlite
)
options=(!lto)
source=(buildbtw::"git+https://gitlab.archlinux.org/archlinux/buildbtw.git#tag=v${pkgver}?signed")
sha256sums=('4e3bbb559f9271f0de5fa4d89d8d8963d385292f124e970182b7edf02a6f6894')
b2sums=('3803ab01f6b8861e8f44b27c09327c4c7373fdf0a088893d7980912abb0b62b135756109cbe0d5cba969f5bdc35e9ed7068ba6ead33af2ea03fe8ce6330bfc6f')
validpgpkeys=(
  E240B57E2C4630BA768E2F26FC1B547C8D8172C8 # Levente Polyak <anthraxx@archlinux.org>
  AAE2599CE2DFD58B8E884726B4EFE6DC59FAE118 # Rafael Eppl√©e <raffomania@archlinux.org>
  8FC15A064950A99DD1BD14DD39E4B877E62EB915 # Sven-Hendrik Haase <svenstaro@archlinux.org>
  F00B96D15228013FFC9C9D0393B11DAA4C197E3D # Christian Heusel <gromit@archlinux.org>
)

prepare() {
  cd buildbtw
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd buildbtw
  cargo build --bin ${_pkgname} --frozen --release
}

check() {
  cd buildbtw
  cargo test --bin ${_pkgname} --frozen
}

package() {
  cd buildbtw
  install -Dm 755 target/release/${_pkgname} "${pkgdir}/usr/bin/${pkgname}"
}

# vim: ts=2 sw=2 et:
