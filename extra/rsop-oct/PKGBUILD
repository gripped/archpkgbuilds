# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop-oct
_name=${pkgname/-*}
pkgver=0.1.2
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) CLI tool for OpenPGP card devices based on rPGP and rpgpie"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  dbus
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('d152f368af324aa8ba1a8278bd59915b0f355bf527c73e2ba3bde820b62c6b15fef732ce724692cbee552bf87874c92fb72cdf23ea2879e6cc9f9a77aa037bb2')
b2sums=('e7cfb618c6bf196cde0aff85d0ceea339ec4e83f7506ddbe38fd7288e35ba58ef60a0a6837b80ac58de0b50ec962a672c1390490609f2275f23e5605af220303')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 0755 "target/release/rsoct" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
