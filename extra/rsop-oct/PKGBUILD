# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop-oct
_name=${pkgname/-*}
pkgver=0.1.3
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) CLI tool for OpenPGP card devices based on rPGP and rpgpie"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  dbus
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('f4b326eb739b9e270454c3e7a9f71b4a243ada616c7314aa18b4e1337f527a343c03d1397d72828eee2f6e78e67b31dddc499cf56a85c70e45c3a20abe7c18e8')
b2sums=('a44768d8b3111baf4bd411b2bb9ca8febb5d17f4fbfabb577faf218a0ef0f73e9aa85aa68d491962f42867c301b8bab98116bf4986b2b8d068ae26668eee9035')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 0755 "target/release/rsoct" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
