# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop-oct
_name=${pkgname/-*}
pkgver=0.1.4
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) CLI tool for OpenPGP card devices based on rPGP and rpgpie"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  dbus
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('50e8a5694dc92fb98c522ef464f18e176e20e9a0a9766c5a2dd2da159dcbb575ab2b20a6bcbcf919229b64a0b894ee291702c931a1a5fafbc4c431ad728b8f63')
b2sums=('d37cbc51597e7f8ecb5d2f7fb44d3f5412860083541a59be9be6328180ac67f45cd57317e3ba0bf0f6425fdfdc4824e5069f8071e3b5537917a7b9902a09b75c')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 0755 "target/release/rsoct" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
