# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop-oct
_name=${pkgname/-*}
pkgver=0.1.5
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) CLI tool for OpenPGP card devices based on rPGP and rpgpie"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  dbus
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('3e1bff8610c011e775f63c729a80ea69cb30a92c727e38fbb440454aa9c18c5f779d7ac225b39f0718459a617acf149ae66a78bc6d6bfd37b66c9dd46283bcd3')
b2sums=('5a316c4c6cb0da783d6d4552e8c3ee0912b0f2a8ec4d3110d38a3ca9a404196e2490ad364fc96d8a1b9d228eef3edc3e10baf7365644585f91f5531415510cd9')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 0755 "target/release/rsoct" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
