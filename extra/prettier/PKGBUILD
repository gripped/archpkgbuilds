# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>

pkgname=prettier
pkgver=3.4.2
pkgrel=2
pkgdesc='An opinionated code formatter'
arch=(any)
url=https://prettier.io
license=(MIT)
depends=(nodejs-lts-jod)
makedepends=(
  gcc
  git
  make
  python
  yarn
)
checkdepends=(npm)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver")
b2sums=('74c125d02f75a5e253f10b3aa64e23cbdfe8a3dab812ca727d7bf19e5fa02ab2ace5d10630d9e87ceb227ee2a562286544922b2d41d21538a0e172c98c8e986d')

prepare() {
  cd $pkgname
  # https://github.com/prettier/prettier/issues/16930
  git cherry-pick --no-commit c450b6ad8e5e18d8e60182057b76cc728ffc9425

  yarn install --immutable
}

build() {
  cd $pkgname
  yarn build
}

check() {
  cd $pkgname
  yarn test:dist
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,usr/share/licenses/$pkgname,$mod_dir}
  ln -s $mod_dir/bin/$pkgname.cjs "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  cp -a dist/* "$pkgdir"/$mod_dir
}
