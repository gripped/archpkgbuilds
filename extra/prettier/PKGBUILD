# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>

pkgname=prettier
pkgver=3.4.1
pkgrel=1
pkgdesc='An opinionated code formatter'
arch=(any)
url=https://prettier.io
license=(MIT)
depends=(nodejs)
makedepends=(
  gcc
  git
  make
  python
  yarn
)
checkdepends=(npm)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver")
b2sums=('5fda7f9ab1791cc99de3eeade446fa101adde3ef44c654793aab6ca1edfddae89e1a97bc7cd0137810753d1588518fb8b39a24f72c0dfee2b574d2c0062e117b')

prepare() {
  cd $pkgname
  yarn install --immutable
}

build() {
  cd $pkgname
  yarn build
}

check() {
  cd $pkgname
  yarn test:dist
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,usr/share/licenses/$pkgname,$mod_dir}
  ln -s $mod_dir/bin/$pkgname.cjs "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  cp -a dist/* "$pkgdir"/$mod_dir
}
