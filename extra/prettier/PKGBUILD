# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>

pkgname=prettier
pkgver=3.5.3
pkgrel=2
pkgdesc='An opinionated code formatter'
arch=(any)
url=https://prettier.io
license=(MIT)
depends=(nodejs)
makedepends=(
  gcc
  git
  make
  python
  yarn
)
checkdepends=(npm)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver")
b2sums=('07822c9279ce026cf4c8011f2e2ce4f7495661b75f10007f368ce00d34b2cb0289e03f1e6abfa0a648615561b74d41598d76fe20298caf33e8f6180480afdd46')

prepare() {
  cd $pkgname
  yarn install --immutable

  # Fix build script for Node.js v23
  git cherry-pick --no-commit 7af49a1ab3cb484bafe114df58b5f21dd9940336 || true
}

build() {
  cd $pkgname
  yarn build
}

check() {
  cd $pkgname
  yarn test:dist
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,usr/share/licenses/$pkgname,$mod_dir}
  ln -s $mod_dir/bin/$pkgname.cjs "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  cp -a dist/* "$pkgdir"/$mod_dir
}
