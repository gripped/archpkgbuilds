# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>

pkgname=prettier
pkgver=3.5.2
pkgrel=1
pkgdesc='An opinionated code formatter'
arch=(any)
url=https://prettier.io
license=(MIT)
depends=(nodejs-lts)
makedepends=(
  gcc
  git
  make
  python
  yarn
)
checkdepends=(npm)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver")
b2sums=('a9995f84ac81afb0fd1d89e77432656bbbc604fad0c57b926bd3c0d83a78dd0ab8972623bc558207671a11569573e7c72e6403a14c315448f48088f0bb306441')

prepare() {
  cd $pkgname
  yarn install --immutable
}

build() {
  cd $pkgname
  yarn build
}

check() {
  cd $pkgname
  yarn test:dist
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,usr/share/licenses/$pkgname,$mod_dir}
  ln -s $mod_dir/bin/$pkgname.cjs "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  cp -a dist/* "$pkgdir"/$mod_dir
}
