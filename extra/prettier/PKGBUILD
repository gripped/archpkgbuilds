# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>

pkgname=prettier
pkgver=3.5.1
pkgrel=1
pkgdesc='An opinionated code formatter'
arch=(any)
url=https://prettier.io
license=(MIT)
depends=(nodejs-lts)
makedepends=(
  gcc
  git
  make
  python
  yarn
)
checkdepends=(npm)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver")
b2sums=('fa6b913c631950c542fdd00d725d74bb428f859c46a509ea1fba02254459bf1281c8988b6c8b224b955ff651498856aea4ce1abb3643209777f765d253f2e1e5')

prepare() {
  cd $pkgname
  yarn install --immutable
}

build() {
  cd $pkgname
  yarn build
}

check() {
  cd $pkgname
  yarn test:dist
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,usr/share/licenses/$pkgname,$mod_dir}
  ln -s $mod_dir/bin/$pkgname.cjs "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  cp -a dist/* "$pkgdir"/$mod_dir
}
