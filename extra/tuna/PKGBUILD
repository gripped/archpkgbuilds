# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=tuna
pkgver=0.20
pkgrel=2
epoch=1
pkgdesc="Thread and IRQ affinity setting GUI and cmd line tool"
arch=(any)
url="https://git.kernel.org/pub/scm/utils/tuna/tuna.git/"
license=(GPL-2.0-only)
groups=(realtime)
depends=(
  python
  python-linux-procfs
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
# python-inet_diag is not compatible with python3:
# https://bugs.archlinux.org/task/69208
optdepends=(
  'python-gobject: for oscilloscope'
  'python-matplotlib: for oscilloscope'
  'python-numpy: for oscilloscope'
  'python-perf: for process view'
)
backup=(etc/$pkgname.conf)
source=(https://git.kernel.org/pub/scm/utils/$pkgname/$pkgname.git/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('0ff5fe03d0c74f6de9f5a45393249dec0261bb387af9d1ed25fcc4cca0c1c05ff94718806f169ae12833aeab7249117f7713ec66fa561437c8df0305409f0739')
b2sums=('b3f4b40ce412d8f07b806d0d7a9e21b83a473c6711bf3bedae24989c7715f86ae856deae25cf441870768dc13369fc41d474b81b82c63020c748e8623ba2ccb2')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  local _language _translation

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 755 oscilloscope-cmd.py "$pkgdir/usr/bin/oscilloscope"
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 755 $pkgname-cmd.py "$pkgdir/usr/bin/$pkgname"

  install -vDm 644 $pkgname/${pkgname}_gui.glade -t "$pkgdir/usr/share/$pkgname/"

  install -vDm 644 etc/$pkgname/example.conf -t "$pkgdir/etc/$pkgname/"
  install -vDm 644 etc/$pkgname.conf -t "$pkgdir/etc/"
  install -vDm 644 docs/oscilloscope+$pkgname.pdf -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 help/kthreads/* -t "$pkgdir/usr/share/$pkgname/help/kthreads"
  install -vDm 644 org.$pkgname.policy -t "$pkgdir/usr/share/polkit-1/actions/"
  install -vDm 644 docs/$pkgname.8 -t "$pkgdir/usr/share/man/man8/"

  # translations
  while read _language; do
    install -vdm 755 "$pkgdir/usr/share/locale/$_language/LC_MESSAGES"
    msgfmt po/$_language.po -o "$pkgdir/usr/share/locale/$_language/LC_MESSAGES/$pkgname.mo"
  done < po/LINGUAS
}
# vim:set ts=2 sw=2 et:
