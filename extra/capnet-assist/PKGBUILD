# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=capnet-assist
pkgver=8.0.1
pkgrel=1
pkgdesc='Captive Portal Assistant'
arch=(x86_64)
url=https://github.com/elementary/capnet-assist
license=(GPL3)
groups=(pantheon)
depends=(
  gcr-4
  glib2
  gtk4
  libadwaita-1.so
  libgranite-7.so
  networkmanager
  webkitgtk-6.0
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=09fa51bef6f7e7ff45246d23322710a34d134302
source=(git+https://github.com/elementary/capnet-assist.git#tag=${_tag})
b2sums=('7d61ed7a4c8285605589726f471e37809cc8247a51231aa56b7481a78b504050ab64ae82c9fe19d5fcf783950cb8cac6262424f2628102241f40507af053c9eb')

pkgver() {
  cd capnet-assist
  git describe --tags
}

build() {
  arch-meson capnet-assist build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
