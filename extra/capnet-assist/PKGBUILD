# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=capnet-assist
pkgver=8.0.0
pkgrel=1
pkgdesc='Captive Portal Assistant'
arch=(x86_64)
url=https://github.com/elementary/capnet-assist
license=(GPL3)
groups=(pantheon)
depends=(
  gcr-4
  glib2
  gtk4
  libadwaita-1.so
  libgranite-7.so
  libsoup
  networkmanager
  webkitgtk-6.0
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=3dd49f55ddf9d61e7fb1cafdb44a85a8baf6cf6e
source=(git+https://github.com/elementary/capnet-assist.git#tag=${_tag})
b2sums=('e9027f622fb1dddb7a65f4054e34c32138737f029ec8873d146944021c7c508f19c9fcfb768aad9f605b009cd630d82c45438a403da2984388c0243e43f42cfb')

pkgver() {
  cd capnet-assist
  git describe --tags
}

build() {
  arch-meson capnet-assist build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
