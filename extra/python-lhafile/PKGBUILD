# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: grimi

pkgname=python-lhafile
pkgver=0.3.1
pkgrel=2
pkgdesc='LHA archive support for Python'
arch=('x86_64')
url='https://github.com/FrodeSolheim/python-lhafile/'
license=('BSD-3-Clause')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://github.com/FrodeSolheim/python-lhafile/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('df3b0966e25905dbf55c190d4f60551b307489684a028fe081e33a0f60a2fd65')

build() {
    cd "${pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    python -m installer --destdir="$pkgdir" "${pkgname}-${pkgver}/dist"/*.whl
    install -D -m644 "${pkgname}-${pkgver}/COPYING.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
