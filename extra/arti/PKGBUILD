# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=arti
pkgver=1.2.3
pkgrel=1
pkgdesc="An implementation of Tor in Rust"
arch=('x86_64')
url="https://gitlab.torproject.org/tpo/core/arti"
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
  'liblzma.so'
  'libsqlite3.so'
  'libssl.so'
)
makedepends=('cargo')
backup=('etc/arti.d/arti.toml')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/$pkgname-v$pkgver/$pkgname-$pkgname-v$pkgver.tar.gz")
sha256sums=('2cfbbc6aa203e0203f773158c4994ee0d55f7213c488281febeef9a15e25aec4')
b2sums=('f4bb9d139942fee587ab4a80eac2a02744b428b3e900b5bb9331896fa8c06600caa0b92c2e125f90b5a2161b61485c6059a43a4c7623aa1290fe7fe610a8eb17')

prepare() {
  mv "$pkgname-$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --features full
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
  "target/release/$pkgname" --help
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"

  # config
  install -Dm 640 crates/arti/src/arti-example-config.toml "$pkgdir/etc/arti.d/arti.toml"

  # docs
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" \
    README.md \
    crates/arti/src/arti-example-config.toml

  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
