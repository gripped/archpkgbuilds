# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=arti
pkgver=1.3.1
pkgrel=1
pkgdesc="An implementation of Tor in Rust"
arch=('x86_64')
url="https://gitlab.torproject.org/tpo/core/arti"
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
  'liblzma.so'
  'libsqlite3.so'
  'libssl.so'
)
makedepends=('cargo')
backup=('etc/arti.d/arti.toml')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/$pkgname-v$pkgver/$pkgname-$pkgname-v$pkgver.tar.gz")
sha256sums=('dd841bfb9e9c11d77ae5ed136d3f0e7a3af570cce5dc0164bbb7f5ea9c26708d')
b2sums=('bd9ce1ad97395f87f6f77d0fee47a00755aef07eb9154ff1496de9f3282a010cede34d1a1b42285ad33026cac9b9892832c15867b73503d9a69e8c9a3901bd5e')

prepare() {
  mv "$pkgname-$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --features full
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
  "target/release/$pkgname" --help
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"

  # config
  install -Dm 640 crates/arti/src/arti-example-config.toml "$pkgdir/etc/arti.d/arti.toml"

  # docs
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" \
    README.md \
    crates/arti/src/arti-example-config.toml

  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
