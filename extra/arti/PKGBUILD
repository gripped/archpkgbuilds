# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=arti
pkgver=2.0.0
pkgrel=1
pkgdesc="An implementation of Tor in Rust"
arch=('x86_64')
url="https://gitlab.torproject.org/tpo/core/arti"
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
  'liblzma.so'
  'libsqlite3.so'
  'libssl.so'
)
makedepends=('cargo')
backup=('etc/arti.d/arti.toml')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/$pkgname-v$pkgver/$pkgname-$pkgname-v$pkgver.tar.gz")
sha256sums=('19106e4706222de65f8e735193ec50ba4c312abc2a28b48cca7e13d01fdeacab')
b2sums=('8bcc19451e74c433895d595da1636f05c0a7de1f957cee3285aa0e25d85c2949695ba4fe6907eec15cb431ebee64f8075c177045dbc7aa9944c53904cc705f22')

prepare() {
  mv "$pkgname-$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --features full -p arti
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
  "target/release/$pkgname" --help
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"

  # config
  install -Dm 640 crates/arti/src/arti-example-config.toml "$pkgdir/etc/arti.d/arti.toml"

  # docs
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" \
    README.md \
    crates/arti/src/arti-example-config.toml

  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
