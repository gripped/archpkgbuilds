# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=arti
pkgver=1.9.0
pkgrel=1
pkgdesc="An implementation of Tor in Rust"
arch=('x86_64')
url="https://gitlab.torproject.org/tpo/core/arti"
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
  'liblzma.so'
  'libsqlite3.so'
  'libssl.so'
)
makedepends=('cargo')
backup=('etc/arti.d/arti.toml')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/$pkgname-v$pkgver/$pkgname-$pkgname-v$pkgver.tar.gz")
sha256sums=('5a5bab65e6575dfea7c174f73cc372b23e21030dc69805695d70bfa00efed7e2')
b2sums=('65f5da1681f6c0f85b52899802365746723853ef3bbdd04061210e80de0f606a9c9aa6198c2a6ef13e41052e9a9885fe9ce045e52b8c3a8c6583f7a20402a88f')

prepare() {
  mv "$pkgname-$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --features full -p arti
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
  "target/release/$pkgname" --help
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"

  # config
  install -Dm 640 crates/arti/src/arti-example-config.toml "$pkgdir/etc/arti.d/arti.toml"

  # docs
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" \
    README.md \
    crates/arti/src/arti-example-config.toml

  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
