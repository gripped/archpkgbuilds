# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=4.0.6
pkgrel=1
pkgdesc='A KDE program that browses samba shares'
arch=(x86_64)
url='https://smb4k.sourceforge.net/'
license=(GPL-2.0-or-later)
depends=(glibc
         kauth
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kdnssd
         kdsoap-qt6
         kdsoap-ws-discovery-client
         ki18n
         kiconthemes
         kio
         kjobwidgets
         knotifications
         kstatusnotifieritem
         kwallet
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libstdc++
         qt6-base
         qt6-declarative
         qtkeychain-qt6
         smbclient
         solid)
makedepends=(extra-cmake-modules
             kdoctools
             kirigami
             libplasma)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('5ae52e1f1a66bd7d0f1a9bcaccc80447e0b8ea181c40c87119b2427546ed0e26')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DSMB4K_WITH_WS_DISCOVERY=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
