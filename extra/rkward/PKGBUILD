# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor : Vassilis Palassopoulos <palasso [at] gmail [dot] com>

pkgname=rkward
pkgver=0.8.2
pkgrel=2
pkgdesc='An easy to use and easily extensible IDE/GUI for R'
url='https://rkward.kde.org/'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(breeze-icons
         gcc-libs
         glibc
         karchive
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdsingleapplication
         ki18n
         kio
         kparts
         ktexteditor
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         qt6-base
         qt6-declarative
         qt6-webengine
         r)
makedepends=(extra-cmake-modules
             kdoctools)
source=(https://download.kde.org/stable/rkward/$pkgver/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('be8abdfcd7a17aa5196f63e0136fdbc693e93d9f2f0ec66f2bdacacfc22b9ca8'
            'SKIP')
validpgpkeys=(6F2DC0C0830CEA3890FF22CF391591DF1858CBB6) # Thomas Friedrichsmeier <thomas.friedrichsmeier@kdemail.net>

prepare() {
  find -name index.docbook | xargs sed -e 's|" preview_custom"|"preview_custom"|' -i # Fix build with libxml2 2.15
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
