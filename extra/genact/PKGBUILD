# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=genact
pkgver=1.4.0
pkgrel=1
pkgdesc="A nonsense activity generator"
url="https://github.com/svenstaro/genact"
arch=("x86_64")
license=("MIT")
depends=('gcc-libs')
makedepends=("rust")
source=("$pkgname-$pkgver.tar.gz::https://github.com/svenstaro/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('a240f0b3cd3563d533b20417fd54011f3b190c776793be94d71f9772328269291ef5d7d4fd2e296297d8982e0fbdf13b2f8ed56be464fe097360d7de1b3518b7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/$pkgname "$pkgdir"/usr/bin/$pkgname

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/$pkgname --print-completions bash > "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  target/release/$pkgname --print-completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_$pkgname
  target/release/$pkgname --print-completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgname.fish

  mkdir -p "$pkgdir"/usr/share/man/man1
  target/release/$pkgname --print-manpage | gzip > "$pkgdir"/usr/share/man/man1/$pkgname.1.gz

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
