# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-modsecurity
epoch=1
pkgver=1.0.4
pkgrel=4

pkgdesc='ModSecurity v3 Nginx Connector (module for nginx)'
arch=('x86_64')
depends=('libmodsecurity')
makedepends=('nginx' 'nginx-src')
url="https://github.com/SpiderLabs/ModSecurity-nginx"
license=('Apache-2.0')

source=(
	https://github.com/SpiderLabs/ModSecurity-nginx/releases/download/v$pkgver/modsecurity-nginx-v$pkgver.tar.gz{,.asc}
)
validpgpkeys=('190EFACCA1E9FA466A8ECD9CE6DFB08CE8B11277' # Felipe Zimmerle
              'F126692E9BA86B3958E73ED2F2FC4E45883BCBA4' # Martin Vierula
              '0B2BA1924065B44691202A2AD286E022149F0F6E') # OWASP ModSecurity
sha256sums=('6bdc7570911be884c1e43aaf85046137f9fde0cfa0dd4a55b853c81c45a13313'
            'SKIP')
backup=('etc/nginx/modules.d/20-modsecurity.conf')

prepare() {
	mkdir -p build
	cd build
	ln -sf /usr/src/nginx/auto
	ln -sf /usr/src/nginx/src
}

build() {
	cd build
	auto/configure \
		--with-ld-opt="$LDFLAGS" \
		--with-compat \
		--add-dynamic-module=../ModSecurity-nginx-v$pkgver
	make modules
}

package() {
	if [[ "$BUILDTOOL" == devtools ]]; then
		local nginx_dep="nginx=$(nginx -v 2>&1 | sed 's|.*/||')"
		depends+=($nginx_dep)
	else
		depends+=(nginx)
	fi

	install -dm0755 "$pkgdir"/etc/nginx/modules.d

	cd build/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir/usr/lib/nginx/modules/$mod"
		echo "load_module \"/usr/lib/nginx/modules/$mod\";" >> "$pkgdir/etc/nginx/modules.d/20-modsecurity.conf"
	done
}
