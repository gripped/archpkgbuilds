# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Jingbei Li <i@jingbei.li>
# Contributor: Jose Riha <jose1711@gmail.com>

pkgname=python-resampy
pkgver=0.4.3
pkgrel=4
pkgdesc='Efficient sample rate conversion in python'
arch=(any)
url=https://github.com/bmcfee/resampy
license=(ISC)
depends=(
  python
  python-numba
  python-numpy
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=7c484f8eec54b87e8f566578dc3e48a2be81cc80
source=(git+https://github.com/bmcfee/resampy.git#tag=${_tag})
b2sums=('f003d16e113a6d87b4e040f16c6faee2f41c1a1b3415bfbd8967c760fc1b2345595399571bb457cb54bd22e67d268bc4223c1ddf7a197960ed1582523b4b882c')

pkgver() {
  cd resampy
  git describe --tags
}

build() {
  cd resampy
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" resampy/dist/*.whl
  install -Dm 644 resampy/LICENSE.md -t "${pkgdir}"/usr/share/licenses/python-resampy/
}

# vim:set ts=2 sw=2 et:
