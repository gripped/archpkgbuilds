# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=redo-sh
pkgver=4.0.6
pkgrel=1
pkgdesc="An implementation of the Redo build system in Bash"
arch=('any')
url="http://news.dieweltistgarnichtso.net/bin/redo-sh.html"
license=('AGPL-3.0-or-later')
depends=('sh')
makedepends=('git')
optdepends=('graphviz: dependency graph support via redo-dot')
provides=('redo')
conflicts=('redo-python' 'redo-jdebp' 'redo-c')
source=("$pkgname::git+http://news.dieweltistgarnichtso.net/bin.git#tag=redo-v$pkgver")
sha512sums=('6b40ca79ce23deae7130e862596684efffd4b3aceaba3166dc5bfcc3712fd43cff09cc644c314978901e8363b3c76e6da10087ba8343250226d5d913483eecfc')
b2sums=('91cc963b3536e480219190e21e9679915f4d2690339b45c904992e5d9bdff1bb9e6a9bbad538fd6871472c7d476b69e9e255cd36f0e3f8ca4fd7c754108659d0')

prepare() {
  cd "$pkgname"

  # description of each expression:
  # 1 - strip executable components in middle of file
  # 2 & 3 - remove shebang & subsequent 'cat' invocation
  # 4 - remove html comments
  # boom, we now have a valid HTML file
  sed -i \
    -e '/^EOF/,/^cat <<EOF/d' \
    -e '/#!\/bin\/sh/d' \
    -e '/cat <<EOF/d' \
    -e '/^<!--/,/^-->/d' \
    "redo-sh.do"
}

package() {
  cd "$pkgname"

  # scripts
  find . \
    -maxdepth 1 \
    -executable \
    -type f \
    -name "redo*" \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # man pages
  find . \
    -maxdepth 1 \
    -type f \
    -name "redo*.1" \
    -exec install -vDm644 -t "$pkgdir/usr/share/man/man1" "{}" +

  # documentation
  install -vDm644 "redo-sh.do" "$pkgdir/usr/share/doc/$pkgname/index.html"
}

