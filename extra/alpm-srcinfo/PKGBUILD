# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-srcinfo
_upstream=alpm
pkgver=0.2.0
pkgrel=1
pkgdesc="A CLI for parsing and validation of ALPM SRCINFO files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-srcinfo/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('c412df8e32b79c2ba17c44f9f7887837066f548b5481fa782aa1fb7c62a10d8156992f599edc90735864b612658af2174064613af91efc4c53cb4972dbdde8e8')
b2sums=('14cef124443fa09fb2de71916fe384224bcd4fc78d89d90ba33db4bb22e9d7e4ae956839000c492cf86b9472cbc520c0df35ee7a8ba4701d2e4539b10f96f406')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --package $pkgname

  just generate shell_completions $pkgname
  just generate specifications $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
