# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=fcitx5-pinyin-zhwiki
pkgname=(fcitx5-pinyin-zhwiki rime-pinyin-zhwiki)
_converterver=0.3.0
_zhwikiver=20251220
_webslangver=20251223
pkgver=$_converterver.$_webslangver
pkgrel=1
epoch=1
pkgdesc="Fcitx 5 Pinyin Dictionary from zh.wikipedia.org"
arch=('any')
url="https://github.com/felixonmars/fcitx5-pinyin-zhwiki"
license=('CC-BY-SA-4.0' 'GFDL-1.3-or-later')
makedepends=('git' 'libime' 'opencc' 'pypinyin' 'python-regex' 'python-requests' 'python-tenacity')
source=(git+https://github.com/felixonmars/fcitx5-pinyin-zhwiki.git#tag=$_converterver
        https://github.com/felixonmars/fcitx5-pinyin-zhwiki/releases/download/$_converterver/web-slang-$_webslangver.wikitext
        https://dumps.wikimedia.org/zhwiki/$_zhwikiver/zhwiki-$_zhwikiver-all-titles-in-ns0.gz
        https://dumps.wikimedia.org/zhwikisource/$_zhwikiver/zhwikisource-$_zhwikiver-all-titles-in-ns0.gz
        https://dumps.wikimedia.org/zhwiktionary/$_zhwikiver/zhwiktionary-$_zhwikiver-all-titles-in-ns0.gz)
sha512sums=('99631877e69714df40662c99727e4534164e54e46dcfaad678a31ba6fa1850978c656a8d1184f3081f32cab739758d8b78846ae86aeb8ddec4f21bd2d3599365'
            'f3a492f14cac07e4ca5cfd7a5e768200941745b95bd42fd858358bd05bc302c083d7a1a2d230ebdeeb24c41e4bf759349036b23c2bebfa129c482cbca584d260'
            'a1b2afe484b651957177f085082527d8132c8eac4c1aa4f52bc3974336176518e8b6c35a780b871d4d9943ffaf7d78468d66fe4159a7a031271f931fb51f689d'
            'e27e017bf7c0e5cb91b74ce5e179d1110b478a23389045cbc266fad62648c71e209603f58439d109cb4cf18ab8f7177685bef0b42add61ee947a2a158e37ce61'
            '34a3abf6059dbd620be09e2bd6bf5ad59f2b4b293c7b48c81a13010c64810639e6ff0e6cc2de1c98e715311311d13b44a166218c14d9618e15edf511445b8d4b')

_make="make -L VERSION=$_zhwikiver WEB_SLANG_VERSION=$_webslangver"

prepare() {
  cd $pkgbase
  sed -i 's/zhwikidictionary/zhwiktionary/' Makefile

  # Workaround pacman decompression
  touch zhwik{i,isource,tionary}-$_zhwikiver-all-titles-in-ns0.gz

  ln -s ../zhwiki-$_zhwikiver-all-titles-in-ns0
  ln -s ../zhwikisource-$_zhwikiver-all-titles-in-ns0
  ln -s ../zhwiktionary-$_zhwikiver-all-titles-in-ns0
  ln -s ../web-slang-$_webslangver.wikitext
}

build() {
  cd $pkgbase
  $_make build build_rime_dict
}

package_fcitx5-pinyin-zhwiki() {
  cd $pkgbase
  $_make DESTDIR="$pkgdir" install
}

package_rime-pinyin-zhwiki() {
  cd $pkgbase
  $_make DESTDIR="$pkgdir" install_rime_dict
}
