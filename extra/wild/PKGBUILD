# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=wild
pkgver=0.3.0
pkgrel=1
pkgdesc="A very fast linker for Linux written in Rust"
url='https://github.com/davidlattimore/wild'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
checkdepends=(
  'clang'
  'lld'
)
options=(!lto)
source=(https://github.com/davidlattimore/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ca500e8bf60cfba07ab45f454e451d2d2e10bccd525caabd4291ef9868188c47')
b2sums=('b19a1d1e8d0144653b4b930daa23d1b6b63edfe48b629cebb57af51a0675ca30e1c57b3beaba33e357df49ebce7ce55496e1fa2b8990456a069a5e0951ffbf7c')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  # https://github.com/davidlattimore/wild/issues/230
  cargo test --frozen -- \
    --skip integration_test::program_name_06___weak_vars_c__ \
    --skip integration_test::program_name_26___libc_integration_c__ \
    --skip integration_test::program_name_27___rust_integration_rs__ \
    --skip integration_test::program_name_28___rust_integration_dynamic_rs__ \
    --skip integration_test::program_name_30___rust_tls_rs__
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/wild
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
