# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=wild
pkgver=0.4.0
pkgrel=1
pkgdesc="A very fast linker for Linux written in Rust"
url='https://github.com/davidlattimore/wild'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
checkdepends=(
  'aarch64-linux-gnu-binutils'
  'clang'
  'lld'
)
options=(!lto)
source=(https://github.com/davidlattimore/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0f1f7bc93d5709a04f636311508a32e8a8a08db4333f903ea6ac7ec18d38d0d3')
b2sums=('00ebdd783e6b706a355ff14a89841affbe421620d47cc6288811e0356f81d27792278441b33547f8bf28a11246d0d2daedbbe96c787d9908d3ae7cf78b1930e2')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  # Skip tests requiring nightly compiler
  cargo test --frozen -- \
    --skip integration_test::program_name_31___rust_integration_rs__ \
    --skip integration_test::program_name_32___rust_integration_dynamic_rs__ \
    --skip integration_test::program_name_34___rust_tls_rs__
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/wild
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
