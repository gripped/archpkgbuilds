# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=wild
pkgver=0.7.0
pkgrel=1
pkgdesc="A very fast linker for Linux written in Rust"
url='https://github.com/davidlattimore/wild'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
checkdepends=(
  'clang'
  'lld'
  'rust-musl'
)
options=(!lto)
source=(
  https://github.com/davidlattimore/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
  test-config.toml
)
sha256sums=('4e361126f564f721efca627a414759111d88b06fe26ed15c7604a6420cb9a8ab'
            'f6d85c23f76bd8881c7e5d0e3de385aa130472bcef2f5d372d61e78774fa0248')
b2sums=('bb38526c84a7af7e5bd36f082a2c3e04136da01205797fc8970365475263d428c9efd4dfd16cf316f0147e1a8c74c2415c7eb4d487eb05f42923f2910a13ee41'
        'feea6dac7af3ff51d160dde9850da85013c63296ac16d1fa00e2a273bd18a9d9038ba2324209c0c064087e1e57acd83094332d9e71c5d195705c0021a001b63f')

prepare() {
  cd "${pkgname}-${pkgver}"
  cp "${srcdir}/test-config.toml" .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --test-threads 1
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/wild
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
