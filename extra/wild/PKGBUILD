# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=wild
pkgver=0.5.0
pkgrel=2
pkgdesc="A very fast linker for Linux written in Rust"
url='https://github.com/davidlattimore/wild'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
checkdepends=(
  'clang'
  'lld'
  'rust-musl'
)
options=(!lto)
source=(
    https://github.com/davidlattimore/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
    test-config.toml
)
sha256sums=('fccbb88a7a0064fd031e92a50f675260492ec3f3cd116138b0ab7df6aeab5b04'
            '7ee2471b5796efd6e254b3647b8589137026efabed760c2ac40e444185bfdbda')
b2sums=('6f87ec4434d1eaf1ae3eb406482b251a41bea298cbfaecfa8a4b3ea333e2671fb7946f476cbb8b5a493816701eba3120fcbf5184ab3873dfa09f767614626aac'
        '6e9714a0d836a9598d648a8b214bd87be8098eabfc0a92b4c9db06d87c8d44e1c3f4cdc7ab5e9b8fa3425fc7d6e3113b67f3f4572bd956415c47fa3d403f5f09')

prepare() {
  cd "${pkgname}-${pkgver}"
  cp "${srcdir}/test-config.toml" .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/wild
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
