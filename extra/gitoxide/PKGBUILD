# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gitoxide
pkgver=0.32.0
pkgrel=1
pkgdesc="An idiomatic, lean, fast & safe pure Rust implementation of Git"
url='https://github.com/Byron/gitoxide'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
makedepends=('cargo' 'cmake')
options=(!lto)
source=(https://github.com/Byron/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('5a17da0379254bd996fe1888de4104d551a41bdd8bd4b93034f9d0757382fa75')
b2sums=('9c21b9bfa4e96c6504414e172bee3029251f454908626893650ff77636c3c66a7b7fb739791b59234c05bb233cdc586145ae5bbea6f472f1590afe99bcf46ac0')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release --no-default-features --features max-pure
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gix \
    target/release/ein
}

# vim: ts=2 sw=2 et:
