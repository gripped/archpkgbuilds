# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Ilya Zlobintsev <ilya.zl@protonmail.com>
# Contributor: Matt Armand <marmand68 at gmail dot com>

pkgname=rofi-rbw
pkgver=1.3.0
pkgrel=4
pkgdesc='Rofi frontend for Bitwarden'
arch=('any')
url=https://github.com/fdw/rofi-rbw
license=('MIT')
depends=('python-configargparse' 'rbw')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-poetry-core'
  'python-wheel'
)
optdepends=(
  'rofi: for the X.Org selector'
  'xclip: for one of the X.Org clipboarders'
  'xsel: for one of the X.Org clipboarders'
  'xdotool: for an X.Org typer'
  'wofi: for the Wayland selector'
  'wl-clipboard: for the Wayland clipboarder'
  'wtype: for a Wayland typer'
  'ydotool: for a generic typer'
)
source=("git+$url.git#tag=$pkgver")
b2sums=('09488568435e4283aa56f8a32b8d3346c82104ead51492479420040438ddd573f9ed82c999fa227eb5490e0ae404bec5896eac48886b00999fe6bacd1e67143c')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 docs/$pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
