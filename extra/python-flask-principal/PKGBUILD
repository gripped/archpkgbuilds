# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-flask-principal
pkgver=0.4.0
pkgrel=15
pkgdesc='Identity management for Flask applications'
arch=('any')
url='https://github.com/pallets-eco/flask-principal'
license=('MIT')
depends=(
  'python'
  'python-flask'
)
makedepends=(
  'python-blinker'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-sphinx'
  'python-wheel'
)
source=(
  "$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
  "$pkgname-convert-conf.py-to-python3.patch"
  "$pkgname-sphinx8-fix.patch"
)
sha512sums=('ed8cb28c4e8d936de96db0bf9f7cb45b253dc204c4b8f8dd8022ef1552592ff6324b4a33d3ee862794a6e20eb8c32a0365e7b9397d427da5022c5ded3dfa308a'
            '288b761f8a1af2ebe362293677215c5fbc228edc1f9cf725bebf226ed8cacbca0d9f0749996ea1507a7f8fe05b8744285f4f65c6c337ec413d177abd7f7a6166'
            '417922cb84c34064daed920244320e44f255645103608fbf5b72cee7fceb46526706e6529a0ba3fa5415fb09121ea376e19bfd1bdab1bcd59219224e2f20a0ba')

prepare() {
  cd ${pkgname#python-}-$pkgver
  patch -Np1 -i ../$pkgname-convert-conf.py-to-python3.patch
  patch -Np1 -i ../$pkgname-sphinx8-fix.patch
}

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m unittest discover -v
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst CHANGES
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" docs/_build/text/*.txt
  install -vDm 644 docs/_build/man/flask-principal.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
