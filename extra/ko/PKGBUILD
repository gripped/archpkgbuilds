# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=ko
pkgver=0.18.0
pkgrel=2
pkgdesc="Build and deploy Go applications on Kubernetes"
arch=('x86_64')
url="https://github.com/google/ko"
license=('Apache')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/google/ko/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('b66b276e00adaa8c4dc1c2c358d2d3dd72fd01e919ed0020c249bb96597ce0bac85b93fab217c753e11b5d201fced9382acd7c69bdf124ba2fc5f715a37a1a12')
b2sums=('e48292fd964fb7e64a311b61c3f8ed429474d3f80ae0b94fd2d271a7e73369a45cd3892ab7d9f005bae6d23d55e41b9fb576b874125b2b490762053e6a527ecb')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags="-w -s -linkmode=external" .
  chmod +x ./ko
}

check() {
  cd "${pkgname}-${pkgver}"
  # currently disabled
  #go test -v -x ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dsm755 ./ko "${pkgdir}/usr/bin/ko"
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  ./ko completion > "${pkgdir}/usr/share/bash-completion/completions/ko"
  ./ko completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_ko"
  ./ko completion completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/ko.fish"
}
