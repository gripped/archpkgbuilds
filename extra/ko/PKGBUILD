# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=ko
pkgver=0.18.1
pkgrel=2
pkgdesc="Build and deploy Go container images"
arch=('x86_64')
url="https://github.com/ko-build/ko"
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'repro-go'
)
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ko-build/ko/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('651d0e2ab075b954aaa6e82a12708515b84f08ae9f9fad1335931a317aa1ac72a8f62f8ac238f550790e43328b5c4b701436e6763b0bb87c3f80145848d8a469')
b2sums=('d67fc6d7618cc354318d2c83f22d73cf716e801e1b5a4c51024815a85cfce213ffe64e05ad00beac6a646670d09bddc167229bd336a4de6dc323b34f5225e920')

build() {
  cd "${pkgname}-${pkgver}"
  repro-go build -modcacherw .
}

check() {
  cd "${pkgname}-${pkgver}"
  # currently disabled
  #go test -v -x ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dsm755 ./ko "${pkgdir}/usr/bin/ko"
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  ./ko completion > "${pkgdir}/usr/share/bash-completion/completions/ko"
  ./ko completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_ko"
  ./ko completion completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/ko.fish"
}
