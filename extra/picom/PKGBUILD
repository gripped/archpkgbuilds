# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=picom
pkgver=11
pkgrel=4
pkgdesc='X compositor that may fix tearing issues'
arch=(x86_64)
url='https://github.com/yshui/picom'
license=('MPL-2.0 AND MIT')
makedepends=(asciidoc git mesa meson ninja setconf uthash)
depends=(hicolor-icon-theme libconfig libdbus libev libgl pcre2 pixman
         xcb-util-image xcb-util-renderutil)
backup=(etc/xdg/picom.conf)
optdepends=('dbus:          for controlling picom via D-Bus'
            'python:        for running picom-convgen.py'
            'xorg-xprop:    for picom-trans'
            'xorg-xwininfo: for picom-trans')
source=("git+$url?signed#commit=197b4bd396590cb5df61eb54ec6a1dadf1115a5d"
        "xrender-rollback-pixmap-region.patch")
validpgpkeys=('A22F5C0F4FCF9C7C89A167462C965E9E5D45D730') # Yuxuan Shui <yshuiv7@gmail.com>
b2sums=('SKIP'
        'c35ac19b1931697ba5f2fac36afba74712af3534c9e534e48fd11a8bd26b70e2a8a49075e2aec6043618ad806ad5dc79556c053b810e6b0da6bf43f762b3258e')

prepare() {
  cd $pkgname
  # More subtle transparency in the default settings
  setconf picom.sample.conf 'frame-opacity=0.9;'
  setconf picom.sample.conf 'inactive-opacity=0.95;'
  # Improve performance (recommended option)
  setconf -u picom.sample.conf 'glx-no-stencil=true;'

  # Temporary patch to rollback this upstream commit which produces freezes with the xrender backend when vsync is enabled: https://github.com/yshui/picom/commit/23a29470e50ef1d68b9c99186b069659a793184c
  # See https://github.com/yshui/picom/issues/1166
  patch -Np1 <${srcdir}/xrender-rollback-pixmap-region.patch
}

build() {
  mkdir -p build
  arch-meson --buildtype=release -D with_docs=true build $pkgname
  ninja -v -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # Default and example configuration
  install -Dm644 $pkgname/picom.sample.conf "$pkgdir/etc/xdg/picom.conf"
  install -Dm644 $pkgname/picom.sample.conf "$pkgdir/usr/share/doc/picom/picom.conf.example"

  # Man pages and licenses
  install -Dm644 build/man/picom.1 "$pkgdir/usr/share/man/man1/picom.1"
  install -Dm644 build/man/picom-trans.1 "$pkgdir/usr/share/man/man1/picom-trans.1"
  install -Dm644 $pkgname/LICENSES/MIT "$pkgdir/usr/share/licenses/$pkgname/MIT"
  install -Dm644 $pkgname/LICENSES/MPL-2.0 "$pkgdir/usr/share/licenses/$pkgname/MPL-2.0"
}

# getver: github.com/yshui/picom/releases
