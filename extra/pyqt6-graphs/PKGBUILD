# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-graphs
pkgname=(python-pyqt6-graphs)
pkgver=6.8.0
pkgrel=2
pkgdesc='Python bindings for QtGraphs'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqtgraphs/intro'
license=(GPL3)
groups=(pyqt6)
depends=(gcc-libs
         glibc
         python
         python-pyqt6
         qt6-base
         qt6-graphs)
makedepends=(pyqt-builder
             sip)
source=(https://pypi.python.org/packages/source/P/PyQt6-Graphs/PyQt6_Graphs-$pkgver.tar.gz)
sha256sums=('9c457c1b218593ca2d7ef046426f01ef493636aa6567421941750321ed37e4ce')

build() {
  cd PyQt6_Graphs-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6 \
    --pep484-pyi
  cd build
  make
}

package_python-pyqt6-graphs(){
  cd PyQt6_Graphs-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
