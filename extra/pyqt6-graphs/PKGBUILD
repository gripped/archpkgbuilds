# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-graphs
pkgname=(python-pyqt6-graphs)
pkgver=6.9.0
pkgrel=1
pkgdesc='Python bindings for QtGraphs'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqtgraphs/intro'
license=(GPL3)
groups=(pyqt6)
depends=(gcc-libs
         glibc
         python
         python-pyqt6
         qt6-base
         qt6-graphs)
makedepends=(pyqt-builder
             sip)
source=(https://pypi.python.org/packages/source/P/PyQt6-Graphs/pyqt6_graphs-$pkgver.tar.gz)
sha256sums=('f96eca719ce8c8c1647695136e69837d05fe56c31ca66533db568e108c8cc919')

build() {
  cd pyqt6_graphs-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6 \
    --pep484-pyi
  cd build
  make
}

package_python-pyqt6-graphs(){
  cd pyqt6_graphs-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
