# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-annotation
_pkgname=zope.annotation
pkgver=5.2
pkgrel=1
pkgdesc="Object annotation mechanism"
arch=('any')
url="https://github.com/zopefoundation/zope.annotation"
license=('ZPL-2.1')
depends=(
  'python'
  'python-zope-component'
  'python-zope-interface'
  'python-zope-location'
  'python-zope-proxy'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-zope-configuration'
  'python-zope-testing'
  'python-zope-testrunner'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.annotation/archive/$pkgver.tar.gz")
sha512sums=('d754f8e37fc98b7250213386f35c76a20109fa42ab11b3c02d921f36c0959f4e265b607eccab8195da1d6944d0650e9eb6433f269c74a8c6cf9fe913407fd9bc')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
