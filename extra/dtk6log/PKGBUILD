# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6log
pkgver=0.0.5
pkgrel=3
pkgdesc='Simple, convinient and thread safe logger for Qt-based C++ apps'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6log"
license=('LGPL-3.0-only')
depends=('glibc' 'libsystemd' 'qt6-base' 'spdlog' 'fmt' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtk6log.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('5460f82ac4ed14304150dae0bc87da2c5537d6a4c736fb8084b17cfdbe218db5914fb9fea8bcd377d0cfc9d6028253f94478f66f8efe54ee532b86457700c862'
            '6c9d2a1c1fcb3ab2f4299de2f9d16f1fb4c25f46d1a33275a084a13af89f1adb06aefd162bafd67e8f15d8d4aa15f4d678bb2c7d0928f837f40fcbdf6b06f900')

prepare() {
  cd dtk6log
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd dtk6log
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt6/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_SYSTEMD=ON \
    -DBUILD_WITH_QT6=ON
  ninja
}

package() {
  cd dtk6log
  DESTDIR="$pkgdir" ninja install
}
