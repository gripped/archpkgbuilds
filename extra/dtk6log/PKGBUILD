# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6log
pkgver=0.0.2
pkgrel=5
pkgdesc='Simple, convinient and thread safe logger for Qt-based C++ apps'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6log"
license=('LGPL3')
depends=('glibc' 'libsystemd' 'qt6-base' 'spdlog' 'fmt' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/dtk6log.git#tag=$pkgver"
         qt-6.9.patch)
sha512sums=('1c404161d69686bc0666107f73c36526ecc720551bda6c6cf4a6bf3c4fb5c98e291ee7cf5514e2296617e943260492d380d0117b9d3bef171ef66601c09f4036'
            '9eb8d7089ccfed8207dfb55eaa678fd1f7cf1a29a10804d1ffb499549c0aab75ad6a2834db787492ee23eb6f158a638e6c90e6bcb346b076fb50e04030245b53')

prepare() {
  patch -d $pkgname -p1 < qt-6.9.patch # Fix build with Qt 6.9
}

build() {
  cd dtk6log
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DMKSPECS_INSTALL_DIR=lib/qt6/mkspecs/modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_SYSTEMD=ON \
    -DBUILD_WITH_QT6=ON \
    -DDLOG_VERSION=$pkgver
  ninja
}

package() {
  cd dtk6log
  DESTDIR="$pkgdir" ninja install
}
