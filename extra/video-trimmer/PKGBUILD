# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=video-trimmer
pkgver=25.03
pkgrel=2
pkgdesc='Cut out a fragment of a video quickly'
arch=(x86_64)
url='https://apps.gnome.org/VideoTrimmer/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  ffmpeg
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugins-base
  gst-plugins-good
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
source=("git+https://gitlab.gnome.org/YaLTeR/video-trimmer.git#tag=v$pkgver")
b2sums=(c7f60b1aba8ca191a34a5d19f4c36d2ed8fec00dd13aefb84010e3c14ea0a50c7c375d4830fe22f88eb1cabee307886e734b4aeaf5cef31d34b6bf50f62b9f97)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
