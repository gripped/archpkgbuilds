# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=video-trimmer
pkgver=0.9.0
pkgrel=1
pkgdesc='Cut out a fragment of a video quickly'
arch=(x86_64)
url='https://apps.gnome.org/VideoTrimmer/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  ffmpeg
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugins-base
  gst-plugins-good
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
source=("git+https://gitlab.gnome.org/YaLTeR/video-trimmer.git#tag=v$pkgver")
b2sums=(5532f0e3728412d6bfaf868d6d54a48cff4918975f2e4656d097b103250becc91b445e4c539ee661fa51e3cd382c1e368545705e7a0dba837b5608788ac9cccd)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
