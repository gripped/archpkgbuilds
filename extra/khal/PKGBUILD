# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: eolianoe <eolianoe [at] googlemail [dot] com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Thomas Weißschuh <thomas t-8ch de>
# Contributor: Étienne Deparis <etienne [at] depar [dot] is>
# Contributor: Max Gautier <mg@max.gautier.name>

pkgname=khal
pkgver=0.11.4
pkgrel=1
pkgdesc='CLI calendar application built around CalDAV'
arch=(any)
url=https://lostpackets.de/khal/
license=(MIT)
depends=(
  python
  python-click
  python-click-log
  python-configobj
  python-dateutil
  python-icalendar
  python-pytz
  python-pyxdg
  python-tzlocal
  python-urwid
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-sphinxcontrib-newsfeed
  python-wheel
)
checkdepends=(
  python-pytest
  python-freezegun
  python-hypothesis
  python-packaging
  vdirsyncer
)
optdepends=(
  'gawk: for mutt2khal'
  'python-setproctitle: to set process name'
  'vdirsyncer: to synchronize CalDAV calendars'
)
source=(
  "git+https://github.com/pimutils/khal.git#tag=v$pkgver"
  0001-support-icalendar-6.0.0.patch
)
sha256sums=('16ccfcb69c55f104a04f262300d7f7fe5e03db98102b3fb021fec81f4b6e1f55'
            '8acdb96d22357ea8e8191cbb97b4ff05f65e324021dbdc7797513128c8d457a5')
b2sums=('3ea0dc0cd2e12a6fa5459c49a081bf850493932d6a465235aa8226158ecc26d6df463e5cb0d19e2cdcdab276b3e21d06a348cee80d3a6d2c721ad563c6df01a0'
        'bcd7b4e2a1e8542741c4abb281b24e8a39a7e283fc2de99f21fc449b654d3238e59c4f50d4b70134022813d1c4f748ea3301d5dad4596d570b9d4b81743df6d5')

prepare() {
  cd $pkgname
  # fix issue with python-sphinx > 8: https://github.com/pimutils/khal/issues/1356
  sed -i '/intersphinx_mapping/d' -i doc/source/conf.py

  git apply ../0001-support-icalendar-6.0.0.patch
}

build() {
  cd $pkgname
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --skip-dependency-check --no-isolation
  make -C doc man PYTHONPATH="$PWD"
}

check() {
  cd $pkgname
  LC_ALL=C PYTHONPATH=build pytest -v
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  # executables
  install -vDm 755 bin/{i,}$pkgname misc/mutt2khal -t "$pkgdir"/usr/bin/
  # configuration
  install -vDm 644 $pkgname.conf.sample "$pkgdir"/usr/share/doc/$pkgname/examples/$pkgname.conf
  # man page
  install -vDm 644 doc/build/man/$pkgname.1 -t "$pkgdir"/usr/share/man/man1/
  # completions
  install -vd "$pkgdir"/usr/share/{bash-completion/completions,fish/vendor_completions.d,zsh/site-functions}
  PYTHONPATH="$pkgdir/$site_packages:$PYTHONPATH" _KHAL_COMPLETE=bash_source "$pkgdir"/usr/bin/khal > "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  PYTHONPATH="$pkgdir/$site_packages:$PYTHONPATH" _KHAL_COMPLETE=fish_source "$pkgdir"/usr/bin/khal > "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgname.fish
  PYTHONPATH="$pkgdir/$site_packages:$PYTHONPATH" _KHAL_COMPLETE=zsh_source "$pkgdir"/usr/bin/khal > "$pkgdir"/usr/share/zsh/site-functions/_$pkgname
  # desktop file
  install -vDm 644 misc/khal.desktop -t "$pkgdir"/usr/share/applications/
  # docs
  install -vDm 644 {AUTHORS.txt,{CHANGELOG,CONTRIBUTING,README}.rst} -t "$pkgdir"/usr/share/doc/$pkgname/
  # symlink license file
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
