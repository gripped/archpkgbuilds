# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Israel Herraiz <isra@herraiz.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=mpop
pkgver=1.4.21
pkgrel=2
pkgdesc="A small, fast POP3 client suitable as a fetchmail replacement"
arch=('x86_64')
url="https://marlam.de/mpop/"
license=('GPL-3.0-or-later')
depends=('gnutls' 'libidn2' 'libsecret')
install=mpop.install
source=("https://marlam.de/mpop/releases/mpop-${pkgver}.tar.xz"{,.sig})
sha256sums=('4ca0d1e0d01366fe3e0cf490d88d154df511278fb595638713be3ca675665855'
            'SKIP')
validpgpkeys=('2F61B4828BBA779AECB3F32703A2A4AB1E32FD34') # Martin Lambers

build() {
  cd "$srcdir/mpop-$pkgver"

  ./configure --prefix=/usr --with-ssl=gnutls
  make
}

package() {
  cd "$srcdir/mpop-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 scripts/vim/mpop.vim "$pkgdir/usr/share/vim/vimfiles/syntax/mpop.vim"
  install -Dm644 doc/mpoprc.example   "$pkgdir/usr/share/doc/mpop/mpoprc.example"
}
