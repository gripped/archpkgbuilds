# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=xdg-desktop-portal-hyprland
pkgver=1.3.0
_protocolver=4d29e48433270a2af06b8bc711ca1fe5109746cd
pkgrel=2
pkgdesc='xdg-desktop-portal backend for hyprland'
url="https://github.com/hyprwm/$pkgname"
arch=(x86_64)
license=(MIT)
provides=(xdg-desktop-portal-impl)
depends=(gcc-libs
         glibc
         hyprlang libhyprlang.so
         libinih
         pipewire
         qt6-base
         qt6-wayland
         sdbus-cpp
         util-linux-libs libuuid.so
         wlroots
         xdg-desktop-portal)
makedepends=(cmake
             wayland
             wayland-protocols)
optdepends=('grim: required for the screenshot portal to function'
            'slurp: support for interactive mode for the screenshot portal')
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz"
        "$url/commit/f46cff1.patch"
        "$url/commit/ea95896.patch"
        "$url/commit/4b65a23.patch"
        "https://github.com/hyprwm/hyprland-protocols/archive/$_protocolver.tar.gz"
        hyprland-portals.conf) # TODO move this to hyprland where it belongs
sha256sums=('ea15bad792320dd02f63141eff5d8d8355034102697f6cb97d5148b3a45cd7cf'
            'c33ca9926012d563d1c5e8c68132556dae039dcc8e7b5805b3be55f14ab1c075'
            '53617072d7df2b9aa7e893eb77abe397e5021db61d0cc22a21069d85e9e847a0'
            'e323be2575b7eb36f0ab443973841612ce6631b6827b57ad347ec21a1a8e18c1'
            '5f6be4d870e94314f05ec7ff9c9c1f028748230ff80a7f89aeaf66c08e5c70e0'
            '20bc215211f16a361086d59fa051df7337d95f91c695a29d8c5d23d40407fad5')

prepare() {
	cd "$_archive"
	patch -p1 < ../f46cff1.patch # https://github.com/hyprwm/xdg-desktop-portal-hyprland/issues/151
	patch -p1 < ../ea95896.patch # https://github.com/hyprwm/xdg-desktop-portal-hyprland/issues/153
	patch -p1 < ../4b65a23.patch # https://github.com/hyprwm/xdg-desktop-portal-hyprland/issues/153
	pushd subprojects
	rm -rf hyprland-protocols sdbus-cpp
	ln -sfT "$srcdir/hyprland-protocols-$_protocolver" hyprland-protocols
}

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_INSTALL_LIBEXECDIR=/usr/lib \
		-D CMAKE_BUILD_TYPE=Release
	cmake --build build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" cmake --install build
        install -Dm0644 -t "$pkgdir/usr/share/xdg-desktop-portal/" "${srcdir}/hyprland-portals.conf"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
