# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=6.1.2
pkgrel=168
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL-3.0-or-later')
makedepends=('linux-headers')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=(
  "https://github.com/linuxdeepin/deepin-anything/archive/$pkgver/deepin-anything-$pkgver.tar.gz"
  0001-feat-Fix-compile-error-and-add-support-to-kernel-6.1.patch
  0002-Avoid-init_module-deprecation.patch
)
sha512sums=('a69483f914aeee97b565a6191b21712b06dee1fe2076792ce0decf64da698b7c58ed64cf75073f2d2862c01c35ec1b29568f955bce8cb6b447ec9fa69c59f5e1'
            '0f251c79539a9d7efab22c1ebcad3098703951ff9cdbf9d6dd097a508fc55b88a0c4a54e48a576822285cd4de7e64b6631206d65ff5dfd43fc4689872597e95a'
            '0ccdc51a91e4b310f38d8b2e3e55b5214a1fd2c351eca403b91e11889ec204df0de872895293d1809378f4f37c84d95f8d36812c5e321e01341c138dff464713')

prepare() {
  cd deepin-anything-$pkgver

  # Support linux 6.15
  patch -Np1 -i ../0001-feat-Fix-compile-error-and-add-support-to-kernel-6.1.patch
  patch -Np1 -i ../0002-Avoid-init_module-deprecation.patch
}

build() {
  cd deepin-anything-$pkgver/src/kernelmod
  make kdir=/usr/src/linux
}

package() {
  depends=('linux')

  cd deepin-anything-$pkgver/src/kernelmod

  local extradir=/usr/lib/modules/$(</usr/src/linux/version)/extramodules
  install -Dt "$pkgdir$extradir" -m644 *.ko
  find "$pkgdir" -name '*.ko' -exec strip --strip-debug {} +
  find "${pkgdir}" -name '*.ko' -exec zstd --rm -19 {} +
}
