# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=6.2.10
pkgrel=8
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL-3.0-or-later')
makedepends=('linux-headers' "deepin-anything-dkms=$pkgver")
replaces=('deepin-anything-module')
conflicts=('deepin-anything-module' 'deepin-anything-dkms')
provides=('DEEPIN-ANYTHING-MODULE')

build() {
  _kernver="$(</usr/src/linux/version)"

  fakeroot dkms build --dkmstree "$srcdir" -m deepin-anything/0.0 -k ${_kernver}
}

package() {
  depends=('linux')

  _kernver="$(</usr/src/linux/version)"

  install -Dt "$pkgdir/usr/lib/modules/$_kernver/extramodules" -m0644 \
    deepin-anything/0.0/${_kernver}/${CARCH}/module/*.ko

  # compress each module individually
  find "${pkgdir}" -name '*.ko' -exec zstd --rm -19 {} +
}
