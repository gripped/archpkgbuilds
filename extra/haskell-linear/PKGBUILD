# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=linear
pkgname=haskell-linear
pkgver=1.23.2
pkgrel=6
pkgdesc="Linear Algebra"
url="https://github.com/ekmett/linear/"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-adjunctions' 'haskell-base-orphans' 'haskell-bytes' 'haskell-cereal'
         'haskell-distributive' 'haskell-hashable' 'haskell-indexed-traversable' 'haskell-lens'
         'haskell-random' 'haskell-reflection' 'haskell-semigroupoids' 'haskell-tagged'
         'haskell-transformers-compat' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-void')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-simple-reflect' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('bbd69d619beb81ce4646bdd36a89a433b8d206c042e5679022a3f28c740c6917')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --datasubdir=haskell-linear --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -f-herbie -ftemplate-haskell

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
