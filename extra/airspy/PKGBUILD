# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=airspy
_pkgname=airspyone_host
pkgver=1.0.10
pkgrel=3
pkgdesc="Host code for Airspy SDR"
arch=(x86_64)
url="https://www.airspy.com"
license=('GPL-2.0-or-later')
depends=(
  glibc
  libusb
)
makedepends=(cmake)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/airspy/airspyone_host/archive/v$pkgver.tar.gz"
  "airspy.conf"
)
sha256sums=('fcca23911c9a9da71cebeffeba708c59d1d6401eec6eb2dd73cae35b8ea3c613'
            'b210dd0698c3bb8ad59f0393c12a74e1ed8fe1b16a2faabc38467f68ebed0120')

prepare() {
  cd $_pkgname-$pkgver
  # FS#47169
  sed -i 's|MODE.*$|TAG+="uaccess"|' airspy-tools/52-airspy.rules
}

build() {
  cd $_pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

package() {
  cd $_pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/lib/udev/rules.d" airspy-tools/52-airspy.rules
  install -vDm644 -t "$pkgdir/etc/modprobe.d" "$srcdir/airspy.conf"
}
