# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=dante
pkgver=1.4.4
pkgrel=2
pkgdesc="SOCKS v4 and v5 compatible proxy server and client"
arch=(x86_64)
url="https://www.inet.no/dante"
license=('LicenseRef-Dante')
depends=(
  'e2fsprogs'
  'glibc'
  'krb5'
  'libxcrypt'
  'pam'
)
backup=(
  'etc/socks.conf'
  'etc/sockd.conf'
)
source=(
  "https://www.inet.no/dante/files/dante-$pkgver.tar.gz"
  "sockd.tmpfiles"
  "sockd.service"
)
sha512sums=('bf6b82fcad63d0920b77cebe360612a639c8532fbda6dc84f1bc88e89671e358f4819bf9759b1ba3bb7fbe703eec7e59d9cef620b6e65f177686d5b00e5f221a'
            '6c7c3128eb1da82cdb89bc097793597f7a218ea31da5be71ef7cc1180fcac82eee43ae6fbaf9c6bc95782766974e34619a98d288f52e590c9b31503f1ace654d'
            'd3a1929533ea58b2daf0a15eefec80f6cfb39dec688e414a5a7c3321c80e24c9ec0c3a3d2719ed4af5c97d58ec26fecdea77d84b3837bf4d242f37943f2213ce')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-libwrap
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -vDm644 -t "$pkgdir/etc" example/{socks,sockd}.conf
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -vDm644 "$srcdir/sockd.service" "$pkgdir/usr/lib/systemd/system/sockd.service"
  install -vDm644 "$srcdir/sockd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/sockd.conf"
}
