# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=pop-launcher
pkgver=1.2.4
pkgrel=1
pkgdesc='Modular IPC-based desktop launcher service'
arch=(x86_64)
url=https://github.com/pop-os/launcher
license=(MPL-2.0)
depends=(
  dbus
  fd
  gcc-libs
  glibc
  libegl
  libqalculate
  libxkbcommon
  pop-icon-theme
  sh
  xdg-utils
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_tag=fca3b25552deca3bd64de8bfd1ebb38c45e4fb39
source=(git+https://github.com/pop-os/launcher.git#tag=${_tag})
b2sums=('e73e1d80adde5a46e6bfefa49c894a7adaa5c38176df56db7f520b3e4f22c0a81958f3af7bd2638570b9add1303139b75c742d388e90baa44453c1f099cba72f')

prepare() {
  cd launcher
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/g' -i Cargo.toml
}

pkgver() {
  cd launcher
  git describe --tags
}

build() {
  cd launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  CC=clang just build-release --frozen
}

check() {
  cd launcher
  just check
}

package() {
  cd launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
