# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=pop-launcher
pkgver=1.2.1.r66.6a1b8b9
pkgrel=1
pkgdesc='Modular IPC-based desktop launcher service'
arch=(x86_64)
url=https://github.com/pop-os/launcher
license=(MPL-2.0)
depends=(
  dbus
  fd
  gcc-libs
  glibc
  libegl
  libqalculate
  libxkbcommon
  pop-icon-theme
  sh
  xdg-utils
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_commit=6a1b8b9ad0563693136872809dac2a30fb7d633f
source=(git+https://github.com/pop-os/launcher.git#commit=${_commit})
b2sums=('90cc013c2621cb26022efd9bdf68894df3481f5f3dadbeac356223cffbc525df96050802f8363d85fb760cab2ed88a4bc98d539749fd3ba80be9cbe428ebd372')

prepare() {
  cd launcher
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/g' -i Cargo.toml
}

pkgver() {
  cd launcher
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  cd launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  CC=clang just build-release --frozen
}

check() {
  cd launcher
  just check
}

package() {
  cd launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
