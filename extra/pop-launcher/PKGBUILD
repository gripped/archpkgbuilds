# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=pop-launcher
pkgver=1.2.6
pkgrel=1
pkgdesc='Modular IPC-based desktop launcher service'
arch=(x86_64)
url=https://github.com/pop-os/launcher
license=(MPL-2.0)
depends=(
  dbus
  fd
  gcc-libs
  glibc
  libegl
  libqalculate
  libxkbcommon
  pop-icon-theme
  sh
  xdg-utils
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_tag=0e8aa22f970ae228bed5573f640cac01eb706a37
source=(git+https://github.com/pop-os/launcher.git#tag=${_tag})
b2sums=('8bd2d4daee88543d168b4824c0f666efa6a00d18e91978b3f94344eb32a513ed2d068bd810c34e9eb432e7a568575a48369256d3ff80437fc85b7e09a0d1175f')

prepare() {
  cd launcher
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/g' -i Cargo.toml
}

pkgver() {
  cd launcher
  git describe --tags
}

build() {
  cd launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  CC=clang just build-release --frozen
}

check() {
  cd launcher
  just check
}

package() {
  cd launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
