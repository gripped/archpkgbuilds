# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Mitch Bigelow <ipha00@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>

pkgname=pianobar
pkgver=2024.12.21
pkgrel=1
pkgdesc="Console-based frontend for Pandora"
arch=('x86_64')
url="https://6xq.net/pianobar/"
license=('MIT')
depends=('libao' 'ffmpeg' 'curl' 'json-c')
source=(https://6xq.net/pianobar/$pkgname-$pkgver.tar.bz2{,.asc})
validpgpkeys=('017D74E27F5856963801781DF663943E08D8092A') # Lars-Dominik Braun <lars@6xq.net>
sha256sums=('16f4dd2d64da38690946a9670e59bc72a789cf6a323f792e159bb3a39cf4a7f5'
            'SKIP')

build() {
  make -C ${pkgname}-${pkgver}
}

package() {
  make -C ${pkgname}-${pkgver} DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm644 $pkgname-"${pkgver}"/COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 $pkgname-"${pkgver}"/contrib/config-example "${pkgdir}"/usr/share/$pkgname/config-example
  install -Dm755 $pkgname-"${pkgver}"/contrib/headless_pianobar "${pkgdir}"/usr/bin/pianoctl
}
