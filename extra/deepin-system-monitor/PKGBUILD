# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=6.5.10
pkgrel=2
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'icu' 'dconf' 'libxcb' 'xcb-util-wm'
         'gcc-libs' 'libnl' 'libcap' 'libpcap' 'polkit-qt6')
makedepends=('git' 'cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt6-tools' 'deepin-tray-loader')
groups=('deepin')
install=deepin-system-monitor.install
source=("git+https://github.com/linuxdeepin/deepin-system-monitor.git#tag=$pkgver")
sha512sums=('8ee8b64ec7588198b38b014579edc8c77a7093bc04bef907ec7de61be3b669be7f9cd54d8ca66aef7bb27738903ffac7d3eed3dd46f990f4a51437894e78b41a')

build() {
  cd deepin-system-monitor
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_C_FLAGS="$CXXFLAGS -Wno-error=incompatible-pointer-types"
  ninja
}

package() {
  cd deepin-system-monitor
  DESTDIR="$pkgdir" ninja install
}
