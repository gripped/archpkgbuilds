# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=6.5.1
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
# startdde: org.deepin.dde.Display1
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'dwayland' 'qt5-base' 'qt5-svg' 'gsettings-qt'
         'icu' 'dconf' 'libxcb' 'xcb-util-wm' 'gcc-libs' 'libnl' 'libcap' 'libpcap' 'startdde'
         'polkit-qt5')
makedepends=('git' 'cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt5-tools' 'deepin-tray-loader')
groups=('deepin')
install=deepin-system-monitor.install
source=("git+https://github.com/linuxdeepin/deepin-system-monitor.git#tag=$pkgver")
sha512sums=('fbd0a741cdac6146075c53d7c65823f829c609f3ff551819a22b1fbca87c4a0f87d23e36c00d50bc4397ba1ab3a080179d7e1080babaddc02b06baabb2655319')

prepare() {
  cd deepin-system-monitor
  sed -i '/lscpu.h/d' deepin-system-monitor-main/3rdparty/dmidecode/dmioem.c
  sed -e 's|CMAKE_CXX_STANDARD 11|CMAKE_CXX_STANDARD 17|' -i */CMakeLists.txt # Fix build with ICU 75
}

build() {
  cd deepin-system-monitor
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_C_FLAGS="$CXXFLAGS -Wno-error=implicit-function-declaration -Wno-error=int-conversion" # Fix build with GCC 14
  ninja
}

package() {
  cd deepin-system-monitor
  DESTDIR="$pkgdir" ninja install
}
