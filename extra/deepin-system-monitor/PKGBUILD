# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=6.0.13
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
# startdde: org.deepin.dde.Display1
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'dwayland' 'qt5-base' 'qt5-svg' 'gsettings-qt'
         'icu' 'dconf' 'libxcb' 'xcb-util-wm' 'gcc-libs' 'libnl' 'libcap' 'libpcap' 'startdde')
makedepends=('cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt5-tools' 'deepin-dock')
groups=('deepin')
install=deepin-system-monitor.install
source=("https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7bfc7861c72c2e6a544f57bb09a7cc99c22f5ad244800159bc9dc0d6ca4a53a5304bd0ae91aa5ee28d61c9345b5f3206fbe552d75527a54b97f1133a61cdc86d')

build() {
  cd deepin-system-monitor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd deepin-system-monitor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
