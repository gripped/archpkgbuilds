# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=6.5.17
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'icu' 'dconf' 'libxcb' 'xcb-util-wm'
         'gcc-libs' 'libnl' 'libcap' 'libpcap' 'polkit-qt6')
makedepends=('git' 'cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt6-tools' 'deepin-tray-loader')
groups=('deepin')
install=deepin-system-monitor.install
source=("git+https://github.com/linuxdeepin/deepin-system-monitor.git#tag=$pkgver")
sha512sums=('e37b9c499eb95eaacc75bf36da3b852b2c956fcd35bb79ef35a9c70cbfaa36f31d8920d7e38cf130ce311a94a20e7421fb8b00efc0255291cd0b0d64a2fc4125')

build() {
  cd deepin-system-monitor
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  ninja
}

package() {
  cd deepin-system-monitor
  DESTDIR="$pkgdir" ninja install
}
