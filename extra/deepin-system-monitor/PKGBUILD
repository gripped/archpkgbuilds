# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=6.5.12
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'icu' 'dconf' 'libxcb' 'xcb-util-wm'
         'gcc-libs' 'libnl' 'libcap' 'libpcap' 'polkit-qt6')
makedepends=('git' 'cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt6-tools' 'deepin-tray-loader')
groups=('deepin')
install=deepin-system-monitor.install
source=("git+https://github.com/linuxdeepin/deepin-system-monitor.git#tag=$pkgver")
sha512sums=('49feb1298ad88b6089fdaf51cc5f5792575939d1fc621f9903ad80a684f1cbfe82e32ec27694dc7e80b15429248712e45828443be37a12654b9864f21fbb9523')

build() {
  cd deepin-system-monitor
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_C_FLAGS="$CXXFLAGS -Wno-error=incompatible-pointer-types"
  ninja
}

package() {
  cd deepin-system-monitor
  DESTDIR="$pkgdir" ninja install
}
