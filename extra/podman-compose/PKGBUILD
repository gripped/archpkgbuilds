# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=podman-compose
pkgver=1.4.1
pkgrel=1
pkgdesc='A script to run docker-compose.yml using podman'
arch=(any)
url='https://github.com/containers/podman-compose'
license=(GPL-2.0-only)
depends=(
  podman
  python
  python-dotenv
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-parameterized
)
optdepends=(
  'aardvark-dns: resolve hostnames of linked containers'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('dd253d06637dd486ecf54932ca5b12a50a5a3fcf666755931187b1c18e5aa6d6e6243f46bfde2c24aa96c98bcf47329533dbe722ebe957a08b121ccae593dc90')
b2sums=('20a10719daa13289c1a9a19e1fd72b78134f89ce2867068c9aa7580ca63a5becf746e8d015161d1f3a7ee744783956036a2d889a025a85ed9b406aacf9edf601')

build() {
  cd $pkgname-$pkgver

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # ignore tests where podman is called (as we can not run the tests due to missing subuid/subgid entries)
    --ignore tests/integration
    --ignore tests/test_podman_compose.py
    --ignore tests/test_podman_compose_additional_contexts.py
    --ignore tests/test_podman_compose_build_secrets.py
    --ignore tests/test_podman_compose_build_ulimits.py
    --ignore tests/test_podman_compose_config.py
    --ignore tests/test_podman_compose_in_pod.py
    --ignore tests/test_podman_compose_include.py
    --ignore tests/test_podman_compose_networks.py
    --ignore tests/test_podman_compose_tests.py
    --ignore tests/test_podman_compose_up_down.py
    --ignore tests/test_utils.py
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
  find examples -type f -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
  install -vDm 644 {README.md,docs/*} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
