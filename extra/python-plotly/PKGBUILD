# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Grey Christoforo <first name at last name dot net>

pkgname=python-plotly
pkgver=6.0.0
pkgrel=3
pkgdesc="An open-source, interactive graphing library"
arch=('any')
url="https://github.com/plotly/plotly.py"
license=('MIT')
depends=(
    python
    python-narwhals
)
# optdepends based on requires-optional.txt
optdepends=(
    jupyterlab
    python-anywidget
    python-numpy
    python-matplotlib
    python-pillow
    python-scipy
    python-pandas
    python-colorcet
    python-inflect
    python-psutil
    python-pyshp
    python-geopandas
    python-shapely
)
makedepends=(
    git
    npm
    python-wheel
    python-build
    python-installer
    python-setuptools
    python-jupyter_core
    python-tornado
    jupyterlab
)
checkdepends=(
    python-requests
    python-pytest
)
source=(
     "${pkgname}::git+https://github.com/plotly/plotly.py#tag=v$pkgver"
)
sha256sums=('0c2465660369eb087064c0a64f894363194cc69904060ccff9722ed0f2636714')
options=(!strip !debug) # strip and debug aren't useful for python files and take forever

prepare() {
  cd python-plotly

  git clean -dfx
}

build() {
  cd python-plotly/packages/python/plotly

  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd python-plotly/packages/python/plotly
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
  )
  pytest "${pytest_options[@]}" plotly/tests/test_core
}

package() {
  cd python-plotly/packages/python/plotly
  python -m installer --destdir="$pkgdir"/ dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
