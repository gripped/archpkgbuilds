# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cl-rt
_pkgname="${pkgname#cl-}"
pkgver=1990.12.19.r19.ga6a7503
pkgrel=4
pkgdesc='Common Lisp regression tester from MIT'
arch=(any)
url='https://github.com/grawlinson/cl-rt'
license=(MIT)
depends=(common-lisp cl-asdf)
makedepends=(git)
checkdepends=(sbcl)
_commit=a6a7503a0b47953bc7579c90f02a6dba1f6e4c5a
source=(
  "$pkgname::git+https://github.com/grawlinson/cl-rt#commit=$_commit"
  run-tests.lisp
)
sha512sums=('a18c12710d35b347cc4b5b451b644582c21eb660ef3f2921ae834999a5d42b5ab46e293dc9e1a53dcbea0317a036c18099bb5fa1e3daad07b4e0e85b346801f3'
            'f950cc21504be904a27f1c353a63d4e20ba5c44c266c369da5d846ea48fe1707168374adbc1abac68f1037c5a2cd9aa759af2584998f51ea09fe424aa948859b')
b2sums=('bdefa62d340b65b8a295c47ae52e17b28e29ec3bd723e13e7371ae17e273d1ea3f4dc0580a0c18d51373ff19291f919e6d71b54a38f38c55b3ab5a47c3ea3b5e'
        '2a9602fe10824973ee233b4a11a4d0bce91189e324afe350fb14389e03f5b742231c98a6a0e4aea2c2bd43cd22bd91db63f6d7a41aa6c71d235f06afa8e49434')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed -e 's/^v//' -e 's/-/.r/' -e 's/-/./g'
}

prepare() {
  cd "$pkgname"

  # license extraction
  sed -n '/Copyright/,/SOFTWARE\./p' rt-doc.txt | \
    sed -e 's/|//g' -e 's/[ \t]*$//' -e 's/^[ \t]*//' \
    > LICENSE
}

# XXX 15/30 tests fail.
#check() {
#  cd "$pkgname"
#
#  sbcl --script ../run-tests.lisp
#}

package() {
  cd "$pkgname"

  # library
  install -vDm644 -t "$pkgdir/usr/share/common-lisp/source/$_pkgname" ./*.{lisp,asd}

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" rt-doc.txt

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
