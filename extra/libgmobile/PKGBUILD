# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=libgmobile
pkgver=0.4.0
pkgrel=1
pkgdesc='Functions useful in mobile related, glib based projects'
arch=('x86_64' 'aarch64')
url='https://gitlab.gnome.org/World/Phosh/gmobile'
license=(GPL-3.0-only)
depends=(
        "json-glib"
        "glib2"
        "vala"
)
makedepends=(
            "meson"
            "git"
            "libgirepository"
            "json-glib"
            "gobject-introspection"
            "vala"
)
source=("git+${url}.git#tag=v${pkgver}")

build() {
  arch-meson gmobile build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
sha256sums=('2978db123c81804002843a10cf65e44081b90486e13eb5a7702c780cf0934ec2')
