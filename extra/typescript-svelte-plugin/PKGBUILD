# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=svelte-language-tools
pkgname=typescript-svelte-plugin
pkgver=0.3.45
pkgrel=1
pkgdesc='TypeScript plugin for Svelte intellisense'
url=https://github.com/sveltejs/language-tools/tree/master/packages/typescript-plugin
license=(MIT)
arch=(any)
depends=(
  nodejs
  typescript
)
makedepends=(
  git
  npm
  pnpm
)
source=("$_name::git+https://github.com/sveltejs/language-tools.git#tag=typescript-plugin-$pkgver")
b2sums=('491c75632ae7f226a27588bf285c2951e81beffb4061e9b1b182ec0232e379eb0097021d6175b303e0ac06889b05d8e3856d2201884b57ced9215b3bd9181eae')

prepare() {
  cd $_name
  pnpm install --frozen-lockfile
}

build() {
  cd $_name/packages/svelte2tsx
  pnpm build
  cd ../typescript-plugin
  pnpm build
}

package() {
  install -d "$pkgdir"/usr/lib/node_modules/$pkgname

  cd $_name
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE

  cd packages/typescript-plugin
  # Prune unnecessary packages
  cp package.json{,.bak}
  sed -i 's/workspace:~/*/' package.json
  rm -r node_modules
  npm install --production
  rm -r node_modules/typescript
  mv package.json{.bak,}

  cp -r dist node_modules package.json "$pkgdir"/usr/lib/node_modules/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
}
