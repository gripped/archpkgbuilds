# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>
# Contributor: Pierre-Yves Ritschard <pyr@spootnik.org>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>
# Contributor: David Harrigan <dharrigan@gmail.com>

pkgname=clojure
pkgver=1.12.0.1530
pkgrel=1
pkgdesc='Lisp dialect for the JVM'
url='https://clojure.org/'
arch=(any)
license=(EPL-1.0)
depends=(java-environment)
makedepends=(setconf)
optdepends=('leiningen: automate clojure projects'
            'rlwrap: friendly shell with readline support')
install=clojure.install
source=("https://download.clojure.org/install/clojure-tools-$pkgver.tar.gz")
b2sums=('ac318bf9b0edcb633035329dc494b67557d0fb8e6fab06b1eaeaae216b0c6e3e8206586b09d1d98f568976371289db8327779d62381f1d66bdb49bfc9e8e4259')

prepare() {
  cd clojure-tools
  setconf clj bin_dir=/usr/bin
  setconf clojure install_dir=/usr/share/clojure
  setconf -a clojure.sh 'export CLOJURE_HOME=/usr/share/clojure'
}

package() {
  cd clojure-tools
  install -Dm644 *.edn -t "$pkgdir/usr/share/clojure"
  install -Dm644 *.jar -t "$pkgdir/usr/share/clojure/libexec"
  install -Dm755 clj "$pkgdir/usr/bin/clj"
  install -Dm755 clojure "$pkgdir/usr/bin/clojure"
  install -Dm644 clojure.sh "$pkgdir/etc/profile.d/clojure.sh"
  install -Dm644 clojure.1 "$pkgdir/usr/share/man/man1/clojure.1"
  install -Dm644 clj.1 "$pkgdir/usr/share/man/man1/clj.1"
}

# getver: https://clojure.org/releases/tools
