# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=immich-go
pkgver=0.19.1
pkgrel=1
pkgdesc="Unofficial immich-cli alternative"
arch=('x86_64')
url="https://github.com/simulot/immich-go"
license=('AGPL3')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/simulot/immich-go/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('179d2aa8de76080aae84b6523d0a7d5aaab18a2f2a277727b0cd7b2179f5963c258e907831bd408de1dabeee6823d0f0232152b88086a86a6d7634b12a2f4c1d')

prepare(){
  cd "$pkgname-$pkgver"
  mkdir -p build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build main.go
}

check() {
  cd "$pkgname-$pkgver"
  # Currently broken, oh well
  # go test -v -x ./...
}

package() {
  cd "$pkgname-$pkgver"
  ls -lha build
  install -Dsm755 build/main "$pkgdir/usr/bin/immich-go"
}
