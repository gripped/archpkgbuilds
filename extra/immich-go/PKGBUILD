# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=immich-go
pkgver=0.24.2
pkgrel=1
epoch=1
pkgdesc="Unofficial immich-cli alternative"
arch=('x86_64')
url="https://github.com/simulot/immich-go"
license=('AGPL3')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/simulot/immich-go/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('26b12836ee54f6f125c3b87c2be133a3c0760009bb704175780ea86efff36c7c654792922ee3c04e502f5a92b2c487cb8ce61945b4d37688914ac32341fd09e5')

prepare(){
  cd "$pkgname-$pkgver"
  mkdir -p build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build main.go
}

check() {
  cd "$pkgname-$pkgver"
  go test -v -x ./...
}

package() {
  cd "$pkgname-$pkgver"
  ls -lha build
  install -Dsm755 build/main "$pkgdir/usr/bin/immich-go"
}
