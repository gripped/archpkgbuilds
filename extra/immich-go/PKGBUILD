# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=immich-go
pkgver=0.31.0
pkgrel=1
epoch=1
pkgdesc="Unofficial immich-cli alternative"
arch=('x86_64')
url="https://github.com/simulot/immich-go"
license=('AGPL-3.0-only')
makedepends=('go' 'git')
source=("git+https://github.com/simulot/immich-go.git#tag=v$pkgver")
sha512sums=('6bae2bd5ebcf73edf3a72e0694c6e172d064b0a3277d85e6cca6bb6f0f9627a7c9809a6016ac96c0ac8c7c69b5e258e4af2dee5994b4b769dbc4d36cd2ebd433')

prepare() {
  cd "$pkgname"
  mkdir -p build
}

build() {
  cd "$pkgname"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build \
    -ldflags "-linkmode=external -X github.com/simulot/immich-go/app.Version=$pkgver-arch -X github.com/simulot/immich-go/app.Commit=$(git rev-parse HEAD)" \
    -o build main.go
}

check() {
  cd "$pkgname"
  go test -v -x ./...
}

package() {
  cd "$pkgname"
  ls -lha build
  install -Dsm755 build/main "$pkgdir/usr/bin/immich-go"
}
