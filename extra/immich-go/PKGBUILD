# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=immich-go
pkgver=0.17.1
pkgrel=1
pkgdesc="Unofficial immich-cli alternative"
arch=('x86_64')
url="https://github.com/simulot/immich-go"
license=('AGPL3')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/simulot/immich-go/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('10bdbed1385384ec3eda781b5a50f677f9e29620cbb3b0490a2ce63206a2710f86f6cea7592243c90ce372e6330282a58785ef958375b090587b0b3035731c2e')

prepare(){
  cd "$pkgname-$pkgver"
  mkdir -p build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build main.go
}

check() {
  cd "$pkgname-$pkgver"
  go test -v -x ./...
}

package() {
  cd "$pkgname-$pkgver"
  ls -lha build
  install -Dsm755 build/main "$pkgdir/usr/bin/immich-go"
}
