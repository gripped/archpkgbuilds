# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=immich-go
pkgver=0.15.0
pkgrel=1
pkgdesc="Unofficial immich-cli alternative"
arch=('x86_64')
url="https://github.com/simulot/immich-go"
license=('AGPL3')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/simulot/immich-go/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('f01bf509d8f0f8ed27089b144e7473fdda982fc10f3e6dded7bb9c02f5326db95b7a894a0351446268337c0cf09451f4f1c5a676bceb9a09f1235c2e94ad005b')

prepare(){
  cd "$pkgname-$pkgver"
  mkdir -p build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build main.go
}

check() {
  cd "$pkgname-$pkgver"
  go test -v -x ./...
}

package() {
  cd "$pkgname-$pkgver"
  ls -lha build
  install -Dsm755 build/main "$pkgdir/usr/bin/immich-go"
}
