# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname='keras'
pkgname=("python-${_pkgname}")
pkgdesc='Deep Learning for humans'
url='https://keras.io/'
license=('Apache')
pkgver=3.3.3
pkgrel=1
arch=('any')
source=("https://files.pythonhosted.org/packages/py3/${_pkgname::1}/$_pkgname/${_pkgname//-/_}-$pkgver-py3-none-any.whl"
        "${_pkgname}-${pkgver}-LICENSE::https://raw.githubusercontent.com/keras-team/${_pkgname}/v${pkgver}/LICENSE")
makedepends=('python-build' 'python-installer' 'python-wheel')
depends=('absl-py' 'python' 'python-h5py' 'python-optree' 'python-rich'
         'python-ml-dtypes' 'python-packaging' 'python-numpy' 'python-pandas'
         'python-pillow' 'python-pydot' 'python-scipy' 'python-tensorflow' 'python-yaml'
         'tensorboard')
optdepends=('python-pytorch')
conflicts=('python-keras-preprocessing')
provides=('python-keras-preprocessing')
replaces=('python-keras-preprocessing')
b2sums=('02b1d573ee49e4de12804eef2bcfb0180c10bcfdb9d2febef31f10493651f89ca5c05d615c01b73209060557a341064c5e502717f8050e3e2b04f88049254a38'
        'a7012f8ac44cd4cc809a89967060560383ac20c7e66f13e30dbd2e28ed0c60a78a84ee4f58baf21f7f5ef63cefe5b6bb4b65d2046e1cc98049218040b06b104d')

package () {
  python -m installer --destdir="$pkgdir" *.whl
  install -Dm 644 "${_pkgname}-${pkgver}-LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
