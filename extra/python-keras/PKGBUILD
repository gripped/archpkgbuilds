# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname='keras'
pkgname=("python-${_pkgname}")
pkgdesc='Deep Learning for humans'
url='https://keras.io/'
license=('Apache-2.0')
pkgver=3.13.2
pkgrel=1
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname//-/_}/${_pkgname//-/_}-$pkgver.tar.gz")
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
depends=('absl-py' 'python' 'python-h5py' 'python-ml-dtypes' 'python-optree'
         'python-numpy' 'python-packaging' 'python-pandas' 'python-pillow'
         'python-pydot' 'python-rich' 'python-scipy' 'python-tensorflow'
         'tensorboard')
optdepends=('python-pytorch'
            'python-requests: for RemoteMonitor'
            'python-scikit-learn: wrappers for scikit-learn')
conflicts=('python-keras-applications' 'python-keras-preprocessing')
provides=('python-keras-applications' 'python-keras-preprocessing')
replaces=('python-keras-applications' 'python-keras-preprocessing')
b2sums=('56a164dec93e37e99ba29d3b36feca79cd1bb0843f9481ccfee01d6f23ca9cc27191ebd8aa69e3e75e58c4bbbf6dd780f0e8ad5896275c39ed92059963a7501b')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package () {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
