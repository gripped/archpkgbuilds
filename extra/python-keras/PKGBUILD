# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname='keras'
pkgname=("python-${_pkgname}")
pkgdesc='Deep Learning for humans'
url='https://keras.io/'
license=('Apache-2.0')
pkgver=3.11.3
pkgrel=1
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname//-/_}/${_pkgname//-/_}-$pkgver.tar.gz")
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
depends=('absl-py' 'python' 'python-h5py' 'python-ml-dtypes' 'python-optree'
         'python-numpy' 'python-packaging' 'python-pandas' 'python-pillow'
         'python-pydot' 'python-rich' 'python-scipy' 'python-tensorflow'
         'tensorboard')
optdepends=('python-pytorch'
            'python-requests: for RemoteMonitor'
            'python-scikit-learn: wrappers for scikit-learn')
conflicts=('python-keras-applications' 'python-keras-preprocessing')
provides=('python-keras-applications' 'python-keras-preprocessing')
replaces=('python-keras-applications' 'python-keras-preprocessing')
b2sums=('c9c971c6f0bc1fbd13bf2598f38947525e5a8a4c0f069333b8137e8716852e5f9130483033aa9fc4e2e87560fb4890d8599227b2c5bc5d438e435ab265c5ff15')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package () {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
