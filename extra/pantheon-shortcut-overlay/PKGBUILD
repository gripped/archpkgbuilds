# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-shortcut-overlay
pkgver=8.0.1
pkgrel=1
pkgdesc='A native, OS-wide shortcut overlay'
arch=(x86_64)
url=https://github.com/elementary/shortcut-overlay
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  libgee
  libgranite-7.so
)
makedepends=(
  git
  meson
  vala
)
_tag=53b974e0ec38b76f425e0a75e274b5e57458a9c6
source=(pantheon-shortcut-overlay::git+https://github.com/elementary/shortcut-overlay.git#tag=${_tag})
b2sums=('3454d51370ef58a2a692215e04df6868173d105fa863b5b145d4e13fe0b4749d2a660c86ba1491a5d1e15d2585b0ba0f1e419c9614dcdbb7934cbd82f24251b4')

prepare() {
  cd pantheon-shortcut-overlay
  git cherry-pick -n 130f78eb4b7770586ea98ba0a5fdbbf5bb116f3f
}

pkgver() {
  cd pantheon-shortcut-overlay
  git describe --tags
}

build() {
  arch-meson pantheon-shortcut-overlay build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
