# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-shortcut-overlay
pkgver=8.1.0
pkgrel=1
pkgdesc='A native, OS-wide shortcut overlay'
arch=(x86_64)
url=https://github.com/elementary/shortcut-overlay
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  libgee
  libgranite-7.so
  pantheon-wayland
)
makedepends=(
  git
  meson
  vala
)
_tag=f831f994c36295fb236cf87c722cf733f43d32eb
source=(pantheon-shortcut-overlay::git+https://github.com/elementary/shortcut-overlay.git#tag=${_tag})
b2sums=('66d8d2ab9ebf77906191e18d7f857c3771e6134437bba4874edef22025b45da54eea84b7c0824d9f03e981a80ec724f3372c39437e8fbbd5b0a842a07a91de3f')

prepare() {
  cd pantheon-shortcut-overlay
  git cherry-pick -n 130f78eb4b7770586ea98ba0a5fdbbf5bb116f3f
}

pkgver() {
  cd pantheon-shortcut-overlay
  git describe --tags
}

build() {
  arch-meson pantheon-shortcut-overlay build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
