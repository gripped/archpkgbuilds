# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=genius
pkgver=1.0.27
pkgrel=4
pkgdesc='Advanced calculator including a mathematical programming language'
arch=(x86_64)
url='https://www.jirka.org/genius.html'
license=(GPL-3.0-or-later)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gmp
  gtk3
  gtksourceview4
  hicolor-icon-theme
  mpfr
  ncurses
  pango
  readline
  vte3
)
makedepends=(
  autoconf-archive
  git
  intltool
)
source=(
  "git+https://gitlab.gnome.org/GNOME/genius.git#tag=${pkgname}_${pkgver//./_}"
  org.gnome.Genius.appdata.xml
)
b2sums=(
  08e9c154a6750240b6c4e6175cfda43c19fd2e4f7d3961a6aeff9f38ea515a5e5c21b8ee36c1622e1692cb6d99c07953bd064b6744c4dce28c59f8065736878a
  6fffc6df8f2afd779e856df8add87317d34df9aef7b19adaeb513d2591e6136acdb149cd3a992512605a4e125f028d95781ae73824c62ab09f89021d377bb9bf
)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/genius \
    --disable-update-mimedb
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir/usr/share/metainfo/" ../org.gnome.Genius.appdata.xml
}
