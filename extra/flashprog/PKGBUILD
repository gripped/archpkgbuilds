# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Nico Huber <nico.h@gmx.de>

pkgname=flashprog
pkgver=1.3
pkgrel=3
pkgdesc='A utility which can be used to detect, read, erase, or write BIOS chips (DIP, PLCC, SPI)'
arch=('x86_64')
url='https://flashprog.org/'
license=(GPL-2.0-or-later)
depends=(
  libftdi
  libgpiod
  libjaylink
  libusb
  pciutils
)
makedepends=(
  git
  meson
)
optdepends=(
  'dmidecode: for SMBIOS/DMI table decoder support'
)
source=(git+https://review.sourcearcade.org/flashprog.git?signed#tag=v${pkgver})
b2sums=('5d14261c66addbf6db326b875d7a6d133bacafee906eb8efac84e7751bc472099bbfc696cd0287701e88855e824bf6e1615ce250a80fcb80ec54b05c17f85387')
validpgpkeys=('2853079C9C66AB7E82C64966A5C163B7E557CAEB')

prepare() {
  sed -i 's#GROUP="plugdev"#TAG+="uaccess"#g' flashprog/util/50-flashprog.rules
}

build() {
  arch-meson ${pkgname} build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 flashprog/util/50-flashprog.rules "${pkgdir}"/usr/lib/udev/rules.d/50-flashprog.rules
}

