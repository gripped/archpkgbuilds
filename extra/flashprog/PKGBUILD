# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Nico Huber <nico.h@gmx.de>

pkgname=flashprog
pkgver=1.4
pkgrel=1
pkgdesc='A utility which can be used to detect, read, erase, or write BIOS chips (DIP, PLCC, SPI)'
arch=('x86_64')
url='https://flashprog.org/'
license=(GPL-2.0-or-later)
depends=(
  libftdi
  libgpiod
  libjaylink
  libusb
  pciutils
)
makedepends=(
  git
  meson
)
optdepends=(
  'dmidecode: for SMBIOS/DMI table decoder support'
)
source=(git+https://review.sourcearcade.org/flashprog.git?signed#tag=v${pkgver})
b2sums=('41fe0d7babcd1d17a55b91b79b79b621f519a5c816a0177a1ff269f8114dbca306f6f0bf31552982b4066cd26f71f3a12f39d361109a920f07ac68f566a97241')
validpgpkeys=('2853079C9C66AB7E82C64966A5C163B7E557CAEB')

prepare() {
  sed -i 's#GROUP="plugdev"#TAG+="uaccess"#g' flashprog/util/50-flashprog.rules
}

build() {
  arch-meson ${pkgname} build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 flashprog/util/50-flashprog.rules "${pkgdir}"/usr/lib/udev/rules.d/50-flashprog.rules
}

