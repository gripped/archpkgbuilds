# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=ali
pkgver=0.8.0
pkgrel=1
pkgdesc="Generate HTTP load and plot the results in real-time"
arch=('x86_64')
url="https://github.com/nakabonne/ali"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('8ad6f4b88ebf032327a5204683ebf1efeff499d3c0888f56e98c913cc383524d41bf1ba3b0517989a8f75f87907c256ce556ebbac19d20e6620a733fa574cfaa')

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o "$pkgname" .
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
