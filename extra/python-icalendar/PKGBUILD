# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=python-icalendar
_name=${pkgname#python-}
pkgver=6.3.0
pkgrel=1
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=(any)
url=https://github.com/collective/icalendar
license=(BSD-2-Clause)
depends=(python-dateutil)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
)
checkdepends=(
  python-hypothesis
  python-pytest
  python-pytz
)
source=(
  "git+$url.git#tag=v$pkgver"
  tzdata.patch
)
b2sums=('e371dd6abfa8d6b120951d9516c4a2dad25dcebf8c252fcbccb0526c9e56a8d44eba4a721ad9658c2622cad8b0badea6caa87f62db66df9f69c8d03d8940e3c1'
        '97090020758909ff25acd3eb42a426f6bbf763a4fce3fb798f615c04a4c177dd2d07112818495d3f00db8ad17a3098233211a1f53364f57528a887bd149dd1fa')

prepare() {
  cd "$_name"
  patch --forward --input=../tzdata.patch
}

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  # https://github.com/collective/icalendar/issues/763
  pytest -v -k 'not test_we_can_identify_dateutil_timezones and not test_can_identify_dateutil and not test_timezone_names_are_known[tzp_0-America/Coyhaique]'
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"

  install -d "$pkgdir/usr/share/licenses/$pkgname/"
  ln -s "$site_packages/$_name-$pkgver.dist-info/licenses/LICENSE.rst" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"

  rm -frv "$pkgdir/$site_packages/$_name/tests/"
}
