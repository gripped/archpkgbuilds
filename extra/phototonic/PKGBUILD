# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Peter Mattern <pmattern at arcor dot de>
# Contributor: Michael Straube <straubem at gmx dot de>
# Contributor: Ofer Kashayov <oferkv at gmail dot com>

pkgname=phototonic
pkgver=3.1.0
pkgrel=1
pkgdesc="Image viewer and organizer built with Qt and Exiv2"
arch=('x86_64')
url="https://github.com/luebking/${pkgname}"
license=('GPL-3.0-or-later')
depends=('exiv2' 'hicolor-icon-theme' 'qt6-imageformats' 'qt6-svg')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('034f6a100b7e4d3755e2d7320bf4685bc1d8e9a84f18737e143b61b29ff57aa3')

prepare() {
	sed -i '/INSTALLS +=/ s/ iconPixmaps//' "${pkgname}-${pkgver}/phototonic.pro"
}

build() {
	cd "${pkgname}-${pkgver}/"
	qmake6 PREFIX=/usr phototonic.pro
	make
}

package() {
	make -C "${pkgname}-${pkgver}" INSTALL_ROOT="${pkgdir}" install
}
