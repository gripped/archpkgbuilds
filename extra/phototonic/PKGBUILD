# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Peter Mattern <pmattern at arcor dot de>
# Contributor: Michael Straube <straubem at gmx dot de>
# Contributor: Ofer Kashayov <oferkv at gmail dot com>

pkgname=phototonic
pkgver=3.0.0
pkgrel=1
pkgdesc='Image viewer and organizer built with Qt and Exiv2'
arch=('x86_64')
url='https://github.com/luebking/phototonic'
license=('GPL3')
depends=('exiv2' 'hicolor-icon-theme' 'qt6-imageformats' 'qt6-svg')
source=("https://github.com/luebking/phototonic/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('e7015bbfd4e0d4f152f503e2e1ee7b698f048b5ad77e185716eb8da6d65fff91')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/INSTALLS +=/ s/ iconPixmaps//' phototonic.pro
}

build() {
  cd $pkgname-$pkgver
  qmake6 PREFIX=/usr phototonic.pro
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
