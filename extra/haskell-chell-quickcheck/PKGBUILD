# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=chell-quickcheck
pkgname=haskell-chell-quickcheck
pkgver=0.2.5.4
pkgrel=79
pkgdesc="QuickCheck support for the Chell testing library"
url="https://github.com/typeclasses/chell"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-chell' 'haskell-quickcheck' 'haskell-random')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        quickcheck-2.15.patch)
sha512sums=('967f61e1e6d03c2c1d5195e6efdfcb33c6fcded0b3383ca2ac6cb4dedce163634d841b5eba77cebbe7f6952dfbb95bb72e08ec8d1ead64fdb4be7d8780f20c64'
            '3434db0bcd3178bf8e1c55ef85c36c2d84480347ce1e16c4264f4ab72ba88915e77f0e3138ace3d54533a8c04bc9d272aeaabc79d836c253be30a63f324c7385')

prepare() {
    cd $_hkgname-$pkgver
    patch -p2 -i ../quickcheck-2.15.patch
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
