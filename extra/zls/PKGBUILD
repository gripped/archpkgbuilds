# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=zls
pkgver=0.14.0
pkgrel=2
pkgdesc="A language server for Zig"
arch=('x86_64')
url="https://github.com/zigtools/zls"
license=('MIT')
depends=('zig')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/zigtools/zls/archive/refs/tags/${pkgver}.tar.gz")
# From build.zig.zon
source+=(
  "known-folders-aa24df42183ad415d10bc0a33e6238c437fc0f59.tar.gz::https://github.com/ziglibs/known-folders/archive/aa24df42183ad415d10bc0a33e6238c437fc0f59.tar.gz"
  "diffz-ef45c00d655e5e40faf35afbbde81a1fa5ed7ffb.tar.gz::https://github.com/ziglibs/diffz/archive/ef45c00d655e5e40faf35afbbde81a1fa5ed7ffb.tar.gz"
  "zig-lsp-codegen-063a98c13a2293d8654086140813bdd1de6501bc.tar.gz::https://github.com/zigtools/zig-lsp-codegen/archive/063a98c13a2293d8654086140813bdd1de6501bc.tar.gz"
)
noextract=("${source[@]:1}")
sha256sums=('44cae74073b2f75cf627755398afadafaa382cccf7555b5b66b147dcaa6cef0d'
            '500d9df2df8b44060dac523b0402126c0341e01d77f59101e1335654b8ae25a1'
            '1dd8f4678171bfd2476aa74743e48bce10aad2b90c2df038d3ac6b0ef37fc3ba'
            'eeb9d02dd9e5b7fae2444444977d5786a4199f81491c601c41acffcdcb251cdb')

prepare() {
  zig fetch --global-cache-dir ./zig-global-cache "./${source[1]%%::*}"
  zig fetch --global-cache-dir ./zig-global-cache "./${source[2]%%::*}"
  zig fetch --global-cache-dir ./zig-global-cache "./${source[3]%%::*}"
}

build() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="build" zig build \
    --summary all \
    --global-cache-dir ../zig-global-cache \
    --system ../zig-global-cache/p \
    --prefix /usr \
    --release=safe \
    -Dtarget=native-linux.6.1-gnu.2.38 \
    -Dcpu=baseline \
    -Dpie=true
}

package() {
  cd "${pkgname}-${pkgver}"
  cp -a build/* "$pkgdir"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
