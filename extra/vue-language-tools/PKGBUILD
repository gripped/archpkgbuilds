# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Yufan You <ouuansteve at gmail>

pkgbase=vue-language-tools
pkgname=vue-language-server
pkgver=3.0.5
pkgrel=1
pkgdesc='Language server for Vue'
arch=(any)
url=https://github.com/vuejs/language-tools/tree/master/packages/language-server
license=(MIT)
depends=(
  nodejs
  typescript
)
makedepends=(
  git
  jq
  npm
  pnpm
)
provides=(vue-typescript-plugin)
replaces=(vue-typescript-plugin)
options=(!emptydirs)
source=("$pkgbase::git+https://github.com/vuejs/language-tools.git#tag=v$pkgver")
b2sums=('194afb0a4a23354afef4e4230e326c4d7a1091ef8ef19a25f9ba308fa7937b5b6ca4ec9a479de40967e169f8fb8f4b7a4c333c7498438f0dd449ede3232d2897')

prepare() {
  cd $pkgbase
  pnpm install --frozen-lockfile
}

build() {
  cd $pkgbase
  COREPACK_ENABLE_STRICT=0 pnpm build
}

check() {
  cd $pkgbase
  COREPACK_ENABLE_STRICT=0 pnpm test
}

package() {
  local mod_dir=/usr/lib/node_modules/@vue/language-server

  install -d "$pkgdir"/{/usr/bin,$mod_dir}
  ln -s $mod_dir/bin/"${pkgname[0]}".js "$pkgdir"/usr/bin/"${pkgname[0]}"

  cd $pkgbase/packages/language-server
  # Prune unnecessary packages
  rm -r node_modules
  npm install --omit=dev --omit=peer

  shopt -s globstar
  export GLOBIGNORE='node_modules/*'
  mapfile -t mod_files < <(jq -r '.files[]' package.json)
  # shellcheck disable=SC2068
  cp --parents -r ${mod_files[@]} node_modules package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
