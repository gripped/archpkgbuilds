# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Yufan You <ouuansteve at gmail>

pkgbase=vue-language-tools
pkgname=(
  vue-language-server
  vue-typescript-plugin
)
pkgver=2.2.10
pkgrel=1
pkgdesc='Language server for Vue'
arch=(any)
url=https://github.com/vuejs/language-tools/tree/master/packages/language-server
license=(MIT)
makedepends=(
  git
  jq
  npm
  pnpm
)
source=("$pkgbase::git+https://github.com/vuejs/language-tools.git#tag=v$pkgver")
b2sums=('da773d91eac7ba54671721e03ae68e3a834c289c29157b13294ae63cf178662a82c00895c8fcb64d426465a9c99b3be33046f8ac4c8c24ecb7164d6a4c90c157')

prepare() {
  cd $pkgbase
  pnpm install --frozen-lockfile
}

build() {
  cd $pkgbase
  COREPACK_ENABLE_STRICT=0 pnpm build
}

check() {
  cd $pkgbase
  COREPACK_ENABLE_STRICT=0 pnpm test
}

package_vue-language-server() {
  depends=(nodejs)
  optdepends=('typescript: for use in typescript.tsdk')

  local mod_dir=/usr/lib/node_modules/@vue/language-server

  install -d "$pkgdir"/{/usr/bin,$mod_dir}
  ln -s $mod_dir/bin/"${pkgname[0]}".js "$pkgdir"/usr/bin/"${pkgname[0]}"

  cd $pkgbase/packages/language-server
  # Prune unnecessary packages
  rm -r node_modules
  npm install --omit=dev

  shopt -s globstar
  export GLOBIGNORE='node_modules/*'
  mapfile -t mod_files < <(jq -r '.files[]' package.json)
  # shellcheck disable=SC2068
  cp --parents -r ${mod_files[@]} node_modules package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/licenses/"${pkgname[0]}" LICENSE
}

package_vue-typescript-plugin() {
  depends=(
    nodejs
    typescript
  )

  local mod_dir=/usr/lib/node_modules/@vue/typescript-plugin

  install -d "$pkgdir"/$mod_dir

  cd $pkgbase/packages/typescript-plugin
  # Prune unnecessary packages
  rm -r node_modules
  npm install --omit=dev

  shopt -s globstar
  export GLOBIGNORE='node_modules/*'
  mapfile -t mod_files < <(jq -r '.files[]' package.json)
  # shellcheck disable=SC2068
  cp --parents -r ${mod_files[@]} node_modules package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/licenses/"${pkgname[1]}" LICENSE
}
