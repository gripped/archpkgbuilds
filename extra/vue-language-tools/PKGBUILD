# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Yufan You <ouuansteve at gmail>

pkgbase=vue-language-tools
pkgname=('vue-language-server' 'vue-typescript-plugin')
pkgver=2.0.19
pkgrel=1
pkgdesc='Language server for Vue'
arch=('any')
url=https://github.com/vuejs/language-tools/tree/master/packages/language-server
license=('MIT')
makedepends=('git' 'jq' 'npm' 'pnpm')
source=("$pkgbase::git+https://github.com/vuejs/language-tools.git#tag=v$pkgver")
b2sums=('14e256e37fdac5b70920e10a3424204242228631a2ed47a832a39975ab87c26faa7f685acd7d185b778d57b1ae89dc6f045097207779d1b3efc5091de654531c')

prepare() {
  cd $pkgbase
  pnpm install --frozen-lockfile
}

build() {
  cd $pkgbase
  COREPACK_ENABLE_STRICT=0 pnpm build
}

check() {
  cd $pkgbase
  COREPACK_ENABLE_STRICT=0 pnpm test
}

package_vue-language-server() {
  depends=('nodejs')
  optdepends=('typescript: for use in typescript.tsdk')

  local mod_dir=/usr/lib/node_modules/@vue/language-server
  install -d "$pkgdir"/{/usr/bin,$mod_dir}
  ln -s $mod_dir/bin/"${pkgname[0]}".js "$pkgdir"/usr/bin/"${pkgname[0]}"

  cd $pkgbase/packages/language-server
  # Prune unnecessary packages
  rm -r node_modules
  npm install --production

  shopt -s globstar
  export GLOBIGNORE='node_modules/*'
  readarray -t mod_files < <(jq -r '.files[]' package.json)
  # shellcheck disable=SC2068
  cp --parents -r ${mod_files[@]} node_modules package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/licenses/"${pkgname[0]}" LICENSE
}

package_vue-typescript-plugin() {
  depends=('nodejs' 'typescript')

  local mod_dir=/usr/lib/node_modules/@vue/typescript-plugin
  install -d "$pkgdir"/$mod_dir

  cd $pkgbase/packages/typescript-plugin
  # Prune unnecessary packages
  rm -r node_modules
  npm install --production

  shopt -s globstar
  export GLOBIGNORE='node_modules/*'
  readarray -t mod_files < <(jq -r '.files[]' package.json)
  # shellcheck disable=SC2068
  cp --parents -r ${mod_files[@]} node_modules package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/licenses/"${pkgname[1]}" LICENSE
}
