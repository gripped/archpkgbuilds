# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Mikuro Kagamine <mikurok@forgecrushing.com>

pkgname=croc
pkgver=10.0.10
pkgrel=1
epoch=1
pkgdesc='Easily and securely send things from one computer to another.'
arch=(x86_64)
url="https://github.com/schollz/$pkgname"
license=(MIT)
depends=(glibc)
makedepends=(go)
_archive="$pkgname-v$pkgver"
source=("$_archive.tar.gz::$url/releases/download/v$pkgver/${pkgname}_v${pkgver}_src.tar.gz")
sha256sums=('9f95626cbe251d1b08f468098497bec88fcc4429aa9c6917e8f1c918e109a144')

build() {
    cd "$_archive"
    export CGO_CPPFLAGS="$CPPFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS='-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=vendor -modcacherw -buildvcs=false'
    go build
}

check() {
    cd "$_archive"
    go test ./...
}

package() {
    cd "$_archive"
    install -Dm0755 -t "$pkgdir/usr/bin/" "$pkgname"
    install -Dm0644 -t "$pkgdir/usr/lib/systemd/system/" "$pkgname.service"
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
