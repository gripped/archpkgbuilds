# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=ssh-keyonly
pkgver=0.1.0
pkgrel=1
pkgdesc="Audit an ssh server for supported authentication methods"
url='https://github.com/kpcyrd/ssh-keyonly'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('b6f6c0d9fc9988035ed2f10269f1e74b34c3aa0ca618146e5ecf637cfaa7f0f0')
b2sums=('81a503dff61bb48b8fc7e7cb23cddbf88d139a38f52dc78a6d1283de0b454987b917af635912fb3d3caa9d5ef575b5e8ed00fe00e51978fd1d651d99b9a732ab')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/ssh-keyonly --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/ssh-keyonly
}

# vim: ts=2 sw=2 et:
