# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=ssh-keyonly
pkgver=0.1.2
pkgrel=2
pkgdesc="Audit an ssh server for supported authentication methods"
url='https://codeberg.org/kpcyrd/ssh-keyonly'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'glibc'
  'libgcc'
)
makedepends=(
  'cargo'
  'git'
)
options=(!lto)
source=(git+https://codeberg.org/kpcyrd/${pkgname}.git#tag=v$pkgver)
sha256sums=('683b071b65e5889bf48d884b6f44b90a7be9ce8ca3107d9d3abe840e6a8f3c9c')
b2sums=('e49c186cadebf620cf65e908a468585ccff9cc1b1896e678a1507b75057c167ec78760ef78fc24e1fab048363a8575fec315bed86704b0d6cd355c9392b0fda4')

prepare() {
  cd "${pkgname}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}"
  cargo test --frozen
  target/release/ssh-keyonly --help
}

package() {
  cd "${pkgname}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/ssh-keyonly
}

# vim: ts=2 sw=2 et:
