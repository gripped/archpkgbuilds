# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=ssh-keyonly
pkgver=0.1.2
pkgrel=1
pkgdesc="Audit an ssh server for supported authentication methods"
url='https://codeberg.org/kpcyrd/ssh-keyonly'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://codeberg.org/kpcyrd/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('12536762b9d9a584f5063f3ff4e20e9884eeb14692fb769b9af695ef32d8c55a')
b2sums=('bb61724fe854ee5dc4ec4286650f756181a6a29e23c67a279509b3ca8f248ae8eb847e1c45fbedbdd43dd57184e80de69467972fd463f76e6e6108c1c0d36504')

prepare() {
  cd "${pkgname}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}"
  cargo test --frozen
  target/release/ssh-keyonly --help
}

package() {
  cd "${pkgname}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/ssh-keyonly
}

# vim: ts=2 sw=2 et:
