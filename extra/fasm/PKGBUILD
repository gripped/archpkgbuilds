# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris Andrews <christophersimonandrews [at] gmail [dot] com>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Serge Victor <arch@random.re>
# Contributor: Krzysztof Wloch <wloszekk [at] gmail [dot] com>
# Contributor: Luke McCarthy <luke@iogopro.co.uk>

pkgname=fasm
pkgver=1.73.33
pkgrel=1
pkgdesc='Fast and efficient self-assembling x86 assembler for DOS, Windows and Linux operating systems'
url='https://flatassembler.net/'
arch=('x86_64')
license=('LicenseRef-BSD-2-Clause-fasm')
depends_x86_64=('lib32-glibc' 'lib32-gcc-libs')
source=(https://flatassembler.net/${pkgname}-${pkgver}.tgz)
sha512sums=('33eab58a8f030f49c60eae906224aea24e9423220241eda7f3206a7e3f6a0c3ecc55643ca4d008e9935941b93c84edfe16a519449a3e2a5e58b5c346d930d0e9')
b2sums=('d3a5c343b74f473c6fc1e164e6d4abebae202345e85c967ecfcfa3c7747b76be7e6f00fec012acb1f4d84d2712f0e2e6c74659c030cab0a384731385464ed89d')
options=(!strip)

build() {
  cd ${pkgname}

  if [ "${CARCH}" == "x86_64" ]; then
    SRCDIR=Linux/x64
    ./fasm.x64 source/${SRCDIR}/fasm.asm
  fi
  mv source/${SRCDIR}/fasm ./fasm

  # tools
  local s
  for s in listing prepsrc symbols; do
    ./fasm tools/libc/${s}.asm ${s}.o
    gcc -m32 ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -o ${s} ${s}.o
  done
}

package() {
  cd ${pkgname}

  install -Dm 755 fasm -t "${pkgdir}/usr/bin"
  # tools
  local s
  for s in listing prepsrc symbols; do
    install -Dm 755 ${s} "${pkgdir}/usr/bin/fasm-${s}"
  done

  # docs
  install -Dm 644 ${pkgname}.txt whatsnew.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/elfexe/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe"
  install -Dm 644 examples/elfexe/dynamic/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe/dynamic"
  install -Dm 644 examples/elfexe/dynamic/*.inc -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe/dynamic"
  install -Dm 644 examples/elfobj/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfobj"
  install -Dm 644 examples/libcdemo/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/libcdemo"
  install -Dm 644 examples/libcdemo/*.inc -t "${pkgdir}/usr/share/doc/${pkgname}/examples/libcdemo"
  install -Dm 644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
