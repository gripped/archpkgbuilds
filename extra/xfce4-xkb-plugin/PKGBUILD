# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-xkb-plugin
pkgver=0.8.4
pkgrel=1
pkgdesc="Plugin to switch keyboard layouts for the Xfce4 panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-xkb-plugin/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxklavier' 'librsvg' 'libwnck3' 'libnotify')
makedepends=('git' 'xfce4-dev-tools')
optdepends=('xfce4-notifyd: show notifications on layout change')
install=$pkgname.install
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-xkb-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('75b60b711df4e4eff8bc4be3f0f6bd34cf4c3eff109f73d8c6400a662cfbdf76')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
