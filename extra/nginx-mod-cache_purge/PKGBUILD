# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-cache_purge
pkgver=2.5.3
pkgrel=11

_modname="${pkgname#nginx-mod-}"

pkgdesc='Nginx module with ability to purge content from FastCGI, proxy, SCGI and uWSGI caches'
arch=('x86_64')
makedepends=('nginx' 'nginx-src')
url="https://github.com/nginx-modules/ngx_cache_purge"
license=('BSD-2-Clause')

source=(https://github.com/nginx-modules/ngx_cache_purge/archive/$pkgver/ngx_cache_purge-$pkgver.tar.gz)
sha256sums=('43158a75bbc8d4ba66ec9c60a1ec13a792803a36b62ffc3fa01088d8675108e0')
backup=('etc/nginx/modules.d/20-cache_purge.conf')

prepare() {
	mkdir -p build
	cd build
	ln -sf /usr/src/nginx/auto
	ln -sf /usr/src/nginx/src
}

build() {
	cd build
	auto/configure \
		--with-ld-opt="$LDFLAGS" \
		--with-compat \
		--add-dynamic-module=../ngx_cache_purge-$pkgver
	make modules
}

package() {
	if [[ "$BUILDTOOL" == devtools ]]; then
		local nginx_dep="nginx=$(nginx -v 2>&1 | sed 's|.*/||')"
		depends+=($nginx_dep)
	else
		depends+=(nginx)
	fi

	install -Dm644 "$srcdir"/ngx_cache_purge-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	install -dm0755 "$pkgdir"/etc/nginx/modules.d

	cd build/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
		echo "load_module \"/usr/lib/nginx/modules/$mod\";" >> "$pkgdir/etc/nginx/modules.d/20-cache_purge.conf"
	done
}
