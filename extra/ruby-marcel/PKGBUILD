# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='marcel'
pkgname="ruby-${_gemname}"
pkgver=1.1.0
pkgrel=1
pkgdesc='Simple mime type detection using magic numbers, filenames, and extensions'
arch=('any')
url='https://github.com/rails/marcel'
license=('Apache' 'MIT')
options=(!emptydirs)
depends=('ruby')
makedepends=('ruby-bundler' 'ruby-rake')
checkdepends=('ruby-minitest' 'ruby-rack')
source=(
  "${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
)
sha512sums=('0465955f7cb0d8001d6116c5d3fef6bd6ab4a114cab5a8efb74e17ee53e2ab844aae935e716ddd522d266dd9bf0dd8a0e4e0f5aaee903d8cd61870f4de4617a1')
b2sums=('88ca72d9c11da62052152fab9a8d49b170509365386efac281704b5e9722925aa3a8f7290e4dd26316bcffe0db49f1bafc45a529cd1585c12fc4bc415323020b')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we dont need debuggers for testing
  sed --in-place --regexp-extend '/byebug/d' "${_gemname}.gemspec"  test/test_helper.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  rake build
}

check() {
  cd "${_gemname}-${pkgver}"

  rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -Dm 644 APACHE-LICENSE MIT-LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
