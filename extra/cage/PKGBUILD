# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

pkgname=cage
pkgver=0.2.1
pkgrel=1
pkgdesc="A kiosk compositor for Wayland"
arch=(x86_64)
url="https://www.hjdskes.nl/projects/cage/"
license=(MIT)
depends=(
  'glibc'
  'wayland'
  'wlroots0.19'
)
makedepends=(
  'git'
  'libxkbcommon'
  'meson'
  'scdoc'
  'wayland-protocols'
  'xorg-server-xwayland'
)
provides=('wayland-compositor')
optdepends=(
  'polkit: System privilege control. Required if not using seatd service'
  'xorg-server-xwayland: X11 support'
)
# It's not uncommon to need to update to the latest commit so we use git-based
# fetching.
source=("git+https://github.com/cage-kiosk/cage.git#tag=v$pkgver?signed")
install=cage.install
sha512sums=('4cda60e4fcdffeefb5ff8c0b50fee4fe3d008f706d3bae1cc4ac899e3cf72f01da2bb806626fc0043521669136a24f839f672a3670f3b992b4e7584a1bbc6fc1')
validpgpkeys=(
    '13B6A35E9FD677D02CD68DBE37C445296EBC43B1' # Jente Hidskes <dev@hjdskes.nl>
    '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48' # Simon Ser <contact@emersion.fr>
)

build() {
  arch-meson "$pkgname" build
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(libwlroots-0.19.so libxkbcommon.so)

  DESTDIR="${pkgdir}" ninja -C build install
  install -vDm 644 "$pkgname/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "$pkgname/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
