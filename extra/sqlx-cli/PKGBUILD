# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=sqlx-cli
pkgver=0.7.3
pkgrel=1
pkgdesc='Command-line utility for SQLx, the Rust SQL toolkit'
license=('Apache' 'MIT')
arch=('x86_64')
url="https://github.com/launchbadge/sqlx"
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/launchbadge/sqlx/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('2868f84b02585a6894d3737b0477b616262fb9e76341f40552128780b32c2d5ac2d9707775a205b7e0e72751dddc2306f854712fa8ba91e80419d62ee4fc3c61')
options=('!lto')

build() {
    cd "sqlx-$pkgver/sqlx-cli"
    # Upstream messed up this release so we can't lock it: https://github.com/launchbadge/sqlx/issues/3031
    #cargo build --locked --release
    cargo build --release
}

check() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo test
    # Currently broken
    #cargo test --locked
}

package() {
    cd "sqlx-$pkgver"
    install -Dm755 "target/release/cargo-sqlx" "$pkgdir/usr/bin/cargo-sqlx"
    install -Dm755 "target/release/sqlx" "$pkgdir/usr/bin/sqlx"
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
