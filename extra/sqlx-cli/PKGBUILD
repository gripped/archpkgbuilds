# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=sqlx-cli
pkgver=0.8.6
pkgrel=2
pkgdesc='Command-line utility for SQLx, the Rust SQL toolkit'
license=('Apache-2.0' 'MIT')
arch=('x86_64')
url="https://github.com/launchbadge/sqlx"
depends=('libgcc' 'glibc' 'openssl' 'sqlite')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/launchbadge/sqlx/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('d2d6713c11f0ba77d24cc1c48e62c528ec1cb2095fd70e6bb10ecab41d27bfa9d1fc58ea6663548b9fadefb00a0e954bf71dfb1a299d8bf06097c3c6907ad920')

build() {
    cd "sqlx-$pkgver/sqlx-cli"
    LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo build --locked --release
}

check() {
    cd "sqlx-$pkgver/sqlx-cli"
    LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo test --locked
}

package() {
    cd "sqlx-$pkgver"
    install -Dm755 -t "$pkgdir/usr/bin" \
        "target/release/cargo-sqlx" \
        "target/release/sqlx"

    # install completions
    install -d "$pkgdir/usr/share/bash-completion/completions" \
        "$pkgdir/usr/share/zsh/site-functions" \
        "$pkgdir/usr/share/fish/vendor_completions.d"
    "$pkgdir/usr/bin/sqlx" completions bash > "$pkgdir/usr/share/bash-completion/completions/sqlx"
    "$pkgdir/usr/bin/sqlx" completions zsh > "$pkgdir/usr/share/zsh/site-functions/_sqlx"
    "$pkgdir/usr/bin/sqlx" completions fish > "$pkgdir/usr/share/fish/vendor_completions.d/sqlx.fish"

    # install license files
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
