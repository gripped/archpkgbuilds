# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tharre <tharre3@gmail.com>

pkgname=python-enlighten
pkgver=1.14.1
pkgrel=1
pkgdesc='Progress bar for Python console apps'
url=https://github.com/Rockhopper-Technologies/enlighten
arch=(any)
license=(MPL2)
depends=(
  python-blessed
  python-prefixed
)
makedepends=(
  git
  python-setuptools
)
_tag=5ca1d2e206f8d9539e9e502ee156b83fac6721ed
source=(git+https://github.com/Rockhopper-Technologies/enlighten.git#tag=${_tag})
b2sums=('41635333bb2a37a3fae9a3d2c9ffb193bf2c920b93359ced2624785cc72e539c2065adbd2030edd9eb7e235209ecde30fc1495337d5fc667192eeb379b0cbac5')

prepare() {
  rm -rf enlighten/benchmarks
}

pkgver() {
  cd enlighten
  git describe --tags
}

build() {
  cd enlighten
  python setup.py build
}

package() {
  cd enlighten
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
