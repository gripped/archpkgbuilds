# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tharre <tharre3@gmail.com>

pkgname=python-enlighten
pkgver=1.12.0
pkgrel=1
pkgdesc='Progress bar for Python console apps'
url=https://github.com/Rockhopper-Technologies/enlighten
arch=(any)
license=(MPL2)
depends=(
  python-blessed
  python-prefixed
)
makedepends=(
  git
  python-setuptools
)
_tag=6f8aaae0975e85fbd8533c4baba21364dd1e3886
source=(git+https://github.com/Rockhopper-Technologies/enlighten.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  rm -rf enlighten/benchmarks
}

pkgver() {
  cd enlighten
  git describe --tags
}

build() {
  cd enlighten
  python setup.py build
}

package() {
  cd enlighten
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
