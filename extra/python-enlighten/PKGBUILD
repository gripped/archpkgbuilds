# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tharre <tharre3@gmail.com>

pkgname=python-enlighten
pkgver=1.13.0
pkgrel=1
pkgdesc='Progress bar for Python console apps'
url=https://github.com/Rockhopper-Technologies/enlighten
arch=(any)
license=(MPL2)
depends=(
  python-blessed
  python-prefixed
)
makedepends=(
  git
  python-setuptools
)
_tag=9ac26ff23e39e8f39680d30afdd9eabf0fd31b51
source=(git+https://github.com/Rockhopper-Technologies/enlighten.git#tag=${_tag})
b2sums=('403657e8ebdc31044ec0a12ed6d8755eeb5ffcc79356ea48acbbd132e090ed71438f9b618b150f21271f637fa333fa54cee9cf26f6411b45fea87a6ab31fd94f')

prepare() {
  rm -rf enlighten/benchmarks
}

pkgver() {
  cd enlighten
  git describe --tags
}

build() {
  cd enlighten
  python setup.py build
}

package() {
  cd enlighten
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
