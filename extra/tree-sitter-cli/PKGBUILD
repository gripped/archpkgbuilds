# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tree-sitter-cli
_name=${pkgname%-cli}
pkgver=0.21.0
pkgrel=1
pkgdesc='CLI tool for developing, testing, and using Tree-sitter parsers'
arch=('x86_64')
url=https://github.com/tree-sitter/tree-sitter
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'rust')
optdepends=('nodejs: for the generate subcommand')
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
b2sums=('SKIP')

prepare() {
  cd "$_name"/cli
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_name"/cli
  cargo build --release --locked --offline --all-features
}

package() {
  cd "$_name"
  install -Dt "$pkgdir"/usr/bin target/release/"$_name"
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

