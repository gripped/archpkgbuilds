# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=48.2
pkgrel=1
epoch=1
pkgdesc="Music player and management application"
url="https://apps.gnome.org/Music/"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  graphene
  grilo
  grilo-plugins
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgirepository
  libmediaart
  libsoup3
  localsearch
  python
  python-cairo
  python-gobject
  tinysparql
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  yelp-tools
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-music.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('e2281c783d2b6fb3516b5f447134c87513d144004f3b54503763cf4d0c65025f6a17caf77e8bf512f8e692cb05686a088b8826a7e866c0c5044cc012139b45fd')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
