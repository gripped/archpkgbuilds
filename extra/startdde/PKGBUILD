# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=6.1.6
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL-3.0-or-later')
depends=('dconf' 'glib2' 'glibc' 'gtk3' 'libgudev' 'libx11' 'libxi')
makedepends=('go' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/startdde.git#tag=$pkgver")
sha512sums=('46ba31e81607cd091f005595e5eb980036ccb77850bda5673316ce2f9796ccedd7e430662044eeebcf667ba4626e647db625d520b75f47aa6f6b992a8aa708a9')

prepare() {
  cd startdde
  go get github.com/linuxdeepin/go-gir
  sed -i 's/sbin/bin/' Makefile
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd startdde
  make
}

package() {
  cd startdde
  make DESTDIR="$pkgdir" install
}
