# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=6.1.2
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL-3.0-or-later')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
depends=('dconf' 'glib2' 'glibc' 'gtk3' 'libgudev' 'libx11' 'libxi'
         'deepin-wloutput-daemon')
makedepends=('go' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/startdde.git#tag=$pkgver")
sha512sums=('ee102559e20eb567915d81867c9137d506020dc61359127d629909bfb6dd0c3190fff3025059c82e5af2fdb42429f27d7161b05b14b4fbd6379130bd5335503f')

prepare() {
  cd startdde
  go get github.com/linuxdeepin/go-gir
  sed -i 's/sbin/bin/' Makefile
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd startdde
  make
}

package() {
  cd startdde
  make DESTDIR="$pkgdir" install
}
