# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=6.0.11
pkgrel=2
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
depends=('dconf' 'glib2' 'glibc' 'gtk3' 'libgnome-keyring' 'libgudev' 'libx11' 'libxi'
         'deepin-wloutput-daemon')
makedepends=('go' 'git')
groups=('deepin')
source=("https://github.com/linuxdeepin/startdde/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c401dd594fcb8d6019cc1ced3f5e6ed1d4a477585937399adf33fa4e9885f85ffa66e176dd7dc767796df224564caa8ae59e7bc80113e580ea111888702bd89e')

prepare() {
  cd startdde-$pkgver
  sed -i 's/sbin/bin/' Makefile
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install
}
