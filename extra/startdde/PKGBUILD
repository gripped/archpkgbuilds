# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=6.1.5
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL-3.0-or-later')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
depends=('dconf' 'glib2' 'glibc' 'gtk3' 'libgudev' 'libx11' 'libxi'
         'deepin-wloutput-daemon')
makedepends=('go' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/startdde.git#tag=$pkgver")
sha512sums=('1cd1959c07cc75991e26715454e0a6de3c33e7d53d956992784747114a28fdbc3c6df293ab7e318bf80f53e28b2de21ffad4ff525bbb508f4d65dfca4e2a1a53')

prepare() {
  cd startdde
  go get github.com/linuxdeepin/go-gir
  sed -i 's/sbin/bin/' Makefile
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd startdde
  make
}

package() {
  cd startdde
  make DESTDIR="$pkgdir" install
}
