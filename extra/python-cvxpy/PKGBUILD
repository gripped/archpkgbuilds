# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=cvxpy
pkgname=python-$_pyname
pkgver=1.4.0
pkgrel=1
pkgdesc='A Python-embedded modeling language for convex optimization problems'
url='https://www.cvxpy.org/'
license=(Apache)
arch=(x86_64)
depends=(python
         python-ecos
         python-osqp
         python-scipy
         python-scs)
makedepends=(pybind11
             python-build
             python-installer
             python-setuptools
             python-wheel)
optdepends=('python-cvxopt: GLPK solver'
            'python-cylp: CBC solver'
            'python-cvxopt: GLPK solver'
            'python-pyscipopt: SCIP solver')
checkdepends=(python-ecos
              python-osqp
              python-pytest
              python-scs)
source=(https://github.com/cvxpy/cvxpy/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('991aa72316c95791c855b6fb57c3672665ab08d2f377dcb1066dd26654b44f90')

prepare() {
  sed -e 's|,<1.24||' -i $_pyname-$pkgver/pyproject.toml # Drop version constraints
}

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v -k 'not test_scipy_mi_lp_2'
}

package() {
    cd $_pyname-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
}
