# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=cvxpy
pkgname=python-$_pyname
pkgver=1.8.0
pkgrel=1
pkgdesc='A Python-embedded modeling language for convex optimization problems'
url='https://www.cvxpy.org/'
license=(Apache-2.0)
arch=(x86_64)
depends=(gcc-libs
         glibc
         python
         python-clarabel
         python-ecos
         python-highspy
         python-numpy
         python-osqp
         python-scipy
         python-scs)
makedepends=(git
             pybind11
             python-build
             python-installer
             python-setuptools
             python-wheel)
optdepends=('python-cvxopt: GLPK solver'
            'python-cylp: CBC solver'
            'python-pyscipopt: SCIP solver')
checkdepends=(python-cvxopt
              python-cylp
              python-hypothesis
              python-pyscipopt
              python-pytest)
source=(git+https://github.com/cvxpy/cvxpy#tag=v$pkgver)
sha256sums=('7db8aeae67b5910a8abec4da4f7c420e59c9b937f0c04dcfeca76ed5fa2568d4')

prepare() {
  sed -e 's|,<1.24||' -e '/oldest-supported-numpy/d' -i $_pyname/pyproject.toml # Drop version constraints
}

build() {
  cd $_pyname
# https://github.com/cvxpy/cvxpy/issues/1690
  CXXFLAGS="$CFLAGS -DNDEBUG" \
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v -k 'not test_scipy_mi_lp_2 and not test_oprelcone_1_m1_k3_complex and not test_oprelcone_1_m3_k1_complex and not test_oprelcone_2'
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
