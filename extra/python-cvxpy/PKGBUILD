# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=cvxpy
pkgname=python-$_pyname
pkgver=1.5.3
pkgrel=1
pkgdesc='A Python-embedded modeling language for convex optimization problems'
url='https://www.cvxpy.org/'
license=(Apache-2.0)
arch=(x86_64)
depends=(gcc-libs
         glibc
         python
         python-clarabel
         python-ecos
         python-numpy
         python-osqp
         python-scipy
         python-scs)
makedepends=(git
             pybind11
             python-build
             python-installer
             python-setuptools
             python-wheel)
optdepends=('python-cvxopt: GLPK solver'
            'python-cylp: CBC solver'
            'python-pyscipopt: SCIP solver')
checkdepends=(python-pytest)
source=(git+https://github.com/cvxpy/cvxpy#tag=v$pkgver)
sha256sums=('ef201870ce1b34f66c3735593cc8b52313a50b4a04dcf7827f5361b2f95a0dbe')

prepare() {
  sed -e 's|,<1.24||' -e '/oldest-supported-numpy/d' -i $_pyname/pyproject.toml # Drop version constraints
}

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v -k 'not test_scipy_mi_lp_2'
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
