# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname=yubikey-manager
epoch=1
pkgver=5.8.0
pkgrel=2
pkgdesc='Python library and command line tool for configuring a YubiKey'
arch=('any')
url='https://developers.yubico.com/yubikey-manager/'
license=('BSD-2-Clause')
depends=(
  'ccid'
  'python'
  'python-click'
  'python-cryptography'
  'python-fido2'
  'python-keyring'
  'python-pyscard'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-poetry-core'
)
checkdepends=(
  'python-makefun'
  'python-pytest'
)
source=(
  "https://github.com/Yubico/yubikey-manager/releases/download/$pkgver/yubikey_manager-$pkgver.tar.gz"
	"https://github.com/Yubico/yubikey-manager/releases/download/$pkgver/yubikey_manager-$pkgver.tar.gz.sig"
)
sha256sums=('3af0da65e1fdd46763c94ee74e2da55a4b6e7771da776c197f5f4b4581738560'
            'SKIP')
# https://developers.yubico.com/Software_Projects/Software_Signing.html
validpgpkeys=(
  '8D0B4EBA9345254BCEC0E843514F078FF4AB24C3'  # Dag Heyman <dag@yubico.com>
  '57A9DEED4C6D962A923BB691816F3ED99921835E'  # Emil Lundberg (Software Developer) <emil@yubico.com>
  '20EE325B86A81BCBD3E56798F04367096FBA95E8'  # Dain Nilsson <dain@yubico.com>
)

build() {
	cd "${pkgname/-/_}-$pkgver"
	python -m build --wheel --no-isolation

  # Completions
  python -m venv --system-site-packages completion-env
  completion-env/bin/python -m installer dist/*.whl
  _YKMAN_COMPLETE=bash_source completion-env/bin/ykman > ykman.bash
  _YKMAN_COMPLETE=zsh_source completion-env/bin/ykman > ykman.zsh
}

check(){
	cd "${pkgname/-/_}-$pkgver"
	pytest
}

package() {
	cd "${pkgname/-/_}-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -vDm0644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
	install -vDm644 ykman.bash "$pkgdir/usr/share/bash-completion/completions/ykman"
	install -vDm644 ykman.zsh "$pkgdir/usr/share/zsh/site-functions/_ykman"
}
