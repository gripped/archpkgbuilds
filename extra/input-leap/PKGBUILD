# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Maintainer: C. R. <crdaley at gmail>
# Contributor: Chris Rizzitello <sithlord48@gmail.com>
# Contributor: Lahfa Samy <akechishiro-aur@lahfa.xyz>
# Contributor: Chih-Hsuan Yen <base64_decode("eWFuMTIxMjUgQVQgYXJjaGxpbnV4IERPVCBvcmc=")>
# Forked from aur/barrier-git
# Contributor: Pellegrino Prevete <pellegrinoprevete@gmail.com>

pkgname=input-leap
pkgver=3.0.3
pkgrel=1
pkgdesc='Open-source KVM software'
url='https://github.com/input-leap/input-leap'
arch=(x86_64)
license=(LicenseRef-GPL-2.0-only-WITH-OpenSSL-Exception)
depends=(
  avahi
  gcc-libs
  glib2
  glibc
  hicolor-icon-theme
  libei
  libportal
  libx11
  libxext
  libxi
  libxinerama
  libxkbcommon
  libxkbcommon-x11
  libxrandr
  libxtst
  openssl
  qt6-base
  xdg-desktop-portal
)
makedepends=(
  cmake
  git
  gmock
  gtest
  gulrak-filesystem
  libice
  libsm
  ninja
  qt6-tools
  xorgproto
)
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('fccdcc6e0794a7d76f5d789ee999cff8c5c1d82804c04114876ae4af157a44619ac416fb7f185cf37d34cf27492b905143e136e6dbab662793d0da946130ef01')
b2sums=('58b9bb7e7e24d17ed1e9085a5dfb65be84342419549e10158d486b0501408223d9cd87b714f1d45f29b5f5b260eeafb4cfd335f40833264cb1f3555a15e8bf50')

prepare() {
  cd "${pkgname}"
  # fix: server config dialog show event
  git cherry-pick -n 0bae409d577e1efddacec5be7b0c97329eff854f
  # fix(gui): Adjust KeySequence.cpp for Qt > 6.9 change
  git cherry-pick -n d5c08a2ac8398f5ee6a1b2032e791d5980c47d60
}

build() {
  cd "${pkgname}"
  cmake \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_C_FLAGS="${CFLAGS}" \
    -D CMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -D CMAKE_EXE_LINKER_FLAGS="${LDFLAGS}" \
    -D CMAKE_SHARED_LINKER_FLAGS="${LDFLAGS}" \
    -D INPUTLEAP_BUILD_LIBEI=ON \
    -D INPUTLEAP_USE_EXTERNAL_GTEST=ON \
    -D INPUTLEAP_BUILD_GULRAK_FILESYSTEM=ON \
    -D INPUTLEAP_VERSION_STAGE=Release \
    -W no-dev \
    -G Ninja \
    -B build \
    -S .
  cmake --build build --verbose
}

check() {
  cd "${pkgname}"
  export QT_QPA_PLATFORM=offscreen
  ./build/bin/unittests
  ./build/bin/integtests
}

package() {
  cd "${pkgname}"
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 README.md doc/input-leap.conf* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
