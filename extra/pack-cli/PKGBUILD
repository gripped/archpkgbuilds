# Maintainer: Leonidas Spyropoulos (artafinde@archlinux.org)
# Contributor: Michael William Le Nguyen <michael at mail dot ttp dot codes>
# Contributor: Buildpacks Maintainers <cncf-buildpacks-maintainers at lists dot cncf dot io>

_base=pack
pkgname=pack-cli
pkgver=0.36.0
pkgrel=1
pkgdesc="CLI for building apps using Cloud Native Buildpacks"
depends=('glibc')
makedepends=('go' 'git')
arch=('x86_64')
url="https://buildpacks.io/"
license=('Apache-2.0')
source=("git+https://github.com/buildpacks/${_base}.git#tag=v${pkgver}")
sha512sums=('4cfc06ac871cca4a490842b5ab9f595ea23427030a4ce95a53c14a4a2492d4fca3823d18f5d2d51e5ad3ff8beb7a1b5a61d505e0c81691a1c096acb5c123b008')

prepare() {
  cd ${_base}
  mkdir -p dist/
}

build() {
  cd ${_base}
  local _BUILDINFO="-X 'github.com/buildpacks/pack.Version=v${pkgver}'"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o "dist/${_base}" \
    -a ./cmd/pack
}

# Seems like it needs docker
# check() {
#   cd ${_base}
#   local GOTESTFLAGS="-v -count=1 -parallel=1"
#   go test ./...
# }

package() {
  cd ${_base}
  install -vDm 755 "dist/${_base}" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 LICENSE  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
