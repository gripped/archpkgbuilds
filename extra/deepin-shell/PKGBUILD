# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-shell
pkgver=1.0.7
pkgrel=1
pkgdesc='New DDE shell'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-shell"
license=('LGPL3')
depends=('deepin-application-manager' 'deepin-tray-loader' 'dtk6core' 'dtk6gui' 'dtk6widget'
         'dtk6declarative' 'gcc-libs' 'glibc' 'libx11' 'libxcb' 'libxtst' 'qt6-5compat' 'qt6-base'
         'qt6-declarative' 'qt6-wayland' 'xcb-util-wm' 'wayland' 'yaml-cpp')
makedepends=('git' 'extra-cmake-modules' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols'
             'treeland-protocols')
groups=('deepin')
replaces=('deepin-dock')
conflicts=('deepin-dock')
source=("git+https://github.com/linuxdeepin/dde-shell.git#tag=$pkgver")
sha512sums=('04417b214a493958b84062f00f3713e0984a48224db577ea45ba92eb6eb59350e2f66e0a7bab5e141506ef8b1cdafd519c3b62f3dbffa6c303c12d14f50bddfd')

prepare() {
  cd dde-shell
  # qt 6.8 fix
  git cherry-pick -n 46871c83cf8ecfcf83bf2fb49e1f09af997eca96
  # treeland fix
  sed -i 's/TreeLandProtocols/TreelandProtocols/' panels/dock/taskmanager/CMakeLists.txt
  # qt 6.9 fix
  git cherry-pick -n 936d62a2c20398b9ca6ae28f9101dd288c8b1678
  git cherry-pick -n 2413161f679891921541f4d6da0ad8bc5a3e49d5
}

build() {
  cd dde-shell
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build .
}

package() {
  cd dde-shell
  DESTDIR="$pkgdir" ninja install
}
