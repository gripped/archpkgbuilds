# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-shell
pkgver=2.0.1
pkgrel=1
pkgdesc='New DDE shell'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-shell"
license=('GPL-3.0-or-later')
depends=('deepin-application-manager' 'deepin-tray-loader' 'dtk6core' 'dtk6gui' 'dtk6widget'
         'dtk6declarative' 'gcc-libs' 'glibc' 'icu' 'libx11' 'libxcb' 'libxtst' 'qt6-5compat'
         'qt6-base' 'qt6-declarative' 'qt6-wayland' 'xcb-util-wm' 'wayland' 'yaml-cpp')
makedepends=('git' 'extra-cmake-modules' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols'
             'treeland-protocols' 'gtest')
groups=('deepin')
replaces=('deepin-dock')
conflicts=('deepin-dock')
source=("git+https://github.com/linuxdeepin/dde-shell.git#tag=$pkgver")
sha512sums=('a7ef74817e2bed9c8bf07c096369b160b596253240fafde798b1f62921c9a19569b6e77b8760fd79c516f37faaab2a718a2236ef865b687bf243a4f28ba14783')

prepare() {
  cd dde-shell
  # qt 6.8 fix
  git cherry-pick -n 46871c83cf8ecfcf83bf2fb49e1f09af997eca96
  # treeland fix
  sed -i 's/TreeLandProtocols/TreelandProtocols/' panels/dock/taskmanager/CMakeLists.txt
  # qt 6.9 fix
  git cherry-pick -n 936d62a2c20398b9ca6ae28f9101dd288c8b1678
  git cherry-pick -n 2413161f679891921541f4d6da0ad8bc5a3e49d5
}

build() {
  cd dde-shell
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build .
}

package() {
  cd dde-shell
  DESTDIR="$pkgdir" ninja install
}
