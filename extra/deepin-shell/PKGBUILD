# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-shell
pkgver=1.0.4
pkgrel=1
pkgdesc='New DDE shell'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-shell"
license=('LGPL3')
depends=('deepin-tray-loader' 'dtk6core' 'dtk6gui' 'dtk6widget' 'dtk6declarative' 'gcc-libs' 'glibc'
         'libx11' 'libxcb' 'libxtst' 'qt6-5compat' 'qt6-base' 'qt6-declarative' 'qt6-wayland'
         'xcb-util-wm' 'wayland' 'yaml-cpp')
makedepends=('git' 'extra-cmake-modules' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols'
             'treeland-protocols')
groups=('deepin')
replaces=('deepin-dock')
conflicts=('deepin-dock')
source=("git+https://github.com/linuxdeepin/dde-shell.git#tag=$pkgver")
sha512sums=('c2b28d609f88b87ba6d7196c80438ffaa43d3d12c9eb8b600a051a21b3fb206a16bf07eeff90856f21fc2e0f3b73b9313df2ff3c2899b40cdc5d90d58d67c0a8')

prepare() {
  cd dde-shell
  # qt 6.8 fix
  git cherry-pick -n 46871c83cf8ecfcf83bf2fb49e1f09af997eca96
  # treeland fix
  sed -i 's/TreeLandProtocols/TreelandProtocols/' panels/dock/taskmanager/CMakeLists.txt
}

build() {
  cd dde-shell
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build .
}

package() {
  cd dde-shell
  DESTDIR="$pkgdir" ninja install
}
