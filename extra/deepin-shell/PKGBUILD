# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-shell
pkgver=2.0.21
pkgrel=1
pkgdesc='New DDE shell'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-shell"
license=('GPL-3.0-or-later')
depends=('deepin-application-manager' 'deepin-tray-loader' 'dtk6core' 'dtk6gui' 'dtk6widget'
         'dtk6declarative' 'gcc-libs' 'glibc' 'icu' 'libx11' 'libxcb' 'libxtst' 'qt6-5compat'
         'qt6-base' 'qt6-declarative' 'qt6-wayland' 'xcb-util-wm' 'wayland' 'yaml-cpp')
makedepends=('git' 'extra-cmake-modules' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols'
             'treeland-protocols' 'gtest')
groups=('deepin')
replaces=('deepin-dock')
conflicts=('deepin-dock')
source=("git+https://github.com/linuxdeepin/dde-shell.git#tag=$pkgver")
sha512sums=('97e44b8ebb4864ed691512e8b8688b0e8c0cb4cbba3e1516383894420d8f23828226adc0aacac093848e8596afa41b8619b81719127a80ff57c7dbaf9d37147b')

build() {
  cd dde-shell
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build .
}

check() {
  ctest --test-dir dde-shell --output-on-failure
}

package() {
  cd dde-shell
  DESTDIR="$pkgdir" ninja install
}
