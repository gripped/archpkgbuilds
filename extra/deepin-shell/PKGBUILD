# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-shell
pkgver=2.0.13
pkgrel=1
pkgdesc='New DDE shell'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-shell"
license=('GPL-3.0-or-later')
depends=('deepin-application-manager' 'deepin-tray-loader' 'dtk6core' 'dtk6gui' 'dtk6widget'
         'dtk6declarative' 'gcc-libs' 'glibc' 'icu' 'libx11' 'libxcb' 'libxtst' 'qt6-5compat'
         'qt6-base' 'qt6-declarative' 'qt6-wayland' 'xcb-util-wm' 'wayland' 'yaml-cpp')
makedepends=('git' 'extra-cmake-modules' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols'
             'treeland-protocols' 'gtest')
groups=('deepin')
replaces=('deepin-dock')
conflicts=('deepin-dock')
source=("git+https://github.com/linuxdeepin/dde-shell.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('1600ab0f0e340ebaebe9ce372036811b319f42b7e561b4ca08be898878968e9fb707c4b52f18fafb073ccb10c38d01dc4236e17e3ef70073ac9894c45840e522'
            '7ffbec30312fdf8b1c4a8c96698d7e69ab0b2240940c0a870aa5aba7a1eba4e4bbc6d8b662dc69ab069f7085c6155a6ded13d7616c2625285db48177dd5a7e3e')

prepare() {
  cd dde-shell
  # qt 6.8 fix
  git cherry-pick -n 46871c83cf8ecfcf83bf2fb49e1f09af997eca96
  # treeland fix
  sed -i 's/TreeLandProtocols/TreelandProtocols/' panels/dock/taskmanager/CMakeLists.txt
  # qt 6.9 fix
  git cherry-pick -n 936d62a2c20398b9ca6ae28f9101dd288c8b1678
  git cherry-pick -n 2413161f679891921541f4d6da0ad8bc5a3e49d5
  # qt 6.10 fix
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd dde-shell
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build .
}

package() {
  cd dde-shell
  DESTDIR="$pkgdir" ninja install
}
