# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-shell
pkgver=2.0.17
pkgrel=1
pkgdesc='New DDE shell'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-shell"
license=('GPL-3.0-or-later')
depends=('deepin-application-manager' 'deepin-tray-loader' 'dtk6core' 'dtk6gui' 'dtk6widget'
         'dtk6declarative' 'gcc-libs' 'glibc' 'icu' 'libx11' 'libxcb' 'libxtst' 'qt6-5compat'
         'qt6-base' 'qt6-declarative' 'qt6-wayland' 'xcb-util-wm' 'wayland' 'yaml-cpp')
makedepends=('git' 'extra-cmake-modules' 'cmake' 'ninja' 'qt6-tools' 'wayland-protocols'
             'treeland-protocols' 'gtest')
groups=('deepin')
replaces=('deepin-dock')
conflicts=('deepin-dock')
source=("git+https://github.com/linuxdeepin/dde-shell.git#tag=$pkgver")
sha512sums=('d5a098c2d72ab653861e94861865a6bf4c76279302bd7dbc6354d22020a6bff9e6ce574242d76b24eb15ddbc7b8c29da05aad47dcce4d5ad35dbb97c2d52f016')

build() {
  cd dde-shell
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build .
}

package() {
  cd dde-shell
  DESTDIR="$pkgdir" ninja install
}
