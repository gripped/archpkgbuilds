# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=nerdctl
pkgver=2.2.0
pkgrel=1
pkgdesc='Docker-compatible CLI for containerd'
arch=('x86_64')
url='https://github.com/containerd/nerdctl'
license=('Apache-2.0')
depends=('containerd')
makedepends=('git' 'go')
optdepends=(
  'buildkit: to build OCI images'
  'cni-plugins: to run OCI images'
  'rootlesskit: for rootless mode'
  'slirp4netns: for rootless mode'
)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('9c8279c2724c8f90c7007b36b1971cb34b87651d968b1f20ab7669e6a2d128e3fcc5da06421c7ffaf5762efbd87b5fabe9b264e5dd1d2b58a0a175f8c0be0b94')
b2sums=('9c8e18563774c579e27de8783cc1c70474ce2c4d10f5a283625448b3b6eadde4f8e3e6270caae9c733cabaa2d1564241b6de3153e8d2670ad12b77ffcc937676')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  local _commit="$(git rev-parse HEAD)"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X github.com/containerd/nerdctl/v2/pkg/version.Version=$pkgver \
    -X github.com/containerd/nerdctl/v2/pkg/version.Revision=$_commit" \
    -o build \
    ./cmd/...

  # generate shell auto-completions
  for completion in bash fish zsh; do
    ./build/nerdctl completion "$completion" > "build/$completion-completions"
  done
}

#check() {
#  cd "$pkgname"
#
#  go test -v ./pkg/...
#}

package() {
  cd "$pkgname"

  # binary & rootless scripts
  install -vDm755 -t "$pkgdir/usr/bin" build/nerdctl extras/rootless/*.sh

  # shell completions
  install -vDm644 build/bash-completions "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 build/fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -vDm644 build/zsh-completions "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
