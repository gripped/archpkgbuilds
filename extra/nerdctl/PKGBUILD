# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=nerdctl
pkgver=2.1.3
pkgrel=1
pkgdesc='Docker-compatible CLI for containerd'
arch=('x86_64')
url='https://github.com/containerd/nerdctl'
license=('Apache-2.0')
depends=('containerd')
makedepends=('git' 'go')
optdepends=(
  'buildkit: to build OCI images'
  'cni-plugins: to run OCI images'
  'rootlesskit: for rootless mode'
  'slirp4netns: for rootless mode'
)
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('91ad67e15e5dc29fccae9bb1ed1752234112449286b98bf23d8b367302e78db382a6e81a32a3e14c318cfb3f391371d9d97731176d89d42179aecbc33e6e9fa5')
b2sums=('955e079e08873549bdd5c427e362c96dc44aff37d11b2457a7810820ab0d2afa6c6d400649d9360f29ae7ded5c1b423b7c91012a35335b0378da661757c7aa46')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  local _commit="$(git rev-parse HEAD)"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X github.com/containerd/nerdctl/v2/pkg/version.Version=$pkgver \
    -X github.com/containerd/nerdctl/v2/pkg/version.Revision=$_commit" \
    -o build \
    ./cmd/...

  # generate shell auto-completions
  for completion in bash fish zsh; do
    ./build/nerdctl completion "$completion" > "build/$completion-completions"
  done
}

#check() {
#  cd "$pkgname"
#
#  go test -v ./pkg/...
#}

package() {
  cd "$pkgname"

  # binary & rootless scripts
  install -vDm755 -t "$pkgdir/usr/bin" build/nerdctl extras/rootless/*.sh

  # shell completions
  install -vDm644 build/bash-completions "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 build/fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -vDm644 build/zsh-completions "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
