# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=nerdctl
pkgver=2.0.1
pkgrel=1
pkgdesc='Docker-compatible CLI for containerd'
arch=('x86_64')
url='https://github.com/containerd/nerdctl'
license=('Apache')
depends=('containerd')
makedepends=('git' 'go')
optdepends=(
  'buildkit: to build OCI images'
  'cni-plugins: to run OCI images'
  'rootlesskit: for rootless mode'
  'slirp4netns: for rootless mode'
)
options=('!lto')
source=(
  "$pkgname::git+$url.git#tag=v$pkgver"
  'skip-failing-test.patch'
)
sha512sums=('d5d981bc23e7c327d4e1a836e1d02aff44ff318ba12af1259e554d9732b0f39e5768b51f84f8dc6d16675f3a8c04ac1668df486b87c27c8387654e1bc6b17171'
            'c168de6147edcebf31df4b2e22dc830cad62b81780d10105aada8e17347523747d413c4398bec3840f079a37177b6f9c3ca87c8f0119f4f651f7e4c6462eb8b3')
b2sums=('3597810b14ded3c3798dbdd602bd2de5fac0c061eb7217c5c04c20c008c14c9830739e1f61306c6eeb2e23462403d424396424f7e1955bb68aa9f6b539523407'
        'c4dd2803d0fb6ef0067ee2ec3fb34f606618e157264e03f9b607629900b11a774f484c8cb320054f90911400bb436ca65f024bf5d63f39766e5b7f19fd8814e5')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # skip failing unit test
  patch -p1 -i ../skip-failing-test.patch
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  local _commit="$(git rev-list -1 HEAD)"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X github.com/containerd/nerdctl/pkg/version.Version=$pkgver \
    -X github.com/containerd/nerdctl/pkg/version.Revision=$_commit" \
    -o build \
    ./cmd/...

  # generate shell auto-completions
  for completion in bash fish zsh; do
    ./build/nerdctl completion "$completion" > "build/$completion-completions"
  done
}

check() {
  cd "$pkgname"

  go test -v ./pkg/...
}

package() {
  cd "$pkgname"

  # binary & rootless scripts
  install -vDm755 -t "$pkgdir/usr/bin" build/nerdctl extras/rootless/*.sh

  # shell completions
  install -vDm644 build/bash-completions "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 build/fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -vDm644 build/zsh-completions "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
