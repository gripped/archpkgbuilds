# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>
# Contributor: Jose Riha <jose1711@gmail.com>

pkgname=python-librosa
pkgver=0.11.0
pkgrel=2
pkgdesc='Python library for music and audio analysis'
arch=(any)
url=https://librosa.org
license=(ISC)
depends=(
  python
  python-audioread
  python-decorator
  python-joblib
  python-lazy-loader
  python-matplotlib
  python-msgpack
  python-numba
  python-numpy
  python-pooch
  python-resampy
  python-scikit-learn
  python-scipy
  python-soundfile
  python-soxr
  python-typing_extensions
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("$pkgname::git+https://github.com/librosa/librosa.git#tag=$pkgver")
sha512sums=('dad526d004cd9b217a2128d82cd14c377704b1349afc37770f558c953c1d5474a78cefae78cbb781599e9a016a628fc3bad3e5fabd1dd0ce40ef3d910847bf19')
b2sums=('bee55cdb828545779fff9bae1cb615603a6d51636af6ed85e099288ac74eaa66a597cb3b34576a9a7cac805ce78b92dbe57bda4ac39f397a2523885b2f13cbc1')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
# vim:set ts=2 sw=2 et:
