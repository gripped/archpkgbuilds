# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>
# Contributor: Jose Riha <jose1711@gmail.com>

pkgname=python-librosa
pkgver=0.10.2.post1
pkgrel=3
pkgdesc='Python library for music and audio analysis'
arch=(any)
url=https://librosa.org
license=(ISC)
depends=(
  python
  python-audioread
  python-decorator
  python-joblib
  python-lazy-loader
  python-matplotlib
  python-msgpack
  python-numba
  python-numpy
  python-pooch
  python-resampy
  python-scikit-learn
  python-scipy
  python-six
  python-soundfile
  python-soxr
  python-typing_extensions
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=c295e2ada7c307bc55765b1fc72ef5c1cb7cbbac
source=(git+https://github.com/librosa/librosa.git#tag=${_tag})
b2sums=('15f5d0d872351a9d42a521b2cc103ab73f7679f598dc81de9f5cc6bbfa10af4f4957fae6ff1012af94a1268226c624072db98671e0d62f58c421f7d0ee11d7ba')

pkgver() {
  cd librosa
  git describe --tags
}

build() {
  cd librosa
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" librosa/dist/*.whl
  install -Dm 644 librosa/LICENSE.md -t "${pkgdir}"/usr/share/licenses/python-librosa/
}

# vim:set ts=2 sw=2 et:
