# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=netscanner
pkgver=0.6.41
pkgrel=1
pkgdesc="Network scanner"
arch=('x86_64')
url="https://github.com/Chleba/netscanner"
license=('GPL3')
depends=('gcc-libs' 'iw')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c73eb4b4dd455afeac889c70a7af20827d9dc75a5f9e25cd28543cfac1dbde055d66c18c58c7008ebdb228c81266f588806939aa6df2e7a7b22a63f81db052ef')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc --print host-tuple)" # --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
