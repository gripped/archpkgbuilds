# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Tiago de Paula Peixoto <tiago@skewed.de>

pkgname=python-graph-tool
pkgver=2.92
pkgrel=1
pkgdesc='A Python module for manipulation and statistical analysis of graphs'
arch=(x86_64)
url='https://graph-tool.skewed.de'
license=(LGPL-3.0-or-later)
depends=(
  boost-libs
  cairomm-1.16
  expat
  gcc-libs
  glib2
  glibc
  gmp
  python
  python-cairo
  python-gobject
  python-numpy
  python-scipy
  python-zstandard
)
makedepends=(
  autoconf-archive
  boost
  cgal
  sparsehash
)
optdepends=(
  'graphviz: graph layout'
  'python-matplotlib: graph drawing'
)
source=("https://downloads.skewed.de/graph-tool/graph-tool-$pkgver.tar.bz2")
b2sums=('5eb54b8c296d119b8205af1eb5f43649c6a7d9c38c8559529d9273bd4ed27b51ad4f11f8c40f20c905f3e2762ffd4cd7de5c0f01e057bd929baaa27fa70301e4')

# -flto needs to be set via MOD_CXXFLAGS below, not globally, since it must be
# overridden in specific compilation units to prevent performance degradation
# (due to unwanted mixture of optimization flags during link time).
options=(!lto)

build() {
  cd graph-tool-$pkgver

  # disable assertions which lead to runtime performance degradation
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS/}
  # the package benefits from -O3 (due to -funswitch-loops and others)
  export CXXFLAGS=${CXXFLAGS/-O2/-O3}

  ./configure --enable-openmp --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    MOD_CXXFLAGS="-flto=auto"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  # building graph-tool requires a lot of memory per thread, so do not use all available cores
  make -j$(( $(nproc) / 2 ))
}

check() {
  cd graph-tool-$pkgver
  make check
}

package() {
  cd graph-tool-$pkgver
  make DESTDIR="$pkgdir" install

  # remove doc subdirectory with unbuilt sphinx sources
  rm -frv "$pkgdir"/usr/share/doc/$pkgname/doc/
}
