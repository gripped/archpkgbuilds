# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=warzone2100
pkgver=4.5.5
pkgrel=2
pkgdesc="3D realtime strategy game on a future Earth"
url="https://wz2100.net/"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('sdl2' 'openal' 'libvorbis' 'libtheora' 'libsodium' 'physfs' 'sqlite' 'freetype2' 'opus' 'fmt')
makedepends=('zip' 'unzip' 'libzip' 'asciidoc' 'mesa' 'vulkan-headers' 'shaderc' 'cmake' 'ninja')
source=(${pkgname}-${pkgver}_src.tar.xz::https://github.com/Warzone2100/warzone2100/releases/download/${pkgver}/${pkgname}_src.tar.xz)
sha256sums=('07f61bae721687edeb62da4877e85030a03a053a593d645194fc65778e0480ff')

build() {
  # /usr/bin/warzone2100 uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  # FTBFS workaround for CMake 4+
  # https://github.com/Warzone2100/warzone2100/issues/4264
  export CMAKE_POLICY_VERSION_MINIMUM=3.5

  cmake \
    -B build \
    -S ${pkgname} \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DWZ_DISTRIBUTOR="ArchLinux" \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build

  # why is this installed ? Just remove it !
  rm -rfv ${pkgdir}/usr/include
  rm -rfv ${pkgdir}/usr/lib
}
