# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ganymede < ganymede _AT_ analogon _DOT_ eu>

pkgname=perl-file-libmagic
_dist=File-LibMagic
pkgver=1.23
pkgrel=7
pkgdesc="Determine MIME types of data or files using libmagic"
url="https://metacpan.org/dist/$_dist"
arch=(x86_64)
license=('Artistic-1.0-Perl OR GPL-1.0-or-later')
depends=(
  file
  glibc
  perl
)
makedepends=(
  perl-config-autoconf
  perl-test-fatal
)
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DR/DROLSKY/$_dist-$pkgver.tar.gz")
b2sums=('a409cdfbb7ac448858202ad79ee7b5cceb7d0bd17e42de108818ca6b03e8f8688f15dd5b5b0adc8ccab1a97174b02ccd93d5660dce2c04f585449182bd25a2aa')

build() {
  cd $_dist-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd $_dist-$pkgver
  make test
}

package() {
  cd $_dist-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
