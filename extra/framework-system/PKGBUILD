# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=framework-system
pkgver=0.5.0
pkgrel=1
pkgdesc='Tools to interact with the Framework Computer systems'
url='https://github.com/FrameworkComputer/framework-system'
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo
  git
)
options=(!lto)
source=($pkgname::"git+https://github.com/FrameworkComputer/framework-system.git#tag=v${pkgver}")
sha256sums=('85d6dad07a5e9474d647eae9805f9b25510b689e890751f7f33dd9123b7dc593')
b2sums=('13345007ab152a5287b7a5fcb5969f9c481bdcce10263be7acf6047b5324e95585b84c5a00724f3a2b51a9745bb696b9168e2309db7d9189adf4bb7dd76b5ef5')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  cargo build --frozen --release
}

check() {
  cd $pkgname
  cargo test --frozen
}

package() {
  cd $pkgname
  install -Dm 755 target/release/framework_tool -t "${pkgdir}/usr/bin"
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" 'LICENSE.md'
  install -Dm 644 -t "${pkgdir}/usr/share/bash-completion/completions/" completions/bash/framework_tool
  install -Dm 644 -t "${pkgdir}/usr/share/zsh/site-functions/" completions/zsh/_framework_tool
}

# vim: ts=2 sw=2 et:
