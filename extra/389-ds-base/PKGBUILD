# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Javier Torres <javitonino [at] gmail [dot] com>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=389-ds-base
pkgver=3.1.2
pkgdesc="389 Directory Server (base)"
pkgrel=1
arch=(x86_64)
url="http://port389.org/"
license=('GPL')
provides=('libsvrcore.so')
replaces=('python-lib389')
backup=(etc/dirsrv/config/certmap.conf
        etc/dirsrv/config/ldap-agent.conf
        etc/dirsrv/config/slapd-collations.conf
        etc/dirsrv/config/template-initconfig)
depends=('cracklib' 'libevent' 'nspr' 'nss' 'net-snmp' 'pam' 'openldap' 'icu' 'db5.3' 'python-cryptography'
         'python-argcomplete' 'python-dateutil' 'python-ldap' 'python-packaging' 'lmdb')
makedepends=('cargo' 'rsync' 'doxygen' 'cmocka' 'python-setuptools' 'python-argparse-manpage'
             'npm' 'nodejs' 'systemd')
optdepends=('cockpit: LDAP administration through the Cockpit web UI')
source=(https://github.com/389ds/389-ds-base/archive/389-ds-base-${pkgver}.tar.gz
        389-ds-base.sysusers
        389-ds-base.tmpfiles)
sha512sums=('8ec4c689ebe467ff71f34033169ace5ef9060cde1dc1f8fa6a177af2829773f3a5fd5a7140e9ef07ef052d5ad3bbf5e04eb62f4842ed40f3593dbfd93a821cf1'
            '7947f50e0fc26f07b388a20f53360cc5e1faf6dfa26dd6a48ffa833c4d247ade3f4269479b5ed4dbb99a2a1367b0ace4054da3d58e3bc3563c7daf947da72a72'
            '69ed8b8f3bdbf9098088b0c92c41a238f16d14ba9f86ebc2b5debe5f001b4d8e235f7cff4731d72b30b5ac70486b0f4300b99646aa3926a3fa59515a64f16402')

prepare() {
  cd "${pkgname}-${pkgname}-${pkgver}"

  ./autogen.sh
}

build() {
  cd "${pkgname}-${pkgname}-${pkgver}"

  # Build 389-ds-base
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname} \
    --with-tmpfiles-d=/usr/lib/tmpfiles.d \
    --with-db-inc=/usr/include/db5.3 \
    --with-systemd \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-systemdsystemconfdir=/etc/systemd/system \
    --with-journald \
    --with-openldap \
    --enable-autobind \
    --enable-cmocka \
    --enable-rust \
    --with-libldap_r=no
  make

  # Build lib389
  make src/lib389/setup.py
  cd src/lib389
  python setup.py build

  # Build cockpit plugin
  cd ../cockpit/389-console
  npm install
  npm run build
  mv dist cockpit_dist
}

check() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  # Currently broken: https://github.com/389ds/389-ds-base/issues/6598
  # make check
}

package() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  # make DESTDIR="${pkgdir}/" install
  make -j1 DESTDIR="${pkgdir}/" install

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # Upstream expects lib389 and 389-ds-base to be shipped in the same package more or less
  # so that's why it's not a split package.
  cd src/lib389
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin/
  rmdir "${pkgdir}"/usr/sbin

  mv "${pkgdir}"/usr/libexec/dirsrv/dscontainer "${pkgdir}"/usr/lib/dirsrv
  rm -r "${pkgdir}"/usr/libexec

  install -Dm644 "${srcdir}/${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
