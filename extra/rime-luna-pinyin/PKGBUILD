# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-luna-pinyin
pkgver=0.0.0.20250122
_commit=a9f71dad6c9ac2348462b0f98a21d7ab5e42c684
pkgrel=1
pkgdesc="Luna pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-luna-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-luna-pinyin.git#commit=$_commit")
sha512sums=('0670d6649b30c5ea57555247801457b4edbcafee897eb702dce98594fb59ee62325562822d52d8ad1e761acf7b1a69446c8768872dbd3fbeb60370858cc0b39f')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
