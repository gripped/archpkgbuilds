# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-luna-pinyin
pkgver=0.0.0.20240725
_commit=32631c9521b0d0fabe6cb938cb0c7bf0826a0600
pkgrel=1
pkgdesc="Luna pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-luna-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-luna-pinyin.git#commit=$_commit")
sha512sums=('62abfe71c1f7602c69bb804d1b8ed860cb5be98cad157559b36512d218c3ad55062decc6712af893376aca7c525194b7dad22694bc174d406417d0d0627da92b')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
