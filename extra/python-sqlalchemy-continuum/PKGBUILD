# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org

_name=sqlalchemy-continuum
pkgname=python-sqlalchemy-continuum
pkgver=1.5.2
pkgrel=2
pkgdesc='Versioning and auditing extension for SQLAlchemy'
url='https://github.com/kvesteri/sqlalchemy-continuum'
arch=('any')
license=('BSD')
depends=('python' 'python-sqlalchemy' 'python-sqlalchemy-utils')
makedepends=('python-setuptools' 'python-sphinx' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('sqlite' 'python-pytest' 'python-flexmock' 'python-psycopg2' 'python-mysql-connector'
              'python-flask' 'python-flask-login' 'python-flask-sqlalchemy'
              'python-sqlalchemy-i18n')
optdepends=('python-flask: flask plugin'
            'python-flask-login: flask plugin'
            'python-flask-sqlalchemy: flask plugin'
            'python-sqlalchemy-i18n: internationalization')
options=('!makeflags')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/${_name//-/_}/${_name//-/_}-${pkgver}.tar.gz)
sha512sums=('e424c246f49a704aeec677787adf10eacc92d98a5da709b05a8aea92f2ba6fc541f3476f292b0133d43441c192378be970b6d7d84260149336920628327e3ba1')
b2sums=('234eeaf4341cee0e4fe225a9db71f5c602a4f8633f118a924daadc81535d438d9066fda4adf8d90f569ec26672d17d5fcc31f431436df7808a1bd4f357bec99a')

prepare() {
  cd ${_name/-/_}-${pkgver}

  # https://github.com/kvesteri/sqlalchemy-continuum/issues/358
  sed -i '/^intersphinx_mapping =/d' docs/conf.py
}

build() {
  cd ${_name/-/_}-${pkgver}
  python -m build --wheel --no-isolation
  make -C docs text man
}

check() {
  cd ${_name/-/_}-${pkgver}/tests
  DB=sqlite pytest -k 'not test_flask' || true
}

package() {
  cd ${_name/-/_}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/sqlalchemy-continuum.1 -t "${pkgdir}/usr/share/man/man1"
  ln -sf /usr/share/man/man1/sqlalchemy-continuum.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
}

# vim: ts=2 sw=2 et:
