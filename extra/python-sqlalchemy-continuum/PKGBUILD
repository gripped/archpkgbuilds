# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org

_name=sqlalchemy-continuum
pkgname=python-sqlalchemy-continuum
pkgver=1.4.2
pkgrel=3
pkgdesc='Versioning and auditing extension for SQLAlchemy'
url='https://github.com/kvesteri/sqlalchemy-continuum'
arch=('any')
license=('BSD')
depends=('python' 'python-sqlalchemy' 'python-sqlalchemy-utils')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('sqlite' 'python-pytest' 'python-flexmock' 'python-psycopg2' 'python-mysql-connector'
              'python-six' 'python-anyjson' 'python-flask' 'python-flask-login' 'python-flask-sqlalchemy'
              'python-sqlalchemy-i18n')
optdepends=('python-flask: flask plugin'
            'python-flask-login: flask plugin'
            'python-flask-sqlalchemy: flask plugin'
            'python-sqlalchemy-i18n: internationalization')
options=('!makeflags')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/${_name//-/_}/${_name//-/_}-${pkgver}.tar.gz)
sha512sums=('fe6fe3a9c5ada2cffe25c9fcdf84bad48fcdacaf26276d774032ac6929f46fe93e9544d54bd549af47e6a8e6cc6fe09178646f251ffac95933bd0bcb45f0b959')
b2sums=('41d6fa3584fc4a9d146e24115f8861d80df8ccac368384dda7130af586d5fd4a2b338b03c1135b6c730dffd5ad60aedf546dad5fd9e3656ac15a96c9ea01a918')

prepare() {
  cd ${_name/-/_}-${pkgver}

  # https://github.com/kvesteri/sqlalchemy-continuum/issues/358
  sed -i '/^intersphinx_mapping =/d' docs/conf.py
}

build() {
  cd ${_name/-/_}-${pkgver}
  python setup.py build
  make -C docs text man
}

check() {
  cd ${_name/-/_}-${pkgver}/tests
  DB=sqlite pytest -k 'not test_flask' || true
}

package() {
  cd ${_name/-/_}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/sqlalchemy-continuum.1 -t "${pkgdir}/usr/share/man/man1"
  ln -sf /usr/share/man/man1/sqlalchemy-continuum.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
}

# vim: ts=2 sw=2 et:
