# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: acxz <akashpatel2008 at yahoo dot com>
# Contributer: JP-Ellis <josh@jpellis.me>

pkgname=mivisionx
pkgver=7.1.1
pkgrel=2
pkgdesc="Set of comprehensive computer vision and machine intelligence libraries"
arch=('x86_64')
url="https://rocm.docs.amd.com/projects/MIVisionX/en/latest/"
license=('MIT')
depends=(
  'ffmpeg'
  'hip-runtime-amd'
  'miopen-hip'
  'opencv'
  'protobuf'
  'rocblas'
  'rocm-core'
  'rpp'
)
makedepends=(
  'cmake'
  'half'
  'rocm-cmake'
)
_git='https://github.com/ROCm/MIVisionX'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz"
    mivisionx-7.1-fix-include.patch)
sha256sums=('7a7bd6ccb67e2b858526667decea938cc80c72d6279e7f6d9f3c0bb89ef823d7'
            'd095e9f73d0d950818a5266ee97204fb0a26f9c27c103a02ddcfdf32675680fb')
options=(!lto)
_dirname="$(basename "$_git")-$(basename "${source[0]}" ".tar.gz")"

prepare() {
  cd "$_dirname"
  patch -Np1 < "${srcdir}/mivisionx-7.1-fix-include.patch"
}

build() {
  # -fcf-protection is not supported by HIP, see
  # https://docs.amd.com/bundle/ROCm-Compiler-Reference-Guide-v5.3/page/Appendix_A.html
  CXXFLAGS+=" -fcf-protection=none"
  local cmake_args=(
    -Wno-dev
    -B build
    -S "$_dirname"
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
    -D CMAKE_CXX_COMPILER=amdclang++
    -D CMAKE_BUILD_TYPE=None
    # Building migraphx currently fails.
    # TODO: Revisit migraphx package
    -D MIGRAPHX=OFF
  )
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$_dirname/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
