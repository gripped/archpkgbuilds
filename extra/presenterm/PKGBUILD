# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=presenterm
pkgver=0.16.0
pkgrel=1
pkgdesc="A markdown terminal slideshow tool"
arch=('x86_64')
url="https://github.com/mfontanini/presenterm"
license=('BSD-2-Clause')
depends=('gcc-libs' 'libsixel' 'oniguruma')
makedepends=('cargo')
optdepends=('python-weasyprint: PDF export support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('345fdf0e48bd2b9cac53bb6b480953d009608917d23ae12f0548630ac6b97fd9d7f5fac4f31bcafab5da1ba5369cb39963ada9530680da9e69d37cbd8c580d5c')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTONIG_DYNAMIC_LIBONIG=1
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
