# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=presenterm
pkgver=0.9.0
pkgrel=1
pkgdesc="A markdown terminal slideshow tool"
arch=('x86_64')
url="https://github.com/mfontanini/presenterm"
license=('BSD-2-Clause')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('48d349ac3f8c243d56fffb209a1495de0fde1909de546056ca8584616b21bd4e3882e1e7417bb42cb4e3f7426ccb5712c50da02e65eb50070b4ba4c28ebffa69')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --features sixel
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --features sixel
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
