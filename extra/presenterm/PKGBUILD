# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=presenterm
pkgver=0.12.0
pkgrel=1
pkgdesc="A markdown terminal slideshow tool"
arch=('x86_64')
url="https://github.com/mfontanini/presenterm"
license=('BSD-2-Clause')
depends=('gcc-libs' 'libsixel')
makedepends=('cargo')
optdepends=('python-weasyprint: PDF export support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('b1ccb0994d60f916f2ad62f571f9bac731c3964b882cf4046d57814bd45cc753b59579d9f7e62d7b1f8338616d2e4c7d0a356e638e78634b9876b191b031b696')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --features sixel
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --features sixel
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
