# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=KiKit
pkgname=python-kikit
pkgver=1.6.0
pkgrel=3
pkgdesc='Automation tools for KiCAD'
arch=('any')
url='https://github.com/yaqwsx/KiKit'
license=('MIT')
depends=(
  'python'
  'kicad'
  'python-numpy'
  'python-shapely'
  'python-click'
  'python-markdown2'
  'python-pybars3'
  'python-solidpython'
  'python-pcbnewtransition'
  'python-commentjson'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('7864885d2255fe3f19385e2a53cd9dc4f3bd4ac3e0ba7d6cf2d89cb68e0d8c00ad23ba2ade542191ab08eb7410d1bc5734ed267d5db61d781b1df4765e5ed6d4')
b2sums=('47ab7a7bc5d1ce2aa953269e859e397bce3962cad1367fe8cfc7691db6aab5ce3d5e67bbcfcb1c5ef7f698a35b96997b6f2870c73401e3479e540d22fd749063')

prepare() {
  sed -i '/versioneer/d' $pkgname/setup.py
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENCE
}
