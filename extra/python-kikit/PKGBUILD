# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-kikit
pkgver=1.7.2
pkgrel=3
pkgdesc='Automation tools for KiCAD'
arch=('any')
url='https://github.com/yaqwsx/KiKit'
license=('MIT')
depends=(
  'python'
  'kicad'
  'python-numpy'
  'python-shapely'
  'python-click'
  'python-markdown2'
  'python-pybars3'
  'python-solidpython'
  'python-pcbnewtransition'
  'python-commentjson'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'remove-versioneer.patch'
  'ftbfs-workarounds.patch'
)
sha512sums=('d58039bc3abeedd03b0e8173f786d155804a779d5347694346167738ea71dfcb9d1d203d7e1913e29d8919f643bac5859612efcf2c187d770dd397ad65c753e9'
            '5f3f335511f29972da15a35f7e7bf68af85c1f8592b2cc3bbe86fddcec78fea426023cf004e7e0dcb00ceaeacf45d935ef40e5896a2b4681e66a48c80a4ceca9'
            '3a31fa6502435c12b9c321c4ed67997b57cc4c189ffa94ee5169ff3fee5e0ea50202bcbca303ad90eb92344e82784301e8c1f1f9511731b6836ff4936c241397')
b2sums=('3fd9723ab38c08c7d85193c98c922a8eb64129b555c8c74689e52009f71acc32fa825bfb1d7223ac1261468fc2b32ed72ea1540aa4711033423f0172f47b7af0'
        'c907f6c279de7c384fcc5d4ba1f45efd4599a01f515cb9713a39c71b62a1b70c4d29d5327892da9346cae790501fb3c9f6aa9e60ed26890f0f94c5067658280f'
        'a6497eb611d5768d49cb5fb5526be1241361f457f2ec215b7d4f8b89961eb2fba2b22de9373f0e7b2f85208db25eee2da10fa7b43f7cac2e4a77cf63c751d37c')

prepare() {
  cd "$pkgname"

  # do not require versioneer for build
  patch -p1 -i "$srcdir/remove-versioneer.patch"

  # remove unnecessary workarounds
  patch -p1 -i "$srcdir/ftbfs-workarounds.patch"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENCE
}
