# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: a821
# Contributor: asm0dey <pavel.finkelshtein+AUR@gmail.com>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=pgcli
pkgver=4.4.0
pkgrel=2
pkgdesc='A command line interface for PostgreSQL with auto-completion and syntax highlighting'
arch=(any)
url='https://www.pgcli.com/'
license=(BSD-3-Clause)
depends=(
  python
  python-cli_helpers
  python-click
  python-configobj
  python-pgspecial
  python-prompt_toolkit
  python-psycopg
  python-pygments
  python-setproctitle
  python-sqlparse
  python-tzlocal
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-pytest
  python-sshtunnel
)
optdepends=(
  'python-keyring: for keyring integration'
  'python-sshtunnel: for accessing remote instances over SSH'
)
source=("$pkgname::git+https://github.com/dbcli/pgcli.git#tag=v$pkgver")
sha512sums=('b9dc05886e51dceea6ba7b7d57b85448b6f53b0a84990b386af241c3e4c237365f60cf9c5ef2f1419848982c87e7a8fed9c4543e8fd266bd4ab0487a802d0bb3')
b2sums=('38e70b1966636ca27058fe39164dab02ec53fe974eedd2eb2be08a64ab09a58982f53686d1413054034fa6099221c8c647dc6c6dc3d26d1fb2079b3edfe15342')

build() {
  cd "$pkgname"

  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
