# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: a821
# Contributor: asm0dey <pavel.finkelshtein+AUR@gmail.com>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=pgcli
pkgver=4.3.0
pkgrel=1
pkgdesc='A command line interface for PostgreSQL with auto-completion and syntax highlighting'
url='https://www.pgcli.com/'
arch=('any')
license=('BSD-3-Clause')
depends=(
  'python'
  'python-cli_helpers'
  'python-click'
  'python-configobj'
  'python-pgspecial'
  'python-prompt_toolkit'
  'python-psycopg'
  'python-pygments'
  'python-setproctitle'
  'python-sqlparse'
  'python-tzlocal'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-sshtunnel'
)
optdepends=(
  'python-keyring: for keyring integration'
  'python-sshtunnel: for accessing remote instances over SSH'
)
source=("$pkgname::git+https://github.com/dbcli/pgcli.git#tag=v$pkgver")
sha512sums=('ab3517e94321c6b638313996a5d55395fe6397e3f8e60619024e4e82427de03e6dda316cebc816f4f9525e776855c18376eaddff7b45c780698ad56fbce6fe9a')
b2sums=('38c7a01a10ae3a2e54f73d374bb121a3642fa9f286ec0c4f5d482a95379e36a2e35b285f66d2264d862fdc69af548abb495f954fc0e6af77179f4af3c23effc2')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
