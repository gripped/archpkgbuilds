# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=5.9.4
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=(GPL-3.0-only LGPL-3.0-only)
depends=(
  glibc
  gcc-libs
  capstone
  file
  libuv
  libzip
  lz4
  sh
  xxhash
  zlib
)
makedepends=(
  meson
)
optdepends=('r2ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/radare/radare2/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('51d42b6d98027d3f3d85570e79ea18a8f220c145e3451fd10d82eea1a0542648f83447bb6cb6f5bbf76241677a2412bf064ffbade75e69b486c6148543aa4b0f')
b2sums=('14903c087fbb140a8b4b6056adcb236b76ebcad0c7821150599c0732b0bb832b26a4fee5edda914ecd1be4dcdbe28087fadb48e623c593cf949cbc421acc5080')

build() {
  cd ${pkgname}-${pkgver}
  touch libr/config.mk
  arch-meson build \
    -D use_sys_capstone=true \
    -D use_capstone_version=v5 \
    -D use_sys_magic=true \
    -D use_sys_zip=true \
    -D use_sys_zlib=true \
    -D use_sys_lz4=true \
    -D use_sys_xxhash=true \
    -D use_libuv=true \
    -D use_webui=true \
    -D want_threads=false

  ninja -C build
}

package() {
  depends+=(
    libcapstone.so
    libmagic.so
    libzip.so
  )

  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  cp -r doc/* "${pkgdir}/usr/share/doc/radare2"
  ln -s /usr/share/man/man1/radare2.1.gz "${pkgdir}/usr/share/man/man1/r2.1.gz"
# Install missing header
  install -Dm644 shlr/sdb/include/sdb/rangstr.h -t "$pkgdir"/usr/include/libr/sdb
}

# vim: ts=2 sw=2 et:
