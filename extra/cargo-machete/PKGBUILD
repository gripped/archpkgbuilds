# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-machete
pkgver=0.8.0
pkgrel=1
pkgdesc="Remove unused Rust dependencies"
arch=('x86_64')
url="https://github.com/bnjbvr/cargo-machete"
license=('MIT')
depends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('5d7b5cb94a6742cd76838a2a36f701cdf1e5d97bb0477dbb7ba03587dd791248')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

# https://github.com/bnjbvr/cargo-machete/issues/140
check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked -- --skip "test_ignore_target"
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
