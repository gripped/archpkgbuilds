# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-trio
pkgver=0.33.0
pkgrel=1
pkgdesc='A friendly Python library for async concurrency and I/O'
arch=(any)
url=https://github.com/python-trio/trio
license=(MIT)
depends=(
  python
  python-attrs
  python-idna
  python-outcome
  python-sniffio
  python-sortedcontainers
  python-typing_extensions
)
makedepends=(
  git
  python-build
  python-setuptools
  python-installer
  python-wheel
)
checkdepends=(
  python-astor
  python-black
  python-isort
  python-pytest
  python-ruff
  python-trustme
  python-yaml
)
provides=(python-multio-provider)
_tag=3073f0c2108bad83c27dda6c83474a9b8ce4a957
source=("git+$url.git#tag=$_tag")
b2sums=('5cff98fb630237d861922bbb1323fd0933cea6a222640a31534e065ccff269aee4e9db782b045b7727cf24c53a373f105e892ff04a0772ad339680b9acc30376')

pkgver() {
  cd ${pkgname#python-}
  git describe --tags | sed 's/^v//'
}

build() {
  cd ${pkgname#python-}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -W=ignore::DeprecationWarning \
    -m "not redistributors_should_skip" \
    --deselect=src/trio/_tests/tools/test_gen_exports.py::test_run_ruff
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/python-trio" LICENSE
}

# vim: ts=2 sw=2 et:
