# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-stopit
_pyname=stopit
pkgver=1.1.2
pkgrel=7
pkgdesc='Library to raise asynchronous exceptions in other thread, control the timeout of blocks or callables'
url='https://github.com/glenfant/stopit'
arch=('any')
license=('MIT')
depends=('python' )
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/glenfant/${_pyname}/archive/${pkgver}.tar.gz")
sha512sums=('4b1771a357f35a82128e92d7e91fa3e788271a3d6f1e08bacee69546c7c2fa857104394d8617259235c413b7f1706ccd4ff9ed480af5e1faef1149d1c1e0a0e0')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH="$PWD/src" python -m unittest tests.suite -v
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "${site_packages}/${pkgname}-${pkgver}.dist-info/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
