# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_pkgname=fold-debounce
pkgname=haskell-fold-debounce
pkgver=0.2.0.11
pkgrel=4
pkgdesc='Fold multiple events that happen in a given period of time'
arch=(x86_64)
url="https://hackage.haskell.org/package/$_pkgname"
license=(BSD)
depends=(ghc-libs haskell-stm-delay haskell-data-default-class)
checkdepends=(haskell-hspec haskell-hspec-discover)
makedepends=(ghc)
source=("https://hackage.haskell.org/packages/archive/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('53989271496fc2bebd2cd9b8f2c8c548ac4b0ee66e880b6017b775854e81788e560dcc8f7dd9fb5ef696663ed0279e6ba9bd8342f7a26e5ff12787bb3df484ab')

build() {
  cd $_pkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_pkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_pkgname-$pkgver
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
