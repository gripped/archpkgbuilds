# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_pkgname=fold-debounce
pkgname=haskell-fold-debounce
pkgver=0.2.0.12
pkgrel=3
pkgdesc='Fold multiple events that happen in a given period of time'
arch=(x86_64)
url="https://hackage.haskell.org/package/$_pkgname"
license=(BSD-3-Clause)
depends=(ghc-libs haskell-stm-delay haskell-data-default-class)
checkdepends=(haskell-hspec haskell-hspec-discover)
makedepends=(ghc)
source=("https://hackage.haskell.org/packages/archive/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('e743104d025eaa9417ac5dd2401020f2307e6a3a3375ec3f3d173e40eb345c0825357e47628ca8917e4441d90cfb82c138b05a911df7bda1346f49a8adde885f')

build() {
  cd $_pkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_pkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_pkgname-$pkgver
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
