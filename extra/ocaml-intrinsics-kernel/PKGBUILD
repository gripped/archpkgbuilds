# Maintainer:
# Contributor: Daniel Peukert <daniel@peukert.cc>

pkgname=ocaml-intrinsics-kernel
pkgver=0.17.1
pkgrel=2
pkgdesc='OCaml interface to operations that have dedicated hardware instructions on some micro-architectures'
arch=(x86_64)
url='https://github.com/janestreet/ocaml_intrinsics_kernel'
license=(MIT)
depends=(glibc
         ocaml)
makedepends=(dune
             git)
options=(!debug !strip)
source=(git+https://github.com/janestreet/ocaml_intrinsics_kernel#tag=v$pkgver)
b2sums=('2ae3cc380f3b4111d783edbe1483b6cbba2489f1bbef70831ed5028cf36908488759f45a1fea54ef34c9aac6ba00e4378f82f599ca5448a3734be1e9adab0c93')

build() {
  cd ocaml_intrinsics_kernel
  dune build --release --verbose
}

package() {
  cd ocaml_intrinsics_kernel
  DESTDIR="$pkgdir" dune install --prefix '/usr' --libdir '/usr/lib/ocaml' --docdir '/usr/share/doc' --mandir '/usr/share/man' --release --verbose

  for _folder in "$pkgdir/usr/share/doc/"*; do
    mv "$_folder" "$pkgdir/usr/share/doc/ocaml-$(basename "$_folder")"
  done

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  ln -sf /usr/share/doc/$pkgname/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
