# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alejandro Valdes <alejandrovaldes at live dot com>

pkgname=ncspot
pkgver=1.3.3
pkgrel=1
pkgdesc="Cross-platform ncurses Spotify client written in Rust, inspired by ncmpc and the likes."
arch=('x86_64')
url="https://github.com/hrkfdn/ncspot"
license=('BSD-2-Clause')
depends=(
  'dbus'
  'gcc-libs'
  'glibc'
  'hicolor-icon-theme'
  'libpulse'
  'libxcb'
  'openssl'
)
makedepends=(
  'cargo'
  'pandoc-cli'
  'pkgconf'
  'portaudio'
  'python'
  'ueberzug'
)
optdepends=('ueberzug: display album art in terminal (X11)')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hrkfdn/ncspot/archive/v${pkgver}.tar.gz")
b2sums=('aeec753e4103139952822337b180319dd0c4bb955c6e76c9b35288f02f4a2110d9e3279f15aae6516e4ea8fb96463e65fcb29ab0cfc5d8d1d678bdbf8756fced')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}-${pkgver}
  export CFLAGS="$CFLAGS -ffat-lto-objects"
  cargo build --frozen --release --features cover
  # generate docs
  pandoc README.md -t man -s --columns=500 \
    | grep -vE "\[IMAGE:|Click to show/hide" > ncspot.1
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen --features cover
}

package() {
  cd ${pkgname}-${pkgver}
  install -vDm 755 -t "${pkgdir}/usr/bin" target/release/ncspot
  install -vDm 644 -t "${pkgdir}/usr/share/applications" "misc/ncspot.desktop"
  install -vDm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
  install -vDm 644 -t "${pkgdir}/usr/share/man/man1" "ncspot.1"
  install -vDm 644 "images/logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/ncspot.svg"
}
