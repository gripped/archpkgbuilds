# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alejandro Valdes <alejandrovaldes at live dot com>

pkgname=ncspot
pkgver=1.3.1
pkgrel=1
pkgdesc="Cross-platform ncurses Spotify client written in Rust, inspired by ncmpc and the likes."
arch=('x86_64')
url="https://github.com/hrkfdn/ncspot"
license=('BSD-2-Clause')
depends=(
  'dbus'
  'gcc-libs'
  'glibc'
  'hicolor-icon-theme'
  'libpulse'
  'libxcb'
  'openssl'
)
makedepends=(
  'cargo'
  'pandoc-cli'
  'pkgconf'
  'portaudio'
  'python'
  'ueberzug'
)
optdepends=('ueberzug: display album art in terminal (X11)')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hrkfdn/ncspot/archive/v${pkgver}.tar.gz")
b2sums=('f5db7ed8b739538519846f8daf287235b4b90eb03e9df27ceeb4a005a7e1e64e582f5c7d3daae5d56734737f1b3a706bb3cb8af04f80b40c9f5651faea2b2f30')

prepare() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  export CFLAGS="$CFLAGS -ffat-lto-objects"
  cargo build --frozen --release --features cover
  # generate docs
  pandoc README.md -t man -s --columns=500 \
    | grep -vE "\[IMAGE:|Click to show/hide" > ncspot.1
}

check() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --features cover
}

package() {
  cd ${pkgname}-${pkgver}
  install -vDm 755 -t "${pkgdir}/usr/bin/ncspot" target/release/ncspot
  install -vDm 644 -t "${pkgdir}/usr/share/applications" "misc/ncspot.desktop"
  install -vDm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
  install -vDm 644 -t "${pkgdir}/usr/share/man/man1" "ncspot.1"
  install -vDm 644 "images/logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/ncspot.svg"
}
