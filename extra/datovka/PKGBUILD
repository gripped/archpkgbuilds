# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=datovka
pkgver=4.24.2
pkgrel=1
pkgdesc="GUI application for a 'Databox' - an electronic communication interface endorsed by the Czech government"
arch=('i686' 'x86_64')
url='https://www.datovka.cz/'
license=('custom')
depends=('qt6-base' 'qt6-svg' 'qt6-5compat' 'quazip-qt6' 'qt6-websockets'
         'libdatovka' 'openssl' 'hicolor-icon-theme')
makedepends=('qt6-tools')
source=(https://datovka.nic.cz/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('3556853c5703d6eff2784649d453d5b546b52918bd66157c79082b98df027d38')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  /usr/lib/qt6/bin/lrelease datovka.pro
  qmake6 PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
