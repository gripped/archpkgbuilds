# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=datovka
pkgver=4.25.0
pkgrel=1
pkgdesc="GUI application for a 'Databox' - an electronic communication interface endorsed by the Czech government"
arch=('i686' 'x86_64')
url='https://www.datovka.cz/'
license=('custom')
depends=('qt6-base' 'qt6-svg' 'qt6-5compat' 'quazip-qt6' 'qt6-websockets'
         'libdatovka' 'openssl' 'hicolor-icon-theme')
makedepends=('qt6-tools')
source=(https://datovka.nic.cz/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('c9229de5ecdaf52f11d56c67bcfd300eddf581f202fceb40099a7cc1dcd8dea9')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  /usr/lib/qt6/bin/lrelease datovka.pro
  qmake6 PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
