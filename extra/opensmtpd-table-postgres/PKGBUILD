# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=table-postgres
pkgname=opensmtpd-table-postgres
pkgver=1.1.1
pkgrel=1
pkgdesc='PostgreSQL support for OpenSMTPD'
arch=(x86_64)
url=https://github.com/OpenSMTPD/table-postgres
license=(ISC)
depends=(glibc postgresql-libs)
makedepends=(git)
source=(git+$url.git#tag=$pkgver)
sha512sums=('84ef72d62606109561e3bb2814f1affe9f3e3c110fa34e6f2d6d70785bf28d8e0dfde06bc0d81aa753ddcd5432f67153911e9656aafa0135cc39eab3f10df4ef')
b2sums=('c3adf161664f4a0995cfaf9657432b0f58df03d32d5aaa8f3bd0005900834d317c1c87c614120a2b448e38c3c97697c8a23ebc7a159b98de801edcd9f06058cb')

prepare() {
  cd $_pkgname
  sed -i 's|libexec/smtpd|lib/smtpd/opensmtpd|' Makefile.am
  autoreconf -vfi
}

build() {
  cd $_pkgname
  ./configure --prefix=/usr
  make
}

check() {
  cd $_pkgname
  make -k check
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
}
