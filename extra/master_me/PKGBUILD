# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=master_me
pkgname=(
  master_me
  master_me-{clap,ladspa,lv2,standalone,vst,vst3}
)
pkgver=1.3.1
pkgrel=1
pkgdesc="Automatic mastering plugin for live streaming, podcasts and internet radio"
arch=(x86_64)
url="https://github.com/trummerschlunk/master_me"
license=(GPL-3.0-or-later)
makedepends=(
  dbus
  faustpp
  gcc-libs
  gendesk
  glibc
  libglvnd
  libx11
  libxext
  libxrandr
)
source=(https://github.com/trummerschlunk/$pkgname/releases/download/$pkgver/$pkgname-$pkgver-src.tar.xz)
sha512sums=('55f82383885fd71385a97e3650fd06b735d0ccb7cbb62659a9d1e9a68f1779aa614ac23bac951b542751e2acefc1efa49887eb86b387728b196b7f8c44f629fb')
b2sums=('dc0b89d9d12d990f69ba5a5c1a98c550a6064730fd47d46eeb2cba99aa4ac031335637cde8068346a9cf5f164002618a586218bf9a15d45a62023c66807d9867')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  gendesk -n -f \
          --pkgname "com.github.Trummerschlunk.Master_me" \
          --name "master_me" \
          --exec "master_me" \
          --icon "master_me" \
          --genericname "Automatic mastering plugin"
}

build() {
  make -C $pkgname-$pkgver
}

package_master_me() {
  depends=(
    master_me-{clap,ladspa,lv2,standalone,vst,vst3}=$pkgver
  )

  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname-$pkgver

  (
    cd "$pkgdir"
    # have find-libdeps resolve dependencies properly:
    # https://gitlab.archlinux.org/archlinux/devtools/-/issues/102
    find usr/lib -type f \( -iname "*.clap" -or -iname "*.so" \) -exec chmod +x {} \;

    _pick master_me-clap usr/lib/clap
    _pick master_me-ladspa usr/lib/ladspa
    _pick master_me-lv2 usr/lib/lv2/
    _pick master_me-standalone usr/bin/
    _pick master_me-vst usr/lib/vst/
    _pick master_me-vst3 usr/lib/vst3/
  )
}

package_master_me-clap() {
  pkgdesc+=" - CLAP plugin"
  depends=(
    dbus
    gcc-libs
    glibc
    libglvnd
    libx11
    libxext
    libxrandr
    clap-host
  )
  groups=(clap-plugins)

  mv -v $pkgname/* "$pkgdir"
}

package_master_me-ladspa() {
  pkgdesc+=" - LADSPA plugin"
  depends=(
    gcc-libs
    glibc
    ladspa-host
  )
  groups=(ladspa-plugins)

  mv -v $pkgname/* "$pkgdir"
}

package_master_me-lv2() {
  pkgdesc+=" - LV2 plugin"
  depends=(
    dbus
    gcc-libs
    glibc
    libglvnd
    libx11
    libxext
    libxrandr
    lv2-host
  )
  groups=(lv2-plugins)

  mv -v $pkgname/* "$pkgdir"
}

package_master_me-standalone() {
  pkgdesc+=" - standalone"
  depends=(
    dbus
    gcc-libs
    glibc
    hicolor-icon-theme
    libglvnd
    libx11
    libxext
    libxrandr
  )
  conflicts=($pkgbase-jack)
  replaces=($pkgbase-jack)

  mv -v $pkgname/* "$pkgdir"

  install -vDm 644 ./*.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgbase-$pkgver/artwork/LogoAssets/signet_master_me_black.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/master_me.svg"
}

package_master_me-vst() {
  pkgdesc+=" - VST plugin"
  depends=(
    dbus
    gcc-libs
    glibc
    libglvnd
    libx11
    libxext
    libxrandr
    vst-host
  )
  groups=(vst-plugins)

  mv -v $pkgname/* "$pkgdir"
}

package_master_me-vst3() {
  pkgdesc+=" - VST3 plugin"
  depends=(
    dbus
    gcc-libs
    glibc
    libglvnd
    libx11
    libxext
    libxrandr
    vst3-host
  )
  groups=(vst3-plugins)

  mv -v $pkgname/* "$pkgdir"
}
