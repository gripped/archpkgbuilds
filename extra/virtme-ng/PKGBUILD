# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Manuel Wiesinger <m {you know what belongs here} mmap {and here} at>

pkgname=virtme-ng
pkgver=1.33
pkgrel=1
pkgdesc="A tool that allows to easily and quickly recompile and test a Linux kernel, starting from the source code."
arch=('x86_64')
url="https://github.com/arighi/virtme-ng"
license=('GPL-2.0-only')
depends=(
  bash
  busybox
  coreutils
  gcc-libs
  glibc
  python
  python-requests
  qemu
  python-argcomplete
  python-argparse-manpage
  python-setuptools
  python-argcomplete
)
makedepends=(
  cargo
  git
  python-build
  python-installer
  python-wheel
)
optdepends=('virtiofsd: better filesystem performance inside the virtme-ng guests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/arighi/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('07bffbd9774d581bc934b1baab24eac5a6527fa683addbc639d57d1ce18cd07fe1794cf0455b63356d07d1bafa798fd6512448a978598706ca5bd1fe9bd6104b')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    BUILD_VIRTME_NG_INIT=1 python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
