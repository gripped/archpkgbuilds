# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Manuel Wiesinger <m {you know what belongs here} mmap {and here} at>

pkgname=virtme-ng
_virtme_ng_init_commit=9b1e02a0deb094a36741b6172fb7ea8dc7dd8ddb
pkgver=1.32
pkgrel=1
pkgdesc="A tool that allows to easily and quickly recompile and test a Linux kernel, starting from the source code."
arch=('x86_64')
url="https://github.com/arighi/virtme-ng"
license=('GPL-2.0-only')
depends=(
  bash
  busybox
  coreutils
  gcc-libs
  glibc
  python
  python-requests
  qemu
  python-argcomplete
  python-argparse-manpage
  python-setuptools
  python-argcomplete
)
makedepends=(
  cargo
  git
  python-build
  python-installer
  python-wheel
)
optdepends=('virtiofsd: better filesystem performance inside the virtme-ng guests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/arighi/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
       "virtme-init-ng-${_virtme_ng_init_commit}.zip::https://github.com/arighi/virtme-ng-init/archive/${_virtme_ng_init_commit}.zip")
b2sums=('fe9b197a196e03e8a4e203a875c3541e5d1b4f5d62540795469b8b265caf46b7b9f7766f6c9f4f8a098d72650dcce069a02c8c692cbcc024f7569dad006e0f20'
        'f78eb6677a9b233097a207f11fed2ab6950a546eed0b5d0d64d2d91ad0f72fc00e58406e93725d2b4af629efc3ee85498d30f6f69cc448c95409977c97f633e5')

prepare() {
    cd "${srcdir}"

    # When rebuilding this dir is not empty, so rmdir(1) fails
    rm -rf "${pkgname}-${pkgver}/virtme_ng_init"

    cp -r "virtme-ng-init-${_virtme_ng_init_commit}" "${pkgname}-${pkgver}/virtme_ng_init"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    BUILD_VIRTME_NG_INIT=1 python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
