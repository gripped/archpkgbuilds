# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Manuel Wiesinger <m {you know what belongs here} mmap {and here} at>

pkgname=virtme-ng
pkgver=1.38
pkgrel=1
pkgdesc="A tool that allows to easily and quickly recompile and test a Linux kernel, starting from the source code."
arch=('x86_64')
url="https://github.com/arighi/virtme-ng"
license=('GPL-2.0-only')
depends=(
  bash
  busybox
  coreutils
  gcc-libs
  glibc
  python
  python-requests
  qemu
  python-argcomplete
  python-argparse-manpage
  python-setuptools
  python-argcomplete
)
makedepends=(
  cargo
  git
  python-build
  python-installer
  python-wheel
)
optdepends=('virtiofsd: better filesystem performance inside the virtme-ng guests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/arighi/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('d1ca6e7d4117feed94465c9144d222225fad0f11b158d1a9a4705a43e3910a60872c757f1754b6eeb1084e742e6d434356c5a70bc4c67f4d2cb9626e54397054')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    BUILD_VIRTME_NG_INIT=1 python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
