# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Manuel Wiesinger <m {you know what belongs here} mmap {and here} at>

pkgname=virtme-ng
pkgver=1.40
pkgrel=2
pkgdesc="A tool that allows to easily and quickly recompile and test a Linux kernel, starting from the source code."
arch=('x86_64')
url="https://github.com/arighi/virtme-ng"
license=('GPL-2.0-only')
depends=(
  bash
  busybox
  coreutils
  gcc-libs
  glibc
  python
  python-requests
  qemu
  python-argcomplete
  python-argparse-manpage
  python-setuptools
  python-argcomplete
)
makedepends=(
  cargo
  git
  python-build
  python-installer
  python-wheel
)
optdepends=('virtiofsd: better filesystem performance inside the virtme-ng guests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/arighi/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('3c5063384d86a3644251e2825af32561b4b3498e6ea60f6d98ce4abd34b8994adc585ac49890014ffee294469acfc5e43ae8860608369a0a4c45588ffc1333fe')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    BUILD_VIRTME_NG_INIT=1 python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
