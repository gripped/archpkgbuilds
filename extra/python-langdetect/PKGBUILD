# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-langdetect
pkgver=1.0.9
pkgrel=9
pkgdesc="Language detection library ported from Google's language-detection"
arch=(any)
url='https://github.com/Mimino666/langdetect'
license=(Apache-2.0)
depends=(python python-six)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#commit=a1598f1afcbfe9a758cfd06bd688fbc5780177b2")
sha512sums=('df7dcf3dd6992132f0a8f6e302a7c5fbe3218515710812d100c07d2195a9af48f5802468de1b3b588b2ce34937889762081710d33ecd805689877533bc0521fd')
b2sums=('26f1bfe027e612d5820967f105db1a8c06b6015a873b2f904363903120cfd4c874f075b90e39861412952b4523a24b10fd5629e6c0645eb7aa4304d0d1efcd37')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
