# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Luis Martinez <luis dot martinez at tuta dot io>
# Contributor: Michal Wojdyla < micwoj9292 at gmail dot com >
# Contributor: arno <apithon.AT.free.DOT.fr>
# Contributor: Pierre Bourdon <delroth@gmail.com>
# Contributor: ianux <ianux AT free DOT fr>

pkgname=python-hachoir
pkgver=3.3.0
pkgrel=1
pkgdesc='A Python library to view and edit a binary stream field by field'
arch=('any')
url='https://hachoir.readthedocs.io/'
license=('GPL-2.0-only')
depends=(
  'python'
  'python-darkdetect'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
  'python-urwid'
  'python-wxpython'
)
optdepends=(
  'python-urwid: required for hachoir-urwid'
  'python-wxpython: required for hachoir-wx'
)
source=("$pkgname::git+https://github.com/vstinner/hachoir.git#tag=$pkgver")
sha512sums=('2e4561807e32e770d5b440dc20c342973a323314fd7f5c7b2795a2c699eb345b8ba6c7c3854c554dab8873cc960ae1adc79b2a5b29960bd361fdb3c49e66ff36')
b2sums=('a43cde899235f33446526e6181acf94f85dd678b64c106ab8de438af192ac48f4da583b07f472518cddc48ce42d850a3cf67ba835bb567c0c7da74d4cac46429')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python runtests.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
