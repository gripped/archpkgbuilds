# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>

pkgname=bfs
pkgver=4.0.3
pkgrel=1
pkgdesc='A breadth-first version of find'
arch=('x86_64')
url='https://tavianator.com/projects/bfs.html'
license=('0BSD')
depends=(
  'glibc'
  'acl'
  'libcap'
  'liburing'
  'oniguruma'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/tavianator/bfs#tag=$pkgver")
sha512sums=('7f68404bcc01092e02d83cf67541adc30f9262055e7b8285a93fe02e111261e3568f6f3848b2dbcace1e9bded5a3b7b19a54003fefbca878bf9ffaef892e62e8')
b2sums=('9f997501756e1676198393f895b5b57c09c34ba4c764d1549325618d6f0cd9ff8d687fbbece84aa9482eb4a409695d2f04fd8ed5314c6bde430741b8c733609e')

build() {
  cd "$pkgname"

  ./configure RELEASE=y
  make CFLAGS="$CFLAGS"
}

check() {
  cd "$pkgname"

  make -k check CFLAGS="$CFLAGS"
}

package() {
  cd "$pkgname"

  make install DESTDIR="$pkgdir"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
