# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>

pkgname=bfs
pkgver=4.1
pkgrel=1
pkgdesc='A breadth-first version of find'
arch=('x86_64')
url='https://tavianator.com/projects/bfs.html'
license=('0BSD')
depends=(
  'glibc'
  'acl'
  'libcap'
  'liburing'
  'oniguruma'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/tavianator/bfs#tag=$pkgver")
sha512sums=('b91c080020c3ea2ad01583b8d5c03295412fbae6e5a632715845705564d37f135e733cbdb4fa0653c94f973ff8ed085b94aa9cc30c189649297dbb559b16810c')
b2sums=('f00aade00f588b943c158f07331ba803ac1ee6c98f214967611ede8616657b4212d50cc8366bb8d96afadfb7f04d148ed45f88e8a3628ba4c13dcdd7451dccaf')

build() {
  cd "$pkgname"

  ./configure RELEASE=y
  make CFLAGS="$CFLAGS"
}

check() {
  cd "$pkgname"

  make -k check CFLAGS="$CFLAGS"
}

package() {
  cd "$pkgname"

  make install DESTDIR="$pkgdir"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
