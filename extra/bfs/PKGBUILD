# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>

pkgname=bfs
pkgver=4.0.4
pkgrel=1
pkgdesc='A breadth-first version of find'
arch=('x86_64')
url='https://tavianator.com/projects/bfs.html'
license=('0BSD')
depends=(
  'glibc'
  'acl'
  'libcap'
  'liburing'
  'oniguruma'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/tavianator/bfs#tag=$pkgver")
sha512sums=('f4a0a743df1886cb651a2cb38c6cb7ba4d88df9fbf25776ff069ff465a7bcc96b44ecd7a74ab973be4a2996fb36dce09605b885614fa97c82546633703bf8f88')
b2sums=('2cb3b8e5386cd8a42c39113687f14555fd9be10b8e9766e3a55efbd3e3b1b2848a8d02fe89741507f7f42c5d65db5cce8ae96995ea9dc5ce2250b5165045a223')

build() {
  cd "$pkgname"

  ./configure RELEASE=y
  make CFLAGS="$CFLAGS"
}

check() {
  cd "$pkgname"

  make -k check CFLAGS="$CFLAGS"
}

package() {
  cd "$pkgname"

  make install DESTDIR="$pkgdir"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
