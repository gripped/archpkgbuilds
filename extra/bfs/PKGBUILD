# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>

pkgname=bfs
pkgver=4.0.5
pkgrel=1
pkgdesc='A breadth-first version of find'
arch=('x86_64')
url='https://tavianator.com/projects/bfs.html'
license=('0BSD')
depends=(
  'glibc'
  'acl'
  'libcap'
  'liburing'
  'oniguruma'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/tavianator/bfs#tag=$pkgver")
sha512sums=('edead6c5da2b074b3ce35ec88e81b935b8204fafcf6e2eb5043ae4c20751c3d5d3ae8a7314031c80ca6802017fa6d40325c152ee567fa125c341660b7753576a')
b2sums=('91e747444e6f21c45856dae6958bb2139a96aed56f487b130ec4faea8f8317e28c27752f2492aa202f0d6eb7d58286325c0b14a9c06999beb69c9cea9289aeaa')

build() {
  cd "$pkgname"

  ./configure RELEASE=y
  make CFLAGS="$CFLAGS"
}

check() {
  cd "$pkgname"

  make -k check CFLAGS="$CFLAGS"
}

package() {
  cd "$pkgname"

  make install DESTDIR="$pkgdir"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
