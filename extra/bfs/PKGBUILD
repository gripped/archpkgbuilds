# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>

pkgname=bfs
pkgver=4.0.6
pkgrel=1
pkgdesc='A breadth-first version of find'
arch=('x86_64')
url='https://tavianator.com/projects/bfs.html'
license=('0BSD')
depends=(
  'glibc'
  'acl'
  'libcap'
  'liburing'
  'oniguruma'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/tavianator/bfs#tag=$pkgver")
sha512sums=('e84d12ea76b766f55d089dccef72c26faee8abd19f864e5f69e81a1433d5e4b2ec1200859dc80f120ca6ced425760ab158e9ff4450b41778f75bcb1a9b8252cb')
b2sums=('5b50d76ee5d831659e4728549aa06a9fdb1192416ea91d4ebab91f658803669462dd5e05a1d997287134f39a4d06f98aef886b1f3c3a98f0839d46bde47cc14c')

build() {
  cd "$pkgname"

  ./configure RELEASE=y
  make CFLAGS="$CFLAGS"
}

check() {
  cd "$pkgname"

  make -k check CFLAGS="$CFLAGS"
}

package() {
  cd "$pkgname"

  make install DESTDIR="$pkgdir"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
