# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=traefik
pkgver=3.1.5
pkgrel=1
pkgdesc="Modern reverse proxy written in Go"
arch=('x86_64')
url="https://github.com/traefik/traefik"
license=('MIT')
makedepends=('go')
depends=('glibc')
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  "traefik.service"
  "traefik.sysusers"
)
options=('!lto')
sha512sums=('2240770ceaeba034fbdde729c4c88e8fd7923c7e53f281713b39a3e660b3cd247b7e20785221f4e19e81119df9e0dac68cf4444783d582bfdc8c0fd9b7659c68'
            '25ab1113bb69936c443fe49d5d3da7d39171fc82e8fa06f46ed4e2dfcaa89685d50ca50b58c32215852d79e5c8d4736a287c15b75420c78a853c51502cb2641a'
            '5fecfed5df77bf28c2c976ebaebedb030904d41509e740821140a4889bda20327f416e78f8d19b0ee78c8bc422d1907ce05ef3562e4d3e36ddfbdbea5e860b2a')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -X github.com/traefik/traefik/v3/pkg/version.Version=$pkgver \
    -compressdwarf=false  \
    -linkmode=external \
  "
  go build -v -ldflags "$ld_flags" ./cmd/traefik
}

check() {
  cd $pkgname-$pkgver
  # Skip tests:
  # - v3/integration: requires a running Docker daemon.
  # - v3/pkg/server/service: seem to be flaky.
  local tests=$(
    go list ./... \
      | grep -v github.com/traefik/traefik/v3/integration \
      | grep -v github.com/traefik/traefik/v3/pkg/server/service \
  )
  # shellcheck disable=SC2086
  go test $tests
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" traefik
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" "$srcdir/traefik.service"
  install -vDm644 "$srcdir/traefik.sysusers" "$pkgdir/usr/lib/sysusers.d/traefik.conf"
  # Create empty acme.json file, otherwise the service file will fail.
  install -vDm644 /dev/null "$pkgdir/etc/traefik/acme.json"
}
