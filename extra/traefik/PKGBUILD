# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=traefik
pkgver=3.5.0
pkgrel=1
pkgdesc="Modern reverse proxy written in Go"
arch=('x86_64')
url="https://github.com/traefik/traefik"
license=('MIT')
depends=('glibc')
makedepends=(
  'go'
  'nodejs-lts-jod'
  'npm'
  'yarn'
)
backup=("etc/traefik/traefik.yml")
options=('!lto')
source=(
  "$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "traefik.service"
  "traefik.sysusers"
  "traefik.tmpfiles"
)
sha512sums=('21113b23151320c97b398d03b1a09380c4db268898e51c8b70cab773a4d1daf75ca4def75333cf7f6c63e8ca51e98d397e10504d557da0de1465d383fde0e12b'
            '8a068799588d8982cfcafe994254af1f29b4aa0065e5d29de0b7acf89e442ff43723adf9519654822d7d44dc6c6a46025f514768f0d3ec0e34a434576a9f4d45'
            '48d4ae18774ee3c4a3cbd76951aac90a98281c96e8696c3ebc0bff3bf6e726fe44cf303e11cb7391b29b958c30ca3013a0f3926c3e43537e60926ff429ce17bc'
            '4b4200188bb1803e8068c0369a53621a64bbdf37962c6153dafb46e3babcc47061412d65d596e23072ed07dabeccc38779af7724c9f50191e67870710a76722c')

prepare() {
  cd $pkgname-$pkgver

  npm --prefix webui install quasar

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver

  npm --prefix webui run build

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"
  local ld_flags=" \
    -X github.com/traefik/traefik/v3/pkg/version.Version=$pkgver \
    -compressdwarf=false  \
    -linkmode=external \
  "
  go build -v -ldflags "$ld_flags" ./cmd/traefik
}

check() {
  cd $pkgname-$pkgver
  # Skip tests:
  # - v3/integration: requires a running Docker daemon.
  # - v3/pkg/server/service: seem to be flaky.
  local tests=$(
    go list ./... \
      | grep -v github.com/traefik/traefik/v3/integration \
      | grep -v github.com/traefik/traefik/v3/pkg/provider/kubernetes/ingress \
      | grep -v github.com/traefik/traefik/v3/pkg/proxy/fast \
      | grep -v github.com/traefik/traefik/v3/pkg/server/router/tcp \
      | grep -v github.com/traefik/traefik/v3/pkg/server/service \
  )
  # shellcheck disable=SC2086
  go test $tests
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" traefik
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" "$srcdir/traefik.service"
  install -vDm644 "$srcdir/traefik.sysusers" "$pkgdir/usr/lib/sysusers.d/traefik.conf"
  install -vDm644 "$srcdir/traefik.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/traefik.conf"
  install -vDm644 traefik.sample.yml "$pkgdir/etc/traefik/traefik.yml"
}
