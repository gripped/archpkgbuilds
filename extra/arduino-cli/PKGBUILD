# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
pkgver=1.2.2
pkgrel=1
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
depends=('glibc')
makedepends=('go' 'git')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('a7a58ac7f7376f22ed056ec2674c9ac5859f433cb24aa7706337ecbf45a05724a5dd04c2a3c9d35d66a69f6a17455fd298e1506395e4c2e9e313b798a45d5717')

build(){
  cd $pkgname

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'

  go build \
        -ldflags "-linkmode=external -X github.com/arduino/arduino-cli/version.versionString=$pkgver-arch -X github.com/arduino/arduino-cli/version.commit=$(git rev-parse HEAD)" \
        -v .
}

package() {
  install -Dm 755 $pkgname/$pkgname "$pkgdir"/usr/bin/$pkgname
}
