# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
pkgver=1.1.1
pkgrel=1
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
depends=('glibc')
makedepends=('go' 'git')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('4625e0d79519271d32311b41c6cc858d541f06a2b03d75655ada322ee076d956c0910e05cd486dee82047a13bacd6dd00fa2df8ae1b44fd2d4dabb2995cd7679')

build(){
  cd $pkgname

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'

  go build \
        -ldflags "-linkmode=external -X github.com/arduino/arduino-cli/version.versionString=$pkgver-arch -X github.com/arduino/arduino-cli/version.commit=$(git rev-parse HEAD)" \
        -v .
}

package() {
  install -Dm 755 $pkgname/$pkgname "$pkgdir"/usr/bin/$pkgname
}
