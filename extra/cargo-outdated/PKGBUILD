# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Jian Zeng <anonymousknight96+aur AT gmail.com>
# Contributor: Alexandre Bury <alexandre.bury+aur AT gmail.com>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=cargo-outdated
pkgver=0.18.0
pkgrel=2
pkgdesc="A cargo subcommand for displaying when Rust dependencies are out of date"
url="https://github.com/kbknapp/cargo-outdated"
arch=('x86_64')
license=('MIT')
depends=(
  'cargo'
  'glibc'
  'libcurl.so'
  'libgcc'
  'libssh2.so'
  'libssl.so'
  'libz.so'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kbknapp/cargo-outdated/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('a0acb73eca2cc65915db426e49a0b834d20ef2cb302bd2ce21c1d59f3cb1894b')
b2sums=('3ff96b196ce1bb7f9f0c38f3402e275b0f592676808019cbb5232a5e798bc2a64b6c5af0f0c293a702d919e8272d75c7a32fba966b1c654407f781b05c9cfc5a')
options=('!lto')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
  cargo build --frozen --release
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
