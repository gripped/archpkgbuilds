# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=tld
pkgname=python-${_pyname}
pkgver=0.13.1
pkgrel=1
pkgdesc="Extracts the top level domain (TLD) from the URL given"
arch=(any)
url="https://github.com/barseghyanartur/tld"
license=('MPL' 'GPL' 'LGPL')
depends=('python')
makedepends=('python-'{build,installer,wheel}
             'python-setuptools-scm')
source=("${url}/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz")
b2sums=('b7184946af62fc967a1a98de3179702bff569aeb2dcdfe9885bde1f5e8eb952fa1741ff79d0517693643b24326bd10285e201640ba2a6b21dddd54945b9b358b')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build -wn
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
