# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=signstar-configure-build
_name=signstar
pkgver=0.1.1
pkgrel=1
pkgdesc="A commandline tool to configure a Signstar system during build"
arch=(x86_64)
url="https://gitlab.archlinux.org/archlinux/signstar/-/tree/main/signstar-configure-build"
_url="https://gitlab.archlinux.org/archlinux/signstar"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  mold
  rust
  rust-script
)
source=($_name::git+$_url.git?signed#tag=$pkgname/$pkgver)
sha512sums=('29eb7923552c9650feb77186a2825fee47bf87c97b451f058a54a1675f360f7e79b3d457c0671298d3823778d060c17e5eb41c573fec0f8b944e5edfef3bc642')
b2sums=('26fc72d25cf1529aabf989704318d4a2d204fa42b16b2e93fb6a39cb355a2c992934fa250745c207f917a673dfbfb15f8f698688b3f252113c9d8216755a35a9')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features --package $pkgname
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/$pkgname/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $_name/LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/target/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 $_name/target/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm 644 $_name/target/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 $_name/target/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
}
