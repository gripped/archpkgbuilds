# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-sphinx-prompt
pkgver=1.10.1
pkgrel=1
pkgdesc='Sphinx directive to add unselectable prompt'
arch=('any')
url='https://github.com/sbrunner/sphinx-prompt'
license=('BSD-3-Clause')
depends=(
  'python'
  'python-sphinx'
  'python-pygments'
  'python-docutils'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-poetry-core'
)
source=(
  "$pkgname::git+$url#tag=$pkgver"
  'pyproject.toml-template.patch'
  'remove-unnecessary-dependencies.patch'
)
sha512sums=('776a8582de15eb8b6559c581df2ae1832c93cb75c2ee9ef3507c8c5370422f60b74161730d3a4ea422231aa40aed37ec9c28b27632ea9416f2c2e2495e6c1a75'
            '7d706f2d0cbd9393c390c7e489c5f23c030c0ba62145f3954baa7d27026a5f7dba8efe61c25c8ca17d0245d96c46059df5df33a733e41ec5ecff2afa33a46901'
            '2ae1c57464924e7c0061501288274c8e34c61d19c91db80aaf57f7537d99cab0f6b548ad57e7a19d559a4198304ac163db4f59ee0e6e1879b8237ae89184fdb1')
b2sums=('0eb5592847688181aad8788e7a7288f2cc4498f6b66882f7244720bb629d9ea7cc4efe03da78329c2f69166cee93fabaa74359a9fb3f368dc3c23dfadf09c236'
        'd15cddaf61bb25498ce225de227c7e8849669af8eb74dd93c0d192986ca2d57c44b2ea4b36820653ecc51fc0e285d52993241ab3344251db5013c23be7c354d8'
        '9eaf5a38be518b989a5d2e1554ebe87104e4edacaede302e6c2d6568d221c27a853e121030c3b1ef7c6e80f817b5d5e4a652567efb1e6575a6036a861ca9d4fa')

prepare() {
  cd "$pkgname"

  # manually set version
  patch -p1 -i "$srcdir/pyproject.toml-template.patch"

  # remove useless dependencies
  patch -p1 -i "$srcdir/remove-unnecessary-dependencies.patch"

  # WTF is wrong with Python developers
  # https://github.com/sbrunner/sphinx-prompt/commit/4e4638bd449e33ea737b704d867d4ee35d4d5ce3
  cp -r sphinx{_,-}prompt
}

build() {
  cd "$pkgname"

  # set version
  sed -e "s/@PKGVER@/$pkgver/" -i pyproject.toml

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
