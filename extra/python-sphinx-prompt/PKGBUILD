# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-sphinx-prompt
pkgver=1.10.2
pkgrel=2
pkgdesc='Sphinx directive to add unselectable prompt'
arch=(any)
url='https://github.com/sbrunner/sphinx-prompt'
license=(BSD-3-Clause)
depends=(
  python
  python-sphinx
  python-pygments
  python-docutils
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-poetry-core
)
source=(
  "$pkgname::git+$url#tag=$pkgver"
  pyproject.toml-template.patch
  remove-unnecessary-dependencies.patch
)
sha512sums=('b6a292b7098320024bf51a53e36e6061e109198555b6eb950b0a457e0965fe50d1f37cee0e84e166215ee97856178e417d351576d584f56f1f0a2080e6c70e6e'
            '7d706f2d0cbd9393c390c7e489c5f23c030c0ba62145f3954baa7d27026a5f7dba8efe61c25c8ca17d0245d96c46059df5df33a733e41ec5ecff2afa33a46901'
            '2ae1c57464924e7c0061501288274c8e34c61d19c91db80aaf57f7537d99cab0f6b548ad57e7a19d559a4198304ac163db4f59ee0e6e1879b8237ae89184fdb1')
b2sums=('7007d67eeb776b0bc1ca99bc0fc8c6fc3ac9afee8eda7a25ffc68099aa1de7acb0c5dacbd9f844ca8960bd46a2b05b43dadaaef70328122d39d0d59f82216c9a'
        'd15cddaf61bb25498ce225de227c7e8849669af8eb74dd93c0d192986ca2d57c44b2ea4b36820653ecc51fc0e285d52993241ab3344251db5013c23be7c354d8'
        '9eaf5a38be518b989a5d2e1554ebe87104e4edacaede302e6c2d6568d221c27a853e121030c3b1ef7c6e80f817b5d5e4a652567efb1e6575a6036a861ca9d4fa')

prepare() {
  cd "$pkgname"

  # manually set version
  patch -p1 -i "$srcdir/pyproject.toml-template.patch"

  # remove useless dependencies
  patch -p1 -i "$srcdir/remove-unnecessary-dependencies.patch"

  # WTF is wrong with Python developers
  # https://github.com/sbrunner/sphinx-prompt/commit/4e4638bd449e33ea737b704d867d4ee35d4d5ce3
  cp -r sphinx{_,-}prompt
}

build() {
  cd "$pkgname"

  # set version
  sed -e "s/@PKGVER@/$pkgver/" -i pyproject.toml

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
