# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-sphinx-prompt
pkgver=1.9.0
pkgrel=3
pkgdesc='Sphinx directive to add unselectable prompt'
arch=('any')
url='https://github.com/sbrunner/sphinx-prompt'
license=('BSD-3-Clause')
depends=(
  'python'
  'python-sphinx'
  'python-pygments'
  'python-docutils'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-poetry-core'
)
source=(
  "$pkgname::git+$url#tag=$pkgver"
  'pyproject.toml-template.patch'
  'remove-unnecessary-dependencies.patch'
)
sha512sums=('caa90d9acdee7fe709119ba43c0307c78fe5327ec944baabc4767202aee75de8e0d5144401f96ad72f8e5cba888aafad556823ffc4e96eff4f5591ac0cd49468'
            '090ce2fba167aebf8809e2f368ffc2ce6c3d2500c88f2873c5f67599ba6bae8bac5fe5eaefee69f6e00117039af8daf089e392fe28fad7b64800f47a9b1afdb1'
            'b3f868acce55474129c4ddfce566635e26aace3f5737f7be6974be0c8bbd3fef07610ea2f57166167d1f24fff0781c8dd4a1ff955b9526eb3e785b263f764d95')
b2sums=('a3c2ad86a479579e3d174e8915ee1e1b72e8171378d95d16733654a7cb58c751c664eff27cb47c06624ec7e37495397e53303a60887b6e6a8af05fac6ffb0194'
        '9ebc31d79afe81f3aa2c50a9797e550fe05155f625436ca02c5ea4cf3a147c513a7a6a84dbde56ab91043c7003a71ea6f5b9b97b9366762398ca9e2b004a9a01'
        '80650ab9af6f6820803fd5839c5fefdb280a047a128dba1d64398dcf7d8a3f2b9d6750dfa7b817a9c42e31d6a825622246fd9e2abaf769e688a869f3979f4b48')

prepare() {
  cd "$pkgname"

  # manually set version
  patch -p1 -i "$srcdir/pyproject.toml-template.patch"

  # remove useless dependencies
  patch -p1 -i "$srcdir/remove-unnecessary-dependencies.patch"

  # WTF is wrong with Python developers
  # https://github.com/sbrunner/sphinx-prompt/commit/4e4638bd449e33ea737b704d867d4ee35d4d5ce3
  cp -r sphinx{_,-}prompt
}

build() {
  cd "$pkgname"

  # set version
  sed -e "s/@PKGVER@/$pkgver/" -i pyproject.toml

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/sphinx_prompt-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
