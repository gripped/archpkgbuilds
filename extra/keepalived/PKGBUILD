# Maintainer: SÃ©bastien Luttringer
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=keepalived
pkgver=2.3.4
pkgrel=2
pkgdesc='Failover and monitoring daemon for LVS clusters'
arch=('x86_64')
url='https://www.keepalived.org/'
license=('GPL-2.0-or-later')
backup=('etc/keepalived/keepalived.conf'
        'etc/sysconfig/keepalived')
depends=('glibc' 'kmod' 'libmnl' 'libnftnl' 'libnl' 'openssl' 'file' 'iptables' 'systemd-libs')
optdepends=('ipset: ipset support')
makedepends=('git' 'ipset')
options=('!emptydirs')
source=("git+https://github.com/acassen/keepalived.git#tag=v${pkgver}")
sha256sums=('60d7893465a698c7bc791b1bd6a5c818e6602c3381d04525102f6c941c4e24c8')

prepare() {
  cd "${pkgname}"

  autoreconf -fi
}

build() {
  # trick broken ./configure systemctl test
  printf "#!/bin/sh\necho -.mount\n" > systemctl
  chmod +x systemctl
  PATH=$PWD:$PATH

  cd "${pkgname}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --runstatedir=/run \
    --enable-json
  make
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install

  # move conf file to correct name
  mv "${pkgdir}"/etc/keepalived/keepalived.conf{.sample,}

  # move examples to /usr/share
  install -d -m 0755 "${pkgdir}/usr/share/${pkgname}"
  mv "${pkgdir}/etc/keepalived/samples" "${pkgdir}/usr/share/$pkgname/samples"
}

# vim:set ts=2 sw=2 et:
