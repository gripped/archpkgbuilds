pkgbase=ast-grep
pkgname=(
  ast-grep
  python-ast-grep
)
pkgver=0.41.0
pkgrel=1
pkgdesc='CLI tool for code structural search, lint and rewriting'
url='https://ast-grep.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo
  python-build
  python-installer
  python-maturin
  python-wheel
)
options=(!lto)
source=(https://github.com/ast-grep/ast-grep/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha512sums=('1dfa0b53ac0a5651922e373ef8737c11d953791e86d6904419229b8501fa2763bc571752eed68cb4c4ac42e5345dccc60f2d9956ae0cd8e53f41c573d6fb435f')
b2sums=('1bfb50dee5544ff3bdc6ba3247f844528f7b795b654bdbbdb35e7414158b288173493c1a8f61be301576b8945c0efa0c8be6236bc00cf25caa91867966f799ab')

prepare() {
  cd ${pkgbase}-${pkgver}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgbase}-${pkgver}
  cargo build --frozen --release --all-features

	local shell; for shell in bash fish zsh; do
		./target/release/ast-grep completions "${shell}" > target/ast-grep.${shell}
	done

  cd crates/pyo3
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgbase}-${pkgver}
  cargo test --frozen --all-features --workspace
}

package_ast-grep() {
  depends=(
    gcc-libs
    glibc
  )

  cd ${pkgbase}-${pkgver}
  install -Dm 755 target/release/ast-grep -t "${pkgdir}/usr/bin/"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

	install -Dm 644 target/${pkgname}.bash "${pkgdir}"/usr/share/bash-completion/completions/${pkgname}
	install -Dm 644 target/${pkgname}.fish "${pkgdir}"/usr/share/fish/vendor_completions.d/${pkgname}.fish
	install -Dm 644 target/${pkgname}.zsh "${pkgdir}"/usr/share/zsh/site-functions/_${pkgname}
}

package_python-ast-grep() {
  depends=(
    gcc-libs
    glibc
    python
  )

  cd ${pkgbase}-${pkgver}
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  cd crates/pyo3
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
