pkgbase=ast-grep
pkgname=(
  ast-grep
  python-ast-grep
)
pkgver=0.40.5
pkgrel=1
pkgdesc='CLI tool for code structural search, lint and rewriting'
url='https://ast-grep.github.io/'
arch=(x86_64)
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo
  python-build
  python-installer
  python-maturin
  python-wheel
)
options=(!lto)
source=(https://github.com/ast-grep/ast-grep/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha512sums=('b7319059da2e395a6de10265469fa49368beb6b141da9d260b3d05eddc760540abdd2b0737167f00eb1b873b789faf8f6ef858e07465344d151bafeded70efd0')
b2sums=('c3f45308fd668cfde3dabd614edf6d47a7cd36d525229ecd8834480f13c54cf9a890b4abdb42ddf6b680a6b8a8194c343836cad64559de32197a1385808a87bf')

prepare() {
  cd ${pkgbase}-${pkgver}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgbase}-${pkgver}
  cargo build --frozen --release --all-features

	local shell; for shell in bash fish zsh; do
		./target/release/ast-grep completions "${shell}" > target/ast-grep.${shell}
	done

  cd crates/pyo3
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgbase}-${pkgver}
  cargo test --frozen --all-features --workspace
}

package_ast-grep() {
  depends=(
    gcc-libs
    glibc
  )

  cd ${pkgbase}-${pkgver}
  install -Dm 755 target/release/ast-grep -t "${pkgdir}/usr/bin/"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

	install -Dm 644 target/${pkgname}.bash "${pkgdir}"/usr/share/bash-completion/completions/${pkgname}
	install -Dm 644 target/${pkgname}.fish "${pkgdir}"/usr/share/fish/vendor_completions.d/${pkgname}.fish
	install -Dm 644 target/${pkgname}.zsh "${pkgdir}"/usr/share/zsh/site-functions/_${pkgname}
}

package_python-ast-grep() {
  depends=(
    gcc-libs
    glibc
    python
  )

  cd ${pkgbase}-${pkgver}
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  cd crates/pyo3
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
