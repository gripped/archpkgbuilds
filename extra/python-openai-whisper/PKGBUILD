# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=whisper
pkgname=python-openai-$_pyname
pkgver=20240930
pkgrel=2
pkgdesc='Robust speech recognition via large-scale weak supervision'
arch=(any)
url='https://github.com/openai/whisper'
license=(MIT)
depends=(python
         python-more-itertools
         python-numba
         python-numpy
         python-pytorch
         python-regex
         python-tiktoken
         python-tqdm)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
source=(git+https://github.com/openai/whisper#tag=v$pkgver)
sha256sums=('32457d48f7f884d06e7a1ff712d6c9c77dad71b77cf101ef713b0cc68c82bf65')

prepare() {
  cd $_pyname
  git cherry-pick -n 9b8c3cbf2daeea55ffad5310223a96c7372bbed2 # Fix build with Python 3.13
}

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname
# Deselect tests that need CUDA
  PYTHONPATH="$PWD" \
  pytest -v -x \
    --deselect tests/test_timing.py::test_dtw_cuda_equivalence \
    --deselect tests/test_timing.py::test_median_filter_equivalence \
    --deselect tests/test_transcribe.py::test_transcribe[large-v1] \
    --deselect tests/test_transcribe.py::test_transcribe[large-v2] \
    --deselect tests/test_transcribe.py::test_transcribe[large-v3] \
    --deselect tests/test_transcribe.py::test_transcribe[large]
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
