# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.18.0
pkgrel=2
pkgdesc='Fuzzy Finder in Rust!'
arch=('x86_64')
url='https://github.com/lotabout/skim'
license=('MIT')
depends=('ncurses')
makedepends=('git' 'rust')
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim-plugin-runtime: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('840b0c1990588fc35a8e93fe4a7eadfc2d1add443f7e2ab009a16b8eb1c8e23dcecfe699b318031cd69bb9059c377b7e189e2cdb54d3ee1ee2662db8e2b39d16')
b2sums=('6e5a206dd8b27536da30b4ba4b9093205caccd6a2fb85b605ee705c2f0b4f43e96e6bc8c7299bc405326e31770b1bb350a0b8b0d44890c8049b9a2a1fe2bf89b')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/vendor_functions.d/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
