# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.16.0
pkgrel=1
pkgdesc='Fuzzy Finder in Rust!'
arch=('x86_64')
url='https://github.com/lotabout/skim'
license=('MIT')
depends=('ncurses')
makedepends=('git' 'rust')
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim-plugin-runtime: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('c9845940b82b5f6523410ff3b63ebe57424fbdef15de1e3263c7ac0100541168e63553c2c1797ea12adf8eb3f9ecbfde6e96f855c65b2244d648da8d1fb76727')
b2sums=('bbb8e9529cce2f112ecded2068a550f4a5a7b3527a4d600c975b1b706d7f7421be73a01297134c88560d3776246a50d0f92f92f3c4f19862863368d7fb4e2b25')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/functions/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
