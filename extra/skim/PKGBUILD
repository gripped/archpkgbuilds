# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.15.5
pkgrel=1
pkgdesc='Fuzzy Finder in Rust!'
arch=('x86_64')
url='https://github.com/lotabout/skim'
license=('MIT')
depends=('ncurses')
makedepends=('git' 'rust')
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim-plugin-runtime: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('cbcf7eeb3bc9cb49f1888ffd1978c6f5478242e7f3a375818f4154f42b367acbbaa91205d08cd32c4b20fc971c41d5b014ec3dc96b4849eea6dd9f14e399ca44')
b2sums=('e95f171d3b49ab3121d2f805fc647c76fcdf532ceaaf757adbd41f6b5647f8b3b48844bb6dbf9313bfb5525cdf14618aae632482c639c24f905df9e20aae82e9')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/functions/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
