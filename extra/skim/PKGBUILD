# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=1.7.2
pkgrel=1
pkgdesc='Fuzzy Finder in Rust'
arch=(x86_64)
url='https://github.com/skim-rs/skim'
license=(MIT)
depends=(glibc gcc-libs ncurses)
makedepends=(git rust)
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim-plugin-runtime: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('3dd7e5c237d312c6a8be86ae8c987d6ca23caa99bf3a43f0048fed80515793d6c7b78c62705568595a5d13fd2de25a63e88c8b6cd366d9ce496ededafab2deb5')
b2sums=('b6e3993851f3b200b71e3474e8e2aabd16d50dfa0ebf05b18445d2062fa270624cabbca62fddcf6d49dc3458c9e74b433bdca1d687d91f7ed5bf6224ddbdc8be')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  # https://github.com/skim-rs/skim/issues/905
  cargo build --frozen --release \
    --no-default-features --features cli
}

# test currently require tmux
#check() {
#  cd "$pkgname"
#
#  cargo test --frozen --no-default-features \
#    --features cli,test-utils
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/vendor_functions.d/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"
  install -vDm644 shell/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/sk.fish"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
