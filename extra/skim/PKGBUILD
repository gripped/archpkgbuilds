# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=1.6.0
pkgrel=1
pkgdesc='Fuzzy Finder in Rust'
arch=(x86_64)
url='https://github.com/skim-rs/skim'
license=(MIT)
depends=(glibc gcc-libs ncurses)
makedepends=(git rust)
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim-plugin-runtime: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('09e18d34b6b3220522febc5c30ff6dad4b0ed2eff394dd89cbdb07014097038b6f48a8974229005c31fb86dc028ad75df6fbc251f984f2e13476e289ce1db674')
b2sums=('6ca9ef519210636d57460d548910db45f15033258b32557a7004c07e0de5fef91383d393421a3d8203b025fa58204e15c0160ed3487c9c84bfb3c6d5b2f94a0d')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  # https://github.com/skim-rs/skim/issues/905
  cargo build --frozen --release \
    --no-default-features --features cli
}

# test currently require tmux
#check() {
#  cd "$pkgname"
#
#  cargo test --frozen --no-default-features \
#    --features cli,test-utils
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/vendor_functions.d/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"
  install -vDm644 shell/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/sk.fish"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
