# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=3.4.0
pkgrel=1
pkgdesc='Fuzzy Finder in Rust'
arch=(x86_64)
url='https://github.com/skim-rs/skim'
license=(MIT)
depends=(glibc # libc.so libm.so
         libgcc libgcc_s.so
         ncurses)
makedepends=(git rust)
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim-plugin-runtime: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('1aa60a5f56e6a2fd10cd0e1cd935322b7c313ffd07ec8690e2ae36d5e748a032edd6b89a1af229c5d7d76ef04491d9e919a41908c086b774ae5c70b250d24ab0')
b2sums=('a6cfb42475e94f54c3bb88494aaf1fdc8bf04bf9a0efb2012e52b0dc2476cb588c77f8363ad71aa06a8f8ae4a17e7ae311e993b2b1f7a91a8d2ba5d9d0428112')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgname"

  # https://github.com/skim-rs/skim/issues/905
  cargo build --frozen --release \
    --no-default-features --features cli
}

# test currently require tmux
#check() {
#  cd "$pkgname"
#
#  cargo test --frozen --no-default-features \
#    --features cli,test-utils
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/vendor_functions.d/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"
  install -vDm644 shell/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/sk.fish"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
