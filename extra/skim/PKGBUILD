# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=3.1.1
pkgrel=1
pkgdesc='Fuzzy Finder in Rust'
arch=(x86_64)
url='https://github.com/skim-rs/skim'
license=(MIT)
depends=(glibc gcc-libs ncurses)
makedepends=(git rust)
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim-plugin-runtime: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0f8da8858644c1d3b958bd9da7c36e2dbe46924b52ad906c05206dc68a422a2ee506907dced1a899a501f2e9d5203c67322b3119962c237078077e31e4d890f6')
b2sums=('af79ca5ac176ed83be85c33ab18c6ca81c3e46b355c906def2344c0fd4c20da364d4c7a6cceaaf273b9d624a80616fe2522e8b24804d7f42c42c58c0c1789978')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  # https://github.com/skim-rs/skim/issues/905
  cargo build --frozen --release \
    --no-default-features --features cli
}

# test currently require tmux
#check() {
#  cd "$pkgname"
#
#  cargo test --frozen --no-default-features \
#    --features cli,test-utils
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/vendor_functions.d/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"
  install -vDm644 shell/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/sk.fish"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
