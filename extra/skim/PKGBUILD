# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=1.11.2
pkgrel=1
pkgdesc='Fuzzy Finder in Rust'
arch=(x86_64)
url='https://github.com/skim-rs/skim'
license=(MIT)
depends=(glibc gcc-libs ncurses)
makedepends=(git rust)
optdepends=(
  'bash: for resp. completions and key bindings, and for sk-tmux script'
  'fish: for resp. key bindings'
  'tmux: for sk-tmux script'
  'vim-plugin-runtime: for N/Vim plugin'
  'zsh: for resp. completions and key bindings'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('3b9d26d6b1854452bc594f31a89eb74421df1bfa6e005073fbde79fd91c5bf73d47f763426669516d960e6fddb63011ae05c4671316de53d077371c105b9825e')
b2sums=('273c75957815e9cc2fb007a544127196c2ec21006cd528a23c6248cb0586c9f67c92dfeb6ed48b9b72005cba0d25173bdb7607af813d69563cb089506c376d51')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  # https://github.com/skim-rs/skim/issues/905
  cargo build --frozen --release \
    --no-default-features --features cli
}

# test currently require tmux
#check() {
#  cd "$pkgname"
#
#  cargo test --frozen --no-default-features \
#    --features cli,test-utils
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/sk bin/sk-tmux

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/man1/*

  # vim plugin
  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/skim.vim

  # key bindings
  # TODO: figure out where the bash/zsh key bindings go
  install -vDm644 -t "$pkgdir/usr/share/skim" shell/key-bindings.{bash,zsh}
  install -vDm644 shell/key-bindings.fish "$pkgdir/usr/share/fish/vendor_functions.d/skim_key_bindings.fish"

  # shell completion
  install -vDm644 shell/completion.bash "$pkgdir/usr/share/bash-completion/completions/sk"
  install -vDm644 shell/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_sk"
  install -vDm644 shell/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/sk.fish"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
