# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=atac
pkgver=0.20.2
pkgrel=1
pkgdesc="A simple API client (postman like) in your terminal"
arch=('x86_64')
url="https://github.com/Julien-cpsn/ATAC"
license=('MIT')
depends=('gcc-libs' 'oniguruma')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('5a0833c9a237bc37880f3a2ac2b39d6d8c28bc0c6d7986993f54e2e7b3d1d739a8f5c3f957abe0396dba6f3004c7ca058792b02f4053b0ae6e0c2100c2e02ace')
options=('!lto')

prepare() {
  cd "${pkgname^^}-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  mkdir completions/
}

build() {
  cd "${pkgname^^}-$pkgver"
  export RUSTONIG_DYNAMIC_LIBONIG=1
  cargo build --release --frozen
  local compgen="target/release/$pkgname completions"
  $compgen bash "completions"
  $compgen fish "completions"
  $compgen zsh "completions"
  target/release/$pkgname man
}

check() {
  cd "${pkgname^^}-$pkgver"
  cargo test --frozen
}

package() {
  cd "${pkgname^^}-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 "$pkgname.1" -t "$pkgdir/usr/share/man/man1/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "completions/$pkgname.bash" -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm644 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -Dm644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions/"
}

# vim: ts=2 sw=2 et:
