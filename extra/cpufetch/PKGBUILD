# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Dr-Noob <peibolms at gmail dot com>

pkgname=cpufetch
pkgver=1.07
pkgrel=1
pkgdesc="Simple yet fancy CPU architecture fetching tool"
arch=(x86_64)
url="https://github.com/Dr-Noob/cpufetch"
license=(GPL-2.0-or-later)
depends=(glibc)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('dc3ec8f9c9d41d8434702a778cc150b196d5d178fd768a964f57d22f268a2c17')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/cpufetch-git/cpufetch/g' Makefile
}

build() {
  cd $pkgname-$pkgver
  CFLAGS="$CFLAGS $LDFLAGS" make GIT_VERSION="$pkgver"
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
