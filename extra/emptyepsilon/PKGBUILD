# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mewp <aur.archlinux.org@mewp.pl>

pkgname=emptyepsilon
pkgver=2024.08.09
pkgrel=1
pkgdesc='Open source spaceship bridge simulator'
url='https://github.com/daid/EmptyEpsilon'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libglvnd
  libx11
  libxrandr
  sdl2
)
makedepends=(
  cmake
  git
  mesa
  python
)
source=("git+https://github.com/daid/EmptyEpsilon.git#tag=EE-${pkgver}"
        "git+https://github.com/daid/SeriousProton.git#tag=EE-${pkgver}"
        gcc12.patch)
sha512sums=('97f87e43ddbf6e6697c89074f6397d3c1bd00981d13473bd843586ce8587930c30633f3c8ac5e4ed9917986f6ecbae5a3487f95750aeb0786230e28fa0b6f7cc'
            '84779b98ff949250cf1a151de7a565763805c9fb3b420ea80d79e37771090206c3aa71946aad30c09a391f1b41bfe1ccd1b65de2c61ed35f93e8884bba3c8422'
            '479b76b604b4a4fc8152da11775533b8d56eb4bfd688a28ea6c64a39bfe40e5d365ee035f34cf3fa2cfa16d877cb5324bd2b911270e19f55e77d60386b08ed13')

prepare() {
  patch -Np0 <gcc12.patch
  # fix shader loading, should be upstreamed
  sed 's|new Engine();|chdir(RESOURCE_BASE_DIR); new Engine();|' -i EmptyEpsilon/src/main.cpp
  # fix crashes
  cd SeriousProton
  git cherry-pick -n d0c08263fc0895db59d29b4c0e23efc2847a78a1
}

build() {
  mkdir -p EmptyEpsilon/build
  cd EmptyEpsilon/build
  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  # add cppflags to use fortify
  export CXXFLAGS="${CXXFLAGS} ${CPPFLAGS}"
  # release type is important here to get optimisation flags
  # defined in the project's cmake files
  cmake -DSERIOUS_PROTON_DIR="${srcdir}/SeriousProton" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCPACK_PACKAGE_VERSION_MAJOR="$(echo ${pkgver} | cut -d. -f1)" \
        -DCPACK_PACKAGE_VERSION_MINOR="$(echo ${pkgver} | cut -d. -f2)" \
        -DCPACK_PACKAGE_VERSION_PATCH="$(echo ${pkgver} | cut -d. -f3)" \
        -DOpenGL_GL_PREFERENCE=GLVND \
        ..
  make
}

package() {
  cd EmptyEpsilon
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/emptyepsilon"
  mv "${pkgdir}"/usr/share/doc/EmptyEpsilon/script_reference.html "${pkgdir}/usr/share/doc/emptyepsilon"
  rmdir "${pkgdir}"/usr/share/doc/EmptyEpsilon
}

# vim: ts=2 sw=2 et:
