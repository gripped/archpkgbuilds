# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=forge-sparks
pkgver=0.4.0
pkgrel=3
pkgdesc='Simple notifier app with support for Github, GitLab, Gitea and Forgejo'
arch=(any)
url='https://apps.gnome.org/ForgeSparks/'
license=(MIT)
depends=(
  dconf
  gjs
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libportal
  libportal-gtk4
  libsecret
  libsoup3
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/rafaelmardojai/forge-sparks.git#tag=$pkgver"
  git+https://github.com/sonnyp/troll.git
)
b2sums=(
  7854c572ecea01c35a374f42126cf178b3de24029a03653b4f6e5a8483e3c81f055d0b649ce06644c4822c8e846a355efca6a5af63f43f6393c95ff88184c67e
  SKIP
)

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update

  # https://github.com/rafaelmardojai/forge-sparks/pull/45
  git cherry-pick -n baf3cf8bf083c647072ed8f7844f6c9556dba1b5

  # https://github.com/rafaelmardojai/forge-sparks/pull/48
  git cherry-pick -n 0883de3bf3706d67307e4ff9aec100166c8ebac5

  # https://github.com/rafaelmardojai/forge-sparks/pull/49
  git cherry-pick -n 863ae6b201f9f1b684c46953d490c2b903a751d6
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname/LICENSE
}
