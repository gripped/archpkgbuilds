# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=forge-sparks
pkgver=1.0.0
pkgrel=1
pkgdesc='Simple notifier app with support for Github, GitLab, Gitea and Forgejo'
arch=(any)
url='https://apps.gnome.org/ForgeSparks/'
license=(MIT)
groups=(gnome-circle)
depends=(
  dconf
  gjs
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libportal
  libportal-gtk4
  libsecret
  libsoup3
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/rafaelmardojai/forge-sparks.git#tag=$pkgver"
  git+https://github.com/sonnyp/troll.git
)
b2sums=(
  27741bd782f364c199bd3de454329bc99552946c6b71fe706eb2aaa334dcc4b1f1995d31283231d9995420826a5a34920f07de64e21a5e6ea2029b25a5919151
  SKIP
)

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname/LICENSE
}
