# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributor: Dan Beste <dan.ray.beste@gmail.com>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>
# Contributor: stef204 <https://aur.archlinux.org/account/stef204>

pkgname=borgmatic
_commit=e80f27f9221d0e7e628708f4e230d21a1d2d0608
pkgver=1.7.14
pkgrel=3
pkgdesc='Simple, configuration-driven backup software for servers and workstations'
arch=('any')
url=https://torsion.org/borgmatic
license=('GPL3')
depends=('borg' 'python-colorama' 'python-jsonschema' 'python-requests'
         'python-ruamel-yaml' 'python-setuptools')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-flexmock' 'python-pytest')
optdepends=('python-llfuse: for mount action')
source=("git+https://github.com/borgmatic-collective/borgmatic.git#tag=$_commit")
b2sums=('SKIP')

prepare() {
  cd $pkgname

  # this can be dropped after the release of 1.7.15
  git cherry-pick --strategy recursive -X ours -n \
    1d7c7eaaa7ec0ab67add6e95c7a95a14ec1498ca

  sed -i 's,root/.local,usr,' sample/systemd/borgmatic{,-user}.service
}

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest -c /dev/null \
    --ignore=tests/end-to-end
}

package() {
  cd $pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system/ sample/systemd/borgmatic.{service,timer}
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/user/ sample/systemd/borgmatic-user.{service,timer}

  install -d "$pkgdir"/usr/share/{bash-completion/completions,fish/vendor_completions.d}
  ./test-env/bin/borgmatic --bash-completion > \
    "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  ./test-env/bin/borgmatic --fish-completion > \
    "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgname.fish
}
