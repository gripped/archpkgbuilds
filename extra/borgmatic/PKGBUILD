# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributor: Dan Beste <dan.ray.beste@gmail.com>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>
# Contributor: stef204 <https://aur.archlinux.org/account/stef204>

pkgname=borgmatic
pkgver=1.9.0
pkgrel=2
pkgdesc='Simple, configuration-driven backup software for servers and workstations'
arch=(any)
url=https://torsion.org/borgmatic
license=(GPL-3.0-or-later)
depends=(
  borg
  python
  python-colorama
  python-jsonschema
  python-packaging
  python-requests
  python-ruamel-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  apprise
  python-flexmock
  python-pytest
)
optdepends=(
  'apprise: for Apprise notifications'
  'python-pyfuse3: for mount action'
)
source=(
  "git+https://projects.torsion.org/borgmatic-collective/borgmatic.git#tag=$pkgver"
  0001-Comment-out-encrypted-credentials.patch
)
b2sums=('8e4e618408660fc91b83667f0a5e38fc96b0ef0487db78253b81afe8ad8fcdffcbbc0bad00083885966ea875cc5047d0a1f8fd9b0d2a3588e8a60542a2ebf7b3'
        '53bf49630e91a7aba1bb3a75fea1a1f8b994eb91f8fac1f047e523a1025475101ddea4d0ba917b57f5eff6a6709bbc3a4b45f4e239801c43bc491260bed6a7ca')

prepare() {
  cd $pkgname
  sed -i 's,root/.local,usr,' sample/systemd/borgmatic{,-user}.service

  # https://projects.torsion.org/borgmatic-collective/borgmatic/pulls/929
  patch --forward --strip=1 --input=../0001-Comment-out-encrypted-credentials.patch
}

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation

  # Build shell completions
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  mkdir completions
  ./test-env/bin/borgmatic --bash-completion > completions/bash
  ./test-env/bin/borgmatic --fish-completion > completions/fish
}

check() {
  cd $pkgname
  # Override addopts as they invoke coverage testing
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest \
    --override-ini="addopts=" --ignore=tests/end-to-end
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system \
    sample/systemd/borgmatic.{service,timer}
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/user \
    sample/systemd/borgmatic-user.{service,timer}

  install -Dm644 completions/bash \
    "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  install -Dm644 completions/fish \
    "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgname.fish
}
