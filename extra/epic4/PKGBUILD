# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Chris Brannon <cmbrannon (at) cox.net>

pkgname=epic4
pkgver=2.10.11
pkgrel=2
pkgdesc="It's a new direction in ircII development"
arch=('x86_64')
url="http://www.epicsol.org/"
license=('custom')
depends=('openssl' 'ncurses' 'tcl' 'perl')
options=('!emptydirs')
source=(http://ftp.epicsol.org/pub/epic/EPIC4-PRODUCTION/$pkgname-$pkgver.tar.xz
        test-cflags.patch)
sha256sums=('622f9a11d86cb6b768d1004872e1931cdcc01e7f10029ecfc42a3945368ed151'
            'd1caf896c21529e4ad81644b808b223a0d1c2e46bf0d5ad06226160c4a65eda3')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../test-cflags.patch
}

build() {
 cd "${srcdir}"/$pkgname-$pkgver

  CFLAGS+=" -DHAVE_STRUCT_SOCKADDR_STORAGE -std=gnu17" \
  ./configure --prefix=/usr \
       --mandir=/usr/share/man \
       --with-ssl \
       --with-ipv6 \
       --with-perl \
       --with-tcl
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix="${pkgdir}"/usr \
       libexecdir="${pkgdir}"/usr/bin \
       mandir="${pkgdir}"/usr/share/man install

#license
  install -Dm644 COPYRIGHT "${pkgdir}"/usr/share/licenses/$pkgname/COPYRIGHT
}
