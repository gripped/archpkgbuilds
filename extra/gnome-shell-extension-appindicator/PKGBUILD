# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gnome-shell-extension-appindicator
pkgver=63
pkgrel=1
epoch=1
pkgdesc='AppIndicator/KStatusNotifierItem support for GNOME Shell'
url='https://github.com/ubuntu/gnome-shell-extension-appindicator'
arch=('any')
license=('GPL-2.0-or-later')
depends=('gnome-shell')
makedepends=(
  'jq'
  'meson'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ubuntu/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('5c9d19a9a17f66b02378865dac89071e7e08421e634676326fbd6fb2746fe1c8')

build() {
  arch-meson "${pkgname}-${pkgver}" build -Dlocal_install=disabled
  meson compile -C build
}

package() {
  meson install -C build --destdir="${pkgdir}"
  # this file is generated at pkg install time
  rm "${pkgdir}/usr/share/glib-2.0/schemas/gschemas.compiled"
}

# vim: ts=2 sw=2 et:
