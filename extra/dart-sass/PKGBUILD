# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.77.5
pkgrel=1
pkgdesc='Sass makes CSS fun again'
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('git' 'dart' 'buf')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
_sass_version=2.7.1
source=(
  "$pkgname::git+https://github.com/sass/dart-sass.git#tag=$pkgver"
  "github.com-sass-sass::git+https://github.com/sass/sass.git#tag=embedded-protocol-$_sass_version"
)
sha512sums=('5f1bb84af96526facf1e05cd0e1b759ef642a97cd55be8740dbebcad6a3074cbea7a494cb6c72a19b359945e549e67048ff57cdbbc4b2529b4907cd047dc861f'
            'a1a20703416a8c60d5287a2eac5da684883d35525b7b53e89165416266d07e42558f6e8a3cc31a763583fd4b5c458d1b33146e14e98f05f131b5ca43b8c215cb')
b2sums=('41478c8099f34d183e27d6f80674d9bc1ed4d2928d6611b90f4224a0868876200f0501bf1195e5718a295557e9f8b74e20e242b77561db1cee05498b86279370'
        'd1a1795d34a2876132917b0a05639de4bb4c09199132c95f719c677d292d066e838f9d6663370202760d27f3bdad7d200b43b4e5a0a81a0768020f4bfd4edd14')

prepare() {
  cd "$pkgname"

  mkdir -p build
  ln -sf "$srcdir/github.com-sass-sass" build/language

  # disable analytics
  dart --disable-analytics

  # download dependencies
  dart pub get
}

build() {
  cd "$pkgname"

  UPDATE_SASS_PROTOCOL=false dart run grinder protobuf
  dart compile exe \
    -Dversion=$pkgver \
    -Dprotocol-version=$(cat build/language/spec/EMBEDDED_PROTOCOL_VERSION) \
    -o sass \
    bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # embedded-protocol protobuf file
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" build/language/spec/embedded_sass.proto

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
