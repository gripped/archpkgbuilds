# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.77.4
pkgrel=1
pkgdesc='Sass makes CSS fun again'
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('git' 'dart' 'buf')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
_sass_version=2.7.1
source=(
  "$pkgname::git+https://github.com/sass/dart-sass.git#tag=$pkgver"
  "github.com-sass-sass::git+https://github.com/sass/sass.git#tag=embedded-protocol-$_sass_version"
)
sha512sums=('3f366e6aa8d4e6e933528d22541b9dfe5c872c1c689b61d38af541a3d31f847ea8f518e6490119110ebbf1e7b0cedc3524140d65341350a2fe33198921c4e1fd'
            'a1a20703416a8c60d5287a2eac5da684883d35525b7b53e89165416266d07e42558f6e8a3cc31a763583fd4b5c458d1b33146e14e98f05f131b5ca43b8c215cb')
b2sums=('bf92f8d5212ef483ef62ea2ccaef7692614a33a926964dda3c48c49c73539ff706b39d51c3ab34fd8831d0c6e0f9b110293faff976631965ddd3988e0e7436cc'
        'd1a1795d34a2876132917b0a05639de4bb4c09199132c95f719c677d292d066e838f9d6663370202760d27f3bdad7d200b43b4e5a0a81a0768020f4bfd4edd14')

prepare() {
  cd "$pkgname"

  mkdir -p build
  ln -sf "$srcdir/github.com-sass-sass" build/language

  # disable analytics
  dart --disable-analytics

  # download dependencies
  dart pub get
}

build() {
  cd "$pkgname"

  UPDATE_SASS_PROTOCOL=false dart run grinder protobuf
  dart compile exe \
    -Dversion=$pkgver \
    -Dprotocol-version=$(cat build/language/spec/EMBEDDED_PROTOCOL_VERSION) \
    -o sass \
    bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # embedded-protocol protobuf file
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" build/language/spec/embedded_sass.proto

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
