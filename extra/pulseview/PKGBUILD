# Maintainer: Anatol Pomozov
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=pulseview
pkgver=0.4.2
pkgrel=16
pkgdesc='A Qt based logic analyzer GUI for sigrok'
arch=('x86_64')
url='https://sigrok.org/wiki/Main_Page'
license=('GPL-3.0-or-later')
depends=(
  'boost-libs'
  'gcc-libs'
  'glib2'
  'glibc'
  'glibmm'
  'libsigrok'
  'libsigrokdecode'
  'qt5-base'
  'qt5-svg'
)
makedepends=(
  'boost'
  'cmake'
  'git'
  'qt5-tools'
)
#source=("git+git://sigrok.org/pulseview#tag=pulseview-$pkgver")
# git repo is offline, use mirror
source=(git+https://github.com/sigrokproject/pulseview#tag=pulseview-$pkgver)
sha512sums=('c74329f4ae5486b70f757664f305b699bf9dc14a3d71e241f5c15ad12f6d95499443ad70f3a5155262324622a64b1fca7d34caaa49ca5b4366bee9b1dfed6b21')

prepare() {
  cd $pkgname

  # Replace obsolete/deprecated Qt methods
  git cherry-pick -n ae726b70a7ada9a4be5808e00f0c951318479684
  # Fix broken build due to C++ template behind C linkage
  git cherry-pick -n ed643f0b4ac587204a5243451cda181ee1405d62
}

build() {
  local cmake_options=(
    -S $pkgname \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev \
    -DCMAKE_CXX_FLAGS=-fext-numeric-literals \
    -DDISABLE_WERROR=ON \
    -DENABLE_TESTS=ON \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  )

  # remove hardedcoded c++11 reference which breaks boost 1.87.0
  sed -i "s/-std=c++11/-std=c++17/" "$pkgname/CMakeLists.txt"

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
