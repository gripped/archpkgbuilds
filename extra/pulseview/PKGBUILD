# Maintainer: Anatol Pomozov
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=pulseview
pkgver=0.4.2
pkgrel=13
pkgdesc='A Qt based logic analyzer GUI for sigrok'
arch=('x86_64')
url='https://sigrok.org/wiki/Main_Page'
license=('GPL-3.0-or-later')
depends=(
  'boost-libs'
  'gcc-libs'
  'glib2'
  'glibc'
  'glibmm'
  'libsigrok'
  'libsigrokdecode'
  'qt5-base'
  'qt5-svg'
)
makedepends=(
  'boost'
  'cmake'
  'git'
  'qt5-tools'
)
source=("git+git://sigrok.org/pulseview#tag=pulseview-$pkgver")
sha512sums=('c74329f4ae5486b70f757664f305b699bf9dc14a3d71e241f5c15ad12f6d95499443ad70f3a5155262324622a64b1fca7d34caaa49ca5b4366bee9b1dfed6b21')

prepare() {
  cd $pkgname
  git config user.name builduser
  git config user.email builduser@archlinux
  # Replace obsolete/deprecated Qt methods
  git cherry-pick ae726b70a7ada9a4be5808e00f0c951318479684
  # Fix broken build due to C++ template behind C linkage
  git cherry-pick ed643f0b4ac587204a5243451cda181ee1405d62
}

build() {
  cd $pkgname
  cmake -S . -B build \
  	-DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev \
  	-DCMAKE_CXX_FLAGS=-fext-numeric-literals \
  	-DDISABLE_WERROR=ON \
    -DENABLE_TESTS=ON
  cmake --build build
}

check() {
  cd $pkgname
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" cmake --install build
}
