# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Artem Vorotnikov <artem at vorotnikov dot me>

_gemname=addressable
pkgname="ruby-${_gemname}"
pkgver=2.8.8
pkgrel=1
pkgdesc='URI Implementation'
arch=('any')
url='https://github.com/sporkmonger/addressable'
license=('Apache')
depends=(
  ruby
  ruby-public_suffix
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bigdecimal
  ruby-bundler
  ruby-launchy
  ruby-memory_profiler
  ruby-rake
  ruby-rspec
  ruby-rspec-core
  ruby-rspec-its
  ruby-yard
)
options=('!emptydirs')
source=(
  "${url}/archive/${_gemname}-${pkgver}/${pkgname}-${_gemname}-${pkgver}.tar.gz"
  # see https://github.com/sporkmonger/addressable/pull/571
  "https://github.com/sporkmonger/addressable/commit/5fc1d931e7fdc79d878f240fa4706b89cb01e171.patch"
  "https://github.com/sporkmonger/addressable/commit/c4f92fc45a018268c6a204aa0b13017d1e0f49ec.patch"
)
sha512sums=('e2526edd93cc19faf6aefcbf45177f9c35964b627b90cc1adccc8580fa46da8fe9ab9476e0e162fcc3873544835bcd79a2d01f6a46ff43603eea57c7c8310fb6'
            '2dd8ba24452e03ef43d96dae0013bbdb09018e8c8aa99bc4db280d2e339ff3ec7a4d587e325678554d5cf9a66e36210f962359365c6ae4a0f901d9e007d4b6e5'
            'ccc15554439825a4dd7fcb019cbf427954141b95c40cc54f664478cabae7e880dc2ea7cadb670f51dc06f4a4b6962701984abeb0ebc876119011dda423a89c5d')
b2sums=('4a19cccd7f953dadd7bf675473fe12c11ed1ae7a3e088498e197eb41736045889450ad995e58768a2d98c1b1a9f690a1b3c0215f923ef1e43affca6ba6ee9016'
        '52aa05e754d5cdf970e7491c3b6c1d7badf0b816cca18dd47c27d888332444716bed808e7c44f980fc3f53d4d671f65337c36b5a5cdbdaa8f057d27bab53e0c8'
        '7456c1c4fb2f00139d6d4896b67be5721fdb58239a824c3a65cbf34a82c76993b74b5f10c6a5476a8c9b810acee1203212d0add844d255d61554390108614463')

prepare() {
  cd ${_gemname}-${_gemname}-${pkgver}

  patch --verbose --strip=1 --input="../5fc1d931e7fdc79d878f240fa4706b89cb01e171.patch"
  patch --verbose --strip=1 --input="../c4f92fc45a018268c6a204aa0b13017d1e0f49ec.patch"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec" Gemfile

  # fix gemspec
  sed --in-place --regexp-extended 's|"data/unicode.data".freeze, ||g' "${_gemname}.gemspec"

  # ignore coverage
  sed --in-place --regexp-extended \
    --expression '/group :coverage/,/end/d' \
    Gemfile
  sed --in-place --regexp-extended \
    --expression '/begin/,/end if/d' \
    spec/spec_helper.rb
}

build() {
  cd "${_gemname}-${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  IDNA_MODE=pure GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "${_gemname}-${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
