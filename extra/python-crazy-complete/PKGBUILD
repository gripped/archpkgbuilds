# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=python-crazy-complete
pkgver=0.3.6
pkgrel=2
pkgdesc="Generate shell auto completion files"
arch=('any')
url="https://github.com/crazy-complete/crazy-complete"
license=('GPL-3.0-only')
depends=(
    'python'
    'python-yaml'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
)
source=("https://github.com/crazy-complete/crazy-complete/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f566d2cafc18a247b67d18a605f2e4867cd053588fadcb9beb5faf23ba67d84ecabbd6f4075ab21fc8d5158d5e906e29759de18978d258ec71ba95a82d4a6b52')

build() {
    cd "crazy-complete-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "crazy-complete-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl

    install -dm755 "$pkgdir/usr/share/doc/$pkgname/examples"
    install -Dm644 *.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 examples/* -t "$pkgdir/usr/share/doc/$pkgname/examples"
}
