# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=python-crazy-complete
pkgver=0.3.5
pkgrel=1
pkgdesc="Generate shell auto completion files"
arch=('any')
url="https://github.com/crazy-complete/crazy-complete"
license=('GPL-3.0-only')
depends=(
    'python'
    'python-yaml'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
)
source=("https://github.com/crazy-complete/crazy-complete/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ae6b6945675851f1e05c2caa220294c1b716c51c4ce90ab37daff97c04b2dc64a556f4a2c003e9b5327f343f3e94264fcdaa05789ca25f61faaaefcb77479aac')

build() {
    cd "crazy-complete-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "crazy-complete-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl

    install -dm755 "$pkgdir/usr/share/doc/$pkgname/examples"
    install -Dm644 *.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 examples/* -t "$pkgdir/usr/share/doc/$pkgname/examples"
}
