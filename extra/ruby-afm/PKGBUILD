# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Dmitry Kharitonov <arch[at]nano-model[dot]com>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=ruby-afm
_pkgname=${pkgname#ruby-}
pkgver=0.2.2
pkgrel=6
pkgdesc="A simple library to read Adobe Font Metrics (afm) files"
arch=('any')
url='http://github.com/halfbyte/afm'
license=(MIT)
depends=(ruby)
makedepends=(rubygems)
checkdepends=(
  ruby-bundler
  ruby-minitest
  ruby-rake
  ruby-rdoc
)
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('706379849a70cb8406b32e40508cfa0b8c4b60fe7893f00f81fd9876e843e37c')

prepare() {
  cd "$_pkgname-$pkgver"
  sed -i -E 's|~>|>=|g' "$_pkgname.gemspec"
}

build() {
  cd "$_pkgname-$pkgver"
  local gemdir="$(gem env gemdir)"

  gem build "$_pkgname.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "$_pkgname-$pkgver.gem"

  rm --force --recursive --verbose \
    "tmp_install/$gemdir/cache/" \
    "tmp_install/$gemdir/gems/$_pkgname-$pkgver/vendor/" \
    "tmp_install/$gemdir/doc/$_pkgname-$pkgver/ri/ext/"

  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
    -iname "*.o" -o \
    -iname "*.c" -o \
    -iname "*.so" -o \
    -iname "*.time" -o \
    -iname "gem.build_complete" -o \
    -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/$gemdir/extensions/" \
    -type f \
    \( \
    -iname "mkmf.log" -o \
    -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "$_pkgname-$pkgver"
  GEM_HOME="tmp_install/$(gem env gemdir)" rake test
}

package() {
  cd "$_pkgname-$pkgver"
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
