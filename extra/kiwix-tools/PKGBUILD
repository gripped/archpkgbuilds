# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=kiwix-tools
pkgver=3.8.0
pkgrel=1
pkgdesc="kiwix command line tools"
license=('GPL-3.0-or-later')
arch=('armv7h' 'i686' 'x86_64')
depends=('libkiwix>=14.1.0' 'docopt')
makedepends=('meson')
url='https://github.com/kiwix/kiwix-tools'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('1c7495b30b58826629d1bad647084ed84f0d03687fe45c5ee6ab2129c8307190')

build() {
	cd "${pkgname}-${pkgver}"
	arch-meson build
	ninja -C build
}

check() {
	cd "${pkgname}-${pkgver}/build"
	meson test
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
