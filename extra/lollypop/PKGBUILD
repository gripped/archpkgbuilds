# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=1.4.45
pkgrel=2
pkgdesc='Music player for GNOME'
arch=(any)
url=https://gitlab.gnome.org/World/lollypop
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gst-python
  gstreamer
  gtk3
  hicolor-icon-theme
  libgoa
  libhandy
  libsecret
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-cairo
  python-gobject
  python-pillow
  totem-plparser
)
makedepends=(
  appstream
  git
  meson
)
optdepends=(
  'gst-libav: FFmpeg plugin for GStreamer'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
  'kid3-common: Store covers in tags'
  'yt-dlp: YouTube support'
)
_commit=f97055c225027305514aceeb517738ebdff109a3
source=(git+https://gitlab.gnome.org/World/lollypop.git#commit=${_commit}
        git+https://gitlab.gnome.org/gnumdk/lollypop-po)
sha256sums=('e6e0b8d11af55a7876fc5c68e8019e08518b5486001d2b3ff40c2f4753d4b2fa'
            'SKIP')

prepare() {
  cd lollypop

  git submodule init
  git config submodule.subprojects/po.url "${srcdir}/lollypop-po"
  git -c protocol.file.allow=always submodule update
}

pkgver() {
  cd lollypop
  git describe --tags
}

build() {
  arch-meson lollypop build \
    --libexecdir=lib/lollypop
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"

  python -m compileall -d /usr/lib "${pkgdir}/usr/lib"
  python -O -m compileall -d /usr/lib "${pkgdir}/usr/lib"
}

# vim: ts=2 sw=2 et:
