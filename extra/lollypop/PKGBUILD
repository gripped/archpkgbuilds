# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=1.4.40
pkgrel=4
pkgdesc='Music player for GNOME'
arch=(any)
url=https://gitlab.gnome.org/World/lollypop
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gst-python
  gstreamer
  gtk3
  hicolor-icon-theme
  libgoa
  libhandy
  libsecret
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-cairo
  python-gobject
  python-pillow
  totem-plparser
)
makedepends=(
  appstream-glib
  git
  meson
)
optdepends=(
  'gst-libav: FFmpeg plugin for GStreamer'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
  'kid3-common: Store covers in tags'
  'yt-dlp: YouTube support'
)
source=(git+https://gitlab.gnome.org/World/lollypop.git#tag=${pkgver}
        git+https://gitlab.gnome.org/gnumdk/lollypop-po)
sha256sums=('d9b5847d1c9f32a7aff7f6d045524d87ae9ec5f32538ac82577d4b6a08c70dee'
            'SKIP')

prepare() {
  cd lollypop

  git submodule init
  git config submodule.subprojects/po.url "${srcdir}/lollypop-po"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson lollypop build \
    --libexecdir=lib/lollypop
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"

  python -m compileall -d /usr/lib "${pkgdir}/usr/lib"
  python -O -m compileall -d /usr/lib "${pkgdir}/usr/lib"
}

# vim: ts=2 sw=2 et:
