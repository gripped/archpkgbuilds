# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=1.4.44
pkgrel=1
pkgdesc='Music player for GNOME'
arch=(any)
url=https://gitlab.gnome.org/World/lollypop
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gst-python
  gstreamer
  gtk3
  hicolor-icon-theme
  libgoa
  libhandy
  libsecret
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-cairo
  python-gobject
  python-pillow
  totem-plparser
)
makedepends=(
  appstream
  git
  meson
)
optdepends=(
  'gst-libav: FFmpeg plugin for GStreamer'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
  'kid3-common: Store covers in tags'
  'yt-dlp: YouTube support'
)
_commit=de7330cde7f230ae757f1a842f11e020be17cb69
source=(git+https://gitlab.gnome.org/World/lollypop.git#commit=${_commit}
        git+https://gitlab.gnome.org/gnumdk/lollypop-po)
sha256sums=('128464b1b8201380187a9848fb1218bfa779efc39388a5161f678b24d831fb43'
            'SKIP')

prepare() {
  cd lollypop

  git submodule init
  git config submodule.subprojects/po.url "${srcdir}/lollypop-po"
  git -c protocol.file.allow=always submodule update

  # Fixes for the search provider
  # https://gitlab.gnome.org/World/lollypop/-/merge_requests/1451
  git cherry-pick -n 2052dd9a960bb6d9d439e632ac4a7eaeae5e1cd4
  # https://gitlab.gnome.org/World/lollypop/-/merge_requests/1452
  git cherry-pick -n a8d23bd73e89af2c0048c588799e7677a6a0c966

  # Always present window on launch
  # https://gitlab.gnome.org/World/lollypop/-/merge_requests/1454
  git cherry-pick -n cab670bab2e60d995dbedb2e8fbf6ddc9775e270
}

pkgver() {
  cd lollypop
  git describe --tags
}

build() {
  arch-meson lollypop build \
    --libexecdir=lib/lollypop
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"

  python -m compileall -d /usr/lib "${pkgdir}/usr/lib"
  python -O -m compileall -d /usr/lib "${pkgdir}/usr/lib"
}

# vim: ts=2 sw=2 et:
