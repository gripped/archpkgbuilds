# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=1.4.42
pkgrel=2
pkgdesc='Music player for GNOME'
arch=(any)
url=https://gitlab.gnome.org/World/lollypop
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gst-python
  gstreamer
  gtk3
  hicolor-icon-theme
  libgoa
  libhandy
  libsecret
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-cairo
  python-gobject
  python-pillow
  totem-plparser
)
makedepends=(
  appstream
  git
  meson
)
optdepends=(
  'gst-libav: FFmpeg plugin for GStreamer'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
  'kid3-common: Store covers in tags'
  'yt-dlp: YouTube support'
)
_commit=3453047d7024ab68f659cd7a108866b0cf534129
source=(git+https://gitlab.gnome.org/World/lollypop.git#commit=${_commit}
        git+https://gitlab.gnome.org/gnumdk/lollypop-po
        lollypop-search-launch.patch
        lollypop-search-timeout.patch
        lollypop-missing-icons.patch
        lollypop-present-window.patch)
sha256sums=('c9e120a7fbda0b4c89dba80df23ef6f83b198fa62cc90b14cee7bc743effede4'
            'SKIP'
            '887e5df8d87389991d2788493ebbbb8485c996eb2cad49360ac57ddc1c7410e9'
            '6485e98162876824678b9c4ad99baed37c395ac318d915085745902bc21c22dc'
            '637bea0c4eb779ed00060c3c3dc65838c975f4b5653f0830c1925e75761a9b55'
            '7e2b71cdd2d54b3f6b467d0e9fd8627415ebd38d042ddfd6725b60c7b1a1647c')

prepare() {
  cd lollypop

  git submodule init
  git config submodule.subprojects/po.url "${srcdir}/lollypop-po"
  git -c protocol.file.allow=always submodule update

  # Fixes for the search provider
  # https://gitlab.gnome.org/World/lollypop/-/merge_requests/1451
  git apply -3 ../lollypop-search-launch.patch
  # https://gitlab.gnome.org/World/lollypop/-/merge_requests/1452
  git apply -3 ../lollypop-search-timeout.patch

  # Replace emblem icons removed from Adwaita
  # https://gitlab.gnome.org/World/lollypop/-/merge_requests/1453
  git apply -3 ../lollypop-missing-icons.patch

  # Always present window on launch
  # https://gitlab.gnome.org/World/lollypop/-/merge_requests/1454
  git apply -3 ../lollypop-present-window.patch
}

pkgver() {
  cd lollypop
  git describe --tags
}

build() {
  arch-meson lollypop build \
    --libexecdir=lib/lollypop
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"

  python -m compileall -d /usr/lib "${pkgdir}/usr/lib"
  python -O -m compileall -d /usr/lib "${pkgdir}/usr/lib"
}

# vim: ts=2 sw=2 et:
