# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=xaos
pkgver=4.3.1
pkgrel=4
pkgdesc='Interactive fractal zoomer'
arch=(x86_64)
url='https://xaos-project.github.io/'
license=(GPL-2.0-only)
makedepends=(gendesk git qt6-tools)
depends=(qt6-base)
source=("git+https://github.com/xaos-project/XaoS#tag=release-$pkgver")
b2sums=('7884d07d7b3a61812b2e4eeab29f48cff9041007239f9c234ac96ae59cc7378057f820d5b735b890225786fd2a180541edb4ee1fcc34f94543cf25c615310956')

prepare() {
  # Desktop shortcut
  gendesk -f -n \
    --categories='Application;Graphics' \
    --exec='xaos -threads $(nproc)' \
    --icon=xaos \
    --pkgdesc="$pkgdesc" \
    --pkgname=XaoS

  cd XaoS

  # Add missing include
  sed -i '30i#include <cstdlib>' src/util/timers.cpp

  # Look for resources in /usr instead of in /usr/local
  sed -i 's,usr/local,usr,g' XaoS.pro
}

build() {
  cd XaoS
  export DEFINES='USE_OPENGL USE_FLOAT128'
  /usr/lib/qt6/bin/qmake && make
}

package() {
  cd XaoS

  # Executable
  install -Dm755 bin/xaos "$pkgdir/usr/bin/xaos"

  # Additional resources
  install -d "$pkgdir/usr/share/XaoS/"
  cp -r catalogs tutorial i18n "$pkgdir/usr/share/XaoS"

  # Examples
  install -d "$pkgdir/usr/share/XaoS/examples"
  find examples -name "*.xpf" -exec install -Dm644 -t "$pkgdir"/usr/share/XaoS/examples/ {} \;

  # Desktop shortcut and icon
  install -Dm644 ../XaoS.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "src/ui/images/xaosbig.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Man page
  install -Dm644 doc/xaos.6 "$pkgdir/usr/share/man/man6/xaos.6"
}
