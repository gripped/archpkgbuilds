# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=25.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=(x86_64)
url="https://www.rosegardenmusic.com/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  lilv
  lirc
  hicolor-icon-theme
  qt5-base
  zlib
)
makedepends=(
  alsa-lib
  cmake
  dssi
  fftw
  git
  jack
  ladspa
  liblrdf
  liblo
  libsamplerate
  libsndfile
  qt5-tools)
checkdepends=(
  lilypond
  python
  tex-gyre-fonts
  ttf-dejavu
  xorg-server-xvfb
)
optdepends=(
  'cups: Printing support'
  'evince: Print preview support'
  'flac: FLAC support'
  'lilypond: Notation display support'
  'mupdf: Print preview support'
  'okular: Print preview support'
  'timidity++: Softsynth support'
  'wavpack: Lossless audio codec support'
)
provides=(
  dssi-host
  ladspa-host
  lv2-host
)
source=(
  $pkgname::git+https://git.code.sf.net/p/rosegarden/git#tag=$pkgver
)
sha512sums=('e99444d23fe5a381cc3f5f7ccceb3d5a4a6730afb422c1f4cd5283c73a22d8f9d34a05917afb099fbb55373211055fd04702aa0e4dbd38d9b4ae5e5ffc3410c6')
b2sums=('a458faee418bbad19254e7f6bcab6fe8ea2905312ef29ce3ee740f43ab61af244ddc36138f5c86f34d1d0f4856db3c7b7263725c58cbfdbb10195381c1a9e3f5')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5
    -S $pkgname
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  mkdir -p test-tmp
  chmod 700 test-tmp
  export XDG_RUNTIME_DIR="$(pwd)/test-tmp"
  # disable broken lilypond_export_test: https://sourceforge.net/p/rosegarden/bugs/1640/
  # disable tests incompatible with debug symbols: https://sourceforge.net/p/rosegarden/bugs/1641/
  xvfb-run ctest --output-on-failure --test-dir build/test -E '^(lilypond_export_test|test_notationview_selection|convert)$'
}

package() {
  depends+=(
    alsa-lib libasound.so
    fftw libfftw3f.so
    jack libjack.so
    liblrdf liblrdf.so
    liblo liblo.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname/{AUTHORS,CHANGELOG,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
