# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=24.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=(x86_64)
url="https://www.rosegardenmusic.com/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  lirc
  hicolor-icon-theme
  qt5-base
  zlib
)
makedepends=(
  alsa-lib
  cmake
  dssi
  fftw
  jack
  ladspa
  liblrdf
  liblo
  libsamplerate
  libsndfile
  qt5-tools)
checkdepends=(
  lilypond
  python
  tex-gyre-fonts
  ttf-dejavu
  xorg-server-xvfb
)
optdepends=(
  'cups: Printing support'
  'evince: Print preview support'
  'flac: FLAC support'
  'lilypond: Notation display support'
  'mupdf: Print preview support'
  'okular: Print preview support'
  'timidity++: Softsynth support'
  'wavpack: Lossless audio codec support'
)
provides=(
  dssi-host
  ladspa-host
)
source=(
  https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz
)
sha512sums=('dd948e2f9bd0fe11249c1e138afc79da10043664c85ea300f9050dd01e959be50f1056d5035f89b8b1f62fb58254890f6f03384eda546f8968cd42baf1153555')
b2sums=('e8976ef1c46d4e137ad78f762cc979363e66427b47760e8ee5e5813e89fa68349d1cc541223c980b6e3972b2d55d33bc8cc2de77d61f95b903f897626be1571e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  mkdir -p test-tmp
  chmod 700 test-tmp
  export XDG_RUNTIME_DIR="$(pwd)/test-tmp"
  # disable broken lilypond_export_test: https://sourceforge.net/p/rosegarden/bugs/1640/
  # disable tests incompatible with debug symbols: https://sourceforge.net/p/rosegarden/bugs/1641/
  xvfb-run ctest --output-on-failure --test-dir build/test -E '^(lilypond_export_test|test_notationview_selection|convert)$'
}

package() {
  depends+=(
    alsa-lib libasound.so
    fftw libfftw3f.so
    jack libjack.so
    liblrdf liblrdf.so
    liblo liblo.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,CHANGELOG,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
