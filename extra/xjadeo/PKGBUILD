# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.14
pkgrel=4
pkgdesc="A simple video player that is synchronized to jack transport"
arch=(x86_64)
url="https://github.com/x42/xjadeo"
license=(GPL-2.0-or-later)
depends=(
  glibc
  imlib2
  libx11
  libxext
  libxpm
  libxv
)
makedepends=(
  alsa-lib
  ffmpeg
  gendesk
  git
  glu
  jack
  libglvnd
  liblo
  libltc
)
backup=(etc/xjadeorc)
source=("$url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz"
        "$url"/commit/0023d0c6deb245870d8e0cce01c40c28f9593a9b.patch)
sha512sums=('0f7f694a91e097b4129b755c7c38af889ddf6c21d49ec1bd6f700dc2bfe8224e37449494418c20066d56ba8ab2849499a8904218f2d4780ea3676d2177f10b64'
            '0b5f7f93a1f9a7d44522f2b24a7511ba2236471de423a7295c4b298a870e660f184ea6f5e7f615ef8abdbe7bc90d69b7f5edc0408fd206a505ba89f7a90de310')
b2sums=('bdeb217200d2ea48d6a79a5c583b9511d2a32dfc7eaa958ecd39eb4381b2876092ea9fbad316a259abdf28acc265c566e5881330853008bf12ed355a8b7d0e83'
        '333b12835653fcf8225e22b8ae026ce96688ad24e14c3ba136f9e83fa45a4b9d6c7b6c50a236e2a1e3002836681c076e4e5a66888c7a834818022121a2a85715')

prepare() {
  cd $pkgname-$pkgver
  gendesk -n \
          --pkgname $pkgname \
          --pkgdesc "$pkgdesc" \
          --categories "AudioVideo;Audio"
  autoreconf -fiv

  # Build fix for ffmpeg 8
  patch -Np1 -i ../0023d0c6deb245870d8e0cce01c40c28f9593a9b.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  depends+=(
    alsa-lib libasound.so
    ffmpeg libavcodec.so libavformat.so libavutil.so libswscale.so
    freetype2 libfreetype.so
    jack libjack.so
    libglvnd libGL.so
    liblo liblo.so
    libltc libltc.so
  )

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # config
  install -vDm 644 doc/xjadeorc -t "$pkgdir/etc/"
  # desktop file
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # icon
  install -vDm 644 src/$pkgname/icons/xjadeoH128.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
