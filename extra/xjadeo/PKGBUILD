# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.15
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport"
arch=(x86_64)
url="https://github.com/x42/xjadeo"
license=(GPL-2.0-or-later)
depends=(
  glibc
  imlib2
  libx11
  libxext
  libxpm
  libxv
)
makedepends=(
  alsa-lib
  ffmpeg
  gendesk
  git
  glu
  jack
  libglvnd
  liblo
  libltc
)
backup=(etc/xjadeorc)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('9872e75d519131b2ff5b72ffc675d7f9c590ecc0b3f6f7efe9aaaccf21ba72ac39ebb9db3987e46f345bacfe044d2f0e8b08488b94a5216a99b54d6b8c9978ca')
b2sums=('20b0fb8bc0df57081aa702fd1a329d0e25abb470f5435bf525ecc047f6e0215a4e11c1d0a85e98f5d790d47d4e4e5b0a0e11993eeb6d75e65280067b83d4f9dd')

prepare() {
  cd $pkgname-$pkgver
  gendesk -n \
          --pkgname $pkgname \
          --pkgdesc "$pkgdesc" \
          --categories "AudioVideo;Audio"
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  depends+=(
    alsa-lib libasound.so
    ffmpeg libavcodec.so libavformat.so libavutil.so libswscale.so
    freetype2 libfreetype.so
    jack libjack.so
    libglvnd libGL.so
    liblo liblo.so
    libltc libltc.so
  )

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # config
  install -vDm 644 doc/xjadeorc -t "$pkgdir/etc/"
  # desktop file
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # icon
  install -vDm 644 src/$pkgname/icons/xjadeoH128.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
