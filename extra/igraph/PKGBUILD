# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.10.15
pkgrel=2
pkgdesc='A library for creating and manipulating (un)directed graphs'
arch=(x86_64)
url='https://igraph.org/c/'
license=(GPL-2.0-only)
depends=(arpack
         blas
         gcc-libs
         glibc
         glpk
         gmp
         lapack
         libxml2
         plfit)
makedepends=(cmake
             git)
source=(git+https://github.com/igraph/igraph#tag=$pkgver)
sha256sums=('6417ca3ac6a889065f1655a5d0d7c82bc7e64dfd1d06eef715e21586fea00504')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
