# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bence Balint <balintbence1337@gmail.com>

pkgname=python-w3lib
pkgver=2.2.0
pkgrel=2
pkgdesc="Library of web-related functions"
arch=('any')
license=('BSD-3-Clause')
url="https://github.com/scrapy/w3lib"
depends=('python')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
source=("git+https://github.com/scrapy/w3lib.git#tag=v$pkgver")
sha512sums=('cf5b4597c53c9003bf9e7ffb5f4412adf624aba59f984b56e99a6fed47b7619f5ad4c39ba45aa62d396fa79bb1360aa3fa646641a9052d21a6bd4b53815da851')

build() {
  cd w3lib
  python setup.py build
}

check() {
  cd w3lib
  pytest
}

package() {
  cd w3lib
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
