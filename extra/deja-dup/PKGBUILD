# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=48.4
pkgrel=1
pkgdesc='Simple backup tool, GTK frontend for duplicity'
arch=(x86_64)
url='https://apps.gnome.org/DejaDup/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  duplicity
  glib2
  glibc
  gtk4
  gvfs
  hicolor-icon-theme
  json-glib
  libadwaita
  libgpg-error
  libsecret
  libsoup3
  pango
)
makedepends=(
  appstream
  git
  itstool
  meson
  vala
)
checkdepends=(xdg-user-dirs)
optdepends=(
  'rclone: Backup into Google Drive or Microsoft OneDrive'
  'restic: Alternative backend, experimental'
)
source=("git+https://gitlab.gnome.org/World/$pkgname.git#tag=$pkgver")
b2sums=(4d487bcd507e422b54f09acf76fb2042e683741b00b0c11cc58904ff94b320e613e22558d6ae7f4059f849b3396d4f1accc83b61edf360b8fa573f42868d3c59)
validpgpkeys=(A3A5C2FC56AE7341D308D8571B50ECA373F3F233) # Michael Terry <mike@mterry.name>

build() {
  arch-meson $pkgname build \
    -D enable_restic=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
