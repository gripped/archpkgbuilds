# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=49.1
pkgrel=1
pkgdesc='Easy to use backup tool based on Restic'
arch=(x86_64)
url='https://apps.gnome.org/DejaDup/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  bash
  dconf
  duplicity
  fuse3
  glib2
  glibc
  gtk4
  gvfs
  hicolor-icon-theme
  json-glib
  libadwaita
  libsecret
  libsoup3
  pango
  restic
)
makedepends=(
  appstream
  blueprint-compiler
  git
  itstool
  meson
  vala
)
checkdepends=(xdg-user-dirs)
optdepends=('rclone: Backup into Google Drive or Microsoft OneDrive')
source=("git+https://gitlab.gnome.org/World/$pkgname.git#tag=$pkgver")
b2sums=(96bcbe37db79fe893488261c3a941ca1e1f48a6d96fc47d9c0c955e2a45ad251b95fa92643a81c4f5ab1a460826b3fdb0291ea725f8000c5246c3c6a2dbe4373)
validpgpkeys=(A3A5C2FC56AE7341D308D8571B50ECA373F3F233) # Michael Terry <mike@mterry.name>

build() {
  arch-meson $pkgname build \
    -D packagekit=disabled
  meson compile -C build
}

check() {
  dbus-run-session \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
