# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=47.0
pkgrel=1
pkgdesc='Simple backup tool, GTK frontend for duplicity'
arch=(x86_64)
url='https://apps.gnome.org/DejaDup/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  duplicity
  glib2
  glibc
  gtk4
  gvfs
  hicolor-icon-theme
  json-glib
  libadwaita
  libgpg-error
  libsecret
  libsoup3
  pango
)
makedepends=(
  appstream
  git
  itstool
  meson
  vala
)
checkdepends=(xdg-user-dirs)
optdepends=(
  'rclone: Backup into Google Drive or Microsoft OneDrive'
  'restic: Alternative backend, experimental'
)
source=("git+https://gitlab.gnome.org/World/$pkgname.git#tag=$pkgver")
b2sums=(fc661bd5dc21c16ae087e967699913dfa0c85b4ab26c8439e19f86e622f1d42d50396ce7d9babbe22888ce1c20f9c15cfd1d1b49748f8348711ff8624e77ff79)
validpgpkeys=(A3A5C2FC56AE7341D308D8571B50ECA373F3F233) # Michael Terry <mike@mterry.name>

build() {
  arch-meson $pkgname build \
    -D enable_restic=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
