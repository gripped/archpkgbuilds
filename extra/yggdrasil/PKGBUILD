# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=yggdrasil
pkgver=0.5.2
pkgrel=1
pkgdesc="An experiment in scalable routing as an encrypted IPv6 overlay network"
arch=('x86_64')
url="https://github.com/yggdrasil-network/yggdrasil-go"
license=('LGPL3')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/yggdrasil-network/yggdrasil-go/archive/v${pkgver}.tar.gz"
        'yggdrasil.sysusers')
sha512sums=('a1bd4606d8d3d83f691ab04cdd7316a9e391f44aaddc4d67d7249623e6de02ab55ec670d7bb6885702e9e12de0f526eafae9ec36ca116b365f8aea4065345271'
            'b78d1f5efeeba184588ba7bdb2249d976aec160daa59742e032983da1aedad062d15c7c97cba3eba69412a0f7904ee123d98b58f859892d71188c25624295c32')
b2sums=('b16483bffa7d9112d699d64e150b02bd3cac5a493d831daeb90a054d8baee284163ec50cf710a8e1a2af012900184e8e869ae35b66c29be1b8b884f7c968ca13'
        '3cf5bfb3c09908f841b39f7206bb472e50e60f10d1b87ab6def97a7212e27627f1e795bbaffd964d2d27a80fbbd1a3be63d63c59793ec1967c02f48b8462c08b')

build() {
  cd "${pkgname}-go-${pkgver}"
  for bin in yggdrasil yggdrasilctl; do
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"
-X github.com/yggdrasil-network/yggdrasil-go/src/version.buildName=yggdrasil
-X github.com/yggdrasil-network/yggdrasil-go/src/version.buildVersion=${pkgver}
" \
      "./cmd/${bin}"
  done
}

package() {
  cd "${pkgname}-go-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    yggdrasil \
    yggdrasilctl

  install -Dm644 -t "${pkgdir}/usr/lib/systemd/system" \
    contrib/systemd/yggdrasil.service \
    contrib/systemd/yggdrasil-default-config.service
  install -Dm644 "${srcdir}/yggdrasil.sysusers" "${pkgdir}/usr/lib/sysusers.d/yggdrasil.conf"

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
