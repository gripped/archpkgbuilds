# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=yggdrasil
pkgver=0.5.11
pkgrel=1
pkgdesc="An experiment in scalable routing as an encrypted IPv6 overlay network"
arch=('x86_64')
url="https://github.com/yggdrasil-network/yggdrasil-go"
license=('LGPL-3.0-only')
depends=(
  'glibc'
)
makedepends=(
  'go'
)
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/yggdrasil-network/yggdrasil-go/archive/v${pkgver}.tar.gz"
        'yggdrasil.sysusers')
sha256sums=('1fdff989e7e7278fda9388fed8cd6245b6c97a17b677cbccd322875bbb26a28d'
            '233f436a5b4ff62f3da66f871ae9b49ed2b4a13572a83f057a07232627c4b3fd')
b2sums=('46ea4222fd0b491a7247a473b661aa3cd5f5738b8bbf50466bcfba24a9e333613993209c4ac80ab57e1aa26a3a623a08f14f9d59533e6ec16b1383bbb9dfd15b'
        '3cf5bfb3c09908f841b39f7206bb472e50e60f10d1b87ab6def97a7212e27627f1e795bbaffd964d2d27a80fbbd1a3be63d63c59793ec1967c02f48b8462c08b')

build() {
  cd "${pkgname}-go-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  for bin in yggdrasil yggdrasilctl; do
    go build \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-compressdwarf=false -linkmode=external
-X github.com/yggdrasil-network/yggdrasil-go/src/version.buildName=yggdrasil
-X github.com/yggdrasil-network/yggdrasil-go/src/version.buildVersion=${pkgver}
" \
      "./cmd/${bin}"
  done
}

package() {
  cd "${pkgname}-go-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    yggdrasil \
    yggdrasilctl

  install -Dm644 -t "${pkgdir}/usr/lib/systemd/system" \
    contrib/systemd/yggdrasil.service \
    contrib/systemd/yggdrasil-default-config.service
  install -Dm644 "${srcdir}/yggdrasil.sysusers" "${pkgdir}/usr/lib/sysusers.d/yggdrasil.conf"

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
