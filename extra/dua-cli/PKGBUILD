# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: mexus <gilaldpellaeon@gmail.com>
pkgname=dua-cli
pkgver=2.24.0
pkgrel=1
pkgdesc="A tool to conveniently learn about the disk usage of directories, fast!"
arch=('x86_64')
url="https://github.com/Byron/dua-cli"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Byron/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('04f494de2e69f1eb89c168100da983712cc21d1a6a3ba179655d40e76aefb37e406990ac10d072da19794a3d67f62f837c1067d717b6630ce413f935b81d99dc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 755 "target/release/dua" "$pkgdir/usr/bin/dua"
}
