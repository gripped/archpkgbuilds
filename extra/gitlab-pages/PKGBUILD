# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Melvin Vermeeren <mail@mel.vin>
# Contributor: Martin Rys <martin@rys.pw>

# GitLab Pages without DNS wildcard
# https://gitlab.com/gitlab-org/gitlab/issues/17584

pkgname=gitlab-pages
pkgver=17.0.2
pkgrel=1
pkgdesc='GitLab Pages daemon used to serve static websites for GitLab users'
url="https://gitlab.com/gitlab-org/$pkgname"
license=(MIT)
arch=(x86_64 i686)
makedepends=(go)
_archive="$pkgname-v$pkgver"
source=("$url/-/archive/v$pkgver/$_archive.tar.bz2"
        "$pkgname.service"
        "$pkgname.env")
backup=("etc/$pkgname/config.cfg"
        "etc/$pkgname/service.env")
sha256sums=('86ef71741ad2d7b0c67ff5cb27ff7f5308f644e3628b4ecf9f7fb5bdea46c3b0'
            'fdbb6323cad16ed14571ac7c6346e388afa67db87f09732dd4f11b721d25a21d'
            'fd8f9b60e2247077ad00765904237b6b1c36b11a952cd3b1ad88e74417b82a96')

prepare() {
	cd "$_archive"
	cp ../$pkgname.env service.env
}

build() {
	cd "$_archive"
	export GOPATH="$srcdir"
	export CGO_ENABLED=0
	go build \
		-trimpath \
		-buildmode=exe \
		-mod=readonly \
		-modcacherw \
		-ldflags "-extldflags \"$LDFLAGS\" -X main.VERSION=$pkgver" \
		.
	./$pkgname -help 2>&1 |
		sed -ne '/=/{s/^  -//;s/\([^:]*\): \(.*\)/# \2\n#\1\n/;/config=/d;s/=""/=string/;p}' |
		sed -e '/daemon-.id=/{s/^#//;s/=0/=105/}' \
		> config.cfg
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "$pkgname"
	install -Dm0644 -t "$pkgdir/etc/$pkgname/" config.cfg service.env
	install -Dm0644 -t "$pkgdir/usr/lib/systemd/system/" "../$pkgname.service"
}
