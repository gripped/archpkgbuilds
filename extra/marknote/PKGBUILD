# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=marknote
pkgver=1.2.0
pkgrel=1
pkgdesc='A simple markdown note management app'
url='https://apps.kde.org/marknote/'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kirigami
         kirigami-addons
         kxmlgui
         md4c
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('03ffcd6c151626aae1b89269c3a926473a4427d7df40b68fa7d13fe7ad4dd9ef'
            'SKIP')
validpgpkeys=(39FFA93CAE9C6AFC212AD00202325448204E452A) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
