# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Contributor: VuDu <vudu.curse@gmail.com>

pkgname=nfoview
pkgver=2.1
pkgrel=1
pkgdesc="Simple NFO file viewer in GTK+ 4"
url="https://otsaloma.io/nfoview/"
arch=('any')
license=(GPL-3.0-or-later)
depends=('gobject-introspection-runtime' 'gtk4' 'hicolor-icon-theme' 'pango' 'python' 'python-cairo' 'python-gobject')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'xorg-server-xvfb')
optdepends=('ttf-cascadia-code: recommended font')
source=("git+https://github.com/otsaloma/${pkgname}.git?signed#tag=${pkgver}")
sha256sums=('0c86bb5834d4ade0868f55993493e3133337408bd2f23c9116a3dd63caef97e9')
validpgpkeys=('8DD6B3FCDAB33C91EA915B913A584A9504CACA67') # Osmo Salomaa <otsaloma@iki.fi>

build() {
	cd "${pkgname}"
	make PREFIX=/usr build
}

check() {
	cd "${pkgname}"
	xvfb-run pytest -v "${pkgname}"
}

package() {
	cd "${pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm 644 {AUTHORS,NEWS,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
