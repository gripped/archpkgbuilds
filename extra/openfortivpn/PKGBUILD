# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=openfortivpn
pkgver=1.22.0
pkgrel=1
pkgdesc="An open implementation of Fortinet's proprietary PPP+SSL VPN solution"
arch=(x86_64)
url=https://github.com/adrienverge/openfortivpn
license=(GPL3)
depends=(
  glibc
  openssl
  ppp
  resolvconf
  systemd-libs
)
makedepends=(
  git
  systemd
)
backup=(etc/openfortivpn/config)
_tag=82e4401162fc65efad8267f4cb3287c6ab6fc088
source=(git+https://github.com/adrienverge/openfortivpn.git#tag=${_tag})
sha256sums=('83374eea10955b1d54c2e8a34223d037bb4dac4bc956a47f81e4de58b0714110')

prepare() {
  cd openfortivpn
  autoreconf -fiv
}

pkgver() {
  cd openfortivpn
  git describe --tags | sed 's/^v//'
}

build() {
  cd openfortivpn
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-resolvconf
  make
}

package() {
  make DESTDIR="${pkgdir}" -C openfortivpn install
}

# vim: ts=2 sw=2 et:
