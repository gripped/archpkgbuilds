# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=openfortivpn
pkgver=1.23.1
pkgrel=1
pkgdesc="An open implementation of Fortinet's proprietary PPP+SSL VPN solution"
arch=(x86_64)
url=https://github.com/adrienverge/openfortivpn
license=(GPL-3.0-only)
depends=(
  glibc
  openssl
  ppp
  resolvconf
  systemd-libs
)
makedepends=(
  git
  systemd
)
backup=(etc/openfortivpn/config)
_tag=a011b5c916075bd240db6a03c02eac95baf09ad8
source=(git+https://github.com/adrienverge/openfortivpn.git#tag=${_tag})
sha256sums=('3ca7f038d7a4b8a6199adf8bc92de03dc557461a0ebc5921e3e52572259a1ca3')

prepare() {
  cd openfortivpn
  autoreconf -fiv
}

pkgver() {
  cd openfortivpn
  git describe --tags | sed 's/^v//'
}

build() {
  cd openfortivpn
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-resolvconf
  make
}

package() {
  make DESTDIR="${pkgdir}" -C openfortivpn install
}

# vim: ts=2 sw=2 et:
