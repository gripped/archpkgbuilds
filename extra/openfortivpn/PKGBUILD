# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=openfortivpn
pkgver=1.24.0
pkgrel=1
pkgdesc="An open implementation of Fortinet's proprietary PPP+SSL VPN solution"
arch=(x86_64)
url=https://github.com/adrienverge/openfortivpn
license=(GPL-3.0-only)
depends=(
  glibc
  openssl
  ppp
  resolvconf
  systemd-libs
)
makedepends=(
  git
  systemd
)
backup=(etc/openfortivpn/config)
_tag=9230413f47de40fbf0dc8ba18eff42e943e27ca7
source=(git+https://github.com/adrienverge/openfortivpn.git#tag=${_tag})
sha256sums=('6969bd60a301b8689d10a30be3b1f90f61b34d244e11064112a8026f37ef8fc6')

prepare() {
  cd openfortivpn
  autoreconf -fiv
}

pkgver() {
  cd openfortivpn
  git describe --tags | sed 's/^v//'
}

build() {
  cd openfortivpn
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-resolvconf
  make
}

package() {
  make DESTDIR="${pkgdir}" -C openfortivpn install
}

# vim: ts=2 sw=2 et:
