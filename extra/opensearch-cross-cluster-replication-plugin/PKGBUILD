# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-cross-cluster-replication-plugin
pkgver=3.4.0.0
pkgrel=1
_opensearchver=3.4.0
_jdkver=21
pkgdesc="OpenSearch Cross-Cluster Replication Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/replication-plugin"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/cross-cluster-replication/archive/${pkgver}.tar.gz"
)
sha256sums=('c5be7555f1c13ad8d42a59d941faa3101b1eedef4edcf359bddb37db98eb351d')

build() {
  cd "cross-cluster-replication-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-cross-cluster-replication"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-cross-cluster-replication"
  unzip "${srcdir}/cross-cluster-replication-${pkgver}/build/distributions/opensearch-cross-cluster-replication-${pkgver}.zip"

  install -Dm644 "${srcdir}/cross-cluster-replication-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
