# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parsel
pkgver=1.8.0
pkgrel=1
pkgdesc="Library to extract data from HTML and XML using XPath and CSS selectors"
arch=('any')
license=('BSD')
url="https://github.com/scrapy/parsel"
depends=('python-cssselect' 'python-jmespath' 'python-lxml' 'python-packaging' 'python-w3lib')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil' 'python-psutil')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/parsel/archive/v$pkgver.tar.gz")
sha512sums=('d6ca0eaf05e7127514c7dd13a50e2c0b1aed5f99b6e1778f242bc06326c8d8908ad563166ef389c6125b772325aabe2431636cb4bb82d713207c50f9ca42ab89')

prepare() {
  cd parsel-$pkgver
  sed -i '/pytest-runner/d' setup.py
}

build() {
  cd parsel-$pkgver
  python setup.py build
}

check() {
  cd parsel-$pkgver
  python -m pytest
}

package() {
  cd parsel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
