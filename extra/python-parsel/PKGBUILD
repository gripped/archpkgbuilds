# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parsel
pkgver=1.10.0
pkgrel=1
pkgdesc="Library to extract data from HTML and XML using XPath and CSS selectors"
arch=('any')
license=('BSD-3-Clause')
url="https://github.com/scrapy/parsel"
depends=('python-cssselect' 'python-jmespath' 'python-lxml' 'python-packaging' 'python-w3lib')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-sybil' 'python-psutil')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/parsel/archive/v$pkgver.tar.gz")
sha512sums=('371552c3d364532c38390a97a44a39b6a2eea50c68ddac1e1e14bc884c351fd71e908ae75990f365453278873e7942c344b46ea2cf0638c5d6db17156ba62e66')

prepare() {
  cd parsel-$pkgver
}

build() {
  cd parsel-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd parsel-$pkgver
  python -m pytest
}

package() {
  cd parsel-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
