# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-security-plugin'
_pluginname='security-dashboards-plugin'
pkgver=3.4.0.0
_dashboardsver=3.4.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Security Plugin'
url='https://opensearch.org/docs/latest/security-plugin'
arch=('x86_64')
license=('Apache-2.0')
depends=("opensearch-dashboards=${_dashboardsver}")
makedepends=('yarn' 'npm' 'python' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/${_pluginname}.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
)
sha256sums=('e74406f4287ebcb8d2915e2eb4a17b70892c9b6534d1ff362dfbe8ae82327f61'
            'b99d6edf20a7f16fae2bc6bca2dc1a09392e35961df40921781c819f70b9f321')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" \
    "${_pluginname}/package.json"
  # match cypress version from plugin
  sed -i "s/    \"cypress\": \"12.17.4\",/    \"cypress\": \"^13.5.1\",/" \
    "OpenSearch-Dashboards/package.json"
  cd "OpenSearch-Dashboards"
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      patch -p1 -N -l -i "$srcdir/${filename##*/}"
    fi
  done
}

build() {
  mv "${_pluginname}" "OpenSearch-Dashboards/plugins"
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn osd bootstrap
  yarn plugin-helpers build --skip-archive # `yarn build` will always create the ZIP
}

check() {
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  #yarn test:jest_server # integration tests
  yarn test:jest_ui
}

package() {
  cd "${srcdir}/OpenSearch-Dashboards/plugins/${_pluginname}"

  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
