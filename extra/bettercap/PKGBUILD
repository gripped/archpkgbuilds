# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.41.3
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'glibc'
  'iproute2'
  'libnetfilter_queue'
  'libnfnetlink'
  'libpcap'
  'libpcap.so'
  'libusb'
  'libusb-1.0.so'
)
makedepends=(
  'go'
  'git'
)
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs' '!lto')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('621816cf254c0f51d65e5656e9a4181137543367ec84f6e7df948c04bc10ba7e40e6d9b2e1b7547b19fe953455985e6ed65d72a7229462a7cdc1f086d38ccb5f')
b2sums=('82773477f86e181f4a05ee0ca8b80cb8cb907d79ca0f831767a4f6e5bd1f02f4c8b0412b05c593ac7561c308efd2ae3f097a5d3ca662851f98cde632f61ca230')

prepare() {
  cd ${pkgname}-${pkgver}
  find . -type f -exec sed 's|/usr/local|/usr|g' -i {} +
}

build() {
  cd ${pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 bettercap.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
