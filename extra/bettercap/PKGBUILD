# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.41.0
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'glibc'
  'iproute2'
  'libnetfilter_queue'
  'libnfnetlink'
  'libpcap'
  'libpcap.so'
  'libusb'
  'libusb-1.0.so'
)
makedepends=(
  'go'
  'git'
)
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs' '!lto')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('af6cfd4fa2e08efb7c827c560fd997d34154beb345325ed31954e53e7b8f5aa0e384307f63a9f4fd86659837e92e4b465fd87138a3f022061fad2b35ede4a86c')
b2sums=('c735302867a55f17c489c7ca4f752895bd508b942fb84fb0d2c52e07d6c4108c6df2e409f60aa04b15db8412de0764bf8c0c80867ae624f1235d70fc334fc4c5')

prepare() {
  cd ${pkgname}-${pkgver}
  find . -type f -exec sed 's|/usr/local|/usr|g' -i {} +
}

build() {
  cd ${pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 bettercap.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
