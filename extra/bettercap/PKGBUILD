# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.41.4
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'glibc'
  'iproute2'
  'libnetfilter_queue'
  'libnfnetlink'
  'libpcap'
  'libpcap.so'
  'libusb'
  'libusb-1.0.so'
)
makedepends=(
  'go'
  'git'
)
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs' '!lto')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e575050a9a6a028f2485c5d147286b3ac99eefd566096636c858757c2f83fc19d0dc3cf61a37d4235b20e4f2e6fb07c9352c42e4e6168f61a5d3bf41ec1e6f80')
b2sums=('8f3b06648b634f0ff0a742ed9a36734134c112386ebcae97df04ab7c95ae27f35a88dd386c9c5fe99e688ca7cdf17e8b95d74bac327354acdfcda4465e94d5d1')

prepare() {
  cd ${pkgname}-${pkgver}
  find . -type f -exec sed 's|/usr/local|/usr|g' -i {} +
}

build() {
  cd ${pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 bettercap.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
