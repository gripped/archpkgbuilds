# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.41.5
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'glibc'
  'iproute2'
  'libnetfilter_queue'
  'libnfnetlink'
  'libpcap'
  'libpcap.so'
  'libusb'
  'libusb-1.0.so'
)
makedepends=(
  'go'
  'git'
)
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs' '!lto')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9df5dd582383bc14c886f1ba8de6f15ed6ff3998762cf116824f3479de8b78691d5a21b1a239e4b4eac837db254f2888bd96403ef4a63189fc23b237723403b5')
b2sums=('c6a5af2ecc3d2ac7c8fa860be26aa024d932486533e94463c7d1725945110a5d4359d8b5231e3767b20da2aa7bc5d13ebe69b356229eb9c98207baae617d8301')

prepare() {
  cd ${pkgname}-${pkgver}
  find . -type f -exec sed 's|/usr/local|/usr|g' -i {} +
}

build() {
  cd ${pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 bettercap.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
