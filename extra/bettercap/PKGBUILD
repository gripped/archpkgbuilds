# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.33.0
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'glibc'
  'iproute2'
  'libnetfilter_queue'
  'libnfnetlink'
  'libpcap'
  'libpcap.so'
  'libusb'
  'libusb-1.0.so'
)
makedepends=(
  'go'
  'git'
)
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs' '!lto')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ddfb2b2e00ed2ad944ee0eefeb6de6036e44d526e4f30f283bbfcd89578a76e4691cde54ffe76ed0ad3b8187de8b25a4c003b3aadd03b1195752e7825c5f60c4')
b2sums=('e79ef6f8d9f5f0342f26b4f9724a0859ab0c03fb301681efdb4f1a17fbe03feb4630f86fac41079a007da640cf48d05f8f7ab2814aebdfdb6ce71b191942faf7')

prepare() {
  cd ${pkgname}-${pkgver}
  find . -type f -exec sed 's|/usr/local|/usr|g' -i {} +
}

build() {
  cd ${pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 bettercap.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
