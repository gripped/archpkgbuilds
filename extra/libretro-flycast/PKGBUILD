# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-flycast
pkgver=6943
pkgrel=1
pkgdesc='Sega Dreamcast core'
arch=(x86_64)
url=https://github.com/flyinghead/flycast
license=(GPL-2.0-only)
groups=(libretro)
depends=(
  libgl
  libretro-core-info
  zlib
)
makedepends=(
  cmake
  git
  mesa
  ninja
)
_commit=230d971d4d09747c8c107f740d85a63f9f15b646
source=(
  git+https://github.com/flyinghead/flycast.git#commit=${_commit}
  flycast-asio::git+https://github.com/flyinghead/asio.git
  git+https://github.com/KhronosGroup/glslang.git
  flycast-libchdr::git+https://github.com/flyinghead/libchdr.git
  git+https://github.com/KhronosGroup/Vulkan-Headers.git
  git+https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git
  git+https://github.com/herumi/xbyak.git
)
b2sums=('8c60ff351b16fde2ae57ac08f521896eca8f4e454546ac12edab7183db5388f60f6b467846fbbd005e914d140094f69dffbd24303b6ba5deaa09b8ff6254dff8'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd flycast
  for submodule in core/deps/{glslang,Vulkan-Headers,VulkanMemoryAllocator,xbyak}; do
    git submodule init ${submodule}
    git submodule set-url ${submodule} "${srcdir}/${submodule##*/}"
    git -c protocol.file.allow=always submodule update ${submodule}
  done
  for submodule in core/deps/{asio,libchdr}; do
    git submodule init ${submodule}
    git submodule set-url ${submodule} "${srcdir}/flycast-${submodule##*/}"
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

pkgver() {
  cd flycast
  git rev-list --count HEAD
}

build() {
  cmake -S flycast -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DLIBRETRO=ON \
    -Wno-dev
  cmake --build build
}

package() {
  install -Dm 644 build/flycast_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
