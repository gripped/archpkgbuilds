# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=6.0.5
pkgrel=1
pkgdesc='DDE window manager forked from KWin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-kwin"
license=('GPL3')
depends=(qt6-base qt6-declarative qt6-shadertools qt6-wayland kauth kconfig kconfigwidgets
         kcoreaddons kdbusaddons kdeclarative kglobalacceld ki18n kidletime kitemviews knotifications
         kcrash kpackage libepoxy kwidgetsaddons kwindowsystem kcmutils knewstuff krunner kservice
         kwayland ktextwidgets kxmlgui xcb-util-cursor xorg-xwayland libpipewire libkscreen
         kscreenlocker lcms2 libqaccessibilityclient-qt6 python freetype2 gsettings-qt6 libcap
         systemd-libs kauth5 bash libxkbcommon libxss libxi libinput xcb-util-wm wayland perl libxcb
         xcb-util-keysyms libx11 libxtst libdrm gcc-libs mesa deepin-qt6platform-plugins)
makedepends=(git extra-cmake-modules ninja qt6-tools clang wayland-protocols
             deepin-wayland-protocols)
conflicts=('kwin' 'kwin-x11')
source=("git+https://github.com/linuxdeepin/deepin-kwin.git#tag=$pkgver")
sha512sums=('db3682ff79cc5ff82252e7edf65a463b91a834515a7ee4387cd9e5b76fb6f3482a543fab07a01a661db580630a3b0ff4ee5105fd266d618d9684ade86b6fddfa')

prepare() {
  cd deepin-kwin
  sed -i 's/QGenericUnixServices/QDesktopUnixServices/;s/qgenericunixservices_p/qdesktopunixservices_p/' src/plugins/qpa/integration.{h,cpp}
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/effects/multitaskview/multitaskview.cpp
}

build() {
  cd deepin-kwin
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib \
          -DBUILD_ON_V25=ON -DBUILD_TESTING=OFF
  ninja
}

package() {
  cd deepin-kwin
  DESTDIR="$pkgdir" ninja install
}
