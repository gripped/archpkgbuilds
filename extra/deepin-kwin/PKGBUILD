# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=6.0.8
pkgrel=5
pkgdesc='DDE window manager forked from KWin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-kwin"
license=('GPL-2.0-or-later')
depends=(qt6-base qt6-declarative qt6-shadertools kauth kconfig kconfigwidgets
         kcoreaddons kdbusaddons kdeclarative kglobalacceld ki18n kidletime kitemviews knotifications
         kcrash kpackage libepoxy kwidgetsaddons kwindowsystem kcmutils knewstuff krunner kservice
         kwayland ktextwidgets kxmlgui xcb-util-cursor xorg-xwayland libpipewire libkscreen
         kscreenlocker lcms2 libqaccessibilityclient-qt6 python freetype2 gsettings-qt6 libcap
         systemd-libs bash libxkbcommon libxss libxi libinput xcb-util-wm wayland perl libxcb
         xcb-util-keysyms libx11 libxtst libdrm gcc-libs mesa deepin-qt6platform-plugins)
makedepends=(git extra-cmake-modules ninja qt6-tools clang wayland-protocols
             deepin-wayland-protocols)
conflicts=('kwin' 'kwin-x11')
source=("git+https://github.com/linuxdeepin/deepin-kwin.git#tag=$pkgver"
         qt-6.10.patch
         qt-6.10.2.patch
         disable-kglobalaccel.patch)
sha512sums=('295451187c76e690af0bf3ae87ad1a03cc323efbf9971ba76c9fbaf9ec5590a0bbcdeed4331f106462c5c44519a8a44ed3272ed3bf63fecb1e6042275b557f4d'
            'e2d18def480dc62cdd7d0a0e2d6b313501701616a01983ad2f7fe092ad044b1882e9cd39f7bb47564743e6b2b63fee2585369b319e41021db2f3aa0e1f7466c8'
            'a561fed7fe5922128cc85f0014034555ffd12a56b6edf55ed8e2f10d6bc64cc93fdd2b36d95d3dbb726fba3bbdfdd3addb72a141e8488be26a0cc1d79aa55bd9'
            '18f4ef2cce61fe8e6b6a0ec35fd942ad631e37cfbf2688ef9dbc7767d93a27d27603fcd5b2df4383b2a3a7449402a933751f7b2ebe90edb3ae7110c849e766fa')

prepare() {
  cd deepin-kwin
  # Fix build with Qt 6.9
  git cherry-pick -n 664808264f5a49c306834565d0e0c631a8feda27
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/effects/multitaskview/multitaskview.cpp

  patch -p1 -i ../qt-6.10.patch
  patch -p1 -i ../qt-6.10.2.patch
# Disable kglobalaccel, doesn't build with Plasma 6.5
  patch -p1 -i ../disable-kglobalaccel.patch
}

build() {
  cd deepin-kwin
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib \
          -DBUILD_ON_V25=ON -DBUILD_TESTING=OFF \
          -DENABLE_KGLOBALACCEL=OFF
  ninja
}

package() {
  cd deepin-kwin
  DESTDIR="$pkgdir" ninja install
}
