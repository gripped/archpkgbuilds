# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.25.24
pkgrel=1
pkgdesc='DDE window manager forked from KWin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-kwin"
license=('GPL3')
depends=(qt5-declarative qt5-x11extras kconfig5 kconfigwidgets5 kcoreaddons5 kdbusaddons5 kdeclarative5
         kglobalaccel5 ki18n5 kidletime5 knotifications5 kcrash5 kpackage5 plasma-framework5 libepoxy
         kwidgetsaddons5 kwindowsystem5 kcompletion5 kcmutils5 knewstuff5 kservice5 ktextwidgets5 kxmlgui5
         breeze dwayland xcb-util-cursor xorg-xwayland libpipewire libkscreen5 kdecoration5 kscreenlocker5 qt5-wayland
         lcms2 libqaccessibilityclient-qt5 qt5-quickcontrols python fontconfig freetype2 libcap
         systemd-libs kauth5 bash kactivities5 libxkbcommon libxss libxi libinput xcb-util-wm wayland
         perl libxcb xcb-util-keysyms libx11 libxtst libdrm gcc-libs mesa qt5-base deepin-qt5platform-plugins)
makedepends=(git extra-cmake-modules kdoctools5 ninja qt5-tools wayland-protocols
             deepin-wayland-protocols)
source=("git+https://github.com/linuxdeepin/deepin-kwin.git#tag=$pkgver")
sha512sums=('b593ca13ceb281381ee191009e2a1504152d2759f36ba33823910e261ad8f6eb3257122c803025bc1ee4814d29e6ad0ffdd316dc7da98c8d1d44269a3c0376be')

prepare() {
  cd deepin-kwin
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/effects/multitaskview/multitaskview.cpp
  sed -i 's|org.kde.screensaver.xml|org.kde.screensaver5.xml|' src/CMakeLists.txt
}

build() {
  cd deepin-kwin
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DKScreenLocker_ROOT=/usr/lib/cmake/plasma5 \
    -DKDecoration2_ROOT=/usr/lib/cmake/plasma5 \
    -DKWIN_BUILD_RUNNERS=OFF
  ninja
}

package() {
  cd deepin-kwin
  DESTDIR="$pkgdir" ninja install
}
