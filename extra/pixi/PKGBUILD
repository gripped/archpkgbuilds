# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=pixi
pkgver=0.63.2
pkgrel=1
pkgdesc="A package management and workflow tool"
arch=('x86_64')
url="https://github.com/prefix-dev/pixi"
license=('BSD-3-Clause')
depends=('gcc-libs' 'openssl' 'bzip2')
makedepends=('cargo')
checkdepends=('git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('6983a863327066911a192b769a4878faf206185964bb846eaef5f81a1032a297b5416f3c2c62aa491e41b329017882caa363c42c6414db05f9bb5496f405bde9')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
  mkdir -p completions
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
  local _completion="target/release/$pkgname completion"
  $_completion --shell bash >"completions/$pkgbase"
  $_completion --shell fish >"completions/$pkgbase.fish"
  $_completion --shell zsh >"completions/_$pkgbase"
}

check() {
  cd "$pkgname-$pkgver"
  local skipped=(
    add_tests
    # https://github.com/prefix-dev/pixi/issues/5383
    tests::test_env_vars_are_set
    tests::test_rust_is_in_build_requirements
    tests::test_rust_is_not_added_if_already_present
  )
  cargo test --frozen -- ${skipped[@]/#/--skip }
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 664 "completions/$pkgname" -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm 664 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -Dm 664 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions/"
}

# vim: ts=2 sw=2 et:
