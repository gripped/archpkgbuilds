# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-securesystemslib
pkgver=1.3.0
pkgrel=1
pkgdesc="Cryptographic and general-purpose routines for Secure Systems Lab projects at NYU"
arch=('any')
url="https://github.com/secure-systems-lab/securesystemslib"
license=('MIT')
depends=(
  'python'
  'python-asn1crypto'
  'python-cryptography'
  'python-pykcs11'
)
makedepends=(
  'git'
  'python-build'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
source=(
  "git+$url.git?signed#tag=v$pkgver"
  "skip-failing-test.patch"
)
sha256sums=('508cca0b5dfceda19b1e4fd324e29b7efb998d0d36e3a5c50cf44b70046653df'
            '62f6c0b8f289795c6938504dd2a4445307edc8ececd47e96d87644e03d74f06f')
validpgpkeys=(
  "C2FB9C910758B6827BC43233BC0C6DEDD5E5CC03" # Sebastien Awwad (upstream co-maintainer)
  "8BA69B87D43BE294F23E812089A2AD3C07D962E8" # Lukas Puehringher (upstream maintainer)
  "903BAB73640EB6D65533EFF3468F122CE8162295" # Santiago Torres-Arias <santiago@archlinux.org> (upstream co-maintainer)
  "08F3409FCF71D87E30FBD3C21671F65CB74832A4" # Joshua Lock (GPG on YubiKey) <jlock@vmware.com> (upstream co-maintainer)
  "1343C98FAB84859FE5EC9E370527D8A37F521A2F" # Jussi Kukkonen <jkukkonen@vmware.com>
)

prepare() {
  cd ${pkgname#python-}
  # Test requires python-pyspx which is not packaged.
  patch -Np1 -i ../skip-failing-test.patch
}

build() {
  cd ${pkgname#python-}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}
  python -m unittest discover -vs tests
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
