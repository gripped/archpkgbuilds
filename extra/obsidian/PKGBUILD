# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Gennadiy Mykhailiuta <gmykhailiuta@gmail.com>
# Contributor: Xuanwo <xuanwo@archlinuxcn.org>

pkgname=obsidian
pkgver=1.9.10
pkgrel=2
pkgdesc="A powerful knowledge base that works on top of a local folder of plain text Markdown files"
arch=('x86_64')
url="https://obsidian.md"
license=('LicenseRef-Obsidian')
depends=('glibc' 'gcc-libs' 'fontconfig' 'bash' 'zlib' 'hicolor-icon-theme' 'fuse' 'electron37')
install=obsidian.install
options=(!strip)
source=("obsidian.sh"
        "obsidian.desktop"
        "LICENSE-Obsidian"
        "https://github.com/obsidianmd/obsidian-releases/releases/download/v${pkgver}/obsidian-${pkgver}.tar.gz")
sha256sums=('a014dab212e65bd79901b736f2bdd87c88ea646d94192fde274b77566f3abe8c'
            'be67713e873a5935d910efc6f6005c104b35c7b3a002dfcb5643fd185d1b0056'
            'd9fdc14f9c2fb95bb38310281268fdad413c1b49fa93213df3184f9b0ca1175c'
            'e5df71f7636ef1dcc01829d37981e6bf95d037a696c517a64728c2eb037a9434')

package() {
    cd "$srcdir"

    # Install executable file
    install -Dm755 "$pkgname".sh "$pkgdir"/usr/bin/obsidian

    # Install desktop file
    install -Dm644 "$pkgname".desktop -t "$pkgdir"/usr/share/applications/

    # Most of the release package is electron, but we use system's default one
    # So strip away asar packages and put them to /usr/lib/
    cd "${pkgname}-${pkgver}"/resources/
    install -dm0755 "$pkgdir"/usr/lib/obsidian
    # install icon
    install -Dm644 icon.png "$pkgdir"/usr/share/pixmaps/obsidian.png
    find . -type d -exec install -d {,"$pkgdir"/usr/lib/obsidian/}{} \;
    find . -type f -exec install -D {,"$pkgdir"/usr/lib/obsidian/}{} \;

    install -Dm644 "$srcdir"/LICENSE-Obsidian "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-Obsidian
}
