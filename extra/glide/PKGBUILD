# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=glide
pkgver=0.6.5
pkgrel=1
pkgdesc='Simple and minimalistic media player based on GStreamer and GTK'
arch=('x86_64')
url='https://github.com/philn/glide'
license=('MIT')
depends=('gst-plugins-bad' 'gst-plugins-base' 'gst-plugins-good' 'gtk4' 'libadwaita')
makedepends=('meson' 'rust')
optdepends=('gst-libav: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs')
replaces=('glide-player')
source=("https://github.com/philn/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('d987391efff91b07f4968f0dc929c9a885b3dc8034bd19f445abcef5a8d83117')

build() {
  cd $pkgname-$pkgver
  arch-meson . build
  meson compile -C build
}

package() {
  cd $pkgname-$pkgver
  meson install -C build --destdir "$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
