# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=5.14
pkgrel=2
pkgdesc='Personal and small-business financial-accounting application'
arch=(x86_64)
url='https://gnucash.org/'
license=(GPL-2.0-or-later)
depends=(aqbanking gwenhywfar boost-libs guile libdbi webkit2gtk-4.1 libofx)
makedepends=(boost cmake gtest libdbi-drivers mariadb-libs postgresql-libs swig python)
optdepends=('gnucash-docs: documentation'
            'libdbi-drivers: open and save data into SQL database'
            'perl-date-manip: stock information lookups'
            'perl-finance-quote: stock information lookups')
options=(!emptydirs)
source=(
  https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
)
sha256sums=('0c6fd20214da86a9a0443359f7b62d9a2bd4ed802fd680853da4b757a371ac91')

build() {
  cd $pkgname-$pkgver

  # Do not treat warnings as errors
  sed -e 's|-Werror||g' -i CMakeLists.txt

  cmake	-DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_PYTHON=ON \
        -DCOMPILE_GSCHEMAS=OFF
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || : # 2 tests failed
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Delete the gnucash-valgrind executable because the source files
  # are not included with the package and the executable is hardlinked
  # to the location that it was built at.
  rm -f "$pkgdir/usr/bin/gnucash-valgrind"
}
