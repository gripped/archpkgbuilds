# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>
# Contributor: Jean Lucas <jean@4ray.co>

pkgname=sccache
pkgver=0.12.0
pkgrel=1
pkgdesc='Shared compilation cache'
arch=(x86_64)
url=https://github.com/mozilla/sccache
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(
  git
  rust
)
optdepends=(
  'memcached: Memcached support'
  'valkey: Redis support'
)
options=(!lto)
_tag=de9895c2304b51e790d759dd6f97836a1a4a6204
source=(git+https://github.com/mozilla/sccache.git#tag=${_tag})
b2sums=('b3a924ad43dc15b8c6f1e0dc5010afc1296babfee33d204b5963f2e7c6db90e790f475ee3209e5324cd3d57fe14204504eb8d91d72578980ae4f781697f19f7f')

prepare() {
  cd sccache
  cargo fetch --locked
}

pkgver() {
  cd sccache
  git describe --tags | sed 's/^v//'
}

build() {
  cd sccache

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  cargo build --release --frozen --features all,dist-server
}

#check() {
#  cd sccache
#  cargo test --frozen --features all,dist-server
#}

package() {
  cd sccache
  install -Dt "$pkgdir/usr/bin" target/release/sccache{,-dist}
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md docs/*
}

# vim:set sw=2 sts=-1 et:
