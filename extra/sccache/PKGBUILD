# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>
# Contributor: Jean Lucas <jean@4ray.co>

pkgname=sccache
pkgver=0.10.0
pkgrel=2
pkgdesc='Shared compilation cache'
arch=(x86_64)
url=https://github.com/mozilla/sccache
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(
  git
  rust
)
optdepends=(
  'memcached: Memcached support'
  'valkey: Redis support'
)
options=(!lto)
_tag=e3d1ed560a28d114bd26299b0f717b372e7fc3d2
source=(git+https://github.com/mozilla/sccache.git#tag=${_tag})
b2sums=('f17be61612149fea251e79b564b82ef300edc9139f2bd8da6d43df7549a38d6b0ebd8338e403d18e11078122e563a8af5ca61237c8db2b7019c3b43d813bd203')

prepare() {
  cd sccache
  cargo fetch --locked
}

pkgver() {
  cd sccache
  git describe --tags | sed 's/^v//'
}

build() {
  cd sccache

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  cargo build --release --frozen --features all,dist-server
}

#check() {
#  cd sccache
#  cargo test --frozen --features all,dist-server
#}

package() {
  cd sccache
  install -Dt "$pkgdir/usr/bin" target/release/sccache{,-dist}
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md docs/*
}

# vim:set sw=2 sts=-1 et:
