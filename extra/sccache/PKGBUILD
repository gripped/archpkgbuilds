# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>
# Contributor: Jean Lucas <jean@4ray.co>

pkgname=sccache
pkgver=0.9.1
pkgrel=1
pkgdesc='Shared compilation cache'
arch=(x86_64)
url=https://github.com/mozilla/sccache
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(
  git
  rust
)
optdepends=(
  'memcached: Memcached support'
  'redis: Redis support'
)
options=(!lto)
_tag=77f67d73445a383ffd8af095fb7a8e41f258e80f
source=(git+https://github.com/mozilla/sccache.git#tag=${_tag})
b2sums=('4c776f339b923379055e276a7e18951d33ed7f50baf713bbcc45821a6309525c7e977bb45c3ca2eb9b021ea116d1fcf8ba865a625edd5ae5a06145b262d673fe')

prepare() {
  cd sccache
  cargo fetch --locked
}

pkgver() {
  cd sccache
  git describe --tags | sed 's/^v//'
}

build() {
  cd sccache

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  cargo build --release --frozen --features all,dist-server
}

#check() {
#  cd sccache
#  cargo test --frozen --features all,dist-server
#}

package() {
  cd sccache
  install -Dt "$pkgdir/usr/bin" target/release/sccache{,-dist}
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md docs/*
}

# vim:set sw=2 sts=-1 et:
