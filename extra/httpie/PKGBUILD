# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: daurnimator <daurnimator@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh lavabit com>

pkgname=httpie
pkgver=3.2.4
pkgrel=2
pkgdesc="human-friendly CLI HTTP client for the API era"
url="https://github.com/httpie/cli"
depends=('python-charset-normalizer'
         'python-defusedxml'
         'python-multidict'
         'python-pip'
         'python-pygments'
         'python-pysocks'
         'python-requests'
         'python-requests-toolbelt'
         'python-rich')
makedepends=('python-build'
             'python-installer'
             'python-setuptools'
             'python-wheel')
checkdepends=('python-pytest'
              'python-pytest-httpbin'
              'python-pytest-mock'
              'python-responses'
              'python-werkzeug')
conflicts=(python-httpie)
replaces=(python-httpie python2-httpie)
license=('BSD-3-Clause')
arch=('any')
source=($pkgname-$pkgver.tar.gz::"https://github.com/httpie/cli/archive/$pkgver.tar.gz")
sha256sums=('b185cd8d81325f97c773582e50f1c5e047c2d8575b53d676469c9daf2a52f341')

build() {
  cd cli-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd cli-$pkgver
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/httpie/LICENSE"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Fix upstream, include them in MANIFEST.in and use data_files in setup.py to install them automatically
  # TODO: add zsh support
  install -Dm644 extras/httpie-completion.bash "$pkgdir"/usr/share/bash-completion/completions/http
  install -Dm644 extras/httpie-completion.fish "$pkgdir"/usr/share/fish/vendor_completions.d/http.fish
}

check() {
  cd cli-$pkgver
  local pytest_args=(
    # https://github.com/httpie/cli/issues/1530
    -m='not requires_installation'
    -k='not test_binary and not test_compress_form and not test_stdin_read_warning'
    # Fails on Python 3.13 due to help message formatting
    --deselect=tests/test_cli_ui.py::test_naked_invocation
    # Python 3.14: argparse now calls getter during parser initialization
    --deselect=tests/test_cli_utils.py::test_lazy_choices_help
    # charset_normalizer detects big5 as euc-kr (CJK encoding ambiguity)
    --deselect=tests/test_encoding.py::test_terminal_output_response_charset_detection
    --deselect=tests/test_encoding.py::test_terminal_output_request_charset_detection
    # Flaky daemon timing test
    --deselect=tests/test_update_warnings.py::test_daemon_runner
  )
  pytest "${pytest_args[@]}"
}
