# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=collision
pkgver=3.10.0
pkgrel=2
pkgdesc='GUI tool to generate, compare and verify MD5, SHA-1, SHA-256, SHA-512, Blake3, CRC32 and Adler32 hashes'
arch=(x86_64)
url='https://apps.gnome.org/Collision/'
license=(BSD-2-Clause)
groups=(gnome-circle)
depends=(
  dconf
  gc
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  pcre2
  zlib
)
makedepends=(
  crystal
  git
  gobject-introspection
  shards
)
optdepends=('nautilus-python: Nautilus context menu extension')
source=(
  "git+https://github.com/GeopJr/Collision.git#tag=v$pkgver"
  collision-nonexistent-file.patch
)
b2sums=(
  f8a210eea927da2f3f7ed2fa2f9d257d5be89983d67c6d3674447abba423dbd85ecfe40b3aed2dc39eaaa1627db412764d15645e7aae8586acb0b183507f9c9b
  b61dea8e8982255fa3bcf7467aeb06d8b69c714da5114699d3a4b1c58e07334a40bea7b942e0a91f8e20aac793906606c75fa8714c5774a5c4955cccf52309d0
)

prepare() {
  cd Collision
  shards install

  # https://github.com/hugopl/harfbuzz.cr/pull/2
  echo '  - hb_ot_layout_lookup_collect_glyphs' >>lib/harfbuzz/src/bindings/harfbuzz/binding.yml

  # https://github.com/GeopJr/Collision/pull/226
  git apply -3 ../collision-nonexistent-file.patch

  sed -i -e '/gtk-update-icon-cache/d' -e '/glib-compile-schemas/d' Makefile
}

build() {
  cd Collision
  make -j1
  make metainfo
}

check() {
  cd Collision
  make check
  make validate-appstream
}

package() {
  cd Collision
  make PREFIX="$pkgdir/usr" install
  install -Dm644 -t "$pkgdir/usr/share/nautilus-python/extensions/" nautilus-extension/collision-extension.py
  install -Dm644 -t "$pkgdir/usr/share/metainfo/" data/dev.geopjr.Collision.metainfo.xml
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
