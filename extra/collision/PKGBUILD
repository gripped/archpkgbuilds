# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=collision
pkgver=3.11.0
pkgrel=1
pkgdesc='GUI tool to generate, compare and verify MD5, SHA-1, SHA-256, SHA-512, Blake3, CRC32 and Adler32 hashes'
arch=(x86_64)
url='https://apps.gnome.org/Collision/'
license=(BSD-2-Clause)
groups=(gnome-circle)
depends=(
  dconf
  gc
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  pcre2
  zlib
)
makedepends=(
  crystal
  git
  gobject-introspection
  shards
)
optdepends=('nautilus-python: Nautilus context menu extension')
source=("git+https://github.com/GeopJr/Collision.git#tag=v$pkgver")
b2sums=(7164a456968d77f4cc7e3cc66b779f139b721ce9a2088dc7ca5c180e421e84b8c1424d8dcf54f3bf510a315899d56ef6fc05a54640043113a2cf970627ef70eb)

prepare() {
  cd Collision
  shards install

  sed -i -e '/gtk-update-icon-cache/d' -e '/glib-compile-schemas/d' Makefile
}

build() {
  cd Collision
  make -j1
  make metainfo
}

check() {
  cd Collision
  make check
  make validate-appstream
}

package() {
  cd Collision
  make PREFIX="$pkgdir/usr" install
  install -Dm644 -t "$pkgdir/usr/share/nautilus-python/extensions/" nautilus-extension/collision-extension.py
  install -Dm644 -t "$pkgdir/usr/share/metainfo/" data/dev.geopjr.Collision.metainfo.xml
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
