# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=python-janus
pkgver=1.1.0
pkgrel=4
pkgdesc="Thread-safe asyncio-aware queue for Python"
arch=('any')
url="https://github.com/aio-libs/janus"
license=('Apache-2.0')
depends=('python')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest-asyncio')
source=("git+https://github.com/aio-libs/janus.git#tag=v$pkgver")
sha512sums=('4a9f850b2c3c7fbe0f5a99c064a45bb8e13355518e2e83b80f5ef1a978fe5db7b01c137cf6c830c4b9f9f03bc0a5c67940fe98aa546cbe1866bb860805d39bd0')

prepare() {
  cd janus
  sed -i '/--cov-branch --cov-report/d' setup.cfg
}

build() {
  cd janus
  python -m build --wheel --no-isolation
}

check() {
  cd janus
  python -m pytest
}

package() {
  cd janus
  python -m installer --destdir="$pkgdir" dist/*.whl
}
