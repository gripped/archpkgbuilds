# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=python-janus
pkgver=1.2.0
pkgrel=1
pkgdesc="Thread-safe asyncio-aware queue for Python"
arch=('any')
url="https://github.com/aio-libs/janus"
license=('Apache-2.0')
depends=('python')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest-asyncio' 'python-pytest-benchmark')
source=("git+https://github.com/aio-libs/janus.git#tag=v$pkgver")
sha512sums=('8ca7008f90873868efb027314efa9b0e3b4587ec3977924ccf517df5c7c8ab23f80ccbf878b24dfcaebed11f5f82e28cdf5290319006d7d5b4234005b89062f1')

prepare() {
  cd janus
  sed -i '/--cov-branch --cov-report/d' setup.cfg
}

build() {
  cd janus
  python -m build --wheel --no-isolation
}

check() {
  cd janus
  python -m pytest
}

package() {
  cd janus
  python -m installer --destdir="$pkgdir" dist/*.whl
}
