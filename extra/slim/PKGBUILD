# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Hugo Ideler <hugoideler@dse.nl>

pkgname=slim
pkgver=1.4.1
pkgrel=3
epoch=1
pkgdesc='Simple Login Manager resurrected'
arch=(x86_64)
url='https://slim-fork.sourceforge.io/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         libjpeg-turbo
         libpng
         libx11
         libxext
         libxft
         libxmu
         libxrandr
         pam
         ttf-font
         xorg-xauth)
makedepends=(cmake)
backup=("etc/logrotate.d/$pkgname"
        "etc/pam.d/$pkgname"
        "etc/$pkgname.conf"
        "etc/${pkgname}lock.conf")
_archive="$pkgname-$pkgver"
source=("https://downloads.sourceforge.net/project/slim-fork/$_archive.tar.gz"
        "$pkgname.pam"
        "${pkgname}lock.pam"
        "$pkgname.logrotate"
        default-path.patch
        no-sessreg.patch
        "$pkgname-includes.patch::https://salsa.debian.org/debian/slim/-/raw/master/debian/patches/log.h-Restore-required-includes.patch?ref_type=heads")
sha256sums=('5f88d7f162300574fbd2e3216bf3e2422635118771e719a5395ed3e4ae3f241b'
            'b9a77a614c451287b574c33d41e28b5b149c6d2464bdb3a5274799842bca51a4'
            'dfe35488b50f19fd96526374edc16850ed37dac919834dd579392b1a7518f2ab'
            '5bf44748b5003f2332d8b268060c400120b9100d033fa9d35468670d827f6def'
            '408337b006760b8029b1aeec37b103b1d02a23a85328cf9e2ac9c1a89faab279'
            '56d41eccbcdd68aca03b678d7e78089f4ec111a997f3fe3a8b7a9d815d84ea6b'
            '49ba000947dd76b566f1b7d496c345a3b6f5bf8d5b2aa31ad938109b9dd6eb23')

prepare() {
	cd "$_archive"
	patch -Np1 -i ../default-path.patch
	patch -Np1 -i ../no-sessreg.patch
	patch -Np1 -i ../$pkgname-includes.patch
}

build() {
	cd "$_archive"
	local cmake_flags=(
		CMAKE_POLICY_VERSION_MINIMUM=3.5
		CMAKE_INSTALL_PREFIX=/usr
		CMAKE_BUILD_TYPE=Release
		CMAKE_SKIP_RPATH=On
		BUILD_SLIMLOCK=On
		USE_CONSOLEKIT=Off
		USE_PAM=On
	)
	cmake -B build "${cmake_flags[@]/#/-D }"
	cmake --build build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" cmake --install build
	# https://gitlab.archlinux.org/archlinux/packaging/packages/slim/-/issues/2
	mv "$pkgdir"{,/usr}/lib/systemd
	rmdir "$pkgdir/lib"
	install -Dm644 -t "$pkgdir/etc/" slimlock.conf
	install -Dm644 "../$pkgname.pam" "$pkgdir/etc/pam.d/$pkgname"
	install -Dm644 "../${pkgname}lock.pam" "$pkgdir/etc/pam.d/${pkgname}lock"
	install -Dm644 "../$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
}
