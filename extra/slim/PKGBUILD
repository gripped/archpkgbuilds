# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Hugo Ideler <hugoideler@dse.nl>

pkgname=slim
pkgver=1.4.1
pkgrel=1
pkgdesc="Simple Login Manager resurrected"
arch=('x86_64')
url="https://slim-fork.sourceforge.io/"
license=('GPL-2.0-or-later')
depends=('gcc-libs' 'libjpeg-turbo' 'libpng' 'libx11' 'libxext' 'libxft' 'libxmu'
         'libxrandr' 'pam' 'ttf-font' 'xorg-xauth')
makedepends=('cmake')
backup=('etc/logrotate.d/slim'
        'etc/pam.d/slim'
        'etc/slim.conf'
        'etc/slimlock.conf')
source=(https://downloads.sourceforge.net/project/slim-fork/$pkgname-$pkgver.tar.gz
        slim.pam
        slimlock.pam
        slim.logrotate
        include-pam-header.patch
        default-path.patch
        no-sessreg.patch)
sha256sums=('5f88d7f162300574fbd2e3216bf3e2422635118771e719a5395ed3e4ae3f241b'
            'b9a77a614c451287b574c33d41e28b5b149c6d2464bdb3a5274799842bca51a4'
            'dfe35488b50f19fd96526374edc16850ed37dac919834dd579392b1a7518f2ab'
            '5bf44748b5003f2332d8b268060c400120b9100d033fa9d35468670d827f6def'
            '91705f660519d5b7034c41d61034ff53d15922afd497ad86d375f4081b00a460'
            '408337b006760b8029b1aeec37b103b1d02a23a85328cf9e2ac9c1a89faab279'
            '56d41eccbcdd68aca03b678d7e78089f4ec111a997f3fe3a8b7a9d815d84ea6b')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../include-pam-header.patch
  patch -Np1 -i ../default-path.patch
  patch -Np1 -i ../no-sessreg.patch

  sed -i 's|"/lib/systemd"|"/usr/lib/systemd"|' CMakeLists.txt
}

build() {
  cd $pkgname-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -USYSTEMDDIR \
    -DSYSTEMDDIR=/usr/lib/systemd \
    -DBUILD_SLIMLOCK=ON \
    -DUSE_CONSOLEKIT=OFF \
    -DUSE_PAM=ON
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 slimlock.conf -t "$pkgdir/etc"

  install -Dm644 ../slim.pam "$pkgdir/etc/pam.d/slim"
  install -Dm644 ../slimlock.pam "$pkgdir/etc/pam.d/slimlock"
  install -Dm644 ../slim.logrotate "$pkgdir/etc/logrotate.d/slim"
}

# vim:set ts=2 sw=2 et:
