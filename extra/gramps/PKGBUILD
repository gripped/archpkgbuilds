# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Cory Farmer <rbgkofi@ameritech.net>

pkgname=gramps
pkgver=5.2.4
pkgrel=1
epoch=2
pkgdesc="Genealogy program, which helps you track your family tree"
arch=('any')
url="https://gramps-project.org/"
license=('GPL')
depends=('gtk3' 'python-bsddb' 'python-cairo' 'python-gobject' 'xdg-utils')
makedepends=('intltool' 'python-setuptools')
optdepends=('osm-gps-map: show maps in the geography view'
            'graphviz: enable creation of graphs (required for tree reports)'
            'python-pyicu: improves localised sorting'
            'gtkspell3: enable spell checking in the notes'
            'rcs: manage multiple revisions of your family trees'
            'python-pillow: crop and convert images to JPG'
            'libgexiv2: manage Exif metadata embedded in your media'
            'geocode-glib: associate a geographical position to a place name')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gramps-project/$pkgname/archive/v$pkgver.tar.gz"
	"replace-deprecated-warn-method.patch"
	"replace-deprecated-makesuite.patch")
sha256sums=('7d5f22db92c43cb7798b50837e95ee5da56154596d9dea3b96d2c5cd2e5f0cdd'
            '5b8ebcd419d17011deeffae60a85e02086e4ceb78a050b5481aff63d8a25fa7e'
            'a33d2afb646af97ea47c3f04f7092cc3a5f2049a904aeec0103fbf5286de335c')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "${srcdir}/replace-deprecated-warn-method.patch"
  patch -Np1 -i "${srcdir}/replace-deprecated-makesuite.patch"
}

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --no-compress-manpages --root="$pkgdir" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  echo -n '/usr/share' >"$pkgdir"${site_packages}/gramps/gen/utils/resource-path
}
