# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Cory Farmer <rbgkofi@ameritech.net>

pkgname=gramps
pkgver=6.0.5
pkgrel=2
epoch=2
pkgdesc="Genealogy program, which helps you track your family tree"
arch=('any')
url="https://gramps-project.org/"
license=('GPL-2.0-or-later')
depends=('gtk3' 'python-bsddb' 'python-cairo' 'python-gobject' 'xdg-utils' 'python-pyicu' 'python-orjson')
makedepends=('intltool' 'python-setuptools' 'python-build' 'python-installer')
optdepends=('osm-gps-map: show maps in the geography view'
            'graphviz: enable creation of graphs (required for tree reports)'
            'python-pyicu: improves localised sorting'
            'gtkspell3: enable spell checking in the notes'
            'rcs: manage multiple revisions of your family trees'
            'python-pillow: crop and convert images to JPG'
            'libgexiv2: manage Exif metadata embedded in your media'
            'geocode-glib: associate a geographical position to a place name')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gramps-project/$pkgname/archive/v$pkgver.tar.gz"
        set_SDE_in_gzip.patch)
sha256sums=('1295fe352669cdf419d762445db1d26ba5492da30c0a78fef278856eda9cc680'
            'cbeefd3af162b7168a4eb4f118091e3abddb887a2757843c6eaa2dc3f52d2a5a')

prepare() {
  cd $pkgname-$pkgver
  # Default to SOURCE_DATE_EPOCH (if set) as timestamps with gzip when compressing man pages
  # to avoid recording non-deterministic information in gzip metadata across builds.
  # This is required for reproducible builds.
  # See https://github.com/gramps-project/gramps/pull/2078
#  patch -Np1 -i "$srcdir/set_SDE_in_gzip.patch"
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  echo -n '/usr/share' >"$pkgdir"${site_packages}/gramps/gen/utils/resource-path
}
