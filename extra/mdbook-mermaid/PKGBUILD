# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mdbook-mermaid
pkgver=0.17.0
pkgrel=1
pkgdesc="A preprocessor for mdbook to add mermaid support"
arch=(x86_64)
url="https://github.com/badboy/mdbook-mermaid"
license=(
  MPL-2.0
  MIT
)
depends=(
  gcc-libs
  glibc
  mdbook
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  mermaid.LICENSE.txt::https://raw.githubusercontent.com/mermaid-js/mermaid/becadf0a7de509b6cc81e99a04abfa6cba8e7bfd/LICENSE
)
sha512sums=('b21ee04fc21513b4980cecaf1cffe95abc60abc1f7e747f6381a865cdda4c92919f910d1a9451130f9a5419100ea046a4cd770e3b8456aa615bb10e23b56503c'
            '8f59d58fe107b318fd7e13e76b3f9aa36eff5257f04eed98b87d0da5f9db9dad832e26f7050af5c88b6634bb69ea60e6390ec5554865c016ca8e98580a0d4e53')
b2sums=('92c4eca5ad668c7fae2511a8943d87a7c0eed7f326fc7c55871dc2f3e54d567f7c4fa30e04ca43e2bba2c86f9a13ade418ef2e6441160acfb73d870191dd1c40'
        '518f19395b9e2a99d0f8a063aad97abd6b54640ec6a89dd654978cff0a46116af4f46066d593b83376a3d50077e7a0297cf81cf2742e59d721cff552af0ab9b3')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 ../mermaid.LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
