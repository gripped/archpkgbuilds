# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mdbook-mermaid
pkgver=0.16.0
pkgrel=1
pkgdesc="A preprocessor for mdbook to add mermaid support"
arch=(x86_64)
url="https://github.com/badboy/mdbook-mermaid"
license=(
  MPL-2.0
  MIT
)
depends=(
  gcc-libs
  glibc
  mdbook
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  mermaid.LICENSE.txt::https://raw.githubusercontent.com/mermaid-js/mermaid/becadf0a7de509b6cc81e99a04abfa6cba8e7bfd/LICENSE
)
sha512sums=('0f940a2411551997707a4241979bcaea26486fa4714d8c256fede72f5c7a9dd2fbeefb7b768a94fec0965853fea66d3502b3d0088bd1366e42851e5546dbe241'
            '8f59d58fe107b318fd7e13e76b3f9aa36eff5257f04eed98b87d0da5f9db9dad832e26f7050af5c88b6634bb69ea60e6390ec5554865c016ca8e98580a0d4e53')
b2sums=('639cd9c9965ce1646d6331abaa52dbd20006553281719987ff685e4ac35b3ca9ae53e54e052daa050c771c4f8b1219bdc28c3583ca5830dd883e90b9c7fd3b61'
        '518f19395b9e2a99d0f8a063aad97abd6b54640ec6a89dd654978cff0a46116af4f46066d593b83376a3d50077e7a0297cf81cf2742e59d721cff552af0ab9b3')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 ../mermaid.LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
