# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mdbook-mermaid
pkgver=0.15.0
pkgrel=1
pkgdesc="A preprocessor for mdbook to add mermaid support"
arch=(x86_64)
url="https://github.com/badboy/mdbook-mermaid"
license=(
  MPL-2.0
  MIT
)
depends=(
  gcc-libs
  glibc
  mdbook
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  mermaid.LICENSE.txt::https://raw.githubusercontent.com/mermaid-js/mermaid/becadf0a7de509b6cc81e99a04abfa6cba8e7bfd/LICENSE
)
sha512sums=('c6aa04da7db3f7f03d3a58a747ca67e597052997863ba65a846fd13b95b5955f6a693bb95e3f47dc20c77e56d60e037426fc25a1f445b169303566e97c625d98'
            '8f59d58fe107b318fd7e13e76b3f9aa36eff5257f04eed98b87d0da5f9db9dad832e26f7050af5c88b6634bb69ea60e6390ec5554865c016ca8e98580a0d4e53')
b2sums=('5c22d92d75dbc86da7c4fc26381fc275792427b9be19ba4e44a8a75891b42c3b6492ee5f0cfb7c33af4934da6e5c6059e94522b533d6e64900e0860d3815baee'
        '518f19395b9e2a99d0f8a063aad97abd6b54640ec6a89dd654978cff0a46116af4f46066d593b83376a3d50077e7a0297cf81cf2742e59d721cff552af0ab9b3')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 ../mermaid.LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
