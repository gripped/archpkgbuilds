# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-app-services
pkgver=1.0.28
pkgrel=1
pkgdesc='Service collection of DDE applications, including dconfig-center'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-app-services"
license=('GPL3')
depends=(qt5-base dtkcore dtkwidget)
makedepends=(git cmake ninja doxygen gtest qt5-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-app-services.git#tag=$pkgver)
sha512sums=('2102e61c31a0e1d96f4a61b3dacb370179126d2dbaf7ff4109844f69bf74f1b6847d2a4bdf07f6c21ae06c9c5e1b5c92ffadd272186730ca1af71fad22637bd4')

build() {
  cd dde-app-services
  cmake -GNinja . -DCMAKE_INSTALL_PREFIX=/usr -DDVERSION=$pkgver
  ninja
}

package() {
  cd dde-app-services
  DESTDIR="$pkgdir" ninja install
}
