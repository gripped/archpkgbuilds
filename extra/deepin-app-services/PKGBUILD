# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-app-services
pkgver=1.0.33
pkgrel=1
pkgdesc='Service collection of DDE applications, including dconfig-center'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-app-services"
license=('LGPL-3.0-or-later')
depends=(qt6-base dtk6core dtk6widget)
makedepends=(git cmake ninja doxygen gtest qt6-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-app-services.git#tag=$pkgver)
sha512sums=('7709c3da13f524db1ae5d8b29d90b64670dd65f2d04d00acb8a0914317610fd2e0bf7a406c7c1e72bd900d4cf54aca5df6537a619b0143b245a6d6cd70432257')

prepare() {
  cd dde-app-services
  git cherry-pick -n 7d22e1289e1ef8168d59a92147996597137eab62
}

build() {
  cd dde-app-services
  cmake -GNinja . -DCMAKE_INSTALL_PREFIX=/usr -DDVERSION=$pkgver
  ninja
}

package() {
  cd dde-app-services
  DESTDIR="$pkgdir" ninja install
}
