# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=xdg-desktop-portal-cosmic
pkgver=1.0.0.alpha.5
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/xdg-desktop-portal-cosmic
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libpipewire
  libxkbcommon
  mesa
  wayland
  xdg-desktop-portal
)
makedepends=(
  cargo
  clang
  git
  mold
)
provides=(xdg-desktop-portal-impl)
_tag=e7c92a7316ad5c6e0ccfa08adaae118ee8f2738f
source=(
  git+https://github.com/pop-os/xdg-desktop-portal-cosmic.git#tag=${_tag}
  xdg-desktop-portal-cosmic-lto.patch
)
b2sums=('905c492b3988336691841cd4f13c63ca400522bb95c5f4b4b68d8826415d64e50551ece41cb1901689a597409664b1e31f71739a1f7531022f4388b4013be802'
        'dec9ed0a2913843e4d88dece0be9b8ac6f1f8fbc601b3b6ff3158f8b80604e9edc28951fe42005cef0d5d23dc4efca9836022bae4f031b38c63aad1f654154a8')

prepare() {
  cd xdg-desktop-portal-cosmic
  patch -Np1 -i ../xdg-desktop-portal-cosmic-lto.patch
  cargo fetch --locked
}

#pkgver() {
#  cd xdg-desktop-portal-cosmic
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd xdg-desktop-portal-cosmic
  RUSTFLAGS+=" -C link-arg=-fuse-ld=mold"
  make ARGS+=" --frozen --release"
}

package() {
  cd xdg-desktop-portal-cosmic
  make prefix='/usr' libexecdir='/usr/lib' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
