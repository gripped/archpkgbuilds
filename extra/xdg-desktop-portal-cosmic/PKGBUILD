# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=xdg-desktop-portal-cosmic
pkgver=1.0.0.alpha.3
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/xdg-desktop-portal-cosmic
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libpipewire
  libxkbcommon
  mesa
  wayland
  xdg-desktop-portal
)
makedepends=(
  cargo
  clang
  git
  lld
)
provides=(xdg-desktop-portal-impl)
_tag=7c8313898bffce9e44e0549782316c57597216d1
source=(
  git+https://github.com/pop-os/xdg-desktop-portal-cosmic.git#tag=${_tag}
  xdg-desktop-portal-cosmic-lto.patch
)
b2sums=('33ef8d9074572dd7e5d59ff7a911e19d3828b18c5081230da41044722e6840a9fb8feef13a9e973b0be9f7681a07640af740f2c226248057b9d338d18a95c314'
        'dec9ed0a2913843e4d88dece0be9b8ac6f1f8fbc601b3b6ff3158f8b80604e9edc28951fe42005cef0d5d23dc4efca9836022bae4f031b38c63aad1f654154a8')

prepare() {
  cd xdg-desktop-portal-cosmic
  patch -Np1 -i ../xdg-desktop-portal-cosmic-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd xdg-desktop-portal-cosmic
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd xdg-desktop-portal-cosmic
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd xdg-desktop-portal-cosmic
  make prefix='/usr' libexecdir='/usr/lib' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
