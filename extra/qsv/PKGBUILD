# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=qsv
pkgver=16.0.0
pkgrel=1
pkgdesc='quicksilver data-wrangling toolkit (fork of xsv) for tabular data such as CSV'
arch=(x86_64)
url="https://github.com/dathere/$pkgname"
license=(MIT Unlicense)
depends=(bzip2 libbz2.so
         gcc-libs # libgcc_s.so libstdc++.so
         glibc # libc.so libm.so
         python # libpython3.13.so
         python-xlsxwriter
         wayland libwayland-client.so)
makedepends=(cargo
             clang
             cmake
             luau)
replaces=(qsv-bin)
_archive="$pkgname-$pkgver"
options=(!lto)
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('524b5a9b43da6f54d60a5f3063ba8b608fb28f23eb9155f3fec9fdb7ee2fe1c9')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
	sed -i -e '/^lto/s/true/false/' Cargo.toml
}

build() {
	cd "$_archive"
	env \
		CARGO_PROFILE_RELEASE_LTO=true \
		CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
		CARGO_PROFILE_RELEASE_DEBUG=2 \
	cargo build --frozen --release --features distrib_features
}

check() {
	cd "$_archive"
	# cargo test --frozen --features distrib_features
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	pushd contrib/completions/examples
	install -Dm0644 -t "$pkgdir/usr/share/elvish/lib/" qsv.elv
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" qsv.fish
	install -Dm0644 qsv.bash "$pkgdir/usr/share/bash-completion/completions/qsv"
	install -Dm0644 qsv.zsh "$pkgdir/usr/share/zsh/site-functions/_qsv"
}
