# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: genofire

pkgname=ovenmediaengine
pkgver=0.17.1
pkgrel=2
pkgdesc='Open-source streaming video service with sub-second latency'
arch=('x86_64')
url='https://ovenmediaengine.com'
license=('AGPL-3.0-only')
# See https://github.com/AirenSoft/OvenMediaEngine/blob/master/misc/prerequisites.sh#L490
depends=(
	'openssl>=3'
	'libsrtp'
	'srt'
	'opus'
	'ffmpeg4.4' # https://github.com/AirenSoft/OvenMediaEngine/pull/1626
	'jemalloc'
	'pcre2'
	'hiredis'
)
optdepends=(
	'libva'
	'intel-gmmlib'
	'intel-media-sdk'
	'intel-media-driver'
	'cuda-tools'
	'ffnvcodec-headers'
	'openh264: For h264 encoding, also needs a ffmpeg build with --enable-libopenh264'
)
makedepends=(
	'bc'
	'nasm'
	'x264'
	'x265'
	'libvpx'
	'fdkaac'
)
source=(
	"${pkgname}-${pkgver}.tar.gz::https://github.com/AirenSoft/OvenMediaEngine/archive/refs/tags/v${pkgver}.tar.gz"
	"ovenmediaengine-ffmpeg44.patch"
	"ovenmediaengine.service"
)
sha512sums=('6526b6be63d61e7fe21eb308b22f73157238ec7a9e3ba1c6be302d96c674cfa97c1cba4c31c671e7fa7798d1d193d633b359d878a19b80b7e31b54f61c339506'
            '1a4c59f49d4135a2e20c4c869f6bb3e213027992ce4d9e932712db6809ab9f97e3f2d79d7b1717574c410f46ca3ce15d2f854bbf0ef2af5a85e348ca7bb9e257'
            'c5c4615d4c50a55c6fea177a1bc73b13d1365224c4e102067e45ce956741cb95366ec3b16cdec8932083c923910794e9164b0f9a25a6b0e2f186557fac3e42f1')

backup=(
	'etc/ovenmediaengine/Server.xml'
	'etc/ovenmediaengine/Logger.xml'
)

prepare() {
	cd "OvenMediaEngine-${pkgver}"

	patch -Np1 -i "${srcdir}/ovenmediaengine-ffmpeg44.patch"

	# Assets take up ~13MB of space
	rm -Rf docs/.gitbook
	find docs -type f -name "*.md" -exec sed -i "s|.gitbook/assets|https://raw.githubusercontent.com/AirenSoft/OvenMediaEngine/v${pkgver}/docs/.gitbook/assets|g" {} +
}

build() {
	cd "OvenMediaEngine-${pkgver}/src"

	export CXXFLAGS="${CXXFLAGS} -I/usr/include/ffmpeg4.4"

	DISABLE_CUSTOM_LIBRARY_PATHS=true \
	make release
}

package()  {
	install -D -m0644 "ovenmediaengine.service" "${pkgdir}/usr/lib/systemd/system/ovenmediaengine.service"

	cd "OvenMediaEngine-${pkgver}"

	install -D -m0755 "src/bin/RELEASE/OvenMediaEngine" "${pkgdir}/usr/bin/OvenMediaEngine"

	install -D -m0644 "misc/conf_examples/Origin.xml" "${pkgdir}/etc/ovenmediaengine/Server.xml"
	install -D -m0644 "misc/conf_examples/Logger.xml" "${pkgdir}/etc/ovenmediaengine/Logger.xml"

	install -D -m0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	mkdir -p "${pkgdir}/usr/share/doc"
	cp -a docs "${pkgdir}/usr/share/doc/${pkgname}"
	cp -a misc/conf_examples "${pkgdir}/usr/share/doc/${pkgname}/conf_examples"
}
