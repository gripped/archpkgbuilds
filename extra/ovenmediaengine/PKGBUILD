# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: genofire

pkgname=ovenmediaengine
pkgver=0.16.5
pkgrel=5
pkgdesc='Open-source streaming video service with sub-second latency'
arch=('x86_64')
url='https://ovenmediaengine.com'
license=('AGPL-3.0-only')
# See https://github.com/AirenSoft/OvenMediaEngine/blob/master/misc/prerequisites.sh#L490
depends=(
	'openssl>=3'
	'libsrtp'
	'srt'
	'opus'
	'ffmpeg'
	'jemalloc'
	'pcre2'
	'hiredis'
)
optdepends=(
	'libva'
	'intel-gmmlib'
	'intel-media-sdk'
	'intel-media-driver'
	'cuda-tools'
	'ffnvcodec-headers'
	'openh264: For h264 encoding, also needs a ffmpeg build with --enable-libopenh264'
)
makedepends=(
	'bc'
	'nasm'
	'x264'
	'x265'
	'libvpx'
	'fdkaac'
)
# https://github.com/AirenSoft/OvenMediaEngine/pull/1626
# We skip f7c3e2f41364a11e228b0f960e3b3511d2526022 since it will fail on 0.16.5
source=(
	"${pkgname}-${pkgver}.tar.gz::https://github.com/AirenSoft/OvenMediaEngine/archive/refs/tags/v${pkgver}.tar.gz"
	"ovenmediaengine-ffmpeg7-1-6a310d7c94993e457effb99898c6fb09eeb8a142.patch::https://github.com/AirenSoft/OvenMediaEngine/commit/6a310d7c94993e457effb99898c6fb09eeb8a142.patch"
	"ovenmediaengine-ffmpeg7-2-6777ff56909221c146c14091f0c6319668f91a66.patch::https://github.com/AirenSoft/OvenMediaEngine/commit/6777ff56909221c146c14091f0c6319668f91a66.patch"
	"ovenmediaengine-ffmpeg7-3-48bb062cb9f2d50b6f3e053a6557c71fc14ed5d5.patch::https://github.com/AirenSoft/OvenMediaEngine/commit/48bb062cb9f2d50b6f3e053a6557c71fc14ed5d5.patch"
	"ovenmediaengine-ffmpeg7-4-776a674c58f33626146c8a7600f82ab8b80b5317.patch::https://github.com/AirenSoft/OvenMediaEngine/commit/776a674c58f33626146c8a7600f82ab8b80b5317.patch"
	"ovenmediaengine-ffmpeg7-5-85753d73b55aaf13b1eb2927412f315a5a621227.patch::https://github.com/AirenSoft/OvenMediaEngine/commit/85753d73b55aaf13b1eb2927412f315a5a621227.patch"
	"ovenmediaengine-ffmpeg7-6-6412b8111fe5ab27c5d14ff74f75e7f332df86c4.patch::https://github.com/AirenSoft/OvenMediaEngine/commit/6412b8111fe5ab27c5d14ff74f75e7f332df86c4.patch"
	"ovenmediaengine.service"
)
sha512sums=('6892844ccb11cb99db927e55f619cf8a88086d0723e191464d8ec9a86479917f6ee55720c0bd3dcc702bcccd1ae28626c93f45b8e69784776c647ced1bb6404a'
            'f1e7031f3984b3286879f2c8bee6ad0217362b1db95669bd8527d1bad8fbc9896ae016a42dea0a39eba13dafbeffaed4bd65ffa2f4bb9ad45a64878a6d63787e'
            'fb4bcd8f845d4e1c5ca8084bcb255269c9b9a9d3b28b450dcc15330855029156d9ad932edcccc72222b94e422b2138715fbdf16e947effbfbbc51ff868079c1b'
            '1a81076a30a4ec4b7e0f38fe3e9afe6874d64c8122173fb493764f3046ec4d348473e7ce15b71a98fb86ff7d83a32cd84cb06dcdde669e5e88e0a18a6a2638d5'
            'b567dc4b0085e619e57aab23effdd61f9b88c27638d835713e7951c5b80daaca875af68b689037a9e1c88da5bd310d2cfaaf588a64b5d7dee169b90f20d8bac0'
            '4f1f4bd2bfc1216bc34591e16ca4da4b26f4d8d98e0beb35dd4df217deb4eccebcbe1b82caea30c7de7d87ebe555a3ee2bf77816928d69c39f7ca78262407215'
            '10604efd04ad1b4b9c2f2ae58682c51664e4462328abc5f1b786d19176f72c972cc59fb7d38211c6292ade3a3f27cb424a1dcb993697f2e4640fa6fdb0738489'
            'c5c4615d4c50a55c6fea177a1bc73b13d1365224c4e102067e45ce956741cb95366ec3b16cdec8932083c923910794e9164b0f9a25a6b0e2f186557fac3e42f1')

backup=(
	'etc/ovenmediaengine/Server.xml'
	'etc/ovenmediaengine/Logger.xml'
)

prepare() {
	cd "OvenMediaEngine-${pkgver}"

	for patch in "${srcdir}/"ovenmediaengine-ffmpeg7-*.patch; do
		patch -Np1 -i "$patch"
	done

	# Assets take up ~13MB of space
	rm -Rf docs/.gitbook
	find docs -type f -name "*.md" -exec sed -i "s|.gitbook/assets|https://raw.githubusercontent.com/AirenSoft/OvenMediaEngine/v${pkgver}/docs/.gitbook/assets|g" {} +
}

build() {
	cd "OvenMediaEngine-${pkgver}/src"

	DISABLE_CUSTOM_LIBRARY_PATHS=true \
	make release
}

package()  {
	install -D -m0644 "ovenmediaengine.service" "${pkgdir}/usr/lib/systemd/system/ovenmediaengine.service"

	cd "OvenMediaEngine-${pkgver}"

	install -D -m0755 "src/bin/RELEASE/OvenMediaEngine" "${pkgdir}/usr/bin/OvenMediaEngine"

	install -D -m0644 "misc/conf_examples/Origin.xml" "${pkgdir}/etc/ovenmediaengine/Server.xml"
	install -D -m0644 "misc/conf_examples/Logger.xml" "${pkgdir}/etc/ovenmediaengine/Logger.xml"

	install -D -m0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	mkdir -p "${pkgdir}/usr/share/doc"
	cp -a docs "${pkgdir}/usr/share/doc/${pkgname}"
	cp -a misc/conf_examples "${pkgdir}/usr/share/doc/${pkgname}/conf_examples"
}
