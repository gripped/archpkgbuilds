pkgname=sea-orm-cli
pkgver=1.1.3
pkgrel=1
pkgdesc='An async & dynamic ORM for Rust'
url='https://www.sea-ql.org/SeaORM/'
license=(MIT Apache-2.0)
arch=(x86_64)
makedepends=(
  cargo
  git
)
depends=(
  glibc
  gcc-libs
  openssl
)
options=(!lto)
source=(
  "git+https://github.com/SeaQL/sea-orm.git#tag=${pkgver}"
  Cargo.lock
)
sha256sums=('e9c967ab3e87e8364429f568c9cc72f565a642923fb70b589a26eddd48de0264'
            '811b05bc7806b4df4f113f937c9c9e266d6035f66608ad5b11f10be4e032aa28')

prepare() {
    cd sea-orm/sea-orm-cli
    export RUSTUP_TOOLCHAIN=stable
    cp "${srcdir}/Cargo.lock" .
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd sea-orm/sea-orm-cli
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd sea-orm/sea-orm-cli
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd sea-orm
    install -Dm 755 "sea-orm-cli/target/release/${pkgname}" -t "${pkgdir}/usr/bin/" 
    install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
