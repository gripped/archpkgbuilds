# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
pkgname=sea-orm-cli
pkgver=1.1.14
pkgrel=1
pkgdesc='An async & dynamic ORM for Rust'
url='https://www.sea-ql.org/SeaORM/'
license=(MIT Apache-2.0)
arch=(x86_64)
makedepends=(
  cargo
  git
)
depends=(
  glibc
  gcc-libs
  openssl
)
options=(!lto)
source=(
  "git+https://github.com/SeaQL/sea-orm.git#tag=${pkgver}"
  Cargo.lock
)
sha256sums=('b2ceb19ea270c1af1b84bb9c2693f702fd18c27732e0945369431941d88742b5'
            '9a498ad1a429cb6b0d90182bb4be8e9633674967a75088f2679df6b0fe68a235')

prepare() {
    cd sea-orm/sea-orm-cli
    export RUSTUP_TOOLCHAIN=stable
    cp "${srcdir}/Cargo.lock" .
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd sea-orm/sea-orm-cli
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd sea-orm/sea-orm-cli
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd sea-orm
    install -Dm 755 "sea-orm-cli/target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
    install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
