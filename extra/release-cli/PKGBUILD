# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=release-cli
pkgver=0.20.0
pkgrel=1
pkgdesc="Gitlab Release CLI"
arch=(x86_64)
url="https://gitlab.com/gitlab-org/release-cli"
license=(MIT)
depends=(glibc)
makedepends=(go)
options=(!lto)
source=($pkgname-$pkgver.tar.gz::$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('e671ec46b69295de4925223f8fe3ea3f718e8350f20779273dfc5d8af28306f4ed0134218fec7dfb12c159e2624614a35a9df586bce6b4657b86832b93d63db1')
b2sums=('73ac07bcb3db65a7f01e035510c721e695a1bd94be0977ac5025fd3c131c636fbe3283393e9b871f63a04f751b3b08cf261983b18062889328ec7c36f10abf09')

prepare() {
  mkdir -vp $pkgname-v$pkgver/build
}

build() {
  cd $pkgname-v$pkgver
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  export GOFLAGS="-buildmode=pie  -mod=readonly -modcacherw"

  go build -ldflags "-compressdwarf=false -linkmode=external" -o build/$pkgname ./cmd/$pkgname
}

package() {
  install -vDm 755 $pkgname-v$pkgver/build/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname-v$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-v$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  cd $pkgname-v$pkgver/docs
  find . -type f -iname "*.md" -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
}
