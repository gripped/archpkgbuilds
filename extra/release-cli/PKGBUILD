# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=release-cli
pkgver=0.17.0
pkgrel=1
pkgdesc="Gitlab Release CLI"
arch=(x86_64)
url="https://gitlab.com/gitlab-org/release-cli"
license=(MIT)
depends=(glibc)
makedepends=(go)
source=($pkgname-$pkgver.tar.gz::$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('6925e7f5c00860506753eaa7b32cb1357e0736579bc32d39ed15ca6126b313018b2f5f355d02e1d297b6f607c7a5228ae1d514a07da54104ec348585b837afe6')
b2sums=('50f63ac7bca4be532af7e474274959057280f46976590790b61b2c8c4be634606163440f8ef416fc3d7caa8719e14f00ac7d45c2287995542a3a1a3a0ebe78a1')

prepare() {
  mkdir -vp $pkgname-v$pkgver/build
}

build() {
  cd $pkgname-v$pkgver
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  export GOFLAGS="-buildmode=pie  -mod=readonly -modcacherw"

  go build -ldflags "-compressdwarf=false -linkmode=external" -o build/$pkgname ./cmd/$pkgname
}

package() {
  install -vDm 755 $pkgname-v$pkgver/build/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname-v$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-v$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  cd $pkgname-v$pkgver/docs
  find . -type f -iname "*.md" -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
}
