# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Bert Peters <bert@bertptrs.nl>

pkgname=cargo-geiger
pkgver=0.12.0
pkgrel=1
pkgdesc="Detects usage of unsafe Rust in a Rust crate and its dependencies"
arch=('x86_64')
url="https://github.com/rust-secure-code/cargo-geiger"
license=('MIT' 'Apache-2.0')
depends=('cargo' 'openssl' 'curl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('8318108bfc6a5058134483faab45e9bf9d42f0b98bdf24b5ff42d41b7e060540')
options=('!lto')

prepare() {
  mv "$pkgname-$pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver/$pkgname"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver/$pkgname"
  install -Dm755 "../target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
