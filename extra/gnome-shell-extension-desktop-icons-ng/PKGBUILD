# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Matrix <thysupremematrix a_t tuta d_o_t io>
# Contributor: Yamada Hayao <hayao@fascode.net>

pkgname=gnome-shell-extension-desktop-icons-ng
_pkgname=desktop-icons-ng
pkgver=47.0.11
pkgrel=1
pkgdesc='Desktop icons extension for GNOME Shell'
arch=('any')
url='https://www.rastersoft.com/programas/ding.html'
license=('GPL-3.0-only')
depends=('cairo' 'dconf' 'gdk-pixbuf2' 'glib2' 'gjs' 'gnome-autoar' 'gnome-desktop' 'gnome-shell'
         'gtk3' 'nautilus' 'pango')
makedepends=('git' 'meson')
source=("git+https://gitlab.com/rastersoft/$_pkgname.git#tag=$pkgver"
        '0001-Make-the-preferences-window-scrollable-vertically.patch')
b2sums=('29b3fbab926e8e0f490d71c7827449ac97c80dda9a48ba2e4184b50b293cfd773b3e31d59e430c4bb1c621f518d43f7823456218f52302c7ba9edb0989be1315'
        'be32f930ab6f84f66c02d9c0ca2324b9ea945fe0c258b50682abdeeccc01ae77a9367bb509294ac68958c4ddc0918f3dd8852b45e2831a31ceb8999004406f5f')

prepare() {
  cd $_pkgname
  # https://gitlab.com/rastersoft/desktop-icons-ng/-/merge_requests/423
  patch -Np1 -i ../0001-Make-the-preferences-window-scrollable-vertically.patch
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
