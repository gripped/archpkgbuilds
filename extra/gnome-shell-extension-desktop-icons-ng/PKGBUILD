# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Matrix <thysupremematrix a_t tuta d_o_t io>
# Contributor: Yamada Hayao <hayao@fascode.net>

pkgname=gnome-shell-extension-desktop-icons-ng
_pkgname=desktop-icons-ng
pkgver=49.0.5
pkgrel=1
pkgdesc='Desktop icons extension for GNOME Shell'
arch=(any)
url='https://www.rastersoft.com/programas/ding.html'
license=(GPL-3.0-only)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  gjs
  glib2
  gnome-autoar
  gnome-desktop
  gnome-shell
  gtk3
  nautilus
  pango
)
makedepends=(
  git
  meson
)
source=(
  "git+https://gitlab.com/rastersoft/$_pkgname.git#tag=$pkgver"
  gnome-shell-extension-desktop-icons-ng-fix-dialogs.patch
)
b2sums=(
  c9e0f58217a11e8b2535f4a7f7b12411377f20562df94dd7434196d33767b96454165854e62008369c1653d9d7a963c6e971b2bc4c8de7cfbad39a5f7ccb7683
  6e59e70e46daeb4f35154c8feea711dba631e077f07b889d983c375d0a9a1a8ab43c8b331a20ccba77912b0ed777f8a096f9050c7c9263d7164c8a964ef3314b
)

prepare() {
  cd $_pkgname

  # https://gitlab.com/rastersoft/desktop-icons-ng/-/merge_requests/423
  git apply -3 ../gnome-shell-extension-desktop-icons-ng-fix-dialogs.patch
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
