# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Matrix <thysupremematrix a_t tuta d_o_t io>
# Contributor: Yamada Hayao <hayao@fascode.net>

pkgname=gnome-shell-extension-desktop-icons-ng
_pkgname=desktop-icons-ng
pkgver=48.1.0
pkgrel=1
pkgdesc='Desktop icons extension for GNOME Shell'
arch=(any)
url='https://www.rastersoft.com/programas/ding.html'
license=(GPL-3.0-only)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  gjs
  glib2
  gnome-autoar
  gnome-desktop
  gnome-shell
  gtk3
  nautilus
  pango
)
makedepends=(
  git
  meson
)
source=(
  "git+https://gitlab.com/rastersoft/$_pkgname.git#tag=$pkgver"
  gnome-shell-extension-desktop-icons-ng-fix-dialogs.patch
)
b2sums=(
  f15026ebe1f7bbdf3cdbb54bef743b7bfa451e1dd1d32033597e4bcf2672e59a58cd2556f55fc44f739a448b58b179a4d4b95365a40261cb89c702821f24406c
  e934aa5acd529ed84a2311dbba4535eb4aa73d890424ddc2068a59eade57b37d15f6a09978eca3af4fc2e376ee0142e28cc1e15c97bd6d988e8208c751cf6654
)

prepare() {
  cd $_pkgname

  # https://gitlab.com/rastersoft/desktop-icons-ng/-/merge_requests/423
  git apply -3 ../gnome-shell-extension-desktop-icons-ng-fix-dialogs.patch
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
