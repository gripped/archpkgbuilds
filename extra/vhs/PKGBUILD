# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=vhs
pkgver=0.10.0
pkgrel=1
pkgdesc='A tool for recording terminal GIFs'
arch=('x86_64')
url='https://github.com/charmbracelet/vhs'
license=('MIT')
depends=('glibc' 'ffmpeg' 'ttyd')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('629bcf06432b0fe7c28573cb880868d0db45bc04ed70eb12b521fc9bc8ad0ffac6e86cfdc262a1de26e8f4d88164bfa0a10be11cdd6b4408a64c241c9ac33b44')
b2sums=('fe8396a4db8839954580a9d96e437bbb563ac6f094bf793b8e59c0f350be66dfdabf398df2f5a3686eceed8c8e4cb8fcc31e552ba73ab2027bb0b66bfa491ee3')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.Version=v$pkgver \
    -X main.CommitSHA=$(git rev-parse HEAD) \
    -X main.CommitDate=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d)" \
    -o build \
    .

  # man page
  ./build/vhs man > build/vhs.1

  # completions
  for shell in bash fish zsh; do
    ./build/vhs completion "$shell" > "build/$shell-completion"
  done
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/vhs

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" build/vhs.1

  # completions
  install -vDm644 build/bash-completion "$pkgdir/usr/share/bash-completion/completions/vhs"
  install -vDm644 build/fish-completion "$pkgdir/usr/share/fish/vendor_completions.d/vhs.fish"
  install -vDm644 build/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_vhs"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
