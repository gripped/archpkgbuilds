# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=vhs
pkgver=0.9.0
pkgrel=1
pkgdesc='A tool for recording terminal GIFs'
arch=('x86_64')
url='https://github.com/charmbracelet/vhs'
license=('MIT')
depends=('glibc' 'ffmpeg' 'ttyd')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('333da54250e718a07d95bb8d157cf1d9647a1995b125fa410071977d667674f9d4dfe3588775705bc5cdabf42619f48b5e5e1e7c8d6202572bda4d0fc2907bee')
b2sums=('ab9c0fdfea7e9625ba32bab6c6eb400eaa9837b7e3e217989ce0f641025005031a0eada4531e865692d9e901aab69b1611467abcf579089a40f9b2caa2b55a9b')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.Version=v$pkgver \
    -X main.CommitSHA=$_commit \
    -X main.CommitDate=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d)" \
    -o build \
    .

  # man page
  ./build/vhs man > build/vhs.1

  # completions
  for shell in bash fish zsh; do
    ./build/vhs completion "$shell" > "build/$shell-completion"
  done
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/vhs

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" build/vhs.1

  # completions
  install -vDm644 build/bash-completion "$pkgdir/usr/share/bash-completion/completions/vhs"
  install -vDm644 build/fish-completion "$pkgdir/usr/share/fish/vendor_completions.d/vhs.fish"
  install -vDm644 build/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_vhs"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
