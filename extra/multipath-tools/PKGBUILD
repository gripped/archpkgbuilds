# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
_tag='6bb7330fca4a85c4f5abab0f9de5bd3519923f0a' # git rev-parse ${pkgver}
pkgver=0.9.7
pkgrel=1
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('x86_64')
url='https://github.com/opensvc/multipath-tools'
license=('GPL2')
depends=('libaio' 'device-mapper' 'json-c' 'liburcu' 'systemd-libs')
makedepends=('git' 'systemd')
source=("multipath-tools::git+https://github.com/opensvc/multipath-tools#tag=${_tag}")
sha256sums=('SKIP')

build() {
  cd "${pkgname}"

  make \
    prefix='/usr/' \
    bindir='/usr/bin' \
    LIB='lib' \
    FORTIFY_OPT=''
}

package() {
  cd "${pkgname}"

  make -j1 \
    DESTDIR="${pkgdir}" \
    prefix='/usr/' \
    bindir='/usr/bin' \
    LIB='lib' \
    install

  install -D -m0644 -t "${pkgdir}/usr/share/doc/${pkgname}" README*
}
