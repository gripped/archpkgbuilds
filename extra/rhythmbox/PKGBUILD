# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=3.4.8
pkgrel=3
pkgdesc="Music playback and management application"
url="https://gitlab.gnome.org/GNOME/rhythmbox"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  grilo
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk3
  gvfs
  gvfs-mtp
  hicolor-icon-theme
  json-glib
  libgirepository
  libgudev
  libmtp
  libnotify
  libpeas
  libsecret
  libsoup3
  libx11
  libxml2
  media-player-info
  pango
  python
  python-gobject
  tdb
  totem-plparser
)
makedepends=(
  brasero
  git
  gobject-introspection
  libdmapsharing
  libgpod
  lirc
  meson
  vala
  yelp-tools
  zeitgeist
)
checkdepends=(
  check
  xorg-server-xvfb
)
optdepends=(
  'brasero: Audio CD Recorder plugin'
  'grilo-plugins: Grilo media browser plugin'
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
  'libdmapsharing: DAAP Music Sharing plugin'
  'libgpod: Portable Players - iPod plugin'
  'lirc: LIRC plugin'
  'zeitgeist: Zeitgeist logging plugin'
)
source=("git+https://gitlab.gnome.org/GNOME/rhythmbox.git#tag=v$pkgver")
b2sums=('b4b8b61e7f6978a06982e9cc00fd49ec1011f94bfe6f696f19f228d651b1e2499f6ce3e53c1641c5120e65897be56e200aa7fccb159e0611079eaaf9e26dae90')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D daap=enabled
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs -t 3
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
