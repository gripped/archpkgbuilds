# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wilhem Barbier <wilhem.b@free.fr>

pkgname=raylib
pkgver=5.5
pkgrel=1
pkgdesc='Simple and easy-to-use game programming library'
arch=(x86_64)
url='https://www.raylib.com'
license=(Zlib)
makedepends=(cmake git libx11 libxcursor libxinerama libxrandr ninja vulkan-headers xorg-server-devel xorg-xinput)
source=("git+https://github.com/raysan5/raylib#tag=$pkgver")
b2sums=('7b03b06de247420d621fc75b317c83c9405f09bae3855dc369e0fcba9b395d318739ea284b98f3588a76c4f90f638878aba481b5296e39d72f81bf22c142b817')

build() {
  cmake \
    -B build \
    -D BUILD_EXAMPLES=OFF \
    -D BUILD_SHARED_LIBS=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D OpenGL_GL_PREFERENCE=GLVND \
    -D PLATFORM=Desktop \
    -D USE_EXTERNAL_GLFW=OFF \
    -D WITH_PIC=ON \
    -G Ninja \
    -S $pkgname \
    -W no-dev
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  # Include files that are used in the raylib examples
  for f in rcamera rgestures; do
    install -Dm644 $pkgname/src/$f.h "$pkgdir/usr/include/$f.h"
  done
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
