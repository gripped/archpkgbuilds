# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-minio
pkgver=7.2.15
pkgrel=1
pkgdesc='MinIO Python Library for Amazon S3 Compatible Cloud Storage'
arch=('any')
url='https://github.com/minio/minio-py'
license=('Apache-2.0')
depends=(
  'python'
  'python-argon2_cffi'
  'python-certifi'
  'python-pycryptodome'
  'python-typing_extensions'
  'python-urllib3'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  # 'minio'
  'python-pytest'
)
source=("$pkgname::git+$url.git#tag=$pkgver")
b2sums=('d73d42ee79c9ab2fb4acd8fc97c7967783fbd8c6647e4ed9ccdefe0ecb79387420d7cfe9174f2ed5dc67077b39cd060e2b01fff790f800c6feda1e6fd1ddb4d1')

prepare() {
  cd "$pkgname"
  # ln -sf /usr/bin/minio tests/functional/minio
}

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"
  pytest
  # TODO: Functional tests fail as of 7.2.10 with:
  #       [Errno 111] Connection refused
  # ./run_functional_tests.sh
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
