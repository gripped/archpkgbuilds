# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-minio
pkgver=7.2.14
pkgrel=1
pkgdesc='MinIO Python Library for Amazon S3 Compatible Cloud Storage'
arch=('any')
url='https://github.com/minio/minio-py'
license=('Apache-2.0')
depends=(
  'python'
  'python-argon2_cffi'
  'python-certifi'
  'python-pycryptodome'
  'python-typing_extensions'
  'python-urllib3'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  # 'minio'
  'python-pytest'
)
source=("$pkgname::git+$url.git#tag=$pkgver")
b2sums=('6efb1ccb95d69d021896d47dc0a50e1393c9145a9019ec2a97aa966c9f5233950d2588c5232594ba49d14808208048932c51c4e32f7be97c6cc8e69b8ffab172')

prepare() {
  cd "$pkgname"
  # ln -sf /usr/bin/minio tests/functional/minio
}

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"
  pytest
  # TODO: Functional tests fail as of 7.2.10 with:
  #       [Errno 111] Connection refused
  # ./run_functional_tests.sh
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
