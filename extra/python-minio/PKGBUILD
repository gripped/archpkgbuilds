# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-minio
pkgver=7.2.9
pkgrel=1
pkgdesc='MinIO Python Library for Amazon S3 Compatible Cloud Storage'
arch=('any')
url='https://github.com/minio/minio-py'
license=('Apache-2.0')
depends=(
  'python'
  'python-argon2_cffi'
  'python-certifi'
  'python-pycryptodome'
  'python-typing_extensions'
  'python-urllib3'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'minio'
  'python-pytest'
)
source=("$pkgname::git+$url.git#tag=$pkgver")
b2sums=('6228c5e36b766d94e9feaafad0d94e3e1909bdfd09dd980896f2aca87797deb1a4ecd7e5ee8a1164b64e9f9c9c1284362c9b9268aecf5ab2538d87da63c48b44')

prepare() {
  cd "$pkgname"

  ln -sf /usr/bin/minio tests/functional/minio
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m pytest

  ./run_functional_tests.sh
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
