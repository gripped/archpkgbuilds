# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-minio
pkgver=7.2.19
pkgrel=1
pkgdesc='MinIO Python Library for Amazon S3 Compatible Cloud Storage'
arch=('any')
url='https://github.com/minio/minio-py'
license=('Apache-2.0')
depends=(
  'python'
  'python-argon2_cffi'
  'python-certifi'
  'python-pycryptodome'
  'python-typing_extensions'
  'python-urllib3'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  # 'minio'
  'python-pytest'
)
source=("$pkgname::git+$url.git#tag=$pkgver")
b2sums=('cde8eed8fb96ede33ed66f9269ed8a3ce1e7c4c533957926f6754cb6f6a2127d6a0b595aef9740a94e3ffe77f0dd0ddf8f8020ac2a09794a05a3e4470a66d0fb')

prepare() {
  cd "$pkgname"
  # ln -sf /usr/bin/minio tests/functional/minio
}

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"
  pytest
  # TODO: Functional tests fail as of 7.2.10 with:
  #       [Errno 111] Connection refused
  # ./run_functional_tests.sh
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
