# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander 'z33ky' Hirsch <1zeeky@gmail.com>

pkgname=libabigail
pkgver=2.8
pkgrel=1
pkgdesc='ABI Generic Analysis and Instrumentation Library'
arch=('x86_64')
license=('Apache-2.0')
url='https://sourceware.org/libabigail/'
depends=(
  'glibc'
  'gcc-libs'
  'binutils'
  'libxml2'
  'libelf'
  'elfutils'
  'python'
  'xxhash'
  'xz'
)
makedepends=(
  'git'
  'python-sphinx'
)
source=("git://sourceware.org/git/libabigail.git#tag=libabigail-$pkgver")
sha512sums=('7322deb92019d3dc79f977d56c663c2e641f348ffcfa8e1a756d6fe455c40505f7e5a6cd3e941e33035d294c146ac0cc448165823ab3b25dc6b27eec12588252')
b2sums=('77c00046bb9cec6c67420f1a0feeafa32b1806379e5d5a7fb2d91e4fe60f45d7abd261ede19ca166fc5e948194e67b01f768cde25ba6dfe90e1aa567b81d3626')

prepare() {
  cd "$pkgname"

  autoreconf -fi
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix=/usr \
    --enable-bash-completion \
    --disable-manual \
    --disable-apidoc \
    --disable-static

  make

  # man pages
  make -C doc/manuals man
}

check() {
  cd "$pkgname"

  make check
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # man pages
  OLDIFS="$IFS"
  IFS="."
  pushd doc/manuals/man

  # delete Fedora-specific man page
  rm -vf fedabipkgdiff.1

  for filename in *; do
    read -ra PART <<< "$filename"
    section="${PART[1]}"
    install -vDm644 -t "$pkgdir/usr/share/man/man${section}" "$filename"
  done
  IFS="$OLDIFS"
  unset OLDIFS
  popd

  # bash completion
  cd bash-completion
  for filename in abicompat abidiff abidw abilint abipkgdiff; do
    install -vDm644 -t "$pkgdir/usr/share/bash-completion/completions" "$filename"
  done
}
