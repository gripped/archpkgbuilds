# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander 'z33ky' Hirsch <1zeeky@gmail.com>

pkgname=libabigail
pkgver=2.9
pkgrel=1
pkgdesc='ABI Generic Analysis and Instrumentation Library'
arch=(x86_64)
url='https://sourceware.org/libabigail/'
license=(Apache-2.0)
depends=(
  glibc
  binutils
  libxml2
  libelf
  libgcc
  libstdc++
  elfutils
  python
  xxhash
  xz
)
makedepends=(
  git
  python-sphinx
)
source=("$pkgname::git://sourceware.org/git/libabigail.git#tag=libabigail-$pkgver")
sha512sums=('41ea967448788353c6048e1d45ce4593d7c4f44d73381976cd23811e1d082f8e1f1ffb6d7918e52cfc7afcebbee2ad55523a207955523fecd5fe43e5d4516146')
b2sums=('a9845c22c3d724208866f7440cf1ed2200bb5dfee94e47a663039767b5c752b5f7e7e6fb250c370d186452456cfabf755220e33410260703a61b0f1f32ac2c2d')

prepare() {
  cd "$pkgname"

  autoreconf -fi
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix=/usr \
    --enable-bash-completion \
    --disable-manual \
    --disable-apidoc \
    --disable-static

  make

  # man pages
  make -C doc/manuals man
}

check() {
  cd "$pkgname"

  make check
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # man pages
  OLDIFS="$IFS"
  IFS="."
  pushd doc/manuals/man

  # delete Fedora-specific man page
  rm -vf fedabipkgdiff.1

  for filename in *; do
    read -ra PART <<< "$filename"
    section="${PART[1]}"
    install -vDm644 -t "$pkgdir/usr/share/man/man${section}" "$filename"
  done
  IFS="$OLDIFS"
  unset OLDIFS
  popd

  # bash completion
  cd bash-completion
  for filename in abicompat abidiff abidw abilint abipkgdiff; do
    install -vDm644 -t "$pkgdir/usr/share/bash-completion/completions" "$filename"
  done
}
