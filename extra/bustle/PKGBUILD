# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=bustle
pkgver=0.11.0
pkgrel=3
pkgdesc='Draw sequence diagrams of D-Bus traffic'
arch=(x86_64)
url='https://apps.gnome.org/Bustle/'
license=(LGPL-2.1-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/bustle.git#tag=$pkgver")
b2sums=(0bbe84f3e404f1e72f9e13c3b56ab1534b92cfa79d5e4cf8ddfa5b346def946fcfacfed1abba24b3cb3155bdcd0731d16b207153910442f9bce4e5aa760254a6)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # Remove new-window action entry
  # https://gitlab.gnome.org/World/bustle/-/merge_requests/149
  git cherry-pick -n 970db29f959b7b81b6d091bc84cc2d0429cbed75

  # Add D-Bus service file
  # https://gitlab.gnome.org/World/bustle/-/merge_requests/151
  git cherry-pick -n fefded8ae1c80b861fadaf801b2c14945335a2d6

  # Add "new-window" command-line option
  # https://gitlab.gnome.org/World/bustle/-/merge_requests/150
  git cherry-pick -n 93b0beee8e7a3576ce01ca6626900a677da901ea
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
