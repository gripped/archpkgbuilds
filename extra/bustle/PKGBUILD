# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=bustle
pkgver=0.10.0
pkgrel=1
pkgdesc='Draw sequence diagrams of D-Bus traffic'
arch=(x86_64)
url='https://gitlab.gnome.org/World/bustle'
license=(LGPL-2.1-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/bustle.git#tag=$pkgver")
b2sums=(2ab4ab0d5a27acf032f572a6179382b45898892cb1e9c9caa899cd8677a50f76c579f7bca6d4704a894c43d6e36606c855b526f4c8d709de9333caa3842fff59)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # note: test did not panic as expected
  # https://gitlab.gnome.org/World/bustle/-/issues/36
  sed -i '/#\[should_panic/d' src/i18n.rs
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
