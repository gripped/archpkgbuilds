# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=bustle
pkgver=0.9.2
pkgrel=1
pkgdesc='Draw sequence diagrams of D-Bus traffic'
arch=(x86_64)
url='https://gitlab.gnome.org/World/bustle'
license=(LGPL-2.1-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/bustle.git#tag=$pkgver")
b2sums=(c937bdf558eaae0e0d5345664d2fe5c2ff47fd0cdfa2976dc10f4237488494e19d24d062c3f1858eb243c8e5b5ae830e02fdd11018c9989d4fc4dcac1e5371c3)

build() {
  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
