# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=bustle
pkgver=0.13.0
pkgrel=1
pkgdesc='Draw sequence diagrams of D-Bus traffic'
arch=(x86_64)
url='https://apps.gnome.org/Bustle/'
license=(LGPL-2.1-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/bustle.git#tag=$pkgver")
b2sums=(7abf5259ea30c53865dcdc575bfb213906d3bb094445b6ecd4a1a96bb479190bac5b0c101ee1b1e8f688ff839df3b557c1c780df5098445843db90b3f2738147)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
