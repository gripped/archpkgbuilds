# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: acxz <akashpatel2008 at yahoo dot com>
pkgname=rocprofiler
pkgver=6.4.3
pkgrel=1
pkgdesc="ROC profiler library. Profiling with perf-counters and derived metrics."
arch=('x86_64')
url='https://rocm.docs.amd.com/projects/rocprofiler/en/latest/index.html'
license=('MIT')
depends=('rocm-core' 'hip-runtime-amd' 'python' 'python-lxml' 'hsa-amd-aqlprofile')
makedepends=('cmake' 'git' 'rocm-llvm' 'hsa-rocr' 'rocm-dbgapi' 'gtest' 'rocm-smi-lib'
             'python-cppheaderparser' 'python-yaml' 'python-barectf')
_git='https://github.com/ROCm/rocprofiler'
_roctracer='https://github.com/ROCm/roctracer'
source=("$pkgname::git+$_git#tag=rocm-$pkgver"
        "$pkgname-roctracer::git+$_roctracer#tag=rocm-$pkgver"
        "${pkgname}-perfetto::git+https://github.com/google/perfetto")
sha256sums=('82f522d2565bc3579fa2f3a76a26052a30964dd00e704fa27fdd774c4badafa2'
            'f1716e33df2a29197ebd19cbef62377171f92030ef16a44e62b850ef3cc0663e'
            'SKIP')
options=(!lto)

prepare() {
  cd "${pkgname}"
  git submodule init
  git config submodule."perfetto".url "${srcdir}/${pkgname}"-perfetto
  git -c protocol.file.allow=always submodule update --init --recursive
}

build() {
  local cmake_args=(
    -Wno-dev
    -B build
    -S "$pkgname"
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
    -D ROCPROFILER_BUILD_TESTS=OFF
    -D GPU_TARGETS="gfx803 gfx900 gfx906 gfx908 gfx90a gfx940 gfx941 gfx942 gfx1030 gfx1100 gfx1101 gfx1102"
    -D PROF_API_HEADER_PATH="$srcdir/$pkgname-roctracer/inc/ext"
  )
  HIP_CLANG_PATH=/opt/rocm/llvm/bin \
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
