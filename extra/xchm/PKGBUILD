# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=xchm
pkgver=1.37
pkgrel=1
pkgdesc='Viewer for CHM files (GUI front-end to chmlib)'
arch=('x86_64')
url='https://github.com/rzvncj/xCHM'
license=('GPL2')
depends=('chmlib' 'wxwidgets-gtk3')
source=("https://github.com/rzvncj/xCHM/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c6f059a70cb97c49f5dbe16165c25afdb2e7ea3964027dcc1d2eebfc2608a786')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
