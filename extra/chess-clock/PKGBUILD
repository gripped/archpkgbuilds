# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=chess-clock
pkgver=0.6.2
pkgrel=1
pkgdesc='Simple application to provide time control for over-the-board chess games'
arch=(any)
url='https://apps.gnome.org/Chessclock/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gsound
  gtk4
  hicolor-icon-theme
  libadwaita
  python
  python-gobject
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://gitlab.gnome.org/World/chess-clock.git?signed#tag=v$pkgver")
b2sums=(205240e488d686f2ee07ce30b37bdf4f3772d409747cd6481edd4faa81348c0007efad8ee28188f11525d9405c86e7082ad219fdd64baa931c067a6695a54e9b)
validpgpkeys=(B9386554B7DD266BCB8E29A990F0C9B18A6B4A19) # Evangelos Ribeiro Tzaras <devrtz@fortysixandtwo.eu>

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
