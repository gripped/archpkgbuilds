# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Sam L. Yes <samlukeyes123 at gmail dot com>
# Contributor: Andre Klitzing <aklitzing () gmail () com>

pkgname=msitools
pkgver=0.106
pkgrel=2
pkgdesc="Set of programs to inspect and build Windows Installer (.MSI) files"
url="https://wiki.gnome.org/msitools"
arch=(x86_64)
license=(
  GPL-2.0-or-later  # utilities
  LGPL-2.1-or-later # library
)
depends=(
  bash
  gcab
  gcc-libs
  glib2
  glibc
  libgsf
  libxml2
)
makedepends=(
  bats
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
provides=(libmsi-1.0.so)
source=(
  "git+https://gitlab.gnome.org/GNOME/msitools.git?signed#tag=v$pkgver"
  0001-Use-system-bats.patch
)
b2sums=('91b9a76cd34d1de541fad46e242de52ad2ba01a40f5f8b79f27c857150ba8edd2fa87a74659274f1581103f1050a928c9e084bbd9a72f89decd5a8d9bb282666'
        '79ce5a1f856b6163a000938d9c140962b8a68a52cfede41b6ca10e1379e9aa0b870d647f450a1b2dad98f60920a173e0ad3f28844a5de039274db47bf080e11e')
validpgpkeys=(
  87A9BD933F87C606D276F62DDAE8E10975969CE5 # Marc-Andr√© Lureau <marcandre.lureau@redhat.com>
)

prepare() {
  cd msitools

  # Fixate version
  local v="$(build-aux/git-version-gen .tarball-version)"
  echo "$v" > .tarball-version

  # Use system bats (bash-bats)
  git apply -3 ../0001-Use-system-bats.patch
}

build() {
  arch-meson msitools build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 msitools/copyright -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
