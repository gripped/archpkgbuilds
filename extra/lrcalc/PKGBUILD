# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lrcalc
pkgver=2.1
pkgrel=8
pkgdesc="Littlewood-Richardson calculator"
arch=(x86_64)
url="https://math.rutgers.edu/~asbuch/lrcalc/"
license=(GPL-3.0-or-later)
depends=(glibc)
makedepends=(cython
             python-build
             python-installer
             python-setuptools)
source=("https://math.rutgers.edu/~asbuch/lrcalc/$pkgname-$pkgver.tar.gz")
sha256sums=('996ac00e6ea8321ef09b34478f5379f613933c3254aeba624b6419b8afa5df57')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make

# install to a temp dir to build python bindings
  make DESTDIR="$PWD"/tmp-install install

  cd python
  export CFLAGS+=" -I${srcdir}/$pkgname-$pkgver/tmp-install/usr/include" LDFLAGS+=" -L${srcdir}/$pkgname-$pkgver/tmp-install/usr/lib"
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd python
  python -m installer --destdir="$pkgdir" dist/*.whl
}
