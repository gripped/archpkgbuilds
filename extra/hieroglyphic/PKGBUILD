# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=hieroglyphic
pkgver=2.2.0
pkgrel=1
pkgdesc='Search through over 1000 different LaTeX symbols by sketching'
arch=(x86_64)
url='https://apps.gnome.org/Hieroglyphic/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=("git+https://github.com/FineFindus/Hieroglyphic.git#tag=v$pkgver")
b2sums=(9308afa07166fe033eb3eae7d16d3493adb63cf6e559d79a7ad1795542e427271b3fb48d82080345158f3827e38108125faaef74347c26c3cb8cb15311f28af0)

prepare() {
  cd Hieroglyphic

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Hieroglyphic build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
