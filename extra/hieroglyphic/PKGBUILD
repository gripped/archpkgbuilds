# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=hieroglyphic
pkgver=2.1.0
pkgrel=2
pkgdesc='Search through over 1000 different LaTeX symbols by sketching'
arch=(x86_64)
url='https://apps.gnome.org/Hieroglyphic/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=(
  "git+https://github.com/FineFindus/Hieroglyphic.git#tag=v$pkgver"
  hieroglyphic-dbus-activatable.patch
)
b2sums=(
  d30a3b29c65531a9a0480de484418df28225000e38db300c924c581142974a596aaa693993ab3fad0ad3577e8596c62ef3319e170c2862bd680f8dc9eae652db
  e6e67ae4a261f749e4ea14dabbf237fe6d7e1f2595526567174505aaf2e90c894960c38d59fd26b31966f71d66572f4400cd653ca7531b0334e4ade4e51dd799
)

prepare() {
  cd Hieroglyphic

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://github.com/FineFindus/Hieroglyphic/pull/41
  git apply -3 ../hieroglyphic-dbus-activatable.patch
}

build() {
  arch-meson Hieroglyphic build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
