# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gst-plugins-espeak
pkgver=0.6.0
pkgrel=1
pkgdesc='Use espeak as a sound source for gstreamer'
arch=(x86_64)
url='https://github.com/sugarlabs/gst-plugins-espeak'
license=(LGPL-2.0-or-later)
depends=(
  espeak-ng
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
)
makedepends=(git)
source=("git+https://github.com/sugarlabs/$pkgname.git#tag=v$pkgver")
b2sums=(48bfc52cc76e37c3cd1e03c58e9c58268600f56bd3ea210b503c8cddd892e16c8d5a8af6c730f6593742d788f9485bdfbf1d72d7c88c7cdd2c361688c6b331c0)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
