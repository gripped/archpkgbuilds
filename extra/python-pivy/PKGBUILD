# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Grey Christoforo

_name=pivy
pkgname=python-pivy
epoch=1
pkgver=0.6.9
pkgrel=1
pkgdesc='Bindings to Coin3D'
arch=(x86_64)
license=(ISC)
url='https://github.com/coin3d/pivy'
depends=(coin gcc-libs glibc pyside6 python python-numpy qt6-base soqt)
makedepends=(cmake git glu swig)
source=(git+https://github.com/coin3d/pivy#tag=$pkgver)
b2sums=('b87ca2b04b39ebe8a8c2796e27ff05abd8fde474b670ee07d288128ee7feda55c3c40924a704f5bc01fd16911a0c91cee480c2771ac684a63df988e5792da6e7')

build() {
  cd $_name

  # NOTE: out-of-tree build broken: https://github.com/coin3d/pivy/issues/72
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D PIVY_USE_QT6=ON \
    -W no-dev
  make -C build
}

package() {
  cd $_name

  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # compile Python bytecode as cmake does not do that for us
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
