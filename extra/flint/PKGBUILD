# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>
# Contributor: Alessandro "jakedust" Andrioni <jakedust@gmail.com>

pkgname=flint
pkgver=3.3.0
pkgrel=1
pkgdesc='A C library for doing number theory'
arch=(x86_64)
url='http://www.flintlib.org'
license=(LGPL-3.0-only)
depends=(cblas
         glibc
         gmp
         mpfr)
makedepends=(cmake
             git
             python)
replaces=(antic
          arb)
source=(git+https://github.com/flintlib/flint#tag=v$pkgver)
sha256sums=('2dc6120d4d5624c8fa657e2f66a48177db2e46b61f2921330e817483a6900a2d')

prepare() {
  cd $pkgname
  sed -e 's|NOT WIN32|FALSE|' -i CMakeLists.txt
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_ARCH=NO \
    -DENABLE_AVX2=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
