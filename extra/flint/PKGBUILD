# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>
# Contributor: Alessandro "jakedust" Andrioni <jakedust@gmail.com>

pkgname=flint
pkgver=3.1.0
pkgrel=2
pkgdesc='A C library for doing number theory'
arch=(x86_64)
url='http://www.flintlib.org'
license=(LGPL-3.0-only)
depends=(cblas
         glibc
         gmp
         mpfr)
makedepends=(cmake
             python)
replaces=(antic
          arb)
source=(http://www.flintlib.org/flint-$pkgver.tar.gz)
sha256sums=('b30df05fa81de49c20d460edccf8c410279d1cf8410f2d425f707b48280a2be2')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DHAS_FLAG_GCC_MARCH_NATIVE=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
