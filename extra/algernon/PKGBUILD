# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=algernon
pkgver=1.17.4
pkgrel=1
pkgdesc='Web server with Lua, Markdown, QUIC, Redis and PostgreSQL support'
arch=(x86_64)
url='https://algernon.roboticoverlords.org/'
license=(BSD-3-Clause)
makedepends=(git go)
optdepends=('mariadb: For using the MariaDB/MySQL database backend'
            'ollama: For using Ollama from Lua or .prompt files'
            'postgresql: For using the PostgreSQL database backend'
            'valkey: For using the Redis database backend')
source=("git+https://github.com/xyproto/algernon#tag=v$pkgver")
b2sums=('16d4ce174e049824c3907f60549d77709b274f3efcd02c50cf3a9574850863c1184c83d877993f2a94df6dde69db3035948b6a09cb3155dffb2eb75313868672')

prepare() {
  cd $pkgname
  go build -buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags="-s -w -linkmode=external -extldflags '$LDFLAGS'" -buildvcs=false
}

package() {
  cd $pkgname
  install -Dm755 algernon "$pkgdir/usr/bin/algernon"
  install -d "$pkgdir/usr/share/doc/$pkgname/samples"
  cp -r samples "$pkgdir/usr/share/doc/$pkgname/samples"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd system
  install -Dm644 logrotate "$pkgdir/etc/logrotate.d/algernon"
  install -Dm644 serverconf.lua "$pkgdir/etc/algernon/serverconf.lua"
  install -Dm644 algernon_dev.service "$pkgdir/usr/share/doc/$pkgname/algernon.service.example"

  cd ../desktop
  install -Dm755 mdview "$pkgdir/usr/bin/mdview"
  install -Dm644 algernon.desktop "$pkgdir/usr/share/applications/algernon.desktop"
  install -Dm644 algernon_md.desktop "$pkgdir/usr/share/applications/algernon_md.desktop"
  install -Dm644 markdown.png "$pkgdir/usr/share/pixmaps/markdown.png"
}
