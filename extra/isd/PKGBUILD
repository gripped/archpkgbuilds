# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=isd
pkgver=0.6.0
pkgrel=1
pkgdesc='An interactive systemd TUI'
arch=('any')
url='https://isd-project.github.io/isd/'
license=(GPL-3.0-or-later)
depends=(
  systemd
  python
  python-xdg-base-dirs
  python-pfzy
  python-textual
  python-pydantic
  python-pydantic-settings
  python-rich
  hicolor-icon-theme
)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
  python-editables
  python-setuptools
  python-wheel
)
source=("$pkgname::git+https://github.com/isd-project/isd#tag=v$pkgver")
sha512sums=('64cc57e6ac6c103cf9527ad55c3cf2346f5e90cc87db0e028cff41ef7a9e742d8ace57bdb2852a831e5b81fe2fd01ced73ba2364faf8b6a7faacf08236553cef')
b2sums=('4b348e4017b34840e0d25ece9274ebe480c3059849c354d652b178385648a54f1108e987a4edeb1a8aafec487653c3dda99a49f79fdee5f0ee937fe786b22c44')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
