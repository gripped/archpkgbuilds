# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=isd
pkgver=0.6.1
pkgrel=3
pkgdesc='An interactive systemd TUI'
arch=('any')
url='https://kainctl.github.io/isd/'
license=(GPL-3.0-or-later)
depends=(
  systemd
  python
  python-xdg-base-dirs
  python-pfzy
  python-textual
  python-pydantic
  python-pydantic-settings
  python-rich
  hicolor-icon-theme
)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
  python-editables
  python-setuptools
  python-wheel
)
source=("$pkgname::git+https://github.com/kainctl/isd#tag=v$pkgver")
sha512sums=('cbb1d783a1883814b7b32b3271e39e0ff055ad038002ed35e54c56a05b86c627c095447876ef775f1e3a385c0719d824e19ad547285fd98866b2a7b3e50b06b6')
b2sums=('4b414d5d8ef49481c24492301b21940348ccd3b6001c5b501a36ccd0adacc0719691482432df9350005aafc78a1b7f5684c7aa1127bf7dc4eb0f8a50dde7bbd0')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
