# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=isd
pkgver=0.5.1
pkgrel=1
pkgdesc='An interactive systemd TUI'
arch=('any')
url='https://isd-project.github.io/isd/'
license=(GPL-3.0-or-later)
depends=(
  systemd
  python
  python-xdg-base-dirs
  python-pfzy
  python-textual
  python-pydantic
  python-pydantic-settings
  python-rich
  hicolor-icon-theme
)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
  python-editables
  python-setuptools
  python-wheel
)
source=("$pkgname::git+https://github.com/isd-project/isd#tag=v$pkgver")
sha512sums=('51be85fdb87c479df4480b4408014d64446fe64348b5723095fa7588c7d4f7bdd297010231e983f119673ac249d812dbce2544776b4267592b6be4fb5022da44')
b2sums=('d24b7379de7be2607d826123261f8c3d5b16289376bfeb645c8b04663fbc58c247f24550123d0103758de55ce9e23231d11988851b27d3eaeacf3f4f99b82544')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
