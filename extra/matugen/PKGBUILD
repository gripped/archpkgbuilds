# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=matugen
pkgver=3.1.0
pkgrel=1
pkgdesc='A material you color generation tool'
arch=(x86_64)
url="https://github.com/InioX/$pkgname"
license=(GPL-2.0-only)
depends=(gcc-libs
         glibc)
makedepends=(cargo)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('f00b7bd284ef6184a0c94d54cb745230c6e58f26012491eaf7f7bcea0f3a489b')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
}

_srcenv() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
}

build() {
	_srcenv
	cargo build --frozen --release --all-features
}

check() {
	_srcenv
	cargo test --frozen --all-features
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
