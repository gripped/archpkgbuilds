# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=labplot
pkgver=2.12.0
pkgrel=1
arch=(x86_64)
pkgdesc='KDE Application for interactive graphing and analysis of scientific data'
url='https://labplot.kde.org/'
license=(GPL-2.0-or-later)
depends=(cantor
         cblas
         cfitsio
         discount
         fftw
         gcc-libs
         glibc
         gsl
         hdf5
         karchive
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         kparts
         ktextwidgets
         kuserfeedback
         kwidgetsaddons
         kxmlgui
         libcerf
         libixion
         libmatio
         liborcus
         liborigin
         lz4
         netcdf
         poppler-qt6
         purpose
         qt-advanced-docking-system
         qt6-base
         qt6-mqtt
         qt6-serialport
         qt6-svg
         qxlsx
         readstat
         syntax-highlighting
         vector-blf
         zlib
         zstd)
makedepends=(boost
             dbc-parser-cpp
             eigen
             extra-cmake-modules
             kdoctools
             knewstuff)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz
        fix-qxlsx.patch
        fix-qt-advanced-docking-system.patch)
sha256sums=('25f1751261ec2d11312f0179049ea1f16db33b3a89800dc572d68ad8d267afe7'
            'c9bd0853e11133c25c9cb53b6a5c11d7cb174f96811ee877bde1f79d779f6ea0'
            '5ccef2f374d230d034781022c27764d9d7b12e4da57b7c7fd5496339c509f81d')

prepare() {
  patch -d $pkgname-$pkgver -p1 < fix-qxlsx.patch
  patch -d $pkgname-$pkgver -p1 < fix-qt-advanced-docking-system.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DENABLE_TESTS=OFF \
    -DLOCAL_DBC_PARSER=OFF \
    -DLOCAL_VECTOR_BLF=OFF
  cmake --build build
}

package(){
  DESTDIR="$pkgdir" cmake --install build
}
