# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=labplot
pkgver=2.12.1
pkgrel=8
arch=(x86_64)
pkgdesc='KDE Application for interactive graphing and analysis of scientific data'
url='https://labplot.kde.org/'
license=(GPL-2.0-or-later)
depends=(cantor
         cblas
         cfitsio
         discount
         fftw
         gcc-libs
         glibc
         gsl
         hdf5
         karchive
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         kparts
         ktextwidgets
         kuserfeedback
         kwidgetsaddons
         kxmlgui
         libcerf
         libixion
         libmatio
         liborcus
         liborigin
         lz4
         netcdf
         poppler-qt6
         purpose
         qt-advanced-docking-system
         qt6-base
         qt6-mqtt
         qt6-serialport
         qt6-svg
         qxlsx
         readstat
         syntax-highlighting
         vector-blf
         zlib
         zstd)
makedepends=(boost
             dbc-parser-cpp
             eigen
             extra-cmake-modules
             kdoctools
             knewstuff)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz
        https://invent.kde.org/education/labplot/-/commit/ee17e765.patch
        https://invent.kde.org/education/labplot/-/commit/c2db2ec2.patch
        eigen-5.patch)
sha256sums=('e28155bfddf40e5b6f7c4791313556d1e48138044f216f210d515b9f6d6c106a'
            '547aa708ad7667c5c0392837cfd41cae7f8d98ae804e00b010a8cbcba0be7b41'
            '09082b162b3bbfcafa5fa5db51ddde4ba667749cb0d790524f4330f1140e7797'
            'd7812bd271822833ae8772ca214be25735562efe7f7813ce0bb67b3123b0db45')

prepare() {
  patch -d $pkgname-$pkgver -p1 < ee17e765.patch # liborcus 0.21 support
  patch -d $pkgname-$pkgver -p1 < c2db2ec2.patch # fix build with Qt 6.10
  patch -d $pkgname-$pkgver -p1 < eigen-5.patch # Fix build with eigen 5
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DENABLE_TESTS=OFF \
    -DLOCAL_DBC_PARSER=OFF \
    -DLOCAL_VECTOR_BLF=OFF
  cmake --build build
}

package(){
  DESTDIR="$pkgdir" cmake --install build
}
