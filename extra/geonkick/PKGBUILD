# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=geonkick
pkgname=(geonkick geonkick-{common,lv2,standalone,vst3})
pkgver=3.3.2
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=(x86_64)
url="https://geonkick.org/"
_url="https://gitlab.com/Geonkick-Synthesizer/geonkick"
license=(GPL-3.0-or-later)
makedepends=(
  cairo
  cmake
  gcc-libs
  glibc
  gtkmm3
  hicolor-icon-theme
  jack
  libsndfile
  libx11
  libxkbcommon
  libxkbcommon-x11
  lv2
  pango
  rapidjson
  vst3sdk
  xcb-util
  xcb-util-cursor
  xcb-util-keysyms
)
checkdepends=(lv2lint)
source=($_url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('9144bf449ccc6e5f37af35ba4a7b32d8faaa7b0ed963b08ed0f2bf617cd632bf5c376b5908071d3b351e7fd5b853c4a72547d9407f87cc3f67082b17b1527d20')
b2sums=('e33060ce4cbf026500772896fad9c0e7c33f8b491c2afe56d3b006612008beac397f2ee881f5db4b82a17fb552d97b694f77b1919ff91cd4fe549ccfce7c443e')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D VST3_SDK_PATH=/usr/src/vst3sdk
    -S $pkgname-v$pkgver
    -W no-dev
  )

  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  local lv2lint_options=(
    -Mpack
    -t "Plugin Symbols"  # https://gitlab.com/Geonkick-Synthesizer/geonkick/-/issues/5
    -t "UI Symbols"  # https://gitlab.com/Geonkick-Synthesizer/geonkick/-/issues/5
    -I build/src/plugin/lv2/
    "http://geonkick.org/geonkick"
  )

  cp -av $pkgname-v$pkgver/src/plugin/lv2/$pkgname.lv2/*.ttl build/src/plugin/lv2/
  lv2lint "${lv2lint_options[@]}"
  rm -v build/src/plugin/lv2/*.ttl
}

package_geonkick() {
  depends=(
    $pkgbase-{lv2,standalone,vst3}=$pkgver
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-v$pkgver/data/$pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgname-v$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"

  (
    cd "$pkgdir"
    _pick $pkgbase-common usr/share/$pkgbase
    _pick $pkgbase-common usr/share/doc

    _pick $pkgbase-lv2 usr/lib/lv2

    _pick $pkgbase-standalone usr/bin
    _pick $pkgbase-standalone usr/share/{applications,icons,man,mime}

    _pick $pkgbase-vst3 usr/lib/vst3
  )
}

package_geonkick-common() {
  pkgdesc+=" - common files"

  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    libsndfile
    libx11
    lv2-host
  )
  
  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-standalone() {
  pkgdesc+=" - JACK standalone"
  groups=(pro-audio)
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    hicolor-icon-theme
    jack
    libsndfile
    libx11
  )

  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-vst3() {
  pkgdesc+=" - VST3 plugin"
  groups=(
    pro-audio
    vst3-plugins
  )
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    libsndfile
    vst3-host
  )
  
  mv -v $pkgname/* "$pkgdir"
}
