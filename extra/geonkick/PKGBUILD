# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=geonkick
pkgname=(geonkick geonkick-{common,lv2,standalone})
pkgver=2.10.0
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=(x86_64)
url="https://github.com/free-sm/geonkick"
license=(GPL-3.0-or-later)
makedepends=(
  cairo
  cmake
  gcc-libs
  glibc
  hicolor-icon-theme
  jack
  libsndfile
  libx11
  lv2
  rapidjson
)
checkdepends=(lv2lint)
# issues with visible symbols during check() if LTO is enabled: https://github.com/free-sm/geonkick/issues/164
options=(!lto)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('0695a0613dce200c538d497383f8e39d4066ad83c94709c43a95683dcb621f0a9b215295d2843d4d20b6c96fd300acbaf14717aea9be6b4905658fbff75828fb')
b2sums=('479902b7ea8d6754a493339c8d943c125825603d963bea5b43867f4bf427068836a7950c99366e5d988b69e348b377ef043e946c13b9e1d55fa67b1e16dfd038')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cp -av build/plugin/lv2/*.so $pkgname-$pkgver/plugin/lv2/$pkgname.lv2/
  lv2lint -Mpack -I $pkgname-$pkgver/plugin/lv2/$pkgname.lv2/ "http://geontime.com/geonkick"
  rm -v $pkgname-$pkgver/plugin/lv2/$pkgname.lv2/*.so
}

package_geonkick() {
  depends=(
    $pkgbase-lv2=$pkgver
    $pkgbase-standalone=$pkgver
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/data/$pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"

  (
    cd "$pkgdir"
    _pick $pkgbase-common usr/share/$pkgbase
    _pick $pkgbase-common usr/share/doc

    _pick $pkgbase-lv2 usr/lib/lv2

    _pick $pkgbase-standalone usr/bin
    _pick $pkgbase-standalone usr/share/{applications,icons,man,mime}
  )
}

package_geonkick-common() {
  pkgdesc+=" - common files"

  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    libsndfile
    libx11
    lv2-host
  )
  
  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-standalone() {
  pkgdesc+=" - JACK standalone"
  groups=(pro-audio)
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    hicolor-icon-theme
    jack
    libsndfile
    libx11
  )
  
  mv -v $pkgname/* "$pkgdir"
}
