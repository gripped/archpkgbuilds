# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-bopomofo
pkgver=0.0.0.20251124
_commit=78325817b095c98db8f41d491f8e8be2aa4fdaaa
pkgrel=2
pkgdesc="Bopomofo input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-bopomofo"
license=('LGPL-3.0-only')
# Rime will regenerate yaml&prism bin, which makes terra-pinyin required on runtime (for translator)
# rime-cangjie is dependency for reverse lookup
depends=('rime-cangjie' 'rime-terra-pinyin')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-bopomofo.git#commit=$_commit")
sha512sums=('5b12b93618208b2439ff89dc906b80e2d5a489a4d244795a32b273808ffbe2f02506da6c14f1b5e2602f518d944bc1811edd66a2d1aeb5777ed566a8e017933a')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay rime-terra-pinyin | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/terra_pinyin*
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
