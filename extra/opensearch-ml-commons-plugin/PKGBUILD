# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-ml-commons-plugin
pkgver=3.5.0.0
pkgrel=1
_opensearchver=3.5.0
_jdkver=21
pkgdesc="OpenSearch Machine Learning Commons Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/ml-commons-plugin/index/"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/ml-commons/archive/${pkgver}.tar.gz"
  disable-failed-test.patch
)
sha256sums=('b9473f4f56292b59aa724be0f8b04eba48f8f7fa64ac5d98c9bb6f46d8721dd6'
            'f765fc3a4fa60df1339bfabc898fa1b879a860e636e9fa93893ca3185990f167')

prepare() {
  cd "ml-commons-${pkgver}"
  patch -Np1 -i ../disable-failed-test.patch
}

build() {
  cd "ml-commons-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew build \
    --exclude-task ":opensearch-ml-algorithms:jacocoTestReport" \
    --exclude-task ":opensearch-ml-algorithms:jacocoTestCoverageVerification" \
    --exclude-task ":opensearch-ml-common:jacocoTestReport" \
    --exclude-task ":opensearch-ml-common:jacocoTestCoverageVerification" \
    --exclude-task ":opensearch-ml-client:jacocoTestReport" \
    --exclude-task ":opensearch-ml-client:jacocoTestCoverageVerification" \
    -PexcludeTests=**/PredictionITTests*
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-ml"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-ml"
  unzip "${srcdir}/ml-commons-${pkgver}/plugin/build/distributions/opensearch-ml-${pkgver}.zip"

  install -Dm644 "${srcdir}/ml-commons-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
