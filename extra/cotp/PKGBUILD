# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: replydev <commoncargo@tutanota.com>

pkgname=cotp
pkgver=1.4.5
pkgrel=3
pkgdesc='Trustworthy, encrypted, command-line TOTP/HOTP authenticator app with import functionality'
arch=(x86_64)
url="https://github.com/replydev/$pkgname"
license=(GPL-3.0-only)
depends=(gcc-libs # libgcc_s.so
         glibc) # libc.so libm.so
makedepends=(cargo
             python)
depends=(libxcb
         libxkbcommon-x11)
replaces=("$pkgname-bin")
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('7fcce48d0aae8f96c94010178a9709afdddc46af902d1965bf5454d83d181fb8')

prepare(){
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	cargo build --frozen --release --all-features
}

check() {
	cd "$_archive"
	cargo test --frozen --all-features
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
