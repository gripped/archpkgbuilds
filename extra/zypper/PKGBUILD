# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=zypper
pkgver=1.14.85
pkgrel=1
pkgdesc="openSUSE's most powerful command line package manager"
arch=(x86_64)
url="https://github.com/openSUSE/zypper"
license=('GPL-2.0-or-later')
depends=(
    augeas
    boost-libs
    gcc-libs
    glibc
    libxml2
    libzypp
    readline
)
makedepends=(
    asciidoctor
    boost
    cmake
    git
)
source=("git+https://github.com/openSUSE/zypper.git#tag=${pkgver}")
sha256sums=('d660b5e0b0493aa7b5a1d3a5f01b6c14f4aa87de24d2df294033fbeb616e5d67')

build() {
    local cmake_options=(
        -B build
        -S $pkgname
        -W no-dev
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX=/usr
        -D ENABLE_BUILD_TESTS=ON
    )
    cmake "${cmake_options[@]}"
    cmake --build build
}

check() {
    local excluded_tests=""
    local ctest_flags=(
        --test-dir build
        # show the stdout and stderr when the test fails
        --output-on-failure
        # execute tests in parallel
        --parallel $(nproc)
        # exclude problematic tests
        --exclude-regex "$excluded_tests"
    )
    ctest "${ctest_flags[@]}"
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    rm -v "${pkgdir}/usr/bin/"{apt,apt-get,aptitude}
    rm -vr "${pkgdir}/usr/sbin/"
}
