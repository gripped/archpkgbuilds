# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-authen-radius
pkgver=0.33
pkgrel=1
pkgdesc="perl radius client"
arch=('any')
url="https://search.cpan.org/dist/Authen-Radius/"
license=('Artistic-2.0')
depends=('perl-data-hexdump>=0.02' 'perl-net-ip')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/P/PO/PORTAONE/Authen-Radius-$pkgver.tar.gz)
sha256sums=('c15361fc905dcdc156e5126686123ec079f02ad0d24c87c2729eb153d02bf0ce')

build() {
  cd  "$srcdir"/Authen-Radius-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Authen-Radius-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
