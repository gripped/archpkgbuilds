# Maintainer: Anton Hvornum <torxed@archlinux.org>

pkgname=vehicle-command
pkgver=0.3.3
pkgrel=1
pkgdesc='Tesla Vehicle Command SDK'
arch=('x86_64')
url='https://github.com/teslamotors/vehicle-command'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
source=("git+${url}#tag=v${pkgver}")
sha512sums=('6de052f6af52fba9e5aa855c65a1eff69339cb4b9ffd6a45a032c980aaa7a6b793dc927ffca9f14a5ededf496e1b0b0119addf07afcaa0345c90145f09967af9')
b2sums=('ab979914ec5fd24389effe654f1c5a979df07692145c23af7f321701c4918cd40689e70fe0a8e03eda5e8287676f6dc8051ec715b1f3fbc5ec2ec26e4d1c1c07')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname}"
  mkdir -p build/
  go get ./...
}

build() {
  cd "${pkgname}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  export GOFLAGS='-buildmode=pie -mod=readonly -modcacherw'

  go build -v -o build -ldflags "-compressdwarf=false -linkmode external" ./...
}

check() {
  cd "${pkgname}"
  go test ./...
  go vet ./...
}

package() {
  cd "${pkgname}"

  mkdir -p "${pkgdir}/usr/bin/"

  # Just to avoid name collisions, the undocumented "unlock" binary
  # gets renamed to tesla-unlock. Same goes for "ble"
  mv build/unlock build/tesla-unlock
  mv build/ble build/tesla-ble

  install -Dm 755 build/tesla-keygen "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-control "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-http-proxy "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-auth-token "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-jws "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-unlock "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-ble "${pkgdir}/usr/bin/"

  install -Dm 644 README*.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
