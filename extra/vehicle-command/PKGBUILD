# Maintainer: Anton Hvornum <torxed@archlinux.org>

pkgname=vehicle-command
pkgver=0.3.4
pkgrel=1
pkgdesc='Tesla Vehicle Command SDK'
arch=('x86_64')
url='https://github.com/teslamotors/vehicle-command'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
source=("git+${url}#tag=v${pkgver}")
sha512sums=('84c19d45fd7970f38890dd73490e8b1bbe9b2158a63990b122e49a0227b521910649e0a824c53fbcd9977c0293c77a839a4b11e62e4706bb23d262ef5ba0fb17')
b2sums=('e98ad691557ea8afdc5f674e603ef26b8c6d6b962e5656a7fb079ba70e5f8fcd4ab4d46685d643ce43129c647e19c1c32403f3ef1c598d6ad6cb569fa0d7c94a')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname}"
  mkdir -p build/
  go get ./...
}

build() {
  cd "${pkgname}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  export GOFLAGS='-buildmode=pie -mod=readonly -modcacherw'

  go build -v -o build -ldflags "-compressdwarf=false -linkmode external" ./...
}

check() {
  cd "${pkgname}"
  go test ./...
  go vet ./...
}

package() {
  cd "${pkgname}"

  mkdir -p "${pkgdir}/usr/bin/"

  # Just to avoid name collisions, the undocumented "unlock" binary
  # gets renamed to tesla-unlock. Same goes for "ble"
  mv build/unlock build/tesla-unlock
  mv build/ble build/tesla-ble

  install -Dm 755 build/tesla-keygen "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-control "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-http-proxy "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-auth-token "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-jws "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-unlock "${pkgdir}/usr/bin/"
  install -Dm 755 build/tesla-ble "${pkgdir}/usr/bin/"

  install -Dm 644 README*.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
