# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=warp
pkgver=0.7.0
pkgrel=3
pkgdesc='Securely send files to each other via the internet or local network by exchanging a word-based code'
arch=(x86_64)
url='https://apps.gnome.org/Warp/'
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugin-pipewire
  gst-plugins-bad
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  itstool
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.gnome.org/World/warp.git?signed#tag=v$pkgver")
b2sums=(84cc32a1ae9b031204b734d6695964c53e7e88e5267547211655d479c93143e25a38ca5e3153a4ce5a90c08d6196f35d59034675dfb3de44771b049c5a275c81)
validpgpkeys=(FD04DCE1EB3443DE17FB904ECFD6416C8173524E) # Fina Wilke <fina@felinira.net>

prepare() {
  cd $pkgname

  # Fix buld with Rust 1.80.0
  cargo update time --precise 0.3.36

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
