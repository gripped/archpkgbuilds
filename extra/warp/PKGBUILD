# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=warp
pkgver=0.8.0
pkgrel=1
pkgdesc='Securely send files to each other via the internet or local network by exchanging a word-based code'
arch=(x86_64)
url='https://apps.gnome.org/Warp/'
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugin-pipewire
  gst-plugins-bad
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  itstool
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.gnome.org/World/warp.git?signed#tag=v$pkgver")
b2sums=(1e66555e4427b7778b3cf93d0edb389e263eda521f02c64cf75bbf22c868a74e392929db82a08a1c37fd1129ddd77fe1efdff2d95a7eecb85a7185f561180b01)
validpgpkeys=(FD04DCE1EB3443DE17FB904ECFD6416C8173524E) # Fina Wilke <fina@felinira.net>

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
