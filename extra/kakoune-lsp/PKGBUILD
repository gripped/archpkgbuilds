# Maintainer: Maxim Baz <archlinux at maximbaz dot com>

pkgname=kakoune-lsp
pkgver=18.1.2
pkgrel=1
pkgdesc='Kakoune Language Server Protocol Client'
arch=('x86_64')
url="https://github.com/${pkgname}/${pkgname}"
license=('custom:unlicense')
replaces=('kak-lsp')
conflicts=('kak-lsp')
provides=('kak-lsp')
depends=('kakoune')
makedepends=('rust')
optdepends=(
    'bash-language-server: language server for Bash'
    'gopls: language server for Go'
    'haskell-language-server: language server for Haskell'
    'lua-language-server: language server for Lua'
    'python-lsp-server: language server for Python'
    'rust-analyzer: language server for Rust'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('0dc0d0e779b8e1dd0b19608d64d64bf35f709143eef95bb2b0200de2c4d476a4409cf8f28c41cc81852b4f5b8fa4dd1ba1e84f4393f9088fe10fc57475bccfde')

build() {
    cd "${pkgname}-${pkgver}"
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
