# Maintainer: Maxim Baz <archlinux at maximbaz dot com>

pkgname=kakoune-lsp
pkgver=18.1.0
pkgrel=1
pkgdesc='Kakoune Language Server Protocol Client'
arch=('x86_64')
url="https://github.com/${pkgname}/${pkgname}"
license=('custom:unlicense')
replaces=('kak-lsp')
conflicts=('kak-lsp')
provides=('kak-lsp')
depends=('kakoune')
makedepends=('rust')
optdepends=(
    'bash-language-server: language server for Bash'
    'gopls: language server for Go'
    'haskell-language-server: language server for Haskell'
    'lua-language-server: language server for Lua'
    'python-lsp-server: language server for Python'
    'rust-analyzer: language server for Rust'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('2c6c8d73cb7b06582df5e345523d7ecabef8ecf19021737c06804fd03969e1f7261509991064d9b534a531dda10c3a1916d7c1b7a1b15bb5a552c8e535c537e3')

build() {
    cd "${pkgname}-${pkgver}"
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
