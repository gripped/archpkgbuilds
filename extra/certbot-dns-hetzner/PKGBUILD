# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=certbot-dns-hetzner
pkgdesc="Certbot plugin enabling dns-01 challenge on the Hetzner DNS API"
pkgver=3.0.0
pkgrel=1
arch=("any")
url="https://github.com/ctrlaltcoop/certbot-dns-hetzner"
license=("Apache-2.0")
depends=("certbot" "python-requests" "python-requests-mock" 'python-tldextract' 'dns-lexicon' 'python-parsedatetime')
makedepends=("python-build" "python-installer" "python-wheel" "python-setuptools")
checkdepends=("python-pytest")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('19921702679f9dec11221f06117ade0df24a8fcb9070c69501e23f4ea0eb751e')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHONPATH=. pytest
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
	python -m installer --destdir="$pkgdir" dist/*.whl
}
