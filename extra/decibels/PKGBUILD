# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=decibels
pkgver=46.0
pkgrel=3
pkgdesc='Audio player for GNOME'
arch=(any)
url='https://apps.gnome.org/Decibels/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gjs
  glib2
  graphene
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  typescript
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
source=(
  "git+https://gitlab.gnome.org/GNOME/Incubator/decibels.git#tag=$pkgver"
  git+https://gitlab.gnome.org/BrainBlasted/gi-typescript-definitions.git
  decibels-window-icon.patch
)
b2sums=(
  f254bc63f38fa6f1ec43ba4ab24725e5550ad82db7a9a8db64c185e95dcb2b6a962ebded9e822892fbc86e9c3bf5ae581ba52d46ea790e9ee1a3952c628808ba
  SKIP
  a88e09698d10498d2ece45dee2ea27cbdb6e0b6ab238a34b048e4eb76b8ba6ca5bfe9f2ab712a0473cd46c656a186c54e22bd2eec590de73e7aeddcca60228e5
)

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url gi-types "$srcdir/gi-typescript-definitions"
  git -c protocol.file.allow=always submodule update

  # https://gitlab.gnome.org/GNOME/Incubator/decibels/-/merge_requests/123
  git apply -3 ../decibels-window-icon.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s org.gnome.Decibels "$pkgdir/usr/bin/decibels"
}
