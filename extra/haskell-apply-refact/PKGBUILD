# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=apply-refact
pkgname=haskell-apply-refact
pkgver=0.13.0.0
pkgrel=34
pkgdesc="Perform refactorings specified by the refact library."
url="https://github.com/mpickering/apply-refact"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-filemanip' 'haskell-ghc-exactprint' 'haskell-ghc-paths'
         'haskell-optparse-applicative' 'haskell-refact' 'haskell-syb' 'haskell-uniplate'
         'haskell-unix-compat')
makedepends=('ghc' 'haskell-silently' 'haskell-tasty' 'haskell-tasty-expected-failure'
             'haskell-tasty-golden')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('6ccbbbe4cccc95cc4f9ed97218705df5acf6210b3cc86cf5bfcd8d6a2461b3751cc30bd7cd0baa954365fb8a6e808ea587c350df0ed517d0ea36e32bb57b6d2d')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
