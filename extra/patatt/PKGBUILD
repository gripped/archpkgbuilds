# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>

pkgname=patatt
pkgver=0.7.0
pkgrel=1
pkgdesc="A simple library to add cryptographic attestation to patches sent via email"
arch=("any")
depends=('python-pynacl')
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
url="https://git.kernel.org/pub/scm/utils/patatt/patatt.git/about/"
license=("MIT")
source=("https://git.kernel.org/pub/scm/utils/$pkgname/$pkgname.git/snapshot/$pkgname-$pkgver.tar."{gz,asc})
validpgpkeys=('DE0E66E32F1FDD0902666B96E63EDCA9329DD07E')
sha256sums=('8c796be2faac7bd693fb69d2e23ba38f961d0a899dcb36d6ddaa824022d63fff'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -d "$pkgdir/usr/share/man/man5"
  install -m644 man/*.5 "$pkgdir/usr/share/man/man5"
}
