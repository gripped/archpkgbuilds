# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt6-mqtt
_qtver=6.8.1
pkgver=${_qtver/-/}
pkgrel=1
pkgdesc="Module to implement MQTT protocol v3.1/3.1.1/5.0"
arch=(x86_64)
url="http://qt-project.org/"
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git
             ninja)
source=("git+https://code.qt.io/qt/qtmqtt.git#tag=v$_qtver")
sha512sums=('4a8dc9091c8fcb2f3f13dec86cdee2015a59dea5225622cfdd5296c16c7c2abf9aaf5e6bc444b5b5734a93ec1f60cc475a2cd45733b7c71d2ad57f2b9377d385')
_pkgfn=${pkgname/6-/}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
    
  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
