# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt6-mqtt
_pkgver=6.8.3
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="Module to implement MQTT protocol v3.1/3.1.1/5.0"
arch=(x86_64)
url="http://qt-project.org/"
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git
             ninja)
source=("git+https://code.qt.io/qt/qtmqtt.git#tag=v$_pkgver")
sha512sums=('d4aa8c7ce018fc7f0a4b330d135bc2e56536696d95c8e60912289b9e6da45e6c39c975103db66dad234dda28cf51ff05406bebe422e4b6c30b0a84d5be80cb49')
_pkgfn=${pkgname/6-/}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
    
  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
