# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt6-mqtt
_pkgver=6.9.0
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="Module to implement MQTT protocol v3.1/3.1.1/5.0"
arch=(x86_64)
url="http://qt-project.org/"
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git
             ninja)
source=("git+https://code.qt.io/qt/qtmqtt.git#tag=v$_pkgver")
sha512sums=('167ef198151e4ce278a6d049b20b317d7b89d52a91cac63173bda9be9cc7cbd0e4750d4852eb8c6aac099f6351061f9e69fbe81237a1cf29b0bd01bf3fdb1125')
_pkgfn=${pkgname/6-/}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
    
  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
