# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt6-mqtt
_pkgver=6.9.2
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="Module to implement MQTT protocol v3.1/3.1.1/5.0"
arch=(x86_64)
url="http://qt-project.org/"
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git
             ninja)
source=("git+https://code.qt.io/qt/qtmqtt.git#tag=v$_pkgver")
sha512sums=('7e18482b66faf6b945907443c74044f4ecaceb41ccd2e39a7630e86d5af8bb0579c80ba41a08f1380ee084e1b9c91b5293656f292c2f052ec8050b4c0c7e81e9')
_pkgfn=${pkgname/6-/}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
    
  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
