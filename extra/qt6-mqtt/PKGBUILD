# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt6-mqtt
_pkgver=6.10.1
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="Module to implement MQTT protocol v3.1/3.1.1/5.0"
arch=(x86_64)
url="http://qt-project.org/"
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git
             ninja)
source=("git+https://code.qt.io/qt/qtmqtt.git#tag=v$_pkgver")
sha512sums=('b198a793468acecd8d30f314cfddea89981bb3258dc06ffa3fd1e9dd82ffb9a459c52fb28b3e7532836da07aede38996c09a3bf60b922efdd00f7a523c380e47')
_pkgfn=${pkgname/6-/}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
    
  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
