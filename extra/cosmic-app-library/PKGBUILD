# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-app-library
pkgver=1.0.0.alpha.5.1
pkgrel=1
pkgdesc='Cosmic App Library'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applibrary
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=c01d98d629bf53a961dd37880cebe9a2c23df13c
source=(git+https://github.com/pop-os/cosmic-applibrary.git#tag=${_tag})
b2sums=('3aaa315b1ffc7ae566ca638f5de08a1089d7943e12a3438c6263c5f3cb164dcc587dad05b34546d93e85f4a56b367c7576c2ffeef482f53aa863939965602a1b')

prepare() {
  cd cosmic-applibrary
  cargo fetch --locked
}

pkgver() {
  cd cosmic-applibrary
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applibrary
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applibrary
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
