# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-app-library
pkgver=1.0.0.alpha.5
pkgrel=1
pkgdesc='Cosmic App Library'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applibrary
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=676656da1eca2110db22f11f5a26e1d70628fdbd
source=(git+https://github.com/pop-os/cosmic-applibrary.git#tag=${_tag})
b2sums=('2425e78e49ebc1ae0bd2fe550e7bfecd96006d8d1d0e1a607ac55ec3ab47cf881035e79dfa8345cc56d7128ac0fad6fcb659923c9111c902bc94765a025ec3df')

prepare() {
  cd cosmic-applibrary
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-applibrary
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-applibrary
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applibrary
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
