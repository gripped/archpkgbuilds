# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-app-library
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='Cosmic App Library'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applibrary
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=279c614b91fb0127953cc926d114eeac03aa1e77
source=(git+https://github.com/pop-os/cosmic-applibrary.git#tag=${_tag})
b2sums=('0e3b8986c5ff1cdbdc2a8008491686b9d444e24209179b0b17bffa06bc1aeb1a821074a71165d5ac83893666dbb72e1d901598c36812d28dbc79728cc09f9c86')

prepare() {
  cd cosmic-applibrary
  cargo fetch --locked
}

pkgver() {
  cd cosmic-applibrary
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applibrary
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applibrary
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
