# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-app-library
pkgver=1.0.0.beta.1
pkgrel=1
pkgdesc='Cosmic App Library'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applibrary
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=0c1965cc0cec234425ae132f38df452849660aa9
source=(git+https://github.com/pop-os/cosmic-applibrary.git#tag=${_tag})
b2sums=('20fa3430aa01bff514b85e39756d2fa8fab74c7f359ea8008d92739ef16be7e8caac6533e9dac1d0e22af06e9cd9c1f3fc9cdad49f4833efb3e5e6c87d9df072')

prepare() {
  cd cosmic-applibrary
  cargo fetch --locked
}

pkgver() {
  cd cosmic-applibrary
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applibrary
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applibrary
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
