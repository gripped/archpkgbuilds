# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-app-library
pkgver=1.0.0.beta.1.1
pkgrel=1
pkgdesc='Cosmic App Library'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applibrary
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=62100ee576e82d6e3136ec147e8b82552fac7627
source=(git+https://github.com/pop-os/cosmic-applibrary.git#tag=${_tag})
b2sums=('59b9581373a0059ef99c34f40ce9386cb6f59b049e86948b742e8432073045d084c0ffe8bb50a5b69e84e215bc252dcbc52c95af3ef231fc136e95dd70f40540')

prepare() {
  cd cosmic-applibrary
  cargo fetch --locked
}

pkgver() {
  cd cosmic-applibrary
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applibrary
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applibrary
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
