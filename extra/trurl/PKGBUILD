# Maintainer: Christian Heusel <gromit@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Michael Lass <bevan@bi-co.net>

pkgname=trurl
pkgver=0.16
pkgrel=1
pkgdesc="Command line tool for URL parsing and manipulation"
arch=(x86_64)
url="https://github.com/curl/trurl"
license=(custom)
depends=(curl glibc)
checkdepends=(python valgrind)
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg
source=(https://github.com/curl/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('2c26e3016f591f06234838bbe1dd4b165dce2c871c82ca6a32222d19696588d6'
            'SKIP')

build() {
	cd "$pkgname-$pkgver"
	make
}

check() {
	cd "$pkgname-$pkgver"
	make test
	make test-memory
}

package() {
	cd "$pkgname-$pkgver"

	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
