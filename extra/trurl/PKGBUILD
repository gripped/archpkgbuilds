# Maintainer: Christian Heusel <gromit@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Michael Lass <bevan@bi-co.net>

pkgname=trurl
pkgver=0.10
pkgrel=1
pkgdesc="Command line tool for URL parsing and manipulation"
arch=(x86_64)
url="https://github.com/curl/trurl"
license=(custom)
depends=(curl glibc)
checkdepends=(python)
source=("https://github.com/curl/$pkgname/archive/refs/tags/$pkgname-$pkgver.tar.gz")
sha256sums=('e54ee05a1a39f2547fbb39225f9cf5e2608eeaf07ad3f7dbff0a069d060d3c46')

build() {
	cd "$pkgname-$pkgname-$pkgver"
	make
}

check() {
	cd "$pkgname-$pkgname-$pkgver"
	make test
}

package() {
	cd "$pkgname-$pkgname-$pkgver"

	make PREFIX=/usr DESTDIR="$pkgdir/" install
	install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
