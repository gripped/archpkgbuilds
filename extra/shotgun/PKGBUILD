# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Streetwalrus <streetwalrus@codewalr.us>

pkgname=shotgun
pkgver=2.5.0
pkgrel=1
pkgdesc='Minimal X screenshot utility'
arch=('x86_64')
url=https://github.com/neXromancers/shotgun
license=('MPL2')
depends=('gcc-libs')
makedepends=('git' 'rust')
optdepends=(
  'hacksaw: for area/window selection'
  'slop: for area/window selection'
)
source=("shotgun-$pkgver.tar.gz::https://static.crates.io/crates/shotgun/shotgun-$pkgver.crate")
b2sums=('f3bb45aa2f36a241d4ccc774a725b754c1d417ab05a0bf24799b7504ea2fca7730640c2961c3ef282bba80cab227200198854b4118481d65aa55482a8e72f71a')

prepare() {
  cd shotgun-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd shotgun-$pkgver
  cargo build --release --locked --offline
}

package() {
  cd shotgun-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/shotgun
}
