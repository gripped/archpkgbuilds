# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-hangul
pkgver=5.1.6
pkgrel=1
pkgdesc="Hangul Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-hangul"
license=('GPL')
depends=('fcitx5' 'libhangul')
makedepends=('git' 'extra-cmake-modules')
source=("git+https://github.com/fcitx/fcitx5-hangul.git#tag=$pkgver?signed")
sha512sums=('f4ec8dd4d78272f1b92bb565b601af20e2a45f22b4bea9ac5decf3963ac4f421c054e6ecb426304fac9ccc2bf54e482adf1df53af4f14ebd43a472e1212a435d')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

check() {
  cd $pkgname
  make test
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
