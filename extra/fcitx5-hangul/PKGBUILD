# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-hangul
pkgver=5.1.5
pkgrel=1
pkgdesc="Hangul Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-hangul"
license=('GPL')
depends=('fcitx5' 'libhangul')
makedepends=('git' 'extra-cmake-modules')
source=("git+https://github.com/fcitx/fcitx5-hangul.git#tag=$pkgver?signed")
sha512sums=('4f7eb04c3443233b5adc43244546ccf651eebda5ccb9bc895479a026b260dd0222fc13d546eb42f0f043bd5c97a9b2ef31aa4245aa830e9059f207145c5e90d1')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

check() {
  cd $pkgname
  make test
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
