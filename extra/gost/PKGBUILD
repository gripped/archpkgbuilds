# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=3.2.5
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/go-gost/gost"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/go-gost/gost.git#tag=v$pkgver")
sha512sums=('0e3826e4ef7431b87b8cf8333e474e6d3d4466c4da7b53f2dbe64f1bba2dbf156649b60910c485afbcd6e975630c1abaf0c32a10a701a1048fe8824e42947446')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd gost/cmd/gost
  go build
}

package() {
  cd gost
  install -Dm755 cmd/gost/gost -t "$pkgdir"/usr/bin/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
