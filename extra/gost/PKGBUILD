# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=3.0.0
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/go-gost/gost"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/go-gost/gost.git#tag=v$pkgver")
sha512sums=('b8675b4ae031d3cc05dc65698b7bec71fdf4566db406153db8282ef4d168c9b4b6e872090db6186ae6cabe5f2012f2bcee030a8e83e1e53bfbc917f5228a41f8')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd gost/cmd/gost
  go build
}

package() {
  cd gost
  install -Dm755 cmd/gost/gost -t "$pkgdir"/usr/bin/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
