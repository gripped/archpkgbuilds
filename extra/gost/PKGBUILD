# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.12.0
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/ginuerzh/gost.git#tag=v$pkgver")
sha512sums=('5046d21e3d755f37e2dd6eb48f801f2bf7d1c964586653649a795ea391c42891a53b7296861cdc39a062b90597582936a00b07933208cade037acf55fe3bbcc5')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd gost/cmd/gost
  go build
}

package() {
  cd gost
  install -Dm755 cmd/gost/gost -t "$pkgdir"/usr/bin/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
