# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: AlphaJack <alphajack at tuta dot io>
# Contributor: Pieter Goetschalckx <3.14.e.ter <at> gmail <dot> com>

pkgname=apprise
pkgver=1.7.4
pkgrel=1
pkgdesc='Push Notifications that work with just about every platform'
arch=('any')
url=https://github.com/caronc/apprise
license=('BSD-2-Clause')
depends=(
  'python-certifi'
  'python-click'
  'python-markdown'
  'python-requests-oauthlib'
  'python-yaml'
)
makedepends=(
  'git'
  'python-babel'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('dbus-python' 'python-paho-mqtt' 'python-pytest-mock')
optdepends=(
  'dbus-python: for Linux DBus Notifications'
  'python-gobject: for Linux DBus Notifications'
  'python-paho-mqtt: for MQTT support'
)
provides=("python-$pkgname")
conflicts=("python-$pkgname")
source=("git+$url.git#tag=v$pkgver")
b2sums=('1f15af5c0db158c72008dc8155143a05fd09294f2c070ef06320779a8ea76d4a3e54a9cfcbab2ecc87aab78f0e2bd4876112104e5a7bf5a974b4ec90c4840d78')

build(){
  cd $pkgname
  python -m build --wheel --no-isolation
}

check(){
  cd $pkgname
  PYTHONPATH="$pwd/$pkgname:$PYTHONPATH" pytest
}

package(){
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
