# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jeffrey Zhang <zhang.lei.fly@gmail.com>

pkgname=python-pytest-lsp
_name=${pkgname#python-}
pkgver=0.4.3
pkgrel=4
pkgdesc='Pytest plugin for end-to-end testing of language servers'
arch=(any)
url=https://lsp-devtools.readthedocs.io
license=(MIT)
depends=(
  python-packaging
  python-pygls
  python-pytest-asyncio
)
makedepends=(
  git
  python-build
  python-hatchling
  python-installer
)
source=("git+https://github.com/swyddfa/lsp-devtools.git#tag=$_name-v$pkgver")
b2sums=('8770e704a7a89ffad30d89d39bca1053e19f1447e08e783b95bb80e1c235a16e8bdf1e6f6d7ceb5b988e68864c9a3170cb5f2c790c9271764f434c8107d4cbe5')

build() {
  cd lsp-devtools/lib/"$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
  cd ../lsp-devtools
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd lsp-devtools/lib/"$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m installer ../lsp-devtools/dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -W ignore::ResourceWarning -m pytest -vv
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd lsp-devtools/lib/"$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"${_name/-/_}"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
