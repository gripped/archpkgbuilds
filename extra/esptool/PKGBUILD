# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Marcin Kornat <rarvolt+aur@gmail.com>
# Contributor: MatejSpindler <spindler.matej@gmail.com>

pkgname=esptool
pkgver=5.0.1
pkgrel=2
pkgdesc='Espressif SoC serial bootloader utility'
arch=(any)
url='https://github.com/espressif/esptool'
license=(GPL-2.0-only)
makedepends=(git python-build python-installer python-wheel)
depends=(
  python
  python-pyserial
  python-ecdsa
  python-bitstring
  python-reedsolo
  python-cryptography
  python-yaml
  python-intelhex
  python-click
  python-colorama
  python-rich-click
)
optdepends=('python-python-pkcs11: Support for HSM devices')
source=("git+$url.git#tag=v$pkgver")
b2sums=('54198ca32418a59d8c539af2d90d6ce460794ede52efd34b593e48636c54e2c3b5d2c3ce657e48d00f9a6c6d90172cceb61a0dfb7b72072e705ebc4c7c45f343')

prepare() {
  git -C $pkgname cherry-pick -n 0779a019603129d92833b3d275e82074b4a49ac4 # fix click >8.2.0 compat
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
