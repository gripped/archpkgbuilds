# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Marcin Kornat <rarvolt+aur@gmail.com>
# Contributor: MatejSpindler <spindler.matej@gmail.com>

pkgname=esptool
pkgver=5.0.1
pkgrel=1
pkgdesc='Espressif SoC serial bootloader utility'
arch=(any)
url='https://github.com/espressif/esptool'
license=(GPL-2.0-only)
makedepends=(git python-build python-installer python-wheel)
depends=(
  python
  python-pyserial
  python-ecdsa
  python-bitstring
  python-reedsolo
  python-cryptography
  python-yaml
  python-intelhex
  python-click
  python-colorama
  python-rich-click
)
optdepends=('python-python-pkcs11: Support for HSM devices')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('ab152173616aeabbc026b9502e5430075e90ecc63542840c3fa84b2fc9612ed36a0d4317f2f34c97faa66c9faa964c9f5010b7c4a439c98a7efdf769ff0d38cc')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
