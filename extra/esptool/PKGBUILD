# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Marcin Kornat <rarvolt+aur@gmail.com>
# Contributor: MatejSpindler <spindler.matej@gmail.com>

pkgname=esptool
pkgver=4.8.1
pkgrel=2
pkgdesc='Espressif SoC serial bootloader utility'
arch=(any)
url='https://github.com/espressif/esptool'
license=(GPL-2.0-only)
makedepends=(git python-build python-installer python-wheel)
depends=(python python-pyserial python-ecdsa python-bitstring python-reedsolo python-cryptography python-yaml python-intelhex)
optdepends=('python-python-pkcs11: Support for HSM devices')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('ffd8e6aa4c8460765b21c3542b9e953a4e0810e88e1771f02c80ed7145b20623d4b3922268680cc250c64422ab8b90920261af5ef56b9abc14b60b6a10cb6c59')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
