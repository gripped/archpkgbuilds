# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Marcin Kornat <rarvolt+aur@gmail.com>
# Contributor: MatejSpindler <spindler.matej@gmail.com>

pkgname=esptool
pkgver=5.1.0
pkgrel=1
pkgdesc='Espressif SoC serial bootloader utility'
arch=(any)
url='https://github.com/espressif/esptool'
license=(GPL-2.0-only)
makedepends=(git python-build python-installer python-wheel)
depends=(
  python
  python-pyserial
  python-ecdsa
  python-bitstring
  python-reedsolo
  python-cryptography
  python-yaml
  python-intelhex
  python-click
  python-colorama
  python-rich-click
)
optdepends=('python-python-pkcs11: Support for HSM devices')
source=("git+$url.git#tag=v$pkgver")
b2sums=('54d221b61ccf920a894af5fa0fa5277d19668f43b9ef0f6d803246a495ec863804abee8b97a7bbc0254b4f6f72501de68499532ba62a103a831f8e04cfff1d7f')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
