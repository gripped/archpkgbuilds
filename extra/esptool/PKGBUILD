# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Marcin Kornat <rarvolt+aur@gmail.com>
# Contributor: MatejSpindler <spindler.matej@gmail.com>

pkgname=esptool
pkgver=4.9.0
pkgrel=1
pkgdesc='Espressif SoC serial bootloader utility'
arch=(any)
url='https://github.com/espressif/esptool'
license=(GPL-2.0-only)
makedepends=(git python-build python-installer python-wheel)
depends=(python python-pyserial python-ecdsa python-bitstring python-reedsolo python-cryptography python-yaml python-intelhex)
optdepends=('python-python-pkcs11: Support for HSM devices')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('8ddb37c20c7337e2768f1175a981f23eaabc61ef7851b928ce20f172257b812c73b46ea4db3c46d688d230dd58772e5e43786c70b1e6a9ff9297b26a4ed10e7f')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
