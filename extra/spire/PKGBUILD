# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=spire
pkgname=("spire-agent" "spire-server")
pkgver=1.12.2
pkgrel=1
pkgdesc="SPIFFE Runtime Environment"
url='https://github.com/spiffe/spire'
arch=("x86_64")
license=("Apache")
makedepends=("go")
depends=("glibc")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/spiffe/spire/archive/v${pkgver}.tar.gz")
sha512sums=('27d64075fd122593a76324a766080b4e0e9cfe95323caef12a3a927d502e13a996959c62929935f1727309ddc63537d472efd4eb25bb1a8dac2c818a7b179c4a')
b2sums=('fecd6e7a276add9534f5b3bda75b15a798a5a8e15a1ed2adfd0b54b3e5fbd1747a81b7a685c04e64929f540740e2e93fc3ed4355d44dd486af41509ef1185d1c')

build() {
  cd "${pkgbase}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  cd cmd/spire-agent/
  go build -o spire-agent .
  cd ../../cmd/spire-server
  go build -o spire-server .
}

check() {
  cd "${pkgbase}-${pkgver}"
  # test is blocking for some reason..
  # go test -v ./...
}

package_spire-agent() {
  pkgdesc="SPIFFE runtime environment (agent)"
  provides=("spire-agent")
  depends=("glibc")
  cd "${pkgbase}-${pkgver}"
  install -Dsm755 cmd/spire-agent/spire-agent "${pkgdir}/usr/bin/spire-agent"
}

package_spire-server() {
  pkgdesc="SPIFFE runtime environment (server)"
  provides=("spire-server")
  depends=("glibc")
  cd "${pkgbase}-${pkgver}"
  install -Dsm755 cmd/spire-server/spire-server "${pkgdir}/usr/bin/spire-server"
}
