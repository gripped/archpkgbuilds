# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=spire
pkgname=("spire-agent" "spire-server")
pkgver=1.11.2
pkgrel=1
pkgdesc="SPIFFE Runtime Environment"
url='https://github.com/spiffe/spire'
arch=("x86_64")
license=("Apache")
makedepends=("go")
depends=("glibc")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/spiffe/spire/archive/v${pkgver}.tar.gz")
sha512sums=('8f4d09b0f14b97a01dcfcc7f6eda8438e3566b7145c31148349ed5ca9beb834ead1907027643f8089a82cbb03bf9606b046af714bacd64d17f4f019d6783c425')
b2sums=('76ecf6fb354875de06606ec1cc562e6bba0f2c0d55959e8a8992e61b303fb3085cccaefd4869ab8d2b812c19a16756d2638a72f43f1545e40a262f4afc3107c5')

build() {
  cd "${pkgbase}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  cd cmd/spire-agent/
  go build -o spire-agent .
  cd ../../cmd/spire-server
  go build -o spire-server .
}

check() {
  cd "${pkgbase}-${pkgver}"
  # test is blocking for some reason..
  # go test -v ./...
}

package_spire-agent() {
  pkgdesc="SPIFFE runtime environment (agent)"
  provides=("spire-agent")
  depends=("glibc")
  cd "${pkgbase}-${pkgver}"
  install -Dsm755 cmd/spire-agent/spire-agent "${pkgdir}/usr/bin/spire-agent"
}

package_spire-server() {
  pkgdesc="SPIFFE runtime environment (server)"
  provides=("spire-server")
  depends=("glibc")
  cd "${pkgbase}-${pkgver}"
  install -Dsm755 cmd/spire-server/spire-server "${pkgdir}/usr/bin/spire-server"
}
