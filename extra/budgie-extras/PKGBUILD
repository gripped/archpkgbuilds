# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Campbell Jones <arch at serebit dot com>

pkgname=budgie-extras
pkgver=2.0.0
pkgrel=2
pkgdesc='Additional Budgie Desktop components from the Ubuntu Budgie team'
arch=(x86_64)
license=(GPL-3.0-or-later)
url='https://github.com/UbuntuBudgie/budgie-extras'
depends=(
  at-spi2-core
  budgie-desktop
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gnome-menus
  granite
  gtk3
  hicolor-icon-theme
  json-glib
  libgee
  libhandy
  libkeybinder3
  libnm
  libnotify
  libpeas-2
  libsoup3
  libwnck3
  libxfce4windowing
  pango
  python
  python-cairo
  python-cairosvg
  python-dbus
  python-gobject
  python-pillow
  python-psutil
  python-pyudev
  python-svgwrite
  python-xlib
  sound-theme-freedesktop
  vorbis-tools
  wmctrl
  xdotool
  xorg-xinput
  xorg-xprop
  xprintidle
  zenity
)
makedepends=(
  appstream
  git
  intltool
  meson
  ninja
  vala
)
source=(
  "git+https://github.com/UbuntuBudgie/budgie-extras.git#tag=v$pkgver"
  git+https://github.com/UbuntuBudgie/applications-menu.git
  git+https://github.com/UbuntuBudgie/QuickChar.git
  git+https://github.com/UbuntuBudgie/budgie-network-applet.git
  git+https://github.com/UbuntuBudgie/budgie-trash-applet.git
)
b2sums=(
  f242f3b8a745c4e39f11d836ed8f039e46afbfad9ee3b0ad5c43c116c8551c7e5c760fa279635e0612ddc20c8f16b9ae872b33b5aba4981f5549be91e28b9703
  SKIP
  SKIP
  SKIP
  SKIP
)

prepare() {
  cd $pkgname

  git submodule init
  git config submodule.budgie-applications-menu/applications-menu.url ../applications-menu
  git config submodule.budgie-quickchar/quickchar.url ../QuickChar
  git config submodule.budgie-network-manager/budgie-network-applet.url ../budgie-network-applet
  git config submodule.budgie-trash/trash.url ../budgie-trash-applet
  git -c protocol.file.allow=always submodule update

  # Use libpeas-2
  sed -i -e 's/libpeas-1.0/libpeas-2/' -e "s/python_loader = 'python3'/python_loader = 'python'/" meson.build
  find . -name meson.build -exec sed -i -e 's/budgie-1.0/budgie-2.0/' -e '/libpeas-gtk-1.0/d' {} +
  find . -name '*.py' -exec sed -i "s/gi.require_version('Budgie', '1.0')/gi.require_version('Budgie', '2.0')/" {} +

  # Fix build
  sed -i 's/this.popdown();/this.hide();/' budgie-quicknote/QuickNoteApplet.vala

  # Fix path for libexec directory
  sed -i "s/'libexec'/'lib'/" meson.build

  # Add missing categories
  sed -i '$ a Categories=GTK;Settings;' budgie-hotcorners/misc/org.ubuntubudgie.budgie-extras.HotCorners.desktop.in.in
}

build() {
  arch-meson $pkgname build \
    -D b_pie=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Show only in Budgie
  sed -i '$ a OnlyShowIn=Budgie;' "$pkgdir"/etc/xdg/autostart/*-autostart.desktop "$pkgdir"/usr/share/applications/*.desktop
}
