# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: witchymary

pkgname=vapoursynth-plugin-bestsource
pkgver=R15
pkgrel=1
epoch=1
pkgdesc='BestSource plugin for VapourSynth'
arch=(x86_64)
url=https://github.com/vapoursynth/bestsource
license=(MIT)
depends=(
  gcc-libs
  glibc
  libavcodec.so
  libavformat.so
  libxxhash.so
  vapoursynth
)
makedepends=(
  git
  meson
)
_tag=75e33867a1c5061a4d052322833a70eba5037b27
source=(
  git+https://github.com/vapoursynth/bestsource.git#tag=${_tag}
  git+https://github.com/AviSynth/AviSynthPlus.git
  git+https://bitbucket.org/the-sekrit-twc/libp2p.git
)
b2sums=('5b4bfa5b4d94c1078ac6461ab9829dad2ee470405f054449c12d41fc9e231da9f864ca6c6a99544a4df864edfe71d46b55fc2e22c375806ff49c0f748842af03'
        'SKIP'
        'SKIP')

prepare() {
  cd bestsource
  for submodule in AviSynthPlus libp2p; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

pkgver() {
  cd bestsource
  git describe --tags
}

build() {
  arch-meson bestsource build
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 bestsource/LICENSE -t "${pkgdir}"/usr/share/licenses/vapoursynth-plugin-bestsource/
}

# vim: ts=2 sw=2 et:
