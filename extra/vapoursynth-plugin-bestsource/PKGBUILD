# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: witchymary

pkgname=vapoursynth-plugin-bestsource
pkgver=R10
pkgrel=1
epoch=1
pkgdesc='BestSource plugin for VapourSynth'
arch=(x86_64)
url=https://github.com/vapoursynth/bestsource
license=(MIT)
depends=(
  gcc-libs
  glibc
  libavcodec.so
  libavformat.so
  libxxhash.so
  vapoursynth
)
makedepends=(
  git
  meson
)
_tag=eef4d1d12845dc284b770309a738156bb9b52a0c
source=(
  git+https://github.com/vapoursynth/bestsource.git#tag=${_tag}
  git+https://github.com/AviSynth/AviSynthPlus.git
  git+https://bitbucket.org/the-sekrit-twc/libp2p.git
)
b2sums=('8263a0a61716b9c70df9e65789009629ebf76fba9485961f4e90dd657a23ceaecbcebadfe617dfcfcd162dd038c6e8c583830f5de631dc23297c97b02e75ffe1'
        'SKIP'
        'SKIP')

prepare() {
  cd bestsource
  for submodule in AviSynthPlus libp2p; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

pkgver() {
  cd bestsource
  git describe --tags
}

build() {
  arch-meson bestsource build \
    --libdir /usr/lib/vapoursynth
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 bestsource/LICENSE -t "${pkgdir}"/usr/share/licenses/vapoursynth-plugin-bestsource/
}

# vim: ts=2 sw=2 et:
