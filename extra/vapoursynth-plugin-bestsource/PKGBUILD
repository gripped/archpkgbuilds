# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: witchymary

pkgname=vapoursynth-plugin-bestsource
pkgver=R7
pkgrel=2
epoch=1
pkgdesc='BestSource plugin for VapourSynth'
arch=(x86_64)
url=https://github.com/vapoursynth/bestsource
license=(MIT)
depends=(
  gcc-libs
  glibc
  libavcodec.so
  libavformat.so
  libxxhash.so
  vapoursynth
)
makedepends=(
  git
  meson
)
_tag=16fdf3ed0f9d3843d9bc26750491e31cab007b53
source=(
  git+https://github.com/vapoursynth/bestsource.git#tag=${_tag}
  git+https://github.com/AviSynth/AviSynthPlus.git
  git+https://bitbucket.org/the-sekrit-twc/libp2p.git
)
b2sums=('8c0b46e54f38f2732df5bd54b38740519907e0cca5e35f597a678f71c0a0da6dba82fe36df43f3668aade2eb10d3d810f7290a132e0078fe9ae8a9f201615a8b'
        'SKIP'
        'SKIP')

prepare() {
  cd bestsource
  for submodule in AviSynthPlus libp2p; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

pkgver() {
  cd bestsource
  git describe --tags
}

build() {
  arch-meson bestsource build \
    --libdir /usr/lib/vapoursynth
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 bestsource/LICENSE -t "${pkgdir}"/usr/share/licenses/vapoursynth-plugin-bestsource/
}

# vim: ts=2 sw=2 et:
