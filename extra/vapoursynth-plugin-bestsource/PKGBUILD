# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: witchymary

pkgname=vapoursynth-plugin-bestsource
pkgver=R11
pkgrel=2
epoch=1
pkgdesc='BestSource plugin for VapourSynth'
arch=(x86_64)
url=https://github.com/vapoursynth/bestsource
license=(MIT)
depends=(
  gcc-libs
  glibc
  libavcodec.so
  libavformat.so
  libxxhash.so
  vapoursynth
)
makedepends=(
  git
  meson
)
_tag=2616de27cbf385da4b52f4bd1b18efc83ab7933b
source=(
  git+https://github.com/vapoursynth/bestsource.git#tag=${_tag}
  git+https://github.com/AviSynth/AviSynthPlus.git
  git+https://bitbucket.org/the-sekrit-twc/libp2p.git
)
b2sums=('0e839a78980c57dda097b566fe397407f0bbc135bd54653b5ffedfe72b828211799fa32f414a99b719e3bc99925c5eb1fac3ce5b55cb8e9f2ee04b2b4593ca56'
        'SKIP'
        'SKIP')

prepare() {
  cd bestsource
  for submodule in AviSynthPlus libp2p; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

pkgver() {
  cd bestsource
  git describe --tags
}

build() {
  arch-meson bestsource build
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 bestsource/LICENSE -t "${pkgdir}"/usr/share/licenses/vapoursynth-plugin-bestsource/
}

# vim: ts=2 sw=2 et:
