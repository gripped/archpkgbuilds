# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=errands
pkgver=46.2.9
pkgrel=2
pkgdesc='Todo application for those who prefer simplicity'
arch=(any)
url='https://apps.gnome.org/List/'
license=(MIT)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libgoa
  libportal
  libsecret
  python
  python-caldav
  python-gobject
  python-requests
  python-urllib3
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://github.com/mrvladus/Errands.git#tag=$pkgver")
b2sums=(4619d12e6a24f0c5d473fd29c18eb8b85fd5631cbea13246067aa2fc053d037c3ca11070699ead759280bf121fbfdd141ae656d539e300036431ffc28b83c012)

build() {
  arch-meson Errands build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" Errands/LICENSE
}
