# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=errands
pkgver=46.2.8
pkgrel=2
pkgdesc='Todo application for those who prefer simplicity'
arch=(any)
url='https://apps.gnome.org/List/'
license=(MIT)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libgoa
  libportal
  libsecret
  python
  python-caldav
  python-gobject
  python-requests
  python-urllib3
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://github.com/mrvladus/Errands.git#tag=$pkgver")
b2sums=(fd4abf8c828fc119f06e732e65ef11d39c8d6dd1640a228c83ecd274839b621781c2905fd60c84271efa77c676ff58a180544f9164208c08ed91b8142654ccaf)

build() {
  arch-meson Errands build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" Errands/LICENSE
}
