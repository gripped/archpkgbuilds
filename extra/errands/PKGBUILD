# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=errands
pkgver=46.2.7
pkgrel=3
pkgdesc='Todo application for those who prefer simplicity'
arch=(any)
url='https://apps.gnome.org/List/'
license=(MIT)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libgoa
  libportal
  libsecret
  python
  python-caldav
  python-gobject
  python-requests
  python-urllib3
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://github.com/mrvladus/Errands.git#tag=$pkgver")
b2sums=(e71ca0d81de50a2cd9dca3f2062aaf9e399c2806cb90862991378580c5fd24c0076a1f05500b7d828d7844fc94409fe58c7aea3f4985eb47d65762e5bbe6f87b)

build() {
  arch-meson Errands build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" Errands/LICENSE
}
