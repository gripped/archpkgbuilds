# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: aulonsal <aulonsal at gmail dot com>

pkgname=hck
pkgver=0.11.5
pkgrel=1
pkgdesc='A drop in replacement for cut that can use a regex delimiter instead of a fixed string'
arch=(x86_64)
url='https://github.com/sstadick/hck'
license=(Unlicense MIT)
depends=(glibc gcc-libs)
makedepends=(git rust cmake)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('054dc67c9cc7665278e11ba0ae74aa9401c20b308e08e47ebabfbca85444bcfad56543ab966f4573c07418cd59c998e4e53582b053a26e50c870693036d7ba83')
b2sums=('c299f62f5367e5449d0686d9a51a758cbe45dd6d495750056e198ed2788058836e7ba8b6039a8b605edae70e3456ef10e181abea82049ca331806100503d67cb')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
	cd "$pkgname"

	cargo build --release --frozen
}

check() {
	cd "$pkgname"

	cargo test --frozen
}

package() {
	cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/hck

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licenses
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE-MIT UNLICENSE
}
