# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: aulonsal <aulonsal at gmail dot com>

pkgname=hck
pkgver=0.11.4
pkgrel=1
pkgdesc='A drop in replacement for cut that can use a regex delimiter instead of a fixed string'
arch=('x86_64')
url='https://github.com/sstadick/hck'
license=('Unlicense' 'MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust' 'cmake')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('954b8635efd4b76e235629403201ccfe5bfb572914416f9831c5862293af6f9f44683ff2affb48db051a315c0d20ec55ed29d629ae765057111f21d9bbb5b108')
b2sums=('b389487a7ecedc599fdc79d391221cccc4ed7a14ef9b13b260c6c3605890b16b29b5253506a5c71722b437ea0caf4715bf074c747576841fcc663cdd8cd93dfd')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$pkgname"

	cargo build --release --frozen
}

check() {
	cd "$pkgname"

	cargo test --frozen
}

package() {
	cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/hck

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licenses
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE-MIT UNLICENSE
}
