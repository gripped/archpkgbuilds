# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jacobo Arvelo <unix4all@ya.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=wesnoth
# NOTE: Odd minor versions are unstable! Do not package those.
pkgver=1.18.2
epoch=1
pkgrel=2
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('x86_64')
license=('GPL')
url="https://www.wesnoth.org/"
depends=('sdl2_mixer' 'sdl2_image' 'boost-libs' 'pango' 'dbus' 'lua')
makedepends=('boost' 'cmake' 'git' 'ninja' 'python')
options=(!emptydirs)
source=("git+https://github.com/wesnoth/wesnoth.git#tag=${pkgver}"
         boost-1.86.patch)
sha512sums=('SKIP'
            '9044a1e9b121f5a035712dec40aece61a4624b071cfa959f3d4e8c66e26465423d5bfc31169d2f9e5ed10750caeaa22a71cdccb1b13b89998f35202eaf474bf1')

prepare() {
  cd $pkgname
  git cherry-pick -n 55162c465405d55f03be0f89daf16818c552d506 # Fix build with boost 1.85
  patch -p1 -i ../boost-1.86.patch
}

build() {
  cd "$pkgname"

  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DENABLE_TOOLS=ON \
      -DFIFO_DIR=/run/wesnothd \
      -DENABLE_DISPLAY_REVISION=OFF \
      -DENABLE_SYSTEM_LUA=ON
  ninja -C build
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" ninja -C build install

  # See https://forums.wesnoth.org/viewtopic.php?t=51038
  echo "Linux repository" > "$pkgdir"/usr/share/wesnoth/data/dist

  mkdir -p "$pkgdir"/usr/lib/{tmpfiles.d,systemd/system}
  install -Dm644 packaging/systemd/wesnothd.sysusers.conf.in "$pkgdir"/usr/lib/sysusers.d/wesnoth.conf
  sed "s|@FIFO_DIR@|/run/wesnothd|" packaging/systemd/wesnothd.tmpfiles.conf.in > "$pkgdir"/usr/lib/tmpfiles.d/wesnothd.conf
  sed -e "s|@FIFO_DIR@|/run/wesnothd|" -e "s|@BINARY_SUFFIX@||" -e "s|@CMAKE_INSTALL_FULL_BINDIR@|/usr/bin|" packaging/systemd/wesnothd.service.in > "$pkgdir"/usr/lib/systemd/system/wesnothd.service
}
