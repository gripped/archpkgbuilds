# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jacobo Arvelo <unix4all@ya.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=wesnoth
# NOTE: Odd minor versions are unstable! Do not package those.
pkgver=1.18.3
epoch=1
pkgrel=2
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('x86_64')
license=('GPL')
url="https://www.wesnoth.org/"
depends=('sdl2_mixer' 'sdl2_image' 'boost-libs' 'pango' 'dbus' 'lua')
makedepends=('boost' 'cmake' 'git' 'ninja' 'python')
options=(!emptydirs)
source=("git+https://github.com/wesnoth/wesnoth.git#tag=${pkgver}"
        boost-187.patch)
sha512sums=('59011699186606e5201103f99355e91735130c19f99e88910d5ca27e860ac0e72e556e9f118c616a1251ac3de05522591665810191734d0efed273a8463f704d'
            '67575dc11b4e469334e86431d7cb03283fcdc177d0172f4971ec06c12a3ad8916a9ba5204365da1c52603a0f5ee47ebb09663f7210507afddc41e5e3b8a636d6')

prepare() {
  cd "$pkgname"

  patch -Np1 -i "$srcdir"/boost-187.patch
}

build() {
  cd "$pkgname"

  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DENABLE_TOOLS=ON \
      -DFIFO_DIR=/run/wesnothd \
      -DENABLE_DISPLAY_REVISION=OFF \
      -DENABLE_SYSTEM_LUA=ON
  ninja -C build
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" ninja -C build install

  # See https://forums.wesnoth.org/viewtopic.php?t=51038
  echo "Linux repository" > "$pkgdir"/usr/share/wesnoth/data/dist

  mkdir -p "$pkgdir"/usr/lib/{tmpfiles.d,systemd/system}
  install -Dm644 packaging/systemd/wesnothd.sysusers.conf.in "$pkgdir"/usr/lib/sysusers.d/wesnoth.conf
  sed "s|@FIFO_DIR@|/run/wesnothd|" packaging/systemd/wesnothd.tmpfiles.conf.in > "$pkgdir"/usr/lib/tmpfiles.d/wesnothd.conf
  sed -e "s|@FIFO_DIR@|/run/wesnothd|" -e "s|@BINARY_SUFFIX@||" -e "s|@CMAKE_INSTALL_FULL_BINDIR@|/usr/bin|" packaging/systemd/wesnothd.service.in > "$pkgdir"/usr/lib/systemd/system/wesnothd.service
}
