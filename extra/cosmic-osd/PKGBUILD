# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-osd
pkgver=1.0.0.alpha.6
pkgrel=1
pkgdesc='COSMIC On-Screen Display'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osd
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libpulse
  libxkbcommon
  systemd-libs
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=5dd5f23148af65b0ec5add6d88f38694ab34584c
source=(
  git+https://github.com/pop-os/cosmic-osd.git#tag=${_tag}
)
b2sums=('07e9b3116499476cc93fb0decb1bb32256c1d05609c3026ca6a908ca63ee44a1b96a56365d1f395267de8105345b7d9d7ec51eeb23002fe8d1d7b97fc8327e2d')

prepare() {
  cd cosmic-osd
  cargo fetch --locked
  sed 's|libexec|lib/polkit-1|g' -i Makefile src/subscriptions/polkit_agent_helper.rs
}

pkgver() {
  cd cosmic-osd
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-osd
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-osd
  make prefix='/usr' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
