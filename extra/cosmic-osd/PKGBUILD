# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-osd
pkgver=1.0.4
pkgrel=1
epoch=1
pkgdesc='COSMIC On-Screen Display'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osd
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libinput
  libpipewire
  libpulse
  libxkbcommon
  sound-theme-freedesktop
  systemd-libs
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
source=(
  git+https://github.com/pop-os/cosmic-osd.git#tag=epoch-${pkgver}
  suid-polkit-agent-helper.rs
)
b2sums=('2af7c2b354a9d7f2fd2c4796b10c3707a950dd9bb672965b69f7ab996d4956dc2df0db30dc2ba358dbd918712c092326c82b9aef8e153bc3e28692f7a46cae48'
        'b2c1f956aad81b17fa82f0663e4b67265d904af04f67c003f39614f0338ee1ad0cd32e0ed9d7437800ef4b9f82f105533acd14c3b479ce289db4cee1c1309d92')

prepare() {
  cd cosmic-osd
  cargo fetch --locked
}

build() (
  export RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  rustc $RUSTFLAGS -O --edition 2024 suid-polkit-agent-helper.rs

  cd cosmic-osd
  just polkit-agent-helper-1="/usr/lib/${pkgname}/suid-polkit-agent-helper" build-release
)

package() {
  install -Dm4755 suid-polkit-agent-helper -t "${pkgdir}/usr/lib/${pkgname}"

  cd cosmic-osd
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
