# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-osd
pkgver=1.0.0.beta.8
pkgrel=1
pkgdesc='COSMIC On-Screen Display'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osd
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libinput
  libpipewire
  libpulse
  libxkbcommon
  sound-theme-freedesktop
  systemd-libs
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
source=(
  git+https://github.com/pop-os/cosmic-osd.git#tag=epoch-${pkgver/.beta./-beta.}
)
b2sums=('d0a544ecfb5d30149884cccc5a735b1f056b94c1084b6041c3343da88a5046f48ee098deb594fd1318ad3236862ba6f85396409f5fed96a6f876d0686ae776ed')

prepare() {
  cd cosmic-osd
  cargo fetch --locked
}

build() {
  cd cosmic-osd
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld" \
  POLKIT_AGENT_HELPER_1="/usr/lib/polkit-1/polkit-agent-helper-1" \
  just build-release --frozen
}

package() {
  cd cosmic-osd
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
