# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-osd
pkgver=1.0.0.alpha.4
pkgrel=1
pkgdesc='COSMIC On-Screen Display'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osd
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libpulse
  libxkbcommon
  systemd-libs
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=7eedda7be04688f7766c1660a538baf62c85ee93
source=(
  git+https://github.com/pop-os/cosmic-osd.git#tag=${_tag}
  cosmic-osd-lto.patch
)
b2sums=('4d5a18827b498b771c9ac24c194836e83da2c31a7866102577584a80e43f258dcb5545f00530afeea95a1a8de1eb0f68e1223bc347303eda54765d59978d1e8d'
        '85c53fa0409511b863b11bd7cf28fa253890e00872a84e3c358299ea92a18e4a9e1844c676dba9c2118ff5555900d1043465b61d7768afbf82811cb4b7b58563')

prepare() {
  cd cosmic-osd
  patch -Np1 -i ../cosmic-osd-lto.patch
  cargo fetch --locked
  sed 's|libexec|lib/polkit-1|g' -i Makefile src/subscriptions/polkit_agent_helper.rs
}

pkgver() {
  cd cosmic-osd
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-osd
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-osd
  make prefix='/usr' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
