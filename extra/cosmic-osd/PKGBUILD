# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-osd
pkgver=1.0.2
pkgrel=1
epoch=1
pkgdesc='COSMIC On-Screen Display'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osd
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libinput
  libpipewire
  libpulse
  libxkbcommon
  sound-theme-freedesktop
  systemd-libs
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
source=(
  git+https://github.com/pop-os/cosmic-osd.git#tag=epoch-${pkgver}
)
b2sums=('3a8c6104ecd6a9b877196c60d741fef7d5ba12a0f4c6e8ae37c9dc0ed4bbfae9417735c24faf9a9ae7f519365e503eb766019a83fff71bb29546919e3b44759b')

prepare() {
  cd cosmic-osd
  cargo fetch --locked
}

build() {
  cd cosmic-osd
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld" \
  just polkit-agent-helper-1="/usr/lib/polkit-1/polkit-agent-helper-1" build-release
}

package() {
  cd cosmic-osd
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
