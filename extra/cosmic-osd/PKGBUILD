# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-osd
pkgver=1.0.0.alpha.5.1
pkgrel=1
pkgdesc='COSMIC On-Screen Display'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osd
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libpulse
  libxkbcommon
  systemd-libs
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=9dd146bb50a5dc4c6ce4b31736df087c4d1a2a76
source=(
  git+https://github.com/pop-os/cosmic-osd.git#tag=${_tag}
)
b2sums=('291eb6128772db6677d86bc627b1d643a61bcc84c9581f81b5e47d800db8c765a9c9dfa7fd6299dbf0e13d904adbbdec49e96ef98be351e864ec883929a4d5ef')

prepare() {
  cd cosmic-osd
  cargo fetch --locked
  sed 's|libexec|lib/polkit-1|g' -i Makefile src/subscriptions/polkit_agent_helper.rs
}

pkgver() {
  cd cosmic-osd
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-osd
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-osd
  make prefix='/usr' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
