# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-osd
pkgver=1.0.3
pkgrel=1
epoch=1
pkgdesc='COSMIC On-Screen Display'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osd
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libinput
  libpipewire
  libpulse
  libxkbcommon
  sound-theme-freedesktop
  systemd-libs
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
source=(
  git+https://github.com/pop-os/cosmic-osd.git#tag=epoch-${pkgver}
)
b2sums=('fd816e79d0c68391afd893373beb72b0ef6d640c92c97465fef0ab6dc89b906ffba1eecb96ed0cfc26763a85a719e84a5e3af60a1aca57477e64b3d6e9a37543')

prepare() {
  cd cosmic-osd
  cargo fetch --locked
}

build() {
  cd cosmic-osd
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld" \
  just polkit-agent-helper-1="/usr/lib/polkit-1/polkit-agent-helper-1" build-release
}

package() {
  cd cosmic-osd
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
