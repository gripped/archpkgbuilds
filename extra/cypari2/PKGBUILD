# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cypari2
pkgname=python-cypari2
pkgver=2.2.0
pkgrel=10
pkgdesc='Cython bindings for PARI'
arch=(x86_64)
url='https://www.sagemath.org'
license=(GPL-2.0-or-later)
depends=(glibc
         pari
         python
         python-cysignals)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
source=(git+https://github.com/sagemath/cypari2#tag=$pkgver
        pari-2.17.patch)
sha256sums=('015d7421efabb57680fca1445d98585a43228a3f46c36cd7f9cad9b28f5ea0f8'
            '0746739b9d6ffe12544b34078dea878e669822d2a7a1ec87a2130d00564af8cd')

prepare() {
  cd $pkgbase
# PARI 2.17 compatibility
  git cherry-pick -n 0a5a7b42e53d065f8d78bdaa8181d6afa20d1f4f
  patch -p1 -i ../pari-2.17.patch
}

build() {
  cd $pkgbase
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgbase
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  cd tests
  ../test-env/bin/python -m pytest -v
}

package_python-cypari2() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
