# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cypari2
pkgname=python-cypari2
pkgver=2.2.0
pkgrel=7
pkgdesc='Cython bindings for PARI'
arch=(x86_64)
url='https://www.sagemath.org'
license=(GPL-2.0-or-later)
depends=(glibc
         pari
         python
         python-cysignals)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/sagemath/cypari2#tag=$pkgver
        pari-2.17.patch)
sha256sums=('015d7421efabb57680fca1445d98585a43228a3f46c36cd7f9cad9b28f5ea0f8'
            '90629010327c4cadd1e6742cb73c929736c98bfbb4b143edd0bc16806f6f9f9c')

prepare() {
  cd $pkgbase
  patch -p1 -i ../pari-2.17.patch
}

build() {
  cd $pkgbase
  python -m build --wheel --no-isolation
}

package_python-cypari2() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
