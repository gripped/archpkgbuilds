# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cypari2
pkgname=python-cypari2
pkgver=2.2.1
pkgrel=1
pkgdesc='Cython bindings for PARI'
arch=(x86_64)
url='https://www.sagemath.org'
license=(GPL-2.0-or-later)
depends=(glibc
         pari
         python
         python-cysignals)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
source=(git+https://github.com/sagemath/cypari2#tag=$pkgver)
sha256sums=('7041165aa67af52abfd716dee8389f55f776b2c74fd165128f67b1572dab75fe')

build() {
  cd $pkgbase
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgbase
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  cd tests
  ../test-env/bin/python -m pytest -v
}

package_python-cypari2() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
