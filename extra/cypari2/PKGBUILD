# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cypari2
pkgname=python-cypari2
pkgver=2.2.4
pkgrel=2
pkgdesc='Cython bindings for PARI'
arch=(x86_64)
url='https://www.sagemath.org'
license=(GPL-2.0-or-later)
depends=(glibc
         pari
         python
         python-cysignals)
makedepends=(cython
             git
             meson-python
             python-build
             python-installer)
checkdepends=(python-pytest)
source=(git+https://github.com/sagemath/cypari2#tag=$pkgver)
sha256sums=('1d180573f48f9fe00ead45c6aefff5479f1bb4135af62c84bb29ca189ca0e4b2')

build() {
  cd $pkgbase
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgbase
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  cd tests
  ../test-env/bin/python -m pytest -v
}

package_python-cypari2() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
