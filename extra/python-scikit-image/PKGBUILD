# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=scikit-image
pkgname=python-$_name
pkgver=0.26.0
pkgrel=1
pkgdesc="Image processing in Python"
arch=(x86_64)
url="https://github.com/scikit-image/scikit-image"
license=(
  BSD-3-Clause
  BSD-2-Clause
  MIT
)
depends=(
  gcc-libs
  glibc
  python
  python-imageio
  python-lazy-loader
  python-networkx
  python-numpy
  python-packaging
  python-pillow
  python-scipy
  python-tifffile
)
makedepends=(
  cython
  meson-python
  python-build
  python-installer
  python-pythran
)
checkdepends=(
  python-pytest
  python-pytest-pretty
  python-pytest-localserver
  # optdepends
  python-astropy
  python-dask
  python-matplotlib
  python-pooch
  python-scikit-learn
  python-simpleitk
)
optdepends=(
  'python-astropy: for FITS I/O capability'
  'python-dask: used to speed up certain functions'
  'python-matplotlib: for plotting'
  'python-pooch: for data fetching and caching'
  'python-pyamg: fast cg_mg mode of random walker segmentation'
  'python-pywavelets'
  'python-scikit-learn'
  'python-simpleitk: Optional I/O plugin providing a wide variety of formats. including specialized formats using in medical imaging'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
b2sums=('2969e48298ef8e5439b9806b14f06bde8ede3121d399495f85533f017752ae566bc33e2114cfdfb9161d8dd2ff0c638bc7531fac8c32b0221cf03f5cc46dd507')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    # ValueError: Unexpected warning: Image.Image.getdata is deprecated and will be removed in Pillow 14 (2027-10-15). Use get_flattened_data instead.
    --deselect tests/skimage/io/test_pil.py::test_all_color
    --deselect tests/skimage/io/test_pil.py::test_all_mono
    # assert 2 == 1  where 2 = len(WarningsChecker(record=True))
    --deselect tests/skimage/io/test_plugin.py::test_deprecation_warnings_on_plugin_funcs[call_plugin-args1]
    # RuntimeWarning: divide by zero encountered in scalar divide
    --deselect tests/skimage/measure/test_fit.py::test_ellipse_parameter_stability
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
