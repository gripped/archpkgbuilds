# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=scikit-image
pkgname=python-$_name
pkgver=0.25.2
pkgrel=2
pkgdesc="Image processing in Python"
arch=(x86_64)
url="https://github.com/scikit-image/scikit-image"
license=(
  BSD-3-Clause
  BSD-2-Clause
  MIT
)
depends=(
  gcc-libs
  glibc
  python
  python-imageio
  python-lazy-loader
  python-networkx
  python-numpy
  python-packaging
  python-pillow
  python-scipy
  python-tifffile
)
makedepends=(
  cython
  meson-python
  python-build
  python-installer
  python-pythran
)
checkdepends=(
  python-pytest
  # optdepends
  python-astropy
  python-cloudpickle
  python-dask
  python-matplotlib
  python-pooch
  python-scikit-learn
  python-simpleitk
)
optdepends=(
  'python-astropy: for FITS I/O capability'
  'python-cloudpickle: necessary to provide the 'processes' scheduler for dask'
  'python-dask: used to speed up certain functions'
  'python-matplotlib: for plotting'
  'python-pooch: for data fetching and caching'
  'python-pyamg: fast cg_mg mode of random walker segmentation'
  'python-pywavelets'
  'python-scikit-learn'
  'python-simpleitk: Optional I/O plugin providing a wide variety of formats. including specialized formats using in medical imaging'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
b2sums=('652eff12e70cf23cd91dedc92ee7ebab9ec4901394c0803fdf663b03770ef1c456648bbf4006239e06f3ed6f7c11f1069a2c46aa8fc704a836840a8b8eb3c543')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    # select tests from the Python module rather than by path
    --pyargs skimage
    # skip test that depends on the imread package
    # skip test that downloads data from the internet
    # skip test that fails due to division by zero
    -k "not test_imread_http_url and not test_download_all_with_pooch and not test_ellipse_parameter_stability"
  )

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer $_name-$pkgver/dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
