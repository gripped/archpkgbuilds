# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-docs
_upstream=alpm
_upstream_project=alpm-types
pkgver=0.9.0
pkgrel=1
pkgdesc="Specifications for Arch Linux Package Management"
arch=(any)
url="https://alpm.archlinux.page/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(CC-BY-SA-4.0)
groups=(alpm)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
source=($_upstream::git+$_url?signed#tag=$_upstream_project/$pkgver)
sha512sums=('5ad436deff2a1a59c34e0d84f15a89c00ff6572df5171128a9d051c402856310b1d934fc913167d7e9046a630edb25f1e00eb0628a7967565fb8775e21ee3733')
b2sums=('56b70423a07b7ff663331f30a8e3004771dd6dce9f968987c6fe8d831eb9d0cd9245d00bbf97a0721a650e7a6cd3dab7e60a6c91f56b4de4066fcb69a29b367a')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

build() {
  local projects=(
    $_upstream_project
    alpm-package
    alpm-state-repo
  )

  cd $_upstream
  for project in "${projects[@]}"; do
    just generate specifications "$project"
  done
}

package() {
  install -vDm 644 $_upstream/output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 $_upstream/output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
}
