# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-docs
_upstream=alpm
_upstream_project=alpm-types
pkgver=0.6.0
pkgrel=1
pkgdesc="Specifications for Arch Linux Package Management"
arch=(any)
url="https://alpm.archlinux.page/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(CC-BY-SA-4.0)
groups=(alpm)
makedepends=(
  git
  just
  rust
  rust-script
)
source=($_upstream::git+$_url?signed#tag=$_upstream_project/$pkgver)
sha512sums=('a795ad8ebd39d93f15683ca148f0ebe7ac72d3c5a8149bbc48dc5996713f32f58a7945be3a9bfea58fb67bab5de36ae1ab241e32c659b5be1a84e457b1bc3912')
b2sums=('fcd5efceb28ae2a0de8960110db735d6e659329b916a3ddd2a689c7ebca213a82f89ab235d6ee10d5a236bfb09da3fed358ee2daa166fbda531efd7175c75590')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

build() {
  cd $_upstream
  just generate specifications $_upstream_project
}

package() {
  install -vDm 644 $_upstream/output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 $_upstream/output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
}
