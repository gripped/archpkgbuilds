# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-docs
_upstream=alpm
_upstream_project=alpm-types
pkgver=0.11.1
pkgrel=1
pkgdesc="Specifications for Arch Linux Package Management"
arch=(any)
url="https://alpm.archlinux.page/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(CC-BY-SA-4.0)
groups=(alpm)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
source=($_upstream::git+$_url?signed#tag=$_upstream_project/$pkgver)
sha512sums=('0779abb7e002dfe660f08f64a46d29c8057d6a10a22aad0353d0428be0517a16179ae72f5d4a4d0d4bad9598e6344404997c05dcbc2a16d1e4752d4e962f999f')
b2sums=('acbcdfaf522e0bfd150eb6ea37ff1420a0543ddda052e0b42e0ff9e30af7885bf2e5c884224d43576ca402f76e7209150277d129f941aa4e164fe72e2a7005b7')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

build() {
  local projects=(
    alpm-package
    alpm-repo
    alpm-state-repo
    $_upstream_project
  )

  cd $_upstream
  for project in "${projects[@]}"; do
    just generate specifications "$project"
  done
}

package() {
  install -vDm 644 $_upstream/output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 $_upstream/output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
}
