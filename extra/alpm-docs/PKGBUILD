# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-docs
_upstream=alpm
_upstream_project=alpm-types
pkgver=0.11.0
pkgrel=1
pkgdesc="Specifications for Arch Linux Package Management"
arch=(any)
url="https://alpm.archlinux.page/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(CC-BY-SA-4.0)
groups=(alpm)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
source=($_upstream::git+$_url?signed#tag=$_upstream_project/$pkgver)
sha512sums=('43d70fdbc74627329a112adedccd8152e79fc2a940fe6c76d77903d12cf734077a3de369363e05fd651f36d4afcfcee39e752861a5914f65055c624641ed19dc')
b2sums=('d316505eeb5a4fd650ef307becea925e852932710fa885451eb1d2cfb0f39b94b1db7b94a82c5fbd83578a7be706084e7f6ef36369bfa30e1143aa4e262c8178')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

build() {
  local projects=(
    alpm-package
    alpm-repo
    alpm-state-repo
    $_upstream_project
  )

  cd $_upstream
  for project in "${projects[@]}"; do
    just generate specifications "$project"
  done
}

package() {
  install -vDm 644 $_upstream/output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 $_upstream/output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
}
