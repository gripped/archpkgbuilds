# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-docs
_upstream=alpm
_upstream_project=alpm-types
pkgver=0.7.0
pkgrel=2
pkgdesc="Specifications for Arch Linux Package Management"
arch=(any)
url="https://alpm.archlinux.page/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(CC-BY-SA-4.0)
groups=(alpm)
makedepends=(
  git
  just
  rust
  rust-script
)
source=($_upstream::git+$_url?signed#tag=$_upstream_project/$pkgver)
sha512sums=('a13efd1ca770e85b87e81e38315f85a9fc71d11570072e852b344a89b7a42705d863d7e6cce48cbe07f89c0e4fbdcfd4950425cdf37d078e204d2a37f8858d99')
b2sums=('6791f1256fe75b3a438357f0cc4f7d9d0742f93eef50459644e50520b7c2a3dea927b4713c9e90dcae1e17a97a48c0d41cd268b5ed254a12425550950e658f93')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

build() {
  local projects=(
    $_upstream_project
    alpm-package
    alpm-state-repo
  )

  cd $_upstream
  for project in "${projects[@]}"; do
    just generate specifications "$project"
  done
}

package() {
  install -vDm 644 $_upstream/output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 $_upstream/output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
}
