# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-docs
_upstream=alpm
_upstream_project=alpm-types
pkgver=0.10.0
pkgrel=1
pkgdesc="Specifications for Arch Linux Package Management"
arch=(any)
url="https://alpm.archlinux.page/"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(CC-BY-SA-4.0)
groups=(alpm)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
source=($_upstream::git+$_url?signed#tag=$_upstream_project/$pkgver)
sha512sums=('987878aa703522d18fca44b3b5c753d6393a3b0ee67516fbddf371d99386fc453e98dfc4b9d6a8fb8f67ab6ffef82cecd0c15bc9ac4404e28b7d9db18e295176')
b2sums=('a0783a6e686dfc2211751be040be9600de40cba01e524bbb8f5f7f89949ae66ade36ec68d40551bfa31e75746dc76ce446d1f31c92dcb09706e90b1c960ffd2c')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

build() {
  local projects=(
    alpm-package
    alpm-repo
    alpm-state-repo
    $_upstream_project
  )

  cd $_upstream
  for project in "${projects[@]}"; do
    just generate specifications "$project"
  done
}

package() {
  install -vDm 644 $_upstream/output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 $_upstream/output/manpages/*.7 -t "$pkgdir/usr/share/man/man7/"
}
