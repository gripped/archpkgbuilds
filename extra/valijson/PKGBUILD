# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=valijson
pkgver=1.0.4
pkgrel=1
pkgdesc="Header-only JSON Schema validation library for C++"
url="https://github.com/tristanpenman/valijson"
arch=(any)
license=(BSD-2-Clause)
depends=(
  boost
)
makedepends=(
  cmake
  git
  ninja
  qt5-base
)
source=("git+$url#tag=v$pkgver")
b2sums=('e4e258a8fce74197b9b2afbab5b4ef0882ca4566219660941850329dc4f37593c43a8a955d80c2d16ecd4015003bc76405849dd2032c16cbd5a0af3833569820')

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
  )

  cmake -S valijson -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 valijson/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
