# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=valijson
pkgver=1.0.5
pkgrel=1
pkgdesc="Header-only JSON Schema validation library for C++"
url="https://github.com/tristanpenman/valijson"
arch=(any)
license=(BSD-2-Clause)
depends=(
  boost
)
makedepends=(
  cmake
  git
  ninja
  qt5-base
)
source=("git+$url#tag=v$pkgver")
b2sums=('db6e7f44a0fec290e8b6349a09b9a51898a6ab6557378cb6771f2bab833fa0d5b538d3702533e2e082d6baf8b2a462205100345e2abe8ab488e9f87e5541ea20')

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
  )

  cmake -S valijson -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 valijson/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
