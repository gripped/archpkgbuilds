# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=lua-compat53
_rockname=${pkgbase##lua-}
pkgname=(lua52-$_rockname lua51-$_rockname)
pkgver=0.14.3
pkgrel=3
_rockrel=1
pkgdesc='Compatibility module providing Lua-5.3-style APIs'
arch=(x86_64)
url='https://github.com/lunarmodules/lua-compat-5.3'
license=(MIT)
depends=(glibc)
makedepends=(lua51
             lua52
             luarocks)
_archive="${url##*/}-$pkgver"
_rockspec="$_rockname-$pkgver-$_rockrel"
_rock="$_rockspec.linux-$CARCH.rock"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('db8ffa2c5562fac4fce5e5f48fd25763e79190af5e3d3d91f0699e4711cfbf10')

build() {
	cd "$_archive"
	for LUAVER in 5.{1,2}; do
		luarocks --lua-version "$LUAVER" \
			make --pack-binary-rock --deps-mode none -- "rockspecs/$_rockspec.rockspec"
		install -Dm0644 -t "lua-$LUAVER/" "$_rock"
	done
}

_package() {
	cd "$_archive"
	pkgdesc+=" for Lua $1"
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		install --deps-mode none --no-manifest -- "lua-$1/$_rock"
	install -Dm0664 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_lua51-compat53() {
	_package 5.1
}

package_lua52-compat53() {
	_package 5.2
}
