# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-anthy
pkgver=5.1.5
pkgrel=1
pkgdesc="Anthy Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-anthy"
license=('GPL')
depends=('fcitx5' 'anthy')
makedepends=('git' 'extra-cmake-modules' 'ninja')
source=("git+https://github.com/fcitx/fcitx5-anthy.git#tag=$pkgver?signed")
sha512sums=('f266d15978ffb41cea6b91ef819cffc076b41100597dccf45c2ab774c0dd9c90f0aac9d71e459deec5bb5858049a93748017db5a4f49c2cc2e9e049021401c31')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname
  ninja test
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
