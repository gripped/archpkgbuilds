# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-anthy
pkgver=5.1.9
pkgrel=1
pkgdesc="Anthy Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-anthy"
license=('GPL-2.0-or-later')
depends=('fcitx5' 'anthy')
makedepends=('git' 'extra-cmake-modules' 'ninja')
source=("git+https://github.com/fcitx/fcitx5-anthy.git#tag=$pkgver?signed")
sha512sums=('e0b48cbff44852b6ffa6ea5e2099913692b30ed720e31b42b5858200bef80c546295754055a4b0873af4f7734065e437ce7b1aa135acc13886d7dcce278c8bdc')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname
  ninja test
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
