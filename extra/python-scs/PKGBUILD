# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=scs
pkgname=python-$_pyname
pkgver=3.2.4
pkgrel=1
pkgdesc='Splitting Conic Solver'
url='https://github.com/cvxgrp/scs/'
license=(MIT)
arch=(x86_64)
depends=(python-scipy)
makedepends=(meson-python
             python-build
             python-installer)
checkdepends=(python-pytest)
source=(https://pypi.org/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz
        find-lapack.patch)
sha256sums=('09ffaa670d8608901db94addd8581376498cd9818eb5ef27fed3560f0bf0661b'
            '3df603a4abcad273db20d45458dba65f0e52c4f0c6fd51a3364b2f9c2d738536')

prepare() {
  cd $_pyname-$pkgver
  patch -p1 -i ../find-lapack.patch # Fix linking to lapack
}

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
