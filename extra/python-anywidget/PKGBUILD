# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=anywidget
pkgname=python-$_name
pkgver=0.9.13
pkgrel=1
pkgdesc="Reusable widgets made easy"
arch=(any)
url="https://anywidget.dev/"
license=(MIT)
depends=(
  ipython
  python
  python-comm
  python-ipywidgets
  python-psygnal
  python-traitlets
  python-typing_extensions
)
makedepends=(
  python-build
  python-installer
  python-hatchling
  python-hatch-jupyter-builder
)
checkdepends=(
  python-pytest
  python-pydantic
  python-watchfiles
)
optdepends=(
  'python-watchfiles: for live reloading during development'
)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name//-/_}/${_name//-/_}-$pkgver.tar.gz")
b2sums=('591cab9c46c30f3714cec3ca488d9c02a25f76f4fba15ec3c01958913d50a73b943fdee0827a10c4d4dfbba505d7c2ad71dc99fa3f82aea13d255dad83d30c31')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    # FileNotFoundError: [Errno 2] No such file or directory: '/build/python-anywidget/src/anywidget-0.9.13/tests/../packages/anywidget/package.json'
    --deselect tests/test_widget.py::test_version
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
