# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=moosefs
pkgver=4.57.6
pkgrel=1
pkgdesc='High performing and fault tolerant distributed file system'
url='https://moosefs.com/'
replaces=(mfs-chunkserver mfs-client mfs-master)
conflicts=(mfs-chunkserver mfs-client mfs-master)
arch=(x86_64)
license=(GPL-2.0-only)
depends=(fuse3 libpcap python xfsprogs)
makedepends=(git)
source=("git+https://github.com/moosefs/moosefs#tag=v$pkgver"
        moosefs.tmpfiles
        moosefs.sysusers)
b2sums=('0d25901cd017f003cb68906b417f9c2d5d8108b494f2d0bd16cf188433fe6c2897e10db933d6f1347f6f902ccd2f9aae23bf6b55c865e212436e41716a1d24e4'
        '60c51df91a09e6680e10ffd0b3284a594ba585226f4acad7d389d6b39e98243d980155cb2ab83acf1a1b4ccf167bc682b7672121587e69280832127d472ede38'
        '138c34c230cb72492d2eafeca2269dc1443018e5cd7c8f6d16c0f50f55f22f3c5e1592f05f4070dce68b6e56fb9c36aabc9a2f52737ba3f9c061eb4f6bc983da')

build() {
  cd $pkgname
  ./configure \
    --localstatedir=/var/lib \
    --mandir=/usr/share/man \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-default-group=mfs \
    --with-default-user=mfs \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  make DESTDIR="$pkgdir" -C $pkgname install

  install -Dm644 "$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -Dm644 "$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  # Move mount.moosefs from sbin to usr/bin to silence a warning
  mv "$pkgdir/sbin/"* "$pkgdir/usr/bin/"
  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/sbin" "$pkgdir/usr/sbin"
}
