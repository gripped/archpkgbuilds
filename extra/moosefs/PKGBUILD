# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=moosefs
pkgver=4.58.1
pkgrel=1
pkgdesc='High performing and fault tolerant distributed file system'
url='https://moosefs.com/'
replaces=(mfs-chunkserver mfs-client mfs-master)
conflicts=(mfs-chunkserver mfs-client mfs-master)
arch=(x86_64)
license=(GPL-2.0-only)
depends=(fuse3 libpcap python xfsprogs)
makedepends=(git)
source=("git+https://github.com/moosefs/moosefs#tag=v$pkgver"
        moosefs.tmpfiles
        moosefs.sysusers)
b2sums=('e6b25fbb6b84953656868512467bed60cd5e87c6a8ba09735649fef28d2c45e2b9ff5c2497da23cf079cc6d055ace894f48d72c1fb471d47590f14c9aa9f1abc'
        '60c51df91a09e6680e10ffd0b3284a594ba585226f4acad7d389d6b39e98243d980155cb2ab83acf1a1b4ccf167bc682b7672121587e69280832127d472ede38'
        '138c34c230cb72492d2eafeca2269dc1443018e5cd7c8f6d16c0f50f55f22f3c5e1592f05f4070dce68b6e56fb9c36aabc9a2f52737ba3f9c061eb4f6bc983da')

prepare() {
  # ref https://gitlab.archlinux.org/archlinux/packaging/packages/moosefs/-/issues/1
  echo -n 'MFSM NEW' > metadata.mfs.empty

  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure \
    --localstatedir=/var/lib \
    --mandir=/usr/share/man \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-default-group=mfs \
    --with-default-user=mfs \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  make DESTDIR="$pkgdir" -C $pkgname install

  install -Dm644 "$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -Dm644 "$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  install -Dm644 metadata.mfs.empty "$pkgdir/var/lib/mfs/metadata.mfs.empty"

  # Move mount.moosefs from sbin to usr/bin to silence a warning
  mv "$pkgdir/sbin/"* "$pkgdir/usr/bin/"
  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/sbin" "$pkgdir/usr/sbin"
}
