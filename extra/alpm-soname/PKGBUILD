# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-soname
_upstream=alpm
pkgver=0.2.0
pkgrel=1
pkgdesc="A CLI for looking up soname data in an ALPM context"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-soname/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  alpm-pkgbuild-bridge
  gcc-libs
  glibc
  xz
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
checkdepends=(meson)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('204df4f3fa12102fe56ecdff4183c16ce862b69855e305aace53ebf6d2398ca875bb4baec35c12e3c939851a4664980f191ca27fa05397651ab4eec6ca60d71f')
b2sums=('65b674836ac190adaf66d92c1995d98d4645b89937721da3628d196071fb3a092103e6f149920dbbe8f29ee01af85d446dfc401dafa1808f9619defa8746777c')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
