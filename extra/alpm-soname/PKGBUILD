# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-soname
_upstream=alpm
pkgver=0.3.0
pkgrel=1
pkgdesc="A CLI for looking up soname data in an ALPM context"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-soname/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  alpm-pkgbuild-bridge
  gcc-libs
  glibc
  xz
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
checkdepends=(meson)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('dde5bd07f6f46ae604d5440cb3b62ea4aa985ff6720f82e29a7fc153604c2d4fa24148ece4344e1ed187c9256783598498c22f0e8e0e90966fd24616a213c71c')
b2sums=('bdd97677c14a1b3d4cf78fcfde5a84baa290a1059ecc497ca32fef42f0d1f16efb889c6e2f399f4a0811ddaf4ea8becbecb25eb846d95a11a402fd7f22fe2d72')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
