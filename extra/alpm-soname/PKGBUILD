# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-soname
_upstream=alpm
pkgver=0.4.1
pkgrel=1
pkgdesc="A CLI for looking up soname data in an ALPM context"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-soname/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
  xz
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
checkdepends=(meson)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('a451963a8ebc723fceb57e9f0ddc3c871abc03e22f3fa4a6a4f42e4965dec5b1a5eadf630b9942eeeb5ac98492fe759a4f5fb8621085d2f063ab0bc822461346')
b2sums=('90ef743d286585788de5027733325a7b1573ad98ab441ee7476ee0854657499c873d22d3519465aab37640b7be4755d926b5c3878467e91ee0fe4b002d37c075')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
