# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-soname
_upstream=alpm
pkgver=0.4.0
pkgrel=1
pkgdesc="A CLI for looking up soname data in an ALPM context"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-soname/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  alpm-pkgbuild-bridge
  gcc-libs
  glibc
  xz
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
checkdepends=(meson)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('4c7be25a34eed1914fca3e196cf4e530e90d568fb6d126a31dafcc558fea6ce661c075f2906e1a34a7a860986a857f6a0c6691e66fc5ac6a19da0de07ca4e4cc')
b2sums=('fac38590d5f555289d7aab708448d5323740258ce5906513248db866ad4e7f4740337ac9af91470f9a51be9708c318c2f2167253105282b844d36aa381011a21')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
