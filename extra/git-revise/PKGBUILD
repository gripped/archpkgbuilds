# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Legner <Simon.Legner@gmail.com>

pkgname=git-revise
pkgdesc='A git subcommand to efficiently update, split, and rearrange commits'
pkgver=0.8.0
pkgrel=1
url="https://mystor.github.io/$pkgname.html"
arch=(any)
license=(MIT)
depends=(git
         python)
makedepends=(python-{build,installer,wheel}
             python-hatchling)
_archive="${pkgname/-/_}-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$_archive.tar.gz")
sha256sums=('3239b1809cd659b33f6f323d3bfca5c7e3a9eb2eace223cf63b346f91c8c831c')

build() {
	cd "$_archive"
	python -m build -wn
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
