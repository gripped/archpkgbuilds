# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=python-dateparser
pkgname=(
  python-dateparser
  python-dateparser-docs
)
pkgver=1.2.2
pkgrel=2
pkgdesc='Python parser for human readable dates'
arch=(any)
url='https://github.com/scrapinghub/dateparser'
license=(BSD-3-Clause)
depends=(
  python
  python-dateutil
  python-pytz
  python-regex
  python-tzlocal
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-sphinx_rtd_theme
  python-wheel
)
checkdepends=(
  python-parameterized
  python-pytest
)
source=("git+https://github.com/scrapinghub/dateparser.git#tag=v$pkgver")
b2sums=(5c4f31a1c0ac3510076a37dee194b78a0573421ffef30eaafa5a47906af871356c5b684b94d33235fc176c533604669f841a4d57c00a442b58d6499ab9a34f52)

build() {
  cd dateparser
  python -m build --wheel --no-isolation
}

check() {
  cd dateparser
  pytest tests \
    -k 'not test_relative_base_setting_2_en' \
    --ignore tests/test_dateparser_data_integrity.py \
    --ignore tests/test_hijri.py \
    --ignore tests/test_jalali.py \
    --ignore tests/test_language_detect.py
}

package_python-dateparser() {
  cd dateparser
  python -m installer --destdir="$pkgdir" dist/*.whl
  rm -r "$pkgdir$(python -c 'import site; print(site.getsitepackages()[0])')/dateparser_scripts"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_python-dateparser-docs() {
  pkgdesc="Developer documentation for python-dateparser"
  depends=()

  cd dateparser
  python -m sphinx -b html docs "$pkgdir/usr/share/doc/$pkgbase/html"
  rm -r "$pkgdir/usr/share/doc/$pkgbase/html/.doctrees"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
