# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.52
pkgrel=2
pkgdesc="Fully featured MPD client which runs in a terminal"
arch=(x86_64)
url=https://www.musicpd.org/clients/ncmpc/
_url=https://github.com/MusicPlayerDaemon/ncmpc
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  lirc
  pcre2
)
makedepends=(
  boost
  fmt
  git
  libmpdclient
  meson
  ncurses
  python-sphinx
)
optdepends=(
  'python-beautifulsoup4: for lyrics scripts'
  'python-requests: for lyrics scripts'
)
source=(git+$_url.git?signed#tag=v$pkgver)
sha512sums=('73f9f97e49dd59a72d8603d9d562465da5846549e3eb519adf4272166eb04395c21e2a787a9c9a2f06a1632589f9474d982440c0b4617fafd3edffbd3d257670')
b2sums=('1809cd9f9c24371b225cc4fcaddf08e045bb3d2b2875a9edeb07a1f91e5891289edd5d6eb40404a1a3b81cc29d51f6f58f4fa59e50018d8bc0d48b4bb7b5456c')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  arch-meson -D lyrics_screen=true -D chat_screen=true build $pkgname
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    fmt libfmt.so
    libmpdclient libmpdclient.so
    ncurses libncursesw.so
    pcre2 libpcre2-8.so
  )

  meson install -C build --destdir "$pkgdir"
}
