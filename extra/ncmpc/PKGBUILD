# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.51
pkgrel=1
pkgdesc="Fully featured MPD client which runs in a terminal"
arch=(x86_64)
url=https://www.musicpd.org/clients/ncmpc/
_url=https://github.com/MusicPlayerDaemon/ncmpc
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  lirc
  pcre2
)
makedepends=(
  boost
  fmt
  git
  libmpdclient
  meson
  ncurses
  python-sphinx
)
optdepends=(
  'python-beautifulsoup4: for lyrics scripts'
  'python-requests: for lyrics scripts'
)
source=(git+$_url.git?signed#tag=v$pkgver)
sha512sums=('555a7f2d1eb5bd14bf3e82f83f2da6766a0c9ea1c2ff6200d66d384bd176c83b7eccb968ac35acc84898dd4a7711ed0e07bca31b5d2c5461f74f46985fbe424e')
b2sums=('c18ba31da7a1dba59785032bb5d041eb06a562f1176f68bf0b4dff75464c6c392133c6db94da62b54101145186f5694823c839ce7c99cc60b2b62557877a91bc')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  arch-meson -D lyrics_screen=true -D chat_screen=true build $pkgname
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    fmt libfmt.so
    libmpdclient libmpdclient.so
    ncurses libncursesw.so
    pcre2 libpcre2-8.so
  )

  meson install -C build --destdir "$pkgdir"
}
