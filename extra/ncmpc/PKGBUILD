# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.50
pkgrel=1
pkgdesc="Fully featured MPD client which runs in a terminal"
arch=(x86_64)
url=https://www.musicpd.org/clients/ncmpc/
_url=https://github.com/MusicPlayerDaemon/ncmpc
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  lirc
  pcre2
)
makedepends=(
  boost
  fmt
  git
  libmpdclient
  meson
  ncurses
  python-sphinx
)
optdepends=(
  'python-beautifulsoup4: for lyrics scripts'
  'python-requests: for lyrics scripts'
)
source=(git+$_url.git?signed#tag=v$pkgver)
sha512sums=('171bcf8ba9d1ab2491f9be53256bc2cd85133957d6d5432c506d6967b443cf7a041cd2c4baa6407dd8c006c54e4a9d1ac1a1b81b932c50a228877c564f8f5e2a')
b2sums=('81289df71855e44ebea66ba6748549bd3419de66dcbf56a1bee328de02e5eafd9339372fea894bae2882e3cf8652e4760ee154350adc463ac77d24f04184df84')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  arch-meson -D lyrics_screen=true -D chat_screen=true build $pkgname
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    fmt libfmt.so
    libmpdclient libmpdclient.so
    ncurses libncursesw.so
    pcre2 libpcre2-8.so
  )

  meson install -C build --destdir "$pkgdir"
}
