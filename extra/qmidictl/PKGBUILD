# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=1.0.2
pkgrel=2
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=(x86_64)
url="https://qmidictl.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  qt6-tools
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('f17f6b9552ea0446ac9b6d7118a64051166381bf574fd00ad045e0dbe2fa130bf4f045d0ce789df372de4ba8843916d784e17d2e5491eca0dea67160fbbfdd18')
b2sums=('128316eb115098f10470a927d1e3d675a8dc981d4fe0db97510512f3ed1fa145bec630bfab8a5d93e4f362f426b5b7abcc95a4891360dd9324e3f94cf79b3f9b')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
