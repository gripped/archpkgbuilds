# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=1.0.1
pkgrel=1
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=(x86_64)
url="https://qmidictl.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  qt6-tools
)
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('ebf6dd398e61ea7ee263813cf064aca8cc658ac37204234f42186c0fc08e82abd0559bd2e6677c1a53cc318b64d928df9abf856cbd30d4dce6e3f5a57bb5c8f0')
b2sums=('55e6482e898ec58651ca9b109af9e9ab0ba363562f6e46719c059cd183db1bffcfad6280a7f726407252c61aa40cc993f0151f51b44d3185ae31f7d125c11a81')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
