# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: maoxuner <maoxuner at 126 dot com>
# Contributor: SÃ©bastien Deligny <sebdeligny at gmail>

pkgname=thunar-shares-plugin
pkgver=0.4.0
pkgrel=1
epoch=1
pkgdesc="Thunar plugin to quickly share a folder using Samba without requiring root access"
arch=('x86_64')
url="https://docs.xfce.org/xfce/thunar/thunar-shares-plugin"
license=('GPL-2.0-or-later')
depends=('thunar' 'samba')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/thunar-plugins/thunar-shares-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('bedb41dc7b2967d3622abce58dce47187598ddc4d1917a5f70b7595ac04f7999')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
