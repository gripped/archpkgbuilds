# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=nwg-drawer
pkgver=0.6.1
pkgrel=1
pkgdesc="Application drawer for sway and other wlroots-based compositors"
url="https://github.com/nwg-piotr/nwg-drawer"
arch=('x86_64')
license=('MIT')
depends=('gtk3' 'gtk-layer-shell' 'xdg-utils')
makedepends=('go' 'gobject-introspection')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('e6b1b1c2ba3f4bb1e691c4c9c94ebe400be91575fcac9fb6f4f4134508b25bff')

build() {
        cd "${pkgname}-${pkgver}"
        export CGO_CPPFLAGS="${CPPFLAGS}"
        export CGO_CFLAGS="${CFLAGS}"
        export CGO_CXXFLAGS="${CXXFLAGS}"
        export CGO_LDFLAGS="${LDFLAGS}"
        export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
        go build -o "build/${pkgname}" *.go
}

package() {
        cd "${pkgname}-${pkgver}"
	install -Dm 755 "build/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 drawer.css "${pkgdir}/usr/share/${pkgname}/drawer.css"
	install -Dm 644 img/* -t "${pkgdir}/usr/share/${pkgname}/img/"
	install -Dm 644 desktop-directories/* -t "${pkgdir}/usr/share/${pkgname}/desktop-directories/"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
