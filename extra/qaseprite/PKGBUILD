# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=qaseprite
pkgver=1.0
pkgrel=1
pkgdesc="A Qt image format plugin for loading Aseprite files"
arch=('x86_64')
url="https://github.com/mapeditor/qaseprite"
license=('MIT')
depends=('glibc' 'qt6-base' 'zlib' 'libpng' 'pixman' 'freetype2' 'harfbuzz' 'libxcursor' 'xorg-xinput')
makedepends=('cmake' 'ninja')
source=("https://github.com/mapeditor/qaseprite/releases/download/$pkgver/$pkgname-$pkgver-source.tar.gz")
sha256sums=('f43f9313854c25228d8a6565a67e2220440a85c3aff526aef2948ac0cee710d2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DUSE_SHARED_ZLIB=on \
    -DUSE_SHARED_LIBPNG=on \
    -DUSE_SHARED_PIXMAN=on \
    -DUSE_SHARED_FREETYPE=on \
    -DUSE_SHARED_HARFBUZZ=on
  cmake --build build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
