# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-settings-daemon
pkgver=1.0.0.alpha.6
pkgrel=1
pkgdesc='Cosmic settings daemon'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-settings-daemon
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  acpid
  adw-gtk-theme
  alsa-utils
  gcc-libs
  geoclue
  glibc
  libinput
  playerctl
  systemd-libs
)
makedepends=(
  cargo
  git
  lld
)
optdepends=(
  'pulseaudio-alsa: Media keys support'
)
_tag=58ff27f9723cd68b6e143b7acadcfe20324ea83c
source=(
  git+https://github.com/pop-os/cosmic-settings-daemon.git#tag=${_tag}
  cosmic-settings-daemon-lto.patch
)
b2sums=('4410f740e72a85c6fca3333a7ff94503268cba2eedca6e6b9c41b5e807315689f22a5e8f1b06d941c93c144eeea0b7fdeedef2f66c69a80612db776202e2066a'
        'd08aacbcc5b98304f9a99bc2d6a0072473f4ccd9175dcd558334179561bb2e68c0c339471f6e85b849087108998fb1b17707be9d3c87bd018c75b45b7774e91e')

prepare() {
  cd cosmic-settings-daemon
  patch -Np1 -i ../cosmic-settings-daemon-lto.patch
  cargo fetch --locked
  sed 's/libexec/lib/g' -i Makefile src/main.rs
  sed 's/sudo/wheel/g' -i data/polkit-1/rules.d/cosmic-settings-daemon.rules
}

pkgver() {
  cd cosmic-settings-daemon
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-settings-daemon
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-settings-daemon
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
