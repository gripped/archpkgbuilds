# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=snd
pkgver=24.2
pkgrel=1
pkgdesc="An advanced sound editor"
arch=(x86_64)
url="https://ccrma.stanford.edu/software/snd/"
license=(
  BSD-2-Clause
  'GPL-2.0-or-later OR GL2PS'
  'LGPL-2.1-only WITH LLGPL'
  TCL
)
groups=(pro-audio)
depends=(
  glibc
  glu
  gmp
  gsl
  libglvnd
  libmpc
  libx11
  libxext
  libxpm
  libxt
  mpfr
  openmotif
)
# TODO: maybe replace mpg123 with mpg321
makedepends=(
  alsa-lib
  fftw
  flac
  gendesk
  jack
  ladspa
  libsamplerate
  mpg123
  ruby
  speex
  timidity++
  vorbis-tools
  wavpack
)
optdepends=(
  'flac: For flac support'
  'mpg123: For MPEG support'
  'ruby: for snd-ruby'
  'speex: For speex support'
  'timidity++: For MIDI to WAVE support'
  'vorbis-tools: For OGG support'
  'wavpack: For wav support'
)
source=(
  https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
  GL2PS.txt
)
sha512sums=('36ae3d30312a2140448647ca4ce5709bc7f4f8f25bd90e6b89a6fb90afe2f11c23a79aba61ddbea86765b5829ecb63722981591bb026c0354ca35ae90f8ef64d'
            'b4d26fb150cdce132138e080599e5690cde980ab7c687a2d4cb690a8ec9489588de8cd763cfb93a5617f017f438fcde827be71611aa9e460c5f0612d112e76cd')
b2sums=('dba1af54e39de735ad1dce4ae66e2b03945021023b2d720c141509939bd6094b7e72da10123e176f65121d8ff65e8c8ce9f97dfd149dfd5fcfc244f4fce531a0'
        'c4afa4f44d97eca049002d7fa2c1b20547b550800fe7133625d3c75b24f39005f0f846f7412ef366d820ebb01c82f60c484d1559d31766ab8f6b37514f439a3c')

prepare() {
  head -n 25 $pkgname-$pkgver/sndins/sndins.h tail -n 22 | sed 's/\* //g;s/\*//g' > BSD-2-Clause.txt
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname-s7" \
          --name "$pkgname (s7)" \
          --exec "$pkgname-s7" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname-ruby" \
          --name "$pkgname (ruby)" \
          --exec "$pkgname-ruby" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname" \
          --name $pkgname \
          --exec $pkgname \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  cp -av $pkgname-$pkgver $pkgname-ruby-$pkgver
}

build() {
  local options=(
    --prefix=/usr
    --with-alsa
    --with-gl
    --with-gmp
    --with-jack
    --with-ladspa
    --with-motif
  )
  (
  cd $pkgname-$pkgver
  ./configure "${options[@]}" --with-s7
  make
  )
  (
  cd $pkgname-ruby-$pkgver
  ./configure "${options[@]}" --with-ruby
  make
  )
}

package() {
  depends+=(libasound.so libfftw3.so libjack.so libsamplerate.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  mv -v "$pkgdir/usr/bin/snd"{,-s7}

  make DESTDIR="$pkgdir" install -C $pkgname-ruby-$pkgver
  mv -v "$pkgdir/usr/bin/snd"{,-ruby}

  # set s7 version as default
  ln -sv snd-s7 "$pkgdir/usr/bin/snd"

  # XDG desktop integration
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications/"
  # license
  install -vDm 644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/TCL.txt"
  install -vDm 644 BSD-2-Clause.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 GL2PS.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  # docs
  install -vDm 644 $pkgname-$pkgver/{{HISTORY,README}.Snd,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/*.html -t "$pkgdir/usr/share/doc/$pkgname/html"
  install -vDm 644 $pkgname-$pkgver/pix/*.png -t "$pkgdir/usr/share/doc/$pkgname/html/pix"

  # remove examples of unsupported extension language forth (not in the repos)
  rm -v "$pkgdir/usr/share/$pkgname/"*.fs
}
