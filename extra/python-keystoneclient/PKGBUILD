# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-keystoneclient
pkgver=5.6.0
pkgrel=1
pkgdesc="Client Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/python-keystoneclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-debtcollector' 'python-oslo-config' 'python-oslo-i18n'
         'python-oslo-serialization' 'python-oslo-utils' 'python-requests' 'python-stevedore'
         'python-packaging')
makedepends=('git' 'python-setuptools')
checkdepends=('python-fixtures' 'python-keyring' 'python-lxml' 'python-oauthlib'
              'python-os-client-config' 'python-oslotest' 'python-requests-mock'
              'python-stestr' 'python-testresources' 'python-testscenarios' 'python-testtools')
source=("git+https://github.com/openstack/python-keystoneclient.git#tag=$pkgver")
sha512sums=('74ce98eff9ca027a21420f3a12e24115ff24b910e1a15e777b9245791d0446ef4d84c8981b628d4747fa427f9958755fd62fe9ce21cae4f108db18de6737de2d')

build() {
  cd python-keystoneclient
  python setup.py build
}

check() {
  cd python-keystoneclient
  stestr run
}

package() {
  cd python-keystoneclient
  python setup.py install --root="$pkgdir" --optimize=1
}
