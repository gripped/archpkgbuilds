# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=decoder
pkgver=0.7.1
pkgrel=2
pkgdesc='QR code scanner and generator'
arch=(x86_64)
url='https://apps.gnome.org/Decoder/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugin-gtk4
  gst-plugin-pipewire
  gst-plugins-bad
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  zbar
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.gnome.org/World/decoder.git#tag=$pkgver")
b2sums=(0c8e6c8330dc72bbab61cb6409d2583837ffc350fb0033c555451061df8b1e45ac78ded0a517d93b481f7a20235852cf7b4c527037647d755822285d22e91a6d)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/decoder/-/merge_requests/149
  git cherry-pick -n da3d95b1aa64a37dea0764f991eb1cf05cad8568
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
