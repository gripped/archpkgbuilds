# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=decoder
pkgver=0.8.0
pkgrel=1
pkgdesc='QR code scanner and generator'
arch=(x86_64)
url='https://apps.gnome.org/Decoder/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugin-gtk4
  gst-plugin-pipewire
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.gnome.org/World/decoder.git#tag=$pkgver")
b2sums=(2e21d76bf63df1c78df6960353b7be5d8300a18a079ef67296832034e9e26f7b149ba1d7f183beede372e959ff5a5d1e91a08e0232e3267887cb54d7a36c007c)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
