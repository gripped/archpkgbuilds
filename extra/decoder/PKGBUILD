# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=decoder
pkgver=0.7.0
pkgrel=1
pkgdesc='QR code scanner and generator'
arch=(x86_64)
url='https://apps.gnome.org/Decoder/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugin-gtk4
  gst-plugin-pipewire
  gst-plugins-bad
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  zbar
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.gnome.org/World/decoder.git#tag=$pkgver")
b2sums=(45674e4f46caa3567ada857006e1e38256cc1b6f17158753b57ba1aab448951713aa878985dd79a8fd3e800fbbb053030fec5d32e51c3e935fb362ce1b9cecb5)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
