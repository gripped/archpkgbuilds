# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Francesco Minnocci <ascoli.minnocci@gmail.com>

pkgname=python-bleak
pkgver=1.1.0
pkgrel=1
pkgdesc='Bluetooth Low Energy platform Agnostic Klient for Python'
arch=(any)
url=https://github.com/hbldh/bleak
license=(MIT)
depends=(
  python
  python-dbus-fast
  python-service-identity
)
makedepends=(
  bluez-utils
  git
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
_tag=edc9c5125614f816ac7e8edbf2b4338e3142fd3b
source=(git+https://github.com/hbldh/bleak.git#tag=${_tag})
b2sums=('c23dd32d54c8a84502c9dccd5cfe927d1be8e937cc304ee80428a74c82fc11c20af12de7967f1a9a895eede0132b8e16e597058656dad84dca5602b62da8b5fe')

pkgver() {
  cd bleak
  git describe --tags | sed 's/^v//'
}

build() {
  cd bleak
  python -m build --wheel --no-isolation
}

check() {
  cd bleak
  PYTHONPATH="$PWD" pytest -vv
}

package() {
  python -m installer --destdir="${pkgdir}" bleak/dist/*.whl
  install -Dm 644 bleak/LICENSE -t "${pkgdir}"/usr/share/licenses/python-bleak/
}

# vim: ts=2 sw=2 et:
