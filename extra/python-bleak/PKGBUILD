# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Francesco Minnocci <ascoli.minnocci@gmail.com>

pkgname=python-bleak
pkgver=0.22.1
pkgrel=1
pkgdesc='Bluetooth Low Energy platform Agnostic Klient for Python'
arch=(any)
url=https://github.com/hbldh/bleak
license=(MIT)
depends=(
  python
  python-async-timeout
  python-dbus-fast
  python-service-identity
)
makedepends=(
  bluez-utils
  git
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
_tag=e285de5876c5b31bcb265668cc32ac054598211d
source=(git+https://github.com/hbldh/bleak.git#tag=${_tag})
b2sums=('c0cc3cc0f266df6d6dfb492d554f061659e3d3ee80c7d9e70ec80a784fe516ebc6cfba630b1ad90fac6336ee7e16967bb4724c11c4db2a93b49250c21d2ff91d')

pkgver() {
  cd bleak
  git describe --tags | sed 's/^v//'
}

build() {
  cd bleak
  python -m build --wheel --no-isolation
}

check() {
  cd bleak
  PYTHONPATH="$PWD" pytest -vv
}

package() {
  python -m installer --destdir="${pkgdir}" bleak/dist/*.whl
  install -Dm 644 bleak/LICENSE -t "${pkgdir}"/usr/share/licenses/python-bleak/
}

# vim: ts=2 sw=2 et:
