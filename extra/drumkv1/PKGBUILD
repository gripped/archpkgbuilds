# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=drumkv1
pkgname=(
  drumkv1
  drumkv1-lv2
  drumkv1-standalone
)
pkgver=1.3.2
pkgrel=3
pkgdesc="An old-school drum-kit sampler"
arch=(x86_64)
url="https://drumkv1.sourceforge.io/"
license=(GPL-2.0-or-later)
makedepends=(
  alsa-lib
  cmake
  jack
  liblo
  libsndfile
  lv2
  qt6-base
  qt6-svg
  qt6-tools
)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('753aec73246a344bddb10d64d936dccf469beb6cd368903e5054d5c8e2ceb49124955b69393f8b9ac477be38783b1a7ff019bd458a0a8c794b1d3667cb2d0fb9')
b2sums=('0f6e4bbe11dc712b74991556bca1b0fbc461e48a126b403d4196f7afb6589218bd7ba8c9152cde8bb0b9b131ece842a7084b5d23ee0d1d81a22c650ccddf7391')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package_drumkv1() {
  depends=(
    $pkgbase-{lv2,standalone}=$pkgver
  )

  DESTDIR="$pkgdir" cmake --install build

  (
    cd "$pkgdir"

    # have find-libdeps resolve dependencies properly:
    # https://gitlab.archlinux.org/archlinux/devtools/-/issues/102
    find usr/lib -type f -iname "*.so" -exec chmod +x {} \;

    _pick $pkgbase-lv2 usr/lib/lv2
    _pick $pkgbase-standalone usr/{bin,share}
  )
}

package_drumkv1-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    gcc-libs
    glibc
    libsndfile libsndfile.so
    lv2-host
    qt6-base
    qt6-svg
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}

package_drumkv1-standalone() {
  pkgdesc+=" - standalone"
  groups=(
    pro-audio
  )
  depends=(
    alsa-lib
    gcc-libs
    glibc
    hicolor-icon-theme
    jack
    liblo
    libsndfile libsndfile.so
    qt6-base
    qt6-svg
  )
  optdepends=(
    'new-session-manager: for session management using the standalone application'
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
