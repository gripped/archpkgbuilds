# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=drumkv1
pkgname=(
  drumkv1
  drumkv1-lv2
  drumkv1-standalone
)
pkgver=1.1.3
pkgrel=1
pkgdesc="An old-school drum-kit sampler"
arch=(x86_64)
url="https://drumkv1.sourceforge.io/"
license=(GPL-2.0-or-later)
makedepends=(
  alsa-lib
  cmake
  jack
  liblo
  libsndfile
  lv2
  qt6-base
  qt6-svg
  qt6-tools
)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('ff37ccddb07ffb001397e29facbf3151527f19b056ea3ae1fd853edc5f1b6393989f57960ceb42c8dc9eb962905c33987d806b02646625a8caf5b26ce8dc683e')
b2sums=('fa483d3f793aa03dc1e647288cfb8f840fb04329d93edbae678f20a9c8aadc0d809c02a22c9499d7a1d4cae3feaad719e56d023a0cae347b22969381d14acbd9')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package_drumkv1() {
  depends=(
    $pkgbase-{lv2,standalone}=$pkgver
  )

  DESTDIR="$pkgdir" cmake --install build

  (
    cd "$pkgdir"

    # have find-libdeps resolve dependencies properly:
    # https://gitlab.archlinux.org/archlinux/devtools/-/issues/102
    find usr/lib -type f -iname "*.so" -exec chmod +x {} \;

    _pick $pkgbase-lv2 usr/lib/lv2
    _pick $pkgbase-standalone usr/{bin,share}
  )
}

package_drumkv1-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    gcc-libs
    glibc
    libsndfile libsndfile.so
    lv2-host
    qt6-base
    qt6-svg
  )
  optdepends=(
    'qt6-wayland: for native wayland support'
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}

package_drumkv1-standalone() {
  pkgdesc+=" - standalone"
  groups=(
    pro-audio
  )
  depends=(
    alsa-lib
    gcc-libs
    glibc
    hicolor-icon-theme
    jack
    liblo
    libsndfile libsndfile.so
    qt6-base
    qt6-svg
  )
  optdepends=(
    'new-session-manager: for session management using the standalone application'
    'qt6-wayland: for native wayland support'
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
