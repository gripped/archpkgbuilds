# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Gabriel Souza Franco
# Contributor: Florian Pritz
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Mickele
# Contributor: Marcus Fritzsch <fritschy@googlemail.com>

pkgname=coin
pkgver=4.0.6
epoch=1 # downgrade, ref https://gitlab.archlinux.org/archlinux/packaging/packages/freecad/-/issues/23
pkgrel=1
pkgdesc='3D graphics library compatible with Open Inventor'
url='https://github.com/coin3d/coin'
license=(BSD-3-Clause)
arch=(x86_64)
depends=(libgl)
makedepends=(boost cmake doxygen git glu ninja)
optdepends=('fontconfig: dynamic linking support'
            'freetype2: dynamic linking support'
            'openal: audio/dynamic linking support'
            'zlib: dynamic linking support')
source=("git+$url#tag=v$pkgver")
b2sums=('ce6afac17f41880b794b6825e855e6ba826ed3bf5ee91ecb3736e959973b3fdaaee91aaae582d5191807a3ceef39b62166b95dfd66fa2d2627cce716ea5ed74d')

build() {
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -fPIC -w" \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_POLICY_VERSION_MINIMUM=3.10 \
    -D COIN_BUILD_DOCUMENTATION=ON \
    -D COIN_BUILD_DOCUMENTATION_CHM=OFF \
    -D COIN_BUILD_DOCUMENTATION_MAN=ON \
    -D COIN_THREADSAFE=ON \
    -D USE_EXTERNAL_EXPAT=ON \
    -G Ninja \
    -S $pkgname
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  # Skip the HTML documentation
  rm -rf "$pkgdir/usr/share/doc"
  # Remove an empty directory
  rmdir "$pkgdir/usr/share/Coin/profiler"
  # Package the BSD-3 license
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
