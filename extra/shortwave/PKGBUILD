# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=shortwave
pkgver=3.2.0
pkgrel=1
pkgdesc='Internet radio player'
arch=(x86_64)
url='https://apps.gnome.org/Shortwave/'
license=(GPL-3.0-or-later)
depends=(
  dbus
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-bad
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libshumate
  openssl
  sqlite
  zlib
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.gnome.org/World/Shortwave.git#tag=$pkgver")
b2sums=(466378d1d25d1bfcb3b8c894ca8d500df4681b47bc1f639a63eff2f3a81089993a8197227b78a2372586b397cd510d4f738395ff832c50a3787c5ce6d323534a)

prepare() {
  cd Shortwave

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Shortwave build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
