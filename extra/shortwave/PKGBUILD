# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=shortwave
pkgver=4.0.1
pkgrel=1
pkgdesc='Internet radio player'
arch=(x86_64)
url='https://apps.gnome.org/Shortwave/'
license=(GPL-3.0-or-later)
depends=(
  bubblewrap
  dconf
  fontconfig
  gcc-libs
  glib2
  glibc
  glycin
  graphene
  gst-plugins-bad
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libseccomp
  libshumate
  openssl
  pango
  sqlite
  zlib
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.gnome.org/World/Shortwave.git#tag=$pkgver")
b2sums=(b9f9b04dbabd5c0f5c8618a5aabd96203ba8536e2ec5d484be5163d1c38247d32938d5e90329517c2b4c57fae29e7be75cd95a998d7cc953dadee606a1530318)

prepare() {
  cd Shortwave

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Shortwave build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
