# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=shortwave
pkgver=5.0.0
pkgrel=3
pkgdesc='Internet radio player'
arch=(x86_64)
url='https://apps.gnome.org/Shortwave/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  bubblewrap
  dconf
  fontconfig
  gcc-libs
  glib2
  glibc
  glycin1
  graphene
  gst-plugins-bad
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libseccomp
  libshumate
  openssl
  pango
  sqlite
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.gnome.org/World/Shortwave.git#tag=$pkgver")
b2sums=(d0019aaf3069c44cf8ceb92e2d66d94de586a26c7a623c09f5fccce358d0dfbb62c3a6e557e4ff911c00a15ff12e915b4d4bff89a1722dfe89fd21ab2dbf97a1)

prepare() {
  cd Shortwave

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Shortwave build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
