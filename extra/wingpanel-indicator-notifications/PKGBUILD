# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-notifications
pkgver=7.1.1
pkgrel=1
pkgdesc='Notifications indicator for Wingpanel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-notifications
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  dconf
  glib2
  glibc
  gdk-pixbuf2
  gtk3
  libgee
  libgranite.so
  libhandy-1.so
  libwingpanel.so
)
makedepends=(
  git
  meson
  vala
)
_tag=564089f9b7e9a8a664e3d1fe3a87e7e21786c480
source=(git+https://github.com/elementary/wingpanel-indicator-notifications.git#tag=${_tag})
b2sums=('18d43468a8a32ebae9aa3f09b905e3252d5a14e10fa1acad279a4f9dca67808e538f5eb2110d74e661585c5365f4fe6054fad50d980c9c02669342709d140727')

pkgver() {
  cd wingpanel-indicator-notifications
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
