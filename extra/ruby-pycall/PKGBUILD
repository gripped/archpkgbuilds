# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname=ruby-pycall
pkgver=1.5.2
pkgrel=6
pkgdesc='Calling Python functions from the Ruby language'
arch=(x86_64)
url='https://github.com/mrkn/pycall.rb'
license=(MIT)
depends=(
  ruby
  python
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  python-numpy
  ruby-bundler
  ruby-pry
  ruby-pry-byebug
  ruby-rake
  ruby-rake-compiler
  ruby-rspec
  ruby-test-unit
)
options=(!emptydirs)
source=(
  "git+${url}#tag=v${pkgver}"
  "${pkgname}_fix_tests.patch::${url}/pull/191.patch"
)
sha512sums=('84396383428c2a70983436598f793fd6ec9a5f2f116b5ebd32bc7969660133bbc3018705bf1ef69d44dd02ba28d7c996cc3d308edb882e9c0759de5dc98629dc'
            '882eb2b0c37065c93e18937578800ddf5ed56ef9c734ed40cfb4f428d3c0c1daef1482543cff8a8ac9590a00853ff634887c402932ee150501fedbbb38b1db5c')
b2sums=('823bae125b6ab9d4b0b47b7a4be0bf7cde0fe8f4741bfba32e3c2acebe09707de782712c3c96e1251e1e8eefefbd127a675605a5db514d8dc1e33c52a7917e3e'
        '677600734f87a83905949e93df866930084baad556a967608a8bc6d7265083105a88627610cb65dd6bd15ba34679a48c14752e1b8e44e8b423a829feef2e098c')

prepare() {
  cd pycall.rb

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"

  sed -r -e '/rake-compiler-dock/d' -e '/launchy/d' -i pycall.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd pycall.rb
  gem build --verbose pycall.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pycall-$pkgver.gem

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd pycall.rb
  RSPEC_SEED=6894 GEM_HOME="tmp_install/$_gemdir" rake spec
}

package() {
  cd pycall.rb
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
