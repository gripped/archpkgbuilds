# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-eval-plugin
pkgname=haskell-hls-eval-plugin
pkgver=2.1.0.0
pkgrel=45
pkgdesc="Eval plugin for Haskell Language Server"
url="https://github.com/haskell/haskell-language-server"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-quickcheck' 'haskell-aeson' 'haskell-data-default'
         'haskell-dlist' 'haskell-extra' 'haskell-ghc' 'haskell-ghc-paths' 'haskell-ghcide'
         'haskell-hashable' 'haskell-hls-graph' 'haskell-hls-plugin-api' 'haskell-lens'
         'haskell-lsp' 'haskell-lsp-types' 'haskell-megaparsec' 'haskell-parser-combinators'
         'haskell-pretty-simple' 'haskell-safe-exceptions' 'haskell-unliftio'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils' 'haskell-row-types')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('2075d8b4059b4ddd53a8370870655f5d7a1477a1f44380109cd104c90ea6456e51a3e0fd75574fa71adad2d15c4be2e02a09864d28e2ab18095cd7654583e22a')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
