# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=libmirage
pkgver=3.3.1
pkgrel=1
pkgdesc="CD-ROM image (B6T/C2D/CCD/CDI/CIF/CUE/ISO/MDS/MDX/NRG/TOC) access library"
url="https://cdemu.sourceforge.io/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  bzip2
  glib2
  glibc
  libgcrypt
  libsamplerate
  libsndfile
  xz
  zlib
)
makedepends=(
  cmake
  git
  gobject-introspection
  gtk-doc
  intltool
  ninja
  vala
)
source=("cdemu-code::git+https://git.code.sf.net/p/cdemu/code#tag=$pkgname-$pkgver")
b2sums=('8b052e937120cca196bab2fda901090a3fc1ec266ddb239bc9b12b1137e7e47fc3077543ff5b94c091be66b24ee1a02209295c06c7def4c58503b4a7b7b603f2')

prepare() {
  cd cdemu-code/$pkgname
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_LIBDIR=lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D POST_INSTALL_HOOKS=OFF
  )

  cmake -S cdemu-code/$pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
