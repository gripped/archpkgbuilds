# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami-addons
pkgver=1.12.0
pkgrel=1
pkgdesc='Add-ons for the Kirigami framework'
url='https://invent.kde.org/libraries/kirigami-addons'
arch=(x86_64)
license=(GPL-2.0-or-later
         LGPL-2.1-or-later)
depends=(glibc
         kcolorscheme
         kconfig
         kcoreaddons
         kcrash
         kglobalaccel
         kguiaddons
         ki18n
         kiconthemes
         kirigami
         kitemmodels
         ksvg
         libstdc++
         qt6-base
         qt6-declarative
         qt6-multimedia
         sonnet)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('513051dff8417da1819d6ae89d6c21a03654c9a60891df60df6aba13df19d21b'
            'SKIP')
validpgpkeys=(0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D  # Bhushan Shah (mykolab address) <bshah@mykolab.com>
              39FFA93CAE9C6AFC212AD00202325448204E452A) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
