# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=junction
pkgver=1.10
pkgrel=1
pkgdesc='Application chooser to open files and links'
arch=(any)
url='https://apps.gnome.org/Junction/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  dconf
  gjs
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libportal
  libportal-gtk4
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/sonnyp/Junction.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
)
b2sums=(
  96c7d8a44d7c11815cc683c967d80943a48637071839f0f045392c927b5e0887b0684e19f646c68989c30e043bb578eea5496ac948997b1709b15763b3619da2
  SKIP
)

prepare() {
  cd Junction

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson Junction build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  ln -s re.sonny.Junction "$pkgdir/usr/bin/junction"
}
