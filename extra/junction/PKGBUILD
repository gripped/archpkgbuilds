# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=junction
pkgver=1.8
pkgrel=3
pkgdesc='Application chooser to open files and links'
arch=(any)
url='https://apps.gnome.org/Junction/'
license=(GPL-3.0-only)
depends=(
  dconf
  gjs
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libportal
  libportal-gtk4
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/sonnyp/Junction.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
)
b2sums=(
  3112b4ef690933fc57260554fe3d406aab56fdbe9f46f5be5affaebce71cc7a5d3eebb6ba6badf81f23680c1579e6ccd054a03a0f4b2fbaca93941ddd24bcc57
  SKIP
)

prepare() {
  cd Junction

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson Junction build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s re.sonny.Junction "$pkgdir/usr/bin/junction"
}
