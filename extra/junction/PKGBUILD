# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=junction
pkgver=1.9
pkgrel=1
pkgdesc='Application chooser to open files and links'
arch=(any)
url='https://apps.gnome.org/Junction/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  dconf
  gjs
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  libportal
  libportal-gtk4
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://github.com/sonnyp/Junction.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
  junction-fix-metainfo.patch
  junction-single-welcome-window.patch
)
b2sums=(
  bc5fa852ef949762ed45f22b5054c26b9911f5d04213e415e84a7fcba7055fea18a12eafc0b28618001ee84976bc5fda089fdd492624f1bca228fbc97b68739d
  SKIP
  3d3c81a419a6564ea4cef6b0004ccd5dad9b4bdc6eefd2f7270fa72589e9d02d17894b4e8bdad77460bae092b23f76d7b781e01fc1b6cdecd79f5bef8dab13d3
  dad057cf4155e08fb109f84cb38e6804b4e67f654d84d04b9bba7af6ddd79fa42a01eb68184bb6e674f0633b95a9bd76ebb863236650cefb549cc5eb0730f556
)

prepare() {
  cd Junction

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update

  # https://github.com/sonnyp/Junction/pull/185
  git apply -3 ../junction-fix-metainfo.patch

  # https://github.com/sonnyp/Junction/pull/186
  git apply -3 ../junction-single-welcome-window.patch
}

build() {
  arch-meson Junction build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  ln -s re.sonny.Junction "$pkgdir/usr/bin/junction"
}
