# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-geometry
pkgver=1.34.16
pkgrel=1
pkgdesc='Python library housing basic geometry objects and computation methods'
arch=(any)
url='https://github.com/ladybug-tools/ladybug-geometry'
license=(AGPL-3.0-only)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('eacbefe7295cba4da7b86fa4213bce153c31f72ab34bded91452b1af30a4875e805d718cbd0c1decda0acfc9269b26770a6830b5eac58795a7f713e5b252d8a4')
b2sums=('f8786f14eb5b46060365c88ec7f00cc4d57c1b553f7e748b6acd9913f309a21a5706ba98e3709d063ce2e7abfa530c7238b59b968d091664a4af3212f0d58395')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
