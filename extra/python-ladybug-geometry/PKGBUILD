# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-geometry
pkgver=1.34.17
pkgrel=1
pkgdesc='Python library housing basic geometry objects and computation methods'
arch=(any)
url='https://github.com/ladybug-tools/ladybug-geometry'
license=(AGPL-3.0-only)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('8d1e65b9c53c9df17b83a61cfe00ede59215203f48f58f4cb78a54b869b5b3c1dddf1eefcf0dc72610a2e86172d487d0d832d2810fe5e6653038fd1a43132dfb')
b2sums=('fa143943d896ae2b9e273ad0af5619543dfc54a99a61ef60de45bf052d01e7bbf7e415e4ae1a07549f7db585af1f58689d50146261111da2ac8f81919ac216cc')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
