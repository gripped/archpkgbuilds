# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-geometry
pkgver=1.34.10
pkgrel=1
pkgdesc='Python library housing basic geometry objects and computation methods'
arch=(any)
url='https://github.com/ladybug-tools/ladybug-geometry'
license=(AGPL-3.0-only)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('1743fa8692fa8c131e0cd0b33300bd0dd6532ff77f72070ff3f71d3c0cf31fc9d29c1ea1f3dbe49188eeb162d74dd24132d80cf1db888e21c66183f3c71dcef0')
b2sums=('5d47bf20385acd528eafe1588c801411fa310f7ad13d89f5209a9098db248695715311e8a9c9f523c53a2b061fdaef82b9f12332d15d3db37bab7d8abc2d3bad')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
