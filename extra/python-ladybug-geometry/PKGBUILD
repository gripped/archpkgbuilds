# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-geometry
pkgver=1.34.14
pkgrel=2
pkgdesc='Python library housing basic geometry objects and computation methods'
arch=(any)
url='https://github.com/ladybug-tools/ladybug-geometry'
license=(AGPL-3.0-only)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('7464c680179ec274a92661031fa112a4b90155e0736d3d6221aba42f273e9f889576dca6d05a8232f4cbc2f0201abbdbc9bf83495416c011f4bb0d0030fe0595')
b2sums=('a99b217efe32e5d14f22e8be81fc7eb769912d59bf3c727022d90a390ed7c2b82b004af5a537041acdc7cba50859d8bab9fb436394af0abae2b449a97fc7ec10')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
