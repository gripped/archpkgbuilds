# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-ladybug-geometry
pkgver=1.34.13
pkgrel=1
pkgdesc='Python library housing basic geometry objects and computation methods'
arch=(any)
url='https://github.com/ladybug-tools/ladybug-geometry'
license=(AGPL-3.0-only)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0808719c324655ea260c5ec0f03744986252c0e30d385869e8856a47e87e1625bc8c524acfa4685e7c62cf119f961a47122e4934de8e810d15360c0ad7db4806')
b2sums=('4a2ed851825641a3c7355c0473079f7824c75423e4d55b71fd9caac4b2af9d488ddd1abbce5979b23df10634631ef54162a040bc20a1a33757fdf28b9c3ee56e')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
