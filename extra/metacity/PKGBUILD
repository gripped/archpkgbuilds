# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.56.0
pkgrel=1
pkgdesc='Window manager of GNOME Flashback'
arch=(x86_64)
url='https://wiki.gnome.org/Projects/Metacity'
license=(GPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gsettings-desktop-schemas
  gtk3
  libcanberra
  libgtop
  libice
  libsm
  libx11
  libxcomposite
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxinerama
  libxpresent
  libxrandr
  libxrender
  libxres
  pango
  startup-notification
  zenity
)
makedepends=(
  autoconf-archive
  git
  glib2-devel
)
source=(
  "git+https://gitlab.gnome.org/GNOME/$pkgname.git?signed#tag=$pkgver"
  metacity-38.patch
)
b2sums=(
  c666ccdff6d4b027157d0065a2335376185bed8a2d2e2ad61f3c722540ab864fb0307198463aadc9540f6afd634f7e1f3a640562ffc77d53ea6f30cce4627bf6
  aa75a7b379cc0880a31f4863d6666287da5ff35e9494a7c467fb50f6d649f50b6f256d9c2cc40fb43ea4d28f9b0364e981e0114715f8fd2bc1c42b206cde5a4a
)
validpgpkeys=(7B44FD78E49334EC10B3B288A3D013EC303E1894) # Alberts Muktupāvels <alberts.muktupavels@gmail.com>

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/GNOME/metacity/-/merge_requests/38
  git apply -3 ../metacity-38.patch

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug \
    --enable-compile-warnings \
    --disable-vulkan
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
