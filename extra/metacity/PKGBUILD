# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.54.0
pkgrel=1
pkgdesc='Window manager of GNOME Flashback'
arch=(x86_64)
url='https://wiki.gnome.org/Projects/Metacity'
license=(GPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gsettings-desktop-schemas
  gtk3
  libcanberra
  libgtop
  libice
  libsm
  libx11
  libxcomposite
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxinerama
  libxpresent
  libxrandr
  libxrender
  libxres
  pango
  startup-notification
  zenity
)
makedepends=(
  autoconf-archive
  git
  glib2-devel
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git?signed#tag=$pkgver")
b2sums=(dd13996f87d09c654995c48dc19c80651f191197def708eec14fd97daac2ef7006f1d9c0cc78991abe25d79aaae0799d3c064496289aff12c3010fc99bb7654c)
validpgpkeys=(7B44FD78E49334EC10B3B288A3D013EC303E1894) # Alberts Muktupāvels <alberts.muktupavels@gmail.com>

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug \
    --enable-compile-warnings \
    --disable-vulkan
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
