# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=waylock
pkgver=1.4.0
pkgrel=2
pkgdesc="A simple screenlocker for wayland compositors"
arch=(x86_64)
url="https://codeberg.org/ifreund/waylock"
license=(ISC)
depends=(
  glibc
  libxkbcommon
  pam
  wayland
)
makedepends=(
  git
  scdoc
  wayland-protocols
  zig
)
_zig_deps=(
  # https://codeberg.org/ifreund/waylock/src/tag/v${pkgver}/build.zig.zon
  zig-wayland-v0.3.0.tar.gz::https://codeberg.org/ifreund/zig-wayland/archive/v0.3.0.tar.gz
  zig-xkbcommon-v0.3.0.tar.gz::https://codeberg.org/ifreund/zig-xkbcommon/archive/v0.3.0.tar.gz
)
source=(
  "git+$url?signed#tag=v$pkgver"

  "${_zig_deps[@]}"
)
noextract=("${_zig_deps[@]}")
sha512sums=('e8894a446338f579018fd411f115272aae455172820afeaaf3c187622bc55e0fd485f865fc789198e5b1090a08024b9dc5432b0c361f5819a4fc62daef7fa683'
            '436c7104c41bb3c26bedc3491d887293fa0a40c11a14bc0973a1fa873c5161a05c9681a857f6cf89c9b34106e2b9e92b4f67eebabaf16836cb99a459ad11d20e'
            'fe0dc46a8556f19c2ce1badbeee7576065cb56495b94b6d468ee6ed8667f9fef5377e26864b0c8e81c3a2718cfcaf8a4f5e13aab23a615f3a58ec639372fb758')
b2sums=('40bbb8624e1d19a7b9797b5f1459f7ad2c81a1be3c417d4d11fd45c6e60e87b776e72e9a29081e577c67d58fd227534d56f3fde680ebcdefadbefd8ea0637290'
        '8dda2bae62c65068825068dbe91e701ad4a71685d2209bb703d0e8b71b75f1546690b83fc66e9b81de59e26c9e5a9d8d5cfa3f2098cd16a3f13ada7f38fb94ce'
        '6870fc2465e2b1ef4faa6330183a1dade4a4bcf36130113444e5221ff9c764db488304f2f25a603f2511c2562d55bf1011a5f8fe41a068e7e5b707e1d06c0792')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  for dep in "${_zig_deps[@]}"; do
    zig fetch --global-cache-dir ./zig-cache "${dep%%::*}"
  done
}

build() {
  local zig_options=(
    -Dcpu=baseline
    -Dpie
    -Dtarget=native-linux.6.1-gnu
    --prefix /usr
    --search-prefix /usr
    --system ../zig-cache/p
    --verbose
  )

  cd $pkgname
  DESTDIR=build zig build "${zig_options[@]}"
}

package() {
  depends+=(
    libxkbcommon.so
    libpam.so
    libwayland-client.so
  )

  cd $pkgname

  mv -v build/* "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
