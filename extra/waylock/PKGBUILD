# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=waylock
pkgver=1.3.0
pkgrel=1
pkgdesc="A simple screenlocker for wayland compositors"
arch=(x86_64)
url="https://codeberg.org/ifreund/waylock"
license=(ISC)
depends=(
  glibc
  libxkbcommon
  pam
  wayland
)
makedepends=(
  git
  scdoc
  wayland-protocols
  zig
)
_zig_deps=(
  # https://codeberg.org/ifreund/waylock/src/branch/master/build.zig.zon
  zig-wayland-v0.2.0.tar.gz::https://codeberg.org/ifreund/zig-wayland/archive/v0.2.0.tar.gz
  zig-xkbcommon-v0.2.0.tar.gz::https://codeberg.org/ifreund/zig-xkbcommon/archive/v0.2.0.tar.gz
)
source=(
  "git+$url?signed#tag=v$pkgver"

  "${_zig_deps[@]}"
)
noextract=("${_zig_deps[@]}")
sha512sums=('fc7e0e20f47b3444980e25d6b8310f0bff78fc05b47d9cae8008dd68f81f6c6d15784b49dda4576266061e829708a84c2cdfc0ba3852b79bfc1c5d4d28993559'
            'dfb140c06d9e3c566034f9102426e7bd47dcde87a2ae16d43ce3ea72cf44bea872bab1d9a23a24ae9d57d2d4ae6c56060a6b10f869de3990046426b0462db85e'
            'a99b6a2fefe34507fd687b58a9618a27ca617966798b410a72c9e53462c3f9c6dd0476a22bb9686cbf49994ca502743b4d25fdc55125359d4fe2860094bf0e82')
b2sums=('7c0678f22f2b8d7d8637d229d4e886f18be3c1252153979bf926b6587b60da67d9d9da87525627a011a620a8474a0bce0edbc1c6666b878f25745007f3a2e152'
        'aaf82efe78d25c59a87de050147857fcf9a19764141cb40d92b0dd48b96ea0ec52d00a1ab0df1afe4c45cda6d64dda97f296f0aa29d8b9ea3da49e1d5cd69e50'
        '85045d002b4ec1ac8dadda67d3ce6f588e53938ac0a67c06d6cebc85a944d7a23fd1cc295410ce1a91cce9ffb587834f7eca2400e1468fec0193583ac71d6f86')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  for dep in "${_zig_deps[@]}"; do
    zig fetch --global-cache-dir ./zig-cache "${dep%%::*}"
  done
}

build() {
  local zig_options=(
    -Dcpu=baseline
    -Dpie
    -Dtarget=native-linux.6.1-gnu
    --prefix /usr
    --search-prefix /usr
    --system ../zig-cache/p
    --verbose
  )

  cd $pkgname
  DESTDIR=build zig build "${zig_options[@]}"
}

package() {
  depends+=(
    libxkbcommon.so
    libpam.so
    libwayland-client.so
  )

  cd $pkgname

  mv -v build/* "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
