# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=waylock
pkgver=1.1.0
pkgrel=1
pkgdesc="A simple screenlocker for wayland compositors"
arch=(x86_64)
url="https://codeberg.org/ifreund/waylock"
license=(ISC)
depends=(
  glibc
)
makedepends=(
  git
  libxkbcommon
  pam
  scdoc
  wayland
  wayland-protocols
  zig
)
source=(
  "git+$url?signed#tag=v$pkgver"
)
sha512sums=('c9f925af5a0a95e091260628c0c854fe191083be05e4ab46419d06b00f7d36668fdc58c2e3c6ce3be3ba5a9d9ef024a4423daa1f67a042bc2e82760ca6fb5ebb')
b2sums=('5a2dfd9a3ebfe17fbb0b7204821d2892a9f83e288f73b6ecc30ec18872497ff7821e1be6d5e191ee8e6e40f6fd80e9d742d659bdbcb9ea60a5cf8f645b7d7cf6')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  zig fetch --global-cache-dir zig-cache https://codeberg.org/ifreund/zig-wayland/archive/v0.1.0.tar.gz
  zig fetch --global-cache-dir zig-cache https://codeberg.org/ifreund/zig-xkbcommon/archive/v0.1.0.tar.gz
}

build() {
  local zig_options=(
    -Dcpu=baseline
    -Dpie
    -Dtarget=native-linux.5.15-gnu
    --prefix /usr
    --search-prefix /usr
    --system ../zig-cache/p
    --verbose
  )

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  cd $pkgname
  DESTDIR=build zig build "${zig_options[@]}"
}

package() {
  depends+=(
    libxkbcommon libxkbcommon.so
    pam libpam.so
    wayland libwayland-client.so
  )

  cd $pkgname

  mv -v build/* "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
