# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-alerting-plugin'
_pluginname='alerting-dashboards-plugin'
pkgver=2.16.0.0
_dashboardsver=2.16.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Alerting Plugin'
url='https://opensearch.org/docs/latest/monitoring-plugins/alerting'
arch=('x86_64')
license=('Apache-2.0')
depends=("opensearch-dashboards=${_dashboardsver}" "opensearch-dashboards-notifications-plugin" 'coffeescript')
makedepends=('yarn' 'npm' 'python' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/${_pluginname}.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
  fix-ut.patch
)
sha256sums=('713ee788a85a37468e63fe6445b04f7628d11a2b3dd1d5cd67451055c8335727'
            '461b77cc2acae8df36213d7b27b4719c28486ef96f8451ce1666c4a7dbe56103'
            '253048e791348374d794e2c687846b8b111c34fc1d06c08b4ab1f96bc0020a59')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" \
    "${_pluginname}/package.json" \
    "OpenSearch-Dashboards/package.json"
  
  cd "${srcdir}/${_pluginname}"
  patch -Np1 -i "$srcdir"/fix-ut.patch # https://github.com/opensearch-project/alerting-dashboards-plugin/pull/1041
}

build() {
  mv "${_pluginname}" "OpenSearch-Dashboards/plugins"
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn osd bootstrap
  yarn plugin-helpers build --skip-archive # `yarn build` will always create the ZIP
}

check() {
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn test:jest -u
}

package() {
  cd "${srcdir}/OpenSearch-Dashboards/plugins/${_pluginname}"

  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
