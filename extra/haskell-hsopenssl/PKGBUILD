# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=HsOpenSSL
pkgname=haskell-hsopenssl
pkgver=0.11.7.8
pkgrel=3
pkgdesc="Partial OpenSSL binding for Haskell"
url="https://github.com/haskell-cryptography/HsOpenSSL"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-network' 'openssl')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('f466165a1ce72f51bcf68d5e8ad42e2d891b65fa748a98ecba5152e6cdc0b10afa000c8d0f33054bfb724c0fef285603ee7e2c72db6fe8ff659f4346343a54f8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        --ghc-option="-optc=-Wno-incompatible-pointer-types" \
        -fuse-pkg-config
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
