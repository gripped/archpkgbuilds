# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=conduit
pkgname=haskell-conduit
pkgver=1.3.6
pkgrel=13
pkgdesc="Streaming data processing library."
url="https://github.com/snoyberg/conduit"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-mono-traversable' 'haskell-primitive'
         'haskell-resourcet' 'haskell-unliftio-core' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck' 'haskell-safe' 'haskell-silently'
             'haskell-split' 'haskell-unliftio')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f02839275539dfbeccd0ffedcb8b9671689f7d5d0f64ded80b6c518e87b462901a17f3b8e228d53b14585f5f9c8087ca61758ad65c72a2a574e8d184ed6ee06a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
