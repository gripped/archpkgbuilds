# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxdumptool
pkgver=7.1.2
pkgrel=1
pkgdesc='Small tool to capture packets from wlan devices'
url='https://github.com/ZerBea/hcxdumptool'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'libpcap')
source=(https://github.com/ZerBea/hcxdumptool/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/ZerBea/hcxdumptool/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('073f2cc77e107ea910d569039af3349c5c1f65766b92a2e08b7f7ce26536fcd25257f80879005188b20338710e9bf8ae636525a9a4e79a4d7d2597aabb9cf236'
            'SKIP')
b2sums=('766364300b4dff94761bb7306df1fc767dd8a7247b160ff16326ec1c29cd6a2849da5edb5678172d5eed0b97abc41a3bdaa975d6a75e533371b6994401274715'
        'SKIP')
validpgpkeys=('5920CE1C567948AFD2C0A9B7375516A45DB88630') # ZeroBeat <ZeroBeat@gmx.de>

#prepare() {
#  sed -e 's|pcap/pcap.h|pcap.h|' -i $pkgname-$pkgver/hcxdumptool.c
#}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" $LDFLAGS" \
  make PRODUCTION=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr PRODUCTION=1 install
  install -Dm 644 README.md changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
