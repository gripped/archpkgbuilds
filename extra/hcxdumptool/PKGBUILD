# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxdumptool
pkgver=6.3.5
pkgrel=1
pkgdesc='Small tool to capture packets from wlan devices'
url='https://github.com/ZerBea/hcxdumptool'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'libpcap')
source=(https://github.com/ZerBea/hcxdumptool/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/ZerBea/hcxdumptool/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('372dcf4afad7608649b78857407d896bc8f0d050c1a7b0c75101bebc10a08cabf5257fce467ea3b99c06912d969ebc68d3b8699c93c7e26da7962f72947112fc'
            'SKIP')
b2sums=('c87806e6cd638d3c63fd4e6d10698677fca80c023adcba567b8bb9907354588b1386dd3cae633fd3545eaf14c7ec0b54daf292f5f5289571ab90d8b646aa6cbb'
        'SKIP')
validpgpkeys=('5920CE1C567948AFD2C0A9B7375516A45DB88630') # ZeroBeat <ZeroBeat@gmx.de>

#prepare() {
#  sed -e 's|pcap/pcap.h|pcap.h|' -i $pkgname-$pkgver/hcxdumptool.c
#}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" $LDFLAGS" \
  make PRODUCTION=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr PRODUCTION=1 install
  install -Dm 644 README.md changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
