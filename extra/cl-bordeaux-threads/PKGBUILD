# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Dacoda Strack <dacoda.strack@gmail.com>

pkgname=cl-bordeaux-threads
_pkgname="${pkgname#cl-}"
pkgver=0.9.4
pkgrel=1
pkgdesc='Portable shared-state concurrency for Common Lisp'
arch=(any)
url='https://sionescu.github.io/bordeaux-threads/'
license=(MIT)
depends=(
  common-lisp
  cl-asdf
  cl-alexandria
  cl-global-vars
  cl-trivial-features
  cl-trivial-garbage
)
makedepends=(git)
checkdepends=(sbcl cl-fiveam)
source=(
  "$pkgname::git+https://github.com/sionescu/bordeaux-threads#tag=v$pkgver"
  run-tests.lisp
)
sha512sums=('bb6f52ca611ac663df00396788431e80758134e39f5bc2c101b5a80a1bc59684f05918e356730e9a15b35486e69f0bf555312b00110d85cf08f43bcbe13a4754'
            '71408f43e0c72616d26e696c6de7c57a72edb46cf63358f3f5856f220e4e8e13fcb6bc56e28ed759940f42dd4a2835cc2831e7a15906f758c5426a19b39e0eb4')
b2sums=('d57d2639583812e22f4a7e33e6b60661e309a18efbd5f54e070bc88c6d9bd24081a452168970457e974fbe5042545b81531033c17730438a7f1abc823f7b4285'
        '91273084a1ebd68bdafc7ad4904d232f07046de74684575813b25eaa145eb8b7d1c6e0a2f0a114376cbe6cc30bd59f88cffdea007d0f71d36c29288fa70f733f')

check() {
  cd "$pkgname"

  sbcl --script ../run-tests.lisp
}

package() {
  cd "$pkgname"

  # library
  install -vd "$pkgdir/usr/share/common-lisp/source/$_pkgname"
  cp -vr apiv{1,2} test version.sexp "$_pkgname.asd" "$pkgdir/usr/share/common-lisp/source/$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md site/*

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
