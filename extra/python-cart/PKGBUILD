# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=python-cart
_name="${pkgname#python-}"
pkgver=1.2.2
pkgrel=2
pkgdesc="Python implementation of the CaRT library for (un)inerting files"
url="https://github.com/CybercentreCanada/cart"
arch=('any')
license=('MIT')
depends=('python' 'python-pycryptodome')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('38a71caa4e78a754ef24f47a75048bc5d22d64aa886edd38a3931834838f1386ec2d977c4cb456912ffd879dbe99f649d40f35a30acdd8165d49fbcff700bf08')
b2sums=('cd3aa384532e026589063453c1db94eb9e2fab072935d27c261b35641dfde0a894c17cfb2728b8713564c03f58ebc60b85b3cf086c4f57f474c9075e8a2de510')

build() {
	cd "${_name}-${pkgver}"
	python -m build --wheel --no-isolation
}

check() {
	cd "${_name}-${pkgver}"
	pytest
}

package() {
	cd "${_name}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
