# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=python-cart
_name="${pkgname#python-}"
pkgver=1.2.3
pkgrel=2
pkgdesc="Python implementation of the CaRT library for (un)inerting files"
url="https://github.com/CybercentreCanada/cart"
arch=('any')
license=('MIT')
depends=('python' 'python-pycryptodome')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('bdbafb82bcf5401a39153f29d1aa766d71c76bdb83c76316ec6c808b49c5cb767e37e04afbd7e0d94698d76b577b293d482f03b7651dc7c95ceb2e1a32fb60ed')
b2sums=('3ceedcd618882e4e1d04fba6f737d9ca30804b15d646e198910c2661722aefebdbf2b8624b8bbe79bc5251c7d0003fb98b44426a1301604b8093bfd8290de1a0')

build() {
	cd "${_name}-${pkgver}"
	python -m build --wheel --no-isolation
}

check() {
	cd "${_name}-${pkgver}"
	pytest
}

package() {
	cd "${_name}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
