# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=gersemi
pkgname=python-$_name
pkgver=0.26.0
pkgrel=1
pkgdesc="A formatter to make your CMake code the real treasure"
arch=(any)
url="https://github.com/blankspruce/gersemi"
license=(MPL-2.0)
depends=(
  python
  python-colorama
  python-ignore
  python-lark-parser
  python-platformdirs
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
)
checkdepends=(
  git
  python-pytest
  python-pytest-xdist
  python-pydantic
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
b2sums=('7c018a0d6e9f50d45437bd662f75146248753e4adf556d1507cdf0d5c7f20fb2ecb4a20a1be05a9964901dc6b9e887389ffd5c5eec9b5b83b2f1db9906654320')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    # distribute tests across multiple CPUs
    -n auto
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # the tests use Python subprocess module to execute gersemi, so we must actually activate the venv
  (
    source test-env/bin/activate
    python -m pytest "${pytest_options[@]}" tests
  )
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
