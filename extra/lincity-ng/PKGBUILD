# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=lincity-ng
pkgver=2.14.2
pkgrel=1
pkgdesc="A city simulation game"
arch=('x86_64')
url="https://github.com/lincity-ng/lincity-ng"
license=(
  'CC-BY-SA-2.0'
  'GPL-2.0-or-later'
  'LicenseRef-ArevFontsCopyright'
  'LicenseRef-BitstreamVeraFontsCopyright'
)
depends=(
  'fmt'
  'gcc-libs'
  'glibc'
  'hicolor-icon-theme'
  'libxml++-5.0'
  'libxml2'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
  'sdl2_ttf'
  'zlib'
)
makedepends=(
  'cmake'
  'libxslt'
)
source=("$url/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('cbc2319c47db4dce468a2ed545e7b321330c10e5ca6333d81eb749289a2b59ca')

build() {
  cd ${pkgname}-${pkgname}-${pkgver}
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver}
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING-data.txt
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING-fonts.txt
}
