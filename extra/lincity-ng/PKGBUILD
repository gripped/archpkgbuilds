# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=lincity-ng
pkgver=2.13.1
pkgrel=2
pkgdesc="A city simulation game"
arch=('x86_64')
url="https://github.com/lincity-ng/lincity-ng"
license=(
  'CC-BY-SA-2.0'
  'GPL-2.0-or-later'
  'LicenseRef-ArevFontsCopyright'
  'LicenseRef-BitstreamVeraFontsCopyright'
)
depends=(
  'gcc-libs'
  'glibc'
  'hicolor-icon-theme'
  'libglvnd'
  'libxml++-5.0'
  'libxml2'
  'physfs'
  'sdl2'
  'sdl2_gfx'
  'sdl2_image'
  'sdl2_mixer'
  'sdl2_ttf'
  'zlib'
)
makedepends=(
  'cmake'
  'libxslt'
)
source=("$url/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('7057f287680d597473e7ef6f74655c620aea4cf13b210cbae956ccf974f844d3')

build() {
  cd ${pkgname}-${pkgname}-${pkgver}
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver}
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING-data.txt
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING-fonts.txt
}
