# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>

pkgname=gbrainy
pkgver=2.4.6
pkgrel=4
epoch=1
pkgdesc='Brain teaser game and trainer to have fun and to keep your brain trained'
arch=(any)
url='https://wiki.gnome.org/Apps/gbrainy'
license=(GPL-2.0-or-later)
depends=(
  bash
  gtk-sharp-3
  hicolor-icon-theme
  mono
)
makedepends=(
  git
  intltool
  yelp-tools
)
source=("git+https://gitlab.gnome.org/Archive/gbrainy.git#tag=$pkgver")
b2sums=(3d7d671f50caeb179b5c1de72331b851f019280d38b512ac46d374b8091b374b8df13933edfdb25fca6a36aa19d847e7803ee6e30db418c28b0b291e82807e28)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
