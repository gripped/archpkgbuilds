# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-reports-scheduler-plugin
pkgver=3.5.0.0
pkgrel=1
_opensearchver=3.5.0
_jdkver=21
pkgdesc="OpenSearch Reports Scheduler Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/dashboards/reporting"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/reporting/archive/${pkgver}.tar.gz"
)
sha256sums=('1b3082fb6133ed460cec9610bf701d13c434b3fcbc04cd741b589b81b1006212')

build() {
  cd "reporting-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  unzip "${srcdir}/reporting-${pkgver}/build/distributions/opensearch-reports-scheduler-${pkgver}.zip"

  install -Dm644 "${srcdir}/reporting-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
