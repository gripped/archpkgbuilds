# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-reports-scheduler-plugin
pkgver=2.16.0.0
pkgrel=1
_opensearchver=2.16.0
_jdkver=11
pkgdesc="OpenSearch Reports Scheduler Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/dashboards/reporting"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/reporting/archive/${pkgver}.tar.gz"
)
sha256sums=('3ce4cdb6df9b3f18f5561a19381b47fb664049359d7be19307c9c36829254796')

build() {
  cd "reporting-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  unzip "${srcdir}/reporting-${pkgver}/build/distributions/opensearch-reports-scheduler-${pkgver}.zip"

  install -Dm644 "${srcdir}/reporting-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
