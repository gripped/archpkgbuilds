# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Danilo Bargen <gezuru@gmail.com>

pkgname=vim-jedi
_reponame=jedi-vim
pkgver=0.11.2
pkgrel=5
pkgdesc='Vim plugin for jedi, an awesome Python autocompletion'
url='https://github.com/davidhalter/jedi-vim'
arch=('any')
license=('MIT')
depends=('vim-plugin-runtime' 'python-jedi' 'python')
optdepends=('vim-supertab: tab completion')
checkdepends=('python-pytest')
groups=('vim-plugins')
options=('!emptydirs')
source=(https://github.com/davidhalter/${_reponame}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        python-3.13-accept-test-results.patch
	test-signature.patch)
sha256sums=('e169429a6a4bce5a32b94a3d7cc784c746f9d4fd78354122895ba50ded867afa'
            '08fe5fd50fac1f5d74ec95defb2411d079e518c396b94d4721105f6d5900099f'
            '03e03f8a6b2b1ad6fd41034e9aeb0a274f1835ebefcb6a3fcc59a2bde58f2f9e')
b2sums=('375cdf4742eb6328f5dc601b7a0b529a3d18b70711f93ab860e2827275f36454281c9d4ec85de0cfe280115fadde3a45f1bf74bfb4a4f1036b603eaa6051bf56'
        '5c160c31c46cf887a4b20e72b4386ad8c762dd229a469c184257dbc288c56f02fc256fce72cd993dfd7c843d347d55aef55683997449ad9955330adf21270d15'
        'd6f2a067ea28a9dd6ae7af010f90a79965f1051a00f1139212ae33d7f38b7b9e7d8c913c4ee5027179fb85fe041e39d7b3c0ea0602382c37e61863b532643a88')

prepare() {
  cd ${_reponame}-${pkgver}
  patch -Np1 -i ../python-3.13-accept-test-results.patch
  patch -Np1 -i ../test-signature.patch
}

check() {
  cd ${_reponame}-${pkgver}
  py.test
}

package() {
  cd ${_reponame}-${pkgver}

  install -d "${pkgdir}/usr/share/vim/vimfiles"
  cp -dpr --no-preserve=ownership pythonx "${pkgdir}/usr/share/vim/vimfiles"
  cp -dpr --no-preserve=ownership doc "${pkgdir}/usr/share/vim/vimfiles/doc"
  cp -dpr --no-preserve=ownership after "${pkgdir}/usr/share/vim/vimfiles/after"
  cp -dpr --no-preserve=ownership autoload "${pkgdir}/usr/share/vim/vimfiles/autoload"
  cp -dpr --no-preserve=ownership ftplugin "${pkgdir}/usr/share/vim/vimfiles/ftplugin"
  cp -dpr --no-preserve=ownership plugin "${pkgdir}/usr/share/vim/vimfiles/plugin"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
