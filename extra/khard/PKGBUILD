# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=khard
pkgver=0.19.0
pkgrel=1
pkgdesc='Console address book manager'
arch=('any')
url=https://github.com/lucc/khard
license=('GPL3')
depends=(
  'python-atomicwrites'
  'python-configobj'
  'python-ruamel-yaml'
  'python-unidecode'
  'python-vobject'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-sphinx'
  'python-sphinx-autoapi'
  'python-sphinx-autodoc-typehints'
  'python-wheel'
)
checkdepends=('python-pytest')
optdepends=(
  'diffutils: Using sdiff_khard_wrapper.sh'
  'vdirsyncer: Synchronization of address books with a DAV server'
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('SKIP')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
  make -C doc man
}

check() {
  cd $pkgname
  pytest -v
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  # additional wrapper script
  install -vD misc/sdiff/sdiff_${pkgname}_wrapper.sh \
    "$pkgdir"/usr/lib/$pkgname/sdiff_${pkgname}_wrapper.sh
  # twinkle integration
  install -vDm 644 misc/twinkle/scripts/*.py \
    -t "$pkgdir"/usr/share/$pkgname/twinkle/scripts/
  install -vDm 644 misc/twinkle/sounds/*.ogg \
    -t "$pkgdir"/usr/share/$pkgname/twinkle/sounds/
  # zsh
  install -vDm 644 misc/zsh/_*$pkgname \
    -t "$pkgdir"/usr/share/zsh/site-functions/
  # docs
  install -vDm 644 {CHANGES,CONTRIBUTING.rst,README.md} \
    -t "$pkgdir"/usr/share/doc/$pkgname/
  # man
  install -vDm 644 doc/build/man/$pkgname.1 \
    -t "${pkgdir}/usr/share/man/man1"
  install -vDm 644 doc/build/man/$pkgname.conf.5 \
    -t "${pkgdir}/usr/share/man/man5"
}
