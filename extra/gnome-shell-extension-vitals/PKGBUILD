# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-shell-extension-vitals
_uuid=Vitals@CoreCoding.com
pkgver=66.0.0
pkgrel=3
pkgdesc="GNOME Shell extension to display computer's temperature, voltage, fan speed, memory usage, processor load, system resources, network speed and storage stats"
arch=('any')
url='https://github.com/corecoding/Vitals'
license=('GPL-2.0-or-later')
depends=('gnome-shell')
optdepends=('libgtop: show storage stats')
source=("$pkgname-$pkgver.zip::https://github.com/corecoding/Vitals/releases/download/v$pkgver/vitals.zip"
        'gnome-shell-extension-vitals-swap-details.patch')
sha256sums=('c8b4431e42872dec22ed2c3dc1e625c69ac57d0c2b449ec6117865454bda66d0'
            '0bd0d7d297bb18e7e69a22bdaae2755da84565d0f51f478c9a3a0632aa922939')

prepare() {
  # https://github.com/corecoding/Vitals/pull/417
  patch -Np1 -i gnome-shell-extension-vitals-swap-details.patch
  rm *.patch *.zip
}

package() {
  install -dm755 "$pkgdir/usr/share/gnome-shell/extensions/$_uuid"
  cp -a * "$pkgdir/usr/share/gnome-shell/extensions/$_uuid/"
  install -dm755 "$pkgdir/usr/share/glib-2.0"
  mv "$pkgdir"/usr/share/gnome-shell/extensions/*/schemas "$pkgdir/usr/share/glib-2.0"
  mv "$pkgdir"/usr/share/gnome-shell/extensions/*/locale "$pkgdir/usr/share"
  rm "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"
}
