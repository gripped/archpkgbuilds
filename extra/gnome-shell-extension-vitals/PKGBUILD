# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-shell-extension-vitals
_uuid=Vitals@CoreCoding.com
pkgver=69.0.0
pkgrel=2
pkgdesc="GNOME Shell extension to display computer's temperature, voltage, fan speed, memory usage, processor load, system resources, network speed and storage stats"
arch=(any)
url='https://github.com/corecoding/Vitals'
license=(GPL-2.0-or-later)
depends=(gnome-shell)
makedepends=(git)
optdepends=('libgtop: Show storage stats')
source=("git+https://github.com/corecoding/Vitals.git#tag=v$pkgver")
b2sums=(e60d6dc0d2181273a7460691432b34ca9ac56d0a02f1940256d64992eda4336201eb97a56babe6bb181b2acaaa9f5f461e037e04908cccf598a7340410e7c601)

build() {
  cd Vitals
  for i in locale/*/; do
    msgfmt "$i/LC_MESSAGES/vitals.po" -o "$i/LC_MESSAGES/vitals.mo"
  done
}

package() {
  cd Vitals
  install -dm755 "$pkgdir/usr/share/gnome-shell/extensions/$_uuid"
  cp -a helpers icons metadata.json ./*.css ./*.js ./*.ui LICENSE "$pkgdir/usr/share/gnome-shell/extensions/$_uuid/"
  install -Dm644 -t "$pkgdir/usr/share/glib-2.0/schemas/" schemas/org.gnome.shell.extensions.vitals.gschema.xml

  for i in locale/*/; do
    install -Dm644 -t "$pkgdir/usr/share/$i/LC_MESSAGES/" "$i/LC_MESSAGES/vitals.mo"
  done
}
