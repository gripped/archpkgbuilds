# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=goocanvas
pkgver=3.0.0
pkgrel=8
pkgdesc='Cairo canvas widget for GTK'
arch=(x86_64)
url='https://wiki.gnome.org/Projects/GooCanvas'
license=(LGPL-2.0-only)
depends=(
  at-spi2-core
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  pango
)
makedepends=(
  glib2-devel
  gobject-introspection
  python-gobject
)
source=(
  "https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
  goocanvas-incompatible-pointer.patch
)
b2sums=(
  4ee632a614b6b881000bbee8d5e21a166151a94667c603c92b94c5e076fab78a6df44fe1d45fb4d7c4d65c0e060b599abf3970313542836a63efbca33d6a075b
  f663817c987cd02011b5325808d8c40144d49bac1f51998929530d64c5056cd14681476b0d0c302b82f1eb9fd220d53e784c433e80ef83f7aabe677e96ee9589
)

prepare() {
  cd $pkgname-$pkgver

  # https://gitlab.gnome.org/Archive/goocanvas/-/merge_requests/15
  patch -Np1 -i ../goocanvas-incompatible-pointer.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
