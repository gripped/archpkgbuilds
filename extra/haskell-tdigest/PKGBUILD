# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tdigest
pkgname=haskell-tdigest
pkgver=0.3.1
pkgrel=110
pkgdesc="On-line accumulation of rank-based statistics"
url="https://github.com/phadej/haskell-tdigest#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-foldable1-classes-compat' 'haskell-reducers' 'haskell-vector'
         'haskell-vector-algorithms')
makedepends=('ghc' 'uusi' 'haskell-tasty' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('193127a89f276537cc1ae889063eb26125baa90b31e47332b71b4a18080072ec')

prepare() {
  cd $_hkgname-$pkgver
  uusi -d semigroups $_hkgname.cabal
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
