# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=sway-contrib
# Upstream versions with <current_sway_version>-contrib.<version>. Since that
# is illegal in PKGBUILD-land, let's just remove the "-contrib" portion
# entirely.
# EDIT: Seems like they stopped doing that?
# At least, latest tag doesn't have the "-contrib" suffix.
pkgver=1.10.1
#_upstream_pkgver="$(echo "$pkgver" | sed -E 's/^([0-9]+\.[0-9]+)\.(.*)$/\1-contrib.\2/')"
pkgrel=1
pkgdesc="A collection of user-contributed scripts for sway"
arch=('any')
url="https://github.com/OctopusET/sway-contrib"
license=('MIT')
depends=(
    "grim"
    "jq"
    "libnotify"
    "python"
    "python-i3ipc"
    "slurp"
    "sway"
    "wl-clipboard"
)
source=("$pkgname-$pkgver::https://github.com/OctopusET/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('92d936dce14b36eec55b63efdebe83ee6128d9b7474827a830b163a33f3b6956')

package() {
    cd "$pkgname-$pkgver"

    install -dm755 "$pkgdir/usr/share/$pkgname"
    for util in autoname-workspaces.py \
                firefox-focus-monitor.py \
                grimpicker/grimpicker \
                grimshot/grimshot \
                float-window-manager/float-window-managerd.sh \
                inactive-windows-transparency.py \
                layout-per-window.py \
                switch-top-level.py; do
        install -Dm755 "$util" -t \
                       "$pkgdir/usr/share/$pkgname/"
    done

    install -dm755 "$pkgdir/usr/lib/systemd/user/"
    install sway-session.target -t "$pkgdir/usr/lib/systemd/user/"

    install -dm755 "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 float-window-manager/float-window-managerd-readme.txt -t "$pkgdir/usr/share/doc/$pkgname"

    install -dm755 "$pkgdir/usr/share/man/man1"
    install -Dm644 grimshot/grimshot.1 -t "$pkgdir/usr/share/man/man1"

    install -dm755 "$pkgdir/usr/share/bash-completion/completions/"
    install -Dm644 grimpicker/completion.bash "$pkgdir/usr/share/bash-completion/completions/grimpicker"
    install -Dm644 grimshot/grimshot-completion.bash "$pkgdir/usr/share/bash-completion/completions/grimshot"

    install -dm755 "$pkgdir/usr/share/zsh/site-functions/"
    install -Dm644 grimpicker/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_grimpicker"

    install -dm755 "$pkgdir/usr/share/fish/vendor_completions.d/"
    install -Dm644 grimpicker/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/grimpicker.fish"

    install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
