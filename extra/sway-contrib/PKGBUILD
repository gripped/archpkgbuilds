# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=sway-contrib
pkgver=1.11
pkgrel=1
pkgdesc="A collection of user-contributed scripts for sway"
arch=('any')
url="https://github.com/OctopusET/sway-contrib"
license=('MIT')
depends=(
    "grim"
    "jq"
    "libnotify"
    "python"
    "python-i3ipc"
    "slurp"
    "sway"
    "wl-clipboard"
)
makedepends=(
    'scdoc'
)
source=("$pkgname-$pkgver::https://github.com/OctopusET/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('cba416852de5e3db92c36d2444a709d2c87cf7c631f40e3e2b615bd4ab1b3eab')

package() {
    cd "$pkgname-$pkgver"

    # Utilities more specific to sway rather than general-purpose utilities
    install -dm755 "$pkgdir/usr/share/$pkgname"
    for util in autoname-workspaces.py \
                firefox-focus-monitor.py \
                float-window-manager/float-window-managerd.sh \
                inactive-windows-transparency.py \
                layout-per-window.py \
                swaystack.py \
                switch-top-level.py; do
        install -Dm755 "$util" -t \
                       "$pkgdir/usr/share/$pkgname/"
    done

    # General-purpose utilities
    install -dm755 "$pkgdir/usr/bin"
    for bin in grimpicker/grimpicker \
               grimshot/grimshot; do
        install -Dm755 "$bin" -t "$pkgdir/usr/bin/"
    done

    install -dm755 "$pkgdir/usr/lib/systemd/user/"
    install sway-session.target -t "$pkgdir/usr/lib/systemd/user/"

    install -dm755 "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 float-window-manager/float-window-managerd-readme.txt -t "$pkgdir/usr/share/doc/$pkgname"

    install -dm755 "$pkgdir/usr/share/man/man1"
    scdoc < grimshot/grimshot.1.scd > "$pkgdir/usr/share/man/man1/grimshot.1"
    scdoc < grimpicker/grimpicker.1.scd > "$pkgdir/usr/share/man/man1/grimpicker.1"

    install -dm755 "$pkgdir/usr/share/bash-completion/completions/"
    install -Dm644 grimpicker/completion.bash "$pkgdir/usr/share/bash-completion/completions/grimpicker"
    install -Dm644 grimshot/grimshot-completion.bash "$pkgdir/usr/share/bash-completion/completions/grimshot"

    install -dm755 "$pkgdir/usr/share/zsh/site-functions/"
    install -Dm644 grimpicker/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_grimpicker"

    install -dm755 "$pkgdir/usr/share/fish/vendor_completions.d/"
    install -Dm644 grimpicker/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/grimpicker.fish"

    install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
