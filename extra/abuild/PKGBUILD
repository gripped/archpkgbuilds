# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]
# Contributor: Clar Fon <them@lightdark.xyz>

pkgname=abuild
pkgver=3.13.0
pkgrel=4
pkgdesc="Alpine build tools"
url="https://git.alpinelinux.org/abuild"
arch=("x86_64")
license=("GPL-2.0-or-later")
depends=("busybox" "glibc" "pax-utils" "openssl" "apk-tools" "attr" "tar" "pkgconf" "lzip" "curl" "bubblewrap" "gettext" "git")
makedepends=("zlib" "scdoc")
optdepends=("perl: for cpan resolver"
            "perl-libwww: for cpan resolver"
            "perl-json: for cpan resolver"
            "perl-module-build-tiny: for cpan resolver"
            "perl-lwp-protocol-https: for cpan resolver"
            "ruby: for gem resolver"
            "ruby-augeas: for gem resolver")
backup=("etc/${pkgname}.conf")
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.tmpfiles")
sha512sums=('9aa46693984bd65a1a940d4547239e0a10170e82f186ab231b0d3a26b09a75cf258fae0e16f936e79cf52353dce9fc8a18881e84042b7961f390aba3f34d88da'
            '3fc94f88e572a46005eabbca97b3950a56c1026b320c2c8b137e1757cda9f97cc7fa35832574ccbd872efe5a39d6725e685d1fda6af4175e4594951cc89e3cb8')

build() {
	cd "${pkgname}-${pkgver}"
	make VERSION="${pkgver}"
}

package() {
	cd "${pkgname}-${pkgver}"
	make install VERSION="${pkgver}" DESTDIR="${pkgdir}"

	for bin in "${pkgdir}"/usr/bin/*; do
		sed -E -i "${bin}" \
			-e '1s|#!/bin/ash$|#!/bin/busybox ash|' \
			-e '1s|#!/bin/ash\s+(.*)$|#!/bin/busybox ash\nset \1|'
	done

	install -Dm 644 "${pkgname}.conf" "${pkgdir}/etc/${pkgname}.conf"
	install -Dm 644 "../${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
