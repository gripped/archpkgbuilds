# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]
# Contributor: Clar Fon <them@lightdark.xyz>

pkgname=abuild
pkgver=3.14.0
pkgrel=1
pkgdesc="Alpine build tools"
url="https://git.alpinelinux.org/abuild"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('alpine-keyring' 'busybox' 'glibc' 'pax-utils' 'openssl' 'apk-tools' 'attr' 'tar' 'pkgconf' 'lzip' 'curl' 'bubblewrap' 'gettext' 'git')
makedepends=('zlib' 'scdoc')
optdepends=('atools: additional scripts to maintain Alpine packages'
            'perl: for cpan resolver'
            'perl-libwww: for cpan resolver'
            'perl-json: for cpan resolver'
            'perl-module-build-tiny: for cpan resolver'
            'perl-lwp-protocol-https: for cpan resolver'
            'ruby: for gem resolver'
            'ruby-augeas: for gem resolver')
backup=("etc/${pkgname}.conf")
install="${pkgname}.install"
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles"
        'allow_running_abuild_rootbld.patch')
sha512sums=('997c9c2dadd5ba886477af099f0f500539fe7f276aa5fffc34dac5f43f33b19fa21dd4e015aa20e85f01792d18f2f7417fc1c911cd446022d99239bf4571dea4'
            'b6099d7dd0ce1128e1e5263c2fc95bfb8d0e26e02b4f7255f066f65ce405c19bf519fce90420e796a510b51c228a515415bdb021d32814271b11f9306b1ff62b'
            'f070f0dfb0b4c0ba443851f7e1115e42f76b0d1c6f866101df9dd3dda5ba2280065e50d459e7eda6262a42d10c2460f2ad0f88f02faf69a2188c02773bbbe423'
            '88a972802934f2d3e8440ff3de0ce9c3d2a4b4215244cea0508ff453c48fe1064749339ee1d24f196cec96ae2a9a396b8587adabeceeec155f10ed101d3b7677')

prepare() {
	cd "${pkgname}-${pkgver}"
	patch -Np1 < "${srcdir}/allow_running_abuild_rootbld.patch"
}

build() {
	cd "${pkgname}-${pkgver}"
	make VERSION="${pkgver}"
}

package() {
	cd "${pkgname}-${pkgver}"
	make install VERSION="${pkgver}" DESTDIR="${pkgdir}"

	for bin in "${pkgdir}"/usr/bin/*; do
		sed -E -i "${bin}" \
			-e '1s|#!/bin/ash$|#!/bin/busybox ash|' \
			-e '1s|#!/bin/ash\s+(.*)$|#!/bin/busybox ash\nset \1|'
	done

	install -Dm 644 "${pkgname}.conf" "${pkgdir}/etc/${pkgname}.conf"
	install -Dm 644 "../${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
	install -Dm 644 "../${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
