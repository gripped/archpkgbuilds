# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=fretboard
pkgver=9.1
pkgrel=1
pkgdesc='Look up guitar chords'
arch=(x86_64)
url='https://apps.gnome.org/Fretboard/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=(
  "git+https://github.com/bragefuglseth/fretboard.git#tag=v$pkgver"
  fretboard-window-icon.patch
  fretboard-metainfo-path.patch
)
b2sums=('3cabacce964d8c657a139bd8acbb86387307c5b32b4fb177bc10d9682c30ade9dded097225dd6eea91c9b4a59b9537829365d883fb8282d796ecfda07927fdcf'
        '5ec0fe78bf9cb057ee65abf9ca35168a30c85c4fdd5a75af7f44648e8bbfd7f0c654bab0d015663f8790ec261d746b5340c98bd8ffbf430ce92a489db5f36026'
        '162f4583bfb3f75b75ccee05fa7dcba030a7f90be805424617f02c412f9fa9725b789c4f442e9abd617875cf982f54595f0412e680294ac01038a8acf3f0a5d1')

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://github.com/bragefuglseth/fretboard/pull/57
  git apply -3 ../fretboard-window-icon.patch

  # https://github.com/bragefuglseth/fretboard/pull/58
  git apply -3 ../fretboard-metainfo-path.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
