# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=6.0.60
pkgrel=1
pkgdesc='Control panel of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
# startdde: org.deepin.dde.Display1
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'deepin-account-faces' 'deepin-api'
         'libpwquality' 'deepin-daemon' 'startdde' 'deepin-qt5integration' 'deepin-pw-check'
         'deepin-network-core' 'qt5-multimedia' 'polkit-qt5' 'deepin-desktop-theme' 'icu')
makedepends=('git' 'cmake' 'deepin-tray-loader' 'gmock' 'gtest' 'ninja' 'qt5-tools'
             'deepin-gettext-tools' 'doxygen' 'libsystemd')
optdepends=('redshift: automatic color temperature support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-control-center.git#tag=$pkgver"
        $pkgname-systeminfo-deepin-icon.patch)
sha512sums=('fcfcc23649af605d6d9ecf8c828dd21bc541289fac5e36508e5e54fc2778f3726193add4bd3f60596464f1fda88710f5493205d3268d3ecf0b6005ee92cc617e'
            '74fd63391e923ca37f4559f30da967ba7f33d4426b60d58d1ece8cd9a154578e8184b1a376a8d7ff3ef81ffce530915f79d0845a2612ae4e06522b96855ab3dd')

prepare() {
  patch -d dde-control-center -Np1 < $pkgname-systeminfo-deepin-icon.patch
  mkdir -p build
}

build() {
  cd build
  cmake \
    -GNinja \
    -DDISABLE_AUTHENTICATION=YES \
    -DDISABLE_LANGUAGE=YES \
    -DDISABLE_UPDATE=YES \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../dde-control-center
  ninja
}

check() {
  cd build
  #ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
