# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=6.1.6
pkgrel=2
pkgdesc='Control panel of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL-3.0-or-later')
# startdde: org.deepin.dde.Display1
depends=('dtk6core' 'dtk6declarative' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-declarative'
         'qt6-multimedia' 'qt6-svg' 'deepin-account-faces' 'deepin-api' 'libpwquality'
         'deepin-daemon' 'startdde' 'deepin-pw-check' 'deepin-network-core' 'deepin-desktop-theme'
         'icu' 'polkit-qt6')
makedepends=('git' 'cmake' 'deepin-tray-loader' 'gmock' 'gtest' 'ninja' 'qt6-tools'
             'deepin-gettext-tools' 'doxygen' 'libsystemd' 'extra-cmake-modules'
             'treeland-protocols')
optdepends=('redshift: automatic color temperature support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-control-center.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('77f2c9ee565c90ae6054b2eb8d3978973decb93d6b08070cd754f1cec7c368443faa8455f34264f905ac58017b3c7b47a6329e2c863fe99d73b9ca88905cade6'
            'ebe03d1701775e7f7e15fcd8b028216a8354a8ab0dad186c97a22039d5e8bda236b57215310da722586e8d53ad2dc688d56e3200fa266a94b9b66b5d40f12126')

prepare() {
  mkdir -p build

  cd dde-control-center
  # They broke the flag
  sed -i 's/BUILD_DCC_OLD ON/BUILD_DCC_OLD OFF/' CMakeLists.txt
  # Well, how did they compile like this?
  sed -i '/dde-control-center/d' src/plugin-accounts/CMakeLists.txt src/plugin-update/CMakeLists.txt
  # Fix build with Qt 6.10
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd build
  cmake \
    -GNinja \
    -DDISABLE_AUTHENTICATION=YES \
    -DDISABLE_LANGUAGE=YES \
    -DDISABLE_UPDATE=YES \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../dde-control-center
  ninja
}

check() {
  cd build
  #ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
