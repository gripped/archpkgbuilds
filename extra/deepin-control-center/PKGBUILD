# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=6.0.75
pkgrel=1
pkgdesc='Control panel of Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL-3.0-or-later')
# startdde: org.deepin.dde.Display1
depends=('dtk6core' 'dtk6declarative' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-declarative'
         'qt6-multimedia' 'qt6-svg' 'qt6-wayland' 'deepin-account-faces' 'deepin-api' 'libpwquality'
         'deepin-daemon' 'startdde' 'deepin-pw-check' 'deepin-network-core' 'deepin-desktop-theme'
         'icu')
makedepends=('git' 'cmake' 'deepin-tray-loader' 'gmock' 'gtest' 'ninja' 'qt6-tools'
             'deepin-gettext-tools' 'doxygen' 'libsystemd' 'extra-cmake-modules'
             'treeland-protocols')
optdepends=('redshift: automatic color temperature support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-control-center.git#tag=$pkgver")
sha512sums=('a332828238af4899a1e27f23e2d654007b6afb76584e2eccc05a5867ed1ad611b399d04dd1076518265613a3d3bde63186de4210553b9705cb58513d2fe8d5b7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake \
    -GNinja \
    -DDISABLE_AUTHENTICATION=YES \
    -DDISABLE_LANGUAGE=YES \
    -DDISABLE_UPDATE=YES \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_DCC_OLD=OFF \
    ../dde-control-center
  ninja
}

check() {
  cd build
  #ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
