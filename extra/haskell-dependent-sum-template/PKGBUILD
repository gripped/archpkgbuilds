# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=dependent-sum-template
pkgname=haskell-dependent-sum-template
pkgver=0.2.0.2
pkgrel=12
pkgdesc="Template Haskell code to generate instances of classes in some package"
url="https://github.com/obsidiansystems/dependent-sum-template"
license=('LicenseRef-PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-some' 'haskell-th-abstraction')
makedepends=('ghc' 'haskell-constraints-extras')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('44fa503fe6b4f6d649cf21300535596048c3e9c18906c204762186dfd7a3efe5')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
}
