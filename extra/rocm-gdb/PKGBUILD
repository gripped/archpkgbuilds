# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=rocm-gdb
pkgver=7.1.1
pkgrel=2
pkgdesc='ROCm source-level debugger for Linux, based on GDB'
arch=('x86_64')
url='https://github.com/ROCm/ROCgdb'
license=('GPL-3.0-or-later')
depends=(
    'bash'
    'expat'
    'gcc-libs'
    'glibc'
    'gmp'
    'guile'
    'libelf'
    'mpfr'
    'ncurses'
    'python'
    'readline'
    'rocm-dbgapi'
    'xz'
    'zlib'
    'zstd'
)
makedepends=('texinfo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/rocm-$pkgver.tar.gz")
sha256sums=('4369b0dc0bea6c371872d517c43867fdfba3f12af7d5ae3900d4a4311bd49e30')
_dirname="$(basename "$url")-$(basename "${source[0]}" ".tar.gz")"

build() {
    export PKG_CONFIG_PATH="/opt/rocm/share/pkgconfig:$PKG_CONFIG_PATH"
    cd "$_dirname"
    mkdir -p build && cd build
    ../configure \
        --prefix=/opt/rocm \
        --program-prefix=roc \
        --disable-binutils \
        --disable-gprofng \
        --disable-gprof \
        --enable-tui \
        --enable-64-bit-bfd \
        --enable-targets="amdgcn-amd-amdhsa" \
        --with-system-readline \
        --with-python=/usr/bin/python \
        --with-expat \
        --with-system-zlib \
        --with-lzma \
        --disable-gdbtk \
        --disable-ld \
        --disable-gas \
        --disable-gdbserver \
        --disable-sim

    make
}

package() {
    cd "$_dirname/build"
    make -C gdb DESTDIR="$pkgdir" install
}
