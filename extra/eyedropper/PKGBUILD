# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=eyedropper
pkgver=2.1.0
pkgrel=2
pkgdesc='Color picker and formatter'
arch=(x86_64)
url='https://apps.gnome.org/Eyedropper/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=(
  "git+https://github.com/FineFindus/eyedropper.git#tag=v$pkgver"
  eyedropper-fix-search-provider.patch
  eyedropper-dbus-activatable.patch
)
b2sums=(
  2b2f2f5cc9a979599d9562fc25dabdb47ec43527d45a1bd0224afac6bc429583ae35320c269203295b37e71df8a6895b664d8c47a7f8beb9ad23acd60f5280b0
  a0dc135e8a31baa574d512e18ca1babca56264b2c4a3025ef5eca76fdee6142dd45d8d55734f0c9367f1732a2b2381a07a9c76c77d81aee98270d8822c1295ee
  70f08f70da63d7e15cd2aa89aea6cb8e81fe31be5cc30fc1a0b2cbc95cd148c713af1970340090026397f5e839c305ab0846137bad169d181e931d0daf2701b2
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # Fix metainfo validation
  git cherry-pick -n 549d1e11290a37660c0207865b52310d5c580a32 \
                     5182e34ae171d4669ca2a0a65f54638116a7ad08

  # https://github.com/FineFindus/eyedropper/pull/202
  git apply -3 ../eyedropper-fix-search-provider.patch

  # https://github.com/FineFindus/eyedropper/pull/203
  git apply -3 ../eyedropper-dbus-activatable.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
