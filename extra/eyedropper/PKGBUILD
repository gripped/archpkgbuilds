# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=eyedropper
pkgver=2.1.0
pkgrel=3
pkgdesc='Color picker and formatter'
arch=(x86_64)
url='https://apps.gnome.org/Eyedropper/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://github.com/FineFindus/eyedropper.git#tag=v$pkgver")
b2sums=(2b2f2f5cc9a979599d9562fc25dabdb47ec43527d45a1bd0224afac6bc429583ae35320c269203295b37e71df8a6895b664d8c47a7f8beb9ad23acd60f5280b0)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # Fix metainfo validation
  git cherry-pick -n 549d1e11290a37660c0207865b52310d5c580a32 \
                     5182e34ae171d4669ca2a0a65f54638116a7ad08

  # https://github.com/FineFindus/eyedropper/pull/203
  git cherry-pick -n 4b5f913e2025eb51c6ec94b171aaf181532060ee

  # https://github.com/FineFindus/eyedropper/pull/202
  git cherry-pick -n 913d7f70377c721e6f67d1b395fa784258ffb639

  # https://github.com/FineFindus/eyedropper/pull/204
  git cherry-pick -n fb6db3a7f94be28e133c77d12808690aa5daaf6e
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
