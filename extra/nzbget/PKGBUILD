# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=24.5
pkgrel=1
pkgdesc="Efficient usenet downloader"
arch=('x86_64')
url="https://nzbget.com"
license=('GPL-2.0-or-later')
depends=(
  'boost-libs'
  'gcc-libs'
  'glibc'
  'libxml2'
  'ncurses'
  'openssl'
  'zlib'
)
makedepends=(
  'boost'
  'cmake'
  'git'
)
optdepends=(
  'python: to run included scripts'
  'unrar: unpacking archives'
  '7zip: unpacking archives'
)
changelog=$pkgname.changelog
source=(
  "https://github.com/nzbgetcom/nzbget/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "git+https://github.com/nzbgetcom/par2cmdline-turbo.git"
  "use-par2cmdline-turbo-from-sources.patch"
)
sha256sums=('d8a26fef9f92d63258251c69af01f39073a479e48c14114dc96d285470312c83'
            'SKIP'
            '862cf5a0e04274f141d308a5ce3c8c52baff3804e7d34f153202e7d3fc5a32e4')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../use-par2cmdline-turbo-from-sources.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DENABLE_TESTS=ON
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
