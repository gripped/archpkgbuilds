# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=26.0
pkgrel=1
pkgdesc="Efficient usenet downloader"
arch=('x86_64')
url="https://nzbget.com"
license=('GPL-2.0-or-later')
depends=(
  'boost-libs'
  'gcc-libs'
  'glibc'
  'libxml2'
  'ncurses'
  'openssl'
  'zlib'
)
makedepends=(
  'boost'
  'cmake'
  'git'
)
optdepends=(
  'python: to run included scripts'
  'unrar: unpacking archives'
  '7zip: unpacking archives'
)
source=(
  "https://github.com/nzbgetcom/nzbget/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "git+https://github.com/nzbgetcom/par2cmdline-turbo.git"
  "$pkgname-use-par2cmdline-turbo-from-sources.patch"
)
b2sums=('189554c285da93521495041461dd4b4ad9fae302a6ecddfe65b060ae93e19d658db3475bd9991dcf264592d5f3d2a738f374a01191b3c035bbb8c810c0694ddb'
        'SKIP'
        '801f142ccb38b7f4b5c5f3666bb5bcdb39eb8101e9be0e9aad17614b4c40628a0b598ae58fb506bc6423f63fbc49281fcd54c68d29b09baadc8c75fd8da9bd81')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../$pkgname-use-par2cmdline-turbo-from-sources.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DENABLE_TESTS=ON
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" linux/pkg/$pkgname.service
}
