# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=25.3
pkgrel=1
pkgdesc="Efficient usenet downloader"
arch=('x86_64')
url="https://nzbget.com"
license=('GPL-2.0-or-later')
depends=(
  'boost-libs'
  'gcc-libs'
  'glibc'
  'libxml2'
  'ncurses'
  'openssl'
  'zlib'
)
makedepends=(
  'boost'
  'cmake'
  'git'
)
optdepends=(
  'python: to run included scripts'
  'unrar: unpacking archives'
  '7zip: unpacking archives'
)
source=(
  "https://github.com/nzbgetcom/nzbget/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "git+https://github.com/nzbgetcom/par2cmdline-turbo.git"
  "$pkgname-use-par2cmdline-turbo-from-sources.patch"
)
sha256sums=('dc875b96606d0bc1dc6514fd51950f8bd6075698d6e29742ce145d2ae2553501'
            'SKIP'
            '9608a7166969593fea35c2db61a788a0c0083dece1907fcc33c978c70226ff0a')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../$pkgname-use-par2cmdline-turbo-from-sources.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DENABLE_TESTS=ON
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
