# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=bandit
pkgver=1.9.4
pkgrel=1
pkgdesc='Python security linter from OpenStack Security'
arch=('any')
url='https://github.com/PyCQA/bandit'
license=('Apache-2.0')
depends=('python-yaml' 'python-stevedore' 'python-gitpython' 'python-rich')
makedepends=('python-build' 'python-installer' 'python-pbr' 'python-setuptools' 'python-wheel' 'git')
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c7708f63800daaace130c08abae4c131f488baef4b81dde9b05959abfef88d1a28667c3abcbe258cf01e234bb4d5352b5c0d7a9e433d8a9ff58149e5bd48c8fa')

build() {
    cd bandit-$pkgver
    python -m build --wheel --no-isolation
}

package() {
    cd bandit-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
}
