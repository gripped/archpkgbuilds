# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-wacom
pkgver=8.0.1
pkgrel=1
pkgdesc='Switchboard wacom Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-wacom
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libgudev-1.0.so
  libswitchboard-3.so
  libwacom
  libx11
  libxi
)
makedepends=(
  git
  meson
  vala
)
_tag=ccf40dd4c3e3ee89c4fad2cb1dd5d46e6d9b2cd5
source=(git+https://github.com/elementary/switchboard-plug-wacom.git#tag=${_tag})
sha256sums=('b5b49b7118dfc7545092c7f3f46db975513485b42e2e672afd1ba96fa8c0cf86')

pkgver() {
  cd switchboard-plug-wacom
  git describe --tags
}

build() {
  arch-meson switchboard-plug-wacom build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
