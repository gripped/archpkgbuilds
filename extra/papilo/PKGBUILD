# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=papilo
pkgver=2.4.1
pkgrel=2
pkgdesc='Parallel Presolve for Integer and Linear Optimization'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(LGPL-3.0-only)
depends=(boost-libs
         gcc-libs
         glibc
         gmp
         tbb)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/scipopt/papilo#tag=v$pkgver)
sha256sums=('39b6c359f3987d534065e499538f5c94974430c8610e47e5697b83ffe7d49f60')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
