# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=papilo
pkgver=2.2.1
pkgrel=1
pkgdesc='Parallel Presolve for Integer and Linear Optimization'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(LGPL-3.0-only)
depends=(boost-libs
         gcc-libs
         glibc
         gmp
         tbb)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/scipopt/papilo#tag=v$pkgver)
sha256sums=('33bc4aec7b0e0ec6e7f08fbb4b823be2ffa179c095f8498b3859a02a4498581a')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
