# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=papilo
pkgver=2.4.2
pkgrel=1
pkgdesc='Parallel Presolve for Integer and Linear Optimization'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(LGPL-3.0-only)
depends=(boost-libs
         gcc-libs
         glibc
         gmp
         tbb)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/scipopt/papilo#tag=v$pkgver)
sha256sums=('ed666475dd473884737f800d3771f8f48dc3db680ae544cf2cbebf169c3ca27b')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
