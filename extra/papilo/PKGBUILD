# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=papilo
pkgver=3.0.0
pkgrel=1
pkgdesc='Parallel Presolve for Integer and Linear Optimization'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(LGPL-3.0-only)
depends=(boost-libs
         gcc-libs
         glibc
         gmp
         tbb)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/scipopt/papilo#tag=v$pkgver)
sha256sums=('2f7d368964ef6f8d9970ed2fb67715c8082a5904b01f6dc5de6a8f4eab2fb2da')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
