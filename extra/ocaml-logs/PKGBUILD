# Maintainer: Jürgen Hötzel <juergen@archlinux.org>

_pkgname=logs
pkgname=ocaml-${_pkgname}
pkgver=0.8.0
pkgrel=1
pkgdesc="Logging infrastructure for OCaml "
arch=('x86_64')
url="https://erratique.ch/software/logs"
license=('ISC')
depends=('ocaml' 'ocaml-fmt' 'ocaml-cmdliner')
makedepends=('ocaml-findlib' 'ocamlbuild' 'ocaml-topkg' 'opam')
options=('!strip')
source=("https://erratique.ch/software/${_pkgname}/releases/${_pkgname}-${pkgver}.tbz")
sha256sums=('9a61514095fa42f3012332623b6c8d605d427bea904b6a0d177f8ec3388236d8')


build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ocaml pkg/pkg.ml build --dev-pkg false --with-js_of_ocaml-compiler false --with-lwt false
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  opam-installer --prefix="$pkgdir/usr" --libdir='lib/ocaml' --docdir='share/doc'
  mv "${pkgdir}/usr/share/doc/${_pkgname}" "${pkgdir}/usr/share/doc/${pkgname}"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "/usr/share/doc/$pkgname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
