# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname="probe-rs"
pkgver=0.29.1
pkgrel=4
pkgdesc='A collection of on chip debugging tools to communicate with microchips.'
url='https://probe.rs'
license=('Apache-2.0' 'MIT')
arch=('x86_64')
depends=('gcc-libs' 'systemd-libs')
makedepends=('cargo' 'cargo-auditable' 'cmake')
conflicts=('cargo-embed' 'cargo-flash' 'rtthost')
replaces=('rtthost')
source=("$pkgname-$pkgver.tar.gz::https://github.com/probe-rs/probe-rs/archive/v$pkgver.tar.gz"
  "$pkgname-install-shell-completions-manual.patch::https://github.com/probe-rs/probe-rs/commit/57cce4bdecc26f5bdc1028f27784cf104b0f85fa.patch"
  "69-probe-rs.rules")
sha512sums=('7c5453fb2c212ebf851cff32c53e5ed1dc9ed148e8585ccceb7ac7c5caca91d8b971ab4cb027deab749fff44133efc350ebb324f534dd6ad5936f242d9bc0281'
            '6885134973477238a3ff539af0d02a2a5cfa9cdef334d9cf6c96b26368f32ab8fcd0cf3fc2b6102711777aebea2202d30ce8cc4d3e93e6281dbf7fa97eed39a7'
            'bb16b7e2c1d1522bae4b457c3547337531d4ebc9f2db1f1e319c4d7ad4308da6888afa746516cb2a41349cca6114b7cf1927825936ab879b066b362197b15095')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 -i ../"$pkgname-install-shell-completions-manual.patch"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"

  cargo auditable build \
    --frozen \
    --release \
    --features remote

  SHELL=zsh  target/release/probe-rs complete --shell zsh install --manual > _probe-rs
  SHELL=bash target/release/probe-rs complete --shell bash install --manual > probe-rs.bash
  SHELL=fish target/release/probe-rs complete --shell fish install --manual > probe-rs.fish
}

# TODO: Figure out how to run tests
# check() {
#   cd "$pkgname-$pkgver"
#
#   cargo test --frozen
# }

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/cargo-embed" -t "$pkgdir/usr/bin"
  install -Dm755 "target/release/cargo-flash" -t "$pkgdir/usr/bin"
  install -Dm755 "target/release/probe-rs" -t "$pkgdir/usr/bin"
  install -Dm755 "target/release/rtthost" -t "$pkgdir/usr/bin"
  install -Dm644 _probe-rs -t "$pkgdir/usr/share/zsh/site-functions"
  install -Dm644 probe-rs.bash -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm644 probe-rs.fish -t "$pkgdir/usr/share/fish/vendor_completions.d"
  install -Dm644 "$srcdir/69-probe-rs.rules" -t "$pkgdir//usr/lib/udev/rules.d/"
  install -Dm644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
