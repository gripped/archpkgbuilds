# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname="probe-rs"
pkgver=0.31.0
pkgrel=1
pkgdesc='A collection of on chip debugging tools to communicate with microchips.'
url='https://probe.rs'
license=('Apache-2.0' 'MIT')
arch=('x86_64')
depends=('gcc-libs' 'systemd-libs')
makedepends=('cargo' 'cargo-auditable' 'cmake')
conflicts=('cargo-embed' 'cargo-flash' 'rtthost')
replaces=('rtthost')
source=("$pkgname-$pkgver.tar.gz::https://github.com/probe-rs/probe-rs/archive/v$pkgver.tar.gz"
  "69-probe-rs.rules")
sha512sums=('435939aeda651ed355c3c72273318513133d8152eb7b0a0274568771719c1dbf529a6f0f52e09249e52e82cb892e3cbd9d4f8b0b79973d8d0ee8f83435700c20'
            'bb16b7e2c1d1522bae4b457c3547337531d4ebc9f2db1f1e319c4d7ad4308da6888afa746516cb2a41349cca6114b7cf1927825936ab879b066b362197b15095')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"

  cargo auditable build \
    --frozen \
    --release \
    --features remote

  SHELL=zsh  target/release/probe-rs complete --shell zsh install --manual > _probe-rs
  SHELL=bash target/release/probe-rs complete --shell bash install --manual > probe-rs.bash
  SHELL=fish target/release/probe-rs complete --shell fish install --manual > probe-rs.fish
}

# TODO: Figure out how to run tests
# check() {
#   cd "$pkgname-$pkgver"
#
#   cargo test --frozen
# }

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/cargo-embed" -t "$pkgdir/usr/bin"
  install -Dm755 "target/release/cargo-flash" -t "$pkgdir/usr/bin"
  install -Dm755 "target/release/probe-rs" -t "$pkgdir/usr/bin"
  install -Dm755 "target/release/rtthost" -t "$pkgdir/usr/bin"
  install -Dm644 _probe-rs -t "$pkgdir/usr/share/zsh/site-functions"
  install -Dm644 probe-rs.bash -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm644 probe-rs.fish -t "$pkgdir/usr/share/fish/vendor_completions.d"
  install -Dm644 "$srcdir/69-probe-rs.rules" -t "$pkgdir//usr/lib/udev/rules.d/"
  install -Dm644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
