# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=composer
pkgver=2.9.5
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php' 'unzip')
makedepends=('git')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://getcomposer.org/download/${pkgver}/composer.phar")
sha256sums=('663ea7ff8bca2884b9c69fbb6359055eee835bf011027aa95a7f4ce1cf83e2ae'
            'c86ce603fe836bf0861a38c93ac566c8f1e69ac44b2445d9b7a6a17ea2e9972a')

prepare() {
	cd "${srcdir}/${pkgname}"

	php "${srcdir}/${pkgname}-${pkgver}.phar" update --no-interaction --no-dev --no-progress --prefer-dist
}

build() {
	cd "${srcdir}/${pkgname}"

	php -d phar.readonly=Off bin/compile
	# Rename to generate correct completion
	mv composer.phar composer
	chmod +x composer
	./composer completion bash > completion.bash
}

check() {
	cd "${srcdir}"

	mkdir integration-test
	cd integration-test

	"${srcdir}/${pkgname}/composer" init \
		--no-interaction \
		--type=project \
		--name='archlinux/test' \
		--description='Composer Test Project' \
		--license='GPL-3.0-or-later' \
		--require='symfony/console:*'
	"${srcdir}/${pkgname}/composer" update --no-interaction --no-progress --prefer-dist
	"${srcdir}/${pkgname}/composer" validate --no-interaction
}

package() {
	cd "${srcdir}/${pkgname}"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 composer "${pkgdir}/usr/bin/composer"
	install -Dm644 completion.bash "${pkgdir}/usr/share/bash-completion/completions/composer"
}
