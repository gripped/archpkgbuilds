# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=composer
pkgver=2.8.4
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php' 'unzip')
makedepends=('git')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://getcomposer.org/download/${pkgver}/composer.phar")
sha256sums=('2e19f02ac74f59fa1a464b8812e4a3d166486e37f30a1bf70728f98514aea1a3'
            'c4c4e2e1beab0ea04e0bd042a5dbba9feda1fbf5eda0d36203958edd343c0a8a')

prepare() {
	cd "${srcdir}/${pkgname}"

	php "${srcdir}/${pkgname}-${pkgver}.phar" install --no-interaction --no-dev --no-progress --prefer-dist
}

build() {
	cd "${srcdir}/${pkgname}"

	php -d phar.readonly=Off bin/compile
	# Rename to generate correct completion
	mv composer.phar composer
	chmod +x composer
	./composer completion bash > completion.bash
}

check() {
	cd "${srcdir}"

	mkdir integration-test
	cd integration-test

	"${srcdir}/${pkgname}/composer" init \
		--no-interaction \
		--type=project \
		--name='archlinux/test' \
		--description='Composer Test Project' \
		--license='GPL-3.0-or-later' \
		--require='symfony/console:*'
	"${srcdir}/${pkgname}/composer" update --no-interaction --no-progress --prefer-dist
	"${srcdir}/${pkgname}/composer" validate --no-interaction
}

package() {
	cd "${srcdir}/${pkgname}"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 composer "${pkgdir}/usr/bin/composer"
	install -Dm644 completion.bash "${pkgdir}/usr/share/bash-completion/completions/composer"
}
