# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-mail-authenticationresults
_pkgname=Mail-AuthenticationResults
pkgver=2.20260216
pkgrel=1
pkgdesc='Object Oriented Authentication-Results Headers'
arch=(any)
url='https://metacpan.org/release/Mail-AuthenticationResults'
license=(GPL-1.0-or-later Artistic-1.0-Perl)
depends=(perl perl-clone perl-json)
checkdepends=(perl-test-exception)
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/M/MB/MBRADSHAW/Mail-AuthenticationResults-$pkgver.tar.gz")
sha512sums=('960ac7d77a2f65c44a1ea29347e2d5895a90ee12b43c5fb5210dc24fd74ecaad13ced86d19325cbf6b2e43de161907d54822f4b4541e1f6196ac86c4c17f8367')
b2sums=('5f14b1a8700102381b481c0179da42460c679f053b7c8cb8d6efc00e57c6091e8c9897e52b535abb5fa1da596a1dd8d8395da0c358cdb94d68987fb23ff178c7')

build() {
  cd "$_pkgname-$pkgver"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  perl Makefile.PL
  make
}

check() {
  cd "$_pkgname-$pkgver"

  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "$_pkgname-$pkgver"

  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
# vim:set ts=2 sw=2 et:
