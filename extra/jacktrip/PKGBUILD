# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=2.3.0
pkgrel=1
epoch=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(
  LGPL-3.0-only
  GPL-2.0-or-later
  GPL-3.0-or-later
  MIT
)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-quickcontrols2
  qt6-shadertools
  qt6-svg
  qt6-webchannel
  qt6-webengine
  qt6-websockets
  qt6-webview
)
makedepends=(
  help2man
  meson
  jack
  python-jinja
  python-yaml
  rtaudio
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('47e231b638c98254cfd86d56f0886b30e29c09d47326056433f9484d7098421fda3705098eb5a4306c5284cf420d174a4c9421b49fecd89c33f50d0e67097e9b')
b2sums=('aa8fc7a49178b5c106bce4539d8f57b143241b7b311d1393679763fb221caa0667a39ae523742748ca14cbf688b1cd82da75c6ff58ff0acf0167bfd110c8d406')

build() {
  local meson_options=(
    -D noupdater=true
  )

  arch-meson "${meson_options[@]}" build $pkgname-$pkgver
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    jack libjack.so
    rtaudio librtaudio.so
  )

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
