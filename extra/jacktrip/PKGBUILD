# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=2.4.1
pkgrel=2
epoch=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(
  LGPL-3.0-only
  GPL-2.0-or-later
  GPL-3.0-or-later
  MIT
)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-shadertools
  qt6-svg
  qt6-webchannel
  qt6-webengine
  qt6-websockets
  qt6-webview
)
makedepends=(
  help2man
  meson
  jack
  python-jinja
  python-yaml
  rtaudio
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
        ignore_gzip_timestamps.patch)
sha512sums=('a95f78d23a944aeade885634abbb8396ca2280d2487a5b7cd2a0614de1761c029164541841184f4933775f4b020b9f1facd0c20fecceb94bc2c0a762b93c7de6'
            '1361f2dd0ca374b68c62340867d2fcff75a142b9b4d1e9e23c46a68dd2aadcccba59b6ecda769f8fc89634439228515ffcef8189042b18d123054f97522acdff')
b2sums=('cf3749b785f390f7f12bfff36a52536962a0aac62a8f67fc071430222313882ce9f73ec506bb2f1a162b2b0a910f89190aed80deeb60e10ec870a189c5562699'
        '8a764c0af74abf6e1340d2fd1f82f7d354ed38ed90a36c4fd57b69eff2332c60a56e6eba95714dc640d945ca7e005d5fda54f82cea9f44ea849aaf02048b6b82')

prepare() {
  cd $pkgname-$pkgver

  # Ignore timestamps recording in gzip metadata
  # This is required for reproducible builds
  # See https://github.com/jacktrip/jacktrip/pull/1394
  patch -Np1 -i $srcdir/ignore_gzip_timestamps.patch
}

build() {
  local meson_options=(
    -D noupdater=true
  )

  arch-meson "${meson_options[@]}" build $pkgname-$pkgver
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    jack libjack.so
    rtaudio librtaudio.so
  )

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
