# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Cosmic <CosmicHorrorDev@pm.me>

pkgname=samply
pkgver=0.13.1
pkgrel=1
pkgdesc="A command-line sampling profiler for macOS and Linux"
arch=('x86_64')
url="https://github.com/mstange/samply"
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgname-v$pkgver.tar.gz")
sha512sums=('2e26ba41a4361fc35ae03b098d708e047f44a04a9d104600c9323a395f276c3e84dd205ae983b911112b4d091fe163e14003ac58f5ac565ba3bd7f857747ac96')
options=('!lto')

prepare() {
  mv "$pkgname-$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver/$pkgname"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" --locked
}

build() {
  cd "$pkgname-$pkgver/$pkgname"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver/$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
