# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=fiery
pkgver=2.0.1
pkgrel=1
pkgdesc='A convergent web browser'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         mauikit
         mauikit-filebrowsing
         qt6-base
         qt6-declarative
         qt6-webengine)
makedepends=(extra-cmake-modules)
groups=(maui)
# source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/maui-$pkgname-$pkgver.tar.xz)
source=(https://invent.kde.org/maui/maui-fiery/-/archive/v$pkgver/maui-fiery-v$pkgver.tar.gz)
sha256sums=('b959c5302771cafeec7b4efa6b14f914e6537c8c134bf3b2d376c57e37933f77')

build() {
  cmake -B build -S maui-$pkgname-v$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
