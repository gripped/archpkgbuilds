# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=param
pkgname=python-${_pkg}
pkgver=2.1.1
pkgrel=4
pkgdesc="Make your Python code clearer and more reliable by declaring Parameters"
arch=(any)
url="https://param.holoviz.org/"
license=(BSD-3-Clause)
depends=(python)
makedepends=(git python-build python-installer python-wheel python-hatchling python-hatch-vcs)
checkdepends=(python-pytest python-pytest-asyncio python-jsonschema python-numpy python-pandas ipython)
# No tests in Pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(git+https://github.com/holoviz/param#tag=v$pkgver)
sha256sums=('57025f9dc79bea9b9aabf47be2a5ee384a2839f4ffe58d2d9106e2157ab33f82')

build() {
  cd ${_pkg}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkg}
  PARAM_TEST_JSONSCHEMA=1 PARAM_TEST_NUMPY=1 PARAM_TEST_PANDAS=1 PARAM_TEST_IPYTHON=1 pytest tests -vv --color=yes
}

package() {
  cd ${_pkg}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
