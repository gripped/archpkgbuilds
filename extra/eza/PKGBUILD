# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=eza
pkgver=0.18.24
pkgrel=1
pkgdesc="A modern replacement for ls (community fork of exa)"
url="https://github.com/eza-community/eza"
arch=(x86_64)
license=(MIT)
provides=(exa)
replaces=(exa)
conflicts=(exa)
depends=(gcc-libs # libgcc_s.so
         glibc # libc.so libm.so
         libgit2
         zlib)
makedepends=(cargo
             pandoc)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('bdcf83f73f6d5088f6dc17c119d0d288fed4acd122466404772be5ef278887de')
b2sums=('02a0c86c9066f8ebcab1a0e54b3cf621c7cae222e8f5f5ea19b10f82704102782dbc4a3a18227d56a3df634ca9d732dff86f77957f9dfa234c3ed8cd0df3bfec')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  # Make sure the build fails if our system libgit2 is out
  # of range rather than silently starting to vendor it.
  export LIBGIT2_NO_VENDOR=1
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release
  mkdir -p target/man
  for manpage in eza.1 eza_colors.5 eza_colors-explanation.5; do
    pandoc --standalone -f markdown -t man "man/${manpage}.md" > "target/man/${manpage}"
  done
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
  target/release/eza -la
}


package() {
  depends+=(libgit2.so
            libz.so)
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  ln -s eza "${pkgdir}/usr/bin/exa"

  # install completions
  install -Dm644 "completions/bash/${pkgname}" -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm644 "completions/zsh/_${pkgname}" -t "${pkgdir}/usr/share/zsh/site-functions/"
  install -Dm644 "completions/fish/${pkgname}.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d"

  # install man pages
  install -Dm644 target/man/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm644 target/man/*.5 -t "${pkgdir}/usr/share/man/man5"

  install -Dm644 LICEN?E "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
