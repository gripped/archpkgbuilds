# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.12.0
pkgrel=1
pkgdesc='Creates virtual Ethernet networks of almost unlimited size.'
arch=('x86_64')
url='https://www.zerotier.com/'
license=('custom:BSL')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/refs/tags/$pkgver.tar.gz"
        'sysusers.conf')
sha512sums=('4e0538f72931fa3aeeee8df27e863ceef6d6a35d44af5a8df565c3027e865b0345f02d6c3a0bcd08e3264c0d9eae4e724af0d0d016cd7f86132ee6fcab513ecb'
            '1fdcc2e2a888ac7a93985bb6c16ac818aa6b7a56ccdeedf3cbbd22158251f6119f841e41e79b5522f9d1c6b8a315525d178cc42197c5eef2c70498b7954541f0')

prepare() {
  cd ZeroTierOne-$pkgver

  sed \
    -e 's/sbin/bin/' \
    -e 's/LDFLAGS=/LDFLAGS?=/' \
    -e 's/RUSTFLAGS=/RUSTFLAGS?=/' \
    -e 's/cargo build $(RUSTFLAGS)/cargo build --release/' \
    -i make-linux.mk debian/zerotier-one.service
}

build() {
  cd ZeroTierOne-$pkgver

  make V=1
}

check() {
  cd ZeroTierOne-$pkgver

  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver

  make DESTDIR="$pkgdir" install
  install -D -m0644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service

  install -D -m0644 "$srcdir"/sysusers.conf "$pkgdir"/usr/lib/sysusers.d/"$pkgname".conf
}
