# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.14.2
pkgrel=3
pkgdesc='Creates virtual Ethernet networks of almost unlimited size.'
arch=('x86_64')
url='https://www.zerotier.com/'
license=('custom:BSL')
depends=('glibc'
         'openssl' 'libcrypto.so' 'libssl.so'
         'gcc-libs'
         'libnatpmp'
         'miniupnpc' 'libminiupnpc.so')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/refs/tags/$pkgver.tar.gz"
        '0001-miniupnpc.patch'
        'sysusers.conf')
sha512sums=('9d022afcf81543d6ee938219a3712da846fe895b0fd65cfd6ec8ed173f0e208516031b6d2303ab42fd21806d9ba5ff6fdb0d850a0cbb32b268d53accb093cdf5'
            'd178b37af7c554ececbe099688ec04396445dae9e0ee7a4b5d53c0c27eb535464502e56fa49ac2928feb4ed729f001edf89c488b35301d368f306e0c6482654c'
            '9499251dcd5cfb415e7cbfae33f17f40a1b9f94547df5edea9d94d6ecfa4b97e49eda1397d70968422e13b902ce0e591bd081bbec57c81465ff84bf5848851dd')

prepare() {
  cd "ZeroTierOne-${pkgver}"

  # remove bundled miniupnpc code, and build with package headers
  # also fix for MINIUPNPC_API_VERSION 18
  rm -rf ext/miniupnpc/
  patch -Np1 < ../0001-miniupnpc.patch

  sed \
    -e 's/sbin/bin/' \
    -e 's/LDFLAGS=/LDFLAGS?=/' \
    -e 's/RUSTFLAGS=/RUSTFLAGS?=/' \
    -e 's/cargo build $(RUSTFLAGS)/cargo build --release/' \
    -i make-linux.mk debian/zerotier-one.service
}

build() {
  cd "ZeroTierOne-${pkgver}"

  make V=1
}

check() {
  cd "ZeroTierOne-${pkgver}"

  make selftest
  ./zerotier-selftest
}

package() {
  cd "ZeroTierOne-${pkgver}"

  make DESTDIR="$pkgdir" install
  install -D -m0644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service

  install -D -m0644 "$srcdir"/sysusers.conf "$pkgdir"/usr/lib/sysusers.d/"$pkgname".conf
}
