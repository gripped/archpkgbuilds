# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-base
pkgver=2024.04.18
pkgrel=1
epoch=2
pkgdesc='Base component for Deepin'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-base"
license=('GPL3')
makedepends=('git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-desktop-base.git#tag=$pkgver"
        distribution.info)
sha512sums=('cd609190061941b39e1cf25cbd15a56bcb196a4f25a6204e5de42530ba1f1e2144eca1d71887eac464ddab3716d9352ae37e4bc348739ca13ab221bd17c3adb9'
            '17b17c3174a052ae93e57cfffaf551cac05a56da62b2f4829f5e8f4d0bd9dbe47e043ffcbd2c6299a771f7f8b8f2fffc727f2b5b754ab1cffbbf72fa3f54d035')

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/distribution.info -t "$pkgdir"/usr/share/deepin/

  # Remove Deepin distro's lsb-release
  rm "$pkgdir"/etc/lsb-release

  # Don't override systemd timeouts
  rm -r "$pkgdir"/etc/systemd

  # Make a symlink for deepin-version
  ln -s ../usr/lib/deepin/desktop-version "$pkgdir"/etc/deepin-version

  # Install os-version and rename to uos-version
  install -Dm644 files/os-version "$pkgdir"/etc/uos-version

  # Remove apt-specific templates
  rm -r "$pkgdir"/usr/share/python-apt

  # Remove empty distro info directory
  rm -r "$pkgdir"/usr/share/distro-info
}
