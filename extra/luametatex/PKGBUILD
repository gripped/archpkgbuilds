# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=luametatex
pkgver=2.11.04
pkgrel=1
pkgdesc='The lean and mean successor of the LuaTeX engine'
arch=(x86_64)
url='https://wiki.contextgarden.net/LMTX'
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(cmake
             git)
source=(git+https://github.com/contextgarden/luametatex#tag=v$pkgver)
sha256sums=('1064e7a010e3db59c66cf5b1667d13bd0429257bccca4b2665e13c9aa5c9d85b')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  ln -s luametatex "$pkgdir"/usr/bin/context
  ln -s luametatex "$pkgdir"/usr/bin/mtxrun
}
