# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=stevia
pkgver=0.51.0
pkgrel=1
pkgdesc='A user friendly on screen keyboard for Phosh'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/stevia'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  feedbackd
  gcc-libs
  glib2
  glibc
  gnome-desktop
  gtk3
  hunspell
  json-glib
  libhandy
  pango
  systemd-libs
  wayland
)
makedepends=(
  git
  glib2-devel
  meson
  wayland-protocols
)
checkdepends=(xorg-server-xvfb)
conflicts=(squeekboard)
provides=(phosh-osk-provider)
source=("git+https://gitlab.gnome.org/World/Phosh/stevia.git?signed#tag=v$pkgver")
b2sums=(690b7df2a5cfce5d6b76353586ae70be5a1c29b39449021852922a5741b0061d44764aa23f2844ea1ac6c61084b406f4674c7af7c8b47a8839719ce4a8d2d9f2)
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido Günther <guido@xcpu.ch>

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
