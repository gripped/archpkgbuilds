# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=stevia
pkgver=0.52.0
pkgrel=1
pkgdesc='A user friendly on screen keyboard for Phosh'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/stevia'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  feedbackd
  gcc-libs
  glib2
  glibc
  gnome-desktop
  gtk3
  hunspell
  json-glib
  libhandy
  pango
  systemd-libs
  wayland
)
makedepends=(
  git
  glib2-devel
  meson
  wayland-protocols
)
checkdepends=(xorg-server-xvfb)
conflicts=(squeekboard)
provides=(phosh-osk-provider)
source=("git+https://gitlab.gnome.org/World/Phosh/stevia.git?signed#tag=v$pkgver")
b2sums=(d3c3476b139ea9032ef065435b66e96f4a8c127ce052361cd1c83b03a7a949e6515dd8542aa48f902d29c7a2a638f0884308e275a6f066dd38d64d0cd15a7a08)
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido Günther <guido@xcpu.ch>

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
