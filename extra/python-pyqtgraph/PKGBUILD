# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Windel Bouwman <windel at windel dot nl>
# Contributor: Jakub Klinkovský <kuba.klinkovsky@gmail.com>

_pkgname=pyqtgraph
pkgname=python-$_pkgname
pkgver=0.13.7
pkgrel=2
pkgdesc='Scientific Graphics and GUI Library for Python'
arch=('any')
url='http://www.pyqtgraph.org'
license=('MIT')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
depends=('python' 'python-numpy')
optdepends=('python-opengl: 3d graphics'
            'qt5-python-bindings: Qt5 backend'
            'qt6-python-bindings: Qt6 backend')
checkdepends=('python-pyqt5' 'python-pyqt6' 'python-pytest' 'python-opengl' 'qt5-svg' 'qt6-svg')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/pyqtgraph/pyqtgraph/archive/pyqtgraph-$pkgver.tar.gz")
sha512sums=('91e06e143fec4b1a6fae5cc7112a1fb6b7e74c7a38605172aacc5639eb220e30c54694e419313c7bb8368ea5c5f45f41d58e4510735e63a7d6ef03cdbb0f05e9')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  python -m build -nw
}

check() {
  cd $_pkgname-$_pkgname-$pkgver

  export QT_QPA_PLATFORM=offscreen
  pytest -v
}

package() {
  cd $_pkgname-$_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
