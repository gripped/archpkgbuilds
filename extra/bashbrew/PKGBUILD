# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>

pkgname=bashbrew
pkgver=0.1.13
pkgrel=1
pkgdesc="Canonical build tool for Docker official images"
arch=('x86_64')
url="https://github.com/docker-library/bashbrew/"
license=('Apache-2.0')
depends=('docker' 'git' 'glibc')
makedepends=('go')
source=("git+https://github.com/docker-library/bashbrew#tag=v${pkgver}")
sha256sums=('7b74ae52e9862bf94ac9ec50949aca45819eb8d0f00157251b5b5f0164948734')

build() {
    cd "${pkgname}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
    go build -o "${pkgname}" ./cmd/...
}

# disable tests as skipping the broken ones seems not possible:
# https://github.com/docker-library/bashbrew/issues/116
# check() {
#     cd "${pkgname}"
#     go test ./...
# }

package() {
    cd "${pkgname}"
    install -m755 -Dt "${pkgdir}/usr/bin/" "${pkgname}"
}
