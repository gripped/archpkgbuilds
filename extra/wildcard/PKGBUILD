# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=wildcard
pkgver=0.3.3
pkgrel=2
pkgdesc='Provides a simple interface to test/practice regular expressions'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Wildcard'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/Wildcard.git?signed#tag=v$pkgver")
b2sums=(53bed5b291c0374d03960a293450f610c46eb6c2f24ef0c781d3b112073d81c506ce9ccc5e13daf99ceca49dcabe9f2bae78b36df16af6a5ef6fd0441b8b1160)
validpgpkeys=(9CBBC6850B53EDB5DC61B072FE494A5BBA386771) # Felipe Kinoshita <kinofhek@gmail.com>

prepare() {
  cd Wildcard

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Wildcard build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
