# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>

pkgname=bash-language-server
pkgver=5.4.3
pkgrel=1
pkgdesc='Bash language server implementation based on Tree Sitter and its grammar for Bash'
arch=(any)
url=https://github.com/bash-lsp/bash-language-server
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  npm
  pnpm
)
optdepends=(
  'bash-completion: for CLI args completion'
  'man-db: for documentation via manual pages'
  'shellcheck: for linting support'
  'shfmt: for formatting support'
)
options=(!emptydirs)
source=("git+$url.git#tag=server-$pkgver")
b2sums=('87ce9254d5ab9c0b99666565eedb58ffcd324dedda090ca45dce33368a2a282b382995f7505716dda88111001ae62ed140e14a6e674891423825affec988e428')

build() {
  cd $pkgname
  pnpm install --frozen-lockfile
  pnpm run compile
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/out/cli.js "$pkgdir"/usr/bin/$pkgname

  cd $pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE

  cd server
  # Prune unnecessary packages
  rm -r node_modules
  npm install --production

  cp -r node_modules out package.json tree-sitter-bash.wasm "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/out/cli.js
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname ./*.md
}
