# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=ptyxis
pkgver=49.3
pkgrel=1
pkgdesc="A container oriented terminal"
url="https://gitlab.gnome.org/chergert/ptyxis"
arch=(x86_64)
license=(GPL-3.0-only)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libportal
  libportal-gtk4
  pango
  vte4
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/chergert/ptyxis.git#tag=${pkgver/[a-z]/.&}")
b2sums=('c9de97e134e4687a0a57a7c4a08633a1438338a2f14a2a08a8fda426f35f62b853ff6f7b3a6262044f35b0241f4a8c579b5aee1e91b7621c0d2270822a5116bb')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
