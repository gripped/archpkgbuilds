# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=ptyxis
pkgver=49.0
pkgrel=1
pkgdesc="A container oriented terminal"
url="https://gitlab.gnome.org/chergert/ptyxis"
arch=(x86_64)
license=(GPL-3.0-only)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libportal
  libportal-gtk4
  pango
  vte4
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/chergert/ptyxis.git#tag=${pkgver/[a-z]/.&}")
b2sums=('8ced0b1d519aed65b98f5c9cff7f2d444b11e653b8b8bc5ee4387151e0e3ee89ad644b9f16455ea767d6ab85e34fb5794aef1c8f4ecc1baca3cf8a8e162e62f5')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
