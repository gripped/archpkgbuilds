# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=ptyxis
pkgver=49.2
pkgrel=1
pkgdesc="A container oriented terminal"
url="https://gitlab.gnome.org/chergert/ptyxis"
arch=(x86_64)
license=(GPL-3.0-only)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libportal
  libportal-gtk4
  pango
  vte4
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/chergert/ptyxis.git#tag=${pkgver/[a-z]/.&}")
b2sums=('0a514dc8e92cb20daffac151a27d74c93514350f08711d64241ef0e07dfee6986fc2fde8ac2658d36109d3316df87c9793350e6b4b3af55e6321bd58756c9e8e')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
