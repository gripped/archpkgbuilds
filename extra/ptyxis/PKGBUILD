# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=ptyxis
pkgver=49.1
pkgrel=1
pkgdesc="A container oriented terminal"
url="https://gitlab.gnome.org/chergert/ptyxis"
arch=(x86_64)
license=(GPL-3.0-only)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libportal
  libportal-gtk4
  pango
  vte4
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/chergert/ptyxis.git#tag=${pkgver/[a-z]/.&}")
b2sums=('d14d637c358939a5254b77e84def6a122ffa3c1eda4994bb7fd792a1806a8ce9d89216ea3ae47341ca818e4dc3db35f9a0bc2700db05e262d80d13bf85742df5')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
