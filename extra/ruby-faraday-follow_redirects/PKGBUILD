# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='faraday-follow_redirects'
_archivename='faraday-follow-redirects'
pkgname="ruby-${_gemname}"
pkgver=0.4.0
pkgrel=1
pkgdesc='Faraday 2.x compatible extraction of FaradayMiddleware::FollowRedirects'
arch=('any')
url="https://github.com/tisba/${_archivename}"
license=('MIT')
depends=(
  ruby
  ruby-faraday
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
  ruby-webmock
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('561c173b7b9a3112b55239fa0ca2d764b85fe9b6647954bfa26e2b683419261f270cd3bb44e571fc25454c3aa559b04e25a1d8288a70837d364438cd02d14580')
b2sums=('7bd82df147fc380a600db3444eac5e350865503ba2851d152c921c7cde5c4352183427e5cba6643f6053e1090bb7692dbac23f5be85a8761d4927dfe90865aaa')

prepare() {
  cd "${_archivename}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_archivename}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_archivename}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "${_archivename}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
