# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Marcel Campello <marcel@prafrentex.com.br>

pkgname=opentofu
pkgver=1.11.1
pkgrel=1
pkgdesc="Lets you declaratively manage your cloud infrastructure"
arch=(x86_64)
url="https://github.com/opentofu/opentofu"
license=(MPL-2.0)
depends=(glibc)
makedepends=(go)
provides=(terragrunt-iac-provider)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('fa4927bd08be41775ad6a104414f3fc72ea297a04b80f5601d47479aabd32d3c')

prepare() {
  cd $pkgname-$pkgver
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false  \
    -linkmode=external  \
    -X github.com/opentofu/opentofu/version.dev=no \
  "
  go build -v -ldflags="$ld_flags" ./cmd/...
}

check() {
  cd $pkgname-$pkgver
  go test ./...
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" tofu

  echo 'complete -C /usr/bin/tofu tofu' \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/tofu"
  {
    echo '#compdef tofu'
    echo 'autoload -U +X bashcompinit && bashcompinit'
    echo 'complete -o nospace -C /usr/bin/tofu tofu'
  } | install -vDm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_tofu"

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -a -t "$pkgdir/usr/share/doc/$pkgname" docs
}
