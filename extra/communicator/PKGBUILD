# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=communicator
pkgver=4.0.1
pkgrel=1
pkgdesc='Contacts and dialer application'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcontacts
         kcoreaddons
         ki18n
         kio
         kpeople
         mauikit
         mauikit-filebrowsing
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/maui-$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('5d3c2629ecebc726ea3d7cef960c66defd2347354eadf4ac9df027040bd0f8bb'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S maui-$pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
