# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=communicator
pkgver=4.0.0
pkgrel=1
pkgdesc='Contacts and dialer application'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcontacts
         kcoreaddons
         ki18n
         kio
         kpeople
         mauikit
         mauikit-filebrowsing
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/maui-$pkgname-$pkgver.tar.xz{,.sig}
        fix-build.patch)
sha256sums=('1fdd7ff3257d8b8e7e47c6f0cd3756ad39d9048644f6d88f1d1bf22c7f2dec95'
            'SKIP'
            '8277d7f201e6324151aae5b0a5bfa3f7bd4af3c52d086f9981e0c84047470cd2')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

prepare() {
  patch -d maui-$pkgname-$pkgver -p1 < fix-build.patch
}

build() {
  cmake -B build -S maui-$pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
