# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=tree-sitter-rust
pkgver=0.24.0
pkgrel=1
pkgdesc='Rust grammar for tree-sitter'
arch=('x86_64')
url=https://github.com/tree-sitter/tree-sitter-rust
license=('MIT')
groups=('tree-sitter-grammars')
makedepends=('git' 'tree-sitter-cli')
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("git+$url.git#tag=v$pkgver")
b2sums=('54bc2503d89ec02b2fdc686a267bd646b002239f3a4af2ce7f8ce546c54be25a5ea9cd56e84ff6824d40a84acb6602cac677fde10a93817727039ef5ebb7d282')

prepare() {
  cd $pkgname
  tree-sitter generate --no-bindings src/grammar.json
}

build() {
  cd $pkgname
  make PREFIX=/usr PARSER_URL="$url"
}

package() {
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
