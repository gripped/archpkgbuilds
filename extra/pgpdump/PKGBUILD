# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=pgpdump
pkgver=0.37
pkgrel=1
pkgdesc='PGP packet visualizer which displays the packet format'
url='https://www.mew.org/~kazu/proj/pgpdump'
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  bzip2
  glibc
  zlib
)
source=(
  https://github.com/kazu-yamamoto/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
)
sha256sums=('bc3b6b85f3c95c68010883675283c1c905e6c4070ac5609ced1a87c53b3ee814')
sha512sums=('58a59c5e7b7df7f885e769291699fecf8c96ae0e17a02a2722c3171249a3e4d2a46bed220776b596b1071e258f862198851ba3030b9ef111dfcb012f865f00d2')

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
