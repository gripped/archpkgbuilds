# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Rafael Dominiquini <rafaeldominiquini at gmail dor com>

pkgname=inferno
pkgver=0.12.4
pkgrel=1
pkgdesc="A Rust port of FlameGraph"
arch=('x86_64')
url="https://github.com/jonhoo/inferno"
license=('CDDL-1.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('af1d4520c9a3ecc25601f0e6f049d6acb484b9736d00f218a5a9859f4a597770')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib -- --skip "collapse"
}

package() {
  cd "$pkgname-$pkgver"

  for bin in "inferno-collapse-perf" "inferno-collapse-dtrace" "inferno-collapse-guess" "inferno-flamegraph" "inferno-diff-folded"; do
    install -Dm755 "target/release/$bin" "${pkgdir}/usr/bin/$bin"
  done

  install -Dm644 "CHANGELOG.md" -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm644 "README.md" -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
