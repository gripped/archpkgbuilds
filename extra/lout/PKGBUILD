# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: catwell <catwell@archlinux.us>

pkgname=lout
pkgver=3.43.3
pkgrel=1
pkgdesc='A lightweight document formatting system'
arch=(x86_64)
url='https://github.com/william8000/lout'
license=(GPL-3.0-or-later)
depends=(glibc zlib)
makedepends=(git)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('002725a4bcf5696bf6155be2b7d65be69d313eb319a38f03fc13d167f7858e6ff1945ee41c2854128a3b27b25e6543c3c0bfce8215c3ca893152456ea484b9a0')
b2sums=('87e76a349c4597aa1331ab9a43e100c609b6cbd0c2035ca5ed2dad8fe405d805b8387c7d45a1cf2403a2717dbcb8f6d8faa59904d0ab5649b08d8b56552d198b')

prepare() {
  cd "$pkgname"

  # simplify paths
  sed -i \
    -e 's:^MANDIR.*:MANDIR  = $(PREFIX)/share/man/man1:' \
    -e 's:^LIBDIR.*:LIBDIR = $(PREFIX)/lib/lout:' \
    -e 's:^LOUTLIBDIR.*:LOUTLIBDIR = $(LIBDIR):' \
    makefile

  # TODO generate documentation
  #-e 's:^LOUTDOCDIR.*:LOUTDOCDIR = $(PREFIX)/share/doc/lout:' \
}

build() {
  cd "$pkgname"

  make PDF_COMPRESSION=1 ZLIB=-lz PREFIX=/usr all
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install installman
}

