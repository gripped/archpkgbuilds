# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: catwell <catwell@archlinux.us>

pkgname=lout
pkgver=3.43.2
pkgrel=1
pkgdesc='A lightweight document formatting system'
arch=(x86_64)
url='https://github.com/william8000/lout'
license=(GPL-3.0-or-later)
depends=(glibc zlib)
makedepends=(git)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('ea4e421c60e081f86c2a1c1b0731b024a53846b6979e81d79b285efba798b831853b01b33af12fc73218e3548b0e76565b21e072cc6edc03d9f079c335d4b680')
b2sums=('d6074f2493325aee13357eb126878f003867b4b7de5226c216a08766e6b9cc8a43525aefa199825acf63876fac6e18d85ce3f423ca0164211dbb824a6de31f0b')

prepare() {
  cd "$pkgname"

  # simplify paths
  sed -i \
    -e 's:^MANDIR.*:MANDIR  = $(PREFIX)/share/man/man1:' \
    -e 's:^LIBDIR.*:LIBDIR = $(PREFIX)/lib/lout:' \
    -e 's:^LOUTLIBDIR.*:LOUTLIBDIR = $(LIBDIR):' \
    makefile

  # TODO generate documentation
  #-e 's:^LOUTDOCDIR.*:LOUTDOCDIR = $(PREFIX)/share/doc/lout:' \
}

build() {
  cd "$pkgname"

  make PDF_COMPRESSION=1 ZLIB=-lz PREFIX=/usr all
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install installman
}

