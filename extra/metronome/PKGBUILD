# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=metronome
pkgver=1.3.0
pkgrel=2
pkgdesc='Beats the rhythm for you to keep the tempo'
arch=(x86_64)
url='https://gitlab.gnome.org/World/metronome'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=(
  "git+https://gitlab.gnome.org/World/metronome.git#tag=$pkgver"
  metronome-dbus-activatable.patch
)
b2sums=(
  1208bdd46f27dee7220ffec0ac73b86c3e0b90c0db8a33e01c4eb3032da2f98bc1e0113c563100236cc95e631d007c37d37ca100ccd3ac2dd0a9fbe07e00cb84
  2677c07f55319d25448ce5b446dbc33f2414d7e4379e933d8ec514d6efba510c56938d22ac7305b0c133060470f4bd483d1b3d9a26540b54cf3b1dfa47838b1d
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/metronome/-/merge_requests/62
  git apply -3 ../metronome-dbus-activatable.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
