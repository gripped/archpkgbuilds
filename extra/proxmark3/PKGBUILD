# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=proxmark3
pkgver=4.19552
pkgrel=2
pkgdesc='Software for the the Proxmark3, an RFID swiss-army tool'
arch=('x86_64')
url='https://github.com/RfidResearchGroup/proxmark3'
license=('GPL2')
depends=('qt5-base' 'bluez-libs' 'jansson')
makedepends=('arm-none-eabi-gcc' 'arm-none-eabi-newlib' 'lua52' 'python')
optdepends=('lua52: lua scripts'
            'python: some scripts')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e17fc317a6a057b082a09b0bf21ac74b74aa18278ad6f8935c48415836bb9b199c6c5e884b3352992f2abdb197bde5ac207ea3cec08a37e7465686c89ecc5105')

prepare() {
  cp -r $pkgname-$pkgver{,-rdv4}
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3GENERIC SKIPQT=0 DONT_BUILD_NATIVE=y all

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 DONT_BUILD_NATIVE=y common bootrom recovery fullimage
}

package() {
  export PREFIX=/usr
  export DESTDIR="$pkgdir"
  export UDEV_PREFIX=/usr/lib/udev/rules.d
  export INSTALLTOOLSRELPATH=bin

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 {bootrom,recovery,fullimage}/install

  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3GENERIC install
}
