# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=6.5.26
pkgrel=1
pkgdesc='Simple editor for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'dtk6declarative' 'gcc-libs'
         'icu' 'qt6-5compat' 'qt6-base' 'qt6-svg' 'kcodecs' 'syntax-highlighting' 'libchardet'
         'uchardet')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja' 'qt6-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-editor.git#tag=$pkgver")
sha512sums=('9cdbcb5fe2259d0639a69495c88ca169855c24991c7c97e148af4a26fcb3a250377278e4fe95df65c0d0607f73edc5cdce5163bbf2506d3efe3d322b1b1209f6')

build() {
  cd deepin-editor
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DVERSION=$pkgver .
  ninja
}

check() {
  cd deepin-editor
  # Tests not available unless in debug mode
  #ninja test || echo "Tests failed"
}

package() {
  cd deepin-editor
  DESTDIR="$pkgdir" ninja install
}
