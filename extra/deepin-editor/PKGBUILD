# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=6.5.14
pkgrel=1
pkgdesc='Simple editor for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'dtk6declarative' 'deepin-qt-dbus-factory' 'gcc-libs'
         'icu' 'qt6-5compat' 'qt6-base' 'qt6-svg' 'kcodecs' 'syntax-highlighting' 'libchardet'
         'uchardet')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja' 'qt6-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-editor.git#tag=$pkgver")
sha512sums=('d8181a2654be49726691310a26002ad20184940fabd0bdf8a63cd9def58cba26bf05e7c6d3e88fe00a0f8691707a5d17b993355d7db3be3e500914af0ce47d72')

prepare() {
  cd deepin-editor
  sed -e 's|CMAKE_CXX_STANDARD 11|CMAKE_CXX_STANDARD 17|' -i CMakeLists.txt # Fix build with ICU 75
}

build() {
  cd deepin-editor
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DVERSION=$pkgver .
  ninja
}

check() {
  cd deepin-editor
  # Tests not available unless in debug mode
  #ninja test || echo "Tests failed"
}

package() {
  cd deepin-editor
  DESTDIR="$pkgdir" ninja install
}
