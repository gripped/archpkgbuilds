# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=6.5.34
pkgrel=1
pkgdesc='Simple editor for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'dtk6declarative' 'gcc-libs'
         'icu' 'qt6-5compat' 'qt6-base' 'qt6-svg' 'kcodecs' 'syntax-highlighting' 'libchardet'
         'uchardet')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja' 'qt6-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-editor.git#tag=$pkgver")
sha512sums=('1c0ebbec17f36bb5daeb22d97fe8d58d0f85c4cbc0b45dc434a444332851bf02a2cadc75ca4a72deed20e0bd29b87f7d6c199da160578bbba750a7b720e7429d')

prepare() {
  cd deepin-editor
  # https://github.com/linuxdeepin/deepin-editor/pull/406
  git cherry-pick -n ec3518a3585ee5e142c464d26dc400399e7ba24a
}

build() {
  cd deepin-editor
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DVERSION=$pkgver .
  ninja
}

check() {
  cd deepin-editor
  # Tests not available unless in debug mode
  #ninja test || echo "Tests failed"
}

package() {
  cd deepin-editor
  DESTDIR="$pkgdir" ninja install
}
