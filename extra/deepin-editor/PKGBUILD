# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=6.5.31
pkgrel=2
pkgdesc='Simple editor for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'dtk6declarative' 'gcc-libs'
         'icu' 'qt6-5compat' 'qt6-base' 'qt6-svg' 'kcodecs' 'syntax-highlighting' 'libchardet'
         'uchardet')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja' 'qt6-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-editor.git#tag=$pkgver")
sha512sums=('ab1a93aa7c22b569314f8fae7451895a6ddac9141cfd2a0342f8427a494ad4c3fd2d982a89627d91b6ed4fbb98d3fbe4ba316aeebbbfcb55627c14fd43656551')

build() {
  cd deepin-editor
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DVERSION=$pkgver .
  ninja
}

check() {
  cd deepin-editor
  # Tests not available unless in debug mode
  #ninja test || echo "Tests failed"
}

package() {
  cd deepin-editor
  DESTDIR="$pkgdir" ninja install
}
