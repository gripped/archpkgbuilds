# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Philipp A. <flying-sheep@web.de>

pkgname=cargo-generate
pkgver=0.23.2
pkgrel=1
pkgdesc="Use pre-existing git repositories as templates"
arch=('x86_64')
url="https://github.com/cargo-generate/cargo-generate"
license=('MIT' 'Apache-2.0')
depends=('cargo' 'curl' 'libgit2' 'libssh2' 'openssl')
makedepends=('cargo-edit')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('a9185d84940bcdd2546eba5513e24be9ae36e88e9dfaea7590fc6894dc316d3e')

prepare() {
  cd "$pkgname-$pkgver"
  # Backport crate bump so we can build against libgit2-1.9
  # c.f. https://github.com/cargo-generate/cargo-generate/commit/be2237177ee7ae996e2991189b07a5d211cd0d01
  cargo upgrade -p git2@0.20.0
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=" -ffat-lto-objects"
  export LIBGIT2_NO_VENDOR=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
  cargo build --release --frozen --no-default-features
}

check() {
  cd "$pkgname-$pkgver"
  export LIBGIT2_NO_VENDOR=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
  cargo test --frozen --lib --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  depends+=('libgit2.so')
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
