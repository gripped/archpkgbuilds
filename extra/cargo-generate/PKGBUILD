# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Philipp A. <flying-sheep@web.de>

pkgname=cargo-generate
pkgver=0.23.7
pkgrel=2
pkgdesc="Use pre-existing git repositories as templates"
arch=('x86_64')
url="https://github.com/cargo-generate/cargo-generate"
license=('MIT' 'Apache-2.0')
depends=('cargo' 'curl' 'libgit2' 'libssh2' 'openssl')
makedepends=('cargo-edit')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('7f6dceb0ede61122684d4852092331c7179a5ea236247688e5074be68c1d9e4d')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=" -ffat-lto-objects"
  export LIBGIT2_NO_VENDOR=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
  cargo build --release --frozen --no-default-features
}

check() {
  cd "$pkgname-$pkgver"
  export LIBGIT2_NO_VENDOR=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
  cargo test --frozen --lib --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  depends+=('libgit2.so')
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
