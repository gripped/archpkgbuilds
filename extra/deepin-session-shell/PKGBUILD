# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-shell
pkgver=6.0.25
pkgrel=1
pkgdesc='Deepin desktop-environment - session-shell module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-shell"
license=('GPL3')
# qt5-tools: for qdbus executable
# startdde: org.deepin.dde.Display1
# deepin-session: org.deepin.dde.Session1
depends=('deepin-daemon' 'deepin-network-core' 'deepin-qt5integration' 'deepin-wallpapers'
         'gsettings-qt' 'liblightdm-qt5' 'lightdm' 'qt5-tools' 'startdde' 'kwayland5'
         'deepin-pw-check' 'dtkcore' 'dtkgui' 'dtkwidget' 'libx11' 'libxcursor'
         'qt5-svg' 'qt5-base' 'libxi' 'dconf' 'qt5-base' 'libxtst' 'glibc' 'libxfixes' 'pam'
         'gcc-libs' 'libxrandr' 'libxcb' 'qt5-x11extras' 'deepin-session')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja')
conflicts=('deepin-session-ui<5')
provides=('lightdm-deepin-greeter')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-session-shell.git#tag=$pkgver")
sha512sums=('09c2ea21dc82a413895c237ff74cfb999bf2bcede92ff1fd6999c884f42e1e153cded7a5558352643463af574f053bc67afd6fe4c9bb3e7e59ed1673ea224fdb')

prepare() {
  cd dde-session-shell
  # We don't have common-auth on Arch
  sed -i 's/common-auth/system-login/' files/pam.d/dde-lock
}

build() {
  cd dde-session-shell
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-session-shell
  DESTDIR="$pkgdir" ninja install

  chmod +x "$pkgdir"/usr/bin/deepin-greeter
}
