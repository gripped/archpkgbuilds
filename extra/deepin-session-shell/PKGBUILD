# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-shell
pkgver=6.0.9
pkgrel=2
pkgdesc='Deepin desktop-environment - session-shell module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-shell"
license=('GPL3')
# qt5-tools: for qdbus executable
depends=('deepin-daemon' 'deepin-network-core' 'deepin-qt5integration' 'deepin-wallpapers'
         'gsettings-qt' 'liblightdm-qt5' 'lightdm' 'qt5-tools' 'startdde' 'kwayland5'
         'deepin-pw-check' 'dtkcore' 'dtkgui' 'dtkwidget' 'libx11' 'libxcursor'
         'qt5-svg' 'qt5-base' 'libxi' 'dconf' 'qt5-base' 'libxtst' 'glibc' 'libxfixes' 'pam'
         'gcc-libs' 'libxrandr' 'libxcb' 'qt5-x11extras')
makedepends=('cmake' 'gmock' 'gtest' 'ninja')
conflicts=('deepin-session-ui<5')
provides=('lightdm-deepin-greeter')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-session-shell/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('601fb34a5e581c4b7e56135931db064f5bd0e5c77432ff15aa8ed2884a88a78638ce41f630785360b62b131c6a39c2a960b8cd77a6b714f9e579bef1290eab55')

prepare() {
  cd dde-session-shell-$pkgver
  # We don't have common-auth on Arch
  sed -i 's/common-auth/system-login/' src/libdde-auth/deepinauthframework.cpp
  sed -i 's|qdbusxml2cpp-fix|/usr/lib/dtk5/DCore/bin/qdbusxml2cpp-fix|' CMakeLists.txt
}

build() {
  cd dde-session-shell-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-session-shell-$pkgver
  DESTDIR="$pkgdir" ninja install

  chmod +x "$pkgdir"/usr/bin/deepin-greeter
}
