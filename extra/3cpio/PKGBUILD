# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=3cpio
pkgver=0.3.2
pkgrel=1
pkgdesc='Manage Linux kernel initramfs cpio files written in Rust'
arch=('x86_64')
url='https://github.com/bdrung/3cpio'
license=('ISC')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust' 'bzip2' 'gzip' 'lz4' 'lzop' 'xz' 'zstd')
optdepends=('bzip2: Support for bzip2 compressed initramfs image'
            'lz4: Support for lz4 compressed initramfs image'
            'lzop: Support for lzo compressed initramfs image'
            'xz: Support for lzma or xz compressed initramfs image'
            'zstd: Support for zstd compressed initramfs image'
)
source=(git+https://github.com/bdrung/3cpio.git#tag=$pkgver?signed)
validpgpkeys=('A62D2CFBD50B9B5BF360D54B159EB5C4EFC8774C' # Benjamin Drung bdrung@debian.de
)
b2sums=('9158ab77e8634393095ec96741c91bd9bf3e1bb10c1cdb539c7e45314e5bd248a2b3e9469fb3e86d6f6b1b1e4dcaf0ef9b6c9bdc40dab5f6382a834dc90a75e8')

build() {
  cd $pkgname
  cargo build --release --locked
}

check() {
  cd $pkgname
  cargo test --release --locked
}

package() {
  cd $pkgname
  install -Dm755 "target/release/3cpio" "$pkgdir/usr/bin/3cpio"
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
