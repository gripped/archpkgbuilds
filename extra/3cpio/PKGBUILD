# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=3cpio
pkgver=0.13.0
#_commit=213416e787f147c2c5066ef45b83fa58aa0d30db
pkgrel=2
pkgdesc='Manage Linux kernel initramfs cpio files written in Rust'
arch=('x86_64')
url='https://github.com/bdrung/3cpio'
license=('ISC')
depends=(
  glibc
  gcc-libs
)
makedepends=(
  asciidoctor
  git
  rust
)
checkdepends=(
  bzip2 
  pigz 
  lz4
  lzop 
  xz 
  zstd
)
optdepends=(
  'bzip2: Support for bzip2 compressed initramfs image'
  'lz4: Support for lz4 compressed initramfs image'
  'lzop: Support for lzo compressed initramfs image'
  'pigz: Support for gzip compressed initramfs image'
  'xz: Support for lzma or xz compressed initramfs image'
  'zstd: Support for zstd compressed initramfs image'
)
source=(
  git+https://github.com/bdrung/3cpio.git#tag=${pkgver}
)
validpgpkeys=(
  A62D2CFBD50B9B5BF360D54B159EB5C4EFC8774C # Benjamin Drung bdrung@debian.de
)
b2sums=('c5c956ae474538e833538250a1c3ed80313b66a6ea99db2d572c4bc926a06b6505dfda66ae78b4c82b60b84c910d07996b1f6e65a1731d99129e7fc75450e22b')

build() {
  cd "${pkgname}"
  cargo build --release --locked
  asciidoctor -b manpage man/3cpio.1.adoc
}

check() {
  cd "${pkgname}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}"
  install -Dm 644 man/3cpio.1 "${pkgdir}/usr/share/man/man1/3cpio.1"
  install -Dm755 target/release/3cpio "${pkgdir}/usr/bin/3cpio"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

