# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=3cpio
pkgver=0.4.0
_commit=213416e787f147c2c5066ef45b83fa58aa0d30db
pkgrel=1
pkgdesc='Manage Linux kernel initramfs cpio files written in Rust'
arch=('x86_64')
url='https://github.com/bdrung/3cpio'
license=('ISC')
depends=(
  glibc
  gcc-libs
)
makedepends=(
  git
  rust
)
checkdepends=(
  bzip2 
  gzip 
  lz4
  lzop 
  xz 
  zstd
)
optdepends=(
  'bzip2: Support for bzip2 compressed initramfs image'
  'lz4: Support for lz4 compressed initramfs image'
  'lzop: Support for lzo compressed initramfs image'
  'xz: Support for lzma or xz compressed initramfs image'
  'zstd: Support for zstd compressed initramfs image'
)
source=(
  git+https://github.com/bdrung/3cpio.git#commit=${_commit}?signed
)
validpgpkeys=(
  A62D2CFBD50B9B5BF360D54B159EB5C4EFC8774C # Benjamin Drung bdrung@debian.de
)
b2sums=('fb22dbe2afa0783c10c1ff23031dcf081ee6bb7b5500c506451f0894b8e7dcbaacb9e0b32bce10dac983477076ae587c25e7c0a1f63724171b416174775e3071')

build() {
  cd "${pkgname}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}"
  install -Dm755 target/release/3cpio "${pkgdir}/usr/bin/3cpio"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

