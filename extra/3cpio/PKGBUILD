# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=3cpio
pkgver=0.5.1
#_commit=213416e787f147c2c5066ef45b83fa58aa0d30db
pkgrel=1
pkgdesc='Manage Linux kernel initramfs cpio files written in Rust'
arch=('x86_64')
url='https://github.com/bdrung/3cpio'
license=('ISC')
depends=(
  glibc
  gcc-libs
)
makedepends=(
  git
  rust
)
checkdepends=(
  bzip2 
  gzip 
  lz4
  lzop 
  xz 
  zstd
)
optdepends=(
  'bzip2: Support for bzip2 compressed initramfs image'
  'lz4: Support for lz4 compressed initramfs image'
  'lzop: Support for lzo compressed initramfs image'
  'xz: Support for lzma or xz compressed initramfs image'
  'zstd: Support for zstd compressed initramfs image'
)
source=(
  git+https://github.com/bdrung/3cpio.git#tag=${pkgver}?signed
)
validpgpkeys=(
  A62D2CFBD50B9B5BF360D54B159EB5C4EFC8774C # Benjamin Drung bdrung@debian.de
)
b2sums=('92529c2137535f10fa50e8e232a62aafaa2bc7f5a875d5117afefac986061875f29831d2fe77536796b3862ed73379cac8259f7c5c7d4280bbb004d25d3594be')

build() {
  cd "${pkgname}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}"
  install -Dm755 target/release/3cpio "${pkgdir}/usr/bin/3cpio"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

