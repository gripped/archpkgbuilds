# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-color-manager
pkgver=3.36.0+r74+g19b6f67d
pkgrel=1
pkgdesc="GNOME Color Profile Tools"
url="https://gitlab.gnome.org/GNOME/gnome-color-manager"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  colord
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  lcms2
  libcolord
  pango
)
makedepends=(
  appstream-glib
  docbook-sgml
  docbook-utils
  git
  meson
  perl-sgmls
  yelp-tools
)
checkdepends=(xorg-server-xvfb)
groups=(gnome)
_commit=19b6f67dcd5f937747524d7b742c58edebb1d9a2  # master
source=("git+$url.git#commit=$_commit")
b2sums=('a3e8bb6ec44c7680584c4346c0f0725705cb701b3c40930b1f5dbcdc7583d40d12e06a78b15de8a177099fa90d62ccaaf377f76157b7080c2325a1bd2a71ae7d')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_COLOR_MANAGER_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
