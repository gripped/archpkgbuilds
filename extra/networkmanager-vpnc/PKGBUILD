# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-vpnc
pkgver=1.4.0
pkgrel=1
pkgdesc="NetworkManager VPN plugin for VPNC"
url="https://networkmanager.dev/docs/vpn/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libnm
  libsecret
  vpnc
)
makedepends=(
  git
  libnma
  libnma-gtk4
  python
)
optdepends=(
  "libnma-gtk4: GUI support (GTK 4)"
  "libnma: GUI support (GTK 3)"
)
source=("git+https://gitlab.gnome.org/GNOME/NetworkManager-vpnc.git?signed#tag=$pkgver")
b2sums=('fd745613d8cddb67f70783465c03192656231ce3ae1beea36e397a7af3cecf9f3d60e87e280a43d91a45c0e59a46e608ec5987cd51d6c93001269d14c5661d4e')
validpgpkeys=(
  4B8EF9745A973724E965939189A2DA5AF73D5E3D # Lubomir Rintel <lkundrak@v3.sk>
)

prepare() {
  cd NetworkManager-vpnc
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --disable-static
    --with-gtk4
  )

  cd NetworkManager-vpnc
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd NetworkManager-vpnc
  make DESTDIR="$pkgdir" install dbusservicedir=/usr/share/dbus-1/system.d
}

# vim:set sw=2 sts=-1 et:
