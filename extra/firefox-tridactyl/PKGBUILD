# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-tridactyl
pkgver=1.24.2
pkgrel=1
pkgdesc="Replace Firefox's control mechanism with one modelled on Vim"
arch=(any)
url=https://github.com/tridactyl/tridactyl
license=(Apache-2.0)
groups=(firefox-addons)
makedepends=(
  git
  node-gyp
  python
  strip-nondeterminism
  yarn
)
source=("git+$url.git#commit=$pkgver?signed")
b2sums=('bcc1303586e3e78c095712b655fb8f3b9e6397697f7d2e2bd1aea4b6ddf05d1aa6392c47907ad85a81915f50e109f2a98636ace1a86fc5382412b4fe1093b8e6')
validpgpkeys=(15D03C86837936A0A638AA2D2BB8C36BB504BFF3) # Oliver Blanthorn <freedom4cows@gmail.com>

prepare() {
  cd tridactyl
  yarn install --frozen-lockfile --non-interactive
}

build() {
  cd tridactyl
  scripts/sign nosignstable
  strip-nondeterminism -t zip web-ext-artifacts/tridactyl-$pkgver.xpi
}

package() {
  cd tridactyl
  install -Dm644 web-ext-artifacts/tridactyl-$pkgver.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/tridactyl.vim@cmcaine.co.uk.xpi
}
