# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-tridactyl
pkgver=1.24.3
pkgrel=1
pkgdesc="Replace Firefox's control mechanism with one modelled on Vim"
arch=(any)
url=https://github.com/tridactyl/tridactyl
license=(Apache-2.0)
groups=(firefox-addons)
makedepends=(
  git
  node-gyp
  python
  strip-nondeterminism
  yarn
)
source=("git+$url.git#commit=$pkgver?signed")
b2sums=('e14c44ceeb79cbe47a3f1c8d581af41437d7c550c1e109a206800257e423c60dd6a8300faee835defe10407bc47eb3b30c86faa13b00972ac638e9ae7a8c77c0')
validpgpkeys=(15D03C86837936A0A638AA2D2BB8C36BB504BFF3) # Oliver Blanthorn <freedom4cows@gmail.com>

prepare() {
  cd tridactyl
  yarn install --frozen-lockfile --non-interactive
}

build() {
  cd tridactyl
  scripts/sign nosignstable
  strip-nondeterminism -t zip web-ext-artifacts/tridactyl-$pkgver.xpi
}

package() {
  cd tridactyl
  install -Dm644 web-ext-artifacts/tridactyl-$pkgver.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/tridactyl.vim@cmcaine.co.uk.xpi
}
