# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.12.0
pkgrel=2
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('x86_64')
license=(LGPL-3.0-only)
depends=(
  gcc-libs
  glibc
  libmediainfo
  qt6-base
  qt6-multimedia
  qt6-svg
  quazip-qt6
)
makedepends=(
  cmake
  ninja
  qt6-tools
  qt6-declarative
)
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ffa6f19dbc7760cb3762d02c24392d5e2015dfa8890fc3c69d172b0930dbbb89')
b2sums=('6847290c64eeba74cdf2d60e8cc2ccb0c819bdee11aaaf7477a914f5ca1526dd303be50593191b77d6130bbe3b0f07d54c9f4c5d9e848628574bcfc9fb850af1')

build() {
  cd MediaElch-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DDISABLE_UPDATER=ON \
    -DUSE_EXTERN_QUAZIP=ON \
    -DMEDIAELCH_FORCE_QT6=ON \
    -DENABLE_TESTS=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd MediaElch-${pkgver}
  DESTDIR="${pkgdir}" cmake --install build
}

# vim: ts=2 sw=2 et:
