# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=bcprov
_pkgname=bc-java
pkgver=1.74
pkgrel=1
pkgdesc='Bouncy Castle Crypto APIs for Java'
arch=(any)
url='https://www.bouncycastle.org/java.html'
_url='https://github.com/bcgit/bc-java'
license=(MIT)
depends=(java-runtime-headless)
makedepends=(ant
             strip-nondeterminism)
_tag=${pkgver//./v}; _tag=r${_tag/v/rv}
_archive="$_pkgname-$_tag"
source=("$_url/archive/$_tag/$_archive.tar.gz")
sha512sums=('887183f825a8834ff512a66fa3f9075259b3266c2861e6e046990bd2dda80b896a8fd221ff2982a127de79fe998727ecc12cade4e6c381f2a152a72ee575d8a1')

build() {
	cd "$_archive"
	ant -f ant/jdk18+.xml clean build-provider build
	# Timestamps in JAR files generated by Ant do not honour SOURCE_DATE_EPOCH
	# (https://bz.apache.org/bugzilla/show_bug.cgi?id=61269)
	strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" build/artifacts/jdk1.8/jars/bcprov-jdk18on-*.jar
}

check() {
	cd "$_archive"
	ant -f ant/jdk18+.xml test
}

package() {
	cd "$_archive"
	install -Dm644 -t "$pkgdir/usr/share/java/$pkgname" build/artifacts/jdk1.8/jars/bcprov-jdk18on-*.jar
	ln -s bcprov-jdk18on-*.jar "$pkgdir/usr/share/java/$pkgname/bcprov.jar"
	install -Dm644 LICENSE.html -t "$pkgdir/usr/share/licenses/$pkgname"
}
