# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=bcprov
_pkgname=bc-java
pkgver=1.83
pkgrel=1
pkgdesc='Bouncy Castle Crypto APIs for Java'
arch=(any)
url='https://www.bouncycastle.org/java.html'
_url='https://github.com/bcgit/bc-java'
license=(MIT)
depends=(java-runtime-headless)
makedepends=(ant
             strip-nondeterminism)
_tag=${pkgver//./v}; _tag=r${_tag/v/rv}
_archive="$_pkgname-$_tag"
source=("$_url/archive/$_tag/$_archive.tar.gz")
sha512sums=('f9d99538c38a65ef6cb3db0eda3a293b91e370d27210e71ef17e63ecd91fd3c5b01d09385c18fc237e686775649306b9a2fe1d8250a0a304fb0d6079f74e4926')

build() {
	cd "$_archive"
	ant -f ant/jdk18+.xml clean build-provider build
	# Timestamps in JAR files generated by Ant do not honour SOURCE_DATE_EPOCH
	# (https://bz.apache.org/bugzilla/show_bug.cgi?id=61269)
	strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" build/artifacts/jdk1.8/jars/bcprov-jdk18on-*.jar
}

# Disabling tests after 1.74. Between 1.74 and 1.75 the test suite itself got
# fixed to actually return an error at the end if tests failed. It turns out it
# has been silently failing all tests for the last several versions anyway, but
# since the suite returned a success at the end we never noticed. The end
# result is a *working* package that does what we need it to for pdftk, so that
# tells me we're just calling the test suite wrong or it wasn't made for this
# use case.
# check() {
# 	cd "$_archive"
# 	ant -f ant/jdk18+.xml test
# }

package() {
	cd "$_archive"
	install -Dm644 -t "$pkgdir/usr/share/java/$pkgname/" build/artifacts/jdk1.8/jars/bcprov-jdk18on-*.jar
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.html
	pushd "$pkgdir/usr/share/java/$pkgname/"
	ln -s bcprov-jdk18on-*.jar bcprov.jar
}
