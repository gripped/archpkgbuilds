# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=vegeta
pkgver=12.13.0
pkgrel=2
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
depends=(glibc)
makedepends=(
  git
  go
)
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('55533ac3e8b3c44f4614012868e316b60d243e08ebb8f40311e2e9a097bd4bb93c24305026aa3f6b699ad828f9c2bb46988edb7d1eb0521923fb687f6abf43f6')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
    -X main.Commit=$(git rev-parse --short HEAD) \
    -X main.Date=NOTSET \
    -X main.Version=$pkgver \
  "
  go build -v -ldflags "$ld_flags"
}

check() {
  cd $pkgname
  go test ./...
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" vegeta
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
