# Maintainer: Anatol Pomozov

pkgname=vegeta
_commit=65f8e702df68da58ec3bc0fa8eb09462dbe5b56f
pkgver=12.9.0
pkgrel=1
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
depends=(glibc)
makedepends=(git go)
source=(git+https://github.com/tsenart/vegeta.git?signed#tag=$_commit)
sha512sums=('SKIP')
validpgpkeys=(00E6D947A1BCE5A2A37D48AB82D93F7867A67C19)

build() {
  cd vegeta
  BUILDDATE=`date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +'%FT%TZ%z'`
  go build -ldflags "-X main.Version=$pkgver -X main.Date='$BUILDDATE'"
}

check() {
  cd vegeta
  go test ./... || true # tests fail https://github.com/tsenart/vegeta/issues/545
}

package() {
  install -Dm755 vegeta/vegeta "$pkgdir/usr/bin/vegeta"
}
