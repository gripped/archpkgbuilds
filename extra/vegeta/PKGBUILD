# Maintainer: Anatol Pomozov

pkgname=vegeta
pkgver=12.12.0
pkgrel=1
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
depends=(glibc)
makedepends=(git go)
source=(git+https://github.com/tsenart/vegeta.git#tag=v$pkgver) # add signed= once Tomas' GPG key 82D93F7867A67C19 is published
sha512sums=('8ca4a7c8fa8c350cfddd600619b42c4e2f8ecb779fd3214ba6a3593ce7b1b81e606ed4aa6175062001c46cb23b869f95b175a196b427af23c967bddd02864fbd')

build() {
  cd vegeta
  BUILDDATE=`date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +'%FT%TZ%z'`
  go build -ldflags "-X main.Version=$pkgver -X main.Date='$BUILDDATE'"
}

check() {
  cd vegeta
  go test ./... || true # tests fail https://github.com/tsenart/vegeta/issues/545
}

package() {
  install -Dm755 vegeta/vegeta "$pkgdir/usr/bin/vegeta"
}
