# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-hstspreload
pkgver=2026.3.1
pkgrel=1
pkgdesc='Chromium HSTS Preload list as a Python package'
arch=(any)
url='https://github.com/sethmlarson/hstspreload'
license=(BSD-3-Clause)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
)
checkdepends=(
  python-pytest
  python-urllib3
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('693d12bd33c39834ab3f8316efad795f945d48c3fafb580948c14dda1362f8ba95e0c58a10c92552842f85b54457d0c42ca5f471b78d89419681edc362f058b3')
b2sums=('ebb9fe21a3124048c3711fdbd38a14fc6e150ff431296394999254ce6efa91f0d3f05bee8201f8d3b3048d75c9a6791a691ebe62ea29953159ecf3ef0d999cc0')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

# This runs around 230k tests, checking to see if the online list matches the current one.
# It can take 5 minutes just to collect the tests. They're not distributed in the PyPI tarball.
#check() {
#  cd "${srcdir}"/${_pkg}-${pkgver}
#
#  python -m pytest
#}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
