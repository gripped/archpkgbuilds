# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=pplpy
pkgname=python-pplpy
pkgver=0.8.8
pkgrel=2
pkgdesc='Python wrapper to the C++ Parma Polyhedra Library (PPL)'
arch=(x86_64)
url='https://gitlab.com/videlec/pplpy/'
license=(LGPL2.1)
depends=(python-cysignals python-gmpy2 ppl)
makedepends=(cython0 python-build python-installer python-setuptools python-wheel python-sphinx)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz
        fcd3a155.patch)
sha256sums=('eaa50927321fb6c5eb8283262c38f4e9ace26263baf06651deebc271031ef031'
            '0005bc7f60ca0ac9fe5dcfe837e06d2372e3ff18f89b20eefe5e0b379a8b6700')

prepare() {
  patch -d $_pipname-$pkgver -p1 < fcd3a155.patch # Fix regressions in Sagemath
}

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation

  _pythonver=`python -c "from sysconfig import get_python_version; print(get_python_version())"`
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-${_pythonver/./}" make -C docs html
}

package() {
  cd $_pipname-$pkgver 
  python -m installer --destdir="$pkgdir" dist/*.whl

  mkdir -p "$pkgdir"/usr/share/doc/pplpy
  cp -r docs/build/html/* "$pkgdir"/usr/share/doc/pplpy
}
