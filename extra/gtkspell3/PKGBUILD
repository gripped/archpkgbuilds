# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gtkspell3
pkgver=3.0.10
pkgrel=3
pkgdesc='Provides word-processor-style highlighting and replacement of misspelled words in a GtkTextView widget'
arch=(x86_64)
url='https://gtkspell.sourceforge.net/'
license=(GPL-2.0-or-later)
depends=(
  enchant
  glib2
  glibc
  gtk3
)
makedepends=(
  git
  gobject-introspection
  intltool
  vala
)
source=("git+https://git.code.sf.net/p/gtkspell/gtkspell#tag=gtkspell_${pkgver//./_}")
b2sums=(3c00123cad134b9dcfd41c8fe54d786aa57fc9ed2643363e6775cfddc3310d42266144a64ad901ced5cb6d62fdcb175749888ec4e17bc52fd881667b8feba81e)

prepare() {
  cd gtkspell
  autoreconf -fi
}

build() {
  cd gtkspell
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd gtkspell
  make DESTDIR="$pkgdir" install
}
