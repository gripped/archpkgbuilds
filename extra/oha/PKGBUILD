# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=oha
pkgver=1.11.0
pkgrel=1
pkgdesc="Ohayou(おはよう), HTTP load generator, inspired by rakyll/hey with tui animation"
arch=('x86_64')
url="https://github.com/hatoo/oha"
license=('MIT')
depends=('openssl' 'gcc-libs' 'glibc')
makedepends=('rust')
options=('!lto')
source=($pkgname-$pkgver.tar.gz::https://github.com/hatoo/oha/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('5507ea38c27b87e2589d0260c27a444d7e0f6f0fd406b07eda1ec5376d2ed0c567e23977069a8c071d012ec14b7ffafd7edf94f06170aebb8b7a8b0d3dfccabf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo test --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/oha "$pkgdir"/usr/bin/oha
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
