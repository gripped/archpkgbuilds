# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: M0Rf30

pkgname=clonezilla
pkgver=5.13.5
pkgrel=1
pkgdesc="ncurses partition and disk imaging/cloning program"
arch=('x86_64' 'aarch64')
url="https://clonezilla.org"
license=('GPL-2.0-only')
depends=(
        'drbl'
        'partclone'
        'ntfs-3g'
        'partimage'
        'pigz'
        'sshfs'
        'parted'
        'gptfdisk'
        'dosfstools'
        'gzip'
        'bzip2'
        'pbzip2'
        'lbzip2'
        'lrzip'
        'xz'
        'pixz'
        'lzop'
        'ecryptfs-utils'
        'screen'
        'perl'
        'cifs-utils'
        'openbsd-netcat'
        'net-tools'
        'which'
)
makedepends=('git')
backup=('etc/drbl/drbl-ocs.conf')
source=(
        "git+https://github.com/stevenshiau/clonezilla.git#tag=v${pkgver}"
        usrbin.patch
)
sha256sums=('135fa9058928e13c9ac69833771d4ed9a4571f62af828aaf606963b127bf2de5'
            '9340f18b27a1b3b41c4935c5831f6503e4cadf333f03433769906da40a6e38e6')

prepare() {
  cd $pkgname

  patch -Np0 -i "$srcdir"/usrbin.patch
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install  
}
