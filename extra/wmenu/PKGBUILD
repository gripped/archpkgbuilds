# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: nullcoder

pkgname=wmenu
pkgver=0.1.8
pkgrel=1
pkgdesc="Efficient dynamic menu for Wayland and wlroots based Wayland compositors"
arch=('x86_64')
url="https://git.sr.ht/~adnano/wmenu"
license=('MIT')
makedepends=(
	'meson'
	'ninja'
	'scdoc'
	'wayland-protocols'
)
depends=(
	'libcairo.so'
	'libxkbcommon.so'
	'libpango-1.0.so'
	'libwayland-client.so'
)
source=(
	"$pkgname-$pkgver.tar.gz::https://git.sr.ht/~adnano/wmenu/archive/$pkgver.tar.gz"
)
b2sums=('3d08de95d9a85d2f8d28ccf2a0cd9fda4db93e9ff2cfa4198c40d6f0963020036a91c1e825dcf32ed3c7099f7d250653a55f50aca701acf835f42d527345c4af')

build() {
	cd "$pkgname-$pkgver"
	arch-meson build
	ninja -C build
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

