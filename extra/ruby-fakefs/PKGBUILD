# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=ruby-fakefs
_pkgname=${pkgname#ruby-}
pkgver=2.5.0
pkgrel=5
pkgdesc='A fake filesystem. Use it in your tests.'
arch=(any)
url='https://github.com/fakefs/fakefs'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  less
  ruby-bundler
  ruby-irb
  ruby-maxitest
  ruby-pry
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(
  "git+$url.git#tag=v$pkgver"
  # https://github.com/fakefs/fakefs/pull/496
  "https://github.com/fakefs/fakefs/pull/496/commits/19fd2e70bf79911caa720954fc631d01294f330d.patch"
)
sha256sums=('0b850305eb820775a9411c4fe0da1dcff3e6ad419f1bb8041e6d69c7df9c46e6'
            '707e8332660055bd21a524df979f6285c1a2058d54ab0011fbd82bcf2e34f847')

prepare() {
  cd "$_pkgname"

  patch --verbose --strip=1 --input="../19fd2e70bf79911caa720954fc631d01294f330d.patch"

  sed -r -e 's|~>|>=|' -i fakefs.gemspec
  # Remove dependency on bump and rubocop
  sed -r -e '/bump|rubocop/d' -i fakefs.gemspec
  sed -r -e '/bump\/tasks/d;/rubocop\/rake_task/,+1d;/namespace :bump/,+4d' -i Rakefile
}

build() {
  local gemdir="$(gem env gemdir)"
  cd "$_pkgname"
  gem build fakefs.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$gemdir" \
    --bindir "tmp_install/usr/bin" \
    fakefs-$pkgver.gem
  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r "tmp_install/$gemdir/cache"
}

check() {
  local gemdir="$(gem env gemdir)"
  cd "$_pkgname"
  GEM_HOME="tmp_install/$gemdir" rake test spec
}

package() {
  cd "$_pkgname"
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
