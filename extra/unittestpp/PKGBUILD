# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Jorge Araya Navarro <elcorreo@deshackra.com>
# Contributor: Piotr Beling <qwak@w8.pl>
# Contributor: Vianney le Clément <vleclement AT gmail · com>

pkgname=unittestpp
pkgver=2.0.0
pkgrel=5
pkgdesc='Lightweight unit testing framework for C++'
url='https://github.com/unittest-cpp/unittest-cpp/'
arch=('x86_64')
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/unittest-cpp/unittest-cpp/archive/v${pkgver}.tar.gz
        fix-pc-version.patch)
sha256sums=('74852198877dc2fdebdc4e5e9bd074018bf8ee03a13de139bfe41f4585b2f5b9'
            '6bb25aadc05f56d97d5dcbd061ad72089f2909f8389343bde050f1ac42f724ab')
sha512sums=('39318f4ed31534c116679a3257bf1438a6c4b3bef1894dfd40aea934950c6c8197af6a7f61539b8e9ddc67327c9388d7e8a6f8a3e0e966ad26c07554e2429cab'
            '0e11b4565308c2c174cbbe12ef3794cb0c877e2292b13acab4ebdb7045aba67571056941f69947e6ea2c6f01436f42fb90dcfe9493fd86573e0ec2359abc4741')

prepare() {
  mkdir -p unittest-cpp-${pkgver}/build

  patch -d unittest-cpp-${pkgver} -p1 < fix-pc-version.patch # Fix version number in pc file
  sed -e 's/-Werror//' -i unittest-cpp-${pkgver}/CMakeLists.txt # Evil -Werror
}

build() {
  cd unittest-cpp-${pkgver}/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  make all
}

package() {
  cd unittest-cpp-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
