# Maintainer: Laurent Carlier <lordheavy@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=hatari
pkgver=2.6.0
pkgrel=1
pkgdesc='An Atari ST and STE emulator'
arch=('x86_64')
url='https://www.hatari-emu.org/'
license=('GPL-2.0-or-later')
depends=(
  'glibc'
  'hicolor-icon-theme'
  'sdl2'
  'libpng'
  'libx11'
  'portmidi'
  'readline'
  'systemd-libs'
  'zlib'
)
makedepends=('cmake' 'systemd')
optdepends=(
  'python: for atari-convert-dir'
  'sh: for atari-hd-image, zip2st, hatari-prg-args'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('abb1e7ec9a689785cede60c050936df9f208c90a023f94662e18b79bdcf1a810')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
