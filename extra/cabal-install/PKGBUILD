# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=3.8.1.0
pkgrel=111
pkgdesc="The command-line interface for Cabal and Hackage."
url="https://hackage.haskell.org/package/cabal-install"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base16-bytestring' 'haskell-cabal-install-solver' 'haskell-cryptohash-sha256'
         'haskell-echo' 'haskell-edit-distance' 'haskell-hackage-security' 'haskell-hashable'
         'haskell-http' 'haskell-network-uri' 'haskell-random' 'haskell-regex-base'
         'haskell-regex-posix' 'haskell-resolv' 'haskell-safe-exceptions' 'haskell-tar' 'haskell-zlib' 'haskell-lukko')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz"
        $pkgname-key-holder-updates-8669.patch::https://github.com/haskell/cabal/pull/8669.patch
        $pkgname-key-holder-updates-9068.patch::https://github.com/haskell/cabal/pull/9068.patch
        $pkgname-key-holder-updates-11095.patch::https://github.com/haskell/cabal/pull/11095.patch)
sha512sums=('850267c8b255f1658beea2ff81d025e80f0a78902d32684a047fdc9347fe0893ccbdf60a00dbb5bb0339e3d1ddefb0f94e31273808c6b72de23fb1340fa74ae4'
            '74a5cf7edae309fb0da8c37c051d59fcde2db5d2322e6c2540633db2689e5b4bc27933740b54335873c14e631c16c86e8909f2bbd7fa33c9e72116d880ccfbe9'
            'd864ecc736f5abc10ff9e977a944802d6c217421e893e388f9e477e222086f96561a908cdec4cf550a2cdfeb9894cbad83670d95270b5c530e35645c8311ae23'
            '82140a875ba94d73c751be17a18b29a186004e0ec35eceddcb62caaf58ce5dcab812fa49c9b8cdef0be393dca3f7bf2696fdcb32a75b9c430d0a641ce5bded5e')

prepare() {
  cd $pkgname-$pkgver
  patch -p2 -i ../$pkgname-key-holder-updates-8669.patch
  patch -p2 -l -i ../$pkgname-key-holder-updates-9068.patch
  patch -p2 -l -i ../$pkgname-key-holder-updates-11095.patch
  uusi -u base -u text -u resolv -u time -u zlib
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname "${_opts[@]}" \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh

}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 bash-completion/cabal \
    "${pkgdir}/usr/share/bash-completion/completions/cabal"
}
