# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=cloudflared
pkgver=2025.9.1
pkgrel=1
pkgdesc='Command-line client for Cloudflare Tunnel'
arch=('x86_64')
url='https://github.com/cloudflare/cloudflared'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url.git#tag=$pkgver")
sha512sums=('31cf100f3695b0d3e33f21680b20d513d40863b91e34f0de4ec1cb30ac549fbd6db65d4c817784b72a425abd6cf944c236b168d0e8bd75f1ca38f0a064a99bf2')
b2sums=('6cc2cdf12286d38ad305fafebfa6254dc2e5a1a5d751ef0ac626e97aa0d4594a95336db55a67a8998ba7fcda6d287265084c5a0b99fb4a36a8c09252598e743e')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build
}

build() {
  cd "$pkgname"

  # define buildtime for use in man page & binary
  local build_time="$(date -d@"$SOURCE_DATE_EPOCH" +%Y%m%d-%H:%M:%S)"

  # generate man page
  sed -e "s/\${VERSION}/${pkgver}/" \
      -e "s/\${DATE}/${build_time}/" \
    cloudflared_man_template > "build/$pkgname.1"

  # verify go modules
  go mod verify

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # build cloudflared
  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
      -X main.Version=${pkgver} \
      -X main.BuildTime=${build_time} \
      -X github.com/cloudflare/cloudflared/cmd/cloudflared/updater.BuiltForPackageManager=pacman" \
    -o build \
    ./cmd/...
}

# tests hang ._.
#check() {
#  cd "$pkgname"
#
#  go test -v -mod=vendor -race ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" "build/$pkgname.1"
}

# vim:set ts=2 sw=2 et:
