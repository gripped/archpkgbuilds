# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=cloudflared
pkgver=2024.12.2
pkgrel=1
pkgdesc='Command-line client for Cloudflare Tunnel'
arch=('x86_64')
url='https://github.com/cloudflare/cloudflared'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url.git#tag=$pkgver")
sha512sums=('4ac7203663b500e50952313021826563849ba68f99c3620cc15b4fe1d1d9c648a8dbb1674207208e28cc57c320954d9d497bc9a332498157d968d36dbb713aa7')
b2sums=('1719f2f318bc87c30843889a98ea14c8735c29910ff55e17d419babdb8e872f202419bb9328e9085355fcd60e4bf6cfd05db8601d505a296b62f5ce65128706b')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build
}

build() {
  cd "$pkgname"

  # define buildtime for use in man page & binary
  local build_time="$(date -d@"$SOURCE_DATE_EPOCH" +%Y%m%d-%H:%M:%S)"

  # generate man page
  sed -e "s/\${VERSION}/${pkgver}/" \
      -e "s/\${DATE}/${build_time}/" \
    cloudflared_man_template > "build/$pkgname.1"

  # verify go modules
  go mod verify

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # build cloudflared
  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
      -X main.Version=${pkgver} \
      -X main.BuildTime=${build_time} \
      -X github.com/cloudflare/cloudflared/cmd/cloudflared/updater.BuiltForPackageManager=pacman" \
    -o build \
    ./cmd/...
}

# tests hang ._.
#check() {
#  cd "$pkgname"
#
#  go test -v -mod=vendor -race ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" "build/$pkgname.1"
}

# vim:set ts=2 sw=2 et:
