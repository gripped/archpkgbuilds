# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=cosmic-initial-setup
pkgver=1.0.0.beta.1
pkgrel=1
pkgdesc="COSMIC Initial Setup"
arch=(x86_64)
url="https://github.com/pop-os/cosmic-initial-setup"
license=(GPL-3.0-only)
depends=(
  cosmic-icon-theme
  libinput
  libpulse
  libxkbcommon
  polkit
  systemd-libs
)
makedepends=(
  cargo
  git
  git-lfs
  just
)
install="$pkgname.install"
_tag=55c6006001d981119cc20c15bb362d36afbfd0ba
source=("git+https://github.com/pop-os/cosmic-initial-setup.git#tag=${_tag}")
sha256sums=('60428b1a93f1ab4b28259be388cd65f4577a30589738d1a454f7e4a5954c1a03')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

prepare() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  git lfs install --local
  git remote add network-origin https://github.com/pop-os/cosmic-initial-setup
  git lfs fetch network-origin
  git lfs checkout
}

build() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  just build-release --frozen
}

package() {
  cd "${pkgname}"
  just rootdir="$pkgdir" install
}
