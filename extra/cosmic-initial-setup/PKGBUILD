# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=cosmic-initial-setup
pkgver=1.0.0.beta.1.1
pkgrel=1
pkgdesc="COSMIC Initial Setup"
arch=(x86_64)
url="https://github.com/pop-os/cosmic-initial-setup"
license=(GPL-3.0-only)
depends=(
  cosmic-icon-theme
  libinput
  libpulse
  libxkbcommon
  polkit
  systemd-libs
)
makedepends=(
  cargo
  git
  git-lfs
  just
)
install="$pkgname.install"
_tag=7d5c21cf1bd725a4264f53c990e5cc1b9da20985
source=("git+https://github.com/pop-os/cosmic-initial-setup.git#tag=${_tag}")
sha256sums=('eee10e0e06215250027c01ed52431c22f53a19b675e2036004518c88867f5a1b')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

prepare() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  git lfs install --local
  git remote add network-origin https://github.com/pop-os/cosmic-initial-setup
  git lfs fetch network-origin
  git lfs checkout
}

build() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  just build-release --frozen
}

package() {
  cd "${pkgname}"
  just rootdir="$pkgdir" install
}
