# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=cosmic-initial-setup
pkgver=1.0.0.beta.2
pkgrel=1
pkgdesc="COSMIC Initial Setup"
arch=(x86_64)
url="https://github.com/pop-os/cosmic-initial-setup"
license=(GPL-3.0-only)
depends=(
  cosmic-icon-theme
  libinput
  libpulse
  libxkbcommon
  polkit
  systemd-libs
)
makedepends=(
  cargo
  git
  git-lfs
  just
)
install="$pkgname.install"
_tag=0080330b6dacafccebc3aac74f3fccaf20f87f67
source=("git+https://github.com/pop-os/cosmic-initial-setup.git#tag=${_tag}")
sha256sums=('c44d68ddef3aff7138f299a301708c0ec03008bb018c08c91f1c31fde3cdea96')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

prepare() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  git lfs install --local
  git remote add network-origin https://github.com/pop-os/cosmic-initial-setup
  git lfs fetch network-origin
  git lfs checkout
}

build() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  just
}

package() {
  cd "${pkgname}"
  just rootdir="$pkgdir" install
}
