# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=cosmic-initial-setup
pkgver=1.0.0.beta.5
pkgrel=1
pkgdesc="COSMIC Initial Setup"
arch=(x86_64)
url="https://github.com/pop-os/cosmic-initial-setup"
license=(GPL-3.0-only)
depends=(
  cosmic-icon-theme
  libinput
  libpulse
  libxkbcommon
  polkit
  systemd-libs
)
makedepends=(
  cargo
  git
  git-lfs
  just
)
install="$pkgname.install"
source=(git+https://github.com/pop-os/cosmic-initial-setup.git#tag=epoch-${pkgver/.beta./-beta.})
sha256sums=('cf19a04753b9446f2369a5abd56d6199e01fd4f8b412872f1f89fe3f50bd68e5')

prepare() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  git lfs install --local
  git remote add network-origin https://github.com/pop-os/cosmic-initial-setup
  git lfs fetch network-origin
  git lfs checkout
}

build() {
  cd "${pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  just
}

package() {
  cd "${pkgname}"
  just rootdir="$pkgdir" install
}
