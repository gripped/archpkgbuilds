# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=44beta
pkgrel=1
pkgdesc="Create and modify archives"
url="https://wiki.gnome.org/Apps/FileRoller"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  file
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libarchive
  libnautilus-extension
  libportal
  libportal-gtk4
  pango
  unzip
  zip
)
makedepends=(
  appstream-glib
  git
  meson
  yelp-tools
)
optdepends=(
  'lrzip: lrzip archive support'
  'p7zip: 7z, arj, exe and encrypted zip files support'
  'squashfs-tools: squashfs image support'
  'unace: ACE archive support'
  'unrar: better RAR archive support'
)
groups=(gnome-extra)
_commit=9c2e28e83e351d87d93d62ac1c4850b658ad4746  # tags/44.beta^0
source=("git+https://gitlab.gnome.org/GNOME/file-roller.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
