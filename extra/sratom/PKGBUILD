# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=sratom
pkgname=(sratom{,-docs})
pkgver=0.6.22
pkgrel=1
pkgdesc="An LV2 Atom RDF serialisation library"
arch=(x86_64)
url="https://drobilla.net/software/sratom.html"
license=(ISC)
makedepends=(
  doxygen
  lv2
  meson
  python-sphinx
  python-sphinx-lv2-theme
  python-sphinxygen
  serd
  sord
)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('bb3a46e88faf1596d2f10ac9d22f063e52f7c068a0712b0234168346727f179ea236280dae89e4136e97c243d0815444af1dcc6b86d70b99f5f88008481288b0'
            'SKIP')
b2sums=('be8726f3fe805b7176e3b3c0c8839536ef2825b1ca06eb0fed281272fd183eaa27b945eceab14ca7e8360cd8720ede99f560bc99af1f6b4028279c2d9ee5366b'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package_sratom() {
  depends=(
    glibc
    serd libserd-0.so
    sord libsord-0.so
    lv2
  )
  provides=(libsratom-0.so)
  optdepends=('sratom-docs: for developer documentation')

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick $pkgname-docs usr/share/doc
  )
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_sratom-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/"sratom{-0,}
}
