# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: David Birks <david@birks.dev>
# Contributor: Mike Williamson <mike at korora dot ca>

pkgname=eksctl
pkgver=0.222.0
pkgrel=1
pkgdesc='Command line tool for creating clusters on Amazon EKS'
url='https://github.com/eksctl-io/eksctl'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'glibc'
  'kubectl'
)
makedepends=('go')
options=('!lto')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('d795e76bfcd65519ebb4b1da9aa53950b5bbd83da9999f3191d4d954b41597f51185f680934a4b9290455e199ca595c387e6ad0daf88cc33d9a7fa0b7c131b36')

prepare() {
  cd ${pkgname}-${pkgver}
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="${srcdir}"
  local ld_flags=" \
    -X github.com/eksctl-io/eksctl/pkg/version.gitTag=${pkgver} \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "${ld_flags}" ./cmd/eksctl
}

check() {
  cd ${pkgname}-${pkgver}
  # Skipping pkg/iam/oidc - test hangs due to TLS server issues
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/weaveworks/eksctl/pkg/iam/oidc
  )
  # shellcheck disable=SC2086
  go test -v ${unit_tests}
}

package() {
  cd ${pkgname}-${pkgver}
  install -vDm 755 -t "$pkgdir/usr/bin" ${pkgname}
  install -vDm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.md

  ./${pkgname} completion bash | install -vDm 644 /dev/stdin \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  ./${pkgname} completion zsh | install -vDm 644 /dev/stdin \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  ./${pkgname} completion fish | install -vDm 644 /dev/stdin \
    "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}
