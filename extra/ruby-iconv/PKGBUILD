# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>

_gemname=iconv
pkgname=ruby-$_gemname
pkgver=1.1.0
pkgrel=3
pkgdesc='Wrapper library for iconv'
arch=('x86_64')
url='https://github.com/ruby/iconv'
license=('BSD-2-Clause')
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('0b3a95f07d921d5d08e3e54e14914af2174bb6086911cf880a30a6953003b5993d67cd329aa9d7dd42dd198a7ff9baec3c229874fd2803b55cd2bbb7cc98b469')
b2sums=('ba061bd7457dc2df29c78823dbb8b8f16b4a663dc2f75db0549e44197b6e069078dfda85d3fc54d05579425de83576ae24e7d699fe3f1542f6191da1610492c2')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --verbose \
    --ignore-dependencies \
    --no-document \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem" \
    -- --with-cflags="$(ruby -r rbconfig -e 'print RbConfig::CONFIG["CFLAGS"]') -Wno-error=incompatible-pointer-types"

  # delete unnecessary files & folders
  cd "$pkgdir/$_gemdir"
  rm -vrf cache
  rm -vrf "extensions/$CARCH-linux/$(basename $_gemdir)/$_gemname-$pkgver/"{gem_make.out,mkmf.log}
  cd "gems/$_gemname-$pkgver"
  find . -type f -name ".*" -delete
  rm -vrf Gemfile Rakefile "$_gemname.gemspec" ext test

  # move documentation
  install -vd "$pkgdir/usr/share/doc/$pkgname"
  mv -vt "$pkgdir/usr/share/doc/$pkgname" README.md

  # move license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  mv -vt "$pkgdir/usr/share/licenses/$pkgname" BSDL LICENSE.txt
}
# vim: ts=2 sw=2 et:
