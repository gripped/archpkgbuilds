# Maintainer: Christian Hesse <eworm@archlinux.org>

pkgname='virtualbox-host-modules-lts'
pkgver=7.1.2
pkgrel=5
pkgdesc='Virtualbox host kernel modules for LTS Kernel'
arch=('x86_64')
url='https://virtualbox.org/'
license=('GPL')
makedepends=('linux-lts-headers' "virtualbox-host-dkms=$pkgver")
provides=('VIRTUALBOX-HOST-MODULES')

build() {
  _kernver="$(</usr/src/linux-lts/version)"

  fakeroot dkms build --dkmstree "$srcdir" -m vboxhost/${pkgver}_OSE -k ${_kernver}
}

package(){
  depends=('linux-lts')

  _kernver="$(</usr/src/linux-lts/version)"

  install -Dt "$pkgdir/usr/lib/modules/$_kernver/extramodules" -m0644 \
    vboxhost/${pkgver}_OSE/${_kernver}/${CARCH}/module/*

  # compress each module individually
  find "${pkgdir}" -name '*.ko' -exec zstd --rm -19 {} +

  # systemd module loading
  printf '%s\n' vboxdrv vboxnetadp vboxnetflt |
    install -D -m0644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}
