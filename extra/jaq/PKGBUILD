# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>

pkgname=jaq
pkgver=2.1.0
pkgrel=1
pkgdesc='A jq clone focussed on correctness, speed, and simplicity'
url="https://github.com/01mf02/$pkgname"
arch=(x86_64)
license=(MIT)
depends=(gcc-libs
         glibc)
makedepends=(cargo)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('59cd17e806a4797e28fa42073c6c8a4d6fb40e28efd7a63f3004d1d738d5be93')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
	cargo build --frozen --release
}

check() {
	cd "$_archive"
	cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-MIT
}
