# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qxgedit
pkgver=1.0.1
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=(x86_64)
url="https://qxgedit.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  qt6-tools
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('bab0999d9210697c5e395dded02f33b3f045cdd130ffb0ccda6b191e576601d93f0ad4ea67b023bc6d96a9d665ad3ad86e94be54480715f281fbbbee9c573603')
b2sums=('c96626e90b4638d85e1df9a34bc17c77333238d7acd18592b06694ca9a6ef8cd1f29c0d7fdd4f6864e02212ff792b120f2eb45f2de8424c8f2913bb6d949686e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
