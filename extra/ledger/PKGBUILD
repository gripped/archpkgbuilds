# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ivy Foster <iff@archlinux.org>
# Contributor: Abdelhakim Qbaich <abdelhakim@qbaich.com>
# Contributor: korjjj <korjjj+aur@gmail.com>
# Contributor: TDY <tdy@archlinux.info>
# Contributor: Adam Ehlers Nyholm Thomsen <adament@gmail.com>
# Contributor: Nathan Jones <nathanj@insightbb.com>
# Contributor: Sergey Zolotorev <sergey.zolotorev@gmail.com>

pkgname=ledger
pkgver=3.3.2
pkgrel=9
pkgdesc='Double-entry accounting system with a command-line reporting interface'
arch=(i686 x86_64)
url='https://ledger-cli.org'
license=(BSD-3-Clause)
depends=(boost-libs libedit mpfr)
makedepends=(boost cmake python)
optdepends=(
	'emacs: emacs interface'
	'python: python library'
)
source=(
	"ledger-$pkgver.tar.gz::https://github.com/ledger/ledger/archive/v$pkgver.tar.gz"
	"boost-1.86.patch"
)
sha256sums=('555296ee1e870ff04e2356676977dcf55ebab5ad79126667bc56464cb1142035'
            '410c2bc68f874386e129d0fa58758013953b134d8e4a11d653443ce08da2ca1e')

prepare() {
	cd "$pkgname-$pkgver"

	patch -Np1 -i "$srcdir/boost-1.86.patch"
}

build() {
	cd "$pkgname-$pkgver"

	cmake -S . -B build \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
		-Wno-dev \
		-DUSE_PYTHON=ON
	cmake --build build
}

check() {
	cd "$pkgname-$pkgver"

	cmake --build build --target test
}

package() {
	cd "$pkgname-$pkgver"

	DESTDIR="$pkgdir" cmake --install build
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/ledger/LICENSE.md"
	install -Dm644 contrib/ledger-completion.bash "$pkgdir/usr/share/bash-completion/completions/ledger"
}
