# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=git-smash
pkgver=0.1.2
pkgrel=1
pkgdesc='Smash staged changes into previous commits to support your Git workflow'
url='https://github.com/anthraxx/git-smash'
arch=(x86_64)
license=(MIT)
depends=(
  fzf
  gcc-libs
  git
  glibc
)
makedepends=(
  cargo
  scdoc
)
source=(${url}/releases/download/v${pkgver}/${pkgname}-v${pkgver}.tar.gz{,.sig})
options=('!makeflags')
sha512sums=('dec8596ebc0316e4e799e87fea9e34021f34ff3f8d2f3613abafe98089c85eb01a221e124c50fc474dd9af2464027e8a85719cc699d1715dc04c7d01529bbf75'
            'SKIP')
b2sums=('c5f9f3443abc6cfbd521ea5d91b358968c061f454ad483f10f48fcb7a2bc901d916243d1a39256810d11d7d3779fba87bc5fd76aeaedd7ec6111ddbdcc48475a'
        'SKIP')
validpgpkeys=(
  'E240B57E2C4630BA768E2F26FC1B547C8D8172C8'  # Levente Polyak
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
