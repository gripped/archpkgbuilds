# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=11.87
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('BSD-3-Clause')
depends=('glibc' 'gcc-libs')
makedepends=('cmake' 'python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('e8ca1ac6a11e1a3ea78e311f480f211c58aa297a6c85ad9a92d2cdd296b70744ae05f428c1236ae64fa7ff796b64eb01e5991a981af28ff34a3d32d8ead9c551'
            '058cc796121e8d14b3862a63fa7c9afec728b57817a585cf194365f4a21e2f300b39666036a6ddf9fb6a245867e74d06fb3a6fd5d460abe9a10700ea4a493ce7')
b2sums=('cfe0a8fa1ab51dd7b4e4dec50078936fd4d19e02e37713e2908761cfba41f2e523cf06f917e3c5f152b1e65014f6718f6007cabb3c4e9ec728a2da66d846abaa'
        '1cc1d61e4f64ce30a0de198133251b3da8b77087020c1a7cfd4716a5a7ffdc738e05a0665ea215126950be30f0d8ecac2d54770a16f4492d70731191fc79f702')

build() {
  cmake \
    -S . \
    -B build \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX='/usr'

  cmake --build build

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
