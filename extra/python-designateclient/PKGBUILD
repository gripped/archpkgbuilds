# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-designateclient
pkgver=6.2.0
pkgrel=1
pkgdesc="OpenStack DNS-as-a-Service - Client"
arch=('any')
url="https://docs.openstack.org/python-designateclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-jsonschema' 'python-osc-lib' 'python-oslo-serialization'
         'python-oslo-utils' 'python-pbr' 'python-keystoneauth1' 'python-requests'
         'python-stevedore' 'python-debtcollector')
makedepends=('git')
checkdepends=('python-oslo-config' 'python-oslotest' 'python-subunit' 'python-requests-mock'
              'python-stestr')
source=("git+https://github.com/openstack/python-designateclient.git#tag=$pkgver")
sha512sums=('d6b1e9d2bc92f242f95522e177c4f4ffd68fb05564a8cfe29e70b0754c03386accaae48e672a9e20e0c856fa98708a2eb82bc6e3e080bf0d36e7771d36185798')

build() {
  cd python-designateclient
  python setup.py build
}

check() {
  cd python-designateclient
  stestr run
}

package() {
  cd python-designateclient
  python setup.py install --root="$pkgdir" --optimize=1
}
