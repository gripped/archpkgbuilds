# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-designateclient
pkgver=6.3.0
pkgrel=3
pkgdesc="OpenStack DNS-as-a-Service - Client"
arch=('any')
url="https://docs.openstack.org/python-designateclient/latest/"
license=('Apache-2.0')
depends=('python-cliff' 'python-jsonschema' 'python-osc-lib' 'python-oslo-serialization'
         'python-oslo-utils' 'python-pbr' 'python-keystoneauth1' 'python-requests'
         'python-stevedore' 'python-debtcollector')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-oslo-config' 'python-oslotest' 'python-subunit' 'python-requests-mock'
              'python-stestr')
source=("git+https://github.com/openstack/python-designateclient.git#tag=$pkgver")
sha512sums=('4bcfec047952d01820249299c05ff0eb225fdd7163ab84213ec9a10eb38803cdcb38c4f91f413ee72cdef0e38550364c0d78f70c148c587d4a95cfbb78b900b9')

build() {
  cd python-designateclient
  python -m build --wheel --no-isolation
}

check() {
  cd python-designateclient
  stestr run
}

package() {
  cd python-designateclient
  python -m installer --destdir="$pkgdir" dist/*.whl
}
