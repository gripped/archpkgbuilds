# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-sys-virt
pkgver=11.6.0
pkgrel=1
pkgdesc="Represent and manage a libvirt hypervisor connection"
arch=('x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/Sys-Virt"
depends=('libvirt' 'perl')
makedepends=('perl-cpan-changes' 'perl-module-build' 'perl-xml-xpath' 'perl-test-pod'
             'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DA/DANBERR/Sys-Virt-v$pkgver.tar.gz")
sha512sums=('646dac33e4f5532dfb3d09eafd9ffb56096071d1c698aeecfa4bb7b5b6c48c03a0596354e20ee84e4eb0d014689b15de87e199f5ff6ec697f164476f1e50dae2')

build() {
  cd Sys-Virt-v$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Sys-Virt-v$pkgver
  make test
}

package() {
  cd Sys-Virt-v$pkgver
  make DESTDIR="$pkgdir" install
}
