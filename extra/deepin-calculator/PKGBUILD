# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=6.5.25
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'qt6-base'
         'qt6-svg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'clang' 'qt6-declarative')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-calculator.git#tag=$pkgver")
sha512sums=('ba2caa7364335292a203a9a6ac589e352e3bfa5b7351874cc7cf48deee3a246bdf9a05891c0f0d633b9b93306a3b52318d47b5541720b5d1e398c7beb0f6f20e')

prepare() {
  cd deepin-calculator
  # respect our LDFLAGS
  sed -i '/CMAKE_EXE_LINKER_FLAGS/d' CMakeLists.txt
}

build() {
  cd deepin-calculator
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
  ninja
}

package() {
  cd deepin-calculator
  DESTDIR="$pkgdir" ninja install
}
