# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=6.5.30
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'qt6-base'
         'qt6-svg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'clang' 'qt6-declarative')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-calculator.git#tag=$pkgver")
sha512sums=('2480c287ccbdd983c2a6394a234ff5b2e1c1e4c2a556fe0ba25389f8d20e905ca77c677fb715d9c9f59ee83d940f7641d7aea789a8d28a3d5ff36d5b95b8bb50')

prepare() {
  cd deepin-calculator
  # respect our LDFLAGS
  sed -i '/CMAKE_EXE_LINKER_FLAGS/d' CMakeLists.txt
  # how did they compile it?
  sed -i 's|"../3rdparty/core/settings.h"|"../../3rdparty/core/settings.h"|' src/control/basickeypad.cpp src/control/scientifickeypad.cpp
}

build() {
  cd deepin-calculator
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
  ninja
}

package() {
  cd deepin-calculator
  DESTDIR="$pkgdir" ninja install
}
