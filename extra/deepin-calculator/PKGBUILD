# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=6.5.1
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'qt5-base' 'qt5-svg')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-calculator.git#tag=$pkgver")
sha512sums=('8645b425b8dc9e65141d5ec95a5ba116d0801182bd7247a59d392c1b8c9c96d963e217e45f295f7e7a95594ad56790642843dbaeace7219cb703a3170ccce655')

prepare() {
  cd deepin-calculator
  # respect our LDFLAGS
  git cherry-pick -n d7b27df6f4046998742e6342c3fd03fbc8947845
}

build() {
  cd deepin-calculator
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
  ninja
}

package() {
  cd deepin-calculator
  DESTDIR="$pkgdir" ninja install
}
