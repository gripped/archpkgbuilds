# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=6.5.21
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'qt6-base'
         'qt6-svg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'clang' 'qt6-declarative')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-calculator.git#tag=$pkgver")
sha512sums=('788f68175a6a8d3386a9d495c4233f5d7ad545b4845f46463fa140651cd58a3a5623fa37c8a94e67ad69928765f1f112ef27f3b7f9281f441ba12acfb0dc9257')

prepare() {
  cd deepin-calculator
  # respect our LDFLAGS
  sed -i '/CMAKE_EXE_LINKER_FLAGS/d' CMakeLists.txt
}

build() {
  cd deepin-calculator
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
  ninja
}

package() {
  cd deepin-calculator
  DESTDIR="$pkgdir" ninja install
}
