# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=6.5.27
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'qt6-base'
         'qt6-svg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'clang' 'qt6-declarative')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-calculator.git#tag=$pkgver")
sha512sums=('f2c8b41d73590ed1d23dfe97c7130a9e020b209ac6571f22e3d815693ce66c9421eb2dc19b279828e25c368fe3d998d5e41a701938c33445bba42652e2fecc44')

prepare() {
  cd deepin-calculator
  # respect our LDFLAGS
  sed -i '/CMAKE_EXE_LINKER_FLAGS/d' CMakeLists.txt
}

build() {
  cd deepin-calculator
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
  ninja
}

package() {
  cd deepin-calculator
  DESTDIR="$pkgdir" ninja install
}
