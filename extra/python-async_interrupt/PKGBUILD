# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Aaron Coach <aur at ezpz.cz>

pkgname=python-async_interrupt
_name=${pkgname#python-}
pkgver=1.2.0
pkgrel=3
pkgdesc='Python library that provides interrupt context manager for asyncio'
arch=(any)
url='https://github.com/bdraco/async_interrupt'
license=(Apache-2.0)
depends=(python)
makedepends=(python-poetry-core python-build python-installer python-wheel)
source=("$url/releases/download/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('d6994990bddda43042675819f6b1863d08936d7d7e128c59be1f36f9fcdac3aa4c538eaa1934bfb5db051c2c1339a7143f7142cbf97c133153721e61813790bf')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
