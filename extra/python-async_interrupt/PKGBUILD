# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Aaron Coach <aur at ezpz.cz>

pkgname=python-async_interrupt
_name=${pkgname#python-}
pkgver=1.2.2
pkgrel=2
pkgdesc='Python library that provides interrupt context manager for asyncio'
arch=(any)
url='https://github.com/bdraco/async_interrupt'
license=(Apache-2.0)
depends=(python)
makedepends=(python-poetry-core python-build python-installer python-wheel)
source=("$url/releases/download/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('0b9077c8f86bf1137dbe3819ef59bc48f5c719f5ca4b2981a4f73775200f41f4c2b0b8414812288246e8957f73971d0a8ee63770d619cbcea41250ba5a060875')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
