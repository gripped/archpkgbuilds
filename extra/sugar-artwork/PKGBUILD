# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-artwork
pkgver=0.121
pkgrel=4
pkgdesc='Sugar icons and themes'
arch=(any)
url='https://github.com/sugarlabs/sugar-artwork'
license=(Apache-2.0)
makedepends=(
  git
  gtk3
  python-empy
  xorg-xcursorgen
)
source=(
  "git+https://github.com/sugarlabs/$pkgname.git#tag=v$pkgver"
  sugar-artwork-empy-4.2.patch
  sugar-artwork-fix-deprecated.patch
)
b2sums=(
  2331265cbed89f8ec10f1e36916cdcc17fd979580156ac5ae4f8ce12c407683cd591b1630640fc14a820b2df94da3e1dea9f95b4df684770984e465a8fdbf7a8
  e7c51ebf5815d112c24a49ab9fb5f7149593a8e7f8e93414556ce9befa807639e63ca1ac9ad4e5e7f1a798a3760357bc918e938393ed7af8868268021470f02f
  7d4c5f7dcd4ad68f72630a43c913bd4c76ab5c88e77d415639c29e92a34485e460a6c82a5af995636c8b04a93e017ace222484d2677f25a9d522087110e6996e
)

prepare() {
  cd $pkgname

  # Fix build with python-empy 4.1
  git cherry-pick -n 84a0f2c115ddabc0d8ec16cc09080d62c067a90f
  git cherry-pick -n 14de4382ab2bc9ce4aa5f4c2fdef595a19c0a452

  # Fix port to Empy-4.2
  # https://github.com/sugarlabs/sugar-artwork/pull/125
  git apply -3 ../sugar-artwork-empy-4.2.patch

  # Remove deprecated properties
  # https://github.com/sugarlabs/sugar-artwork/pull/126
  git apply -3 ../sugar-artwork-fix-deprecated.patch

  # Drop legacy symlinks
  sed -i '/ICON_NAMING_UTILS_REQUIRED/,+14d' configure.ac
  sed -i '/ICONMAP/d' icons/scalable/*/Makefile.am

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --without-gtk2
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
