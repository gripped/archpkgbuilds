# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-artwork
pkgver=0.121
pkgrel=5
pkgdesc='Sugar icons and themes'
arch=(any)
url='https://github.com/sugarlabs/sugar-artwork'
license=(Apache-2.0)
makedepends=(
  git
  gtk3
  python-empy
  xorg-xcursorgen
)
source=("git+https://github.com/sugarlabs/$pkgname.git#tag=v$pkgver")
b2sums=(2331265cbed89f8ec10f1e36916cdcc17fd979580156ac5ae4f8ce12c407683cd591b1630640fc14a820b2df94da3e1dea9f95b4df684770984e465a8fdbf7a8)

prepare() {
  cd $pkgname

  # Fix build with python-empy 4.1
  git cherry-pick -n 84a0f2c115ddabc0d8ec16cc09080d62c067a90f
  git cherry-pick -n 14de4382ab2bc9ce4aa5f4c2fdef595a19c0a452
  git cherry-pick -n cddbb8ee0270ecb26afb25772df5f25d48288faf

  # Replace deprecated properties
  git cherry-pick -n 3c4854d3eec0ba9b02a9ad139462fd559e6c027d

  # Drop legacy symlinks
  sed -i '/ICON_NAMING_UTILS_REQUIRED/,+14d' configure.ac
  sed -i '/ICONMAP/d' icons/scalable/*/Makefile.am

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --without-gtk2
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
