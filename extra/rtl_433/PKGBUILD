# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: BrainDamage

pkgname=rtl_433
pkgver=25.02
pkgrel=1
pkgdesc="Program to decode radio transmissions from devices on the ISM bands (and other frequencies)"
arch=(x86_64)
license=(GPL-2.0-only)
depends=(rtl-sdr soapysdr glibc openssl libusb)
makedepends=(gcc cmake doxygen)
url='https://github.com/merbanan/rtl_433'
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('5aaffbf892fbd1215464ecf0cdda4b55c8ea6c15a502faa8bae5ca7b2fb68ae552df2419a43dd02998b597d4ecf49a1da0348509aa3f29803ea93839ae9fef0c')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  find "$pkgname-$pkgver/examples" -type f -exec \
    install -Dvm 644 -t "$pkgdir"/usr/share/doc/$pkgname {} +;
}

# vim:set ts=2 sw=2 et:
