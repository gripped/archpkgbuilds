# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: BrainDamage

pkgname=rtl_433
pkgver=24.10
pkgrel=2
pkgdesc="Program to decode radio transmissions from devices on the ISM bands (and other frequencies)"
arch=(x86_64)
license=(GPL-2.0-only)
depends=(rtl-sdr soapysdr glibc openssl libusb)
makedepends=(gcc cmake doxygen)
url='https://github.com/merbanan/rtl_433'
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('69b961f48b84c280cdae28ad47d30cddd90f0f77c7214eeb5f3e351569474cc0a2759f28036aa8b06b8a4282adb5228d05704fab430c29232aae8472a373f1fd')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  find "$pkgname-$pkgver/examples" -type f -exec \
    install -Dvm 644 -t "$pkgdir"/usr/share/doc/$pkgname {} +;
}

# vim:set ts=2 sw=2 et:
