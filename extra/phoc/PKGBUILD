# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=phoc
pkgver=0.48.0
pkgrel=1
pkgdesc='Display compositor designed for phones'
arch=('x86_64' 'aarch64')
url='https://gitlab.gnome.org/World/Phosh/phoc'
license=(GPL-3.0-only)
depends=(
        "gnome-desktop"
        "wlroots0.19"
        "gsettings-desktop-schemas"
        "pixman"
        "libinput"
        "libxcb"
        "libxkbcommon"
        "json-glib"
        "glib2"
        "dconf"
        "cairo"
        "wayland"
        "libgmobile"
)
checkdepends=(
              "xorg-server-xvfb"
              "xorg-xauth"
              "mutter"
              "pixman"
)
makedepends=(
            "cmake"
            "meson"
            "git"
            "wayland-protocols"
            "python-jinja"
            "python-pygments"
            "python-typogrify"
            "libgirepository"
            "libgmobile"
            "glib2-devel"
)
optdepends=('xorg-wayland: run X clients under phoc')
_gvdb="4758f6fb7f889e074e13df3f914328f3eecb1fd3"
source=(
      "${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
      "git+https://gitlab.gnome.org/GNOME/gvdb.git#commit=${_gvdb}" #new dependency with no release
)
sha256sums=('3533a26d6a991b4c6ede98a725b4429788e81a7b22f895f2d298488dea5bb4f3'
            'ebe771e60943547279fbf29acb6aea6346fc20df6388252f71bf2bf679d3a7a8')

prepare() {
  cp -r gvdb "${pkgname}-v${pkgver}/subprojects"
  cd "${pkgname}-v${pkgver}"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build -Dembed-wlroots=disabled
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
