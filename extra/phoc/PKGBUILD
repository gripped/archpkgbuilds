# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=phoc
pkgver=0.50.0
pkgrel=2
pkgdesc='Display compositor designed for phones'
arch=('x86_64')
url='https://gitlab.gnome.org/World/Phosh/phoc'
license=(GPL-3.0-or-later)
depends=(
        "gnome-desktop"
        "wlroots0.19"
        "gsettings-desktop-schemas"
        "pixman"
        "libinput"
        "libxcb"
        "libxkbcommon"
        "glib2"
        "dconf"
        "cairo"
        "wayland"
        "libgmobile"
        "mutter"
        "xorg-xwayland"
)
checkdepends=(
              "xorg-server-xvfb"
)
makedepends=(
            "meson"
            "git"
            "wayland-protocols"
            "glib2-devel"
            "python-docutils"
)
_gvdb="4758f6fb7f889e074e13df3f914328f3eecb1fd3"
source=(
      "${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
      "git+https://gitlab.gnome.org/GNOME/gvdb.git#commit=${_gvdb}" #new dependency with no release
)
sha256sums=('024aceb0c3d87d924a11387b5428e5578dc5dc8ab5c3e427a5858118e7841e20'
            'ebe771e60943547279fbf29acb6aea6346fc20df6388252f71bf2bf679d3a7a8')

prepare() {
  cp -r gvdb "${pkgname}-v${pkgver}/subprojects"
  cd "${pkgname}-v${pkgver}"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build -Dembed-wlroots=disabled -Dman=true
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
