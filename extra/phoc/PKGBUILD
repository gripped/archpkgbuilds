# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=phoc
pkgver=0.53.0
pkgrel=1
pkgdesc='Display compositor designed for phones'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/phoc'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gnome-desktop
  gsettings-desktop-schemas
  hicolor-icon-theme
  libgmobile
  libinput
  libxcb
  libxkbcommon
  mutter
  pango
  pixman
  systemd-libs
  wayland
  wlroots0.19
  xorg-xwayland
)
makedepends=(
  git
  glib2-devel
  meson
  python-docutils
  wayland-protocols
)
checkdepends=(xorg-server-xvfb)
source=(
  "git+https://gitlab.gnome.org/World/Phosh/phoc.git?signed#tag=v$pkgver"
  git+https://gitlab.gnome.org/GNOME/gvdb.git
)
b2sums=('68aede88acb1883d4fdc57e75bab31b5a705dbd4243ac6260e963122e470c204836091c1d390580503112b75be88ae9d0df0ba9c8786528dc2da1e39dad8ad58'
        'SKIP')
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido GÃ¼nther <guido@xcpu.ch>

prepare() {
  ln -s "$srcdir/gvdb" "$pkgname/subprojects/gvdb"
  meson subprojects checkout --sourcedir $pkgname
}

build() {
  arch-meson $pkgname build \
    -D embed-wlroots=disabled \
    -D man=true
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
