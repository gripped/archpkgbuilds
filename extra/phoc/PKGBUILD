# Maintainer: Jelle van der Waa <jelle@archlinux.org>

libgmobile_commit=a3c67a1184bfa73c593edefabe77cce57a5e38f6

pkgname=phoc
pkgver=0.36.0
pkgrel=1
pkgdesc='Display compositor designed for phones'
arch=(x86_64 aarch64)
url='https://gitlab.gnome.org/World/Phosh/phoc'
license=(GPL3)
depends=(gnome-desktop wlroots gsettings-desktop-schemas pixman libinput libxcb libxkbcommon
	 json-glib glib2 dconf cairo wayland)
checkdepends=(xorg-server-xvfb xorg-xauth mutter pixman)
makedepends=(cmake meson git wayland-protocols python-jinja python-pygments python-typogrify libgirepository)
optdepends=('xorg-wayland: run X clients under phoc')
source=(https://gitlab.gnome.org/World/Phosh/phoc/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz
	https://gitlab.gnome.org/guidog/gmobile/-/archive/${libgmobile_commit}/gmobile-${libgmobile_commit}.tar.gz)
sha256sums=('daa70f669585aeeee53ce0be098faab3490564559ac8795bde427850d710fa98'
            '1aeac55a790525c2028a66562de877a8f99ce391636d8e0e3d059b0899c7a7ec')

prepare() {
  mv "gmobile-${libgmobile_commit}" "${pkgname}-v${pkgver}/subprojects/gmobile"
  cd "${pkgname}-v${pkgver}"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build -Dembed-wlroots=disabled
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  depends+=(libwlroots.so=12)
  DESTDIR="${pkgdir}" meson install -C build
}
