# Maintainer: Jelle van der Waa <jelle@archlinux.org>

libgmobile_commit=f4d4e5740c3e4b2de40864ab8a9e7b7f957d6aec

pkgname=phoc
pkgver=0.32.0
pkgrel=1
pkgdesc='Display compositor designed for phones'
arch=(x86_64 aarch64)
url='https://gitlab.gnome.org/World/Phosh/phoc'
license=(GPL3)
depends=(gnome-desktop wlroots gsettings-desktop-schemas pixman libinput libxcb libxkbcommon
	 json-glib glib2 dconf cairo wayland)
checkdepends=(xorg-server-xvfb xorg-xauth mutter pixman)
makedepends=(cmake meson git wayland-protocols python-jinja python-pygments python-typogrify libgirepository)
optdepends=('xorg-wayland: run X clients under phoc')
source=(https://gitlab.gnome.org/World/Phosh/phoc/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz
	https://gitlab.gnome.org/guidog/gmobile/-/archive/${libgmobile_commit}/gmobile-${libgmobile_commit}.tar.gz)
sha256sums=('e673a491028648a39dee1c2c4fe966a980a6b49039404ea501f2b9ed273e3703'
            'b071e21a30eb8649b2bd9f416eec97110f0f08090b6c230e928ef1c3339ac344')

prepare() {
  mv "gmobile-${libgmobile_commit}" "${pkgname}-v${pkgver}/subprojects/gmobile"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build -Dembed-wlroots=disabled
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
