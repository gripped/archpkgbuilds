# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=phoc
pkgver=0.44.0
pkgrel=1
pkgdesc='Display compositor designed for phones'
arch=(x86_64 aarch64)
url='https://gitlab.gnome.org/World/Phosh/phoc'
license=(GPL-3.0-only)
depends=(gnome-desktop wlroots0.17 gsettings-desktop-schemas pixman libinput libxcb libxkbcommon
	 json-glib glib2 dconf cairo wayland libgmobile)
checkdepends=(xorg-server-xvfb xorg-xauth mutter pixman)
makedepends=(cmake meson git wayland-protocols python-jinja python-pygments python-typogrify libgirepository libgmobile glib2-devel)
optdepends=('xorg-wayland: run X clients under phoc')
source=(https://gitlab.gnome.org/World/Phosh/phoc/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha256sums=('5e474db1da0721a4f891f884948999fd06f089aef6ae55c290fcb493f782b56a')

prepare() {
  cd "${pkgname}-v${pkgver}"
}

build() {
  export PKG_CONFIG_PATH='/usr/lib/wlroots0.17/pkgconfig'
  arch-meson "${pkgname}-v${pkgver}" build -Dembed-wlroots=disabled
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  depends+=(libwlroots.so=12)
  DESTDIR="${pkgdir}" meson install -C build
}
