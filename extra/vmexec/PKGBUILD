# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=vmexec
pkgver=0.1.4
pkgrel=2
pkgdesc="Run a single command in a speedy virtual machine with zero-setup"
arch=('x86_64')
url="https://github.com/svenstaro/vmexec"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
source=(vmexec-$pkgver.tar.gz::https://github.com/svenstaro/vmexec/archive/v${pkgver}.tar.gz)
sha512sums=('6792f6397838101952117245d714ba8b0c57170fe38a6b2c80c60b412dd85673c3feddde3862e3f8b0043d1444b4bda497104fa8629121b5718578f3176968e1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo test --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/vmexec "$pkgdir"/usr/bin/vmexec

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/vmexec completions bash > "$pkgdir"/usr/share/bash-completion/completions/vmexec
  target/release/vmexec completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_vmexec
  target/release/vmexec completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/vmexec.fish

  mkdir -p "$pkgdir"/usr/share/man/man1
  target/release/vmexec manpage "$pkgdir"/usr/share/man/man1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
