# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=vmexec
pkgver=0.1.5
pkgrel=1
pkgdesc="Run a single command in a speedy virtual machine with zero-setup"
arch=('x86_64')
url="https://github.com/svenstaro/vmexec"
license=('MIT')
depends=('gcc-libs' 'libguestfs')
makedepends=('rust')
source=(vmexec-$pkgver.tar.gz::https://gitlab.archlinux.org/archlinux/vmexec/-/archive/v${pkgver}/vmexec-v${pkgver}.tar.gz)
sha512sums=('4a8374c03ce574f6fe7f8cc699aa628021f2491c9a948c8ccf8f5e8bd8d883369904b4ad0b886914e1e6bfeb1be092edf3acd24dceb497614ccc31cd58acb82a')

build() {
  cd $pkgname-v$pkgver

  cargo build --release --locked
}

check() {
  cd $pkgname-v$pkgver

  cargo test --release --locked
}

package() {
  cd $pkgname-v$pkgver

  install -Dm755 target/release/vmexec "$pkgdir"/usr/bin/vmexec

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/vmexec completions bash > "$pkgdir"/usr/share/bash-completion/completions/vmexec
  target/release/vmexec completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_vmexec
  target/release/vmexec completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/vmexec.fish

  mkdir -p "$pkgdir"/usr/share/man/man1
  target/release/vmexec manpage "$pkgdir"/usr/share/man/man1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
