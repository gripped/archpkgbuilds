# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=vmexec
pkgver=0.3.0
pkgrel=1
pkgdesc="Run a single command in a speedy virtual machine with zero-setup"
arch=('x86_64')
url="https://github.com/svenstaro/vmexec"
license=('MIT')
depends=('libguestfs' 'libgcc' 'glibc')
makedepends=('rust')
source=(vmexec-$pkgver.tar.gz::https://gitlab.archlinux.org/archlinux/vmexec/-/archive/v${pkgver}/vmexec-v${pkgver}.tar.gz)
sha512sums=('5f30d05b9ba2ace6fa9914074980d6f420df250e5d5a301fc91b0c1a667f40c1edb61f440b8f07826ec83959160343bd9a39b85206fdf967237051d6d3b10364')

build() {
  cd $pkgname-v$pkgver

  export CFLAGS+=" -ffat-lto-objects"
  cargo build --release --locked
}

check() {
  cd $pkgname-v$pkgver

  # Only run unittests because the integration tests can't currently work in an
  # nspawn environment.
  cargo test --bins --locked
}

package() {
  cd $pkgname-v$pkgver

  install -Dm755 target/release/vmexec "$pkgdir"/usr/bin/vmexec

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/vmexec completions bash > "$pkgdir"/usr/share/bash-completion/completions/vmexec
  target/release/vmexec completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_vmexec
  target/release/vmexec completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/vmexec.fish

  mkdir -p "$pkgdir"/usr/share/man/man1
  target/release/vmexec manpage "$pkgdir"/usr/share/man/man1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
