# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: quietvoid <tcChlisop0@gmail.com>

pkgname=dovi-tool
pkgver=2.3.1
pkgrel=1
pkgdesc='CLI tool combining multiple utilities for working with Dolby Vision'
arch=(x86_64)
url=https://github.com/quietvoid/dovi_tool
license=(MIT)
makedepends=(
  cargo
  git
  gnu-free-fonts
)
depends=(fontconfig)
_tag=b25558062e4a56973482ec70133bd7b891320e48
source=(git+https://github.com/quietvoid/dovi_tool.git#tag=${_tag})
b2sums=('533188385cae0512ab24b74835258e2e3bcbba959667500bdc4247a943eb61bfbd6d48849201cdfe9e44b884ed96fa9191ba3fc4c2adbda23e675ec4f17e0729')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path dovi_tool/Cargo.toml
}

pkgver() {
  cd dovi_tool
  git describe --tags
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path dovi_tool/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path dovi_tool/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path dovi_tool \
    --root "${pkgdir}"/usr
  install -Dm 644 dovi_tool/LICENSE -t "${pkgdir}"/usr/share/licenses/dovi-tool/
}

# vim: ts=2 sw=2 et:
