# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: quietvoid <tcChlisop0@gmail.com>

pkgname=dovi-tool
pkgver=2.1.3
pkgrel=1
pkgdesc='CLI tool combining multiple utilities for working with Dolby Vision'
arch=(x86_64)
url=https://github.com/quietvoid/dovi_tool
license=(MIT)
makedepends=(
  cargo
  git
  gnu-free-fonts
)
depends=(fontconfig)
_tag=458664f84b52f46b5176ee7e5ed58e8d298d6ba9
source=(git+https://github.com/quietvoid/dovi_tool.git#tag=${_tag})
b2sums=('8a66ad9ad5caddec1488321b0d5071462a026f94ef0bed895b867a868e188c6bd23c84791f2825d2f07e780742932281165bd70879d9f66a9631a8f729bc4a1f')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path dovi_tool/Cargo.toml
}

pkgver() {
  cd dovi_tool
  git describe --tags
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path dovi_tool/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path dovi_tool/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path dovi_tool \
    --root "${pkgdir}"/usr
  install -Dm 644 dovi_tool/LICENSE -t "${pkgdir}"/usr/share/licenses/dovi-tool/
}

# vim: ts=2 sw=2 et:
