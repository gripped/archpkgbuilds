# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: quietvoid <tcChlisop0@gmail.com>

pkgname=dovi-tool
pkgver=2.3.0
pkgrel=1
pkgdesc='CLI tool combining multiple utilities for working with Dolby Vision'
arch=(x86_64)
url=https://github.com/quietvoid/dovi_tool
license=(MIT)
makedepends=(
  cargo
  git
  gnu-free-fonts
)
depends=(fontconfig)
_tag=4fd2b2235c9f93582dd4a00e65ee34a07800afd7
source=(git+https://github.com/quietvoid/dovi_tool.git#tag=${_tag})
b2sums=('40aefc4476b3fb674d2f2fe241de1e8e8b91658c8d5c9d7c729d371bbbb5b70955210d23b3cd088ae2001963f9aa15c29ceb4fa3abf15762f66d44efd6c3ae0e')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path dovi_tool/Cargo.toml
}

pkgver() {
  cd dovi_tool
  git describe --tags
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path dovi_tool/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path dovi_tool/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path dovi_tool \
    --root "${pkgdir}"/usr
  install -Dm 644 dovi_tool/LICENSE -t "${pkgdir}"/usr/share/licenses/dovi-tool/
}

# vim: ts=2 sw=2 et:
