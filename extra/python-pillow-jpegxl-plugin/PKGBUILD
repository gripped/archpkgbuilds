# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=python-pillow-jpegxl-plugin
_pkgname=pillow-jpegxl-plugin
pkgver=1.3.5
pkgrel=1
pkgdesc="A Python Pillow plugin that adds jxl support"
url="https://github.com/Isotr0py/pillow-jpegxl-plugin"
arch=('x86_64')
license=('MIT')
depends=('python' 'python-pillow' 'libjxl')
makedepends=('python-maturin' 'python-installer' 'patchelf')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('81a35b2de2b5e42072c5e23d59032a7722e5e9feddd04795ba5c693c2098de87')

build() {
	cd "${_pkgname}-${pkgver}"
	maturin build --release --features dynamic
}

package() {
	cd "${_pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" target/wheels/*.whl
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
