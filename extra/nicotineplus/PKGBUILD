# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>

pkgname=nicotine+
pkgver=3.3.0
pkgrel=1
pkgdesc='Soulseek music-sharing client, written in python'
arch=(any)
url='https://github.com/Nicotine-Plus/nicotine-plus'
license=(GPL-3.0-only)
depends=(gdk-pixbuf2
         gtk4
         gobject-introspection-runtime
         pango
         python
         python-gobject)
makedepends=(python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://github.com/Nicotine-Plus/nicotine-plus/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('c94bd7ecf6a107445836df80efed9b8c18cab307d812c2b91be18e93dfb4ac1f')

build() {
  cd nicotine-plus-$pkgver
  
  python -m build --wheel --no-isolation
}

package() {
  cd nicotine-plus-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
}
