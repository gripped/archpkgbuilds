# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>

pkgname=nicotine+
pkgver=3.3.9
pkgrel=1
pkgdesc='Soulseek music-sharing client, written in python'
arch=(any)
url='https://github.com/Nicotine-Plus/nicotine-plus'
license=(GPL-3.0-only)
depends=(gdk-pixbuf2
         glib2
         gtk4
         gobject-introspection-runtime
         pango
         python
         python-gobject)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/Nicotine-Plus/nicotine-plus#tag=$pkgver)
sha256sums=('11bc2b6b00c3293a4abc932c6c51e2717a6ae49acb71b584fbd099e1f699c6e3')

build() {
  cd nicotine-plus
  
  python -m build --wheel --no-isolation
}

package() {
  cd nicotine-plus

  python -m installer --destdir="$pkgdir" dist/*.whl
}
