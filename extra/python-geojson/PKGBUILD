# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Alexander Fasching <fasching.a91@gmail.com>
# Contributor: Johannes Loethberg <johannes@kyriasis.com>

_pyname=geojson
pkgname="python-$_pyname"
pkgver=3.2.0
pkgrel=2
pkgdesc='Python bindings and utilities for GeoJSON'
url="https://github.com/jazzband/$_pyname"
arch=(any)
license=(BSD-3-Clause)
depends=(python)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
_archive="$_pyname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz"
         https://github.com/jazzband/geojson/commit/2584c0de.patch)
sha256sums=('4ea0262bc8c733d3ee4a7ff8971bf0065c2148bff170644fe5d194f07694532a'
            '18a8dc66706b08f9b96e2d4383e1a6cbc628921f20ce4cc86a6510a3082daaff')

prepare() {
  patch -d $_archive -p1 < 2584c0de.patch # Support python 3.14
}

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	python -m unittest discover
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.rst
}
