# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thorben GÃ¼nther <echo YWRtaW5AeGVucm94Lm5ldAo= | base64 -d>

pkgname=hut
pkgver=0.7.0
pkgrel=1
pkgdesc='A CLI tool for Sourcehut'
arch=('x86_64')
url='https://sr.ht/~xenrox/hut'
license=('AGPL-3.0-only')
depends=('glibc')
makedepends=('git' 'go' 'scdoc')
options=('!lto' '!debug')
source=("git+https://git.sr.ht/~xenrox/hut#tag=v$pkgver")
sha512sums=('ff3a30b5c97d13835a5a2571a5d5218204c26cc8ff53171f28680623babf8101f24ee67e9fcae2c7c1e1b39672b35338d774c04ca06db036639fd9d0aa26a931')
b2sums=('191ae506f8753db11f62907bbc9b8b31b9fb232e406ed471a8a16756216177df20ad7ac24f4dfc232562d7da75c60b72b3d27b41829a8c244c52ad5190e6bfc6')

prepare() {
  cd "$pkgname"

  # download dependencies
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  make --environment-overrides
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
