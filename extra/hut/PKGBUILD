# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thorben GÃ¼nther <echo YWRtaW5AeGVucm94Lm5ldAo= | base64 -d>

pkgname=hut
pkgver=0.5.0
pkgrel=1
pkgdesc='A CLI tool for Sourcehut'
arch=('x86_64')
url='https://sr.ht/~xenrox/hut'
license=('AGPL-3.0-only')
depends=('glibc')
makedepends=('git' 'go' 'scdoc')
options=('!lto' '!debug')
source=("git+https://git.sr.ht/~xenrox/hut#tag=v$pkgver")
sha512sums=('8466a0fdcfdd458baaaa3e5382b6556d7b98f5d252c4b49f1312a58a4bf933bcfc29915dd15c1c5b9f3445fb29131d83199f12415c1d779f3293db8efe3e1481')
b2sums=('e4dd6e2e535e4bb8749eaa2fc08fb83f32fb5e5b47ac1c06d2c26c019a31341af68349c439e53038896d6d3322a481eaddec3656a2a528cd4907afd2e7b77825')

prepare() {
  cd "$pkgname"

  # download dependencies
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  make --environment-overrides
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
