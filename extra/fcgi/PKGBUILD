# Maintainer: tobias <tobias@justdreams.de>
# Modified by: eliott <eliott@cactuswax.net>
# Original by Benjamin Andresen, klapmuetz [at] gmail [dot] com

pkgname=fcgi
pkgver=2.4.3
pkgrel=2
depends=('gcc-libs')
pkgdesc='FASTCgi (fcgi) is a language independent, high performant extension to CGI'
arch=('x86_64')
license=('custom')
options=('!makeflags')
url='https://github.com/FastCGI-Archives/fcgi2'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/FastCGI-Archives/fcgi2/archive/${pkgver}.tar.gz"
        '0001-Update-fcgiapp-c.patch')
sha256sums=('5273bc54c28215d81b9bd78f937a9bcdd4fe94e41ccd8d7c991aa8a01b50b70e'
            '40245b58e0023e6f1437c4fd47e774d22fe88b8a10c457846e88415aaa9d1d80')

prepare() {
  cd "${srcdir}/fcgi2-${pkgver}"

  patch -Np1 -R < ../0001-Update-fcgiapp-c.patch

  ./autogen.sh
}

build() {
  cd "${srcdir}/fcgi2-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/fcgi2-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
