# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openshadinglanguage
pkgver=1.15.1.0
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('BSD-3-Clause')
depends=('openimageio' 'imath' 'freetype2' 'libpng' 'libtiff' 'zlib' 'pugixml'
         'ncurses' 'clang' 'qt6-base' 'fmt' 'glibc' 'llvm-libs' 'libstdc++')
makedepends=('cmake' 'python' 'llvm' 'ninja' 'git')
optdepends=('python: the Python module')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('0660c3dc798ee4d879f5913eef8fca040bba20434fc2c7108319207158f49906423b3aa9609f20424426e5f0ac820f76c854080b62a86444bd4e414de350e7fa')

build() {
  cd OpenShadingLanguage-$pkgver

  cmake \
    -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=17 \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenShadingLanguage-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install

  # Fixup an upstream fucky (See #1)
  rm -r "$pkgdir"/usr/build-scripts
  rm -r "$pkgdir"/usr/cmake

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
