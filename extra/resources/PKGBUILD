# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=resources
pkgver=1.9.1
pkgrel=1
pkgdesc='Monitor for system resources and processes'
arch=(x86_64)
url='https://apps.gnome.org/Resources/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  polkit
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://github.com/nokyan/resources.git#tag=v$pkgver")
b2sums=(6d16953a4a2c7322c92cf4637d7438510175e75d8db225f2a9723a13db0c17b587889f88b03674f4cf31f18bf5dba80984fcaaad69510394f1bfdbd605de1518)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build \
    -D profile=default

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
