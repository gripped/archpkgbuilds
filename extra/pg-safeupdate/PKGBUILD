# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pg-safeupdate
pkgver=1.5
pkgrel=1
pkgdesc="A simple extension to PostgreSQL that requires criteria for UPDATE and DELETE"
arch=('x86_64')
url="https://github.com/eradman/pg-safeupdate"
license=('ISC')
depends=('postgresql')
makedepends=('git')
checkdepends=('pifpaf' 'ruby')
source=("git+https://github.com/eradman/pg-safeupdate#tag=$pkgver")
sha512sums=('daa6d60a3d72d27d5e00383dcf23c220155ef2452eae7b8f00a093828d5d989c364670d48af70c70485b489a6ec07d3e5d770ece908712394d8264e8b46c16f5')

build() {
  cd $pkgname
  make safeupdate.so
}

check() {
  cd $pkgname
  sed -i 's|^\$url = .*$|\$url = ENV["PIFPAF_URL"]|' test.rb
  eval $(pifpaf run postgresql)
  psql -c "set dynamic_library_path = '$PWD'; load 'safeupdate';"
  # TODO: extension still not loaded, why?
  ruby test.rb || echo "Tests failed"
  pifpaf_stop
}

package() {
  cd $pkgname
  install -Dm755 safeupdate.so -t "$pkgdir"/usr/lib/postgresql/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
# vim:set ts=2 sw=2 et:
