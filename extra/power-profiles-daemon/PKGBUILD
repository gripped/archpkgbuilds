# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tinywrkb <tinywrkb@gmail.com>

pkgname=power-profiles-daemon
pkgver=0.22
pkgrel=2
pkgdesc='Makes power profiles handling available over D-Bus'
url='https://gitlab.freedesktop.org/upower/power-profiles-daemon'
license=(GPL-3.0-only)
arch=(x86_64)
depends=(gcc-libs
         glib2
         glibc
         libgudev
         polkit
         upower)
optdepends=('python-gobject: for powerprofilesctl')
makedepends=(git
             glib2-devel
             meson)
checkdepends=(python-dbusmock
              python-isort
              python-mccabe
              umockdev)
source=(git+https://gitlab.freedesktop.org/upower/$pkgname#tag=$pkgver)
sha256sums=('1bc3965288d36554fcfa56d3de7dc1b16e09204205cf0af23bd47007a91530ec')

prepare() {
  cd $pkgname
  # Fixes AMD GPU Regression
  # https://gitlab.freedesktop.org/upower/power-profiles-daemon/-/issues/163
  git cherry-pick -n 21b58819edcbbd87fc161474d1501debfd9b84b7
}
build() {
  meson $pkgname build \
    --prefix /usr \
    --libexec lib \
    --sysconfdir /usr/share
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
