# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyterlab_widgets
pkgname=${_pipname/_/-}
pkgver=3.0.16
pkgrel=2
pkgdesc='JupyterLab extension to use ipywidgets'
arch=(any)
url='https://jupyter.org/'
license=(BSD-3-Clause)
depends=(jupyterlab
         python
         python-ipywidgets)
makedepends=(python-build
             python-hatch-jupyter-builder
             python-hatchling
             python-installer)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
replaces=(jupyter-widgetsnbextension)
sha256sums=('423da05071d55cf27a9e602216d35a3a65a3e41cdf9c5d3b643b814ce38c19e0')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
