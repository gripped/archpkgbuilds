# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyterlab_widgets
pkgname=${_pipname/_/-}
pkgver=3.0.13
pkgrel=2
pkgdesc='JupyterLab extension to use ipywidgets'
arch=(any)
url='https://jupyter.org/'
license=(BSD-3-Clause)
depends=(jupyterlab
         python
         python-ipywidgets)
makedepends=(python-build
             python-installer
             python-jupyter_packaging
             python-setuptools)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
replaces=(jupyter-widgetsnbextension)
sha256sums=('a2966d385328c1942b683a8cd96b89b8dd82c8b8f81dda902bb2bc06d46f5bed')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
