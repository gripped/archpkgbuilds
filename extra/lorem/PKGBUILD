# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=lorem
pkgver=1.6
pkgrel=1
pkgdesc='Simple app to generate the Lorem Ipsum placeholder text'
arch=(x86_64)
url='https://apps.gnome.org/Lorem/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/World/design/lorem.git#tag=$pkgver")
b2sums=(d9b6adf8456355f80cd55b5b02259bd5a81cc018ab6bb1c0d6cb145b2c989ae1c6ae7d24bde8845b5e28f02bd29441f423e6687fc7a870ae47a65cbc34836e1d)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build \
    -D install_fonts=false

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
