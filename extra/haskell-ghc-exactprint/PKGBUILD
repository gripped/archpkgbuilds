# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-exactprint
pkgname=haskell-ghc-exactprint
pkgver=1.7.1.0
pkgrel=5
pkgdesc="ExactPrint for GHC"
url="https://github.com/alanz/ghc-exactprint.git"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default' 'haskell-free' 'haskell-ordered-containers'
         'haskell-syb')
makedepends=('ghc' 'uusi' 'haskell-diff' 'haskell-extra' 'haskell-ghc-paths' 'haskell-hunit'
             'haskell-filemanip' 'haskell-silently')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('47a84bed1a6cfec3a62905cf7990d1e0edb35d1a443a301b74cde1b87abd9773b7400db41453225db46200df55c0c317777a384311560546224c29eabf61b9c2')

prepare() {
  cd $_hkgname-$pkgver
  uusi -d fail
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
