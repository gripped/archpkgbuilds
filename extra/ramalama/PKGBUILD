# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: erdii <me at erdii dot engineering>
pkgname=ramalama
pkgver=0.14.0
pkgrel=1
pkgdesc="Simple local AI using OCI containers"
arch=('any')
url="https://github.com/containers/ramalama"
license=('MIT')
depends=('python-argcomplete' 'python-jsonschema' 'python-jinja' 'python-yaml')
makedepends=(python-build python-installer python-wheel python-setuptools)
source=("$pkgname-$pkgver.tar.gz::https://github.com/containers/ramalama/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('ad83954398b1519d47711f93204eeca8239e6c62bdbf93d3020438391bcd1dd8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
