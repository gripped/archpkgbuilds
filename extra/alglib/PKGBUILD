# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Aetf <aetf at unlimitedcodeworks dot xyz>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Peng Zhang <pczhang@gmail.com>

pkgname=alglib
pkgver=4.06.0
pkgrel=1
pkgdesc="A cross-platform numerical analysis and data processing library - Free Version"
arch=('x86_64')
url="http://wwws.alglib.net"
license=('GPL-2.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cmake')
source=("https://www.alglib.net/translator/re/alglib-${pkgver}.cpp.gpl.tgz")
sha512sums=('d48f38b1a3484e70e4b3f8d7569a3068177bdda2e35e4f5c8af77c61756e160419a4225f44b2447276606676a92106e34034765eaa6bc6550371b2e06ffee7ed')

prepare() {
  # Generate a simple cmake file to build the library since the release
  # doesn't provide any build system.
  cat > alglib-cpp/CMakeLists.txt << END
cmake_minimum_required(VERSION 4.0)
project(alglib VERSION ${pkgver})

file(GLOB SRCS src/*.cpp)
file(GLOB HEADERS src/*.h)

add_library(alglib_static STATIC \${SRCS})
set_target_properties(alglib_static PROPERTIES
    OUTPUT_NAME alglib
    PUBLIC_HEADER "\${HEADERS}"
)

add_library(alglib SHARED \${SRCS})
set_target_properties(alglib PROPERTIES
    VERSION "\${PROJECT_VERSION}"
    SOVERSION "\${PROJECT_VERSION_MAJOR}"
    PUBLIC_HEADER "\${HEADERS}"
)

include(GNUInstallDirs)
install(TARGETS alglib alglib_static
    LIBRARY DESTINATION "\${CMAKE_INSTALL_LIBDIR}"
    ARCHIVE DESTINATION "\${CMAKE_INSTALL_LIBDIR}"
    PUBLIC_HEADER DESTINATION "\${CMAKE_INSTALL_INCLUDEDIR}/libalglib"
)
install(FILES manual.cpp.html DESTINATION "\${CMAKE_INSTALL_DOCDIR}")
END
}

build() {
  cmake -S alglib-cpp -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR=$pkgdir cmake --install build
}
