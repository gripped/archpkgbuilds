# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=intelli-shell
pkgver=3.0.0
pkgrel=1
pkgdesc="Like IntelliSense, but for shells"
arch=('x86_64')
url="https://github.com/lasantosr/intelli-shell"
license=('Apache-2.0')
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('5018bc99272520bef47408e2ae04fbb13344a6f1d714d958a0d4604ae9d0e538f5ee56524e10abb7d84bf58fe6b073e6fdb90248249c812756736791a021f14c')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
