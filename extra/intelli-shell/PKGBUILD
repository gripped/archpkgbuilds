# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=intelli-shell
pkgver=3.2.1
pkgrel=1
pkgdesc="Like IntelliSense, but for shells"
arch=('x86_64')
url="https://github.com/lasantosr/intelli-shell"
license=('Apache-2.0')
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('cadaf31bbf74be2055feb45181b9d01f7ab7182a550452efc45ef1b4e82bfc439bdaf6ea8e51acf3c8d35dc6e5587f8ebac28da00e08aa5d17e0652084d3efb4')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
