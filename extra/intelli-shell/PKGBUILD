# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=intelli-shell
pkgver=3.2.2
pkgrel=1
pkgdesc="Like IntelliSense, but for shells"
arch=('x86_64')
url="https://github.com/lasantosr/intelli-shell"
license=('Apache-2.0')
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('515471438363da957bfa5076481ebada46b40f0d439b9c3a2248ba56a5396583cc8fa8fc65e14e408b70a0d489163967c534d0524c3fd7dc2b76645571568f01')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
