# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=intelli-shell
pkgver=3.4.0
pkgrel=1
pkgdesc="Like IntelliSense, but for shells"
arch=('x86_64')
url="https://github.com/lasantosr/intelli-shell"
license=('Apache-2.0')
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c164d8e039233ae12503bd7c730fe090daa321969871e781241d5c1468caa0d53a61c009c1e6e0d042fcc9646a2325757dcb5e9ad21c0917e9aebd70b01dd88b')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
