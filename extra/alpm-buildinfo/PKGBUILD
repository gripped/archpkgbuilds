# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-buildinfo
_upstream=alpm
pkgver=0.5.0
pkgrel=1
pkgdesc="A CLI for writing and parsing of ALPM BUILDINFO files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-buildinfo/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('e6cc68eb8330ef77b425c96114fb1f42404695297b6c89e9dc0916da3f2c3cb7044eb717c7ddb403754975777d0fe4e03a7bda6fc1276edb6ac255a41eedd147')
b2sums=('8f3bd0e7516df0fe955fb153b1e65c16fd024e55bd9f999bf9aec25bab122bb0370cf7e9917d4bd361e81b5814a65f2bf727c9f82dfe555522209a187d326cd2')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate specifications $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  # NOTE: for now only install the versioned manpages, because pacman also still provides a BUILDINFO.5
  install -vDm 644 output/manpages/*v{1,2}.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
