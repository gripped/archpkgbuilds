# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-buildinfo
_upstream=alpm
pkgver=0.5.1
pkgrel=1
pkgdesc="A CLI for writing and parsing of ALPM BUILDINFO files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-buildinfo/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('004fc1ee3c7ecd4c3077223278db9a9fded0a5a79fc9e1fc030ba8fbe792311e523854ca407fb04cd3766fd18a191a75802d9257307f6ed12bdd0d5c32f1ed18')
b2sums=('9fa58b7678562da774b68a1ce093df562bef33d30371f48f677e65ac53cc5e63745f2146685d16d89f0a21d80719862f832a8f116940f183f5f2b9af85a1adde')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate specifications $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  # NOTE: for now only install the versioned manpages, because pacman also still provides a BUILDINFO.5
  install -vDm 644 output/manpages/*v{1,2}.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
