# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-buildinfo
_upstream=alpm
pkgver=0.5.2
pkgrel=1
pkgdesc="A CLI for writing and parsing of ALPM BUILDINFO files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-buildinfo/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('02e42cd2010c4da3c73ed27df26dadd698682ec021ef2846440e34165ecc74f8dfdb3aead4b70030950ebe77e9dc70dc625f3ef108c9c7a097368699956f5457')
b2sums=('a52ed1534fbc154bcfe31dbfc4192181c0e5d38fab69ef04da945e74960e2197b29a2bbde1d5196ddf84b782d95f9a85bd6ebd67904263599e635c4049133712')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate specifications $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  # NOTE: for now only install the versioned manpages, because pacman also still provides a BUILDINFO.5
  install -vDm 644 output/manpages/*v{1,2}.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
