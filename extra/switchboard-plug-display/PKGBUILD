# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-display
pkgver=8.0.1
pkgrel=1
pkgdesc='Switchboard Display Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-display
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=da6d0a3bf58add3934119a3b481571428a988893
source=(git+https://github.com/elementary/switchboard-plug-display.git#tag=${_tag})
b2sums=('8ac08ff7a857448eaa9db5e18a2f9ee03aa5056fa3d3d606da6b408e10cf04ccae5307c47b660c79a92a92d523b676215e36c71c94a11c6475064d6316ce5b63')

pkgver() {
  cd switchboard-plug-display
  git describe --tags
}

build() {
  arch-meson switchboard-plug-display build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
