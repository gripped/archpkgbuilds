# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-display
pkgver=8.0.2
pkgrel=1
pkgdesc='Switchboard Display Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-display
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=1e29b9c614695128ed4738da17e878e7b23080c2
source=(git+https://github.com/elementary/switchboard-plug-display.git#tag=${_tag})
b2sums=('a2a40fe5c1022ff835156cc21565c078400819cc3e60a85703bf17ab64f22361431dc89cdf76745ae5be88fd865070ad95269ac3d3281ea29cb338727fa884db')

pkgver() {
  cd switchboard-plug-display
  git describe --tags
}

build() {
  arch-meson switchboard-plug-display build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
