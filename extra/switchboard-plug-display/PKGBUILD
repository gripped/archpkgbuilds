# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-display
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Display Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-display
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=310d75149780b94fe959f7fe576927c84b44ae05
source=(git+https://github.com/elementary/switchboard-plug-display.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-display
  git describe --tags
}

build() {
  arch-meson switchboard-plug-display build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
