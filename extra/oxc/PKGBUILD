# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgbase=oxc
pkgname=(oxlint oxc-language-server)
pkgver=0.16.7
pkgrel=1
pkgdesc="A collection of JavaScript tools written in Rust"
arch=(x86_64)
url="https://github.com/oxc-project/oxc"
license=('MIT')
depends=('gcc-libs')
options=('!lto')
makedepends=('rust' 'cargo' 'nodejs' 'cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/oxc-project/oxc/archive/refs/tags/oxlint_v${pkgver}.tar.gz")
sha256sums=('8897d89d5d8ae9d30906f579225fd0e86ed5bca668693f7e454d185ffb5475f4')

prepare() {
  cd "oxc-oxlint_v${pkgver}"

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
}

build() {
  cd "oxc-oxlint_v${pkgver}"

  cargo build --frozen --release --all-features
}

package_oxlint() {
  pkgdesc="Oxc JavaScript linter"
  cd "oxc-oxlint_v${pkgver}"

  install -Dm755 target/release/oxlint "$pkgdir/usr/bin/oxlint"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_oxc-language-server() {
  pkgdesc="Oxc JavaScript linter language server"
  cd "oxc-oxlint_v${pkgver}"

  # nvim-lspconfig https://github.com/jelly/Dotfiles/commit/9924ec1ef8785185c6cab8410589864df042f710
  install -Dm755 target/release/oxc_language_server "$pkgdir/usr/bin/oxc_language_server"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
