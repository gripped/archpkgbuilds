# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-x-wr-timezone
pkgver=2.0.0
pkgrel=2
pkgdesc='Handling of non-standard X-WR-TIMEZONE icalendar property in Python'
arch=(any)
url='https://github.com/niccokunzmann/x-wr-timezone'
license=(LGPL-3.0-or-later)
depends=(
  python
  python-click
  python-icalendar
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("git+https://github.com/niccokunzmann/x-wr-timezone.git#tag=v$pkgver")
b2sums=(579bb15519a93d05f4ba680cb0ba4a670b434cea895b083404c8af8f69075c77ccdfc6b4a3eb981d1133e2d09b66ce0d7415c3ba23fee27754077bf04441744c)

build() {
  cd x-wr-timezone
  python -m build --wheel --no-isolation
}

check() {
  cd x-wr-timezone
  pytest --ignore=test/test_command_line.py --ignore=test/test_readme.py --ignore=test/test_issue_4_add_vtimezone.py
}

package() {
  cd x-wr-timezone
  python -m installer --destdir="$pkgdir" dist/*.whl
}
