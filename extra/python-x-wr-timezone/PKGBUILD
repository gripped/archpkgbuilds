# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-x-wr-timezone
pkgver=2.0.1
pkgrel=2
pkgdesc='Handling of non-standard X-WR-TIMEZONE icalendar property in Python'
arch=(any)
url='https://github.com/niccokunzmann/x-wr-timezone'
license=(LGPL-3.0-or-later)
depends=(
  python
  python-click
  python-icalendar
)
makedepends=(
  git
  python-build
  python-installer
  python-pytz
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("git+https://github.com/niccokunzmann/x-wr-timezone.git#tag=v$pkgver")
b2sums=(55e0e38f6bb207fc9d505e57d42ee8502b6a8d2050267950dd350f62d4d7625f6b6031f9995f34820a873146180c9939c53a05ab8971f9da0291649579678e4a)

build() {
  cd x-wr-timezone
  python -m build --wheel --no-isolation
}

check() {
  cd x-wr-timezone
  pytest --ignore=test/test_command_line.py --ignore=test/test_readme.py --ignore=test/test_issue_4_add_vtimezone.py
}

package() {
  cd x-wr-timezone
  python -m installer --destdir="$pkgdir" dist/*.whl
}
