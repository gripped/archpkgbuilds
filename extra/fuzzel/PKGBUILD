# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fuzzel
pkgver=1.11.1
pkgrel=1
pkgdesc='Application launcher for wlroots based Wayland compositors'
url="https://codeberg.org/dnkl/$pkgname"
arch=(x86_64)
license=(MIT)
depends=(cairo
         fcft
         fontconfig
         libfcft.so
         libpng
         libxkbcommon
         pixman
         wayland) # nanosvg is bundled
makedepends=(meson
             scdoc
             tllist
             wayland-protocols)
backup=('etc/xdg/fuzzel/fuzzel.ini')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('7b22fefdada10e4658ec1f914a7296ddde73a5d20a2a1ed3c02c50bf2e701d3b')

build() {
	cd "$pkgname"
	arch-meson \
		-Denable-cairo=enabled \
		-Dpng-backend=libpng \
		-Dsvg-backend=nanosvg \
		. build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
