# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-power-manager
pkgver=3.1.1
pkgrel=1
pkgdesc="Power management tool for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-power-manager"
groups=('ukui')
depends=('qt5-x11extras' 'ukui-interface' 'kwindowsystem5')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-power-manager/archive/v$pkgver.tar.gz")
sha512sums=('7fbfc947330806617cc8befab92d61c3dbb1ccb15afc4bfef6e67059588c10b8f883db10b490ef104eb82e44d7e23b782f6f60521d3ab32efd63780e4878b2b2')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|/etc/dbus-1/system.d/|/usr/share/dbus-1/system.d/|' registeredQDbus/registeredQDbus.pro
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
