# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall
pkgver=1.42.3
pkgrel=9
pkgdesc='A configuration language guaranteed to terminate'
url='https://dhall-lang.org'
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-ansi-terminal'
         'haskell-atomic-write' 'haskell-base16-bytestring' 'haskell-case-insensitive'
         'haskell-cborg' 'haskell-cborg-json' 'haskell-contravariant' 'haskell-cryptohash-sha256'
         'haskell-cryptonite' 'haskell-data-fix' 'haskell-diff' 'haskell-dotgen' 'haskell-either'
         'haskell-half' 'haskell-hashable' 'haskell-http-client' 'haskell-http-client-tls'
         'haskell-http-types' 'haskell-indexed-traversable' 'haskell-megaparsec'
         'haskell-microlens' 'haskell-microlens-mtl' 'haskell-mmorph' 'haskell-network-uri'
         'haskell-optparse-applicative' 'haskell-parsers' 'haskell-parser-combinators'
         'haskell-prettyprinter' 'haskell-prettyprinter-ansi-terminal' 'haskell-pretty-simple'
         'haskell-repline' 'haskell-serialise' 'haskell-scientific' 'haskell-text-manipulate'
         'haskell-text-short' 'haskell-th-lift-instances' 'haskell-unix-compat'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-doctest' 'haskell-foldl' 'haskell-generic-random' 'haskell-mockery'
             'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-special-values'
             'haskell-spoon' 'haskell-tasty' 'haskell-tasty-expected-failure' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-tasty-silver' 'haskell-temporary' 'haskell-turtle')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('95e346711c5a1d582c76c131226665e9ce5f8700495b89b51b0fa02192b7fd27857bcab1f5dc64327449dd307c263ef9e921e1897ac88dd77487953858cc90b5')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
