# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-srcache
pkgver=0.33
pkgrel=11

_modname="${pkgname#nginx-mod-}"

pkgdesc='Nginx module for transparent subrequest-based caching layout for arbitrary locations'
arch=('x86_64')
makedepends=('nginx' 'nginx-src')
url="https://github.com/openresty/srcache-nginx-module"
license=('BSD-2-Clause')

source=(https://github.com/openresty/$_modname-nginx-module/archive/v$pkgver/$_modname-$pkgver.tar.gz)
sha256sums=('1ffd3e244a52bc2ad31661b038fa20fab3145dea506d97f53c2ac80b55eb38d3')
backup=('etc/nginx/modules.d/20-srcache.conf')

prepare() {
	mkdir -p build
	cd build
	ln -sf /usr/src/nginx/auto
	ln -sf /usr/src/nginx/src
}

build() {
	cd build
	auto/configure \
		--with-ld-opt="$LDFLAGS" \
		--with-compat \
		--add-dynamic-module=../$_modname-nginx-module-$pkgver
	make modules
}

package() {
	local _nginx_version=$(nginx -v 2>&1)
	_nginx_version=${_nginx_version/* nginx\/}
	depends+=("nginx=${_nginx_version}")

	install -Dm644 "$srcdir"/$_modname-nginx-module-$pkgver/README.markdown \
	               "$pkgdir"/usr/share/licenses/$pkgname/README.markdown
	install -dm0755 "$pkgdir"/etc/nginx/modules.d

	cd build/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
		echo "load_module \"/usr/lib/nginx/modules/$mod\";" >> "$pkgdir/etc/nginx/modules.d/20-srcache.conf"
	done
}
