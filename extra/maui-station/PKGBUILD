# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maui-station
_pkgname=station
pkgver=4.0.2
pkgrel=1
pkgdesc='Convergent terminal emulator'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         mauikit
         mauikit-filebrowsing
         mauikit-terminal
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$_pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('09da03d5195532e513a1ed4eb7052b9e395e85064bd819d664e75736291f4f54'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
