# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Ovi Chis ovi@ovios.org

pkgname=tgt
pkgver=1.0.95
pkgrel=1
pkgdesc="iSCSI Target STGT for Arch Linux"
arch=('x86_64')
url='https://github.com/fujita/tgt'
license=('GPL-2.0-only')
depends=(
  'bash'
  'perl'
  'perl-config-general'
  'rdma-core'
  'systemd-libs'
)
makedepends=(
  'docbook-xsl'
  'git'
  'libxslt'
  'linux-api-headers'
)
optdepends=(
  'sg3_utils'
)
backup=(
  'etc/tgt/targets.conf'
)
source=(git+https://github.com/fujita/tgt.git#tag=v${pkgver})
b2sums=('d9046363d78b5b486eec0f3b16a5f98fcd614ade47c06085525d53b88c317fc309571a4db727514f3e6167d301d7efd73df31dc2ff8fcc0c2a192c32833d342b')

prepare() {
  cd ${pkgname}
  sed -i '/LDFLAGS =/d' usr/Makefile
  sed -i 's#\$(CC) \$^ -o \$@$#\$(CC) \$^ -o \$@ $(LDFLAGS)#g' usr/Makefile
}

build() {
  cd ${pkgname}
  make sbindir=/usr/bin SD_NOTIFY=1 ISCSI=1 ISCSI_RDMA=1 PREFIX="${pkgdir}"
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" sbindir=/usr/bin install
  install -Dm644 scripts/tgtd.service "${pkgdir}"/usr/lib/systemd/system/tgtd.service
}

