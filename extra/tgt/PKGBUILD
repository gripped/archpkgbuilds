# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Ovi Chis ovi@ovios.org

pkgname=tgt
pkgver=1.0.96
pkgrel=2
pkgdesc="iSCSI Target STGT for Arch Linux"
arch=('x86_64')
url='https://github.com/fujita/tgt'
license=('GPL-2.0-only')
depends=(
  'bash'
  'perl'
  'perl-config-general'
  'rdma-core'
  'systemd-libs'
)
makedepends=(
  'docbook-xsl'
  'git'
  'libxslt'
  'linux-api-headers'
)
optdepends=(
  'sg3_utils'
)
backup=(
  'etc/tgt/targets.conf'
)
source=(git+https://github.com/fujita/tgt.git#tag=v${pkgver})
b2sums=('a2e31a4f15e067ab94d17345659faf0c5390304aae8b54d2732b8b33e65b7bfc3915186eab9cfc5250ab521c32ce25c0a725b0fd34d67f08d6891c211d60df73')

prepare() {
  cd ${pkgname}
  sed -i '/LDFLAGS =/d' usr/Makefile
  sed -i 's#\$(CC) \$^ -o \$@$#\$(CC) \$^ -o \$@ $(LDFLAGS)#g' usr/Makefile
}

build() {
  cd ${pkgname}
  make sbindir=/usr/bin SD_NOTIFY=1 ISCSI=1 ISCSI_RDMA=1 PREFIX="${pkgdir}"
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" sbindir=/usr/bin install
  install -Dm644 scripts/tgtd.service "${pkgdir}"/usr/lib/systemd/system/tgtd.service
}

