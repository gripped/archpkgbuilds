# Maintainer: SÃ©bastien Luttringer
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=bird
pkgver=3.1.1
pkgrel=1
pkgdesc='RIP, OSPF, BGP, MPLS, BFD, Babel routing daemon'
arch=('x86_64')
url='https://bird.network.cz/'
license=('GPL-2.0-or-later')
depends=('gcc-libs' 'glibc' 'readline' 'ncurses' 'libssh')
replaces=('bird6')
backup=('etc/bird.conf')
source=("https://gitlab.nic.cz/labs/bird/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz"
        'bird.service')
sha256sums=('d96f65ab31bc88535d86673e4dc1f54d3534028f03aecdc77ca0a3942e41674e'
            '4aa1e8d41229badd276aa3747f613e7df34761892add2258c63bdb5097dfeb2b')

prepare() {
  cd $pkgname-v$pkgver
  autoreconf -vif
}

build() {
  cd $pkgname-v$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --runstatedir=/run/$pkgname \
    --docdir=/usr/share/doc/$pkgname
  make
}

check() {
  cd $pkgname-v$pkgver
  make check
}

package() {
  cd $pkgname-v$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/run" # Remove installed runstatedir, should not be packaged.

  # systemd
  install -vD -m 644 -t "$pkgdir/usr/lib/systemd/system" "$srcdir/bird.service"
}

# vim:set ts=2 sw=2 et:
