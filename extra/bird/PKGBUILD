# Maintainer: SÃ©bastien Luttringer
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=bird
pkgver=3.2.0
pkgrel=1
pkgdesc='RIP, OSPF, BGP, MPLS, BFD, Babel routing daemon'
arch=('x86_64')
url='https://bird.network.cz/'
license=('GPL-2.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
  'libssh'
  'ncurses'
  'readline'
)
replaces=('bird6')
backup=('etc/bird.conf')
source=("https://gitlab.nic.cz/labs/bird/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz"
        'bird.service')
b2sums=('e83801ee645066b81d7d3525d8ae13fa29aa2d4a7aeab4adc4d7cb7813d8db1c236d330c438be5fc365084c154e3d38fe41ae72afb5b7619d5013fb70665a317'
        '03ba113301babdba0f12974157386dab76603e84ee868af9c21718184a8fc87b7a1bc8e363258e76cca6397781d93a9f7210f49c3f602154ed4e2d6730c76e03')

prepare() {
  cd $pkgname-v$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-v$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --runstatedir=/run/$pkgname \
    --docdir=/usr/share/doc/$pkgname
  make
}

check() {
  cd $pkgname-v$pkgver
  make check
}

package() {
  cd $pkgname-v$pkgver
  make DESTDIR="$pkgdir" install
  rm -vr "$pkgdir/run" # Remove installed runstatedir, should not be packaged.
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" "$srcdir/bird.service"
}
