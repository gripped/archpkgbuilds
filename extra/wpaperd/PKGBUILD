# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Firegem <mrfiregem [at] protonmail [dot] ch>

pkgname=wpaperd
pkgver=1.2.1
pkgrel=1
pkgdesc="Modern wallpaper daemon for Wayland"
url="https://github.com/danyspin97/wpaperd"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('libxkbcommon' 'mesa' 'wayland')
makedepends=('dav1d' 'cargo' 'scdoc' 'git')
optdepends=('dav1d: for loading avif images')
source=("git+${url}.git?signed#tag=${pkgver}")
sha256sums=('76ba2005482000ec50dc6bf04e6bc51088bdd097f8aee959b8c5d6efcda7f20b')
validpgpkeys=('DCBD48B9859422FA548BA46B1733BCAC9AF70936'  # Danilo Spinella <danilo.spinella@suse.com>
              'E82A1BBF9952D5EDB765326AF8D0194CC049819F') # Danilo Spinella <oss@danyspin97.org>
options=(!lto)

prepare() {
	cd "${pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features
	scdoc < man/wpaperd-output.5.scd > man/wpaperd-output.5
}

check() {
	cd "${pkgname}"
	export RUSTUP_TOOLCHAIN=stable
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}"
	install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}" 
	install -Dm 755 target/release/wpaperctl "${pkgdir}/usr/bin/wpaperctl"

	install -Dm 644 "target/release/completions/${pkgname}.bash" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
	install -Dm 644 "target/release/completions/${pkgname}.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
	install -Dm 644 target/release/completions/wpaperctl.bash "${pkgdir}/usr/share/bash-completion/completions/wpaperctl"
	install -Dm 644 target/release/completions/wpaperctl.fish "${pkgdir}/usr/share/fish/vendor_completions.d/wpaperctl.fish"

	install -Dm 644 "man/${pkgname}-output.5" "${pkgdir}/usr/share/man/man5/${pkgname}-output.5"
	install -Dm 644 "target/release/man/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
	install -Dm 644 target/release/man/wpaperctl.1 "${pkgdir}/usr/share/man/man1/wpaperctl.1"

	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
