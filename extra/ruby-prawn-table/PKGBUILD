# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Anatoly Bashmakov <anatoly at posteo dot net>

pkgname=ruby-prawn-table
_pkgname=${pkgname#ruby-}
pkgver=0.2.2
pkgrel=9
pkgdesc="Provides support for tables in Prawn"
arch=(any)
url="https://github.com/prawnpdf/prawn-table"
license=('GPL-2.0-only OR GPL-3.0-only OR Ruby')
depends=(
  ruby
  ruby-prawn
)
makedepends=(rubygems)
checkdepends=(
  ruby-mocha
  ruby-pdf-inspector
  ruby-rspec
)
options=(!emptydirs)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  "remove-failing-tests.patch"
)
sha256sums=(
  '25f0bcdc6ffbb0ff3746f96d02e083cbbaf1c71cd928176a23b2bd5e77ccc377'
  '68873f4ea2242f16d1bc25c026c83739935133b907f272f9e3ddc4632037d492'
)

prepare() {
  cd "$_pkgname-$pkgver"
  sed -i -E 's|~>|>=|g' "$_pkgname.gemspec"
  sed -i '/bundler/Id' spec/spec_helper.rb
  # Remove 5 failing tests - 4 of them due to depending on image not packaged
  # with Prawn, 1 due incompatible with recent rspec.
  patch --forward --strip=1 --input="$srcdir/remove-failing-tests.patch"
}

build() {
  cd "$_pkgname-$pkgver"
  local gemdir="$(gem env gemdir)"

  gem build "$_pkgname.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "$_pkgname-$pkgver.gem"

  rm --force --recursive --verbose \
    "tmp_install/$gemdir/cache/" \
    "tmp_install/$gemdir/gems/$_pkgname-$pkgver/vendor/" \
    "tmp_install/$gemdir/doc/$_pkgname-$pkgver/ri/ext/"

  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
    -iname "*.o" -o \
    -iname "*.c" -o \
    -iname "*.so" -o \
    -iname "*.time" -o \
    -iname "gem.build_complete" -o \
    -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/$gemdir/extensions/" \
    -type f \
    \( \
    -iname "mkmf.log" -o \
    -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "$_pkgname-$pkgver"
  GEM_HOME="tmp_install/$(gem env gemdir)" rspec
}

package() {
  cd "$_pkgname-$pkgver"
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
