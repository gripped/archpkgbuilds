# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-sphinx-tabs
pkgver=3.4.7
pkgrel=3
pkgdesc='Create tabbed content in Sphinx documentation'
arch=('any')
url='https://sphinx-tabs.readthedocs.io/'
license=('MIT')
depends=(
  'python'
  'python-sphinx'
  'python-docutils'
  'python-pygments'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
source=("$pkgname::git+https://github.com/executablebooks/sphinx-tabs#tag=v$pkgver")
sha512sums=('6e6dbb4e9593526d89aa8435c1ad14359c9a37d38690cc473a570f80b8ea107d7a96a8b41f6fbf21bc404d99b01cd2df2519443ea672e14488800b1afa5fd38a')
b2sums=('85bc80d191dbe60b64595871213758e62fc1f90ce631bcf6ff941a29403aa1ba8ebe226df806883263e6b555b778e6fbf1216e84bb3c3cc67e5c28216c00b566')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
