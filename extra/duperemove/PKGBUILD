# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Robin Broda <coderobe @ archlinux.org>

pkgname=duperemove
pkgver=0.15.1
pkgrel=1
pkgdesc='Btrfs extent deduplication utility'
arch=('x86_64')
url='https://github.com/markfasheh/duperemove'
license=('GPL-2.0-only')
depends=('libbsd' 'glib2' 'sqlite')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('ab8b2e979d590b9a90c2bdb541e50e176979536fb5294204509f3f70f85b6997e579ec0d0f86b7ef719ef53503ff59a2e92d7febaa3f22679b7fcb0a581f3f6c')

build() {
  cd "${pkgname}-${pkgver}"
  CFLAGS+=" --std=c23" make VERSION="${pkgver}" IS_RELEASE=1
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr SBINDIR=/usr/bin DESTDIR="${pkgdir}" VERSION="${pkgver}" IS_RELEASE=1 install
}
