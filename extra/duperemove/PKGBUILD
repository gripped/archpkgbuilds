# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Robin Broda <coderobe @ archlinux.org>

pkgname=duperemove
pkgver=0.14.1
pkgrel=2
pkgdesc='Btrfs extent deduplication utility'
arch=('x86_64')
url='https://github.com/markfasheh/duperemove'
license=('GPL')
depends=('glib2' 'sqlite')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('16af9fb002d6e51ce469869507d5d4b8f9576620746337252ad733e6f74ae275c1e83da6af1c203543b9d19890180952d87f9fad4a02d4c4e0a25577ce569191')

build() {
  cd "${pkgname}-${pkgver}"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make VERSION="${pkgver}" IS_RELEASE=1
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr SBINDIR=/usr/bin DESTDIR="${pkgdir}" VERSION="${pkgver}" IS_RELEASE=1 install
}
