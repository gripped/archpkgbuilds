# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.12.0
pkgrel=1
pkgdesc='Image compressor GUI, supporting PNG, JPEG, WebP and SVG file types, both lossless and lossy compression mode'
arch=(any)
url='https://apps.gnome.org/Curtail/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  jpegoptim
  libadwaita
  libwebp
  oxipng
  pngquant
  python
  python-gobject
  scour
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://github.com/Huluti/Curtail.git#tag=$pkgver")
b2sums=('f1e6fd22977b62fb3bdafaa0c5ce1921c1c0986d0b672c0ae55daac174d0333e71ea099aafe848da4c9e356674053b5f1b6e5f8aad4a78d623bae2e0967faa4d')

build() {
  arch-meson Curtail build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
