# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.13.0
pkgrel=1
pkgdesc='Image compressor GUI, supporting PNG, JPEG, WebP and SVG file types, both lossless and lossy compression mode'
arch=(any)
url='https://apps.gnome.org/Curtail/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  jpegoptim
  libadwaita
  libwebp
  oxipng
  pngquant
  python
  python-gobject
  scour
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://github.com/Huluti/Curtail.git#tag=$pkgver")
b2sums=('0f6750bf65d81f110863048a94bafca42b26cec386681fb33848a0ecf402c4351b880b730c023255f52977f3a824679e127365906ae9c31b90bf36d8b2c2a0a4')

build() {
  arch-meson Curtail build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
