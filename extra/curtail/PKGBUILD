# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.14.0
pkgrel=1
pkgdesc='Image compressor GUI, supporting PNG, JPEG, WebP and SVG file types, both lossless and lossy compression mode'
arch=(any)
url='https://apps.gnome.org/Curtail/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  jpegoptim
  libadwaita
  libwebp
  oxipng
  pngquant
  python
  python-gobject
  scour
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://github.com/Huluti/Curtail.git#tag=$pkgver")
b2sums=(67692ff7b333880da2d3269aef03ee39b1334b6bd1aa4cbd0733050c54c4a9813bf55ba84889ef6a6528bda108d54085b69f6b0d2ae3a709d0ba7b16db964fba)

prepare() {
  cd Curtail
}

build() {
  arch-meson Curtail build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
