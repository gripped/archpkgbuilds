# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.11.0
pkgrel=1
pkgdesc='Image compressor GUI, supporting PNG, JPEG, WebP and SVG file types, both lossless and lossy compression mode'
arch=(any)
url='https://apps.gnome.org/Curtail/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  jpegoptim
  libadwaita
  libwebp
  oxipng
  pngquant
  python
  python-gobject
  scour
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://github.com/Huluti/Curtail.git#tag=$pkgver")
b2sums=(ddb9faa591b8aee199972a2df035179cbde7dddec0e4593fcd8643feb41a503c36f053733385c9c4c64fec55f006bbd58572d9dd617a198ef3c6f307dd7cc340)

build() {
  arch-meson Curtail build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
