# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>
# Contributor: dserban <dserban01@yahoo.com>

pkgname=switchboard
pkgver=8.0.2
pkgrel=1
pkgdesc='The Pantheon Control Center'
arch=(x86_64)
url=https://github.com/elementary/switchboard
license=(LGPL-2.1-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgee
  libgranite-7.so
)
makedepends=(
  cmake
  git
  intltool
  meson
  sassc
  vala
)
optdepends=(
  'switchboard-plug-about: About plug'
  'switchboard-plug-applications: Applications plug'
  'switchboard-plug-datetime: Date & Time plug'
  'switchboard-plug-desktop: Desktop plug'
  'switchboard-plug-display: Display plug'
  'switchboard-plug-elementary-tweaks: Elementary Tweaks plug'
  'switchboard-plug-keyboard: Keyboard plug'
  'switchboard-plug-locale: Locale plug'
  'switchboard-plug-network: Network plug'
  'switchboard-plug-notifications: Notifications plug'
  'switchboard-plug-power: Power plug'
  'switchboard-plug-security-privacy: Security & Privacy plug'
)
provides=(libswitchboard-3.so)
_tag=efd9be85b539f7ff816d5f1e24f1e478b3593e1f
source=(git+https://github.com/elementary/switchboard.git#tag=${_tag})
b2sums=('f5f7c60613e4a50f60157efffd4904bbdb1b75b0d84fa3d5a768eb96ea151f8dffe503c2183671c7c95ee3f9f361c85dc6acdb6d625d7b0de80e6d86b8d86894')

pkgver() {
  cd switchboard
  git describe --tags
}

build() {
  arch-meson switchboard build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
