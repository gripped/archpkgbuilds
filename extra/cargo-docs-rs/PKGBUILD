# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-docs-rs
pkgver=1.0.1
pkgrel=1
pkgdesc="Imitate the documentation build that docs.rs would do"
arch=('x86_64')
url="https://github.com/dtolnay/cargo-docs-rs"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  "Cargo.lock")
sha512sums=('639e4555b17903701926834f4bfe24b95ecccd42c475e583e794a745aae63cb70887ff5221ac7fb6aa69497a844c41ea2d6865a30c60d4e7422d9037b2ce33e6'
            '281f33d3f5f5a3d8d2c96eb40529c5929f7a92f8c1c1c9fcddb67857c799cc75f747a3ebf889f2eb8c7beae533895022a3646329dbcd1808b5e074776150d17d')

prepare() {
  cd "$pkgname-$pkgver"
  cp "$srcdir/Cargo.lock" .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
