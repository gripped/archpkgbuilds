# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-docs-rs
pkgver=0.1.17
pkgrel=1
pkgdesc="Imitate the documentation build that docs.rs would do"
arch=('x86_64')
url="https://github.com/dtolnay/cargo-docs-rs"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  "Cargo.lock")
sha512sums=('03e64cb702042524b4ece4e8cbda4149e4ebc4046ec481f09e980fe8cceefe244c056b157ed9b9256fa0726da7ace8adf33086a00eeb53ef373278c0ed6eb199'
            '055846054dd7ed10dc9dce31f40e352f925485ea57b9064ef75460b0d43db30af44af56b0c576b840c26e11c4d7fca915356df56a98a413fe203b59e1169f53d')

prepare() {
  cd "$pkgname-$pkgver"
  cp "$srcdir/Cargo.lock" .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
