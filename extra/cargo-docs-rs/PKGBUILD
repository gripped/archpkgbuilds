# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-docs-rs
pkgver=0.1.15
pkgrel=1
pkgdesc="Imitate the documentation build that docs.rs would do"
arch=('x86_64')
url="https://github.com/dtolnay/cargo-docs-rs"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  "Cargo.lock")
sha512sums=('9284a12857d00eed1cd7d5375a896346c2390cf078528425c04023b2d188878624dc5022d39877bd7f4ac9c644bf498a9a5652f71153d9250482b00d6aae1070'
            'c733e94f88e6dd3fc2382a4241ca54db2f1a188ae601d9ea6b4d91b520683e142f36ff5e950178fa13abe5c23a0cceea7804ded6882e4ea684222461df4348cc')

prepare() {
  cd "$pkgname-$pkgver"
  cp "$srcdir/Cargo.lock" .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
