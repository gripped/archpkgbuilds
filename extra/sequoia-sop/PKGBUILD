# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sequoia-sop
pkgver=0.37.3
pkgrel=1
pkgdesc='Implementation of the Stateless OpenPGP Interface using Sequoia'
url='https://gitlab.com/sequoia-pgp/sequoia-sop'
arch=(x86_64)
license=(GPL-2.0-or-later)
groups=(
  sequoia
  stateless-openpgp
)
depends=(
  gcc-libs
  glibc
  gmp
  # this dependency is due to overlinking, but even though it's technically unused the linker is going to expect this .so to be present
  sqlite
)
makedepends=(
  git
  cargo
  clang
  nettle
)
source=(sequoia-sop::"git+$url#tag=v$pkgver?signed")
sha512sums=('255d83511d43ef782d82b756cf444dfb65a4d403e8bfec5c1049e76096f37cebc4136d92c0bff473d05f9711242aa287ab95beaa97d70260074f9d24177c680d')
b2sums=('7f95534da2ad0cc9b35282f097b04c77e865928324d19bcdc984768a94706dcf62142372e6626a027b4096b0d38f001013785d4db49517817ce490c2d819985b')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
  8F17777118A33DDA9BA48E62AACB3243630052D9  # Neal H. Walfield <neal@walfield.org>
)

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cd ${pkgname}
  export ASSET_OUT_DIR="$srcdir/${pkgname}"
  cargo build --release --frozen --features crypto-nettle,cli,cliv
}

check() {
  export RUSTUP_TOOLCHAIN=stable
  cd ${pkgname}
  cargo test --frozen --features crypto-nettle,cli,cliv
}

package() {
  depends+=(
    nettle libnettle.so libhogweed.so
  )

  cd ${pkgname}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/sqop \
    target/release/sqopv

  install -Dm 644 man-pages/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 shell-completions/{sqop,sqopv}.bash -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm 644 shell-completions/_{sqop,sqopv} -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm 644 shell-completions/{sqop,sqopv}.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
