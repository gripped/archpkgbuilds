# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sequoia-sop
pkgver=0.36.0
pkgrel=1
pkgdesc='Implementation of the Stateless OpenPGP Interface using Sequoia'
url='https://gitlab.com/sequoia-pgp/sequoia-sop'
arch=(x86_64)
license=(GPL-2.0-or-later)
groups=(
  sequoia
  stateless-openpgp
)
depends=(
  gcc-libs
  glibc
  gmp
  # this dependency is due to overlinking, but even though it's technically unused the linker is going to expect this .so to be present
  sqlite
)
makedepends=(
  bzip2
  git
  cargo
  clang
  nettle
)
source=(sequoia-sop::"git+$url#tag=v$pkgver?signed")
sha512sums=('02c63609ea66ab163592996dbbe7f919427a6795b4d31e6eff93104b659b468877c7fc18b716457e7ece6185c268fac98a6886eb0df32df27e9b9fc08b1dace3')
b2sums=('133957f24c5cbcffe40c0b522412f0e152b87b9fdf86722847138e4ef918bfd0455efae9405d3a464b7042abd3cc43aff649dfe0b6d15631e7f686627d872ead')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cd ${pkgname}
  export ASSET_OUT_DIR="$srcdir/${pkgname}"
  cargo build --release --frozen --all-features
}

check() {
  export RUSTUP_TOOLCHAIN=stable
  cd ${pkgname}
  cargo test --frozen --all-features
}

package() {
  depends+=(
    bzip2 libbz2.so
    nettle libnettle.so libhogweed.so
  )

  cd ${pkgname}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/sqop \
    target/release/sqopv

  install -Dm 644 man-pages/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 shell-completions/{sqop,sqopv}.bash -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm 644 shell-completions/_{sqop,sqopv} -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm 644 shell-completions/{sqop,sqopv}.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
