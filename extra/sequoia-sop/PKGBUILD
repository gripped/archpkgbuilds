# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sequoia-sop
pkgver=0.37.2
pkgrel=1
pkgdesc='Implementation of the Stateless OpenPGP Interface using Sequoia'
url='https://gitlab.com/sequoia-pgp/sequoia-sop'
arch=(x86_64)
license=(GPL-2.0-or-later)
groups=(
  sequoia
  stateless-openpgp
)
depends=(
  gcc-libs
  glibc
  gmp
  # this dependency is due to overlinking, but even though it's technically unused the linker is going to expect this .so to be present
  sqlite
)
makedepends=(
  bzip2
  git
  cargo
  clang
  nettle
)
source=(sequoia-sop::"git+$url#tag=v$pkgver?signed")
sha512sums=('deb869da031907c994c4a611f465d434b578d73c36d1f8a01e095840ed56f81eb609433d99f0a001f185fc98a5306ca2f500998e1eabb1fef88b49a63eb357f7')
b2sums=('461e999bcd4661d3090106f7f3600587631c9e3494527ae1ee6a62942241a60de01c7277acb2a493d10d14e2215926569f55ee47c7083ad4d718458d5ab12a64')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cd ${pkgname}
  export ASSET_OUT_DIR="$srcdir/${pkgname}"
  cargo build --release --frozen --all-features
}

check() {
  export RUSTUP_TOOLCHAIN=stable
  cd ${pkgname}
  cargo test --frozen --all-features
}

package() {
  depends+=(
    bzip2 libbz2.so
    nettle libnettle.so libhogweed.so
  )

  cd ${pkgname}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/sqop \
    target/release/sqopv

  install -Dm 644 man-pages/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 shell-completions/{sqop,sqopv}.bash -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm 644 shell-completions/_{sqop,sqopv} -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm 644 shell-completions/{sqop,sqopv}.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
