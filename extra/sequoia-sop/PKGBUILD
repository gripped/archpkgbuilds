# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sequoia-sop
pkgver=0.37.1
pkgrel=1
pkgdesc='Implementation of the Stateless OpenPGP Interface using Sequoia'
url='https://gitlab.com/sequoia-pgp/sequoia-sop'
arch=(x86_64)
license=(GPL-2.0-or-later)
groups=(
  sequoia
  stateless-openpgp
)
depends=(
  gcc-libs
  glibc
  gmp
  # this dependency is due to overlinking, but even though it's technically unused the linker is going to expect this .so to be present
  sqlite
)
makedepends=(
  bzip2
  git
  cargo
  clang
  nettle
)
source=(sequoia-sop::"git+$url#tag=v$pkgver?signed")
sha512sums=('9c71535825f39c021730f8818d2f071ee437fe256ca5a5bf15d9e8dfe7eb843fcdf6a26ad940c1aad220d4f4c050b582ecc3b5ae918b92a5dd0a66bf695bd710')
b2sums=('85fdb478832ccba24c897634d493dcfba7836607ebc89bdcffb9e36577f2165ed4db704eca2fce1be017d5ef1f8e3eb8c9645a7b2e9d9bd89ba4ab307554c03f')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cd ${pkgname}
  export ASSET_OUT_DIR="$srcdir/${pkgname}"
  cargo build --release --frozen --all-features
}

check() {
  export RUSTUP_TOOLCHAIN=stable
  cd ${pkgname}
  cargo test --frozen --all-features
}

package() {
  depends+=(
    bzip2 libbz2.so
    nettle libnettle.so libhogweed.so
  )

  cd ${pkgname}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/sqop \
    target/release/sqopv

  install -Dm 644 man-pages/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 shell-completions/{sqop,sqopv}.bash -t "${pkgdir}/usr/share/bash-completion/completions"
  install -Dm 644 shell-completions/_{sqop,sqopv} -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm 644 shell-completions/{sqop,sqopv}.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
