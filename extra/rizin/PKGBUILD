# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.7.4
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('x86_64')
license=(
  'Apache-2.0'
  'BSD-2-Clause'
  'BSD-3-Clause'
  'CC-PDDC'
  'CC0-1.0'
  'GPL-1.0-or-later'
  'GPL-2.0-only'
  'GPL-2.0-or-later'
  'GPL-3.0-only'
  'GPL-3.0-or-later'
  'LGPL-2.0-or-later'
  'LGPL-2.1-only'
  'LGPL-2.1-or-later'
  'LGPL-3.0-only'
  'MIT'
)
depends=(
  glibc
  sh
  openssl
  zlib
  lz4
  xz
)
makedepends=(
  capstone
  file
  libmspack
  libzip
  meson
  ninja
  python
  python-yaml
  tree-sitter
  xxhash
)
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('47f077805d88ae10d69e9eb1a53a691b5c3c4b1604927934948cf8ff69731b23a1df8811297e2e5882a2d09b60a5601fcbc96abaa9342712f52335508f64a20a')
b2sums=('0f55416fc73bf6122e3969aec286c1b77ee9d9495d71aee0c9dcdceb94d411aa9e12a05ad3a4aa8792f40d028e753c30b586bc397a9e72276b8e10fd058eab29')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v5 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_lzma=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_sys_libmspack=enabled \
    -D use_zlib=true \
    -D use_lzma=true \
    -D local=disabled
  ninja -C build
}

package() {
  depends+=(
    capstone libcapstone.so
    file libmagic.so
    libmspack libmspack.so
    libzip libzip.so
    tree-sitter libtree-sitter.so
    xxhash libxxhash.so
  )

  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
  rm "$pkgdir"/usr/lib/pkgconfig/libzstd.pc
}

# vim: ts=2 sw=2 et:
