# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.8.1
pkgrel=2
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('x86_64')
license=(
  'Apache-2.0'
  'BSD-2-Clause'
  'BSD-3-Clause'
  'CC-PDDC'
  'CC0-1.0'
  'GPL-1.0-or-later'
  'GPL-2.0-only'
  'GPL-2.0-or-later'
  'GPL-3.0-only'
  'GPL-3.0-or-later'
  'LGPL-2.0-or-later'
  'LGPL-2.1-only'
  'LGPL-2.1-or-later'
  'LGPL-3.0-only'
  'MIT'
)
depends=(
  glibc
  sh
  openssl
  zlib
  lz4
  xz
)
makedepends=(
  capstone
  file
  libmspack
  libzip
  meson
  ninja
  pcre2
  python
  python-yaml
  tree-sitter
  xxhash
  zstd
)
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('da0de01199e0d314fb5023267918d26ae4748547e60c6ce10a81f32a6e6e5453694a5261cb16b48df1412ef405e4835feb9598fafe75787dcacbf0a5fa6225f9')
b2sums=('52da4a3cb522e540ce03e61691e029fe1e81f5367fc38a32fc32e8a99c00aa91701af8acee8308b8369495f76b0131a65565c11fe9b9bbc04b1f8f6e2c10cbf4')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v5 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_libzstd=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_lzma=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_pcre2=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_sys_libmspack=enabled \
    -D use_zlib=true \
    -D use_lzma=true \
    -D local=disabled
  ninja -C build
}

package() {
  depends+=(
    capstone libcapstone.so
    file libmagic.so
    libmspack libmspack.so
    libzip libzip.so
    pcre2 libpcre2-8.so
    tree-sitter libtree-sitter.so
    xxhash libxxhash.so
    zstd libzstd.so
  )

  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
}

# vim: ts=2 sw=2 et:
