# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kjournald
pkgver=24.05.1
pkgrel=1
pkgdesc='Framework for interacting with systemd-journald'
url='https://invent.kde.org/system/kjournald'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         kirigami
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         systemd
         systemd-libs)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-system)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('2d73dedb89cf247cc869c89bc871b5aa30b31ca44423c89f7ad629d55e01b1d2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
