# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=staticcheck
pkgver=2024.1.1
pkgrel=2
pkgdesc="The advanced Go linter"
arch=('x86_64')
url="https://staticcheck.io"
license=("MIT")
depends=('glibc')
makedepends=('go' 'git')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dominikh/go-tools/archive/$pkgver.tar.gz")
sha512sums=('abd32fd81b4993a99c2fedaf349fca6f5edcb47651d7efff5e71e7c3c4b9ed41188061c7228d82796c65efbb5f5aa1498ea86c7476f530e0587c3787fbe9dae2')
b2sums=('0fe4c4158347f34dbd8f3afd303648659ce0895a529cb0f9f96ad5d4c73025fb798d39a1371a8d1af1ec768af375776b7f7b86e42a34ec62fda1d409e4b0cb84')

prepare(){
  cd "go-tools-$pkgver"
  mkdir -p bin
}

build(){
  cd "go-tools-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o bin\
    ./cmd/staticcheck
}

check(){
  cd "go-tools-$pkgver"
  GOROOT="/usr/lib/go" go test -v ./...
}

package(){
  cd "go-tools-$pkgver"
  install -p -Dm755 bin/staticcheck "${pkgdir}/usr/bin/staticcheck"
  install -Dm644 "LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
  install -Dm644 "LICENSE-THIRD-PARTY"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE-THIRD-PARTY"
}
