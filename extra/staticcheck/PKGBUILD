# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=staticcheck
pkgver=2026.1
pkgrel=1
pkgdesc="The advanced Go linter"
arch=(x86_64)
url="https://staticcheck.io"
license=(MIT)
depends=(glibc)
makedepends=(go git)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::https://github.com/dominikh/go-tools/archive/$pkgver.tar.gz")
sha512sums=('d0594069505f3a16cb0de51c92edf22d53c66ce42829575e29b72f1a83e9b929a2c7876d5af3d12321ead054cf525a9de6cdfbbd163e070314788f5aea20bcab')
b2sums=('0fd3f9c8116b43e0e66bc72c144c333ae9b7f595570a2d860827384877a2df8bdd219b275f6323114cf54cb8a22943c0f8626279f7dd7f3b3cfb93b77108063e')

prepare(){
  cd "go-tools-$pkgver"
  mkdir -p bin
}

build(){
  cd "go-tools-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o bin\
    ./cmd/staticcheck
}

check(){
  cd "go-tools-$pkgver"
  GOROOT="/usr/lib/go" go test -v ./...
}

package(){
  cd "go-tools-$pkgver"
  install -p -Dm755 bin/staticcheck "${pkgdir}/usr/bin/staticcheck"
  install -Dm644 "LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
  install -Dm644 "LICENSE-THIRD-PARTY"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE-THIRD-PARTY"
}
