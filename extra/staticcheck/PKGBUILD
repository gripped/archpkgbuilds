# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=staticcheck
pkgver=2025.1.1
pkgrel=5
pkgdesc="The advanced Go linter"
arch=(x86_64)
url="https://staticcheck.io"
license=("MIT")
depends=(glibc)
makedepends=(go git)
source=("$pkgname-$pkgver.tar.gz::https://github.com/dominikh/go-tools/archive/$pkgver.tar.gz")
sha512sums=('49ec128257070f7b479c97e33cd602767b5b8650ca3fd6461a0634dc26d2f6dc9165bd9938de69e3bc128d7bfed9f4aa2db642c36af52a5382c71041a9c799e9')
b2sums=('2af49e565da90ceb11f8b15d4047b30f751b4a4a2dc7fe40387d0bebb07013b31538697e0070f3b50d73d929f8205ecebbfaea9ef0a35d5548b369c91e78bc49')

prepare(){
  cd "go-tools-$pkgver"
  mkdir -p bin
}

build(){
  cd "go-tools-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o bin\
    ./cmd/staticcheck
}

check(){
  cd "go-tools-$pkgver"
  GOROOT="/usr/lib/go" go test -v ./...
}

package(){
  cd "go-tools-$pkgver"
  install -p -Dm755 bin/staticcheck "${pkgdir}/usr/bin/staticcheck"
  install -Dm644 "LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
  install -Dm644 "LICENSE-THIRD-PARTY"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE-THIRD-PARTY"
}
