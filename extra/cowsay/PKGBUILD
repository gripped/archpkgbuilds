# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org> 
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=cowsay
pkgver=3.8.2
pkgrel=1
pkgdesc='Configurable talking cow (and a bit more)'
arch=('any')
url='https://github.com/cowsay-org/cowsay'
license=('GPL-3.0-only')
makedepends=('git')
depends=('perl')
source=("git+https://github.com/cowsay-org/cowsay.git#tag=v${pkgver}")
sha256sums=('c027055bd77d56a685fc07d5d221d0473c066ac4219e1fb8635bcfeefd37146a')

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install prefix=/usr
  # missing Acme::Cow module
  rm "${pkgdir}"/usr/share/cowsay/cows/*.pm
}
