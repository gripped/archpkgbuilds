# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Julien Virey <julien.virey@gmail.com>

pkgname=rumdl
pkgver=0.1.16
pkgrel=1
pkgdesc='Markdown Linter and Formatter written in Rust'
arch=('x86_64')
options=(!lto)
url=https://github.com/rvben/rumdl
license=('MIT')
depends=(glibc gcc-libs)
makedepends=(cargo)

source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('5662a4d0944504d39a97a45da79a01227a02b54efdbf6778b198faf4492ad04f')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --bin rumdl
}

package() {
  cd $pkgname-$pkgver
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
