# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Julien Virey <julien.virey@gmail.com>

pkgname=rumdl
pkgver=0.1.21
pkgrel=1
pkgdesc='Markdown Linter and Formatter written in Rust'
arch=('x86_64')
options=(!lto)
url=https://github.com/rvben/rumdl
license=('MIT')
depends=(glibc gcc-libs)
makedepends=(cargo)

source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('cb57a732eaef7d41c17547d7604081dd8003356d9427edc82ecef956cb79c759')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --bin rumdl
}

package() {
  cd $pkgname-$pkgver
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
