# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: <waruqi@gmail.com>
# Contributor: lumpyzhu <lumpy.zhu@gmail.com>

pkgname=xmake
pkgver=3.0.6
pkgrel=1
pkgdesc='A cross-platform build utility based on Lua'
arch=(x86_64 i686 aarch64)
url="https://github.com/xmake-io/$pkgname"
license=(Apache)
makedepends=(ncurses
             readline)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/${_archive/-/-v}.tar.gz")
sha256sums=('1f7bd9ab7f7cbeade4ecd81f3580898e8d78aa5f64cea44239a9506ff41bc397')

# Upstream botched their tarball and double wrapped it. Also they keep
# switching up where they use what version tag format. Bah.
prepare() {
	tar xaf "${_archive/-/-v}.tar"
}

build() {
	cd "$_archive"
	./configure
	make
}

package() {
	cd "$_archive"
	make install DESTDIR="$pkgdir" PREFIX="/usr"
}
