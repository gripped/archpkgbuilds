# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: <waruqi@gmail.com>
# Contributor: lumpyzhu <lumpy.zhu@gmail.com>

pkgname=xmake
pkgver=2.9.3
pkgrel=1
pkgdesc='A cross-platform build utility based on Lua'
arch=(x86_64 i686 aarch64)
url="https://github.com/xmake-io/$pkgname"
license=(Apache)
makedepends=(ncurses
             readline)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/${_archive/-/-v}.tar.gz")
sha256sums=('82a9bb6961a39bea8f280c1413d54854423f8f92e2ff43ee1c0fead7a3b5edd8')

# Upstream botched their tarball and double wrapped it. Also they keep
# switching up where they use what version tag format. Bah.
prepare() {
	tar xaf "${_archive/-/-v}.tar"
}

build() {
	cd "$_archive"
	./configure
	make
}

package() {
	cd "$_archive"
	make install DESTDIR="$pkgdir" PREFIX="/usr"
}
