# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-skk
pkgver=5.1.2
pkgrel=1
pkgdesc="libskk wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-skk"
license=('GPL')
depends=('fcitx5-qt' 'libskk' 'skk-jisyo')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('8e2fa3aacb98b0ac5c4fac4b4e4f0dbdfb7a889fe9130336fcf75bc3ad4bcd3301d15685f2df5f7507bab1fa308daa1d160af9880fff41c4d34d77db20e54b2d'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
