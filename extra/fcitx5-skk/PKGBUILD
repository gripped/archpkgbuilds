# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-skk
pkgver=5.1.7
pkgrel=1
pkgdesc="libskk wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-skk"
license=('GPL')
depends=('fcitx5-qt' 'libskk' 'skk-jisyo')
makedepends=('git' 'extra-cmake-modules' 'ninja')
source=("git+https://github.com/fcitx/fcitx5-skk.git#tag=$pkgver?signed")
sha512sums=('32de43f1729a6fed9d775dff3fab03251e0b07e6b0091fb0ec8bdb4e64c247c5c5aa1d3d1b3815a5b2748bddb01b78a08004c8ef53a10be8e5776479727398a5')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
