# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=volk
pkgver=1.3.295
pkgrel=1
arch=(x86_64)
pkgdesc="Meta loader for Vulkan API"
url="https://github.com/zeux/volk"
license=('MIT')
makedepends=('cmake' 'vulkan-headers')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/zeux/volk/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('aea9f09c49f8a4e36738003c7aa5f08f99d68b96e4028ad9fa9039d2ee9fb251')

build() {
  cd "${srcdir}"/volk-${pkgver}

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DVULKAN_HEADERS_INSTALL_DIR=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DVOLK_INSTALL=On \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd "${srcdir}"/volk-${pkgver}/build
  
  make DESTDIR="${pkgdir}" install
  
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/
}
