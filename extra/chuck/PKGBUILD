# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.5.4.4
pkgrel=1
pkgdesc='Concurrent, on-the-fly audio programming language'
arch=(x86_64)
url='https://chuck.cs.princeton.edu/'
license=(GPL-2.0-or-later MIT)
depends=(jack libsndfile)
groups=(pro-audio)
source=("https://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
b2sums=('84d3a9e11205c95695fc2b1e9af9fccb31d9a7f2dd6f4184c8bd3b9425cbdb05dac36d477cb6aeb6e757299dc972e2daff5b18d7f5fd748d3c31e04974dd70b9')

build() {
  make -C "$pkgname-$pkgver/src" linux-jack
}

package() {
  cd $pkgname-$pkgver

  # Executable
  install -Dm755 src/$pkgname "$pkgdir/usr/bin/$pkgname"

  # Examples
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r examples "$pkgdir/usr/share/doc/$pkgname"

  # License (no need to package the GPL license)
  install -Dm644 LICENSE.MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE.MIT"
}
