# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.5.5.1
pkgrel=2
pkgdesc='Concurrent, on-the-fly audio programming language'
arch=(x86_64)
url='https://chuck.cs.princeton.edu/'
license=(GPL-2.0-or-later MIT)
depends=(jack libsndfile)
groups=(pro-audio)
source=("https://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
b2sums=('dae842e2990ed4fc260b69c309ba6fe8adb8d9dcf2c7c76c5335c6d6239496b9a70e66a8e262a32a6b6879bff04185e1de1de440ab6c4705fb88ed2c99a0940a')

build() {
  make -C "$pkgname-$pkgver/src" linux-jack
}

package() {
  cd $pkgname-$pkgver

  # Executable
  install -Dm755 src/$pkgname "$pkgdir/usr/bin/$pkgname"

  # Examples
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r examples "$pkgdir/usr/share/doc/$pkgname"

  # License (no need to package the GPL license)
  install -Dm644 LICENSE.MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE.MIT"
}
