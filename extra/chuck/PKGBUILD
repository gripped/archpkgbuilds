# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.5.5.7
pkgrel=1
pkgdesc='Concurrent, on-the-fly audio programming language'
arch=(x86_64)
url='https://chuck.cs.princeton.edu/'
license=(GPL-2.0-or-later MIT)
depends=(jack libsndfile)
groups=(pro-audio)
source=("https://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
b2sums=('97b8a683a6641a1ac0a84eb6f646a8fd99e71f2613efc7c32706c3af3faf1feab05dd6938dd06b2dd4c8842213a415bf577f70948459537d192d342c254b7696')

build() {
  make -C $pkgname-$pkgver/src linux-jack
}

package() {
  cd $pkgname-$pkgver

  # Executable
  install -Dm755 src/$pkgname "$pkgdir/usr/bin/$pkgname"

  # Examples
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r examples "$pkgdir/usr/share/doc/$pkgname"

  # License (no need to package the GPL license)
  install -Dm644 LICENSE.MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE.MIT"
}
