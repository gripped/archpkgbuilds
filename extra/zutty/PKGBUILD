# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Sopp

pkgname=zutty
pkgver=0.15
pkgrel=1
pkgdesc='Terminal emulator'
arch=(x86_64)
url='https://tomscii.sig7.se/zutty'
license=(GPL3)
depends=(freetype2 libglvnd libxmu xorg-fonts-misc)
makedepends=(git python)
source=('git+https://git.hq.sig7.se/zutty.git#commit=7e481c04507e9b5cacfe67fe2b96bdb449b08726') # tag: 0.15
b2sums=('SKIP')

build() {
  cd $pkgname
  python waf configure --prefix=/usr
  python waf -v build
}

package() {
  cd $pkgname
  python waf --destdir="$pkgdir" install
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -v doc/* "$pkgdir/usr/share/doc/$pkgname"
  cd icons
  install -Dm644 zutty.desktop "$pkgdir/usr/share/applications/zutty.desktop"
  install -Dm644 zutty.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/zutty.svg"
  for res in 16x16 32x32 48x48 64x64 96x96 128x128; do
    install -Dm644 zutty_$res.png "$pkgdir/usr/share/icons/hicolor/$res/apps/zutty.png"
  done
}
