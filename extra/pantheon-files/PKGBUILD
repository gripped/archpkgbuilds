# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=7.1.0
pkgrel=1
pkgdesc='The Pantheon File Browser'
arch=(x86_64)
url=https://github.com/elementary/files
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  atk
  bash
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  gvfs
  libcanberra
  libcloudproviders
  libgee
  libgit2-glib
  libgranite.so
  libhandy-1.so
  libnotify
  libportal
  libportal-gtk3
  pango
  sqlite
  zeitgeist
)
makedepends=(
  cmake
  flatpak-builder
  git
  gnome-common
  meson
  vala
)
optdepends=(
  'contractor: Various context menu entries'
  'tumbler: Thumbnails generation'
)
_tag=d6c30901603f04e7e5cf4a184f212493adddad9e
source=(pantheon-files::git+https://github.com/elementary/files.git#tag=${_tag})
b2sums=('7e41f80e1a6260c71dfc8370fdc91f843f13aea76dc40db9b6951fa8a388373c549680e3c73078239dc84961173cc5d4d2d7b5ed9aa657b17f7879bfe18a0ac6')

pkgver() {
  cd pantheon-files
  git describe --tags
}

build() {
  export CFLAGS+=" -Wno-implicit-function-declaration"
  arch-meson pantheon-files build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
