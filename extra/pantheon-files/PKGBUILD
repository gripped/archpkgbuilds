# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=7.1.6
pkgrel=1
pkgdesc='The Pantheon File Browser'
arch=(x86_64)
url=https://github.com/elementary/files
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  atk
  bash
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  gvfs
  libcanberra
  libcloudproviders
  libgee
  libgit2-glib
  libgranite.so
  libhandy-1.so
  libnotify
  libportal
  libportal-gtk3
  pango
  sqlite
  zeitgeist
)
makedepends=(
  cmake
  flatpak-builder
  git
  gnome-common
  meson
  vala
)
optdepends=(
  'contractor: Various context menu entries'
  'tumbler: Thumbnails generation'
)
_tag=94f3aadbeae24e1da53d6e8e83079f991bc60b24
source=(pantheon-files::git+https://github.com/elementary/files.git#tag=${_tag})
b2sums=('65bcb5e5399edad6094c496038069edafb723bc074766912b8805df4429a0dae36d08ddf15126c8d5caf75523f78d95d45fdb61dfddc716c29c5459153db1fad')

pkgver() {
  cd pantheon-files
  git describe --tags
}

build() {
  export CFLAGS+=" -Wno-implicit-function-declaration"
  arch-meson pantheon-files build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
