# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=7.1.1
pkgrel=1
pkgdesc='The Pantheon File Browser'
arch=(x86_64)
url=https://github.com/elementary/files
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  atk
  bash
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  gvfs
  libcanberra
  libcloudproviders
  libgee
  libgit2-glib
  libgranite.so
  libhandy-1.so
  libnotify
  libportal
  libportal-gtk3
  pango
  sqlite
  zeitgeist
)
makedepends=(
  cmake
  flatpak-builder
  git
  gnome-common
  meson
  vala
)
optdepends=(
  'contractor: Various context menu entries'
  'tumbler: Thumbnails generation'
)
_tag=5222ff8a763783cab2a4d5da177699dd78638c35
source=(pantheon-files::git+https://github.com/elementary/files.git#tag=${_tag})
b2sums=('3ec9c81fe2974d8730e1fed5144a367004fac6a346d761488a015b90b3c09c71f68b3040902a15ddc17350848edd985a3790298cd3f1950a8d2916b8f6a54496')

pkgver() {
  cd pantheon-files
  git describe --tags
}

build() {
  export CFLAGS+=" -Wno-implicit-function-declaration"
  arch-meson pantheon-files build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
