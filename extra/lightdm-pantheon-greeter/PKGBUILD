# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=7.0.0.r82.b61fb080
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=(x86_64)
url=https://github.com/elementary/greeter
license=(GPL-2.0-or-later)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glibc
  libgdk-3.so
  libgee
  libgio-2.0.so
  libglib-2.0.so
  libgl
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
  lightdm
  mutter
)
makedepends=(
  cmake
  meson
  git
  libglvnd
  vala
)
install=lightdm-pantheon-greeter.install
#_tag=bed49e46baaebf9bcc405e3e5577b99b925f56a1
_commit=b61fb08063c32c9433c46a334ffbcd24631f781e
#source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=${_tag})
source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#commit=${_commit})
b2sums=('2c2d88ef1d2eb72affd7ccb795457cca9a45fbb6ea4ec4c054764dccc30d5eaa9a2acd0d8f5ab9ec7993d1ddea2e38a673ebbdf04cd50eb0a3e8079ac708cf8a')

pkgver() {
  cd lightdm-pantheon-greeter
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson lightdm-pantheon-greeter build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
