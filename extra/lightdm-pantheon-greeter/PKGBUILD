# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=8.0.0
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=(x86_64)
url=https://github.com/elementary/greeter
license=(GPL-2.0-or-later)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glibc
  libgdk-3.so
  libgee
  libgio-2.0.so
  libglib-2.0.so
  libgl
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
  lightdm
  mutter46
)
makedepends=(
  cmake
  meson
  git
  libglvnd
  vala
)
install=lightdm-pantheon-greeter.install
_tag=1569079e000c8b537cd68a90c35900ba68b4457d
source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=${_tag})
b2sums=('26790420550a46a77c768b276dbc2862d2b96d774346d6dd775b40d1817f2795945cf5568eb9a5bdc0c8a0ae7148e34b9e734c2ff09c2c993f8626accd5f4d82')

pkgver() {
  cd lightdm-pantheon-greeter
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson lightdm-pantheon-greeter build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
