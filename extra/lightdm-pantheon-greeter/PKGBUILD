# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=7.0.0.r121.af0dfea0
pkgrel=2
pkgdesc='Pantheon greeter for LightDM'
arch=(x86_64)
url=https://github.com/elementary/greeter
license=(GPL-2.0-or-later)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glibc
  libgdk-3.so
  libgee
  libgio-2.0.so
  libglib-2.0.so
  libgl
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
  lightdm
  mutter46
)
makedepends=(
  cmake
  meson
  git
  libglvnd
  vala
)
install=lightdm-pantheon-greeter.install
#_tag=bed49e46baaebf9bcc405e3e5577b99b925f56a1
_commit=af0dfea0e429296bb806d503937f06390112753f
#source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=${_tag})
source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#commit=${_commit})
b2sums=('689be04b6bf6d9f0c5a6d8981a21366d63c36ff35df6ff4d79aff10dda7dd79c34577cabaac6cda5dbdd24c51ca17f6803d26acaaf2963abe9819f7afdfc907e')

pkgver() {
  cd lightdm-pantheon-greeter
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson lightdm-pantheon-greeter build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
