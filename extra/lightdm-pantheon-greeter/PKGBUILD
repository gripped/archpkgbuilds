# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=8.0.1
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=(x86_64)
url=https://github.com/elementary/greeter
license=(GPL-2.0-or-later)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glibc
  libgdk-3.so
  libgee
  libgio-2.0.so
  libglib-2.0.so
  libgl
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
  lightdm
  mutter46
)
makedepends=(
  cmake
  meson
  git
  libglvnd
  vala
)
install=lightdm-pantheon-greeter.install
_tag=dbe29d6f1bf88a459b5c2889ec56251e762bca5c
source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=${_tag})
b2sums=('2dc4a65ce607d6f82d57cfb7cafe5553d9e05cdef0911020179eec7e57958244f4f35e050e6161601e690ee4d257ce5cd0be5518c854c618aebabf9e0b6b74b5')

pkgver() {
  cd lightdm-pantheon-greeter
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson lightdm-pantheon-greeter build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
