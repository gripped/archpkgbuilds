# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=8.1.1
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=(x86_64)
url=https://github.com/elementary/greeter
license=(GPL-2.0-or-later)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glibc
  gnome-desktop
  libgdk-3.so
  libgee
  libgio-2.0.so
  libglib-2.0.so
  libgl
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
  lightdm
  mutter46
)
makedepends=(
  cmake
  meson
  git
  libglvnd
  vala
)
install=lightdm-pantheon-greeter.install
_tag=c3a8756631daceb2c3183bc3a4a24ca009ff5dc3
source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=${_tag})
b2sums=('39be7632a0f18987eea4679b7d96225920b6100748fc2810eb13fc124a6de81cec5cb22b60f134fa574f6a926f13b0537d42251c7c0af6517fee406de76297a3')

pkgver() {
  cd lightdm-pantheon-greeter
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson lightdm-pantheon-greeter build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
