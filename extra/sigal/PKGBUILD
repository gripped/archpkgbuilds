# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sigal
pkgver=2.5
pkgrel=1
pkgdesc="Yet another simple static gallery generator"
arch=(any)
url="http://sigal.saimon.org/en/latest/"
_url="https://github.com/saimn/sigal"
license=(MIT)
depends=(
  ffmpeg
  python
  python-blinker
  python-click
  python-jinja
  python-markdown
  python-natsort
  python-pillow
  python-pilkit
)
makedepends=(
  python-build
  python-installer
  python-setuptools-scm
  python-toml
  python-wheel
)
checkdepends=(
  python-brotli
  python-cryptography
  python-feedgenerator
  python-pytest
  python-zopfli
)
optdepends=(
  'python-brotli: compress assets plugin'
  'python-cryptography: encrypt plugin'
  'python-feedgenerator: feed plugin'
  'python-markupsafe: feed plugin'
  'python-pillow-heif: for HEIF/HEIC support'
  'python-zopfli: compress assets plugin'
)
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/refs/tags/$pkgver.tar.gz
)
sha512sums=('03bd816316e22543239fb5964e977c2844cb68345b0bc860a2b3c87fa243c6847f556164534442e502b2aecba055b0d34a8578f3a1dfc8748c9cca3a591e970c')
b2sums=('564d21acf9307ccc587946ed739ef6bca9147171c9a470f91c5a25fecbe2d869ea7535808ff5a05bf6433b936371cd40ff9531790f9b60c6f4586e2452b46785')

build() {
  cd $pkgname-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,{CONTRIBUTING,README}.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
