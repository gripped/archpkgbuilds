# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=switcheroo
pkgver=2.4.0
pkgrel=1
pkgdesc='Convert between different image filetypes and resize them easily'
arch=(x86_64)
url='https://apps.gnome.org/Converter/'
license=(GPL-3.0-only)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  ghostscript
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  imagemagick
  libadwaita
  libheif
  libjpeg-turbo
  libjxl
  librsvg
  libtiff
  libwebp
  webp-pixbuf-loader
  zip
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/adhami3310/Switcheroo.git#tag=v$pkgver")
b2sums=('4444bf56196611fb3dab3ded32c6964008a06618f15c9918bb2a23e0561a46f013cc062a449d7ff8714ad217b4e2445b38b1a4afbef6b78111e29e0fcf56aafd')

prepare() {
  cd Switcheroo

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Switcheroo build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
