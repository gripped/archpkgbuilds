# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=switcheroo
pkgver=2.5.1
pkgrel=2
pkgdesc='Convert between different image filetypes and resize them easily'
arch=(x86_64)
url='https://apps.gnome.org/Converter/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  ghostscript
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  imagemagick
  libadwaita
  libheif
  libjpeg-turbo
  libjxl
  librsvg
  libtiff
  libwebp
  zip
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/adhami3310/Switcheroo.git#tag=v$pkgver")
b2sums=(b68b5d43e74269a6a3d17a1b7f0946878aa4b886b56a752d2bd42d7bff9e646097459904e451afaa0656681c43f50aeb80cf1800347e286530511fbb6f31222f)

prepare() {
  cd Switcheroo

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  arch-meson Switcheroo build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
