# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=switcheroo
pkgver=2.2.0
pkgrel=1
pkgdesc='Convert between different image filetypes and resize them easily'
arch=(x86_64)
url='https://apps.gnome.org/Converter/'
license=(GPL-3.0-only)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  ghostscript
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  imagemagick
  libadwaita
  libheif
  libjpeg-turbo
  libjxl
  librsvg
  libtiff
  libwebp
  webp-pixbuf-loader
  zip
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=("git+https://gitlab.com/adhami3310/Switcheroo.git#tag=v$pkgver")
b2sums=(f178edd08a20df0e809121a6371fc17ccff06bba7cfa26e75255d15f4dd64a2ef01cb20031e4153d58c597d019b683bec5b93b6d116d384f8a241c0aa034ff0b)

prepare() {
  cd Switcheroo

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Switcheroo build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
