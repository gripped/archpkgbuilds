# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-shell-extension-weather-oclock
_pkgname=weather-oclock
_uuid=weatheroclock@CleoMenezesJr.github.io
pkgver=46.1
pkgrel=1
pkgdesc='GNOME Shell extension that displays the current weather inside the pill next to the clock'
arch=('any')
url='https://github.com/CleoMenezesJr/weather-oclock'
license=('GPL-3.0-or-later')
depends=('gnome-shell')
optdepends=('gnome-weather: configure location')
source=("https://github.com/CleoMenezesJr/$_pkgname/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
        'gnome-shell-extension-weather-oclock-icon-size.patch'
        'gnome-shell-extension-weather-oclock-wait-loaded.patch'
        'gnome-shell-extension-weather-oclock-no-copy.patch')
sha256sums=('708600d781a9776c814bfb7c3a694be97883a93e7254a59a55c195d27f1f6e74'
            '7ea0b79ad2b30d3dd439b0b23eb8d2c073099a2a3a21814628ee3f77dd6feb3b'
            '700c2782260806d66d3a1847fb3831b8cb3caebbeedbf73faed0035b2a6402bf'
            'f47878d4f1f8384853eeae97bd2e6ecf66e0a1f675eac8da4ed8929f87da8bdd')

prepare() {
  cd $_pkgname-$pkgver
  
  # https://github.com/CleoMenezesJr/weather-oclock/pull/26
  patch -Np1 -i ../gnome-shell-extension-weather-oclock-icon-size.patch

  # https://github.com/CleoMenezesJr/weather-oclock/pull/27
  patch -Np1 -i ../gnome-shell-extension-weather-oclock-wait-loaded.patch

  # https://github.com/CleoMenezesJr/weather-oclock/pull/28
  patch -Np1 -i ../gnome-shell-extension-weather-oclock-no-copy.patch
}

package() {
  install -Dm644 -t "$pkgdir/usr/share/gnome-shell/extensions/$_uuid" $_pkgname-$pkgver/$_uuid/*
}
