# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-shell-extension-weather-oclock
_pkgname=weather-oclock
_uuid=weatheroclock@CleoMenezesJr.github.io
pkgver=47.0
pkgrel=2
pkgdesc='GNOME Shell extension that displays the current weather inside the pill next to the clock'
arch=(any)
url='https://github.com/CleoMenezesJr/weather-oclock'
license=(GPL-3.0-or-later)
depends=(gnome-shell)
makedepends=(git)
optdepends=('gnome-weather: Configure location')
source=("git+https://github.com/CleoMenezesJr/weather-oclock.git#tag=$pkgver")
b2sums=(1451c38fb0a3d2ada9ddd81c6708048fd7815aa0cfa7b1a9ee7f6816638c84f14f2db888c873c287dcfae7fd32456638de62deb745569c4cd2eeb8355b464f51)

prepare() {
  cd $_pkgname

  # Add GNOME 48 to supported shell versions
  sed -i 's/"47"/"47", "48"/' $_uuid/metadata.json
}

package() {
  install -dm755 "$pkgdir/usr/share/gnome-shell/extensions"
  cp -a $_pkgname/$_uuid "$pkgdir/usr/share/gnome-shell/extensions/$_uuid"
  install -dm755 "$pkgdir/usr/share/glib-2.0"
  mv "$pkgdir"/usr/share/gnome-shell/extensions/*/schemas "$pkgdir/usr/share/glib-2.0"
}
