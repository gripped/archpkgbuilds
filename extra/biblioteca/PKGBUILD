# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=biblioteca
pkgver=1.6
pkgrel=1
pkgdesc='API documentation viewer for GNOME'
arch=(any)
url='https://apps.gnome.org/Biblioteca/'
license=(GPL-3.0-only)
groups=(gnome-circle)
depends=(
  dconf
  gjs
  glib2
  glib2-docs
  gtk4
  gtk4-docs
  hicolor-icon-theme
  libadwaita
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
install=biblioteca.install
source=(
  "git+https://github.com/workbenchdev/Biblioteca.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
  biblioteca-metainfo.patch
  biblioteca-no-flatpak.patch
  biblioteca-build-index.hook
)
b2sums=(
  261b9c68388e2062466032ca7fb281ff3c88509c71d5fc65e2d94b76af3307ce4cff046e20aea5387e42070ddb5f5cbc9d1bd8250d4d9631d4892ef4c8344f46
  SKIP
  02d6a379ad01586f5e7dde05c926d7d0f0c4e6af5fa1320a718b3de20a35ba48949c60c3146627609f723c1fe84dad4f80fce42ce81d5dd3c8f760730df1086f
  f034f380cddd13538ec2e09085fa58112b79d9ec9474939e6dc8e19df642fb07181ed710df24b18b6612e7ae734ada054fb0306a1d56159fa2f48640cccf3e00
  defc3485c5e0e80322b8782e1706ea9d26e66c2cd766cfc2ae93d60593e502b8ad20c2583c24fbd7e9ef0e964d41a503f80e37f334024c7fc000f6111e69df90
)

prepare() {
  cd Biblioteca

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update

  # Fix metainfo validation
  # https://github.com/workbenchdev/Biblioteca/pull/138
  git apply -3 ../biblioteca-metainfo.patch

  # Don't check if running under Flatpak
  git apply -3 ../biblioteca-no-flatpak.patch

  # Fix paths
  sed -i 's|/app/share/|/usr/share/|' build-aux/build-index.js src/{Shortcuts,window}.*
  sed -i -e 's|/app/bin/||' -e '/build-index/d' src/meson.build
}

build() {
  arch-meson Biblioteca build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild

  # Install script for building doc-index.json on install
  install -Dm755 -t "$pkgdir/usr/share/app.drey.Biblioteca/" Biblioteca/build-aux/build-index.js
  install -Dm644 -t "$pkgdir/usr/share/libalpm/hooks/" biblioteca-build-index.hook
}
