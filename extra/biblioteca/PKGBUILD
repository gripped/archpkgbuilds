# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=biblioteca
pkgver=1.5
pkgrel=3
pkgdesc='API documentation viewer for GNOME'
arch=(any)
url='https://apps.gnome.org/Biblioteca/'
license=(GPL-3.0-only)
depends=(
  dconf
  gjs
  glib2
  glib2-docs
  gtk4
  gtk4-docs
  hicolor-icon-theme
  libadwaita
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
install=biblioteca.install
source=(
  "git+https://github.com/workbenchdev/Biblioteca.git#tag=v$pkgver"
  git+https://github.com/sonnyp/troll.git
  biblioteca-gtk-init.patch
  troll-gtk-init.patch
  biblioteca-command-line-arguments.patch
  biblioteca-no-flatpak.patch
  biblioteca-build-index.hook
)
b2sums=(
  5c186a3759418e3f5a86bc987105d82d86f0c2bca51e20b75fc928b8a91a75a358d1afa751faa300aba75a5642fd9d6d55d3f36af18d2cfd56d3ae17042249d8
  SKIP
  609e6716b1a918ba6120bfdbf745165d66565193b2909c4399acaf80d1148038846215cb15ec7c433da51ee1211b18b9aff37c0ca73cf11f251b8be01c27bddf
  7190a1851b490e1e429de5f571f6acdf7e2254c30d5368acbed270bac9d09e9e4d33ac5479d8f2b813a4178d2c3b3d82d49e82b76111530b05c36fe89d407338
  83f0c4825a04eb1ad5b6bb9397cc4c2b7f57a17dd58a913b289230875f179cb1e6912f357491d15d537df249729fa227a195e63254186fe67f7c1953a20dd9a9
  f034f380cddd13538ec2e09085fa58112b79d9ec9474939e6dc8e19df642fb07181ed710df24b18b6612e7ae734ada054fb0306a1d56159fa2f48640cccf3e00
  defc3485c5e0e80322b8782e1706ea9d26e66c2cd766cfc2ae93d60593e502b8ad20c2583c24fbd7e9ef0e964d41a503f80e37f334024c7fc000f6111e69df90
)

prepare() {
  cd Biblioteca

  git submodule init
  git submodule set-url troll "$srcdir/troll"
  git -c protocol.file.allow=always submodule update

  # Fix metainfo validation
  git cherry-pick -n 3c3fb7dcda414f7a9f52e690d4a51ad91df06f55

  # Fix initialization with GTK 4.18
  git apply -3 ../biblioteca-gtk-init.patch

  # Fix passing command-line arguments
  git apply -3 ../biblioteca-command-line-arguments.patch

  # Don't check if running under Flatpak
  git apply -3 ../biblioteca-no-flatpak.patch

  # Fix paths
  sed -i 's|/app/share/|/usr/share/|' build-aux/build-index.js src/{Shortcuts,window}.*
  sed -i -e 's|/app/bin/||' -e '/build-index/d' src/meson.build

  cd troll
  # Fix initialization with GTK 4.18
  git apply -3 ../../troll-gtk-init.patch

}

build() {
  arch-meson Biblioteca build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Install script for building doc-index.json on install
  install -Dm755 -t "$pkgdir/usr/share/app.drey.Biblioteca/" Biblioteca/build-aux/build-index.js
  install -Dm644 -t "$pkgdir/usr/share/libalpm/hooks/" biblioteca-build-index.hook
}
