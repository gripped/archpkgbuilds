# Maintainer:

_pyname=modern_colorthief
pkgname=python-modern-colorthief
pkgver=0.1.7
pkgrel=1
pkgdesc='Colorthief but with modern codes'
arch=(x86_64)
url='https://modern-colorthief.readthedocs.io/'
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
  python-fast-colorthief
)
makedepends=(
  git
  python-build
  python-installer
  python-maturin
)
checkdepends=(
  python-colorthief
  python-pytest
)
source=(git+https://github.com/baseplate-admin/modern_colorthief#tag=$pkgver)
b2sums=('fd8bf1acc31c46a771e3aaf10d6c52a6420644aa4577dbc5883421359624bcf471a7f03a0c61a311b59cdeab6bcf238e74b904892ecc777712f957ff308671e8')

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
