# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pyname=modern_colorthief
pkgname=python-modern-colorthief
pkgver=0.1.7
pkgrel=2
pkgdesc='Colorthief but with modern codes'
arch=(x86_64)
url='https://modern-colorthief.readthedocs.io/'
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-maturin
  python-wheel
  rust
)
checkdepends=(
  python-pillow
  python-pytest
)
source=("git+https://github.com/baseplate-admin/modern_colorthief#tag=$pkgver")
b2sums=(fd8bf1acc31c46a771e3aaf10d6c52a6420644aa4577dbc5883421359624bcf471a7f03a0c61a311b59cdeab6bcf238e74b904892ecc777712f957ff308671e8)

prepare() {
  cd $_pyname
  cargo fetch --locked
}

build() {
  cd $_pyname

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    python -m build --wheel --no-isolation
}

check() {
  cd $_pyname
  bsdtar -xf target/wheels/*.whl

  PYTHONPATH="$PWD" \
    pytest --ignore=examples/test_time.py
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
