# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.25.15
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=(x86_64)
license=(
  Apache-2.0
  MIT
)
depends=(
  cargo
  gcc-libs
  glibc
  zlib
)
makedepends=(rust)
options=(!lto)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('58ac5dae5f80d98cf59bd958ae04c8af8899c60ea324baef9be5e0dbc387732f8ce42e299040e292ccc39922a4d8f767bc039b79dcd80b0759581079dff829e3')
b2sums=('795bc83714203be39b3f86186d9179a37af91fb933e3869dfc27f57a62f53510a1f3e8c07990d24c05d3e7f1a1fb9991398930dc02fe4390813cda390a0f26aa')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --no-default-features
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
