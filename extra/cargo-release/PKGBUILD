# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.25.6
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=(x86_64)
license=(
  Apache-2.0
  MIT
)
depends=(
  cargo
  gcc-libs
  glibc
  zlib
)
makedepends=(rust)
options=(!lto)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('519679e28ab95d7f2a6ae7b9d11a87c96b0ece4c8f712e07b5822a1bac08668dec7956e7d0ce8a642d00d3bbbfffd53bc9b820d92eb6c284435f849c0b34dc18')
b2sums=('5180ca40421263190fa6a760d61748caea5307552dd2e5e8e00ecef5f889db2fd24ac02794ddd3067a73811d8f286a0eb31ff5b2062d10ac71b18fa694ba2edc')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --no-default-features
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
