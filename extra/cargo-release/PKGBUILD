# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.25.18
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=(x86_64)
license=(
  Apache-2.0
  MIT
)
depends=(
  cargo
  gcc-libs
  glibc
  zlib
)
makedepends=(rust)
options=(!lto)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('0a9d11ec17a005ca2a14f3493e812ced7754a04c9d1572dd3c36c71be6e6ea0dfd8f78829e5fc063b8d6010a984362b795bfa6899ec181544f417d853881165d')
b2sums=('e675abe86821d0085b31b7270f755abacef7b05209c2e7c092629649c1e7231591c05b790ceb27a98a4e98e68f810743bd4b52e48bc3323dd494b3353b3b8e3d')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --no-default-features
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
