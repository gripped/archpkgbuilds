# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=xtrlock
pkgver=2.16
pkgrel=1
pkgdesc='Minimal X display lock program'
url='https://packages.debian.org/sid/xtrlock'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  glibc
  libcrypt.so
  libx11
  libxcrypt
  libxfixes
  libxi
)
source=(https://ftp.debian.org/debian/pool/main/x/xtrlock/xtrlock_${pkgver}.tar.xz)
sha512sums=('69142f39f4f2268724b75a80f1c995fc05556fec79633c941b0bceb5f1e2bc1dcac3ad0b4b1a5d922032ab8a41f5186d40622db0afe74a654e7ea6f978b673c9')
b2sums=('cbab2e98b194fad77c31879a95d0ddf90a67b0f38b79b16fc353801977950dd91810111d6a1a2535a7703863d871c261e97eab7623677768aa789dc5d2f523b7')

build() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.noimake \
    CFLAGS="${CFLAGS} -DSHADOW_PWD -DMULTITOUCH" \
    LDLIBS="-lX11 -lcrypt -lXi" \
    xtrlock
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 4755 xtrlock -t "${pkgdir}/usr/bin"
  install -Dm 644 xtrlock.man "${pkgdir}/usr/share/man/man1/xtrlock.1"
}

# vim: ts=2 sw=2 et:
