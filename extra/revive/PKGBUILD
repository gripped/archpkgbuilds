# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=revive
pkgver=1.13.0
pkgrel=2
pkgdesc="faster, stricter, configurable, extensible, and beautiful drop-in replacement for golint"
arch=('x86_64')
url='https://revive.run'
license=('MIT')
depends=('glibc')
makedepends=(
  'git'
  'go'
)
options=('!lto')
source=("git+https://github.com/mgechev/revive.git#tag=v$pkgver")
sha512sums=('3cd0524ac41b3f070df23a1e5c5d431eb56aae41ff271d1598cb01830097587615bb4c00f00de74dd6c0c55c6e55669ec73427ab9b756b5a2d9fb5ec0d7fbdb0')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
    -X github.com/mgechev/revive/cli.commit=$(git rev-parse --short HEAD) \
    -X github.com/mgechev/revive/cli.date=NOTSET \
    -X github.com/mgechev/revive/cli.version=$pkgver \
  "
  go build -v -ldflags "$ld_flags"
}

check() {
  cd $pkgname
  go test -v ./...
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" revive
  install -vDm644 defaults.toml "$pkgdir/etc/revive/revive.toml"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
