# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.7.9
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('0e50cc06b6ab8f933b8e46439e7f07725ccd050b6bae723a82f7bd83b09c9e5aa7443d2aa63a9e03abae376511f20a1560df9a94b0e94c9f4c1100056e882709')
b2sums=('95c16782a1ec4cff537052fb25d78a83f9fe32e3aeeb68774f7c3a06580a3dc95f4d489254d99e7972af714ccc0783f8ef99a51e38de87f7137670b692933c33')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    alsa-lib libasound.so
    libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
