# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.7.4
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('396fe1e0b8390406637469aed1362564975fddd2d9488dda6f808be056cb65b02ed75bcd0338029aa615eef435db6eb8962787e1f5cf88236d3d195a53ddd505')
b2sums=('507d394b715cdb4756887a332de3e0cdf2903c0571f389d4b93d4ea0d99ee805ffc23897847207f18874a78dfb87d8f06774654821862ca245c2432e651cb02e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    alsa-lib libasound.so
    libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
