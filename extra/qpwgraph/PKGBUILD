# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.7.7
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('8e9a2a16dd0d895c69568b6888eb92a797d47c791531799b16ae89a44243e1c963758a91523a1de13cd1bd433509b0afa8682619fb139bb3af331c654a0f0074')
b2sums=('c7e28eabf80432cf94903e533ac310500c7d4eea9952070af931c8492b7c7efa6cfa5a1640120c052daea6d9322dd54f379bc880362637bc61020e2e9da6fb5e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    alsa-lib libasound.so
    libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
