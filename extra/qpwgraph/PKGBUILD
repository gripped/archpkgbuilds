# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.9.2
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('a613ef7aa81559ce1762112be0fca60ead015df8b1d8edbc21e881f1bcebc54c3debd72e5bb107676f1bb5d591cd7abcae8e9fe5aa8a364c20e4b3921c985952')
b2sums=('6fb1f50066bfd77bd2b4f5f20f3b2c73c487674969160f1d860b4b92348ed7a5cc6a79c94460aeca55ec06b3ec109608e0865289e4529457e8ff262e6ac8e32e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    alsa-lib libasound.so
    libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
