# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.8.2
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('42baf7736dac19ea30b33aea87dc722b40205e9100c1d103b59578c83cc94c9bb56a1bfbf519aab02c0cc41ada5c31839df1eaedd68289bd9529a4e53c21ac61')
b2sums=('b525a7b8b90e62674e096073fb8f9fc7693758909d0337a280dda85d05555d22ae2089dceb19c5c9932aa27dd1848b2c437136092cf04c7b7b21c6a1cfeadc57')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    alsa-lib libasound.so
    libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
