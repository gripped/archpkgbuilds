# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=xca
pkgver=2.7.0
pkgrel=1
pkgdesc='Graphical user interface to OpenSSL, RSA/DSA public keys, certificates, signing requests and revokation lists'
arch=('x86_64')
url='https://hohnstaedt.de/xca/'
license=('BSD-3-Clause')
makedepends=('cmake')
depends=('qt6-tools' 'libltdl' 'desktop-file-utils' 'gtk-update-icon-cache')
source=("https://github.com/chris2511/${pkgname}/releases/download/RELEASE.${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('33b19c43b55077b7508420c73d60f9c8c6969d3ab7a9f80fea75ec7b089f7cb44bc8ddccf9d3c1f8d064b877dcb15183f7875b717591972cdae8c67513c4cb16')

build () {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  cmake --build .
}

package() {
  cd "${pkgname}-${pkgver}"
  cmake --install . --prefix "${pkgdir}/usr"
  install -m 644 -D COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
}
