# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=k9s
pkgver=0.50.18
pkgrel=1
pkgdesc='TUI for managing Kubernetes clusters and pods'
arch=(x86_64)
url='https://github.com/derailed/k9s'
license=(Apache-2.0)
depends=(glibc)
makedepends=(
  git
  go
)
options=('!lto')
source=("git+$url#tag=v$pkgver")
b2sums=('17fd1f82104111f2cedbaa93027b9a58c115eb05c8c03b1be7288a2b21940c1c7c93577b5af53097b51e71c92f906f395482d491d44a6e6b17f3ad042582791d')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -X github.com/derailed/k9s/cmd.commit=$(git rev-parse --short HEAD) \
    -X github.com/derailed/k9s/cmd.date=NOTSET \
    -X github.com/derailed/k9s/cmd.version=$pkgver \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "$ld_flags" -tags=netgo -o execs/k9s main.go
}

check() {
  cd $pkgname
  # Not sure why this test fails
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/derailed/k9s/internal/view/cmd  \
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  execs/k9s completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/k9s"
  execs/k9s completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_k9s"
  execs/k9s completion fish | install -Dm644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/k9s.fish"
  install -Dm755 "execs/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/derailed/k9s/releases
# vim: ts=2 sw=2 et:
