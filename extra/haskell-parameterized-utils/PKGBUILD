# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=parameterized-utils
pkgname=haskell-parameterized-utils
pkgver=2.1.11.0
pkgrel=3
pkgdesc="Classes and data structures for working with data-kind indexed types"
url="https://github.com/GaloisInc/parameterized-utils"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-constraints' 'haskell-hashable'
         'haskell-hashtables' 'haskell-indexed-traversable' 'haskell-lens' 'haskell-profunctors'
         'haskell-th-abstraction' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-hedgehog' 'haskell-hedgehog-classes' 'haskell-tasty'
             'haskell-tasty-ant-xml' 'haskell-tasty-hunit' 'haskell-tasty-hedgehog')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ee9780e2618fd1c9834be64516e37ae2e5b333b991cee35d59edb5c4617ea7337a78f3cb0663fe3ffe66187f48bd86c7922d1d288d104d9b90d8d1c9223a4162')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
