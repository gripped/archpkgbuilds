# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-datetime
pkgver=2.4.2
pkgrel=1
pkgdesc='Date & Time indicator for Wingpanel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-datetime
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  evolution-data-server
  glib2
  glibc
  gtk3
  libgee
  libgranite.so
  libhandy-1.so
  libical
  libwingpanel.so
)
makedepends=(
  git
  meson
  vala
)
_tag=f5fad6090a599f97cbf31335f1a4c654faf23780
source=(git+https://github.com/elementary/wingpanel-indicator-datetime.git#tag=${_tag})
b2sums=('046f672ef1c0edcc6b961fea1a17bf477044f9457c1eb26f09017ef7e142aaf6a1b43aa2e8d81d233d11068d7bfeb8123c73b4074f8a828348d9b8b6e63a5599')

pkgver() {
  cd wingpanel-indicator-datetime
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-datetime build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
