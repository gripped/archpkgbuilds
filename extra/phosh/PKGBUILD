# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker <tw.booker@outlook.com>
# Contributor: Philip Goto <philip.goto@gmail.com>

pkgname=phosh
pkgver=0.49.0
pkgrel=1
pkgdesc='A pure Wayland shell prototype for GNOME on mobile devices'
arch=('x86_64' 'aarch64')
url='https://gitlab.gnome.org/World/Phosh/phosh'
license=(GPL-3.0-or-later)
depends=(
        "gtk3"
        "libhandy"
        "gnome-desktop"
        "gnome-session"
        "upower"
        "libpulse"
        "gcr"
        "libnm"
        "phoc"
        "gnome-shell"
        "callaudiod"
        "evince"
        "fribidi"
        "wayland"
        "evolution-data-server"
        "libsecret"
        "polkit"
        "libadwaita"
        "libical"
        "libedataserverui4"
        "squeekboard"
        "libgmobile"
        "gnome-bluetooth-3.0"
)
makedepends=(
            "meson"
            "git"
            "wayland-protocols"
            "python-docutils"
            "python-packaging"
            "feedbackd"
            "libgmobile"
            "glib2-devel"
)
checkdepends=(
              "xorg-server-xvfb"
              "xorg-xauth"
)
optdepends=(
            'iio-sensor-proxy: accelerometer and other sensors'
            'feedbackd: haptic/visual/audio feedback'
            'xdg-desktop-portal-gtk: for screenshot support'
            'xdg-desktop-portal-wlr: for screencasts support'
)
source=(
        "git+${url}.git#tag=v${pkgver}"
        "pam_phosh"
        "https://gitlab.gnome.org/World/Phosh/phosh/-/merge_requests/1721.patch"
        "https://gitlab.gnome.org/World/Phosh/phosh/-/merge_requests/1763.patch"
)
validpgpkeys=("0DB3932762F78E592F6522AFBB5A2C77584122D3")
sha256sums=('dc19e208c4030a18dc7f8bec291201d5e93b7faeb945c0ea39022d80572d69ed'
            'b7793f80c533e84ad8adfe8bb46c69f107575e724aa9b53b41f370baa37e4fd5'
            '69136ff3b0afbea8e83ded266dcc2bd8192fb40f00badb8d2f4d64f7b261b975'
            '7bbc0a4defc95577cf251921bc053aa433eb93c24715b33321efdb53b97e09d1')

prepare() {
  cd phosh
  patch -Rp1 -i ../1721.patch #check-deprecated-ui-props test fail with this applied, so revert it
  # fix the build test error of: "ninja: error: '/home/xyz//phosh/src/src/dbus/org.gnome.Shell.SearchProvider2.xml', needed by 'tests/services/phosh-search-provider-mock-generated.c', missing and no known rule to make it"
  patch -p1 -i ../1763.patch
  # Run meson once to setup the git submodule for libcall-ui as it should not be packaged
  meson setup -D gtk_doc=false prep
}

build() {
  arch-meson --libexecdir=/usr/lib/phosh  \
    -D tests=true -D phoc_tests=disabled \
    -D man=true -D gtk_doc=false \
    -D lockscreen-plugins=true phosh build 
  meson compile -C build
}

# Test phosh:tools / check-deprecated-ui-props fails, so disable test for now until I can get it fixed.
check() {
    xvfb-run meson test --no-suite screenshots -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  # make squeekboard the default keyboard 
  mkdir -p "$pkgdir"/usr/share/applications
  ln -s sm.puri.Squeekboard.desktop "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop

  install -Dm644 "$srcdir"/pam_phosh "$pkgdir"/etc/pam.d/phosh
}
