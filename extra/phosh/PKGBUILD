# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker <tw.booker@outlook.com>
# Contributor: Philip Goto <philip.goto@gmail.com>

pkgname=phosh
pkgver=0.52.1
pkgrel=1
pkgdesc='A Wayland shell for GNOME on mobile devices'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/phosh'
license=(GPL-3.0-or-later)
depends=(
  appstream
  bash
  cairo
  callaudiod
  dconf
  evince
  evolution-data-server
  feedbackd
  fribidi
  gcc-libs
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gnome-bluetooth-3.0
  gnome-desktop
  gnome-session
  gnome-shell
  gsettings-desktop-schemas
  gtk3
  gtk4
  hicolor-icon-theme
  iio-sensor-proxy
  libadwaita
  libgmobile
  libgudev
  libhandy
  libical
  libmm-glib
  libnm
  libpulse
  libsecret
  libsoup3
  pam
  pango
  phoc
  phosh-osk-provider
  polkit
  qrcodegen
  systemd-libs
  upower
  wayland
  xdg-desktop-portal-gtk
  xdg-desktop-portal-phosh
  xdg-desktop-portal-wlr
)
makedepends=(
  git
  glib2-devel
  meson
  python-docutils
  wayland-protocols
)
checkdepends=(
  xmlstarlet
  xorg-server-xvfb
)
source=(
  "git+https://gitlab.gnome.org/World/Phosh/phosh.git?signed#tag=v$pkgver"
  git+https://gitlab.gnome.org/World/Phosh/libcall-ui.git
  git+https://gitlab.gnome.org/guidog/libgnome-volume-control.git
  phosh.pam
)
b2sums=('855564e4c4a3e1757d72b70ceb448616fddddd7a5625c603bca1c720d0f9f40c71876577f8f77a28d7d01dc2e0b7d6e3b1bfbfa45169e802cabc8a7ff6620d69'
        'SKIP'
        'SKIP'
        'd89e16605e2e8e29834f9537bd09ef3f76f1cf6420cdd99346a760cab8c7217f86898be66c24b206b464239fc8b5277150abd19bae81af72187a435bd8c2f869')
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido GÃ¼nther <guido@xcpu.ch>

prepare() {
  ln -s "$srcdir/libcall-ui" "$pkgname/subprojects/libcall-ui"
  ln -s "$srcdir/libgnome-volume-control" "$pkgname/subprojects/gvc"
  meson subprojects checkout --sourcedir $pkgname
}

build() {
  arch-meson $pkgname build \
    --libexecdir=/usr/lib/phosh \
    -D phoc_tests=disabled \
    -D man=true
  meson compile -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 "$srcdir"/phosh.pam "$pkgdir"/etc/pam.d/phosh
}
