# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=python-cysignals
pkgver=1.11.3
pkgrel=1
pkgdesc='Interrupt and signal handling for Cython'
arch=(x86_64)
url='https://www.sagemath.org'
license=(LGPL)
depends=(glibc
         pari
         python)
makedepends=(cython
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://pypi.python.org/packages/source/c/cysignals/cysignals-$pkgver.tar.gz)
sha256sums=('c2c01f666e5904948952a2250548d8c36ccedd640383791feacf26ea2b958365')

build() {
  cd $pkgbase-$pkgver
  CFLAGS+=" -Wp,-U_FORTIFY_SOURCE" \
  python -m build --wheel --no-isolation
}

package_python-cysignals() {
  cd $pkgbase-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
