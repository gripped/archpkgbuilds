# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=python-cysignals
pkgver=1.11.4
pkgrel=3
pkgdesc='Interrupt and signal handling for Cython'
arch=(x86_64)
url='https://www.sagemath.org'
license=(LGPL)
depends=(glibc
         pari
         python)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/sagemath/cysignals#tag=$pkgver)
sha256sums=('993424eb0e8028961978656a7025f238441e9273a0a1a8a10f77b5f3da0e0ee0')

build() {
  cd $pkgbase
  python -m build --wheel --no-isolation
}

package_python-cysignals() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
