# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=python-cysignals
pkgver=1.12.6
pkgrel=2
pkgdesc='Interrupt and signal handling for Cython'
arch=(x86_64)
url='https://www.sagemath.org'
license=(LGPL-3.0-only)
depends=(glibc
         python)
makedepends=(cython
             git
             meson-python
             python-build
             python-installer)
checkdepends=(python-pytest)
source=(git+https://github.com/sagemath/cysignals#tag=$pkgver)
sha256sums=('3e25bfafdb48ff32e38c6ead9020371e3aa15a88790e343fd94f935e840179d8')

build() {
  cd $pkgbase
  CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=3/} \
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgbase
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package_python-cysignals() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
