# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=python-cysignals
pkgver=1.12.2
pkgrel=2
pkgdesc='Interrupt and signal handling for Cython'
arch=(x86_64)
url='https://www.sagemath.org'
license=(LGPL-3.0-only)
depends=(glibc
         python)
makedepends=(cython
             git
             meson-python
             python-build
             python-installer)
source=(git+https://github.com/sagemath/cysignals#tag=$pkgver)
sha256sums=('5872e1cfb1151b6c6ea4376f4bfac6910b2d5ff33e906f78baf9e24e3a73f34a')

build() {
  cd $pkgbase
  CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=3/} \
  python -m build --wheel --no-isolation
}

package_python-cysignals() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
