# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=python-cysignals
pkgver=1.12.0
pkgrel=3
pkgdesc='Interrupt and signal handling for Cython'
arch=(x86_64)
url='https://www.sagemath.org'
license=(LGPL-3.0-only)
depends=(glibc
         python)
makedepends=(cython
             git
             meson-python
             python-build
             python-installer)
source=(git+https://github.com/sagemath/cysignals#tag=$pkgver
        fix-build-of-consumers.patch)
sha256sums=('61658dda2f7ef2f6dc6005b372a01251a443d63fd3f9634358c13caa9fbfbce4'
            '523d50764e22886632a729172ef37e74776b8e8d88b104388d80e84139d409c2')

prepare() {
# https://github.com/sagemath/cysignals/issues/212
  patch -d $pkgbase -p1 < fix-build-of-consumers.patch
}

build() {
  cd $pkgbase
  CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=3/} \
  python -m build --wheel --no-isolation
}

package_python-cysignals() {
  cd $pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
}
