# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Anton Kudelin <kudelin at protonmail dot com>

_pyname=cylp
pkgname=python-$_pyname
pkgver=0.94.0
pkgrel=2
pkgdesc='A Python interface to CLP, CBC, and CGL to solve LPs and MIPs'
arch=(x86_64)
url='https://github.com/coin-or/CyLP'
license=(EPL-2.0)
depends=(coin-or-cbc
         coin-or-cgl
         coin-or-clp
         coin-or-coinutils
         coin-or-osi
         gcc-libs
         glibc
         python
         python-numpy
         python-scipy)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
source=(git+https://github.com/coin-or/CyLP#tag=v$pkgver)
sha256sums=('52035e08dce40919da8342a679fb5813eddb78ddb219203bdd217cc6496514fe')

build() {
  cd CyLP
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd CyLP
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
# Prevent from loading the uncompiled cylp
  mv cylp/tests .
  rm -r cylp
# Many tests segfault https://github.com/coin-or/CyLP/issues/105
# test-env/bin/python -m pytest -v'
}
  
package() {
  cd CyLP
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm755 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
