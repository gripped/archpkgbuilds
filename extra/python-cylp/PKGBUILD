# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Anton Kudelin <kudelin at protonmail dot com>

_pyname=cylp
pkgname=python-$_pyname
pkgver=0.92.3
pkgrel=2
pkgdesc='A Python interface to CLP, CBC, and CGL to solve LPs and MIPs'
arch=(x86_64)
url='https://github.com/coin-or/CyLP'
license=(EPL-2.0)
depends=(coin-or-cbc
         coin-or-cgl
         coin-or-clp
         coin-or-coinutils
         coin-or-osi
         gcc-libs
         glibc
         python
         python-numpy
         python-scipy)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
source=(git+https://github.com/coin-or/CyLP#tag=v$pkgver
        numpy-2.0.patch)
sha256sums=('0ffa66cb53b5cd626e492ff585298cb9e04b1be23280646d6dc481445d6e6a8d'
            'c32f82aebcad77c1f24d5f1da29b025de926327b0ffb9198eca54776143b47cf')

prepare() {
  patch -d CyLP -p1 < numpy-2.0.patch
}

build() {
  cd CyLP
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd CyLP
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
# Prevent from loading the uncompiled cylp
  mv cylp/tests .
  rm -r cylp
# Many tests segfault https://github.com/coin-or/CyLP/issues/105
# test-env/bin/python -m pytest -v'
}
  
package() {
  cd CyLP
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm755 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
