# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Anton Kudelin <kudelin at protonmail dot com>

_pyname=cylp
pkgname=python-$_pyname
pkgver=0.93.1
pkgrel=1
pkgdesc='A Python interface to CLP, CBC, and CGL to solve LPs and MIPs'
arch=(x86_64)
url='https://github.com/coin-or/CyLP'
license=(EPL-2.0)
depends=(coin-or-cbc
         coin-or-cgl
         coin-or-clp
         coin-or-coinutils
         coin-or-osi
         gcc-libs
         glibc
         python
         python-numpy
         python-scipy)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
source=(git+https://github.com/coin-or/CyLP#tag=v$pkgver
        cython-3.1.patch)
sha256sums=('bca88302f52f7416a3d40c03df479c702f52eab5ab94fe48cf3b03f4508f3a66'
            '309b903517d9cc2b31b3684269c023b06a6ecbc39b95915634030fb1893f46e7')

prepare() {
  cd CyLP
  patch -p1 -i ../cython-3.1.patch
}

build() {
  cd CyLP
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd CyLP
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
# Prevent from loading the uncompiled cylp
  mv cylp/tests .
  rm -r cylp
# Many tests segfault https://github.com/coin-or/CyLP/issues/105
# test-env/bin/python -m pytest -v'
}
  
package() {
  cd CyLP
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm755 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
