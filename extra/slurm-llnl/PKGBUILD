# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>
# Contributor: Alan Orth <alan.orth[at]gmail[dot]com >
# Contributor: Fredrik Tegenfeldt <fredrik.tegenfeldt@unige.ch>

pkgname=slurm-llnl
pkgver=24.11.4.1
# Hyphens (-) are prohibited however they are used by the package author
# therefore it is necessary to invoke ${pkgver} like ${pkgver//./-}
# this will substitute any full stops (.) with hyphens
pkgrel=2
pkgdesc="Simple Linux Utility for Resource Management"
arch=('x86_64')
url="https://www.schedmd.com"
license=('GPL-2.0-or-later')
depends=(
  curl
  dbus
  gcc-libs
  glibc
  hwloc
  lz4
  munge
  numactl
  pam
  readline
)
makedepends=(
  freeipmi
  gtk2
  hdf5
  http-parser
  json-c
  level-zero-headers
  level-zero-loader
  libyaml
  lua
  mariadb-libs
  openpmix
  python
  rdma-core
  rrdtool
)
optdepends=(
  "mariadb-libs: support for accounting"
  "openpmix: support Open MPI applications using PMIx"
  "freeipmi: for the acct_gather_energy_xcc plugin"
  "hdf5: support for acct_gather_profile/hdf5 job profiling"
  "lua: the lua API will be available in various plugins"
  "rdma-core: support for the acct_gather_infiniband/ofed InfiniBand accounting plugin"
  "rrdtool: support for the ext_sensors/rrd plugin"
  "gtk2: enables the sview command, a simple graphical frontend"
  "http-parser: for slurmrestd"
  "json-c: JSON serializer support"
  "libyaml: YAML serializer support"
)
backup=('etc/default/slurmd' 'etc/default/slurmctld' 'etc/default/slurmdbd')
source=("https://github.com/SchedMD/slurm/archive/slurm-${pkgver//./-}.tar.gz"
        "slurm-llnl.sysusers"
        "slurm-llnl.tmpfiles")
sha512sums=('c8d911d9c443a5bd7b9ebf9aae55883460c07f0eec1fbf0154713018cf26ea1c2ae8c8dc549da59832e4e779e0a7c0bdaadd2722fe5f16e30846b3805b00fe3f'
            'c93f383f7e7ec959a3d4375b1d1a14521757c0c2bb034d3dea5604e5b6332f4d5e6f9024f0f650729931055868b80b0d7b5bd6aaea6429a70c63a2b86b53e549'
            '4f7d1e36abc2ca5aa38b40403292b68f769238766ecdd44ea5d29f8106bd9b7c3e0d2236208f92e00818e37dd24c9520b6e9fe06e01b6e552ac485a1df682edd')


build() {
  cd slurm-slurm-${pkgver//./-}

  # If required libraries or header files are in non-standard locations,
  # set CFLAGS and LDFLAGS environment variables accordingly.
  # Optional Slurm plugins will be built automatically when the configure script
  # detects that the required build requirements are present.
  #
  # Read https://slurm.schedmd.com/quickstart_admin.html for more information

  # Slurm requires a relaxed build environment, see https://bugs.schedmd.com/show_bug.cgi?id=2443
  export CFLAGS="${CFLAGS//-z,now/-z,lazy}"
  export CFLAGS="${CFLAGS//-fno-plt/}"
  export LDFLAGS="${LDFLAGS//-z,now/-z,lazy}"

  autoreconf
  ./configure \
    --disable-developer \
    --disable-debug \
    --without-rpath \
    --enable-optimizations \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/slurm-llnl \
    --localstatedir=/var \
    --enable-pam \
    --with-hdf5 \
    --with-hwloc \
    --with-rrdtool \
    --with-munge \
    --with-pmix

  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd slurm-slurm-${pkgver//./-}

  make DESTDIR="$pkgdir" install

  # Install slurm's configs and license
  install -Dm644 etc/slurm.conf.example    -t "$pkgdir"/etc/slurm-llnl/
  install -Dm644 etc/slurmdbd.conf.example -t "$pkgdir"/etc/slurm-llnl/
  install -Dm644 etc/cgroup.conf.example   -t "$pkgdir"/etc/slurm-llnl/
  install -Dm644 LICENSE.OpenSSL           -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -Dm644 COPYING                   -t "$pkgdir"/usr/share/licenses/$pkgname/

  install -dm755 "$pkgdir"/etc/default
  echo 'SLURMD_OPTIONS=""' > "$pkgdir"/etc/default/slurmd
  echo 'SLURMCTLD_OPTIONS=""' > "$pkgdir"/etc/default/slurmctld
  echo 'SLURMDBD_OPTIONS=""' > "$pkgdir"/etc/default/slurmdbd
  chmod 644 "$pkgdir"/etc/default/{slurmd,slurmctld,slurmdbd}

  # Install init related files
  install -Dm644 etc/slurmctld.service -t "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 etc/slurmd.service    -t "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 etc/slurmdbd.service  -t "$pkgdir"/usr/lib/systemd/system/

  # Install SystemD related files
  install -Dm644 ../slurm-llnl.sysusers "$pkgdir"/usr/lib/sysusers.d/slurm-llnl.conf
  install -Dm644 ../slurm-llnl.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/slurm-llnl.conf

  # Create a log and a lib dir
  install -dm755 "$pkgdir"/var/log/slurm-llnl
  install -dm755 "$pkgdir"/var/lib/slurm-llnl
}
