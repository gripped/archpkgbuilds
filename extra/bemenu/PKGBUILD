# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Ivy Foster <iff@archlinux.org>

pkgbase=bemenu
pkgname=(
  bemenu
  bemenu-ncurses
  bemenu-wayland
  bemenu-x11
)
pkgver=0.6.21
pkgrel=1
pkgdesc="Dynamic menu library and client program inspired by dmenu"
url="https://github.com/Cloudef/bemenu"
arch=(x86_64)
license=(
  GPL-3.0-or-later
  LGPL-3.0-or-later
)
makedepends=(
  libxinerama
  libxkbcommon
  ncurses
  pango
  scdoc
  wayland
  wayland-protocols
  wlroots
)
source=(
  "$url/releases/download/$pkgver/$pkgbase-$pkgver.tar.gz"
  "$url/releases/download/$pkgver/$pkgbase-$pkgver.tar.gz.asc"
)
sha256sums=(
  '86af530620ecf6db3ebc196656b76f0f15350028c9c5a137e975f085dafdfe6e'
  'SKIP'
)
validpgpkeys=(16B6B7CACAF339DECE9F154729317348D687B86B) # Bemenu Upstream

build() {
  cd "bemenu-$pkgver"
  make PREFIX=/usr
}

package_bemenu() {
  depends=(
    bemenu-renderer
    glibc
  )
  provides=(libbemenu.so)

  cd "bemenu-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr install-base install-docs
}

package_bemenu-ncurses() {
  pkgdesc="ncurses renderer for bemenu"
  provides=(bemenu-renderer)
  depends=(
    glibc
    libbemenu.so
    ncurses
  )

  cd "bemenu-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr install-curses
}

package_bemenu-wayland() {
  pkgdesc="Wayland (wlroots-based compositors) renderer for bemenu"
  provides=(bemenu-renderer)
  depends=(
    glibc
    cairo
    glib2
    libbemenu.so
    libxkbcommon
    pango
    wayland
  )
  install=bemenu-wayland.install

  cd "bemenu-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr install-wayland
}

package_bemenu-x11() {
  pkgdesc="X11 renderer for bemenu"
  provides=(bemenu-renderer)
  depends=(
    glibc
    cairo
    glib2
    libbemenu.so
    libx11
    libxinerama
    pango
  )

  cd "bemenu-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr install-x11
}
