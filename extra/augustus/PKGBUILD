# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=augustus
pkgver=4.0.0
pkgrel=2
pkgdesc='An enhanced re-implementation of Caesar III'
arch=('x86_64')
url='https://github.com/Keriew/augustus'
license=('AGPL-3.0-only')
depends=(
  'glibc'
  'hicolor-icon-theme'
  'expat'
  'zlib'
  'sdl2'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('c3bafe3d13d281530cc90bd4453a7837dc0b15f249e5bcff03df0001e938944433655a3dd197582905df59cbfcdc4b8273b026375ceabe7b60224ecf557b48fb')
b2sums=('bdb7805c60c11aae31f3e95df573f85af36576eabfec7245b41580f4bff5507ebf9d0b3c1be0cacfb23fe859f005fd91ab15a5507aaafcca86f50a4164d2b302')

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -D CMAKE_INSTALL_PREFIX='/usr' \
    -D CMAKE_BUILD_TYPE='Release'

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE.txt"
}
