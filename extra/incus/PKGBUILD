# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>
# Contributor: Asterios Dimitriou <asterios@pci.gr>
# Contributor: Benjamin Asbach <archlinux-aur.lxd@impl.it>
# Contributer: nightuser <nightuser.android at gmail.com>

pkgbase=incus
pkgname=(incus incus-tools)
pkgver=6.5
pkgrel=2
pkgdesc="Powerful system container and virtual machine manager"
arch=('x86_64')
url="https://linuxcontainers.org/incus/"
license=('Apache-2.0')
depends=('lxc' 'lxcfs' 'squashfs-tools' 'dnsmasq' 'cowsql' 'libuv' 'ebtables' 'raft' 'sqlite' 'libcap' 'acl'

          # VM Support
          'qemu-base' 'qemu-chardev-spice' 'qemu-hw-usb-redirect'
          'qemu-hw-display-virtio-vga' 'qemu-hw-display-virtio-gpu'
)
makedepends=('go' 'git' 'tcl' 'apparmor' 'libseccomp' 'systemd')
optdepends=(
    'lvm2: lvm2 support'
    'thin-provisioning-tools: thin provisioning support'
    'btrfs-progs: btrfs storage driver support'
    'minio: storage buckets support'
    'cdrtools: VM support'
    'systemd-libs: unix device hotplug support'
    'apparmor: apparmor support'
    'xdelta3: support delta downloads of images'
    'skopeo: OCI support'
    'umoci: OCI support'
)
source=("https://linuxcontainers.org/downloads/incus/incus-$pkgver.tar.xz"{,.asc}
        "incus.socket"
        "incus.service"
        "incus-user.socket"
        "incus-user.service"
        "incus.sysusers"
        # qemu 9.1 patches

        # [PATCH] incusd/instance/qemu: Set O_DIRECT when passing in FDs
        "$pkgname-o-direct.patch::https://github.com/lxc/incus/commit/572afb06f66f83ca95efa1b9386fceeaa1c9e11b.patch"
        # [PATCH] incusd/instance/qemu: Make O_DIRECT conditional on
        "$pkgname-o-direct-conditional.patch::https://github.com/lxc/incus/commit/0c37b7e3ec65b4d0e166e2127d9f1835320165b8.patch"
        # [PATCH] incusd/instance/qemu: Force threads I/O mode for
        "$pkgname-force-threads.patch::https://github.com/lxc/incus/commit/58eeb4eeee8a9e7f9fa9c62443d00f0ec6797078.patch"
        # [PATCH] incusd/instance/qemu: Move away from deprecated fd: syntax
        "$pkgname-remove-deprecated-fd.patch::https://github.com/lxc/incus/commit/4acc15c0dcab2fe4c076eb8422bc1215fc5d4d7a.patch"
        # [PATCH] incusd/apparmor: Add sys_rawio for QEMU 9.1
        "$pkgname-apparmror-rawio.patch::https://github.com/lxc/incus/commit/b77c1ee8aca2da814a534ad45ab21223a0f8faf5.patch"
        )
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67'  # St√©phane Graber <stgraber@stgraber.org>
             )
sha256sums=('d79d76c42c430b0346f25fee1059dbe0ab0db2319faaa6e0eeb6ad3982023662'
            'SKIP'
            '602a8035cc51b8e0c4ca265e31ebf96c7977db4239d384d8f60fed0d2d0317c4'
            '28e99ff6333aec1dd1cac539f7664cbf855ddcbea9de4a0fea6247979b10e28f'
            '50288528ea910d186157a2515c5200b3b073f645922fb581d1913f236f14667d'
            '1c934cac1aa61be2ede5830ea76dbb9ba199498f6eb920ce1016bbb54dd088bf'
            '90beead5dbde947936a58df8773519c49d92a2028881ae0ec81ee5402510e39a'
            '61ece65bb584372cf7f3ed7076c3e10f8c54fc4170187a15a08e102b8d71d954'
            '4058744cc1b2a0862ddca2200f0dba7f4f2f464ba2b0c75ec10698cf2dc24722'
            'd713dad6875062ebf55a9c049ff68117b38545cd682b6933512d5a684122aa32'
            'e458a5b83bacc8ec4345d92e2940598bc6b6c641a3d595b335c4c7296a9a4795'
            '18e428fd2fe376190ea6bed4a7eba323e9077884c69bd9fba68ecb131304ddba')

prepare() {
  cd "$pkgbase-${pkgver}"

  mkdir bin
  go mod verify

  patch -Np1 < "$srcdir/incus-o-direct.patch"
  patch -Np1 < "$srcdir/incus-o-direct-conditional.patch"
  patch -Np1 < "$srcdir/incus-force-threads.patch"
  patch -Np1 < "$srcdir/incus-remove-deprecated-fd.patch"
  patch -Np1 < "$srcdir/incus-apparmror-rawio.patch"
}

build() {
  cd "$pkgbase-${pkgver}"

  export GOFLAGS="-buildmode=pie -modcacherw"
  export CGO_LDFLAGS_ALLOW="-Wl,-z,now"
  GO_LDFLAGS="-compressdwarf=false -linkmode external"

  CGO_LDFLAGS="-static" go build -v -tags "agent" -o bin/ ./cmd/incus-agent/...

  go build -v -ldflags "${GO_LDFLAGS}" -tags "netgo" -o bin/ ./cmd/incus-migrate/...
  for tool in fuidshift incus lxc-to-incus lxd-to-incus incusd incus-benchmark incus-user; do
    go build -v -ldflags "${GO_LDFLAGS}" -tags "libsqlite3" -o bin/ ./cmd/$tool
  done

  # Generate manpages - not worth it
  # mkdir manpages
  # ./bin/incus manpage --all ./manpages
  # ./bin/incusd manpage --all ./manpages
}

package_incus() {
  cd "$pkgbase-${pkgver}"

  for tool in incus incusd incus-agent incus-user lxd-to-incus; do
    install -v -p -Dm755 "bin/$tool" "${pkgdir}/usr/bin/$tool"
  done

  # Package license
  install -v -Dm644 "COPYING"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"

  # systemd files
  install -v -Dm644 "${srcdir}/"incus.{service,socket} -t "${pkgdir}/usr/lib/systemd/system"
  install -v -Dm644 "${srcdir}/"incus-user.{service,socket} -t "${pkgdir}/usr/lib/systemd/system"
  install -v -Dm644 "${srcdir}/$pkgname.sysusers" "${pkgdir}/usr/lib/sysusers.d/$pkgname.conf"

  # logs
  install -v -dm700 "${pkgdir}/var/log/incus"

  # documentation
  install -d "${pkgdir}/usr/share/doc/incus/"
  rm -rf doc/html
  cp -vr doc/* "${pkgdir}/usr/share/doc/incus/"

  # Bash completions
	./bin/incus completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/incus"
	./bin/incus completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_incus"
	./bin/incus completion fish | install -Dm644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/incus.fish"
}

package_incus-tools() {
  conflicts=('lxd')
  cd "$pkgbase-${pkgver}"
  for tool in fuidshift lxc-to-incus incus-benchmark; do
    install -v -p -Dm755 "bin/$tool" "${pkgdir}/usr/bin/$tool"
  done
  # Package license
  install -v -Dm644 "COPYING"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
}

# vim:set ts=2 sw=2 et:
