# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jiachen YANG <farseerfc@gmail.com>
# Contributor: Allen Wild <allenwild93@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: dkaylor <dpkaylor@gmail.com>
# Contributor: Pablo Lezaeta <prflr88@gmail.com>

pkgname=xfsdump
pkgver=3.1.12
pkgrel=3
pkgdesc="Additional XFS filesystem utilities"
arch=(x86_64)
url="http://xfs.org"
license=('GPL-2.0-only')
depends=('xfsprogs' 'attr' 'acl' 'util-linux-libs' 'gcc-libs' 'ncurses' 'glibc')
makedepends=('git')
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsdump-dev.git?signed#tag=v${pkgver}")
# Eric R. Sandeen <sandeen@sandeen.net>
# Carlos Eduardo Maiolino <carlos@maiolino.me> <cem@kernel.org>
validpgpkeys=('2B8185919E8D248981869DED20AE1692E13DDEE0'
'4020459E58C1A52511F5399113F703E6C11CF6F0')
b2sums=('56c3d8e6bea56f918fb2d64fe67e4270a3b5daca7cfce665b54f65ec4838673dca1f21917d2a179d453bc715e779855e7391efd32eb0dd07c32597a9299c3d36')

prepare() {
  cd "${pkgname}-dev"
  make configure
}

build() {
  cd "${pkgname}-dev"
  ./configure --prefix=/usr --sbindir=/usr/bin INSTALL_USER=root INSTALL_GROUP=root
  make
}

package() {
  cd "${pkgname}-dev"
  make DESTDIR="${pkgdir}" install

  # root /sbin directory can't be overridden properly with configure,
  # so move files manually
  mv -vf "${pkgdir}/sbin/"* "${pkgdir}/usr/bin/"
  rm -r "${pkgdir}/sbin"
}
