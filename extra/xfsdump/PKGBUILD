# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jiachen YANG <farseerfc@gmail.com>
# Contributor: Allen Wild <allenwild93@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: dkaylor <dpkaylor@gmail.com>
# Contributor: Pablo Lezaeta <prflr88@gmail.com>

pkgname=xfsdump
pkgver=3.2.0
pkgrel=1
pkgdesc="Additional XFS filesystem utilities"
arch=(x86_64)
url="http://xfs.org"
license=('GPL-2.0-only')
depends=(
  acl
  attr
  gcc-libs
  glibc
  ncurses
  util-linux-libs
  xfsprogs
)
makedepends=('git')
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsdump-dev.git?signed#tag=v${pkgver}")
# Eric R. Sandeen <sandeen@sandeen.net>
# Carlos Eduardo Maiolino <carlos@maiolino.me> <cem@kernel.org>
validpgpkeys=('2B8185919E8D248981869DED20AE1692E13DDEE0'
'4020459E58C1A52511F5399113F703E6C11CF6F0')
b2sums=('9b54c1160922dbf2655bbf346861f736ea6d1a8bb9f36c964523d001ab2ff51bcdcd2da1e737d4b5a8d2d82693ea1054b5f9344431fbe53d274c24311d2e10c9')

prepare() {
  cd "${pkgname}-dev"
  make configure
}

build() {
  cd "${pkgname}-dev"
  ./configure --prefix=/usr --sbindir=/usr/bin INSTALL_USER=root INSTALL_GROUP=root
  make
}

package() {
  cd "${pkgname}-dev"
  make DESTDIR="${pkgdir}" install

  # root /sbin directory can't be overridden properly with configure,
  # so move files manually
  mv -vf "${pkgdir}/sbin/"* "${pkgdir}/usr/bin/"
  rm -r "${pkgdir}/sbin"
}
