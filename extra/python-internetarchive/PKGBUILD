# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: David McInnis <dave@dave3.xyz>
# Contributor: jyantis <yantis@yantis.net>

pkgname=python-internetarchive
_pkgname=${pkgname#python-}
pkgver=5.2.1
pkgrel=1
pkgdesc="A Python and Command-Line Interface to Archive.org"
arch=(any)
url="https://github.com/jjjake/internetarchive"
license=(AGPL-3.0-or-later)
depends=(
  python
  python-jsonpatch
  python-requests
  python-tqdm
  python-urllib3
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-responses
)
optdepends=(
  'cython: speedups'
  'python-gevent: concurrent downloads'
  'python-ujson: faster json parsing'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('bb95ab6477e208c23da30b775ba58e87b5d40cef316e75f1b186ab3dc9395862')

build() {
  cd "$_pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-$pkgver"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  cd "$_pkgname-$pkgver"
  python -m installer -d "$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
