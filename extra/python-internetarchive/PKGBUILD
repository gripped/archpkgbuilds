# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: David McInnis <dave@dave3.xyz>
# Contributor: jyantis <yantis@yantis.net>

pkgname=python-internetarchive
_pkgname=${pkgname#python-}
pkgver=5.0.5
pkgrel=1
pkgdesc="A Python and Command-Line Interface to Archive.org"
arch=(any)
url="https://github.com/jjjake/internetarchive"
license=(AGPL-3.0-or-later)
depends=(
  python
  python-jsonpatch
  python-requests
  python-schema
  python-tqdm
  python-urllib3
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-responses
)
optdepends=(
  'cython: speedups'
  'python-gevent: concurrent downloads'
  'python-ujson: faster json parsing'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('9dd140e5b1bc1939300faee3691831823d6238f76cdaad6b607442e7dc18f841')

build() {
  cd "$_pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-$pkgver"
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$PWD/tmp_install/usr/bin:$PATH"
  export PYTHONPATH="$PWD/tmp_install/$site_packages"
  pytest
}

package() {
  cd "$_pkgname-$pkgver"
  python -m installer -d "$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
