# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=8.0.2
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('GPL')
makedepends=('git' 'cmake')
options=('!strip')
source=(
  "git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-symbols.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-templates.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-footprints.git#tag=$pkgver"
)
sha512sums=('68a954524fcb0efe9ec6beb6064201758db91406d13b1e27b23e4c75a54155cceab8d0a3394aa859922ca26b0d12712a3923cf9dadde08f7b4c43177597506f1'
            '4a8966c5e338eeb3af39db61e7d294b31a19f07d21f8a4f8abb3f99830d83fc85d4129db45d20045ebcc92875c18e9544898541f708e1415d40a2a6fc6de391a'
            'd4f7bd552033e365e2c0c77b4e74a947e34f63728d4dfd4c4d70c6766ed78235e682e750538851171c69b8ac2e0db72afc3b4d307b8b647cee78a816cacef005'
            '81c9e5e4427ce7daec5cc451705e17c6f7ba852750cf40faf1d277544865716394e357d4586d513be657d9ff22feeb215de79ed1ed757c7f2e999f9c53805d00')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
