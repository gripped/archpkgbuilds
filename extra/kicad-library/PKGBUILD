# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=8.0.5
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('CC-BY-SA-4.0')
makedepends=('git' 'cmake')
options=('!strip')
source=(
  "git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-symbols.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-templates.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-footprints.git#tag=$pkgver"
)
sha512sums=('10459de39443a6120f1ba83d970c570288e4119b6debd754c4f80c499b3fb032ee49b4fee9519385410150423d1ab0f96dde59f8637e1eae36c773d5d780c8fb'
            'ac74fba5f57d12cf794d9aafa8a1e4139ef4921574b5c12338b6c4cd1b0152f6701c355c07275f6de3e06057cedd3da1bd3ae3e71b3587def4be98817da2e5e8'
            '12e432100b9962e7f7f6c3f122bc8f956f831cc8ccacf793f070fe46bca94cf20968e201e9feeff16a10d9a430f3244535d82a6787cc105658e9ae992c692a96'
            '13748990d22755c43e6cbd8a5e26151219213e571d96a57d149cb852bfab63c3e4b526089721cb95ba00c33378dd88831451051acb7b50ff6cc0a08a60ef03a0')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
