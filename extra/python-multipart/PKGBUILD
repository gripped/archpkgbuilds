# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-multipart
pkgver=1.3.1
pkgrel=1
pkgdesc='Parser for multipart/form-data'
arch=(any)
url='https://github.com/defnull/multipart'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-flit-core
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('6d14babaa210a243d796be9ae7d3c8e0a3207575ff33e1e569d76a9cf3d46d39e50578e9ac877792c95752b6557cce6c739514eedcc46a5f2130942c0da670a1')
b2sums=('2f74e5306e2c1d2932e72dbf62659e7318698f6c65fd186b17d312cee40a408f5774f5fd05c13dc485c28d4fd672e50b828aeab6b89852c8495c163e0d9e986a')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
