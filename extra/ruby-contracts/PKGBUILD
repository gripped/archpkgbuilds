# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-contracts
pkgver=0.17.3
pkgrel=1
pkgdesc='Contracts for Ruby'
arch=(any)
url='https://github.com/egonSchiele/contracts.ruby'
license=(BSD)
depends=(
  ruby
)
makedepends=(
  git
)
checkdepends=(
  ruby-rspec
)
options=(!emptydirs)
source=(
  "git+${url}#tag=v$pkgver"
)
sha512sums=('4fb92ee756a127ba0da129177cb81513b3adb1e51790ec4b70639d71808a08453cdb7f72882e49dbec85d76313eee59871944389c77e0b3e9894b3aacc3775d1')
b2sums=('07b982f58abf661b2888878c04fd7f13ee7639e7917e8f4f75106814edb662952742a58b0abfc5219afe4eee1e9e7812e10086e672cc0c356000cd84d4c0cc8c')

build() {
  local _gemdir="$(gem env gemdir)"
  cd contracts.ruby

  gem build contracts.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    contracts-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd contracts.ruby
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd contracts.ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
