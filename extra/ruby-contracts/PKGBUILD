# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-contracts
pkgver=0.17.2
pkgrel=4
pkgdesc='Contracts for Ruby'
arch=(any)
url='https://github.com/egonSchiele/contracts.ruby'
license=(BSD)
depends=(
  ruby
)
makedepends=(
  git
)
checkdepends=(
  ruby-rspec
)
options=(!emptydirs)
source=(
  "git+${url}#tag=v$pkgver"
  "${url}/commit/b2ffcca90ffa9fbf4a7293065a8926e7b13ac9f2.patch"
)
sha512sums=('bfb3a575800f11b2adba8ef709ce58acab517c7a05ff609e14f8e4cb2786fee20fb4ff97df9e2569fd48e2c3c661580fb51326c64553603932229a5024a01efc'
            'cc8714bdbec6e6c71150535ede19e5bc7b09def10f45d0895be7798f954e7be6ffe05e7ccd9bbfa1262bf86d1578c8a5eab4185ab076579cd440c93e810fd261')
b2sums=('2c7f1f4ad9e9b4abde5740ccd1a9a77d678c6175ff8915d3769d2fd58f6f0107d23ef8c2378cee1cf547ab2460b6c1d8f3fcd55fb96285e8a74d24b90effcab8'
        '0d8cae8605b588b61c84f6c5fa4b1110bb1fc3b50fff75ba6b2e78c12be162e814cce1473cad06b1931c9ccac9341b786f4d985725c321ac7150f5aa444b787b')

prepare() {
  cd contracts.ruby

  patch --verbose --strip=1 --input="../b2ffcca90ffa9fbf4a7293065a8926e7b13ac9f2.patch"
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd contracts.ruby

  gem build contracts.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    contracts-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd contracts.ruby
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd contracts.ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
