# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.47
pkgrel=2
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs' 'libqtxdg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver")
sha512sums=('4ac61522186bb27ea107001ed3ba1edf2a0f08220427c7957417486ad852392b988d5b5c3786e602c803bd21e72368988bf7fc3dd9c2c65df72d62cfd4581b22')

prepare() {
  cd qt6integration
  git cherry-pick -n 6a99088277b141d6d3932caf0e4fe6c27f1fb86f
}

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
