# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.8
pkgrel=1
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver")
sha512sums=('6843814ef4402ccae11da31d54863f330ecbf39471537b94e1a927d130b7cc68a511c88b9254d9a10bad26c9d1bdb30bead5cf10157f039f7a4e63633cdb953b')

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
