# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.16
pkgrel=1
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver")
sha512sums=('6fcf26698d6709fec1da1e4b7bc591c846bef3f96b56e71e486dcc8fb0051f56239ad5f6048c0b4e13e38da2e64d7a7027de243c3f6c700defd0c4f417f2ed1b')

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
