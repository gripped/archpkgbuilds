# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.35
pkgrel=1
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs' 'libqtxdg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver"
         qt-6.9.patch
         qt-6.9.2.patch
         missing-include.patch)
sha512sums=('cebdd8004e3b5ccd38d8d474ea8f9ac754aeab554ba05a4f92bda1ad1f2fe1818684afe4b0dc7ef8f7229a73e9f7c924109b51ac5b60dc99e8df704f37c686bd'
            '75d2c39b13a1e162a3bfb41010542548ed5b362f34ad42b2ec2e26ba6206d0075abc0b318362263c8e701ba5686e3d31d73f36daef9663f3f6392f929219af5d'
            '0153029a4a10227c08aa5cf89006cd0e7745ce74e0c989f585ac6166b5aa4ebb8f9ab0dc60ca82d9d7852267716bb1525f3b185dbd5b87880703d1a0807a4610'
            'f36f1ea7a93e58ed710d297680a28c68dd2c8fb0f139cd90c2946a11015e7eea7242e472cf8c4484ee9b9725dd41697d6ddc71a35043cec11aac31bd1b4d583b')

prepare() {
  patch -d qt6integration -p1 < qt-6.9.patch
  patch -d qt6integration -p1 < qt-6.9.2.patch
  patch -d qt6integration -p1 < missing-include.patch
}

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
