# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.46
pkgrel=1
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs' 'libqtxdg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver")
sha512sums=('9d0c59e07f9eafed75d9d5dd97e6a5bd55ce3e99feebc915cc792207948a845a56f11772600c1087901b580f80d6cf18bfc71377ba547ad523990246771f7372')

prepare() {
  cd qt6integration
  git cherry-pick -n 6a99088277b141d6d3932caf0e4fe6c27f1fb86f
}

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
