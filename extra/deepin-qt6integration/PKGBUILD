# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.19
pkgrel=1
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs' 'libqtxdg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver")
sha512sums=('8dab40da4f6fb498866c1bce11434cb541c0cdf524677da19c289ffe6cb1e955b787f7885f2f550971ecf91b3e9b2f6bf2b267bf843352b7001c6ad9135abc36')

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
