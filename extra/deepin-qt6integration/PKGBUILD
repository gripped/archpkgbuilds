# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.33
pkgrel=1
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs' 'libqtxdg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver"
         qt-6.9.patch
         missing-include.patch)
sha512sums=('4f9438b73cf979ee4232a2c7570983c79eae28d0c4951bebda7f2fac728316dbd5c1801441be6a402c254323cc70316997cc76d559c000fb5980f4d661be33c2'
            '75d2c39b13a1e162a3bfb41010542548ed5b362f34ad42b2ec2e26ba6206d0075abc0b318362263c8e701ba5686e3d31d73f36daef9663f3f6392f929219af5d'
            'f36f1ea7a93e58ed710d297680a28c68dd2c8fb0f139cd90c2946a11015e7eea7242e472cf8c4484ee9b9725dd41697d6ddc71a35043cec11aac31bd1b4d583b')

prepare() {
  patch -d qt6integration -p1 < qt-6.9.patch
  patch -d qt6integration -p1 < missing-include.patch
}

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
