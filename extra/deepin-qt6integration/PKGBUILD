# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.27
pkgrel=3
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs' 'libqtxdg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver"
         qt-6.9.patch)
sha512sums=('c76dfdda64e105bbc38c491fbe4bb9d983b2120d4250a7f31ff0179c9ce055ef1785d89cf0ad946bc4cf675a81b1e2f4848cab4e9f3a7e57b7bfaefce1b3002e'
            '75d2c39b13a1e162a3bfb41010542548ed5b362f34ad42b2ec2e26ba6206d0075abc0b318362263c8e701ba5686e3d31d73f36daef9663f3f6392f929219af5d')

prepare() {
  patch -d qt6integration -p1 < qt-6.9.patch
}

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
