# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6integration
pkgver=6.0.43
pkgrel=1
pkgdesc='Qt 6 platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt6integration"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'glibc' 'gcc-libs' 'libqtxdg')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6integration.git#tag=$pkgver")
sha512sums=('d5f61d1ac4d31a446098c027b465a39d53ff2ff15ee3492dd9fa0f82a25228b27d5ce86e5d090faa9543509aefbda4b67c5124ee20bab75d8c61627f3cbe9a5b')

build() {
  cd qt6integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd qt6integration
  DESTDIR="$pkgdir" ninja install
}
