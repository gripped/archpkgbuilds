# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=6.6.3
pkgrel=1
pkgdesc='Settings daemon for Cinnamon'
arch=(x86_64)
url='https://github.com/linuxmint/cinnamon-settings-daemon'
license=('GPL-2.0-or-later AND LGPL-2.0-or-later')
depends=(
  cairo
  cinnamon-desktop
  dconf
  fontconfig
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  lcms2
  libcanberra-pulse
  libcolord
  libcups
  libgudev
  libnotify
  libwacom
  libx11
  libxext
  libxi
  nspr
  nss
  pango
  polkit
  pulse-native-provider
  systemd-libs
  upower
)
optdepends=('cinnamon-translations: i18n')
makedepends=(
  git
  glib2-devel
  meson
)
source=("git+https://github.com/linuxmint/cinnamon-settings-daemon.git#tag=$pkgver")
b2sums=('bd29dec8898b054c0b6e0be0bdb943e69867731076bca5171384b2640a9ffa9534d62d75105ce21e9b59fb5acd1b53c65de53e28162f6651eb08fa9c2b233fe3')

build() {
  arch-meson $pkgname build \
    --libexecdir=lib/$pkgname
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
