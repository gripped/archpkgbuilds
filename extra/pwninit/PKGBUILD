# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Benjamin Levy <blevy@protonmail.com>

pkgname=pwninit
pkgver=3.3.2
pkgrel=1
pkgdesc='Automate starting binary exploit challenges'
url='https://github.com/io12/pwninit'
arch=(x86_64)
license=(MIT)
depends=(
  elfutils
  gcc-libs
  glibc
  openssl
  patchelf
  xz
  zstd
)
makedepends=(
  git
  cargo
)
options=(!lto)
source=("git+${url}.git?signed#commit=${pkgver}")
sha256sums=('89944db7d36a4e9f9ea68f56a13500a219f666c89052e45b5bcc362ca060d70b')
b2sums=('6e9e43595aadf854dece5664abc3b718d6f71f2977891040460315c1b9c96562ad0efb29bafe1681845dd560f8e2449e50553cac724e19905b189eac3f4ad4d1')
validpgpkeys=(
  05A4711013D373A1F2FEE261AAB1132642EF3045 # Benjamin Levy <blevy@protonmail.com>
)

prepare() {
  cd "${pkgname}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}"
  cargo test --frozen
}

package() {
  cd "${pkgname}"
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
