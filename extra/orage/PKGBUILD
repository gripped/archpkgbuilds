# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: 0b100100 <0b100100 at protonmail dot ch>

pkgname=orage
pkgver=4.18.0
pkgrel=2
pkgdesc="A simple calendar application with reminders for Xfce"
arch=('x86_64')
url="https://docs.xfce.org/apps/orage/start"
license=('GPL-2.0-or-later')
depends=('glib2' 'libxfce4ui' 'libnotify' 'libical')
makedepends=('git' 'xfce4-dev-tools')
optdepends=('libnotify: desktop notifications support')
source=("git+https://gitlab.xfce.org/apps/orage.git#tag=$pkgname-$pkgver")
sha256sums=('b60993de942c6017635900a251011725f7fdcdd8c9e3cecd982ae872cbdc5a20')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
