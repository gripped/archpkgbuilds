# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-caldav
pkgver=2.2.3
pkgrel=4
pkgdesc='CalDAV (RFC4791) client library for Python'
arch=(any)
url='https://github.com/python-caldav/caldav'
license=('Apache-2.0 OR GPL-3.0-only')
depends=(
  python
  python-dateutil
  python-icalendar
  python-icalendar-searcher
  python-lxml
  python-recurring-ical-events
  python-requests
  python-vobject
)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
  python-wheel
)
source=("git+https://github.com/python-caldav/caldav.git?signed#tag=v$pkgver")
b2sums=(491490dc76a87f4c0d6635298970594cab43cbcafbbd978b1d8e00d99b9d938db88561f2ef8eb154e9c8c6955ad6c5f61534a0ad959e1a6ba9f81045e08f4099)
validpgpkeys=(CC67B0D4A21A344811DD5B54A8591F23C40DEF4E) # Tobias Brox <tobias@redpill-linpro.com>

build() {
  cd caldav
  python -m build --wheel --no-isolation
}

package() {
  cd caldav
  python -m installer --destdir="$pkgdir" dist/*.whl
}
