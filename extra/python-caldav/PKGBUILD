# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-caldav
pkgver=2.0.1
pkgrel=1
pkgdesc='CalDAV (RFC4791) client library for Python'
arch=(any)
url='https://github.com/python-caldav/caldav'
license=('Apache-2.0 OR GPL-3.0-only')
depends=(
  python
  python-dateutil
  python-icalendar
  python-lxml
  python-recurring-ical-events
  python-requests
  python-vobject
)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
  python-wheel
)
source=("git+https://github.com/python-caldav/caldav.git?signed#tag=v$pkgver")
b2sums=(3a23db54607c7c4356b498c21bb8e01966f81ed53d2ebcd6fc1833119c683a2a73fc5df81f36493b6cabd6d6b6335213f18f9177385132bf36ac8671e042354b)
validpgpkeys=(CC67B0D4A21A344811DD5B54A8591F23C40DEF4E) # Tobias Brox <tobias@redpill-linpro.com>

build() {
  cd caldav
  python -m build --wheel --no-isolation
}

package() {
  cd caldav
  python -m installer --destdir="$pkgdir" dist/*.whl
}
