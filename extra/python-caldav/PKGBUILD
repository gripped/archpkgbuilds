# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-caldav
pkgver=1.4.0
pkgrel=3
pkgdesc='CalDAV (RFC4791) client library for Python'
arch=(any)
url='https://github.com/python-caldav/caldav'
license=('Apache-2.0 OR GPL-3.0-only')
depends=(
  python
  python-dateutil
  python-icalendar
  python-lxml
  python-recurring-ical-events
  python-requests
  python-vobject
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-sphinx
  python-wheel
)
checkdepends=(
  python-pytest
  python-tzlocal
)
source=("git+https://github.com/python-caldav/caldav.git?signed#tag=v$pkgver")
b2sums=(31c9bda6730042c32bd1f3e01e51c6b959e60bf376cf0edd0319238a389de63d8f95a55a8c971825d94f324a86643e277698e51d539c016d52a76e179239fbab)
validpgpkeys=(CC67B0D4A21A344811DD5B54A8591F23C40DEF4E) # Tobias Brox <tobias@redpill-linpro.com>

build() {
  cd caldav
  python -m build --wheel --no-isolation
}

check() {
  cd caldav
  pytest
}

package() {
  cd caldav
  python -m installer --destdir="$pkgdir" dist/*.whl
  python -m sphinx -b html docs/source "$pkgdir/usr/share/doc/$pkgname/html"
  rm -r "$pkgdir/usr/share/doc/$pkgname/html/.doctrees"
}
