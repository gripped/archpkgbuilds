# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>

pkgname=minder
pkgver=1.17.0
pkgrel=2
pkgdesc='Mind-mapping application designed for Pantheon'
arch=('x86_64')
url='https://github.com/phase1geo/Minder'
license=('GPL3')
depends=('discount' 'granite' 'gtksourceview4' 'libarchive' 'libhandy')
makedepends=('meson' 'vala')
source=("https://github.com/phase1geo/Minder/archive/$pkgver/Minder-$pkgver.tar.gz")
sha256sums=('440dec61b25efdb40d166f683845aba93cb382bb92379b1a1a66a4ac0f3a37a3')

build() {
  arch-meson Minder-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s com.github.phase1geo.minder "$pkgdir/usr/bin/$pkgname"
}
