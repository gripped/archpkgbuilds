# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=xfce4-generic-slider
pkgver=1.0.1
pkgrel=1
pkgdesc="Xfce panel plugin for adjusting the value passed to a command"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-generic-slider/start"
license=('GPL-3.0-only')
depends=('xfce4-panel')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-generic-slider.git#tag=$pkgname-$pkgver")
sha256sums=('022f386791ad71b416d20e10405675eca7cfb298fa5d1b17f7dd001625af5cfb')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
	make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
