# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pint
pkgver=0.74.6
pkgrel=1
pkgdesc='Rule linter/validator for Prometheus'
arch=(x86_64)
url='https://github.com/cloudflare/pint'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('3a5db62563d66fe11cec9a8eb30e2e0bd1b3dbea5b79808472160102352e7faf228a0e407d410b190746ab80ea14a326738c6c0be876e1d6e5998455df022591')
b2sums=('9665248a8f31faba469955cba86d172568943fca36a38253413ec1da41b1eeace36989c533a3854380873dbc374a8260fcd8c5f97c4cfb04b15dd0ca1911a9d0')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.version=${pkgver} \
    -X main.commit=$(git rev-parse HEAD)" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/pint
}
