# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pint
pkgver=0.77.1
pkgrel=1
pkgdesc='Rule linter/validator for Prometheus'
arch=(x86_64)
url='https://github.com/cloudflare/pint'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('775054735bd658dd55c02ce0b2073d3eafe5797abba896822e4f2d49dd018c01175dd5842bac56a213531f127014e83a98b655072bbd609c5c47ff16a11022c8')
b2sums=('e2564d237737ecb26fee58beafcee8125ad90a048ee2f5a7dfe9f8c6ffd4219bf3c9dc7030b0caa1f0ddbb0577e41d81740667f80990fa542bf80f00299861b8')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.version=${pkgver} \
    -X main.commit=$(git rev-parse HEAD)" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/pint
}
