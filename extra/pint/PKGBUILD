# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pint
pkgver=0.79.0
pkgrel=1
pkgdesc='Rule linter/validator for Prometheus'
arch=(x86_64)
url='https://github.com/cloudflare/pint'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('4d598f72b927ea984f84a13cb049eb5981d0c5881e0ad98bf65c20336b2d46ed3bdaa661cf974db5b3ec0d9a8221bf58c7b57ee9041e5058b56aaf2c398c9794')
b2sums=('31dd140b83fb3dc646da0febdcec9790ef0d56aaa4c29196b04ce0fcdf5259cd5d94b43f496a07de937b521737aae72977d35f28bc352c42f0c5f897c5981571')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.version=${pkgver} \
    -X main.commit=$(git rev-parse HEAD)" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/pint
}
