# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pint
pkgver=0.64.0
pkgrel=1
pkgdesc='Rule linter/validator for Prometheus'
arch=('x86_64')
url='https://github.com/cloudflare/pint'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('2463cd54a0683a2f5bab9eceb0d13d0ae0b456b11674309de01e95475cc0610e76f763c010cd11e5f6cddecf58b2f1d96af2e85598ecc2b38f9058428dfb06dc')
b2sums=('2469c54b3a0e68f5644d1dceb92f990f758655a77b1df1beda77e82bacf3a207afaf42f9ff7799bfebff8b827e6d57afc97da69e43b1178ecde9bef5fd39b90f')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.version=${pkgver} \
    -X main.commit=${_commit}" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/pint
}
