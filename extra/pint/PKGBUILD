# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pint
pkgver=0.74.7
pkgrel=1
pkgdesc='Rule linter/validator for Prometheus'
arch=(x86_64)
url='https://github.com/cloudflare/pint'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('c50549c2111a126c839dce02457a0acddea7b215b9051300af59d88284565cddbe6315e695f2049932c64543a7c0149e37a476e3e575a9a40069ff8fd9194ca8')
b2sums=('551180120a73e74dc459a2ea27af00823110650bfe29a6af8441a5fa40d7fc5d45da0024f1a8a9d1a3bce639a631de5ae8ab363b45fa4566498f8891b8d63809')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.version=${pkgver} \
    -X main.commit=$(git rev-parse HEAD)" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/pint
}
