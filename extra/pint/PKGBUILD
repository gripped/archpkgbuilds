# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pint
pkgver=0.74.8
pkgrel=1
pkgdesc='Rule linter/validator for Prometheus'
arch=(x86_64)
url='https://github.com/cloudflare/pint'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('509ef7c7fe9869f8d6331b84798efd6345bff02977541bb2b323818f738bfe822e64495f2723f1013e31b383f2915457dafc8e26c710b257b4cd153043515ce0')
b2sums=('24c7ce798bb89b8bc7e891ca77bf99c6af48aca567f65d6db777da7d7ea8332b5952d76f9bc0ee34e8e83c34a0417aa943ac1083a01e5fb03e71c624864dcd62')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.version=${pkgver} \
    -X main.commit=$(git rev-parse HEAD)" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/pint
}
