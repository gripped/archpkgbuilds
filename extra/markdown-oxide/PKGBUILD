# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=markdown-oxide
pkgver=0.25.8
pkgrel=1
pkgdesc='PKM oriented Markdown Language Server with Obsidian syntax support'
arch=(x86_64)
url="https://github.com/Feel-ix-343/$pkgname"
license=(Apache-2.0)
depends=(gcc-libs
         glibc)
makedepends=(cargo)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('37fab5cc1b6accecb394568f963abaac3041f847a994c73e43d53a2cdfbbd0ad')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	cargo build --release --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

