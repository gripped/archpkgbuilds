# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-tool-git
pkgver=0.1.5
pkgrel=1
pkgdesc="A simple tool for Git signing and verification with a focus on OpenPGP cards"
arch=(x86_64)
url="https://codeberg.org/openpgp-card/oct-git"
_name="${url##*/}"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
  openssl
  org.freedesktop.secrets
  sqlite
)
makedepends=(
  dbus
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
  $pkgname-0.1.5-rpgp-0.14.patch::https://codeberg.org/openpgp-card/oct-git/commit/4826cfa6a61f9624fcd7255e3d07489a1c32e207.patch
  $pkgname-0.1.5-expired-cert-test-case.patch::https://codeberg.org/openpgp-card/oct-git/commit/e89e28bd9aa01bfb5a9e86ad82cbf9a2f88cd461.patch
)
sha512sums=('bc9d2d44db44b8379121a73975800ca65bd42683370b466f1bac4e34c26c9fd4ab4bcb05cb3c8007703c1763bfa470ee2e57055a3ee5892d588252a20e37f140'
            '0e39e761522adc960cd5066a70b6c9d2b7da28ea5ede5c3a7bba07cfb5aca3624d14744cb3d58b615a6add7b9738e8617b7217a16bd2a7a1763033a6291e9b45'
            '6d898500f0ef5684a84da859426dc715f221be5fb242d9c419c39444ee814ead43af11cf4bb88f998f01714f79939974e1567d2c8d21660da4180df945a9aa3a')
b2sums=('cce08cdaf14f6208d8277d935503e951abe66da4e2f7cda8d66b8b917d740053a3cc24810a5c3a69cbd57bbdcefc2451d1166aa95ca047b690c8d4828526a010'
        'f2e8f728ec082a7fbed2798e4c86d894e339b606bd01682ede0fb8e308a803ec26e7cf1d51ee05f7ead4526732ef0e0f61fdfa882681506972ddefcff25d7c2f'
        'fe5efc6a303b84255d2e24805087eee77b4758aaf8ed0b9098a067ae136b2248ae8eebde268d36be23a221144aa2690d557cc1b1f775143fd2ffcae6a4309d7a')

prepare() {
  # fix known issues with test cases
  patch -Np1 -d $_name -i ../$pkgname-0.1.5-rpgp-0.14.patch
  patch -Np1 -d $_name -i ../$pkgname-0.1.5-expired-cert-test-case.patch

  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    dbus libdbus-1.so
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 755 -t "$pkgdir/usr/bin/" target/release/oct-git
  install -vDm 644 README.md -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
