# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-tool-git
pkgver=0.1.6
pkgrel=1
pkgdesc="A simple tool for Git signing and verification with a focus on OpenPGP cards"
arch=(x86_64)
url="https://codeberg.org/openpgp-card/oct-git"
_name="${url##*/}"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
  openssl
  org.freedesktop.secrets
  sqlite
)
makedepends=(
  dbus
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
)
sha512sums=('fa9fb3068fb057860891efb2d7ed47d52a6ab1e8220e9c3ceced5c65b51756a9422de8d5544ba1509b4d490417e54f48c8013351f8c36472409233f48788874a')
b2sums=('aa7cd6eb9eb8950383324bafa301c6adc9af5d7e542cd673414382946e2d3359df57f8778db7210e8b943131641cf5e3cfd95846bf4b091fdbf00be786168510')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    dbus libdbus-1.so
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 755 -t "$pkgdir/usr/bin/" target/release/oct-git
  install -vDm 644 README.md -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
