# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=3.6.7
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
groups=(x-apps)
depends=(gspell gtksourceview4 libpeas xapp)
makedepends=(meson samurai gobject-introspection intltool itstool glib2-devel)
optdepends=('python: required for some plugins')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        libxml-2.12.patch)
sha256sums=('dc9af1c9175e03cc9c3a65d4c06191ca1adac1365b0f93b5cf242dcc03ae837a'
            'ed19b6f419a4f703d290d8c97ed5d904c88a90fc475c738a0e65f7ab31697c40')
b2sums=('8cfc0791f97c4bcb1e4886ac0c3b5fe2ba15cba8c94954473295bbd050778c4a1e3bab4ac555ccbb1eba3503176e429fb25f410c339d57ea0b63d5d4980899f8'
        'bd3f1c73b5873b05db1a734d2b42025d460f6862dbb13813e3fc97602c43e6758e2857f2e2db14e739780a7d58fdb48e87186cfde4393b7799070def14750353')

prepare() {
  patch -d $pkgname-$pkgver -p1 < libxml-2.12.patch
}

build() {
  arch-meson ${pkgname}-${pkgver} build
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install
}
