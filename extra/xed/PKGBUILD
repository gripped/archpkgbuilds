# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=3.8.5
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
groups=(x-apps)
depends=(gspell gtksourceview4 libpeas xapp)
makedepends=(git meson gobject-introspection intltool itstool glib2-devel python-gobject)
optdepends=('python: required for some plugins')
source=(git+${url}#tag=$pkgver)
sha256sums=('d9b92857975f0e0b2ccad2cd27bf94839111655f4d12996d07e3e8519b39124b')
b2sums=('35e30c5b58273572f817853aaefb5721f64e811e034121199fcb41e9a8c2aa79356848e4ca60b7780ec5a66cfd23189e68052a8ac5a181bf094dc02b1961d486')

build() {
  arch-meson ${pkgname} build
  meson compile -C build
}

package(){
  meson install -C build --destdir="$pkgdir"
}

