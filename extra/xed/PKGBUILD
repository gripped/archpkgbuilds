# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=3.6.1
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
groups=(x-apps)
depends=(gspell gtksourceview4 libpeas xapp)
makedepends=(meson samurai gobject-introspection intltool itstool)
optdepends=('python: required for some plugins')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        libxml-2.12.patch)
sha256sums=('97ebcb5f05d6099d37fc081a02903ecbaa6e86021f291114fd9b487aac50696c'
            'ed19b6f419a4f703d290d8c97ed5d904c88a90fc475c738a0e65f7ab31697c40')
b2sums=('c51cf191370af3204f0651c647c2fd367405788a506f37b9b3b4b14eb4977b1e8baf262d994bdc9c653e204d0bd17a9e9c728217cd96baac800bdf026c10c3f1'
        'bd3f1c73b5873b05db1a734d2b42025d460f6862dbb13813e3fc97602c43e6758e2857f2e2db14e739780a7d58fdb48e87186cfde4393b7799070def14750353')

prepare() {
  patch -d $pkgname-$pkgver -p1 < libxml-2.12.patch
}

build() {
  arch-meson ${pkgname}-${pkgver} build
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install
}
