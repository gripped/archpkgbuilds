# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=3.8.7
pkgrel=1
pkgdesc='A small and lightweight text editor'
arch=(x86_64)
url='https://github.com/linuxmint/xed'
license=(GPL-2.0-or-later)
groups=(x-apps)
depends=(
  at-spi2-core
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gspell
  gtk3
  gtksourceview4
  libgirepository
  libpeas
  libx11
  libxml2
  pango
  python
  python-gobject
  xapp
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  intltool
  itstool
  meson
)
source=("git+https://github.com/linuxmint/xed.git#tag=$pkgver")
b2sums=(41c18cf0f7e9c3876e615454e0156eb9ed6e8e7b942078b982be56c42f08bac853e6c22f6cb3486fa0a4d3344481fcdee6ee482cfa59d61454dcf20dda74da82)

prepare() {
  cd $pkgname

  # Fix build
  git revert -n 13f55ff3f0db4e4f3b75883260bb56afbad08fd2
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package(){
  meson install -C build --destdir="$pkgdir"
}
