# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Robin Candau <antiz@archlinux.org>

pkgname=gnome-shell-extension-caffeine
_uuid=caffeine@patapon.info
pkgver=50
pkgrel=2
pkgdesc='Extension for GNOME Shell to disable screensaver and auto suspend'
arch=('any')
url='https://github.com/eonpatapon/gnome-shell-extension-caffeine'
license=('GPL-3.0-or-later')
depends=('gnome-shell')
source=("https://github.com/eonpatapon/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        'drop-deprecated.patch'
        'gnome46.patch')
sha256sums=('ff4a3e938165e032e04a28d4430a85eec3f5c4c9ae0000dc24a2cc4e16da980a'
            'f8b99f8d7dba06040d994effba59a215c394c564deabd09f3b0a803364bc8931'
            '050effbdaecf0c070f1a3843679dab2603a9fe8a690fad165dbc049901cc5d8a')

prepare() {
  cd $pkgname-$pkgver
  # GNOME 46 compatibility
  # https://github.com/eonpatapon/gnome-shell-extension-caffeine/pull/317
  patch -Np1 -i ../drop-deprecated.patch
  patch -Np1 -i ../gnome46.patch
}

build() {
  cd $pkgname-$pkgver
  make build
}

package() {
  cd $pkgname-$pkgver
  install -dm755 "$pkgdir/usr/share/gnome-shell/extensions/$_uuid"
  bsdtar -xf $_uuid.zip -C "$pkgdir/usr/share/gnome-shell/extensions/$_uuid" --no-same-owner
  mkdir -p "$pkgdir/usr/share/glib-2.0"
  mv "$pkgdir"/usr/share/gnome-shell/extensions/*/schemas "$pkgdir/usr/share/glib-2.0"
  rm "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"
  mv "$pkgdir"/usr/share/gnome-shell/extensions/*/locale "$pkgdir/usr/share"
}
