# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=calf
pkgver=0.90.5.2
pkgrel=1
pkgdesc='LV2 plug-in suite and standalone Jack host'
arch=(x86_64)
url='https://calf-studio-gear.org'
_url="https://github.com/calf-studio-gear/calf"
groups=(
  lv2-plugins
  pro-audio
)
license=(
  GPL-2.0-or-later
  LGPL-2.0-or-later
)
depends=(
  cairo
  expat
  fluidsynth
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk2
  hicolor-icon-theme
  jack)
makedepends=(
  cmake
  git
  lv2
)
source=("git+$_url#tag=$pkgver")
b2sums=('1a04be3a801697d12023c481fc799400de9708db68cf2db82102e3333de1c17143c5016725cdc610abc15141849f581b950c51a2fc84c48c6e0a97c2898e568c')

prepare() {
  sed "s/@VERSION@/$pkgver/" $pkgname/$pkgname.desktop.in > $pkgname.desktop
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D WANT_EXPERIMENTAL=ON
    -S $pkgname
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # NOTE: install some files manually because upstream dropped the ball
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgname/$pkgname*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 $pkgname/$pkgname.7 -t "$pkgdir/usr/share/man/man7/"
  install -vDm 644 $pkgname/$pkgname -t "$pkgdir/usr/share/bash-completion/completions/"

  install -Dm644 $pkgname/{AUTHORS,ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
