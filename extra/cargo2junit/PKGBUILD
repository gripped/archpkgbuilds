# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo2junit
pkgver=0.1.14
pkgrel=1
pkgdesc="Convert Cargo's JSON output to JUnit XML"
url='https://github.com/johnterickson/cargo2junit'
license=('MIT')
arch=('x86_64')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('769d6370fcbeb7f6dfc03c62fbbac8eb8533250a2e270e48f7f54fff6861b415')

prepare() {
	cd "$pkgname-$pkgver"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$pkgname-$pkgver"
	cargo build --release --frozen
}

check() {
	cd "$pkgname-$pkgver"
	cargo test --frozen
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
	install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
	install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
