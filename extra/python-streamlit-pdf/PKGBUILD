# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=streamlit-pdf
pkgname=python-$_name
pkgver=2.0.0
pkgrel=1
pkgdesc="A lightweight Streamlit component for rendering PDF files"
arch=(any)
url="https://github.com/streamlit/streamlit-pdf"
license=(Apache-2.0)
depends=(
  python
)
makedepends=(
  nodejs
  npm
  python-build
  python-installer
  python-setuptools
  python-wheel
)
#checkdepends=(
#  python-playwright
#  python-pytest
#  python-pytest-playwright
#  python-requests
#  python-streamlit
#)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
b2sums=('ba5e0c3db861333cf3fe8984e1d8b6663d323df26e1c042c3e50a6d309e2cceb804bcaa0173a3f3830d64531a8d46aa6a2994a661364e45006e6123766632396')

# based on https://github.com/streamlit/streamlit-pdf/blob/main/.github/workflows/release.yml
build() {
  # install frontend dependencies
  cd "$srcdir"/$_name-$pkgver/${_name/-/_}/frontend
  npm ci

  # build frontend into static files
  npm run build

  # build the Python module
  cd "$srcdir"/$_name-$pkgver
  python -m build --wheel --no-isolation
}

# NOTE: tests have cyclic dependency on streamlit
#check() {
#  local pytest_options=(
#    -vv
#    -W ignore::DeprecationWarning
#    -m "not slow"
#    --override-ini="addopts="
#  )
#
#  cd $_name-$pkgver
#  python -m venv --system-site-packages test-env
#  test-env/bin/python -m installer dist/*.whl
#  test-env/bin/python -m pytest "${pytest_options[@]}"
#}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
