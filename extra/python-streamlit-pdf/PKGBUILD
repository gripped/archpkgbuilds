# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=streamlit-pdf
pkgname=python-$_name
pkgver=2.0.1
pkgrel=2
pkgdesc="A lightweight Streamlit component for rendering PDF files"
arch=(any)
url="https://github.com/streamlit/streamlit-pdf"
license=(Apache-2.0)
depends=(
  python
)
makedepends=(
  nodejs
  npm
  python-build
  python-installer
  python-setuptools
  python-wheel
)
#checkdepends=(
#  python-playwright
#  python-pytest
#  python-pytest-playwright
#  python-requests
#  python-streamlit
#)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
b2sums=('8dfc45bf75d847577e756d5917535b4ca8f64db60f4de15760113dfdeb8bbd59b91054205611dd9cf1f5273d3c1ed6913dc45d3021a68fb610914b14a0fef061')

# based on https://github.com/streamlit/streamlit-pdf/blob/main/.github/workflows/release.yml
build() {
  # install frontend dependencies
  cd "$srcdir"/$_name-$pkgver/${_name/-/_}/frontend
  npm ci

  # build frontend into static files
  npm run build

  # build the Python module
  cd "$srcdir"/$_name-$pkgver
  python -m build --wheel --no-isolation
}

# NOTE: tests have cyclic dependency on streamlit
#check() {
#  local pytest_options=(
#    -vv
#    -W ignore::DeprecationWarning
#    -m "not slow"
#    --override-ini="addopts="
#  )
#
#  cd $_name-$pkgver
#  python -m venv --system-site-packages test-env
#  test-env/bin/python -m installer dist/*.whl
#  test-env/bin/python -m pytest "${pytest_options[@]}"
#}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
