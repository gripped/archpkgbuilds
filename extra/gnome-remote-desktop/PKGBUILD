# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-remote-desktop
pkgver=49.3
pkgrel=1
pkgdesc="GNOME Remote Desktop server"
url="https://gitlab.gnome.org/GNOME/gnome-remote-desktop"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  freerdp
  fuse3
  glib2
  glibc
  libdrm
  libei
  libepoxy
  libfdk-aac
  libgcc
  libnotify
  libpipewire
  libsecret
  libva
  libvncserver
  libxkbcommon
  opus
  pipewire
  polkit
  systemd
  systemd-libs
  tpm2-tss
  vulkan-icd-loader
)
makedepends=(
  asciidoc
  ffnvcodec-headers
  git
  glib2-devel
  meson
  vulkan-headers
)
groups=(gnome)
source=("git+$url.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('941fdc907a2bc0b11de8fa37a502be173e14af6aed510f53bbbff287fb875b72c0eb502a1dacbb3e68d560f65b3cd0cee7b741852be7a03a4818562db12f32f6')
validpgpkeys=(
  8307C0A224BABDA1BABD0EB9A6EEEC9E0136164A # Jonas Ã…dahl <jadahl@gmail.com>
  E60DADB5546D3F01AF35B87289F540EAB24513E2 # Ray Strode <rstrode@redhat.com>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D tests=false
    -D vnc=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
