# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-cabal-plugin
pkgname=haskell-hls-cabal-plugin
pkgver=2.1.0.0
pkgrel=41
pkgdesc="Cabal integration plugin with Haskell Language Server"
url="https://hackage.haskell.org/package/hls-cabal-plugin"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-ghcide' 'haskell-hashable' 'haskell-hls-graph'
         'haskell-hls-plugin-api' 'haskell-lens' 'haskell-lsp' 'haskell-lsp-types' 'haskell-regex-tdfa'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils' 'haskell-tasty-hunit' 'haskell-row-types')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('37a7033e9cc28df9dd61f8430340ffe6223fc28081646db9f6c5da4fe40667e3')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
