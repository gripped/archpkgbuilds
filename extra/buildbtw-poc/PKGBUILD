pkgname=buildbtw-poc
pkgver=0.0.0.r380.c17a0c4
_commit=c17a0c436162818ebf23e256fd68c37aaf64f44d
pkgrel=1
pkgdesc='Service for building new versions of packages (proof-of-concept)'
url='https://gitlab.archlinux.org/archlinux/buildbtw/'
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libgit2
  openssh
  openssl
  sqlite
  sqlx-cli
)
makedepends=(
  cargo
  git
  just
)
options=(!lto)
source=(buildbtw::"git+https://gitlab.archlinux.org/archlinux/buildbtw.git#commit=${_commit}")
sha256sums=('881c12e6e18df17c4dbd161239d9f0c7da3e0961c609e25d55aee59bb701f382')
b2sums=('0ee7cec0ea53c216ddc489177ed2df493c5338015985ae72ff7c548c607c5ee303748b634fe00a1357a5a91a0a067195bec922c362eb5b2b330fc1779233a3fe')
validpgpkeys=(
  E240B57E2C4630BA768E2F26FC1B547C8D8172C8 # Levente Polyak <anthraxx@archlinux.org>
  AAE2599CE2DFD58B8E884726B4EFE6DC59FAE118 # Rafael Eppl√©e <raffomania@archlinux.org>
  8FC15A064950A99DD1BD14DD39E4B877E62EB915 # Sven-Hendrik Haase <svenstaro@archlinux.org>
)

pkgver() {
  cd buildbtw/${pkgname}
  printf "%s.r%s.%s" "0.0.0" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd buildbtw/${pkgname}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd buildbtw/${pkgname}
  export DATABASE_URL=sqlite:buildbtw_server.sqlite
  just create-db
  cargo build --frozen --release
}

check() {
  cd buildbtw/${pkgname}
  cargo test --frozen
}

package() {
  cd buildbtw
  install -Dm 755 target/release/buildbtw-{client,server,worker} -t "${pkgdir}/usr/bin"
  ln -sf /usr/bin/buildbtw-client "${pkgdir}/usr/bin/bbtw"
  install -Dm 644 "notes/PoC_User_Guide.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
