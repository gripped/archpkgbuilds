pkgname=buildbtw-poc
pkgver=0.0.0.r342.217f6ca
_commit=217f6ca1a5d01e44e456b54a7a026302558132cc
pkgrel=1
pkgdesc='Service for building new versions of packages (proof-of-concept)'
url='https://gitlab.archlinux.org/archlinux/buildbtw/'
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libgit2
  openssh
  openssl
  sqlite
  sqlx-cli
)
makedepends=(
  cargo
  git
  just
)
options=(!lto)
source=(buildbtw::"git+https://gitlab.archlinux.org/archlinux/buildbtw.git#commit=${_commit}")
sha256sums=('1a65e7fec35b2e7db8646717c23ef72a358ac8c4ba3adb6781cd1efedfdeffb3')
b2sums=('048194d7bfae1cc186f16f0eeb5bd7bfc5f004330f498e2880f07f88917d186086baaeb8ea0008d043a190f2f4f64d3a276f7e80d92bea2a17e7ed338cabfd4c')
validpgpkeys=(
  E240B57E2C4630BA768E2F26FC1B547C8D8172C8 # Levente Polyak <anthraxx@archlinux.org>
  AAE2599CE2DFD58B8E884726B4EFE6DC59FAE118 # Rafael Eppl√©e <raffomania@archlinux.org>
  8FC15A064950A99DD1BD14DD39E4B877E62EB915 # Sven-Hendrik Haase <svenstaro@archlinux.org>
)

pkgver() {
  cd buildbtw/${pkgname}
  printf "%s.r%s.%s" "0.0.0" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd buildbtw/${pkgname}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd buildbtw/${pkgname}
  export DATABASE_URL=sqlite:buildbtw_server.sqlite
  just create-db
  cargo build --frozen --release
}

check() {
  cd buildbtw/${pkgname}
  cargo test --frozen
}

package() {
  cd buildbtw
  install -Dm 755 target/release/buildbtw-{client,server,worker} -t "${pkgdir}/usr/bin"
  ln -sf /usr/bin/buildbtw-client "${pkgdir}/usr/bin/bbtw"
  install -Dm 644 "notes/PoC_User_Guide.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
