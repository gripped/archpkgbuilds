# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Echizen Ryoma <echizenryoma.zhang@gmail.com>
# Contributor: Martchus <martchus@gmx.net>

pkgbase=network-manager-sstp
pkgname=(
  network-manager-sstp
  networkmanager-vpn-plugin-sstp
)
pkgver=1.3.2
pkgrel=4
pkgdesc="NetworkManager VPN plugin for SSTP"
url="https://networkmanager.dev/docs/vpn/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gnutls
  gtk3
  libnm
  libnma
  libsecret
)
makedepends=(
  git
  glib2-devel
  libnma-gtk4
  ppp
  python
  sstp-client
)
source=("git+https://gitlab.gnome.org/GNOME/network-manager-sstp.git?signed#tag=release-$pkgver-1")
b2sums=('ac702cd905c79d652374ce71bf2afe51e7c4238f7738b982ec0d452022c533623ecb0b954ec534d8dfc357a2e682d2cd2aae02807e22ca0ba3d591c2e8dadc98')
validpgpkeys=(
  3F3D68F38ECEA1682671D2B0E102CBD4F22E37E8 # Eivind NÃ¦ss <eivnaes@yahoo.com>
)

prepare() {
  cd network-manager-sstp
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --disable-static
    --with-gtk4
  )

  cd network-manager-sstp
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_network-manager-sstp() {
  pkgdesc+=" (with GUI)"
  depends+=(
    "networkmanager-vpn-plugin-sstp=$pkgver-$pkgrel"
  )
  optdepends=(
    'libnma-gtk4: GUI support (GTK 4)'
  )

  cd network-manager-sstp
  make DESTDIR="$pkgdir" install dbusservicedir=/usr/share/dbus-1/system.d

  cd "$pkgdir"
  _pick plugin usr/lib/NetworkManager/VPN
  _pick plugin usr/lib/NetworkManager/libnm-vpn-plugin-sstp.so
  _pick plugin usr/lib/nm-sstp-service
  _pick plugin usr/lib/pppd
  _pick plugin usr/share/dbus-1
}

package_networkmanager-vpn-plugin-sstp() {
  pkgdesc+=" (VPN plugin only)"
  depends=(
    gcc-libs
    glib2
    glibc
    libnm
    ppp
    sstp-client
  )

  mv plugin/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
