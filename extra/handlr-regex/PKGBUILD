# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: ftilde <ftilde at tamepointer dot tld for germany>

pkgname=handlr-regex
_pkgname=handlr
pkgver=0.12.1
pkgrel=1
pkgdesc="Powerful alternative to xdg-utils written in Rust"
arch=('x86_64')
url="https://github.com/Anomalocaridid/handlr-regex"
license=('MIT')
provides=('handlr')
replaces=('handlr')
conflicts=('handlr')
depends=('shared-mime-info')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('14131059a0ae32a0907a626dc1ba5c36c44ccbc66dec3c116c490308468baf9535857032ef240afaf73e15ff4484fe615e11747fdb75d62660958deeb3df185e')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  mkdir -p completions
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
  COMPLETE=bash target/release/$_pkgname >"completions/$_pkgname"
  COMPLETE=fish target/release/$_pkgname >"completions/$_pkgname.fish"
  COMPLETE=zsh target/release/$_pkgname >"completions/_$_pkgname"
  echo "$PWD/target/release/$_pkgname|/usr/bin/$_pkgname"
  for f in completions/*; do
    sed -i "s|$PWD/target/release/$_pkgname|/usr/bin/$_pkgname|g" "$f"
  done
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "completions/$_pkgname" -t "$pkgdir/usr/share/bash-completion/completions"
  install -Dm 644 "completions/$_pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$_pkgname" -t "$pkgdir/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:
