# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=webfont-kit-generator
pkgver=1.3.0
pkgrel=2
pkgdesc='Utility to generate WOFF, WOFF2 and the necessary CSS boilerplate from non-web font formats (OTF & TTF)'
arch=(any)
url='https://apps.gnome.org/WebfontKitGenerator/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libsoup3
  python
  python-brotli
  python-fonttools
  python-gobject
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://github.com/rafaelmardojai/webfont-kit-generator.git#tag=$pkgver")
b2sums=(7e01fbfbe62e6424324d87d4f945c6e1239a8bfe2af4b54c0fc2a3c1f7f576da7e57c822b7f0b4500c0bf5e32f5bcbb522990f3c76b317a5b4316de65bd7a65d)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
