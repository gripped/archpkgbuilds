# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-pw-check
pkgver=6.0.6
pkgrel=1
pkgdesc='Tool to verify the validity of the password'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-pw-check"
license=('GPL-3.0-or-later')
depends=('cracklib' 'gtk3' 'iniparser')
makedepends=('deepin-gettext-tools' 'git' 'go')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-pw-check.git#tag=$pkgver")
sha512sums=('7a54e44b6f3d9dde7d7b1d56bcde502b0158a037c2d7e343ea0657a618274257d8ca7a0bc183f48ce5e57b0c989e30ad89cc3c2d5076266d993a5e190a3b460e')

prepare() {
  cd deepin-pw-check
  patch -p1 -i rpm/0001-Mangle-Suit-Cracklib2.9.6.patch
  sed -i 's|gcc |gcc ${CFLAGS} ${LDFLAGS} |;/export GOPATH/d' Makefile
  sed -i 's|\${DESTDIR}/lib/|${DESTDIR}/usr/lib/|' Makefile

  go get github.com/linuxdeepin/go-gir
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export GOPATH="${srcdir}/gopath"

  cd deepin-pw-check
  make
}

package() {
  cd deepin-pw-check
  make DESTDIR="$pkgdir" PKG_FILE_DIR=/usr/lib/pkgconfig PAM_MODULE_DIR=/usr/lib/security install
}
