# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-pw-check
pkgver=6.0.5
pkgrel=1
pkgdesc='Tool to verify the validity of the password'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-pw-check"
license=('GPL-3.0-or-later')
depends=('cracklib' 'gtk3' 'iniparser')
makedepends=('deepin-gettext-tools' 'git' 'go')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-pw-check.git#tag=$pkgver")
sha512sums=('88653a0dd8a7cdb8a1df4f46ddc7ea65e304af7c0866044012e4c9dfeb2251806188a04c092c96e2ccf4490cd1fd280e8b96eb6a462ce1f2ce624e53af0885ad')

prepare() {
  cd deepin-pw-check
  patch -p1 -i rpm/0001-Mangle-Suit-Cracklib2.9.6.patch
  sed -i 's|gcc |gcc ${CFLAGS} ${LDFLAGS} |;/export GOPATH/d' Makefile
  sed -i 's|\${DESTDIR}/lib/|${DESTDIR}/usr/lib/|' Makefile

  go get github.com/linuxdeepin/go-gir
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export GOPATH="${srcdir}/gopath"

  cd deepin-pw-check
  make
}

package() {
  cd deepin-pw-check
  make DESTDIR="$pkgdir" PKG_FILE_DIR=/usr/lib/pkgconfig PAM_MODULE_DIR=/usr/lib/security install
}
