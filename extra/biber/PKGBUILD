# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=biber
epoch=1
pkgver=2.21
pkgrel=2
pkgdesc='A Unicode-capable BibTeX replacement for biblatex users'
arch=(any)
license=(Artistic-2.0)
url='https://github.com/plk/biber'
depends=(perl-autovivification
         perl-business-isbn
         perl-business-ismn
         perl-business-issn
         perl-class-accessor
         perl-data-compare
         perl-data-dump
         perl-data-uniqid
         perl-datetime-calendar-julian
         perl-datetime-format-builder
         perl-file-slurper
         perl-io-string # required by Biber::Output::bblxml
         perl-ipc-run3
         perl-lingua-translit
         perl-list-allutils
         perl-log-log4perl
         perl-lwp-protocol-https
         perl-mozilla-ca
         perl-parse-recdescent
         perl-perlio-utf8-strict
         perl-regexp-common
         perl-sort-key
         perl-text-balanced
         perl-text-bibtex
         perl-text-csv
         perl-text-roman
         perl-unicode-linebreak
         perl-xml-libxml-simple
         perl-xml-libxslt
         perl-xml-writer)
# ExtUtils::LibBuilder and Config::AutoConf are not actual makedepends
makedepends=(git
             perl-module-build
             perl-test-differences)
checkdepends=(perl-capture-tiny
              perl-file-which
              texlive-bibtexextra
              texlive-latexextra)
options=(!emptydirs)
# The sourceforge tarball is missing bblxml.pm
source=(git+https://github.com/plk/biber#tag=v$pkgver
        test.tex
        test.bib)
sha256sums=('1b7bd8121887b0a1c9200f92a699893e6419708762a2d357d0b490a7c4747d1f'
            '60fa6badf0f9bef0b3cb069dc272979f583ffe354629b6eeab28550a873fe050'
            '2ca2cb9ff15ba33c8f74cb43824d82597872a9d960608e36307f3309ffd65e78')

prepare() {
  cd $pkgname
  mkdir -p test
  cp ../test.{tex,bib} test/
}

build() {
  cd $pkgname
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  perl Build.PL
  ./Build
}

check() {
  cd $pkgname
  # Test a sample file
  pushd test
  latex test
  PERL5LIB=../blib/lib ../bin/biber test
  latex test
  popd
  # Run test suite
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  LC_CTYPE=en_US.UTF-8 BIBER_DEV_TESTS=1 ./Build test
}

package() {
  cd $pkgname
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  ./Build install installdirs=vendor destdir="$pkgdir"
}
