# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=wallutils
pkgver=5.14.2
pkgrel=1
pkgdesc='Utilities for handling resolutions, wallpapers and timed wallpapers'
arch=(x86_64)
url='https://github.com/xyproto/wallutils'
license=(BSD-3-Clause)
options=(!strip !lto)
depends=(bash glibc libheif libx11 wayland)
makedepends=(git go libxcursor libxmu libxpm upx xbitmaps xorgproto)
optdepends=('feh: for setting the wallpaper for some window managers that runs under X'
            'imagemagick: for supporting HEIF/HEIC (macOS wallpaper) files'
            'nvidia-utils: for the vram utility'
            'procps-ng: for the Labwc window manager'
            'swaybg: for the Sway and Labwc window managers')
source=("git+$url#tag=v$pkgver")
b2sums=('09eaaa510134090c2a6b9b8ec06910cb4abe442007162de573941249fd58c55bce0d46552fd5b37319ccf081b747d5fc75a435fee0cf412a54b5c3d84055b7aa')

build() {
  export CGO_CFLAGS="$CFLAGS -fcf-protection=full"
  export CGO_LDFLAGS="$LDFLAGS -fcf-protection=full -Wl,-z,shstk"
  export LDFLAGS="$(echo $LDFLAGS | sed 's/ -Wl//g;s/,--sort-common//')"
  make -C $pkgname
}

package() {
  DESTDIR="$pkgdir" LDFLAGS="$LDFLAGS" make -C $pkgname install install-man
  find "$pkgdir/usr/bin/" -executable -type f -not -name heic-install -exec \
    upx --best --lzma --no-progress --no-time -q {} >/dev/null \;
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
