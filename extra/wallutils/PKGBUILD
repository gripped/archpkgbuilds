# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=wallutils
pkgver=5.14.3
pkgrel=1
pkgdesc='Utilities for handling resolutions, wallpapers and timed wallpapers'
arch=(x86_64)
url='https://github.com/xyproto/wallutils'
license=(BSD-3-Clause)
options=(!strip !lto)
depends=(bash glibc libheif libx11 wayland)
makedepends=(git go libxcursor libxmu libxpm upx xbitmaps xorgproto)
optdepends=('feh: for setting the wallpaper for some window managers that runs under X'
            'imagemagick: for supporting HEIF/HEIC (macOS wallpaper) files'
            'nvidia-utils: for the vram utility'
            'procps-ng: for the Labwc window manager'
            'swaybg: for the Sway and Labwc window managers')
source=("git+$url#tag=v$pkgver")
b2sums=('bcb75831d3f2b9caa75fb916d5eb88df61c4b7e38efc984dc4bd26247b50e2e157f0359f05e6d02de7c5d035ec9ecf41fd6db9a399b08cb154c217636e8e6668')

build() {
  export CGO_CFLAGS="$CFLAGS"
  make -C $pkgname
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname install install-man
  find "$pkgdir/usr/bin/" -executable -type f -not -name heic-install -exec \
    upx --best --lzma --no-progress --no-time -q {} >/dev/null \;
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
