# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hopenpgp-tools
pkgver=0.23.8
pkgrel=26
pkgdesc="hOpenPGP-based command-line tools"
url="https://salsa.debian.org/clint/hopenpgp-tools"
license=("AGPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base16-bytestring' 'haskell-binary-conduit'
         'haskell-conduit' 'haskell-conduit-extra' 'haskell-crypton' 'haskell-errors'
         'haskell-fgl' 'haskell-graphviz' 'haskell-hopenpgp' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-http-types' 'haskell-ixset-typed' 'haskell-lens'
         'haskell-memory' 'haskell-monad-loops' 'haskell-openpgp-asciiarmor'
         'haskell-optparse-applicative' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal' 'haskell-resourcet' 'haskell-time-locale-compat'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-yaml')
makedepends=('uusi' 'alex' 'happy' 'ghc')
source=(https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('aa83cb30cc78dc5ffab4d039095940824337de8e478e3cfe5784b7b9d0ab6b65b0759e2c90b30afd58bfcecf4ec301be720647007bdc1a9f537f72cc62ab0b85')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    uusi -r cryptonite:crypton
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-executable-dynamic --prefix=/usr \
        --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    rm "$pkgdir/usr/share/doc/hopenpgp-tools/LICENSE"
    rmdir "$pkgdir/usr/share/doc/hopenpgp-tools" "$pkgdir/usr/share/doc" "$pkgdir/usr/share"
}
