# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SÃ½lvan Heuser <sylvan.heuser@gmx.net>
pkgname=upterm
pkgver=0.22.0
pkgrel=1
pkgdesc='Secure Terminal Sharing, an alternative to tmate'
arch=('x86_64')
url="https://github.com/owenthereal/upterm"
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/owenthereal/upterm/archive/v${pkgver}.tar.gz")
sha256sums=('047bcb184cc5189ea5f155d6a62780206fb01f093672e7b4c887cb7aa50bcc7f')

prepare() {
  cd "${pkgname}-${pkgver}"

  mkdir -p build/
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build ./cmd/...
  build/gendoc
}

# check() {
#   cd "${pkgname}-${pkgver}"
#   go test ./...
# }

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "build/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 "build/${pkgname}d" "${pkgdir}/usr/bin/${pkgname}d"
  install -dm755 "${pkgdir}/usr/share/man"
  cp -av etc/man/* "${pkgdir}/usr/share/man/"
  install -Dm644 "etc/completion/upterm.bash_completion.sh" "${pkgdir}/usr/share/bash-completion/completions/upterm"
  install -Dm644 "etc/completion/upterm.zsh_completion" "${pkgdir}/usr/share/zsh/site-functions/_upterm"
  install -Dm644 systemd/uptermd.service "${pkgdir}/usr/lib/systemd/system/uptermd.service"
}

# vim:set ts=2 sw=2 et:
