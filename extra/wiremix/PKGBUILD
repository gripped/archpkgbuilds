# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=wiremix
pkgver=0.8.0
pkgrel=1
pkgdesc="A simple TUI audio mixer for PipeWire"
arch=('x86_64')
url="https://github.com/tsowell/wiremix"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'libpipewire')
makedepends=('cargo' 'clang')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('db357d21c76809d674024f1094c2ac6ddd2d6866d4b8ae53cbb0620599006e31')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  VERGEN_GIT_DESCRIBE="$pkgver-$pkgrel (Arch Linux)" cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
