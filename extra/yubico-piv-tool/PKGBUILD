# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: travisghansen <travisghansen@yahoo.com>

pkgname=yubico-piv-tool
pkgver=2.5.0
pkgrel=1
pkgdesc="Command line tool for the YubiKey PIV application"
arch=('x86_64')
url="https://developers.yubico.com/yubico-piv-tool"
license=('BSD-2-Clause')
depends=('zlib' 'glibc' 'pcsclite' 'openssl')
makedepends=('check' 'cmake' 'gengetopt' 'help2man')
source=(https://developers.yubico.com/yubico-piv-tool/Releases/"${pkgname}-${pkgver}".tar.gz{,.sig})
sha256sums=('76a1b63bed9ff66fef2efcfed89117ee914fda0f2dde2574e084d6c9a1581f4a'
            'SKIP')
validpgpkeys=('1D7308B0055F5AEF36944A8F27A9C24D9588EA0F') # Aveen Ismail <aveen.ismail@yubico.com>

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-Wno-dev
	cmake --build build
}

check() {
	ctest --test-dir build --output-on-failure
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	cd "${pkgname}-${pkgver}"
	install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
