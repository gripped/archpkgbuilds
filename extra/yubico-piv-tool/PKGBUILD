# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: travisghansen <travisghansen@yahoo.com>

pkgname=yubico-piv-tool
pkgver=2.7.3
pkgrel=1
pkgdesc="Command line tool for the YubiKey PIV application"
url="https://developers.yubico.com/yubico-piv-tool"
arch=('x86_64')
license=('BSD-2-Clause')
depends=('zlib' 'glibc' 'pcsclite' 'openssl')
makedepends=('check' 'cmake' 'gengetopt' 'git' 'help2man')
source=("git+https://github.com/Yubico/yubico-piv-tool.git#tag=${pkgname}-${pkgver}?signed")
sha256sums=('cb2407cab31266c240f6aecdebf1ea34303bb099c0cf46559f4c4cb72fddc684')
validpgpkeys=('1D7308B0055F5AEF36944A8F27A9C24D9588EA0F') # Aveen Ismail <aveen.ismail@yubico.com>

build() {
	cmake -B build -S "${pkgname}" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-Wno-dev
	cmake --build build
}

check() {
	ctest --test-dir build --output-on-failure
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	cd "${pkgname}"
	install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
