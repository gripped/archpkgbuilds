# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-qqmusic
pkgver=1.0
pkgrel=1
pkgdesc="feeluown qqmusic plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-qqmusic"
license=('GPL3')
depends=('feeluown' 'python-marshmallow' 'python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("https://github.com/feeluown/feeluown-qqmusic/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a1d21aae9b3676fd473b606df2dc374812822506db823855ded21d3c7aa9b18b3884670d91bf96000ef6cc5b7db2d1aeae31d700eac106b36f36defbae9e8553')

build() {
  cd feeluown-qqmusic-$pkgver
  python setup.py build
}

check() {
  cd feeluown-qqmusic-$pkgver
  pytest
}

package() {
  cd feeluown-qqmusic-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
