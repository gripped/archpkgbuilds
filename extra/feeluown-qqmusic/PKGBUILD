# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-qqmusic
pkgver=1.0.4
pkgrel=1
pkgdesc="feeluown qqmusic plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-qqmusic"
license=('GPL3')
depends=('feeluown' 'python-marshmallow' 'python-requests')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-qqmusic.git#tag=v$pkgver")
sha512sums=('d013be93e33162578c8430d47310827aed5a408e549247236587e821353cdf4dedf5f50b06c0829f146fd8c00da176a84d1a11097797c8a6fb1fd7086904de30')

build() {
  cd feeluown-qqmusic
  python setup.py build
}

check() {
  cd feeluown-qqmusic
  pytest
}

package() {
  cd feeluown-qqmusic
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
