# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-qqmusic
pkgver=1.0.7
pkgrel=1
pkgdesc="feeluown qqmusic plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-qqmusic"
license=('GPL3')
depends=('feeluown' 'python-marshmallow' 'python-requests')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-qqmusic.git#tag=v$pkgver")
sha512sums=('857ab57d74ab6d1176476b11706933e3f3edf0d35141b5f84a07e1fe9953b1f4ec362f26526eb56e36871bc51aa779e4861e1cfc41e9cc4d53448ec5bb481dec')

build() {
  cd feeluown-qqmusic
  python setup.py build
}

check() {
  cd feeluown-qqmusic
  pytest
}

package() {
  cd feeluown-qqmusic
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
