# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-qqmusic
pkgver=1.0.8
pkgrel=1
pkgdesc="feeluown qqmusic plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-qqmusic"
license=('GPL3')
depends=('feeluown' 'python-marshmallow' 'python-requests')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-qqmusic.git#tag=v$pkgver")
sha512sums=('66e0173821c8f04c1080ea0ad17ac4df1de1f1e4d6ea20a5f002f543eb81e3e1a5522c663d0f6aa76b08eb590c9f9e5bf8066bc9fbe30075d723412090dcca3b')

build() {
  cd feeluown-qqmusic
  python setup.py build
}

check() {
  cd feeluown-qqmusic
  pytest
}

package() {
  cd feeluown-qqmusic
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
