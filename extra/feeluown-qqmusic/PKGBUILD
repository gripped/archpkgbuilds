# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-qqmusic
pkgver=1.0.3
pkgrel=1
pkgdesc="feeluown qqmusic plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-qqmusic"
license=('GPL3')
depends=('feeluown' 'python-marshmallow' 'python-requests')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-qqmusic.git#tag=v$pkgver")
sha512sums=('215ba8ce9516835d14526d7a438ca4049b621eb19aa0b01b2320232d5951f02d8b0e4a2e67cbbe58c403c4490a0163a70e2a5c4f9c21f088b09b10e6c6e4e85e')

build() {
  cd feeluown-qqmusic
  python setup.py build
}

check() {
  cd feeluown-qqmusic
  pytest
}

package() {
  cd feeluown-qqmusic
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
