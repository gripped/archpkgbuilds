# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt-advanced-docking-system
pkgver=4.5.0
pkgrel=1
pkgdesc='Advanced Docking System for Qt'
arch=(x86_64)
url='https://github.com/githubuser0xFFFF/Qt-Advanced-Docking-System/'
license=(LGPL-2.1-or-later)
depends=(gcc-libs
         glibc
         libxcb
         qt6-base)
makedepends=(cmake
             git
             qt6-declarative)
source=(git+https://github.com/githubuser0xFFFF/Qt-Advanced-Docking-System#tag=$pkgver)
sha256sums=('2094008ecc5ef18b535ab981c40513f9dcdca3ae140e3bafc73ab8741536fc14')

build() {
  cmake -B build -S Qt-Advanced-Docking-System \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
