# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-swiftclient
pkgver=4.7.0
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://docs.openstack.org/python-swiftclient"
license=('Apache')
depends=('python-requests')
optdepends=('python-keystoneclient: for Auth versions 2.0 and 3 support')
makedepends=('git' 'python-setuptools' 'python-pbr')
checkdepends=('python-keystoneauth1' 'python-stestr' 'python-openstacksdk' 'python-keystoneclient')
source=("git+https://github.com/openstack/python-swiftclient.git#tag=$pkgver")
sha512sums=('8828eadd834cab6dce82429d3ed8cfafe05f51fc677967ffea68f2d78b19464a63ac019fc61d084eddb5961e302886e0bc28ec2d9d55a7bc419b78637fd0d973')

build() {
  cd python-swiftclient
  python setup.py build
}

check() {
  cd python-swiftclient
  stestr run
}

package() {
  cd python-swiftclient
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
