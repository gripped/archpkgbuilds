# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-qt-material
pkgver=2.16
pkgrel=1
pkgdesc='Material inspired stylesheet for PySide & PyQt'
arch=('any')
url='https://qt-material.readthedocs.io/'
license=('BSD-2-Clause')
depends=(
  'python'
  'python-jinja'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
optdepends=(
  'pyside6: Use PySide for Qt bindings'
  'python-pyqt6: Use PyQt for Qt bindings'
)
source=("$pkgname::git+https://github.com/dunderlab/qt-material#tag=v$pkgver")
sha512sums=('c68a36ed29b20ce782f7c605d4d476d0ed016beffceb8d8d7e6016fabeec40e9fd55896abd2afeb603af47ba55b5dcb8873028941b3432f8fbc17c86d18d9aed')
b2sums=('3664ae5507ed3d72a9cc3302d98acb13a5667bf1212bd6565091e0812100b442d9b7d2194e60f3e019a70298921680bf4b83221862a9fa96b523a1ece76b80f7')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
