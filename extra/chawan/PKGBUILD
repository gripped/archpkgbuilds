# Maintainer: Nebulosa <nebulosa2007-at-yandex-dot-ru>

pkgname=chawan
pkgver=0.3.0
pkgrel=1
pkgdesc='A text-mode web browser and pager for Unix-like systems'
arch=(x86_64)
url='https://chawan.net/'
license=(Unlicense)
depends=(
  glibc
  gcc-libs
  brotli
  libssh2
  openssl
  sh
)
makedepends=(git 'nim>=2.0.0')
source=("$pkgname::git+https://git.sr.ht/~bptato/chawan#tag=v$pkgver")
sha512sums=('2be1f5626ee4d44913906fdfac76eadd7e8cfdcc2dfb0de8ebc0edf637427367761800b9d9ec2b2ae297031aae02076a29482de41af13cd58d216c78db488e24')
b2sums=('788ef4c9e7d035428c2e9b700ff829060bda0c2d5c66322808b7dfa6ff9dbac6b4fb0b8fecd025f11430a0496f730c21b2569644310d7449ffa50f40d93a146b')

build() {
  cd "$pkgname"

  export CFLAGS+=" -ffile-prefix-map=$srcdir/="

  make LIBEXECDIR=/usr/lib/chawan
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" PREFIX='/usr' LIBEXECDIR="$pkgdir/usr/lib/chawan" install
}
