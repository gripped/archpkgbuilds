# Maintainer: Nebulosa <nebulosa2007-at-yandex-dot-ru>

pkgname=chawan
pkgver=0.3.2
pkgrel=1
pkgdesc='A text-mode web browser and pager for Unix-like systems'
arch=(x86_64)
url='https://chawan.net/'
license=(Unlicense)
depends=(
  glibc
  gcc-libs
  brotli
  libssh2
  openssl
  sh
)
makedepends=(git 'nim>=2.0.0')
source=("$pkgname::git+https://git.sr.ht/~bptato/chawan#tag=v$pkgver")
sha512sums=('1e20a27aeef9508a6804461bde46afed781b15206f1ac363a113e62740090df49077a60d088d90403df997e0c0aed363db2aaadcd5bd2bc72f5518e8e8fac757')
b2sums=('9b4c8abf3ad2abce15b003cb7e62e6734861b9e99f312b42c305ccfbb47f340c9df050a0ff7e6ed7a1f2c95423223f66a1bf9d98ecc886085b498fec8900f455')

build() {
  cd "$pkgname"

  export CFLAGS+=" -ffile-prefix-map=$srcdir/="

  make LIBEXECDIR=/usr/lib/chawan
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" PREFIX='/usr' LIBEXECDIR="$pkgdir/usr/lib/chawan" install
}
