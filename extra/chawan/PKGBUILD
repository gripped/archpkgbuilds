# Maintainer: Nebulosa <nebulosa2007-at-yandex-dot-ru>

pkgname=chawan
pkgver=0.2.2
pkgrel=2
pkgdesc='A text-mode web browser and pager for Unix-like systems'
arch=(x86_64)
url='https://chawan.net/'
license=(Unlicense)
depends=(
  glibc
  gcc-libs
  brotli
  libssh2
  openssl
  sh
)
makedepends=(git 'nim>=2.0.0')
source=("$pkgname::git+https://git.sr.ht/~bptato/chawan#tag=v$pkgver")
sha512sums=('93d4efa9316b2fcf1ea35d9f3376da4ad189b29879410bf912204420818f960128de92243f3dc689ef87a212644ef09f8c3849095b16e007bc43ca13ee0d5afa')
b2sums=('5564e1cae115348e11c02d17f72666e42572745d3d0990a8484ba6f727ba0c6d3e07f8d140a02109446b71b5d49b3727034aa953b1b1978045206b9fb72c77fc')

build() {
  cd "$pkgname"

  export CFLAGS+=" -ffile-prefix-map=$srcdir/="

  make LIBEXECDIR=/usr/lib/chawan
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" PREFIX='/usr' LIBEXECDIR="$pkgdir/usr/lib/chawan" install
}
