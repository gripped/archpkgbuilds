# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='mise'
pkgver=2026.1.6
pkgrel=1
pkgdesc='The front-end to your dev env'
arch=('x86_64')
url='https://github.com/jdx/mise'
license=('MIT')
depends=(
  bzip2
  gcc-libs
  glibc
  openssl
)
makedepends=(
  cargo
  clang
  cmake
)
optdepends=(
  'bash-completion: bash completion support'
  'usage: completion support'
)
conflicts=(
  rtx
)
replaces=(
  rtx
)
options=(
  '!emptydirs'
  '!lto'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5a5562e6fb7715a90ccac98affd89ab50f932294d96486d43537f54f505375a2ba32d61501a36d4bf964a970cef206ed3ba9fc25c3b8d7edd85755963e36cff7')
b2sums=('cb0ce9fa864edde29b071b64acdfd53bb03d5acc66331b701b95863f08ad476feb47a8e4346d620ec1ccd4dc2bc1a28d0e3e7fa61d30c092e4931bc854cb8ba1')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true
  export MISE_EXPERIMENTAL=1

  cargo test --frozen --all-features

  unset MISE_EXPERIMENTAL
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin"

  # disable self-update
  install --verbose -D --mode=0644 /dev/null "${pkgdir}/usr/lib/mise/.disable-self-update"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 man/man1/mise.1 --target-directory "${pkgdir}/usr/share/man/man1"

  # completion support
  install --verbose -D --mode=0644 completions/mise.bash "${pkgdir}/usr/share/bash-completion/completions/mise"
  install --verbose -D --mode=0644 completions/mise.fish "${pkgdir}/usr/share/fish/vendor_completions.d/mise.fish"
  install --verbose -D --mode=0644 completions/_mise "${pkgdir}/usr/share/zsh/site-functions/_mise"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
