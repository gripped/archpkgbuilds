# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='mise'
pkgver=2025.10.18
pkgrel=1
pkgdesc='The front-end to your dev env'
arch=('x86_64')
url='https://github.com/jdx/mise'
license=('MIT')
depends=(
  bzip2
  gcc-libs
  glibc
  openssl
)
makedepends=(
  cargo
  clang
  cmake
)
optdepends=(
  'bash-completion: bash completion support'
  'usage: completion support'
)
conflicts=(
  rtx
)
replaces=(
  rtx
)
options=(
  '!emptydirs'
  '!lto'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5cd18f37d4e18752bbe3e890d593465304148538e7161a8dfb91bca74473b464a203505606d0fa01d7e542c89e42988cfc8dce5183cac69aab4fc82082d6753a')
b2sums=('0b9994431623e12e14aac64a8dac45440c19d565e53a29aa07341adaea179e4d96f7b58eebf7aeaa41328ae59bdeaf342ff46aa8a25892c3e12452f56b3dcdbb')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true
  export MISE_EXPERIMENTAL=1

  cargo test --frozen --all-features

  unset MISE_EXPERIMENTAL
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin"

  # disable self-update
  install --verbose -D --mode=0644 /dev/null "${pkgdir}/usr/lib/mise/.disable-self-update"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 man/man1/mise.1 --target-directory "${pkgdir}/usr/share/man/man1"

  # completion support
  install --verbose -D --mode=0644 completions/mise.bash "${pkgdir}/usr/share/bash-completion/completions/mise"
  install --verbose -D --mode=0644 completions/mise.fish "${pkgdir}/usr/share/fish/vendor_completions.d/mise.fish"
  install --verbose -D --mode=0644 completions/_mise "${pkgdir}/usr/share/zsh/site-functions/_mise"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
