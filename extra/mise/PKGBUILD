# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='mise'
pkgver=2025.6.0
pkgrel=1
pkgdesc='The front-end to your dev env'
arch=('x86_64')
url='https://github.com/jdx/mise'
license=('MIT')
depends=(
  openssl
)
makedepends=(
  cargo
)
optdepends=(
  'bash-completion: bash completion support'
  'usage: completion support'
)
conflicts=(
  rtx
)
replaces=(
  rtx
)
options=(
  '!emptydirs'
  '!lto'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4b1563df51bd93aa0ee012dfb18b63dc7e845c8f9851bf6d6c7494c527b615f5d4f63a5988484978031d96991a2dbee3a9d98250d54b2170dc4076a789a3a1d2')
b2sums=('025f25b3440b0327a179e4df4299db226b204a2299abeebcaea39faefa6f46206b1654d1f0708a930e7d15d189e591ccb71a12e8404a5ac9728051c695d8ce6b')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true
  export MISE_EXPERIMENTAL=1

  cargo test --frozen --all-features

  unset MISE_EXPERIMENTAL
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin"

  # disable self-update
  install --verbose -D --mode=0644 /dev/null "${pkgdir}/usr/lib/mise/.disable-self-update"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 man/man1/mise.1 --target-directory "${pkgdir}/usr/share/man/man1"

  # completion support
  install --verbose -D --mode=0644 completions/mise.bash "${pkgdir}/usr/share/bash-completion/completions/mise"
  install --verbose -D --mode=0644 completions/mise.fish "${pkgdir}/usr/share/fish/vendor_completions.d/mise.fish"
  install --verbose -D --mode=0644 completions/_mise "${pkgdir}/usr/share/zsh/site-functions/_mise"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
