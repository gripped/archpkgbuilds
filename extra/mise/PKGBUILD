# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='mise'
pkgver=2025.4.5
pkgrel=1
pkgdesc='The front-end to your dev env'
arch=('x86_64')
url='https://github.com/jdx/mise'
license=('MIT')
depends=(
  openssl
)
makedepends=(
  cargo
)
optdepends=(
  'bash-completion: bash completion support'
  'usage: completion support'
)
conflicts=(
  rtx
)
replaces=(
  rtx
)
options=(
  '!emptydirs'
  '!lto'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('81e02904cb21fa36a51c3c9918c2702eee4cf4ebd7ae3c4db03eb9d75c3d3ba391eda45226b28731642a405de8e4824cf37c753e1c1c86912303bbbb627adef4')
b2sums=('29257085b0e99651e13b20a3479365f993c39c1d0d499a13f540decbfc87d6c2b775c9cfc2e9e6b279971fd87e5bdd6912d81d0c3c2c10a0581493c0f6a6300a')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true
  export MISE_EXPERIMENTAL=1

  cargo test --frozen --all-features

  unset MISE_EXPERIMENTAL
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin"

  # disable self-update
  install --verbose -D --mode=0644 /dev/null "${pkgdir}/usr/lib/mise/.disable-self-update"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 man/man1/mise.1 --target-directory "${pkgdir}/usr/share/man/man1"

  # completion support
  install --verbose -D --mode=0644 completions/mise.bash "${pkgdir}/usr/share/bash-completion/completions/mise"
  install --verbose -D --mode=0644 completions/mise.fish "${pkgdir}/usr/share/fish/vendor_completions.d/mise.fish"
  install --verbose -D --mode=0644 completions/_mise "${pkgdir}/usr/share/zsh/site-functions/_mise"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
