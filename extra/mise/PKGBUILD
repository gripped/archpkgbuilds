# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='mise'
pkgver=2025.4.12
pkgrel=1
pkgdesc='The front-end to your dev env'
arch=('x86_64')
url='https://github.com/jdx/mise'
license=('MIT')
depends=(
  openssl
)
makedepends=(
  cargo
)
optdepends=(
  'bash-completion: bash completion support'
  'usage: completion support'
)
conflicts=(
  rtx
)
replaces=(
  rtx
)
options=(
  '!emptydirs'
  '!lto'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('342a4415274b348ab47fbf5835a8965d549bb80921d12132f60c1700ca024a1b8fa82b8ed0f06a3f339b962fc8d607155390ba995a5db4d89bcc243a10bb9dcd')
b2sums=('2e09a6506fcbe019929d3e75bb413b44bf8e13f4dc39c7ebe3d9fed2a049a58f87f7392b59a72ac9050e2d8278ebe2226ce4d67f5f52ec6ee6913cf72f959155')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  export OPENSSL_NO_VENDOR=true
  export MISE_EXPERIMENTAL=1

  cargo test --frozen --all-features

  unset MISE_EXPERIMENTAL
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin"

  # disable self-update
  install --verbose -D --mode=0644 /dev/null "${pkgdir}/usr/lib/mise/.disable-self-update"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 man/man1/mise.1 --target-directory "${pkgdir}/usr/share/man/man1"

  # completion support
  install --verbose -D --mode=0644 completions/mise.bash "${pkgdir}/usr/share/bash-completion/completions/mise"
  install --verbose -D --mode=0644 completions/mise.fish "${pkgdir}/usr/share/fish/vendor_completions.d/mise.fish"
  install --verbose -D --mode=0644 completions/_mise "${pkgdir}/usr/share/zsh/site-functions/_mise"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
