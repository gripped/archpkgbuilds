# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: prg <prg _at_ xannode _dot_ com>
# Contributor: Nico <d3sox at protonmail dot com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>

pkgname=haruna
pkgver=1.2.0
pkgrel=1
pkgdesc='Video player built with Qt/QML on top of libmpv'
arch=(x86_64)
url='https://apps.kde.org/haruna/'
license=(GPL-3.0-or-later)
depends=(ffmpeg
         gcc-libs
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         kdeclarative
         kfilemetadata
         ki18n
         kio
         kirigami
         kwindowsystem
         mpvqt
         qt6-5compat
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
optdepends=('yt-dlp: YouTube support')
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4a9fd360d66e00d0f8d7a11e73dd9bfc55e486c0135e8d133de8abeb2ca09d88'
            'SKIP')
validpgpkeys=(4E421C6554B89766DF9B7A37E12AB207C8755905) # George Florea Bănuș <georgefb899@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
