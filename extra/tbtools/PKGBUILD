# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=tbtools
pkgver=0.3.2
pkgrel=1
pkgdesc='Collection of tools for Linux Thunderbolt/USB4 development, debugging and validation'
arch=('x86_64')
url='https://github.com/intel/tbtools'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'systemd-libs'
)
makedepends=(
  'git'
  'rust'
)
source=("git+https://github.com/intel/tbtools.git#tag=v$pkgver")
b2sums=('775847bba2099c7b913e303a62ef049856d02f65630e62352dc620586a3ed997388656775c29d83599170495b2920db4ea83155f98834d5a6b224c000ec60422')

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked 
}

package() {
  cargo install --locked --root "${pkgdir}"/usr --path "${srcdir}"/${pkgname}
  rm "${pkgdir}"/usr/{.crates.toml,.crates2.json}
  install -Dm644 "${pkgname}"/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
