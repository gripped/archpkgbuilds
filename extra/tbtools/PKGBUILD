# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=tbtools
pkgver=0.6.0
pkgrel=1
pkgdesc='Collection of tools for Linux Thunderbolt/USB4 development, debugging and validation'
arch=('x86_64')
url='https://github.com/intel/tbtools'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'systemd-libs'
)
makedepends=(
  'git'
  'rust'
)
source=("git+https://github.com/intel/tbtools.git#tag=v$pkgver")
b2sums=('01cdf6dafb1c23357fab8a2089529f1a0f0332e8adf02b603a65ab3d3dda05981a806aa9b749ee6d13ba09a25171779889a25955eef0941feb1ecfa19181c98f')

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked 
}

package() {
  cargo install --locked --root "${pkgdir}"/usr --path "${srcdir}"/${pkgname}
  rm "${pkgdir}"/usr/{.crates.toml,.crates2.json}
  install -Dm644 "${pkgname}"/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
