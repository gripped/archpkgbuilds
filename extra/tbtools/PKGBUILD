# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=tbtools
pkgver=0.7.0
pkgrel=2
pkgdesc='Collection of tools for Linux Thunderbolt/USB4 development, debugging and validation'
arch=('x86_64')
url='https://github.com/intel/tbtools'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'systemd-libs'
)
makedepends=(
  'git'
  'rust'
)
source=("git+https://github.com/intel/tbtools.git#tag=v$pkgver")
b2sums=('769d4a445b86fca1c342bec97f9de75756a8872f5a4ca48ddc5284c00481dc982cbc4607986e5de70107adf9216b6dfd1141a575b0737377e456b1a75c3389c5')

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked 
}

package() {
  cargo install --locked --root "${pkgdir}"/usr --path "${srcdir}"/${pkgname}
  rm "${pkgdir}"/usr/{.crates.toml,.crates2.json}
  install -Dm644 "${pkgname}"/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}

  # Additional helper scripts
  install -dm755 "${pkgdir}"/usr/share/tbtools/
  cp -ar "${pkgname}"/scripts "${pkgdir}"/usr/share/tbtools/
}
