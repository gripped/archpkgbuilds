# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=tbtools
pkgver=0.5.0
pkgrel=1
pkgdesc='Collection of tools for Linux Thunderbolt/USB4 development, debugging and validation'
arch=('x86_64')
url='https://github.com/intel/tbtools'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'systemd-libs'
)
makedepends=(
  'git'
  'rust'
)
source=("git+https://github.com/intel/tbtools.git#tag=v$pkgver")
b2sums=('4a276e79aeea8d926cccfcdbc9625c1cc6ecfdb9fd170058e3efab173f847d9be888da917df66e47121d07e094236c7cd9c451403ea7558a7ea49a10791f840d')

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked 
}

package() {
  cargo install --locked --root "${pkgdir}"/usr --path "${srcdir}"/${pkgname}
  rm "${pkgdir}"/usr/{.crates.toml,.crates2.json}
  install -Dm644 "${pkgname}"/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
