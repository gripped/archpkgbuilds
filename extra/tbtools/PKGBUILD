# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=tbtools
pkgver=0.4.0
pkgrel=1
pkgdesc='Collection of tools for Linux Thunderbolt/USB4 development, debugging and validation'
arch=('x86_64')
url='https://github.com/intel/tbtools'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'systemd-libs'
)
makedepends=(
  'git'
  'rust'
)
source=("git+https://github.com/intel/tbtools.git#tag=v$pkgver")
b2sums=('f3b11c3494b16d545a554e14689b1e4d15f41b14c5e0179afa14051914e51f02d5c796694dca6e71fe5fb0ae73d3556c9d5a1b36c5f96bdf13094f9658f7d9d8')

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked 
}

package() {
  cargo install --locked --root "${pkgdir}"/usr --path "${srcdir}"/${pkgname}
  rm "${pkgdir}"/usr/{.crates.toml,.crates2.json}
  install -Dm644 "${pkgname}"/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
