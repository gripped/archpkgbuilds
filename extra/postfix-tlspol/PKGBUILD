# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=postfix-tlspol
pkgver=1.8.18
pkgrel=1
pkgdesc="Lightweight MTA-STS + DANE/TLSA resolver and TLS policy server for Postfix, prioritizing DANE"
url="https://github.com/Zuplu/postfix-tlspol"
arch=('x86_64')
license=('MIT')
depends=('glibc' 'postfix')
makedepends=('go')
optdepends=('less: for dump formatting'
            'jq: for query formatting')
backup=("etc/${pkgname}/config.yaml")
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
        'systemd-service.patch'
        'postfix-tlspol.sysusers'
        'postfix-tlspol.tmpfiles')
sha256sums=('9787cd26b7aab55711832ba84a575484b6f9f30abc8797d96143ee89c18167f1'
            '078ebf65e766c404af7a9205db23a60b0eea2b7eff2184693dd17d8689c33152'
            'f26a774aaf79900f501e02db7ec5246ab8f23ce0ce8f88f0bcd0dddd856d1611'
            'f19e6081b5e0a1b5b7419b6234346cf5e2dc0a609292e484f5e9d863e300d2a7')

prepare(){
	cd "${pkgname}-${pkgver}"
	mkdir -p build/

	# Remove superfluous options from the systemd service and adjust it for System-Wide usage 
	patch -Np1 -i "${srcdir}/systemd-service.patch"

	# Remove mta-sts test, which is flaky / non-deterministic
	rm -f internal/mta-sts_test.go
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	go build -ldflags="-s -w -linkmode=external -buildid='' -X main.Version=${pkgver}" -o "build/${pkgname}" *.go
}

check() {
	cd "${pkgname}-${pkgver}"
	go test ./...
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm 755 "build/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 "init/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	install -dm 755 "${pkgdir}/var/lib/${pkgname}"
	install -Dm 644 configs/config.default.yaml "${pkgdir}/etc/${pkgname}/config.yaml"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 "${srcdir}/${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
	install -Dm 644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
