# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

_name=progressbar2
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=4.4.1
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-progressbar"
license=(BSD-3-Clause)
depends=(
  python
  python-utils
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-sphinx
  python-wheel
)
checkdepends=(
  python-dill
  python-pytest
  python-freezegun
)
source=($url/releases/download/v$pkgver/$_name-$pkgver.tar.gz{,.asc})
sha512sums=('41748c4d51bbf51dc6c91e4612c22e8350753a9c4c0984a4b6cc170ae367224c413833c7e9853880012f746c8d2bb52c62a812f5d4c0054f081b39573228a29c'
            'SKIP')
b2sums=('0817682a33910dd4e1f5c84528298fae3acca49003c9cc3af6034bde8e5a3214e3d87f733ba5a7e04b9648cf87272257a24d2c73c64a7d2dd2992fec5254d0ab'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
