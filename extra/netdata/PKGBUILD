# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SanskritFritz (gmail)

pkgname=netdata
pkgver=1.45.5
pkgrel=1
_go_ver="0.57.2"
pkgdesc="Real-time performance monitoring, in the greatest possible detail, over the web"
url="https://github.com/netdata/netdata"
arch=('x86_64')
license=('GPL')
backup=('etc/netdata/netdata.conf')
depends=('libmnl' 'libnetfilter_acct' 'zlib' 'judy' 'libuv' 'json-c' 'libcap' 'lz4' 'openssl' 'which' 'snappy'
         'protobuf' 'libwebsockets' 'mongo-c-driver' 'libyaml')
makedepends=('cups' 'go' 'cmake' 'ninja')
optdepends=('nodejs: for monitoring named and SNMP devices'
            'lm_sensors: for monitoring hardware sensors'
            'iproute2: for monitoring Linux QoS'
            'python: for most of the external plugins'
            'python-psycopg2: for monitoring PostgreSQL databases'
            'python-mysqlclient: for monitoring MySQL/MariaDB databases'
            'python-requests: for monitoring elasticsearch'
            'hddtemp: for monitoring hhd temperature'
            'fping: for for fping module'
            'apcupsd: for monitoring APC UPS'
            'cups: for CUPS plugin'
            'iw: for monitoring Linux as access point')
source=("$pkgname-$pkgver.tar.gz::https://github.com/netdata/netdata/releases/download/v$pkgver/netdata-v$pkgver.tar.gz"
        fix-protobuf-build-issue.patch
        netdata.sysusers
        netdata.tmpfiles)
sha512sums=('7850f9a91e38c7b77007688a0bf44689a088b5f896934d900e4aa0dae53c9977bd181c1f5ff615e020c8a7b1c86571cddb37a6a256cd0aabc146fa27cef25774'
            '5078f48121418a9abc4ce5bbe52b895fc2eaae2123345fe2cf715e2e7b80006f7c5b51d3ed3805ec7078018f7cd84731e092c14c9173bda6f770ccf1a0a7a109'
            'a910809a823ba58ca7bdaa72e8d68427b48f452c1fbb2343fa182ecb0a091a7640e73af24b8ba2fdd90e00aed8ef53b7fccd25cb8f04ca9b9fa6c8e52223ca66'
            '9dcf6058d7e2b072ca6797c3e8fcb2cccc1f6670a9e58769922e078c95a9431370dc429cc34d8e642dbab10942221f1f730d570df7c40d651872931c070be832')

prepare() {
  cd $pkgname-v$pkgver
  patch -Np1 -i "$srcdir"/fix-protobuf-build-issue.patch

  sed -i "s/CMAKE_CXX_STANDARD 14/CMAKE_CXX_STANDARD 17/" CMakeLists.txt
  sed -i "s|usr/sbin|usr/bin|g" CMakeLists.txt
  sed -i "s|usr/libexec|usr/lib|g" CMakeLists.txt
}

build() {
  cmake \
    -B build \
    -G Ninja \
    -S "$pkgname-v$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX=/ \
    -Wno-dev \
    -DENABLE_PLUGIN_FREEIPMI=OFF \
    -DENABLE_PLUGIN_XENSTAT=OFF \
    -DENABLE_PLUGIN_EBPF=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname-v$pkgver"

  install -Dm644 system/netdata.conf "$pkgdir"/etc/netdata/netdata.conf

  install -Dm644 "${pkgdir}/usr/lib/netdata/system/systemd/netdata.service" "${pkgdir}/usr/lib/systemd/system/netdata.service"
  install -Dm644 "${srcdir}/netdata.sysusers" "${pkgdir}/usr/lib/sysusers.d/netdata.conf"
  install -Dm644 "${srcdir}/netdata.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/netdata.conf"

  chown -R 134:134 \
  	"$pkgdir"/var/cache/netdata \
  	"$pkgdir"/var/lib/netdata \
  	"$pkgdir"/var/log/netdata
  chmod 750 "$pkgdir"/var/cache/netdata
  chmod 750 "$pkgdir"/var/lib/netdata

  # Remove files for other initsystems
  rm -rf "${pkgdir}"/usr/lib/netdata/system

  rm -rf "${pkgdir}"/var/run
}
