# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Carl Smedstad <carl.smedstad at protonmail dot com>
# Contributor:  Po-An,Yang(Antonio) <yanganto gmail.com>

pkgname=clipcat
pkgver=0.20.1
pkgrel=1
pkgdesc="A clipboard manager"
arch=('x86_64')
url="https://github.com/xrelkd/clipcat"
license=('GPL-3.0-only')
depends=('gcc-libs' 'libgit2')
makedepends=('cargo' 'protobuf')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz" 'libgit2.pc')
sha256sums=('91682db3fd1a9dd69054f1f4de08e4a97f5f9b9edb3d357e45b355036a68b925'
            'f0b4952bd7ac83d19e11b491af6debc6cce1263070943a886726915f8c4283df')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_force_libgit19() {
  # Force compilation against libgit2 1.9
  export PKG_CONFIG_PATH="$srcdir"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features
  _force_libgit19
  for cmd in clipcatd clipcatctl clipcat-menu clipcat-notify; do
    "target/release/$cmd" completions bash > "$cmd.bash"
    "target/release/$cmd" completions zsh > "$cmd.zsh"
    "target/release/$cmd" completions fish > "$cmd.fish"
  done
}

check() {
  cd "$pkgname-$pkgver"
  _force_libgit19
  cargo test --frozen --all-features -- \
    --skip test_x11_clipboard \
    --skip test_x11_primary
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 -t "$pkgdir/usr/bin" \
    target/release/clipcat-menu \
    target/release/clipcat-notify \
    target/release/clipcatctl \
    target/release/clipcatd

  for cmd in clipcatd clipcatctl clipcat-menu clipcat-notify; do
    install -Dm644 "$cmd.bash" "$pkgdir/usr/share/bash-completion/completions/$cmd"
    install -Dm644 "$cmd.zsh" "$pkgdir/usr/share/zsh/site-functions/_$cmd"
    install -Dm644 "$cmd.fish" "$pkgdir/usr/share/fish/completions/$cmd.fish"
  done

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}

# vim:set ts=2 sw=2 et:
