# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-mediafire
_pkgname="${pkgname#python-}"
pkgver=0.6.1
pkgrel=5
pkgdesc='Python implementation of MediaFire Core API client'
arch=('any')
url='https://pypi.org/project/mediafire/'
license=('BSD-2-Clause')
depends=(
  'python'
  'python-requests'
  'python-requests-toolbelt'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=(
  "$pkgname-$pkgver.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz"
  'remove-six.patch'
)
sha512sums=('738bff67f506109eba9ef6380f2e9ba22771ce71c6ab8976dd4e8c732ed1ab1fe430a700107e835b2b049c80a064c9d46242b9d22f53b0cd613bbd344d20fa39'
            '46d44c0f2be4e7f7d52a8cb65f7150228125a3ecef7fa0a8a372853fec0358721d0d369332fac3bcc88c3a8a0cba5771a6654ec2120ad9dc6e4db6e6bd46ec2e')
b2sums=('f54637a8a7598395615db9b677eee3333fb1012d138ae528f19b93446e8095511698fac71cc4f1b6052b54fa1a204aee3b1c5e64094b6a296b5cd3ff64f3f15a'
        '4c5db58425c466b5729fcfa14788e13b8090592d2b45965f9ba8c3ee1385927f0dbcfd270d769536eab582e60154df0213e49829b2214268c362a1f77247a198')

prepare() {
  cd "$_pkgname-$pkgver"

  # remove dependency: python-six
  patch -p1 -i "$srcdir/remove-six.patch"
}

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
