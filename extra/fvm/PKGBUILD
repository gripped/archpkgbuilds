# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=fvm
pkgver=4.0.1
pkgrel=2
pkgdesc="Flutter Version Management: A simple CLI to manage Flutter SDK versions."
arch=('x86_64')
url="https://${pkgname}.app"
license=('MIT')
depends=('git' 'glibc' 'unzip')
makedepends=('dart')
source=("git+https://github.com/leoafarias/${pkgname}.git#tag=${pkgver}")
sha256sums=('57aed2218bda21ae6f74c648673cd05de9f09c07c096b01809c816b946b07d15')
options=('!strip' '!debug')

prepare() {
	cd "${pkgname}/"

	# Disable analytics
	dart --disable-analytics

	# Download dependencies
	dart pub get
}

build() {
	cd "${pkgname}/bin/"
	dart compile exe main.dart -o "${pkgname}"
}

check() {
	cd "${pkgname}/"
	dart test
}

package() {
	cd "${pkgname}/"
	install -Dm755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
