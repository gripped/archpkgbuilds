# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=fvm
pkgver=4.0.0
pkgrel=1
pkgdesc="Flutter Version Management: A simple CLI to manage Flutter SDK versions."
arch=('x86_64')
url="https://${pkgname}.app"
license=('MIT')
depends=('git' 'glibc' 'unzip')
makedepends=('dart')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/leoafarias/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('18cf7634d36031e44bc46f482c8d5514c3341d1412ed82dc61ff97dbf829d1ab')
options=('!strip' '!debug')

prepare() {
	cd "${pkgname}-${pkgver}/"

	# Disable analytics
	dart --disable-analytics

	# Download dependencies
	dart pub get
}

build() {
	cd "${pkgname}-${pkgver}/"
	dart compile exe -o "bin/${pkgname}" bin/main.dart
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
