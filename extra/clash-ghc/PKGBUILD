# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=clash-ghc
pkgver=1.8.2
pkgrel=42
pkgdesc="CAES Language for Synchronous Hardware"
url="https://clash-lang.org/"
license=('BSD-2-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-clash-lib' 'haskell-clash-prelude'
         'haskell-data-binary-ieee754' 'haskell-extra' 'haskell-ghc' 'haskell-ghc-typelits-extra'
         'haskell-ghc-typelits-knownnat' 'haskell-ghc-typelits-natnormalise' 'haskell-hashable'
         'haskell-lens' 'haskell-primitive' 'haskell-reflection' 'haskell-split'
         'haskell-string-interpolate' 'haskell-uniplate' 'haskell-unordered-containers'
         'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc')
conflicts=('clash')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('52ac875434a3c7bc3de2a6ed3619a61419f2e1c7d1abf9f9c2c2619d1d93d2fba165e9867b782ab698bddc8009e0a9dc782e0f74bdf3826d4b708372e6ab4697')

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
