# Maintainer: Benjamin Bukowski <benjamin.bukowski@gmail.com>
# Contributor: Johannes LÃ¶thberg <demizide@gmail.com>

pkgname=atftp
pkgver=0.8.0
pkgrel=5
pkgdesc='Client/server implementation of the TFTP protocol that implements RFCs 1350, 2090, 2347, 2348, and 2349'
arch=('x86_64')
url='https://sourceforge.net/projects/atftp/'
license=('GPL')
depends=('pcre2' 'readline')
backup=('etc/conf.d/atftpd')
options=('!lto')
source=("https://downloads.sf.net/sourceforge/atftp/atftp-${pkgver}.tar.gz"
        'atftpd.service'
        'atftpd.conf'
        'atftp.sysusers'
        'atftp.tmpfiles')
sha256sums=('df2aa089c7670f9eab40e5598e5d2cb6a582dc5182926ea50b4d690e4e37f316'
            '74a030f0fb48e97470d59d767039e44d28cbd6c4722621207684220210b8fa65'
            '0b28125099ffdd6869c6fbcb3167e93ccd71a0c6e465b1b62c7dedf9f628dd4e'
            '9a0f81ed10ffa35466e3ff35b82dad181aadacb4a05917f580681ffbb5a597e6'
            '2096272445c736ba05529af628cc2d46d0236c8f1ecbd50bb1db6dc6c4a972c5')

prepare() {
  cd "atftp-${pkgver}/"

  autoreconf -fi
}

build() {
  cd "atftp-${pkgver}/"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin \
    --enable-libreadline \
    --disable-libwrap
  make
}

package() {
  cd "atftp-${pkgver}/"

  make DESTDIR="$pkgdir" install

  install -D -m0644 ../atftpd.service "$pkgdir"/usr/lib/systemd/system/atftpd.service
  install -D -m0644 ../atftpd.conf "$pkgdir"/etc/conf.d/atftpd
  install -D -m0644 ../atftp.sysusers "$pkgdir"/usr/lib/sysusers.d/atftp.conf
  install -D -m0644 ../atftp.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/atftp.conf

  # remove in.tftpd link as it conflicts with extra/tftp-hpa
  rm "${pkgdir}/usr/share/man/man8/in.tftpd.8"
  rm "${pkgdir}/usr/bin/in.tftpd"
}
