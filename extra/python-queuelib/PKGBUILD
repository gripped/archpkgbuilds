# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>

pkgname=python-queuelib
pkgver=1.8.0
pkgrel=2
pkgdesc="Collection of persistent (disk-based) queues."
arch=('any')
license=('BSD-3-Clause')
url="https://scrapy.org"
depends=('python')
makedepends=('git' 'python-hatchling' 'python-build' 'python-installer')
checkdepends=('python-pytest')
source=("git+https://github.com/scrapy/queuelib.git#tag=v$pkgver")
sha512sums=('b950a25ae1d0c36aa4a2f7daae7684201cd2ee8a5ff322fa05aef37098d56a3c53c4157343ba0c528640f9d43ed63e7222210fa28e8598f86fc1db4d92b610cc')

build() {
  cd queuelib
  python -m build --wheel --no-isolation
}

check() {
  cd queuelib
  pytest
}

package() {
  cd queuelib
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -D -m644 README.rst -t "$pkgdir"/usr/share/doc/$pkgname
}
