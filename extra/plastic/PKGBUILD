# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgbase=plastic
pkgname=(plastic plastic_tui)
pkgver=0.3.4
pkgrel=1
pkgdesc="A NES emulator with GUI and TUI"
arch=('x86_64')
url="https://github.com/Amjad50/plastic"
license=('MIT')
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo' 'alsa-lib')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('323c6820d841298c71a15be50e33b59238f86d38f7dde934436e1a29607043adc928c1fc28be8ad7c85bcd61fb298c4131b8eb72ccfab2c96311676a7663aa0e')

prepare() {
  cd "$pkgbase-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  mkdir -p completions/
}

build() {
  cd "$pkgbase-$pkgver"
  cargo build --release --frozen
  cargo build -p "plastic_tui" --release --frozen
}

check() {
  cd "$pkgbase-$pkgver"
  cargo test --frozen -- --skip "test_host_git_source"
}

package_plastic() {
  cd "$pkgbase-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "package/plastic.desktop" "$pkgdir/usr/share/applications/plastic.desktop"
  install -Dm 644 "images/icon.png" "$pkgdir/usr/share/icons/hicolor/256x256/apps/plastic.png"
  install -Dm 644 "docs/man/plastic.1" "$pkgdir/usr/share/man/man1/plastic.1"
}

package_plastic_tui() {
  depends+=('systemd-libs')
  cd "$pkgbase-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
