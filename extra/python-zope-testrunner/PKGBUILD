# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-testrunner
pkgver=6.3
pkgrel=1
pkgdesc="Zope testrunner script"
arch=('any')
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-exceptions')
checkdepends=('python-zope-testing')
source=("https://github.com/zopefoundation/zope.testrunner/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f115e576c2fe01489dec72b46f76e32dc6f63f813b4e2ccea7b29976761c79efe36c0455ad0f18422366096df9cd6a0bbb04068e63505e1be7a62f0f10a8a0f5')

build() {
  cd zope.testrunner-$pkgver
  python setup.py build
}

check() {
  cd zope.testrunner-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python setup.py test
  )
}

package() {
  cd zope.testrunner-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
