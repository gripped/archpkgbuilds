# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eclib
pkgver=20250530
pkgrel=2
pkgdesc='Includes mwrank (for 2-descent on elliptic curves over Q) and modular symbol code used to create the elliptic curve database'
arch=(x86_64)
url='https://github.com/JohnCremona/eclib/'
license=(GPL-2.0-or-later)
depends=(boost-libs
         flint
         gcc-libs
         glibc
         ntl
         pari)
makedepends=(boost
             git)
source=(git+https://github.com/JohnCremona/eclib#tag=v$pkgver)
sha256sums=('224ea94b14d3cefd4802de8b7cde7dabd89de2339e4d50ebdb15cd03cf96bc5b')

prepare() {
  cd $pkgname
  autoreconf -vif
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --with-flint=/usr --with-boost
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
