# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eclib
pkgver=20231211
pkgrel=1
pkgdesc='Includes mwrank (for 2-descent on elliptic curves over Q) and modular symbol code used to create the elliptic curve database'
arch=(x86_64)
url='https://github.com/JohnCremona/eclib/'
license=(GPL)
depends=(boost-libs
         flint
         gcc-libs
         glibc
         ntl
         pari)
makedepends=(boost)
source=(https://github.com/JohnCremona/eclib/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2)
sha256sums=('968ef770819fb6bb894effaef7e797ebd767e90e5910589f976b40ad28de409d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-flint=/usr --with-boost
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
