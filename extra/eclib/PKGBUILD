# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eclib
pkgver=20250122
pkgrel=1
pkgdesc='Includes mwrank (for 2-descent on elliptic curves over Q) and modular symbol code used to create the elliptic curve database'
arch=(x86_64)
url='https://github.com/JohnCremona/eclib/'
license=(GPL-2.0-or-later)
depends=(boost-libs
         flint
         gcc-libs
         glibc
         ntl
         pari)
makedepends=(boost
             git)
source=(git+https://github.com/JohnCremona/eclib#tag=v$pkgver)
sha256sums=('054c063844db96c42cc57da29953a89da8e2a0082243237c3bc4a666d992aaab')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --with-flint=/usr --with-boost
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
