# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=apostrophe
pkgver=3.3
pkgrel=1
pkgdesc='Distraction free Markdown editor'
arch=(any)
url='https://apps.gnome.org/Apostrophe/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  graphene
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libspelling
  pango
  python
  python-chardet
  python-gobject
  python-pypandoc
  python-regex
  typst
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
optdepends=(
  'texlive-bin: Export to LaTeX'
  'texlive-formatsextra: Export to PDF using XeTeX'
  'texlive-latex: Inline math preview'
  'texlive-xetex: Export to PDF using XeTeX'
)
source=(
  "git+https://gitlab.gnome.org/World/apostrophe.git#tag=v$pkgver"
  0001-Don-t-set-Typst-template.patch
)
b2sums=(
  1d82d39bc6f9b5c656a069a4687e23697a6c12fed744760d25164c6a6d46f9ba789c97433f2a03e87463b8a34b52aa8ffb419f03f8ab8f6205a10c0a10d9b4f2
  67c52674f133627c4d2b9ed927aa9e50791dad3137e600324325af681c28af4a5f2e32f231a6f0f92d53f773c4954b02c0051e1b45d31052e7f5fb9ff4b2a059
)

prepare() {
  cd $pkgname

  # Don't set Typst template
  # https://gitlab.gnome.org/World/apostrophe/-/issues/634
  git apply -3 ../0001-Don-t-set-Typst-template.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
