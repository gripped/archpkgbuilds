# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=apostrophe
pkgver=3.2
pkgrel=8
pkgdesc='Distraction free Markdown editor'
arch=(any)
url='https://apps.gnome.org/Apostrophe/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  graphene
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libspelling
  pango
  python
  python-chardet
  python-gobject
  python-pypandoc
  python-regex
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  meson
)
optdepends=(
  'texlive-bin: Export to LaTeX'
  'texlive-formatsextra: Export to PDF'
  'texlive-latex: Inline math preview'
  'texlive-xetex: Export to PDF'
)
source=(
  "git+https://gitlab.gnome.org/World/apostrophe.git#tag=v$pkgver"
  apostrophe-dbus-activatable.patch
)
b2sums=(
  cdc29505c886181ca7ae8c66559a17ceec2884a6a656819930015fc8ba210bdab71d7f216cd9bac580710559c7d74d010f84a9a9e9ed0fb50c8b1f41be1c9178
  fc16255b5aa89f3083d68fdea701c482d53d6cf1899b4247e26e818504039906f7ddcf58c2215ad979cee7cb6d43874ead9344b082b101ac8fb7954e5748a76b
)

prepare() {
  cd $pkgname

  # Various fixes from upstream
  git cherry-pick -n -m 1 v3.2..268c6371b64dc3513ac1d0ddc0be0d43f306c817

  # https://gitlab.gnome.org/World/apostrophe/-/merge_requests/330
  git apply -3 ../apostrophe-dbus-activatable.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
