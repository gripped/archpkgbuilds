# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=srgn
pkgver=0.14.1
pkgrel=1
pkgdesc="A code surgeon for precise text and code transplantation"
url="https://github.com/alexpovel/srgn"
arch=(x86_64)
license=('MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(cargo)
options=(!lto)
source=("$url/archive/$pkgname-v$pkgver.tar.gz")
sha256sums=('5c06be7d74b1c50db2c80b8932733093e5ec35e75405762e7f5d7b0ba357ac06')

prepare() {
  cd $pkgname-srgn-v$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-srgn-v$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-srgn-v$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features -- \
    --skip test_regex_scoping_randomly
}

package() {
  cd $pkgname-srgn-v$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" target/release/srgn
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -va -t "$pkgdir/usr/share/doc/$pkgname" docs

  ./target/release/srgn --completions bash \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/srgn"
  ./target/release/srgn --completions fish \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/srg.fish"
  ./target/release/srgn --completions zsh \
    | install -vDm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_srgn"
}
