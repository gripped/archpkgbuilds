# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-overloaded-record-dot-plugin
pkgname=haskell-hls-overloaded-record-dot-plugin
pkgdesc="Overloaded record dot plugin for Haskell Language Server"
pkgver=2.0.0.1
pkgrel=9
license=('BSD-3-Clause')
arch=('x86_64')
url="https://github.com/haskell/haskell-language-server.git"
depends=('ghc-libs' 'haskell-ghcide' 'haskell-hls-graph' 'haskell-hls-plugin-api'
         'haskell-lens' 'haskell-lsp' 'haskell-syb' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils' 'haskell-lsp-test')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('caa5f7e5827081d445f6ea8a7ea6124f4e068ba3a63a4e6f9d19b54c018f95c6')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd "$_hkgname-$pkgver"

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd "$_hkgname-$pkgver"
  # TODO: posix_spawnp: does not exist
  runhaskell Setup test || echo "Tests failed"
}

package() {
  cd "$_hkgname-$pkgver"

  install -D -m744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -D -m744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
