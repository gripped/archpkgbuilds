# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-maps-plugin'
_pluginname='dashboards-maps'
pkgver=3.2.0.0
_dashboardsver=3.2.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Maps Plugin'
url='https://opensearch.org/docs/latest/dashboards/geojson-regionmaps/'
arch=('x86_64')
license=('Apache-2.0')
depends=("opensearch-dashboards=${_dashboardsver}" 'coffeescript')
makedepends=('yarn' 'npm' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/${_pluginname}.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
)
sha256sums=('665c14260bdf66b379646ac5b1801a35ab1c25937abdab6f2a64599ede6b9c65'
            '5d3a484adf4627c978c0d7139c0536accc87f0369b80fb16f7503aa48b23cc43')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" \
    "OpenSearch-Dashboards/package.json"
  cd "OpenSearch-Dashboards"
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      patch -p1 -N -l -i "$srcdir/${filename##*/}"
    fi
  done
}

build() {
  mv "${_pluginname}" "OpenSearch-Dashboards/plugins/customImportMapDashboards"
  cd "OpenSearch-Dashboards/plugins/customImportMapDashboards"
  yarn osd bootstrap
  yarn plugin-helpers build --skip-archive # `yarn build` will always create the ZIP
}

check() {
  cd "OpenSearch-Dashboards/plugins/customImportMapDashboards"
  yarn test:jest -u
}

package() {
  install -Dm644 "OpenSearch-Dashboards/plugins/customImportMapDashboards/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"

  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"
  cd "${srcdir}/OpenSearch-Dashboards/plugins/customImportMapDashboards"
  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"
  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
