# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-maps-plugin'
_pluginname='dashboards-maps'
pkgver=2.19.1.0
_dashboardsver=2.19.1
pkgrel=1
pkgdesc='OpenSearch Dashboards Maps Plugin'
url='https://opensearch.org/docs/latest/dashboards/geojson-regionmaps/'
arch=('x86_64')
license=('Apache-2.0')
depends=("opensearch-dashboards=${_dashboardsver}" 'coffeescript')
makedepends=('yarn' 'npm' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/${_pluginname}.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
  # Switch to NodeJS LTS 22 "Jod"
  "https://github.com/hashworks/OpenSearch-Dashboards/commit/1a602a8e117f735154230cc5199153a57ec5bbb6.patch"
)
sha256sums=('1d148910c3f8c903b5f870abfe104513c6a5b1a568675a03d738875166028231'
            'd9e3acfeb25ce8c021c20d55f25d1f20fd5f524de81147f4154ab9fad7a8b786'
            'e916c1dd34871372ff4e7ca71e66cb14c472ca94c9143e6b6da28dbef1b25738')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" \
    "OpenSearch-Dashboards/package.json"
  cd "OpenSearch-Dashboards"
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      patch -p1 -N -l -i "$srcdir/${filename##*/}"
    fi
  done
}

build() {
  mv "${_pluginname}" "OpenSearch-Dashboards/plugins/customImportMapDashboards"
  cd "OpenSearch-Dashboards/plugins/customImportMapDashboards"
  yarn osd bootstrap
  yarn plugin-helpers build --skip-archive # `yarn build` will always create the ZIP
}

check() {
  cd "OpenSearch-Dashboards/plugins/customImportMapDashboards"
  yarn test:jest -u
}

package() {
  install -Dm644 "OpenSearch-Dashboards/plugins/customImportMapDashboards/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"

  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"
  cd "${srcdir}/OpenSearch-Dashboards/plugins/customImportMapDashboards"
  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"
  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
