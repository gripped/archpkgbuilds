# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=nlopt
pkgver=2.9.0
pkgrel=4
pkgdesc="Nonlinear optimization library"
arch=(x86_64)
url="https://nlopt.readthedocs.io/en/latest/"
license=(LGPL-2.1-or-later)
depends=(gcc-libs)
makedepends=(cmake python-numpy octave guile swig)
optdepends=('python-numpy: to use with python')
source=(https://github.com/stevengj/nlopt/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6e899e297485e457ec1bf84844de29921aeef674f9d5caf60277df45dca6ff76')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
