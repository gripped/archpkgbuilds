# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=vals
pkgver=0.39.3
pkgrel=1
pkgdesc="Helm-like configuration values loader with support for various sources"
arch=('x86_64')
url="https://github.com/variantdev/vals"
license=('Apache-2.0')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/variantdev/vals/archive/v${pkgver}.tar.gz")
sha512sums=('fb4ab38bfa52b5dce64f108266ac254415a72516249c3e6c931c03a3aff397da1c4b67db074cfe25669ef66cb2878969e4f595771ffc033647aafc4232d69226')
options=(!lto)

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/variantdev "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/variantdev/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/variantdev/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export GOLDFLAGS="-linkmode=external"
  go build -v ./cmd/vals
  chmod +x ./vals
}

# Failing tests due to Google Cloud dependency
#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./vals "${pkgdir}/usr/bin/vals"
}
