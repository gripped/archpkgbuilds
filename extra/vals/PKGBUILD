# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=vals
pkgver=0.43.1
pkgrel=2
pkgdesc="Helm-like configuration values loader with support for various sources"
arch=('x86_64')
url="https://github.com/variantdev/vals"
license=('Apache-2.0')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/variantdev/vals/archive/v${pkgver}.tar.gz")
sha512sums=('5ed19dc51b67279bfacb9d0a49a4174978ffb95d5edf03f2a7a47bbf97d0cd4fcb93115b65dfb157309f94c45e9b41c3c23d4bb89759df3263e16b55448de322')
options=(!lto)

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/variantdev "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/variantdev/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/variantdev/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export GOLDFLAGS="-linkmode=external"
  go build -v ./cmd/vals
  chmod +x ./vals
}

# Failing tests due to Google Cloud dependency
#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./vals "${pkgdir}/usr/bin/vals"
}
