# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tocic <tocic at protonmail dot ch>
# Contributor: Yuki Takagi <takagiy.4dev@gmail.com>

pkgname=immer
pkgver=0.8.1
pkgrel=1
pkgdesc='Postmodern immutable and persistent data structures for C++'
arch=(any)
url='https://sinusoid.es/immer'
license=(BSL-1.0)
makedepends=(catch2
             cmake
             git)
checkdepends=(boost
              fmt)
source=(git+https://github.com/arximboldi/immer#tag=v$pkgver)
sha256sums=('58a8c75270c642e3232d7ae27449ef3d07aa640eeedb53d67e6577493c1b79e7')

prepare() {
  cd $pkgname
  # Update to Catch2 v3 to avoid broken tests
  git cherry-pick -n f381e4988c1562f0f64533146c9b9cb7c0823b3e
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

check() {
  cmake --build build --target check
  ctest --test-dir build \
        --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
