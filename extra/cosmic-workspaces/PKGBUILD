# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-workspaces
pkgver=1.0.0alpha6
pkgrel=1
epoch=1
pkgdesc='Cosmic workspaces'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-workspaces-epoch
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libinput
  libxkbcommon
  mesa
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=7f877f72a373514d298259ad7c6a6b3a5e10432c
source=(
  git+https://github.com/pop-os/cosmic-workspaces-epoch.git#tag=${_tag}
  cosmic-workspaces-lto.patch
)
b2sums=('195f565020b56ed2185a08b3c6caa60df58d6e9a2d435b03a72051c3ae9cf7a6e86d1a1de2b1a306b1bffc54825f14736cb24713b1f8d8a0a3ce91b1e1d99e2d'
        '82495774acf609583991c1cc13376fdfd81107d85030e9fb484cd50336b3f6418b59f5322d85cef83a3d75af42414ea86843bacae39286a643e2a668b15f3048')

prepare() {
  cd cosmic-workspaces-epoch
  patch -Np1 -i ../cosmic-workspaces-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-workspaces-epoch
  git describe --tags | sed 's/^epoch-//; s/-alpha./alpha/'
}

build() {
  cd cosmic-workspaces-epoch
  RUSTFLAGS+=' -C link-arg=-fuse-ld=lld'
  make ARGS+=' --frozen --release'
}

package() {
  cd cosmic-workspaces-epoch
  make prefix=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
