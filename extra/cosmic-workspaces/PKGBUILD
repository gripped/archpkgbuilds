# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-workspaces
pkgver=1.0.0alpha5
pkgrel=1
epoch=1
pkgdesc='Cosmic workspaces'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-workspaces-epoch
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libinput
  libxkbcommon
  mesa
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=e0c0f27f6722e2a91084594c4c815f6af3de27c8
source=(
  git+https://github.com/pop-os/cosmic-workspaces-epoch.git#tag=${_tag}
  cosmic-workspaces-lto.patch
)
b2sums=('a03e43647d5cd888257acec77a113300e442b680870bdbfc47030d80d62f1060e008510878ff44bc1cc8c3d119efc4cb88e86046278b2c48b06706783f330c3d'
        'e8581fbd229f4cec60e0a03986c5adc6273a704ee44129dfc01731aa0c4f2d800b2971103e005f9d5fe853facae8db2e707d27af0e7a1d3577962aa1eb80d2b0')

prepare() {
  cd cosmic-workspaces-epoch
  patch -Np1 -i ../cosmic-workspaces-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-workspaces-epoch
  git describe --tags | sed 's/^epoch-//; s/-alpha./alpha/'
}

build() {
  cd cosmic-workspaces-epoch
  RUSTFLAGS+=' -C link-arg=-fuse-ld=lld'
  make ARGS+=' --frozen --release'
}

package() {
  cd cosmic-workspaces-epoch
  make prefix=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
