# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-workspaces
pkgver=1.0.0alpha4
pkgrel=1
epoch=1
pkgdesc='Cosmic workspaces'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-workspaces-epoch
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libinput
  libxkbcommon
  mesa
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=70d6c415a1cd931f575c3e18274c3fecc7cc9a03
source=(
  git+https://github.com/pop-os/cosmic-workspaces-epoch.git#tag=${_tag}
  cosmic-workspaces-lto.patch
)
b2sums=('06c1eeb25d12287c0c9865543a5bf8bf1fd01d338edea07b2d20ef3783f77ffb43fcfd1550786f2703ffdcdd833a60c39f8beaa4dc59792ca4d825e6b5ac0b88'
        'e8581fbd229f4cec60e0a03986c5adc6273a704ee44129dfc01731aa0c4f2d800b2971103e005f9d5fe853facae8db2e707d27af0e7a1d3577962aa1eb80d2b0')

prepare() {
  cd cosmic-workspaces-epoch
  patch -Np1 -i ../cosmic-workspaces-lto.patch
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-workspaces-epoch
#  git describe --tags | sed 's/^epoch-//; s/-alpha./alpha/'
#}

build() {
  cd cosmic-workspaces-epoch
  RUSTFLAGS+=' -C link-arg=-fuse-ld=lld'
  make ARGS+=' --frozen --release'
}

package() {
  cd cosmic-workspaces-epoch
  make prefix=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
