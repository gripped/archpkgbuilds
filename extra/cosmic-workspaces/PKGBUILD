# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-workspaces
pkgver=1.0.0alpha5.1
pkgrel=1
epoch=1
pkgdesc='Cosmic workspaces'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-workspaces-epoch
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libinput
  libxkbcommon
  mesa
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=8c4e66691c7a7ef3ca61157bb2dad6ec193446e2
source=(
  git+https://github.com/pop-os/cosmic-workspaces-epoch.git#tag=${_tag}
  cosmic-workspaces-lto.patch
)
b2sums=('bc193a9fb9394e429654430304dc38c59c96eb4b3e206c17503dcf447a4dabbd64c3e45959f9d2de7142ed936531669b9b11cc58ee17fad9cf339263d492fd64'
        '82495774acf609583991c1cc13376fdfd81107d85030e9fb484cd50336b3f6418b59f5322d85cef83a3d75af42414ea86843bacae39286a643e2a668b15f3048')

prepare() {
  cd cosmic-workspaces-epoch
  patch -Np1 -i ../cosmic-workspaces-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-workspaces-epoch
  git describe --tags | sed 's/^epoch-//; s/-alpha./alpha/'
}

build() {
  cd cosmic-workspaces-epoch
  RUSTFLAGS+=' -C link-arg=-fuse-ld=lld'
  make ARGS+=' --frozen --release'
}

package() {
  cd cosmic-workspaces-epoch
  make prefix=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
