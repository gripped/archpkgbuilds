# Maintainer:  Robin Candau <antiz@archlinux.org>
# Contributor:  twa022 <twa022 at gmail dot com>
# Contributor: Thanos Apostolou <thanosapostolou@outlook.com>
# Contributor: Cedric Leporcq <cedric at gmail dot com>

pkgname=xfce4-windowck-plugin
pkgver=0.5.2
pkgrel=1
epoch=1
pkgdesc="Xfce panel plugin for displaying window title and buttons"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-windowck-plugin/start"
license=('GPL-3.0-only')
depends=('xfce4-panel' 'libwnck3')
makedepends=('git' 'python' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-windowck-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('620093215e34d58d6d0253827957c091d84ac2e34a981835aaaa5e8b8801dce5')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
