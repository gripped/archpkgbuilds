# Maintainer:  Robin Candau <antiz@archlinux.org>
# Contributor:  twa022 <twa022 at gmail dot com>
# Contributor: Thanos Apostolou <thanosapostolou@outlook.com>
# Contributor: Cedric Leporcq <cedric at gmail dot com>

pkgname=xfce4-windowck-plugin
pkgver=0.6.1
pkgrel=1
epoch=1
pkgdesc="Xfce panel plugin for displaying window title and buttons"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-windowck-plugin/start"
license=('GPL-3.0-only')
depends=('xfce4-panel' 'libwnck3')
makedepends=('git' 'meson' 'python' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-windowck-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('20c553e128b3cbf269bbad3463c48897edfe65f2f5486685d416ed66acc979f0')

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
