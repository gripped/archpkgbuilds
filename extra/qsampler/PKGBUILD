# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=1.0.1
pkgrel=3
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=(x86_64)
url="https://qsampler.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  linuxsampler
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  libgig
  liblscp
  qt6-tools
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('4c5a053b1208a4c53535dbe81de8f4c9497074d3ed1b2485e916e737fe5719e8ac93508c1827fa973dd9953edf7fba5a75f1de9c27b6174cee99251ab506a150')
b2sums=('87bfc25fb23795c512eb12ec1c87ab30e5e873a388ca3a11aa592e578513812107dbb08b5b736530cc2afacbcdd1ec3d55f80f8dce96fb917d732486aba3641c')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    libgig libgig.so
    liblscp liblscp.so
  )
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
