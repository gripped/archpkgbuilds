# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Iyán Méndez Veiga <me (at) iyanmv (dot) com>
# Contributor: Dmytro Aleksandrov <alkersan@gmail.com>

pkgname=rpi-imager
pkgver=1.9.0
pkgrel=2
pkgdesc="Raspberry Pi Imaging Utility"
depends=(
    'curl'
    'gcc-libs'
    'glibc'
    'gnutls'
    'hicolor-icon-theme'
    'libarchive'
    'qt6-base'
    'qt6-declarative'
    'xz'
)
optdepends=(
    'dosfstools: SD card bootloader support'
    'udisks2: Needed if you want to be able to run rpi-imager as a regular user'
)
makedepends=(
    'cmake'
    'qt6-tools'
    'qt6-svg'
)
arch=('x86_64')
url="https://github.com/raspberrypi/rpi-imager"
license=("Apache-2.0")

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/raspberrypi/${pkgname}/archive/v${pkgver}.tar.gz"
        "$pkgname-1_9_0-remove-vendoring.patch::https://github.com/raspberrypi/rpi-imager/commit/0bd585fa0b54a2b85cea6a6768c7e1d953b24334.patch")

b2sums=('d4eae0e1f1d99901938d04d1fcd6cb3dac84296172791f215247aa6873284e4b8d6673e0e847ad9dd41167cea87122a154a440a91bf8546a4a1d0442a02cb494'
        'e02c86d35a2854bd4ab8bd4de938cd1771b6e0e38a494be0d8886c1158627eb4c182309fff7c67ea457c8ab060279d7c57a95f08b52dab976f751163efe0267c')

prepare() {
    # https://github.com/raspberrypi/rpi-imager/issues/924
    patch -Np1 -d "${pkgname}-${pkgver}" -i "$srcdir/$pkgname-1_9_0-remove-vendoring.patch"
}

build() {
    cmake -B build -S "${pkgname}-${pkgver}/src" \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DENABLE_CHECK_VERSION=OFF \
        -DENABLE_VENDORING=OFF
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
