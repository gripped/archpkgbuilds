# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Christopher O'Neill <milkytracker@chrisoneill.co.uk>
# Contributor: Maks Verver <maksverver@geocities.com>

pkgname=milkytracker
pkgver=1.05.01
pkgrel=1
pkgdesc='Music tracker inspired by Fast Tracker II'
arch=(x86_64)
url='https://milkytracker.titandemo.org/'
license=(GPL-3.0-or-later)
depends=(alsa-lib gcc-libs glibc lhasa rtmidi sdl2 zlib zziplib)
makedepends=(cmake jack)
optdepends=('jack: JACK audio support')
options=(!strip)
source=("$pkgname-$pkgver.tar.gz::https://github.com/milkytracker/MilkyTracker/archive/v$pkgver.tar.gz")
b2sums=('447d0fd939d6def41223f4d65afedbf4f98e49dde77d0f94e959d0444f3c133a376613c8dfcc0b8b76184fa1db4a1317c04078512bbc9984149fd6267ef8641f')

prepare() {
  # Remove explicit use of C++98
  sed -e '/CMAKE_CXX_STANDARD/d' -e '/CMAKE_CXX_EXTENSIONS/d' -i MilkyTracker-$pkgver/CMakeLists.txt
}

build() {
  cmake \
    -B build \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -S MilkyTracker-$pkgver \
    -W no-dev
  make -C build
}

package() {
  depends+=(libasound.so librtmidi.so)

  DESTDIR="$pkgdir" make install -C build

  cd MilkyTracker-$pkgver/resources
  install -Dm644 pictures/carton.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -Dm644 org.milkytracker.MilkyTracker.metainfo.xml -t "$pkgdir/usr/share/metainfo"
}
