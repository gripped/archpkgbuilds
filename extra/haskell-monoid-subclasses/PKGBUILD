# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=monoid-subclasses
pkgname=haskell-monoid-subclasses
pkgver=1.2.6.1
pkgrel=1
pkgdesc="Subclasses of Monoid"
url="https://github.com/blamario/monoid-subclasses/"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-commutative-semigroups' 'haskell-primes' 'haskell-vector')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7a64e3d7d49e1b051f45cf971a362de58ac2a2aa97ab2fc683d7aa172e57e8ae2e3be2f59664bae42e299202adb9018576016eb79e6427bb864e854d655e854c')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "BSD3-LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/BSD3-LICENSE.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/BSD3-LICENSE.txt"
}
