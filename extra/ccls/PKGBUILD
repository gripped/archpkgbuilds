# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Fangrui Song <i at maskray.me>

pkgname=ccls
pkgver=0.20250815.1
pkgrel=1
pkgdesc='C/C++/ObjC language server supporting cross references, hierarchies, completion and semantic highlighting'
arch=('x86_64')
url='https://github.com/MaskRay/ccls'
license=('Apache-2.0')
depends=(
  'clang'
  'gcc-libs'
  'glibc'
  'llvm-libs'
)
makedepends=(
  'cmake'
  'llvm'
  'ninja'
  'rapidjson'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2f0bdd62260c65881af55128f7e5008fba8910844173477daf9122f5408f21ac5d297eb0497ea87f1591124b96b9416388bd67894581cbe6bf9469f70d81ffb8')

build() {
  local cmake_options=(
    -B build -S "$pkgname-$pkgver"
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -W no-dev
    -G Ninja
    -D CMAKE_CXX_COMPILER=clang++
    -D CLANG_LINK_CLANG_DYLIB=ON
    -D CCLS_VERSION="$pkgver"
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
