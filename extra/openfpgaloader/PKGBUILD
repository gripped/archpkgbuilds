# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=openfpgaloader
pkgver=1.0.0
pkgrel=1
pkgdesc='Universal utility for programming FPGA'
arch=(x86_64)
url='https://github.com/trabucayre/openFPGALoader'
license=(Apache-2.0)
depends=(
  libftdi
  libftdi-compat
  libhidapi-hidraw.so
  libhidapi-libusb.so
  libudev.so
  libusb
  zlib
  glibc
  gcc-libs
)
makedepends=(
  git
  cmake
  systemd
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('b460819e0f10a659bc0e356cf5155e278207c7508c783b8ee42fb90ddfa7e72f2aa1aac16d25af3c5811bd2c2a029fd40bca15e8d2eb093bb26eafe5e1e5995f')
b2sums=('75b091345607115faa289a6b76aef25fd05a85866f348fedf161c673f348e1f98dc978963443e61572d7fab343b4820171991582f885bcd46a9b09b9e86bdcda')

prepare() {
  cd "$pkgname"

  sed -i 's|MODE="664", GROUP="plugdev"|MODE="666"|g' 99-openfpgaloader.rules
}

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D ENABLE_UDEV=ON
    -D ENABLE_CMSISDAP=ON
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/lib/udev/rules.d" "$pkgname/99-openfpgaloader.rules"
}

