# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.7.3
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('eca8fa2358d18f087c9f7e0c04e75b199531d4337787f2d05c786190b38ee64b')
b2sums=('e879432e25e24e2babb5db40e515fdd63da3abb1087060bdec883ad7870572ae860a327a7cade038652d66d30b0242f6710e0d0fe965c4f0267f42cdbeb0eaf3')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter

  for i in bash fish zsh; do
    ./kube-linter completion ${i} > ./kube-linter.${i}
  done
}

check() {
  cd ${pkgname}-${pkgver}
  ./kube-linter --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 kube-linter -t "${pkgdir}/usr/bin"
  install -Dm644 kube-linter.bash "${pkgdir}"/usr/share/bash-completion/completions/kube-linter
  install -Dm644 kube-linter.fish "${pkgdir}"/usr/share/fish/completions/kube-linter.fish
  install -Dm644 kube-linter.zsh "${pkgdir}"/usr/share/zsh/site-functions/_kube-linter
}

# vim: ts=2 sw=2 et:
