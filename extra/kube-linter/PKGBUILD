# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.6.6
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/stackrox/kube-linter/commit/8ad0795fa1d52b26209355e81b97898c2f762ab9.patch)
sha256sums=('02f49d8f615e8549a8bc690d9aac93fb4fde3deb4d0ad803c654ff19d70736c8'
            'ff1dc9c809248bada349e208af6ebce2a1a5186f37b4289b1a137f46fc38e680')
b2sums=('145ce6a2119282c58e6b4fed925602956c4ad17af6f881444c056dfa9384750cec7873d3a5af81b551e71cee9b1299dd08ce626a854a9225caf26391a0a79c2d'
        '28bfcabcb0a52d1ca49453075d567d1311edc10932a29dedf61db35eb5ee28168a9df69cca218460745fcf9931dd45917e9de7ce6c02f787bd182ea9a17c02d2')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../8ad0795fa1d52b26209355e81b97898c2f762ab9.patch
}

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter

  for i in bash fish zsh; do
    ./kube-linter completion ${i} > ./kube-linter.${i}
  done
}

check() {
  cd ${pkgname}-${pkgver}
  ./kube-linter --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 kube-linter -t "${pkgdir}/usr/bin"
  install -Dm644 kube-linter.bash "${pkgdir}"/usr/share/bash-completion/completions/kube-linter
  install -Dm644 kube-linter.fish "${pkgdir}"/usr/share/fish/completions/kube-linter.fish
  install -Dm644 kube-linter.zsh "${pkgdir}"/usr/share/zsh/site-functions/_kube-linter
}

# vim: ts=2 sw=2 et:
