# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.8.1
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f761520494e514966ef95ecd085f55d6a479a6a746e2b553ac9776fca983cd0a')
b2sums=('80da14fe91e7b93190719aebcdd66ceade17588fcc9bc23d02a48db12376bfd09f561c369f5604c69d97c11742138a7a17ca01765b7894647956b5df485af667')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter

  for i in bash fish zsh; do
    ./kube-linter completion ${i} > ./kube-linter.${i}
  done
}

check() {
  cd ${pkgname}-${pkgver}
  ./kube-linter --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 kube-linter -t "${pkgdir}/usr/bin"
  install -Dm644 kube-linter.bash "${pkgdir}"/usr/share/bash-completion/completions/kube-linter
  install -Dm644 kube-linter.fish "${pkgdir}"/usr/share/fish/vendor_completions.d/kube-linter.fish
  install -Dm644 kube-linter.zsh "${pkgdir}"/usr/share/zsh/site-functions/_kube-linter
}

# vim: ts=2 sw=2 et:
