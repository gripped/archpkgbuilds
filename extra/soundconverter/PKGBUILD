# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: sezanzeb for the 4.0.0 PKGBUILD.
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=soundconverter
pkgver=4.1.3
pkgrel=1
pkgdesc='A simple sound converter application for GNOME'
arch=(any)
license=(GPL-3.0-only)
url=https://soundconverter.org
_url="https://github.com/kassoulet/$pkgname"
depends=(desktop-file-utils
         gst-plugins-good
         gst-plugins-ugly
         gst-python
         python-gobject
         python-setuptools) # https://bugs.archlinux.org/task/71201
makedepends=(python-{build,installer,wheel}
             python-distutils-extra)
#depends=(gst-plugins-base gst-plugins-good gtk3 libnotify python-gobject)
#makedepends=(intltool python-distutils-extra)
_archive="$pkgname-$pkgver"
source=("$_url/archive/refs/tags/$pkgver/$_archive.tar.gz")
sha256sums=('2ab5c870909cf1306ebe0a35786c1261680a27dc997862399f759bca9bd32743')

build() {
  cd "$_archive"
  python -m build -wn
}

package() {
  cd "$_archive"
  python -m installer -d "$pkgdir" dist/*.whl
  # pacman will compile schemas via a hook once placed into /usr,
  # don't let the setup.py script do it.
  rm "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"
}
