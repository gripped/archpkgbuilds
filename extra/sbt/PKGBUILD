# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=sbt
epoch=1
pkgver=1.12.0
pkgrel=1
pkgdesc='The interactive build tool'
arch=('any')
url='https://www.scala-sbt.org/'
license=('Apache-2.0')
_jdkver=8
depends=('java-runtime-headless' 'sh')
makedepends=('git' "jdk${_jdkver}-openjdk")
optdepends=('rsync: offline installation support')
install=sbt.install
options=(!debug)
source=(git+https://github.com/sbt/sbt.git#tag=v${pkgver})
validpgpkeys=('2EE0EA64E40A89B84B2DF73499E82A75642AC823') # sbt build tool
sha256sums=('2c3cdd0b01e6f287afee039c69b2afbbb5b45c3025e558115a54bcdf5ea1f2f9')
b2sums=('145913328068f384b2cc7fc858057f4239e9e680854c03df20406dc08349e636e286fb66ca12249b1d1452958b645b3d71a88d82c94310d6c263be5815f88129')
backup=(etc/${pkgname}/sbtopts)

build() {
  cd "${pkgname}/launcher-package"
  export JAVA_HOME="/usr/lib/jvm/java-$_jdkver-openjdk"
  export PATH="/usr/lib/jvm/java-$_jdkver-openjdk/bin:$PATH"
  # https://github.com/sbt/sbt/issues/8333
  ../sbt \
    -Dsbt.build.version=${pkgver} \
    -Dsbt.build.offline=false \
    clean \
    universal:packageBin \
    universal:packageZipTarball
  tar -xf target/universal/sbt.tgz -C "${srcdir}"
}

check() {
  cd "${pkgname}"
  export JAVA_HOME="/usr/lib/jvm/java-$_jdkver-openjdk"
  export PATH="/usr/lib/jvm/java-$_jdkver-openjdk/bin:$PATH"
  ./sbt test
}

package() {
  cd "${pkgname}"

  mkdir -p "${pkgdir}"/usr/share/${pkgname}
  cp -r bin "${pkgdir}"/usr/share/${pkgname}
  rm "${pkgdir}"/usr/share/${pkgname}/bin/*{.bat,.exe,-darwin}
  chmod -x "${pkgdir}"/usr/share/${pkgname}/bin/*
  chmod +x "${pkgdir}"/usr/share/${pkgname}/bin/{sbt,sbtn-${CARCH}-pc-linux}
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /usr/share/${pkgname}/bin/sbt "${pkgdir}"/usr/bin/sbt

  mkdir -p "${pkgdir}/etc"
  cp -r conf "${pkgdir}"/etc/${pkgname}
  rm "${pkgdir}"/etc/${pkgname}/sbtconfig.txt
  ln -s /etc/${pkgname} "${pkgdir}"/usr/share/${pkgname}/conf

  install -D LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
