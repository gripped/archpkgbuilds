# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=sbt
epoch=1
pkgver=1.12.2
pkgrel=1
pkgdesc='The interactive build tool'
arch=('any')
url='https://www.scala-sbt.org/'
license=('Apache-2.0')
_jdkver=8
depends=('java-runtime-headless' 'sh')
makedepends=('git' "jdk${_jdkver}-openjdk")
optdepends=('rsync: offline installation support')
install=sbt.install
options=(!debug)
source=(git+https://github.com/sbt/sbt.git#tag=v${pkgver})
validpgpkeys=('2EE0EA64E40A89B84B2DF73499E82A75642AC823') # sbt build tool
sha256sums=('6b8b579d51e0c0e1532a287464b3e3f658d4aa6344920ac5d0b7f8f23de715f9')
b2sums=('f3a58d439fb346ab2bbf8e113cdad183659dc6efbed0d87930ca0807325c4239a4f1d3818dc00069c4524f5365e52e54e58588ce23792d9a41bda0a713b2fcd3')
backup=(etc/${pkgname}/sbtopts)

build() {
  cd "${pkgname}"
  export JAVA_HOME="/usr/lib/jvm/java-$_jdkver-openjdk"
  export PATH="/usr/lib/jvm/java-$_jdkver-openjdk/bin:$PATH"
  # https://github.com/sbt/sbt/issues/8333
  ./sbt \
    -Dsbt.build.version=${pkgver} \
    -Dsbt.build.offline=false \
    clean \
    launcherPackage/Universal/packageBin \
    launcherPackage/Universal/packageZipTarball
  tar -xf launcher-package/target/universal/sbt.tgz -C "${srcdir}"
}

check() {
  cd "${pkgname}"
  export JAVA_HOME="/usr/lib/jvm/java-$_jdkver-openjdk"
  export PATH="/usr/lib/jvm/java-$_jdkver-openjdk/bin:$PATH"
  ./sbt test
}

package() {
  cd "${pkgname}"

  mkdir -p "${pkgdir}"/usr/share/${pkgname}
  cp -r bin "${pkgdir}"/usr/share/${pkgname}
  rm "${pkgdir}"/usr/share/${pkgname}/bin/*{.bat,.exe,-darwin}
  chmod -x "${pkgdir}"/usr/share/${pkgname}/bin/*
  chmod +x "${pkgdir}"/usr/share/${pkgname}/bin/{sbt,sbtn-${CARCH}-pc-linux}
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /usr/share/${pkgname}/bin/sbt "${pkgdir}"/usr/bin/sbt

  mkdir -p "${pkgdir}/etc"
  cp -r conf "${pkgdir}"/etc/${pkgname}
  rm "${pkgdir}"/etc/${pkgname}/sbtconfig.txt
  ln -s /etc/${pkgname} "${pkgdir}"/usr/share/${pkgname}/conf

  install -D LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
