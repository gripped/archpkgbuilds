# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=sbt
epoch=1
pkgver=1.11.2
pkgrel=1
pkgdesc='The interactive build tool'
arch=('any')
url='https://www.scala-sbt.org/'
license=('Apache-2.0')
_jdkver=8
depends=('java-runtime-headless' 'sh')
makedepends=('sbt' 'git' "jdk${_jdkver}-openjdk")
optdepends=('rsync: offline installation support')
install=sbt.install
options=(!debug)
source=(git+https://github.com/sbt/sbt.git#tag=v${pkgver}
        0001-fix-semanticdb-expects-Wildcard-imports-to-be-in-the.patch)
validpgpkeys=('2EE0EA64E40A89B84B2DF73499E82A75642AC823') # sbt build tool
sha256sums=('cddfb55e0f63392511e2f3f557acf5a93f49f2f419693b6216e18a9b7bd6a407'
            '0538aef7be7f86cf12166be428ef2fe62b9cfe9ce09f2cf8ff2dbb2469df7099')
b2sums=('a04d7d312385962094f553d99035c31cff89b885498975afc9083b5422888e4a270656fa313ae872cae212a6580d5aae76ce8299f7ab05a0dc63789f46c06d89'
        '9aab56fb69cd92bf0c94e26d02189c3b8a1fc3e2a4ccb5aa61dbfe4dbfd084e916472ff1b4e7621d893d92baa60d638622a6f0902352f0159aea508ff08b14db')
backup=(etc/${pkgname}/sbtopts)

prepare() {
  cd "${pkgname}"
  patch -Np1 -i ../0001-fix-semanticdb-expects-Wildcard-imports-to-be-in-the.patch
}

build() {
  cd "${pkgname}/launcher-package"
  export JAVA_HOME="/usr/lib/jvm/java-$_jdkver-openjdk"
  export PATH="/usr/lib/jvm/java-$_jdkver-openjdk/bin:$PATH"
  sbt \
    -Dsbt.build.version=${pkgver} \
    -Dsbt.build.offline=false \
    clean \
    universal:packageBin \
    universal:packageZipTarball
  tar -xf target/universal/sbt.tgz -C "${srcdir}"
}

check() {
  cd "${pkgname}"
  export JAVA_HOME="/usr/lib/jvm/java-$_jdkver-openjdk"
  export PATH="/usr/lib/jvm/java-$_jdkver-openjdk/bin:$PATH"
  sbt test
}

package() {
  cd "${pkgname}"

  mkdir -p "${pkgdir}"/usr/share/${pkgname}
  cp -r bin "${pkgdir}"/usr/share/${pkgname}
  rm "${pkgdir}"/usr/share/${pkgname}/bin/*{.bat,.exe,-darwin}
  chmod -x "${pkgdir}"/usr/share/${pkgname}/bin/*
  chmod +x "${pkgdir}"/usr/share/${pkgname}/bin/{sbt,sbtn-${CARCH}-pc-linux}
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /usr/share/${pkgname}/bin/sbt "${pkgdir}"/usr/bin/sbt

  mkdir -p "${pkgdir}/etc"
  cp -r conf "${pkgdir}"/etc/${pkgname}
  rm "${pkgdir}"/etc/${pkgname}/sbtconfig.txt
  ln -s /etc/${pkgname} "${pkgdir}"/usr/share/${pkgname}/conf

  install -D LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
