# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=taffybar
pkgver=4.1.0
pkgrel=1
pkgdesc="A desktop bar similar to xmobar, but with more GUI"
url="https://github.com/taffybar/taffybar"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hstringtemplate' 'haskell-quickcheck' 'haskell-x11' 'haskell-aeson'
         'haskell-ansi-terminal' 'haskell-attoparsec' 'haskell-conduit' 'haskell-data-default'
         'haskell-dbus' 'haskell-dbus-hslogger' 'haskell-dyre' 'haskell-either'
         'haskell-enclosed-exceptions' 'haskell-extra' 'haskell-fsnotify'
         'haskell-gi-cairo-connector' 'haskell-gi-cairo-render' 'haskell-gi-gdk3'
         'haskell-gi-gdkpixbuf' 'haskell-gi-gdk3x11' 'haskell-gi-glib' 'haskell-gi-gtk3'
         'haskell-gi-gtk-hs' 'haskell-gi-pango' 'haskell-gtk-sni-tray' 'haskell-gtk-strut'
         'haskell-gi-base' 'haskell-hslogger' 'haskell-hspec' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-http-conduit' 'haskell-http-types' 'haskell-multimap'
         'haskell-optparse-applicative' 'haskell-rate-limit' 'haskell-regex-compat' 'haskell-safe'
         'haskell-scotty' 'haskell-split' 'haskell-status-notifier-item'
         'haskell-time-locale-compat' 'haskell-time-units' 'haskell-tuple' 'haskell-typed-process'
         'haskell-unliftio' 'haskell-unliftio-core' 'haskell-utf8-string' 'haskell-xdg-basedir'
         'haskell-xdg-desktop-entry' 'haskell-xml' 'haskell-xml-helpers' 'xmonad' 'gtk3')
makedepends=('ghc' 'uusi' 'haskell-hspec-core' 'haskell-hspec-discover' 'haskell-hspec-golden')
checkdepends=('python-dbusmock' 'upower' 'xorg-server' 'xorg-server-xvfb' 'xorg-xkbcomp'
              'xorg-xprop' 'xorg-xrandr' 'xterm')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('ce6e5e3d57c531fcef346fe05e9bd9a4b5c60c8b2e920c569a3863b235343bc5')

prepare() {
  cd $pkgname-$pkgver
  gen-setup

  # TODO: make a package for this?
  # Borrowed from nixpkgs:
  # https://github.com/NixOS/nixpkgs/blob/d0c58eb689cc6e610d32730d0f4ec2c484c31ec1/pkgs/by-name/xd/xdummy/package.nix
  cat > dummy-xorg.conf <<EOF
    Section "ServerLayout"
      Identifier     "dummy_layout"
      Screen         0 "dummy_screen"
      InputDevice    "dummy_keyboard" "CoreKeyboard"
      InputDevice    "dummy_mouse" "CorePointer"
    EndSection

    Section "ServerFlags"
      Option "DontVTSwitch" "true"
      Option "AllowMouseOpenFail" "true"
      Option "PciForceNone" "true"
      Option "AutoEnableDevices" "false"
      Option "AutoAddDevices" "false"
    EndSection

    Section "Files"
      ModulePath "/usr/lib/xorg/modules"
      ModulePath "/usr/lib/xorg/modules"
      XkbDir "/usr/share/X11/xkb"
      FontPath "/usr/share/fonts/X11/75dpi"
      FontPath "/usr/share/fonts/X11/100dpi"
      FontPath "/usr/share/fonts/X11/misc"
      FontPath "/usr/share/fonts/X11/misc"
      FontPath "/usr/share/fonts/X11/75dpi"
      FontPath "/usr/share/fonts/X11/100dpi"
      FontPath "/usr/share/fonts/X11/100dpi"
    ''}
    EndSection

    Section "Module"
      Load           "dbe"
      Load           "extmod"
      Load           "freetype"
      Load           "glx"
    EndSection

    Section "InputDevice"
      Identifier     "dummy_mouse"
      Driver         "void"
    EndSection

    Section "InputDevice"
      Identifier     "dummy_keyboard"
      Driver         "void"
    EndSection

    Section "Monitor"
      Identifier     "dummy_monitor"
      HorizSync       30.0 - 130.0
      VertRefresh     50.0 - 250.0
      Option         "DPMS"
    EndSection

    Section "Device"
      Identifier     "dummy_device"
      Driver         "dummy"
      VideoRam       192000
    EndSection

    Section "Screen"
      Identifier     "dummy_screen"
      Device         "dummy_device"
      Monitor        "dummy_monitor"
      DefaultDepth    24
      SubSection     "Display"
        Depth       24
        Modes      "1280x1024"
      EndSubSection
    EndSection
EOF
  cat > xdummy <<EOF
  #!/bin/sh
  exec /usr/bin/Xorg \
    -noreset \
    -logfile /dev/null \
    "\$@" \
    -config "$PWD/dummy-xorg.conf"
EOF
  chmod +x xdummy
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  # TODO: xdummy tests are still failing
  PATH="$srcdir/$pkgname-$pkgver:$PATH" runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
