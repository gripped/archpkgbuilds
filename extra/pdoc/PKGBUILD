# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pdoc
pkgver=15.0.4
pkgrel=1
pkgdesc="API Documentation for Python Projects"
arch=(any)
url="https://github.com/mitmproxy/pdoc/"
license=(
  MIT-0
  MIT  # upstream vendors some older version of python-markdown2: https://github.com/mitmproxy/pdoc/issues/815
)
depends=(
  python
  python-jinja
  python-markupsafe
  python-pygments
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-pytest
  python-pytest-timeout
  # python-pdoc-pyo3-sample-library  # waiting on upstream to tag releases: https://github.com/mitmproxy/pdoc-pyo3-sample-library/issues/1
)
provides=(python-pdoc)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('9aefb1c24be475f2bfb791d09fae566bea24d4d1541db8b45f2960aefe0f57dddc89042bfaea82186888419b9d67df6b1d4ad1e8b1757b82834d2042b3418869')
b2sums=('89bf6c6fc510d362d0742da02349320c09c0b6d64f9e11078f6f5449a7407f98be14dcb3b5e476bf34a47acfe94d347e1b21cb45b00311c123a5289ce71aec56')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    # deselect PyO3 tests until upstream tags releases for pdoc-pyo3-sample-library: https://github.com/mitmproxy/pdoc-pyo3-sample-library/issues/1
    --deselect 'test/test_snapshot.py::test_snapshots[repr-pyo3_sample_library]'
    --deselect 'test/test_snapshot.py::test_snapshots[repr-enums]'
    --deselect 'test/test_snapshot.py::test_snapshots[html-pyo3_sample_library]'
    --deselect 'test/test_snapshot.py::test_snapshots[html-demopackage_dir]'
    --deselect 'test/test_extract.py::test_walk_spec'
    --deselect 'test/test_extract.py::test_parse_spec'
    -vv
  )

  cd $pkgname-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname/markdown2/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE.markdown2"
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
