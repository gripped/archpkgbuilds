# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=watchexec
pkgver=2.4.3
pkgrel=1
pkgdesc="Executes commands in response to file modifications"
url='https://github.com/watchexec/watchexec'
depends=('libgcc' 'glibc')
makedepends=('cargo')
arch=('x86_64')
license=('Apache-2.0')
source=("$pkgname-$pkgver.tar.gz::https://github.com/watchexec/watchexec/archive/refs/tags/v${pkgver}.tar.gz")
options=('!lto')
sha256sums=('080b6ef6d1c5b030def96ac8c4e4d3fec85091b320088144d345331d3e51eeec')

prepare() {
  cd "watchexec-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "watchexec-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "watchexec-$pkgver"
  cargo test --frozen -- --skip scopes
}

package() {
  cd "watchexec-$pkgver"
  install -Dm644 doc/watchexec.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -Dm644 completions/bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 completions/zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm644 completions/fish "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 completions/elvish "$pkgdir/usr/share/elvish/lib/$pkgname.elv"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
