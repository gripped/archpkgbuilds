# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=watchexec
pkgver=1.24.1
pkgrel=1
pkgdesc="Executes commands in response to file modifications"
url='https://github.com/watchexec/watchexec'
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
arch=('x86_64')
license=('Apache')
source=("$pkgname-$pkgver.tar.gz::https://github.com/watchexec/watchexec/archive/refs/tags/v${pkgver}.tar.gz")
options=('!lto')
sha256sums=('9afc736fd4c0f895c89b7d6b1bbbb831fdb255400f785dcd3a414f62a5db6bd5')

build() {
  cd "watchexec-$pkgver"
  cargo build --release --locked
}

check() {
  cd "watchexec-$pkgver"
  cargo test --locked -- --skip scopes
}

package() {
  cd "watchexec-$pkgver"
  install -Dm644 "doc/watchexec.1" "$pkgdir/usr/share/man/man1/watchexec.1"
  gzip -9 "$pkgdir/usr/share/man/man1/watchexec.1"
  install -Dm644 "completions/bash" "$pkgdir/usr/share/bash-completion/completions/watchexec"
  install -Dm644 "completions/zsh" "$pkgdir/usr/share/zsh/site-functions/_watchexec"
  install -Dm644 "completions/fish" "$pkgdir/usr/share/fish/vendor_completions.d/watchexec.fish"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
