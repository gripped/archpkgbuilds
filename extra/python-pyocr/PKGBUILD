# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-pyocr
pkgver=0.8.5
pkgrel=4
pkgdesc='Python wrapper for Tesseract'
arch=(any)
url='https://gitlab.gnome.org/World/OpenPaperwork/pyocr'
license=(GPL-3.0-or-later)
depends=(
  python
  python-pillow
  tesseract
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
checkdepends=(python-pytest)
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/pyocr.git#tag=$pkgver")
b2sums=(8fe2c3d1f6c86787d91cc6431640e70c93640dd582e6e8263cdec9c10e21aa20328a4c2858de8d31ba855b080aceb668138ae09f9d551003c73ca5e2ec79a3fd)

build() {
  cd pyocr
  python -m build --wheel --no-isolation
}

check() {
  cd pyocr
  PYTHONPATH="$PWD/build/lib" \
    pytest
}

package() {
  cd pyocr
  python -m installer --destdir="$pkgdir" dist/*.whl
}
