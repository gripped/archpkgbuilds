# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-pyocr
pkgver=0.8.5
pkgrel=3
pkgdesc='Python wrapper for Tesseract'
arch=(any)
url='https://gitlab.gnome.org/World/OpenPaperwork/pyocr'
license=(GPL-3.0-or-later)
depends=(
  python
  python-pillow
  tesseract
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
checkdepends=(python-pytest)
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/pyocr.git#tag=$pkgver")
b2sums=(77888c4d3dbedede42f87562c52e651c77f56b6491073ba4d9ae6b2bf327a5cd3ea2903b5043c2398a8ebd6affc2ddb1db348f6435e1253089955091abb76d5d)

build() {
  cd pyocr
  python -m build --wheel --no-isolation
}

check() {
  cd pyocr
  PYTHONPATH="$PWD/build/lib" \
    pytest
}

package() {
  cd pyocr
  python -m installer --destdir="$pkgdir" dist/*.whl
}
