# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=calligra
pkgdesc='A set of applications for productivity and creative usage'
pkgver=4.0.1
pkgrel=2
arch=(x86_64)
url='https://www.calligra-suite.org/'
license=(GPL-2.0-only)
depends=(cauchy
         fontconfig
         freetype2
         gcc-libs
         glibc
         gsl
         imath
         karchive
         kcolorscheme
         kcompletion
         kconfigwidgets
         kcoreaddons
         kdbusaddons
         kdiagram
         kcmutils
         kconfig
         kcrash
         kguiaddons
         ki18n
         kiconthemes
         kio
         kitemviews
         knotifications
         knotifyconfig
         kservice
         ktextwidgets
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libodfgen
         librevenge
         openssl
         phonon-qt6
         qt6-base
         qt6-declarative
         qt6-svg
         qtkeychain-qt6
         sonnet
         zlib)
makedepends=(boost
             eigen
             extra-cmake-modules
             kdoctools
             libetonyek
             libvisio
             libwpg
             libwps
             okular
             pstoedit
             qt6-webengine
             vc)
optdepends=('libetonyek: Apple Keynote import filter'
            'libvisio: Microsoft Visio import filter'
            'libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'poppler: PDF to SVG filter'
            'pstoedit: EPS to SVG filter'
            'qt6-webengine: Braindump web shape')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d401f5e73f4f1bdc0b3548ea1a50b0ac1778c1ede30a6a335944ed7fbd88d95f'
            'SKIP')
validpgpkeys=(39FFA93CAE9C6AFC212AD00202325448204E452A) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DRELEASE_BUILD=ON \
    -DPRODUCTSET=DESKTOP \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
