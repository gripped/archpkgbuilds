# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-editor
pkgver=1.0.47
pkgrel=1
pkgdesc='Public library for deepin-image-viewer and deepin-album'
arch=('x86_64')
url="https://github.com/linuxdeepin/image-editor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg' 'libtiff' 'ffmpegthumbnailer'
         'libmediainfo' 'gcc-libs' 'deepin-util-dfm')
makedepends=('git' 'cmake' 'expac' 'ninja' 'qt5-tools')
source=("git+https://github.com/linuxdeepin/image-editor.git#tag=$pkgver"
        remove-broken-flags.patch)
sha512sums=('a03a1b7a16f2b56a146cba9ab91e9498a91dfd9a36de83b1ffbbaa7910aec7aa11be04ed5f19e0f3fe70122a7208aa2b70151b088423872040747227b4ddab3c'
            'bc72e7732ae045a10129336a770d04827c487fb57938c4711e57aeeccde369fca207913477a695cddffc7d0fb6d77add1f75b9b5ca784d0285b8f4f56c2376ad')

prepare() {
  cd image-editor
  patch -p2 < ../remove-broken-flags.patch
  sed -i -e 's/dpkg -s libdtkwidget-dev/expac %v dtkwidget/' -e '/COMMAND grep Version/d' libimageviewer/CMakeLists.txt
}

build() {
  cd image-editor
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd image-editor
  DESTDIR="$pkgdir" ninja install
}
