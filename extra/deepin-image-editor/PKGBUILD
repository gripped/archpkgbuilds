# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-editor
pkgver=1.0.36
pkgrel=1
pkgdesc='Public library for deepin-image-viewer and deepin-album'
arch=('x86_64')
url="https://github.com/linuxdeepin/image-editor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg' 'libtiff' 'ffmpegthumbnailer'
         'freeimage' 'libmediainfo' 'opencv' 'gcc-libs' 'deepin-util-dfm')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("https://github.com/linuxdeepin/image-editor/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        remove-broken-flags.patch)
sha512sums=('a77153eff04ff73ae5b560f6e68644683bf906aa22dee1f8c86dc0d957ce844e949d8ebd566b75efb1b7c68c0e0dc06a2911379f0d5efd6aa1c607936fe9ffbb'
            'bc72e7732ae045a10129336a770d04827c487fb57938c4711e57aeeccde369fca207913477a695cddffc7d0fb6d77add1f75b9b5ca784d0285b8f4f56c2376ad')

prepare() {
  patch -d image-editor-$pkgver -p2 < remove-broken-flags.patch
}

build() {
  cd image-editor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd image-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
