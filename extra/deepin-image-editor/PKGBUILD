# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-editor
pkgver=1.0.37
pkgrel=1
pkgdesc='Public library for deepin-image-viewer and deepin-album'
arch=('x86_64')
url="https://github.com/linuxdeepin/image-editor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg' 'libtiff' 'ffmpegthumbnailer'
         'freeimage' 'libmediainfo' 'opencv' 'gcc-libs' 'deepin-util-dfm')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("https://github.com/linuxdeepin/image-editor/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        remove-broken-flags.patch)
sha512sums=('91ddd6a70996bbd9d4756db26f59d9edc4e7af826e64f3208bd2f7ad7f61e0663a76d92e0fc54b1d428f3b58cd580710ca27b1813e293b21846da53ac73cc37a'
            'bc72e7732ae045a10129336a770d04827c487fb57938c4711e57aeeccde369fca207913477a695cddffc7d0fb6d77add1f75b9b5ca784d0285b8f4f56c2376ad')

prepare() {
  patch -d image-editor-$pkgver -p2 < remove-broken-flags.patch
}

build() {
  cd image-editor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd image-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
