# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-terra-pinyin
pkgver=0.0.0.20240625
_commit=9df66c7edc9474a945e9f4744419b156278d1580
pkgrel=1
pkgdesc="Terra pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-terra-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-terra-pinyin.git#commit=$_commit")
sha512sums=('bc2352b7b1e9d3dd1795717acf1512b57bea549aaf3f86f3303d3c17fd2a783b8f02ed722cf331658ce03e7388e41ce99d1036049feac2bcbbd55c56e3afcc03')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
