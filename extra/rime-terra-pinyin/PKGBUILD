# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-terra-pinyin
pkgver=0.0.0.20250722
_commit=9e7673360c5dc9d945e2ba3f167833bb9217ca6a
pkgrel=1
pkgdesc="Terra pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-terra-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-terra-pinyin.git#commit=$_commit")
sha512sums=('de55c2d983213b97f4549c0fc7d4a8e226c6c5150beb86344bdd179e2d237d5aad1eaf1154c8f71bbc34be6531419cf2a547bb01f9ecc5f086272e247bde9361')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
