# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=puzzles
pkgver=20251021.790f585
pkgrel=1
_commit=${pkgver##*.}
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=(x86_64)
url='https://www.chiark.greenend.org.uk/~sgtatham/puzzles/'
license=(MIT)
depends=(cairo
         gdk-pixbuf2
         glib2
         glibc
         gtk3
         pango)
makedepends=(cmake
             git
             halibut
             imagemagick)
source=(git+https://git.tartarus.org/simon/puzzles.git#commit=$_commit)
sha256sums=('c0b09c2bd6404e1b01743ee8b80783bf30d38509004f263d829bf2c6067395d4')

build() {
  cmake -B build -S puzzles \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DNAME_PREFIX=puzzles-
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 puzzles/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
