# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=duckdb
pkgname=(duckdb python-duckdb)
pkgver=1.4.4
pkgrel=1
pkgdesc='An analytical in-process SQL database management system'
arch=(x86_64)
url='https://www.duckdb.org'
license=(MIT)
depends=(glibc gcc-libs)
makedepends=(
  git
  cmake
  ninja
  pybind11
  uv
  python-installer
  python-setuptools-scm
  python-scikit-build-core
)
source=(
  "$pkgbase::git+https://github.com/duckdb/duckdb#tag=v$pkgver"
  "python-duckdb::git+https://github.com/duckdb/duckdb-python#tag=v$pkgver"
)
sha512sums=('5aef261db968a974539f5de39c1e66efa1388d9a29e74e3bd3937d24c1bb074610b014390e817751be2238a28ce0f89c79870ba87e462b6c84e16382005781e6'
            'd7b37d0b8092c51d8154794dae81101c575c4deb2ec143ca82d906ab9c828cea777eb0532cb7e84a55a5a02835385ff2ae17d03ecc4362d2b3dad8e73cc9968d')
b2sums=('678b4fc3b91a17f016b6a00df96cafb60ddb5b8fad44e15e888d6b30a41d8deac776e57f28275f954ea8d457f34aaba2e691bcbc9bc4d9adc86c8a0afb77f63f'
        'b8494110e68b74aaf8e2da127aa64127f4f140b84c3de490c8d4e0f5f044ef76d051abd0c6f29c2869f8624bfc7b3886eb06589245215e2caf682101f7ad2234')

prepare() {
  cd python-duckdb
  # python-duckdb requires a copy of the duckdb sourcetree *shrugs*
  git submodule init
  git config submodule.external/duckdb.url "$srcdir/$pkgbase"
  git -c protocol.file.allow=always submodule update
}

build() {
  local cmake_options=(
    -S "$pkgbase"
    -B build
    -G Ninja
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D OVERRIDE_GIT_DESCRIBE="v$pkgver"
  )
  cmake "${cmake_options[@]}"

  cmake --build build

  # Python API
  uv build \
    --wheel \
    --directory='python-duckdb' \
    --no-build-isolation
}

package_duckdb() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}

package_python-duckdb() {
  pkgdesc+=' Python API'
  depends+=(
    python
    python-typing_extensions
  )
  optdepends=(
    'ipython: used in duckdb.query_graph'
    'python-fsspec: used in duckdb.filesystem'
    'python-numpy: used in duckdb.experimental.spark and in duckdb.fetchnumpy()'
    'python-pandas: used for pandas dataframes all over the place'
    'python-pyarrow: used for pyarrow support'
    'python-arrow-adbc: for the adbc driver'
  )

  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
