# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgbase=amsynth
pkgname=(amsynth amsynth-{common,dssi,lv2,standalone,vst})
pkgver=1.13.4+r202+gc83787a
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=(x86_64)
url="https://amsynth.github.io/"
license=(GPL-2.0-or-later)
groups=(
  dssi-plugins
  lv2-plugins
  pro-audio
  vst-plugins
)
makedepends=(
  autoconf-archive
  dssi
  fontconfig
  freetype2
  gcc-libs
  git
  glibc
  harfbuzz
  intltool
  jack
  ladspa
  liblo
  libpng
  libxcursor
  libxinerama
  libxrandr
  lv2
  pandoc
  zlib
)
checkdepends=(lv2lint)
_commit=c83787a381dded142f55e9090f20f0f9e816da72  # develop
source=(
  git+https://github.com/amsynth/amsynth.git#commit=$_commit
  git+https://github.com/juce-framework/JUCE.git
  git+https://github.com/ODDSound/MTS-ESP.git
)
sha512sums=('c3514d90b2202c0b7f708779980cae266566db05195f17189ebaa5eee277aba32a37bc0c1b5661b57b81ae18b720ffbc9aa21f7f426c9d29ab6104297c20e841'
            'SKIP'
            'SKIP')
b2sums=('d53329901b880e544a1689b7b2d146c90cb605ab989d99a962bd79fcca47790ce027aa67b34fd8be827d86beb7b205a92a45045b8ec3e448ec301c9fd4b759f0'
        'SKIP'
        'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^release-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url external/JUCE "$srcdir/JUCE"
  git submodule set-url external/MTS-ESP "$srcdir/MTS-ESP"
  git -c protocol.file.allow=always submodule update

# Fix metainfo install dir
  sed -e 's|/appdata|/metainfo|' -i Makefile.am
  autoreconf -fiv
  intltoolize -f
}

build() {
  local configure_options=(
    --prefix=/usr
    --with-alsa
    --with-jack
    --with-dssi
    --with-lv2
    --with-vst
    --with-pandoc
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  cp .libs/${pkgname}_lv2*.so data/$pkgname.lv2/
  lv2lint -Mpack -I data/$pkgname.lv2 "http://code.google.com/p/amsynth/amsynth" || : # fails: UI Symbols
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_amsynth() {
  depends=(
    amsynth-{common,dssi,lv2,standalone,vst}
  )

  make DESTDIR="$pkgdir/" install -C $pkgname
  (
    cd "$pkgdir"
    _pick $pkgname-common usr/share/$pkgname
    _pick $pkgname-dssi usr/lib/dssi
    _pick $pkgname-dssi usr/share/metainfo/dssi-$pkgname-plugin.metainfo.xml
    _pick $pkgname-lv2 usr/lib/lv2
    _pick $pkgname-dssi usr/share/metainfo/lv2-$pkgname-plugin.metainfo.xml
    _pick $pkgname-standalone usr/bin
    _pick $pkgname-standalone usr/share/metainfo/$pkgname.appdata.xml
    _pick $pkgname-standalone usr/share/applications
    _pick $pkgname-standalone usr/share/icons
    _pick $pkgname-standalone usr/share/locale
    _pick $pkgname-standalone usr/share/man
    _pick $pkgname-vst usr/lib/vst
    _pick $pkgname-dssi usr/share/metainfo/vst-$pkgname-plugin.metainfo.xml
  )
}

package_amsynth-common() {
  pkgdesc+=" - common data"

  mv -v $pkgname/* "$pkgdir/"
  install -vDm 644 $pkgbase/{AUTHORS,CHANGELOG.md,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_amsynth-dssi() {
  pkgdesc+=" - DSSI plugin"
  depends=(
    amsynth-common
    dssi-host
    freetype2 libfreetype.so
    gcc-libs
    glibc
    liblo liblo.so
    libpng libpng16.so
    zlib libz.so
  )

  mv -v $pkgname/* "$pkgdir/"
}

package_amsynth-lv2() {
  pkgdesc+=" - LV2 plugin"
  depends=(
    amsynth-common
    freetype2 libfreetype.so
    gcc-libs
    glibc
    libpng libpng16.so
    lv2-host
    zlib libz.so
  )

  mv -v $pkgname/* "$pkgdir/"
}

package_amsynth-standalone() {
  pkgdesc+=" - standalone application"
  depends=(
    amsynth-common
    alsa-lib libasound.so
    freetype2 libfreetype.so
    gcc-libs
    glibc
    hicolor-icon-theme
    jack libjack.so
    liblo liblo.so
    libpng libpng16.so
    zlib libz.so
  )
  optdepends=(
    'new-session-manager: for session management with the standalone application'
  )

  mv -v $pkgname/* "$pkgdir/"
}

package_amsynth-vst() {
  pkgdesc+=" - VST plugin"
  depends=(
    amsynth-common
    freetype2 libfreetype.so
    gcc-libs
    glibc
    libpng libpng16.so
    vst-host
    zlib libz.so
  )

  mv -v $pkgname/* "$pkgdir/"
}
