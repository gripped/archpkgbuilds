# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-hspec
pkgname=haskell-tasty-hspec
pkgver=1.2.0.4
pkgrel=17
pkgdesc="Hspec support for the Tasty test framework"
url="https://github.com/mitchellwrosen/tasty-hspec"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hspec' 'haskell-hspec-api' 'haskell-hspec-core' 'haskell-quickcheck'
         'haskell-tasty' 'haskell-tasty-smallcheck' 'haskell-tasty-quickcheck')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('eea02800ddc34fe8fe26ae2e1792603e502c68e36b31966a69440c411f9ba097cf005a05f2ad0f758699acca9ab2ee7857e638288e61751dd0bf5da9d60c9561')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u tasty
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
