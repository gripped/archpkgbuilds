# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cl-clx
_pkgname="${pkgname#cl-}"
pkgver=0.7.6
pkgrel=1
pkgdesc='An implementation of the X Window System protocol in Common Lisp'
arch=('any')
url='https://github.com/sharplispers/clx'
license=('MIT')
depends=(
  'common-lisp'
  'cl-asdf'
  'xorg-server'
)
makedepends=('git')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('fc3f3b08372b35a2623f56b6ebd6839990a0308f6b3d03f7c7b19f8d2312206f2e1979776ac3143e57af9f3c9d82344c8894db7715a0da623a1f6f66ab61ff6d')
b2sums=('8e532a5c610d96b80d077d296e1699420b743e3184458ad6cc929b5ab2ca87c7fac4a3f6359f087b0df36e1e57b5a490981b4a44e2fbbaa9c89039117bc0864a')

build() {
  cd "$pkgname/manual"

  makeinfo clx.texinfo
}

package() {
  cd "$pkgname"

  # library
  install -vd "$pkgdir/usr/share/common-lisp/source/$_pkgname"
  cp -vr \
    debug demo extensions tests ./*.{lisp,asd} \
    "$pkgdir/usr/share/common-lisp/source/$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md 
  install -vDm644 -t "$pkgdir/usr/share/info" manual/clx.info

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
