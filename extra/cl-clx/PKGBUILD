# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cl-clx
_pkgname="${pkgname#cl-}"
pkgver=0.7.7
pkgrel=1
pkgdesc='An implementation of the X Window System protocol in Common Lisp'
arch=(any)
url='https://github.com/sharplispers/clx'
license=(MIT)
depends=(
  common-lisp
  cl-asdf
  xorg-server
)
makedepends=(git)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('0e4b953221892001f7ca66e16387aef9640dd4af27ca9e06cd825a2e0bce8227b70008308437dada7442744fd6c284178999da8eed5d3b11d4e21cc9178c8a19')
b2sums=('eaf95d333c8c6bdae6b1bfbb293d3d61739ee8415e78c5bfd88695687123dbc5504746d3c4c69a0dfd8f90fe4dac421006f9051ec35ad8e7d2f224f8e9ae9134')

build() {
  cd "$pkgname/manual"

  makeinfo clx.texinfo
}

package() {
  cd "$pkgname"

  # library
  install -vd "$pkgdir/usr/share/common-lisp/source/$_pkgname"
  cp -vr \
    debug demo extensions tests ./*.{lisp,asd} \
    "$pkgdir/usr/share/common-lisp/source/$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md 
  install -vDm644 -t "$pkgdir/usr/share/info" manual/clx.info

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
