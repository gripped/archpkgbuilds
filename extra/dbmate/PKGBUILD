# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.28.0
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=('x86_64')
url='https://github.com/amacneil/dbmate'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('d18e46505a593fc4b921d7224ccf0bbc2f305bd9a75c72c819c288dc3c2cf7441a0eaa9bdac21325f3b45d579ee8ea9eba13e464429a8965214950d67221e044')
b2sums=('4f8058ee2dcc3253234dea2b24d48a1b12920358ef5329985c347d629fbc79c0e12717b350efbb16dc8ac4bb28b8fd4b593020c3a0fa6ff064288a40af94f2aa')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
