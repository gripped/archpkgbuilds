# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.29.2
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=(x86_64)
url='https://github.com/amacneil/dbmate'
license=(MIT)
depends=(glibc)
makedepends=(git go)
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('f64a5247aae9056ff4ac0bfed056c3154a6da859adb74651b409481f2d6338b8bd180f7446c340aae5210f9562ae99a33a4f46c58e6a1c880cdedaea487cef4e')
b2sums=('1cf4d2004d546220b46ec95be884341955fd0caeec9ce13b5a93682066a412ec66bd0aa894285022403c8ecbdb36e053bcb3c60302f191d87e083daa97bf2b9c')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
