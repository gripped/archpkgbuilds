# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.29.3
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=(x86_64)
url='https://github.com/amacneil/dbmate'
license=(MIT)
depends=(glibc)
makedepends=(git go)
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('f939422b284396246524002c14cbf22cf8ccb14bc4d22ba225bc8bdae0f99576c4256099d9a3b66e277f35fb96ddd5e4476a19457f420e3c42cbc8e4e90d72ed')
b2sums=('029ec2fc8592a7db1475649930060fbcb7c83333880a7ad2e6ee51c0a41186151f5877ae16910a7fcba7d9e8f9f3cebd9fae3f2caf3e866fc1564b41c8876bd4')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
