# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.30.0
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=(x86_64)
url='https://github.com/amacneil/dbmate'
license=(MIT)
depends=(glibc)
makedepends=(git go)
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('729af87d60d5a5d6723fa0684a49c50cdfecbc8c9a7544d12bb36fa066bcfbcf4fe0040b519cf481d0f0e43d1ba5916716aed72ba000385d515cd613e3d78ade')
b2sums=('a992f95068ecede941cf7d53afb1fa99ad4fd8754b16c68395a0f45f9d2913c108d654af054fd7d6d025d07e2f8e01337184e547367f5163e38e92cfafd49493')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
