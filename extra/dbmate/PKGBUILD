# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.23.0
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=('x86_64')
url='https://github.com/amacneil/dbmate'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('a2a003691bd0a64501165dd528fbf26fb200c636e68a1a71dd07a64e01a302fcfca7f5e01ddfaac70f0978a991aed2a2512d9f4488f7a84d0ee2691f57900139')
b2sums=('7ef8f02ee544e1521561d5769aae7d68a83c7dba3526a6c3f703a22c6139c5b9b8b0bc89fe3aff7b625255ca850863580351ff45b7c74f9386e5f619f48bd161')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
