# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.29.0
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=(x86_64)
url='https://github.com/amacneil/dbmate'
license=(MIT)
depends=(glibc)
makedepends=(git go)
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('f2a8622af35a8aede5da90698c4aca59df80aee48cc4512531c902946e205efafd2b6b097b04bd4b20643defc4a85a0927e20a447424d8e3a5762cebb6a3b8cf')
b2sums=('c59b82ce079849333782bbefa5ec9e841ebac630493d978221f30b9b11f8a4b20621f2ccadf22c4c5dba6294b30eeccf1c50ae097d93a449c6881d8d9787c12f')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
