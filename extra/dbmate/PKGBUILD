# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.29.5
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=(x86_64)
url='https://github.com/amacneil/dbmate'
license=(MIT)
depends=(glibc)
makedepends=(git go)
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('855a3e989b6b0f8f34b780f9420c929de6e30fe256d536695f7c89f6921d5480ed90890bc47cd13d90f6e3767c85ba2540752eec220c46d34991e795b5832b95')
b2sums=('05b75cad6af9b921351d85d6c0d697a09b69ae1655d6ce54fba290f99f5f2dc4e9c98cc9ac75a4bd67ad2c488c1828dfffefc755d07a80ffb160cfe18fe93d23')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
