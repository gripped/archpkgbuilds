# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.16.0
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=('x86_64')
url='https://github.com/amacneil/dbmate'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('e0ad3007bbf48890d85a55f3cb379d4df9e0ab013501387a0a6d012313bd5e1210f07d8b1cec108354108a1ef76217457d3d2b07e3d296b3d07eff4123a867c5')
b2sums=('afbf573a30c5f1b9152fe3da29101c001dd96e5bcd01c2e36c278549aecff9637ef01460de64bb444142d861eb14d86b4a9e94a432da304cd175b4ebb02e55e5')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
