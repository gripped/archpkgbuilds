# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=text-metrics
pkgname=haskell-text-metrics
pkgver=0.3.3
pkgrel=12
pkgdesc="Calculate various string metrics efficiently"
url="https://github.com/mrkkrp/text-metrics"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-primitive' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b37f4959f6dab6f524a987bd3c9469345a643f3222600c2a86380b2417dc9e329c702d74e960623082a9fcaf6c081f2f2fb67cf4f85c64ef25ba347963a3dff6')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
