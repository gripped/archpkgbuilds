# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgname=python-cairocffi
pkgver=1.5.1
pkgrel=1
pkgdesc="cffi-based cairo bindings for Python"
arch=('any')
url="https://cairocffi.readthedocs.io/"
license=('BSD')
depends=('python-cffi' 'cairo')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel' 'python-xcffib'
             'gdk-pixbuf2')
checkdepends=('python-pytest' 'python-numpy' 'python-pikepdf')
optdepends=('gdk-pixbuf2: for cairocffi.pixbuf'
            'python-xcffib: for cairo xcb support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/cairocffi/archive/v${pkgver}.tar.gz")
sha512sums=('0c0f600014873a29ccdb48133e2d2f351e0ffb0c7c13b177f789a39ac6ce4908b6947425757710a09fe4ea5ad355e251f596e7344c28a8ffe13db0ed8589cc31')

build() {
  cd cairocffi-$pkgver
  python -m build -nw
}

check() {
  cd cairocffi-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd cairocffi-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
