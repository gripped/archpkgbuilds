# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: farid <farid at archlinux-br.org>

pkgname=libfishsound
pkgver=1.0.1
pkgrel=2
pkgdesc="A simple programming interface that wraps Xiph.Org audio codecs"
arch=(x86_64)
url="https://www.xiph.org/fishsound/"
license=(BSD-3-Clause)
depends=(
  flac
  glibc
  libvorbis
  speex
)
provides=(libfishsound.so)
source=(https://gitlab.xiph.org/xiph/libfishsound/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('36921a113e1560075a865a3d7b8f3a6a31e0cd93a5a89c04985bb33c81cbf6d16dcf53648acfb58eb697e692d77f9b03d96b475c20d7775f05c13a2fc80c3a10')
b2sums=('6a562fce0f9ffcd829e303763da66dbefb66440d4ee77d8b14bcf9961130180e9b1a9831747fbc6e7f9f5be3abaadf0f6a34fa00aca75693bfc5ac30979ff1b9')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --enable-static=no --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" docdir=./docs install
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
