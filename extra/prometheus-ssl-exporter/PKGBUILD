# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=prometheus-ssl-exporter
_pkgname=ssl_exporter
pkgver=2.4.3
pkgrel=2
pkgdesc='Prometheus exporter for TLS certificates'
arch=('x86_64')
url='https://github.com/ribbybibby/ssl_exporter'
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
backup=(
  'etc/prometheus/ssl-exporter.yml'
  'etc/prometheus/ssl-exporter.env'
)
options=('!lto')
source=(
  "$pkgname::git+$url.git#tag=v$pkgver"
  'systemd.service'
  'sysusers.conf'
  'config.yml'
  'config.env'
)
sha512sums=('b1d3c75a483dfee934cad722dcf020cf679dfd4cee693da354360f4e850803e53bd81edac3832ded4f73e997653758c22d0af63b3d7371815514f26594befa21'
            '2513b17997d53ef5cac8f0a6466cb553a1395f18fbe01c493f66fb27729857bcac280b98d6cb3506e9bc6025b7788cdf6580d1254758032ccd19b26ee6f37676'
            '1a1df654e6844494b7038d7870a22022caba3274aa0968d37277520908adafa926ba33ceaab3aa80091b35363a003690df2b0dc7927f95308c67236dffb7f1c5'
            '43fdd3ef97d8bff6824542399a9b51f8ca5803dbd9ea3024bc8d3122738c9d31f517a000270ed070f9161d6d51775e883d9b7364ccc8ec862de6bf51e514d71d'
            '0ce0a1f01a9f7b03df172a1f61bb0903bc4e1fdfd6422c8d39f6b33672906073e4da234277a1fdc8f552d30cb772998b321fe9814c63c2cebadf64493667791f')
b2sums=('eef313e3a4a172199f73c8c892d0ec269bdbbf912f9653816d53f3a863358f2494d6030541fd1e21be168b1c096e25e7ac7256cb636a5e04f60313d686f757ed'
        'b83837a4c57161f7a57f3816385e9b1b5e0bb851c84571b2a312da43e4cad1fb797d9ebe8c9c3ae7e7291f51e7f9d44bf86d4592801d8e939114880d18dba7c0'
        '972d9da77e3a889245ea990585e5594254e0951adc9a81c37fd21a5fba23aad0ab8a8eefb74fe03ee0a7798c22574de434c26e1e052eb5a45e501ed3ccc8ce2b'
        '7fd776e40f03f68898cf5de67cf87c99bdab504eafa89a55a20ad408f46ccf4e7a18ab2b430144e5b9a201e4eef48e5b8dd75aca9ab2984988f07acf22e4f3f6'
        '042b25709b320603f3440fd0592bc159b75c26991d4e608540ac78b3cb74b501750e592b938898218872310b16b7ab0d66a888a02f105ff5c1110be0f27101d6')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}


build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # reproducible: use commit date for build date
  local _commit_date=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d-%H:%M:%S)

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/prometheus/common/version.Version=$pkgver \
    -X github.com/prometheus/common/version.Revision=$(git rev-parse HEAD) \
    -X github.com/prometheus/common/version.Branch=main \
    -X github.com/prometheus/common/version.BuildUser=archlinux \
    -X github.com/prometheus/common/version.BuildDate=$_commit_date" \
    -o build \
    ./...
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 config.yml "$pkgdir/etc/prometheus/${pkgname#prometheus-}.yml"
  install -vDm644 config.env "$pkgdir/etc/prometheus/${pkgname#prometheus-}.env"

  cd "$pkgname"

  # binary
  install -vDm755 "build/$_pkgname" "$pkgdir/usr/bin/$pkgname"

  # license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
