# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=goffice
pkgver=0.10.57
pkgrel=2
pkgdesc='A GLib/GTK set of document-centric objects and utilities library'
arch=(x86_64)
url='https://gitlab.gnome.org/GNOME/goffice'
license=('GPL-2.0-only OR GPL-3.0-only')
depends=(
  at-spi2-core
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  lasem
  libgsf
  librsvg
  libspectre
  libxml2
  libxslt
  pango
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  intltool
)
source=("git+https://gitlab.gnome.org/GNOME/goffice.git#tag=GOFFICE_${pkgver//./_}")
b2sums=(831c6df06f80104b475c723d6727cfd621d2fa3faef6f7b1d95c00c2c9c509ebdc66f1243435401de5c62b0f858776c3204b188c991098c86f0ad662edba5813)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-introspection \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
