# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Riley Trautman <asonix@tamu.edu>

pkgname=pantheon-mail
pkgver=8.0.0
pkgrel=3
pkgdesc='Mail app designed for elementary OS'
arch=(x86_64)
url=https://github.com/elementary/mail
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libcamel-1.2.so
  libedataserver-1.2.so
  libedataserverui-1.2.so
  libfolks.so
  libgranite.so
  libhandy-1.so
  libportal-gtk3
  webkit2gtk-4.1
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=21df72f7d4c15588116f7e7e76820f9c0a542913
source=(
  pantheon-mail::git+https://github.com/elementary/mail.git#tag=${_tag}
  0001-Adapt-to-libcamel-API-changes-in-3.57.1.patch
)
sha256sums=('b6735f0c6947e208400a0a72c66dad5e2d5996ab1b45275c3413c746bf3672f9'
            'e6a3be42cbdc1ce45b3e644cd2ec08db9400d612b320fdb04714b90a2ab63d78')

prepare() {
  cd pantheon-mail

  # Build with evolution-data-server 3.58
  # https://github.com/elementary/mail/pull/1023
  git apply -3 ../0001-Adapt-to-libcamel-API-changes-in-3.57.1.patch
}

pkgver() {
  cd pantheon-mail
  git describe --tags
}

build() {
  arch-meson pantheon-mail build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
