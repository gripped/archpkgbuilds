# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Contributor: Thomas Booker <tw.booker@outlook.com>

libgmobile_commit=a3c67a1184bfa73c593edefabe77cce57a5e38f6

pkgname=phosh-mobile-settings
pkgver=0.37.0
pkgrel=1
pkgdesc='A settings app for mobile specific things'
arch=(x86_64 aarch64)
url='https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings'
license=(GPL3)
depends=(feedbackd libadwaita lm_sensors phoc phosh)
makedepends=(git meson wayland-protocols)
source=("${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
	https://gitlab.gnome.org/World/Phosh/gmobile/-/archive/${libgmobile_commit}/gmobile-${libgmobile_commit}.tar.gz)
sha256sums=('560c22c07fe3db0c6fe1ebd210dda7ce1b86fac125ca8935620ae84f25b72060'
            '1aeac55a790525c2028a66562de877a8f99ce391636d8e0e3d059b0899c7a7ec')

prepare() {
  mv "gmobile-${libgmobile_commit}" "${pkgname}-v${pkgver}/subprojects/gmobile"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
