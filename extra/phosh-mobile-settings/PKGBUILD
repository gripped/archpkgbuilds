# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org
# Contributor: Thomas Booker <tw.booker@outlook.com>

pkgname=phosh-mobile-settings
pkgver=0.50.1
pkgrel=1
pkgdesc='A settings app for mobile specific things'
arch=('x86_64' 'aarch64')
url='https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings'
license=(GPL-3.0-or-later)
depends=(
        "feedbackd"
        "libadwaita"
        "libyaml"
        "lm_sensors"
        "phoc"
        "phosh"
        "libgmobile"
        "cellbroadcastd"
)
makedepends=(
            "git"
            "meson"
            "wayland-protocols"
            "python-packaging"
            "glib2-devel"
)
_gvc=5f9768a2eac29c1ed56f1fbb449a77a3523683b6
source=(
        "${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control#commit=${_gvc}" #new dependency with no release
)
sha256sums=('bda9c7aaa498747703cb93b80795cb637d7998b7237a9117940fffd7ce44b89d'
            '587319b45ff7d989635aed0c3bd9ef834d6e53ae46788cb6ba083d42d7e63855')

prepare() {
  cp -r libgnome-volume-control "${pkgname}-v${pkgver}/subprojects/gvc"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
