# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Contributor: Thomas Booker <tw.booker@outlook.com>

libgmobile_commit=a3c67a1184bfa73c593edefabe77cce57a5e38f6

pkgname=phosh-mobile-settings
pkgver=0.41.0
pkgrel=1
pkgdesc='A settings app for mobile specific things'
arch=(x86_64 aarch64)
url='https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings'
license=(GPL3)
depends=(feedbackd libadwaita lm_sensors phoc phosh)
makedepends=(git meson wayland-protocols python-packaging glib2-devel)
source=("${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
	https://gitlab.gnome.org/World/Phosh/gmobile/-/archive/${libgmobile_commit}/gmobile-${libgmobile_commit}.tar.gz)
sha256sums=('c38ba73cb29aafdfb4662777903ca81de80b6875cc70357420bf9e5a2b45aa46'
            '1aeac55a790525c2028a66562de877a8f99ce391636d8e0e3d059b0899c7a7ec')

prepare() {
  mv "gmobile-${libgmobile_commit}" "${pkgname}-v${pkgver}/subprojects/gmobile"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
