# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org
# Contributor: Thomas Booker <tw.booker@outlook.com>

pkgname=phosh-mobile-settings
pkgver=0.53.0
_gvcver=1cdc1cb2d622d64e9ad2781093bcc63719c5ea5b
pkgrel=1
pkgdesc='A settings app for mobile specific things'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings'
license=(GPL-3.0-or-later)
depends=(
  cellbroadcastd
  dconf
  feedbackd
  gcc-libs
  glib2
  glibc
  gnome-desktop-4
  gsound
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgmobile
  libportal
  libportal-gtk4
  libpulse
  libyaml
  lm_sensors
  pango
  phoc
  phosh
  wayland
)
makedepends=(
  git
  glib2-devel
  meson
  python-packaging
  wayland-protocols
)
source=(
  "git+https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings.git?signed#tag=v$pkgver"
  #git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git
  "git+https://gitlab.gnome.org/guidog/libgnome-volume-control.git#commit=$_gvcver"
)
b2sums=('7bb8e7aae44d1dc316b4f06090969c4394e36b91583ac8237b9464c4c697a1177880909a61d73740e0fd9fd5602313937e7e24ad44d49ecd4564ded210a1fa03'
        '6f92aa86fa6c72d9a4e230593a696f918ab8515f1501b91c1121d250fb3a0415207ce68e23f2199b5f0a3e69299af01650a75c0c733fe7e02cb62014c5d4de1f')
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido GÃ¼nther <guido@xcpu.ch>

prepare() {
  ln -s "$srcdir/libgnome-volume-control" "$pkgname/subprojects/gvc"
  meson subprojects checkout --sourcedir $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
