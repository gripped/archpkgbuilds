# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=graphql-client-cli
pkgver=0.14.0
pkgrel=1
pkgdesc='CLI for graphql-client'
license=('Apache-2.0' 'MIT')
arch=('x86_64')
url="https://github.com/graphql-rust/graphql-client"
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/graphql-rust/graphql-client/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('6f52c89dd3850e55337f08f3a7673d06c8a6cc612c3ed2d4680c970d4a74de2315978cefe7f209d18567c706dcd7db005a2837676028ed849756fc7570ec48ec')
options=('!lto')

build() {
    cd "graphql-client-$pkgver"
    cargo build --package graphql_client_cli --release
}

check() {
    cd "graphql-client-$pkgver"
    cargo test --package graphql_client_cli
}

package() {
    cd "graphql-client-$pkgver"
    install -Dm755 -t "$pkgdir/usr/bin" \
        "target/release/graphql-client"

    # install license files
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
