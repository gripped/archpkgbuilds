# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=3.32.3.1
pkgrel=3
pkgdesc='A 2D CAD package based upon Qt'
arch=('x86_64')
url="https://www.qcad.org"
license=('GPL-3.0-or-later')
options=('!staticlibs')
depends=('qt6-svg' 'gcc-libs' 'qt6-tools' 'qt6-shadertools' 'qt6-wayland' 'qt6-5compat')
makedepends=('glu' 'cmake' 'ninja' 'vulkan-headers')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/qcad/qcad/archive/refs/tags/v$pkgver.tar.gz"
    "qtjsapi-$pkgver.tar.gz::https://github.com/qcad/qtjsapi/archive/refs/tags/v$pkgver.tar.gz"
    "qcadjsapi-$pkgver.tar.gz::https://github.com/qcad/qcadjsapi/archive/refs/tags/v$pkgver.tar.gz"
)
sha512sums=('b5c55f54dc73a45eb80b3d9e85a5004cebe4ef46713b47dfd7f2fefb3a93edc5e9de84250b784f0b5439361b192dd69444277fd721e17c721613d33b8426517e'
            'a302a868d4186203db233a9e0353c1d8533c6760eb13a3db60ef1a437551342c493078d85154b0b6eeda191522a61e6a55431b00e1f1fd08b68dd5fde06a7545'
            'b1c83165198df31bdf4af32b2e34bf6e46bcd2351746ce1a105d79a2ef2edec0dbdccd58d9d2cd39476c67dadaba0991b14fe56166ec6caeccaf114b3b36bb98')

prepare() {
  ln -s qcad-$pkgver qcad
  ln -s qtjsapi-$pkgver qtjsapi
  ln -s qcadjsapi-$pkgver qcadjsapi

}

build() {
  cd "$srcdir/qtjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  cmake \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DBUILD_QT6=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcadjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  # prepare the rest of the release items
  cp -a \
      examples \
      fonts \
      libraries \
      linetypes \
      patterns \
      scripts \
      themes \
      ts \
      plugins \
      platforminputcontexts \
      platforms \
      xcbglintegrations \
      release/
  cp readme.txt release/

}

package() {
  cd qcad-$pkgver

  install -dm755 "$pkgdir"/usr/lib/qcad
  cp -a release/* "$pkgdir"/usr/lib/qcad

  # install man
  install -Dm644 qcad.1 "$pkgdir"/usr/share/man/man1/qcad.1

  # desktop
  install -Dm644 scripts/qcad_icon.png "$pkgdir"/usr/share/pixmaps/org.qcad.QCAD.png
  install -Dm644 qcad.desktop "$pkgdir"/usr/share/applications/qcad.desktop

  # qcad bin
  install -dm755 "$pkgdir/usr/bin"
  ln -sf /usr/lib/qcad/qcad-bin "$pkgdir/usr/bin/qcad"
}
