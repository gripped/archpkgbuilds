# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=3.32.4.4
pkgrel=3
pkgdesc='A 2D CAD package based upon Qt'
arch=('x86_64')
url="https://www.qcad.org"
license=('GPL-3.0-or-later')
options=('!staticlibs')
depends=('qt6-declarative' 'qt6-svg' 'gcc-libs' 'qt6-tools' 'qt6-shadertools' 'qt6-5compat')
makedepends=('glu' 'cmake' 'ninja' 'vulkan-headers')
source=(
    "$pkgname-$pkgver-$pkgrel.tar.gz::https://github.com/qcad/qcad/archive/refs/tags/v$pkgver.tar.gz"
    "qtjsapi-$pkgver.tar.gz::https://github.com/qcad/qtjsapi/archive/refs/tags/v$pkgver.tar.gz"
    "qcadjsapi-$pkgver.tar.gz::https://github.com/qcad/qcadjsapi/archive/refs/tags/v$pkgver.tar.gz"
    "qtjsapi-qt-6.10.patch"
)
sha512sums=('60e11e5cef4510bf468c300b70710d5530f4b42cbb482f8df3083cd2f95fea40791a7d8d77292400c8848a6b9fb25a7334be9b338cd7eefb21fe7e083b83cf59'
            '68382c4b8b6295057c17b01b46298536fa4f2d6cdee9b6256af023a5f542e3395c29824e79be0b561ff8350b298389d9169473533e190c31996abf656416ebf5'
            '1b0063ccbb836f23dfe12c3bed58d737872f2a4d66d26487977a6fef709c80ac967c7f0bd99c5e68131e6bbd4092bfbc01bb391b61e8e80c687df011dc6bd0ac'
            '7ea8f83dcd5bf5446ed282144668796c72ca6cdf9d3b6195fc7ad6fcc4eb5b67f9c39195037f1812b537a974264bda090f4aaf1fc46a1a5e0ee0a6c198ae4d08')

prepare() {

  ln -s qcad-$pkgver qcad
  ln -s qtjsapi-$pkgver qtjsapi
  ln -s qcadjsapi-$pkgver qcadjsapi

  cd "$srcdir/qtjsapi-$pkgver"
  patch -p1 -i "$srcdir/qtjsapi-qt-6.10.patch"
}

build() {
  cd "$srcdir/qtjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  cmake \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DBUILD_QT6=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcadjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  # prepare the rest of the release items
  cp -a \
      examples \
      fonts \
      libraries \
      linetypes \
      patterns \
      scripts \
      themes \
      ts \
      plugins \
      platforminputcontexts \
      platforms \
      xcbglintegrations \
      release/
  cp readme.txt release/

}

package() {
  cd qcad-$pkgver

  install -dm755 "$pkgdir"/usr/lib/qcad
  cp -a release/* "$pkgdir"/usr/lib/qcad

  # install man
  install -Dm644 qcad.1 "$pkgdir"/usr/share/man/man1/qcad.1

  # desktop
  install -Dm644 scripts/qcad_icon.png "$pkgdir"/usr/share/pixmaps/org.qcad.QCAD.png
  install -Dm644 scripts/qcad_icon.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/org.qcad.QCAD.svg
  install -Dm644 qcad.desktop "$pkgdir"/usr/share/applications/qcad.desktop

  # qcad bin
  install -dm755 "$pkgdir/usr/bin"
  ln -sf /usr/lib/qcad/qcad-bin "$pkgdir/usr/bin/qcad"
}
