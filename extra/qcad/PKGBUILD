# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=3.32.3.4
pkgrel=3
pkgdesc='A 2D CAD package based upon Qt'
arch=('x86_64')
url="https://www.qcad.org"
license=('GPL-3.0-or-later')
options=('!staticlibs')
depends=('qt6-svg' 'gcc-libs' 'qt6-tools' 'qt6-shadertools' 'qt6-wayland' 'qt6-5compat')
makedepends=('glu' 'cmake' 'ninja' 'vulkan-headers')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/qcad/qcad/archive/refs/tags/v$pkgver.tar.gz"
    "qtjsapi-$pkgver.tar.gz::https://github.com/qcad/qtjsapi/archive/refs/tags/v$pkgver.tar.gz"
    "qcadjsapi-$pkgver.tar.gz::https://github.com/qcad/qcadjsapi/archive/refs/tags/v$pkgver.tar.gz"
)
sha512sums=('39135251f342224cf633c88257e9ebdcc7fad3607c0d32c09fc37b577173e5064f20c20a027402fdb44290ed8b0869698293c5154138ab1320458341a5519be9'
            '3108e9c730c3611bb25a7e3c5a697d734bec91a3a594fe5558ca182693dc47719e8cb7424d3cd2d8fe2848e9d926cb04ac74a811cefde1d5250fc282c8464d69'
            '50bf01b20aff724de84280a12c298fd52d0cfe9d170d0cf05fa3ebb97823527cafb48624a1ea8a8fe391e69edbd11fe8537501734b35662296e6f5fa7ecc5239')

prepare() {
  ln -s qcad-$pkgver qcad
  ln -s qtjsapi-$pkgver qtjsapi
  ln -s qcadjsapi-$pkgver qcadjsapi

}

build() {
  cd "$srcdir/qtjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  cmake \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DBUILD_QT6=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcadjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  # prepare the rest of the release items
  cp -a \
      examples \
      fonts \
      libraries \
      linetypes \
      patterns \
      scripts \
      themes \
      ts \
      plugins \
      platforminputcontexts \
      platforms \
      xcbglintegrations \
      release/
  cp readme.txt release/

}

package() {
  cd qcad-$pkgver

  install -dm755 "$pkgdir"/usr/lib/qcad
  cp -a release/* "$pkgdir"/usr/lib/qcad

  # install man
  install -Dm644 qcad.1 "$pkgdir"/usr/share/man/man1/qcad.1

  # desktop
  install -Dm644 scripts/qcad_icon.png "$pkgdir"/usr/share/pixmaps/org.qcad.QCAD.png
  install -Dm644 scripts/qcad_icon.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/org.qcad.QCAD.svg
  install -Dm644 qcad.desktop "$pkgdir"/usr/share/applications/qcad.desktop

  # qcad bin
  install -dm755 "$pkgdir/usr/bin"
  ln -sf /usr/lib/qcad/qcad-bin "$pkgdir/usr/bin/qcad"
}
