# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=3.32.6.0
pkgrel=1
pkgdesc='A 2D CAD package based upon Qt'
arch=('x86_64')
url="https://www.qcad.org"
license=('GPL-3.0-or-later')
options=('!staticlibs')
depends=('qt6-svg' 'gcc-libs' 'qt6-tools' 'qt6-shadertools' 'qt6-5compat' 'qt6-declarative')
makedepends=('glu' 'cmake' 'ninja' 'vulkan-headers')
source=(
    "$pkgname-$pkgver-$pkgrel.tar.gz::https://github.com/qcad/qcad/archive/refs/tags/v$pkgver.tar.gz"
    "qtjsapi-$pkgver.tar.gz::https://github.com/qcad/qtjsapi/archive/refs/tags/v$pkgver.tar.gz"
    "qcadjsapi-$pkgver.tar.gz::https://github.com/qcad/qcadjsapi/archive/refs/tags/v$pkgver.tar.gz"
)
sha512sums=('f6d10c977ab35e7b3917f26cba3bc7414ee630b83155a32b201f4074f40888ea540f94abd65cf65205c3d601df48ab6c5af1450659e4e9cd5d056adf2a2d13ad'
            '15cc9adb841dfc6cc9a62f17b2f9d9ce26240aac79ef9efe07ca5eca41e0d32f3370548c4b73f76b4cfee457fc15c5fecda1ef47b7771cc8ee97d1077072f0ea'
            '9334d64241a081daf8e1ae0f2d62fd7e1f1ba2f29ba2932969c508c671f7d7bacf81376d6f5e376192b92cea0ae1559e086d9f105aa07db6d45a985e9c2aebfa')

prepare() {

  ln -s qcad-$pkgver qcad
  ln -s qtjsapi-$pkgver qtjsapi
  ln -s qcadjsapi-$pkgver qcadjsapi

}

build() {
  cd "$srcdir/qtjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  cmake \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DBUILD_QT6=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcadjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  # prepare the rest of the release items
  cp -a \
      examples \
      fonts \
      libraries \
      linetypes \
      patterns \
      scripts \
      themes \
      ts \
      plugins \
      platforminputcontexts \
      platforms \
      xcbglintegrations \
      release/
  cp readme.txt release/

}

package() {
  cd qcad-$pkgver

  install -dm755 "$pkgdir"/usr/lib/qcad
  cp -a release/* "$pkgdir"/usr/lib/qcad

  # install man
  install -Dm644 qcad.1 "$pkgdir"/usr/share/man/man1/qcad.1

  # desktop
  install -Dm644 scripts/qcad_icon.png "$pkgdir"/usr/share/pixmaps/org.qcad.QCAD.png
  install -Dm644 scripts/qcad_icon.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/org.qcad.QCAD.svg
  install -Dm644 qcad.desktop "$pkgdir"/usr/share/applications/qcad.desktop

  # qcad bin
  install -dm755 "$pkgdir/usr/bin"
  ln -sf /usr/lib/qcad/qcad-bin "$pkgdir/usr/bin/qcad"
}
