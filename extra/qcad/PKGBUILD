# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=3.32.5.0
pkgrel=1
pkgdesc='A 2D CAD package based upon Qt'
arch=('x86_64')
url="https://www.qcad.org"
license=('GPL-3.0-or-later')
options=('!staticlibs')
depends=('qt6-svg' 'gcc-libs' 'qt6-tools' 'qt6-shadertools' 'qt6-5compat' 'qt6-declarative')
makedepends=('glu' 'cmake' 'ninja' 'vulkan-headers')
source=(
    "$pkgname-$pkgver-$pkgrel.tar.gz::https://github.com/qcad/qcad/archive/refs/tags/v$pkgver.tar.gz"
    "qtjsapi-$pkgver.tar.gz::https://github.com/qcad/qtjsapi/archive/refs/tags/v$pkgver.tar.gz"
    "qcadjsapi-$pkgver.tar.gz::https://github.com/qcad/qcadjsapi/archive/refs/tags/v$pkgver.tar.gz"
)
sha512sums=('1decf7c735d21b7d423b455fa0deacc04ef59f4120633431c404025a9d87fd9f16ee5ddd355a99295d77bde0c0063c42d093a286b97e2918856c65b3f977de61'
            '5183eb91859a07b5a45454fa8a9bd2386b711f1af0e3788efe03a75030f718c7499d8c4e78ae77dc84351ebcf1c149f71455905f1773d25666648e78cbd5a440'
            '74d3097d959bbbfadef3489ae5892d8eacce8b886590f70bf81a15657c2a2e08e15ea8e388a73aabf80a98886677e52a486d25244a226e852272428ed725ebd6')

prepare() {

  ln -s qcad-$pkgver qcad
  ln -s qtjsapi-$pkgver qtjsapi
  ln -s qcadjsapi-$pkgver qcadjsapi

}

build() {
  cd "$srcdir/qtjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  cmake \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DBUILD_QT6=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcadjsapi-$pkgver"
  cmake \
      -DCMAKE_INSTALL_RPATH=/usr/lib/qcad \
      -DCMAKE_BUILD_WITH_INSTALL_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -G Ninja \
      .
  ninja -v $MAKEFLAGS

  cd "$srcdir/qcad-$pkgver"
  # prepare the rest of the release items
  cp -a \
      examples \
      fonts \
      libraries \
      linetypes \
      patterns \
      scripts \
      themes \
      ts \
      plugins \
      platforminputcontexts \
      platforms \
      xcbglintegrations \
      release/
  cp readme.txt release/

}

package() {
  cd qcad-$pkgver

  install -dm755 "$pkgdir"/usr/lib/qcad
  cp -a release/* "$pkgdir"/usr/lib/qcad

  # install man
  install -Dm644 qcad.1 "$pkgdir"/usr/share/man/man1/qcad.1

  # desktop
  install -Dm644 scripts/qcad_icon.png "$pkgdir"/usr/share/pixmaps/org.qcad.QCAD.png
  install -Dm644 scripts/qcad_icon.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/org.qcad.QCAD.svg
  install -Dm644 qcad.desktop "$pkgdir"/usr/share/applications/qcad.desktop

  # qcad bin
  install -dm755 "$pkgdir/usr/bin"
  ln -sf /usr/lib/qcad/qcad-bin "$pkgdir/usr/bin/qcad"
}
