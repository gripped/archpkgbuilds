# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-lumberjack
pkgver=1.3.1
pkgrel=1
pkgdesc='A simple, powerful, and fast logging utility with excellent structured logging support that can be a drop in replacement for the standard library Logger'
arch=(any)
url='https://github.com/bdurand/lumberjack'
license=(MIT)
depends=(ruby)
makedepends=(git)
checkdepends=(ruby-rspec ruby-timecop)
options=(!emptydirs)
source=(git+https://github.com/bdurand/lumberjack.git#tag=v$pkgver)
sha256sums=('872e8113c69cbe9d2ad0d71e1f498e82e12119d62b36fde2e8ab80a907a0c864')

build() {
  local _gemdir="$(gem env gemdir)"
  cd lumberjack
  gem build lumberjack.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    lumberjack-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd lumberjack
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd lumberjack
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT_LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
