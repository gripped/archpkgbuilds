# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=libsigrok
pkgver=0.5.2
pkgrel=25
pkgdesc='Client software that supports various hardware logic analyzers, core library'
arch=('x86_64')
url='https://www.sigrok.org/wiki/Libsigrok'
license=('GPL-3.0-or-later')
depends=(
  'bluez-libs'
  'gcc-libs'
  'glib2'
  'glibc'
  'glibmm'
  'hidapi'
  'libftdi'
  'libieee1284'
  'libserialport'
  'libsigc++'
  'libusb'
  'libzip'
)
makedepends=(
  'autoconf-archive'
  'cmake'
  'doxygen'
  'git'
  'jdk8-openjdk'
  'python'
  'python-gobject'
  'python-numpy'
  'python-setuptools'
  'ruby'
  'swig'
)
optdepends=(
  'jdk8-openjdk'
  'python'
  'ruby'
  'sigrok-firmware-fx2lafw: Cypress FX2-based device support'
)
options=(!lto) # FS#78381
source=(
  "git+git://sigrok.org/libsigrok#tag=libsigrok-$pkgver"
  "$pkgname-swig-4.1.patch"
  "$pkgname-fix-ruby-binding-location.patch"
  "$pkgname-python-3.14.patch"
)
b2sums=('cc85375263dac22adc2f826140d21e3b0643da4fdf59121c99b4220f09d123e29afe9933d7f47f1a0e71864f8afff6a23d75ac99449064023b7f35b5a726dede'
        '4dfed37681870e312acf8844aed8a69ef4998f446b8565552258c38d824f566257e559e5d909282bf2d8f1202019bcfc070722abba6b34ff36c4b81b6ffccc04'
        '348ba0e8615b81001444d6888eb392bfd5cc07fd71adff4bd65b6525a29cbf0cb76dd7b3aadd0971865a8fa482769d624d632c6fdd3e17b6fcaf2c75dff5aaab'
        '9141203f928d6152f04b3962015645369cfbc72e3b7649b34c6fc2e385ee0f47c120063148e2edb699f0e494b19a09a3e3a780630786bbd3266fc82d7902a446')

prepare() {
  cd $pkgname
  git config user.name builduser
  git config user.email builduser@archlinux

  # bindings/java: Fix build issue with SWIG 4.x.
  git cherry-pick e803574173bdac8a7f33085a648c29eaf248a394
  # bindings/ruby: Fix ruby SWIG bindings generation
  git cherry-pick 2e199405e53fee2fb3cad72858ebe7af6990bce0
  # bindings/python: rephrase for Python 3.9 deprecation (call API)
  git cherry-pick 5bc8174531df86991ba8aa6d12942923925d9e72 # Oct 2 18:33:08 2023

  # https://sigrok.org/bugzilla/show_bug.cgi?id=1827
  patch -p1 < ../$pkgname-swig-4.1.patch

  patch -p1 < ../$pkgname-fix-ruby-binding-location.patch
  patch -p1 < ../$pkgname-python-3.14.patch

  # regenerate ./configure so it can detect Python 3.10
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-ruby
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -vDm644 -t "$pkgdir/usr/lib/udev/rules.d" \
    contrib/60-libsigrok.rules \
    contrib/61-libsigrok-uaccess.rules
}
