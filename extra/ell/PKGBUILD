# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=ell
pkgver=0.60
pkgrel=1
pkgdesc="Embedded Linux library"
arch=(x86_64)
url="https://01.org/ell"
#url="https://git.kernel.org/pub/scm/libs/ell/ell.git/"
license=('LGPL2.1')
depends=('gcc-libs' 'glibc')
checkdepends=('dbus' 'procps-ng')
# https://mirrors.edge.kernel.org/pub/linux/libs/ell/sha256sums.asc
source=(https://mirrors.edge.kernel.org/pub/linux/libs/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('61cec2df694b548e51afa3e7ffd1e1ad31a9fea7bedb93a3a3cc60894390c70f'
            'SKIP')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659') # "Marcel Holtmann <marcel@holtmann.org>"

build() {
  cd "$pkgname-$pkgver"
 ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
