# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Kevin MacMartin <prurigro@gmail.com>

pkgname=qtpass
pkgver=1.4.0
pkgrel=4
pkgdesc='A multi-platform GUI for pass'
url='https://qtpass.org/'
license=('GPL-3.0-or-later')
arch=('x86_64')
depends=('qt6-base' 'qt6-svg' 'pass' 'pwgen')
makedepends=('qt6-tools')
validpgpkeys=('DF7FEF8DFBA721E320B18F5615B63ADCA0034B9E') # Anne Jan Brouwer (JinX) <brouwer@annejan.com>
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/IJHack/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/IJHack/${pkgname}/releases/download/v${pkgver}/QtPass-${pkgver}.tar.gz.asc"
        '0001-Fix-multiple-profiles-issue-for-Qt6.patch'
        '0002-fix-add-missing-QDirIterator-include.patch')
sha256sums=('9abe9cde35a412b26b6376a5e8996dfeeeb5910fe6a723b78bcf954656fca0e6'
            'SKIP'
            'a4332e146a53f8591bd48f8c85b85dfb8244d4de055fc60180dac39d31fb85de'
            'd17ea9ff5f8c1e2fcba1e1f503c5b312c04be2d74cdfebf5833a6c788f220d22')

prepare() {
  cd "${srcdir}/QtPass-${pkgver}"

  patch -Np1 < ../0001-Fix-multiple-profiles-issue-for-Qt6.patch
  patch -Np1 < ../0002-fix-add-missing-QDirIterator-include.patch

  sed -i 's|qtpass-icon|qtpass|' ${pkgname}.desktop
}

build() {
  cd "${srcdir}/QtPass-${pkgver}"

  qmake6
  make
}

package() {
  cd "${srcdir}/QtPass-${pkgver}"

  install -D -m0755 main/qtpass "${pkgdir}/usr/bin/${pkgname}"
  install -D -m0644 artwork/icon.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -D -m0644 ${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m0644 ${pkgname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

