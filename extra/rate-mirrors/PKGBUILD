# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Nikita Almakov <nikita.almakov@gmail.com>

pkgname=rate-mirrors
pkgver=0.27.0
pkgrel=1
pkgdesc="Everyday-use client-side map-aware mirror ranking tool"
arch=('x86_64')
url="https://github.com/westandskif/rate-mirrors"
license=('CC-BY-NC-SA-3.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cargo')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('466c9a2713f5d0dbf5ae755ee8635f4cbff8c5a873201bc906c58f123db665ea40d706617b5eb2de3776512985cdbcc703bfb83eaff8aefbb879506943519a04')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname-$pkgver
  export CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 target/release/rate_mirrors "$pkgdir/usr/bin/rate-mirrors"
}
