# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Rafael Dominiquini <rafaeldominiquini at gmail dot com>

pkgname=bazaar
pkgver=0.7.7
pkgrel=1
pkgdesc="A new app store for GNOME with focus on flatpaks, particularly Flathub"
arch=('x86_64')
url="https://github.com/kolunmi/bazaar"
license=(GPL-3.0-only)
depends=(
  glibc
  gcc-libs
  glib2
  gtk4
  glycin-gtk4
  json-glib
  glycin
  cairo
  pango
  graphene
  flatpak
  appstream
  dconf
  md4c
  webkitgtk-6.0
  hicolor-icon-theme
  libadwaita
  libdex
  libxmlb
  libyaml
  libsoup3
  libsecret
)
makedepends=(
  meson
  ninja
  gcc
  pkgconf
  git
  glib2-devel
  blueprint-compiler
)
optdepends=('krunner-bazaar: krunner integration')

source=(git+https://github.com/kolunmi/bazaar#tag=v${pkgver})
sha256sums=('41eaa2abd23d4f2ad8641b46010c64a50c38b6bc2c42830a29a8d0c4a6a2cf60')

build() {
  cd "${pkgname}"

  meson setup build --prefix=/usr
  ninja -C build
}

package() {
  cd "${pkgname}"

  DESTDIR="${pkgdir}" ninja -C build install
}
