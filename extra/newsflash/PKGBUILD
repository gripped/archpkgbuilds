# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Felix Bühler <account at buehler dot rocks>

pkgname=newsflash
pkgver=3.3.2
pkgrel=1
pkgdesc='Keep up with your feeds'
arch=('x86_64')
url='https://gitlab.com/news-flash/news_flash_gtk'
license=('GPL-3.0-only')
depends=(
  cairo
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libclapper
  libclapper-gtk
  libsoup3
  libxml2
  openssl
  pango
  sqlite
  webkitgtk-6.0
)
makedepends=(
  appstream-glib
  blueprint-compiler
  git
  glib2-devel
  meson
  rust
)
source=("git+https://gitlab.com/news-flash/news_flash_gtk.git#tag=v.${pkgver}")
b2sums=('7dd8ce1dfd8731d8ec9ef32b75822406d7240f510e9c2a8edff8f11dec614994c95953facc7e74d1feebab1d51cadc08b02f29d118c768df71231419ab5c51bc')

build() {
  # Used to encrypt user passwords.
  # DO NOT CHANGE
  export PASSWORD_CRYPT_KEY="ypsSXCLhJngks9qGUAqShd8JjRaZ824wT3e"

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  arch-meson news_flash_gtk build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  ln -s io.gitlab.news_flash.NewsFlash "$pkgdir/usr/bin/$pkgname"
}
