# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Felix Bühler <account at buehler dot rocks>

pkgname=newsflash
pkgver=3.3.5
pkgrel=2
pkgdesc='Feed reader application designed to complement an already existing web-based RSS reader account'
arch=(x86_64)
url='https://apps.gnome.org/NewsFlash/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libclapper
  libclapper-gtk
  libxml2
  openssl
  pango
  sqlite
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=(
  "git+https://gitlab.com/news-flash/news_flash_gtk.git#tag=v.$pkgver"
  newsflash-audio-playback.patch
  newsflash-dbus-activatable.patch
)
b2sums=(
  d38010f3328db2f5a722c1cf064160a14a01b0205a385dc318267645123f0841266106b73ae5757ea12b7519832d9b82a5d9a049534b6482f74ba0a5eb165e7f
  1cdbaff9c06b7b90289b70906b878d89da53777b4936b0e2825982d5e1ab80c72ab87bd72657dffbb1a1195cf168075a437e0ca55fcab918c5e7b2913596899a
  f921e7a36358ed2980243a621e33cf9cd1be1f8067842a88e75798396f33b35a807677c9d7d2c42166191627345a944acd536d51f6030cc5c4fc1c2b37577db7
)

prepare() {
  cd news_flash_gtk
  sed -i "s/version: '0.0.0'/version: '$pkgver'/" meson.build

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.com/news-flash/news_flash_gtk/-/merge_requests/234
  git apply -3 ../newsflash-audio-playback.patch

  # https://gitlab.com/news-flash/news_flash_gtk/-/merge_requests/242
  git apply -3 ../newsflash-dbus-activatable.patch
}

build() {
  # Used to encrypt user passwords.
  # DO NOT CHANGE
  export PASSWORD_CRYPT_KEY="ypsSXCLhJngks9qGUAqShd8JjRaZ824wT3e"

  arch-meson news_flash_gtk build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  ln -s io.gitlab.news_flash.NewsFlash "$pkgdir/usr/bin/$pkgname"
}
