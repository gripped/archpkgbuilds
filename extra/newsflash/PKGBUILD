# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Felix Bühler <account at buehler dot rocks>

pkgname=newsflash
pkgver=4.1.2
pkgrel=2
pkgdesc='Feed reader application designed to complement an already existing web-based RSS reader account'
arch=(x86_64)
url='https://apps.gnome.org/NewsFlash/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  cairo
  gcc-libs
  glib2
  glibc
  graphene
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libclapper
  libclapper-gtk
  libxml2
  openssl
  pango
  sqlite
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  clang
  git
  meson
  rust
)
source=("git+https://gitlab.com/news-flash/news_flash_gtk.git#tag=v.$pkgver")
b2sums=(61488826a1560e9fbad39eca07e97897b46a85a98e05c8905a8c99c9cff687d0d0029ea4a1df780cd822ac6753b53dede35c2d515c8e94f776b599bdec8953e5)

prepare() {
  cd news_flash_gtk
  sed -i "s/version: '0.0.0'/version: '$pkgver'/" meson.build

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  # Used to encrypt user passwords.
  # DO NOT CHANGE
  export PASSWORD_CRYPT_KEY="ypsSXCLhJngks9qGUAqShd8JjRaZ824wT3e"

  arch-meson news_flash_gtk build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  ln -s io.gitlab.news_flash.NewsFlash "$pkgdir/usr/bin/$pkgname"
}
