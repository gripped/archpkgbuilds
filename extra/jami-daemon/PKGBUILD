# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-daemon
pkgver=20260206
pkgrel=1
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL-3.0-or-later)
groups=(jami)
depends=(glibc opendht gnutls nettle libgit2 libsecp256k1 libsecp256k1.so ffmpeg speexdsp
         fmt yaml-cpp jsoncpp zlib alsa-lib libpulse jack libupnp libnatpmp
         openssl webrtc-audio-processing-0.3 systemd-libs libarchive)
# portaudio needs a not-yet-upstream patch https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/650
makedepends=(git cmake perl asio msgpack-c msgpack-cxx restinio systemd meson)
checkdepends=(cppunit)
_commit=55dd53736b9e6cd7204cc17e01d10d8a9276618f
_pjprojectver=59d9e1355686cf4f3f3d81d45560058354f42802
_dhtnetver=cff03260fa037f59d6768bac26014b01c07b3fb9
source=(git+https://git.jami.net/savoirfairelinux/${pkgname}.git#commit=${_commit}
        https://github.com/savoirfairelinux/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz
        dhtnet-$_dhtnetver.tar.gz::https://git.jami.net/savoirfairelinux/dhtnet/-/archive/$_dhtnetver/dhtnet-$_dhtnetver.tar.gz
        ffmpeg-7.patch
        ffmpeg-8.patch)
noextract=(pjproject-${_pjprojectver}.tar.gz
           dhtnet-${_dhtnetver}.tar.gz)
sha512sums=('e544bfac1eb2184f2b3a10d9e98a644068b5d2d5d622822e647d23b4ded21054d0e6e72defe7d74df4d2d44ca246643196228ccda19c41b7fbd4371ebfc36ce7'
            'c5966468225782bedfbed218e58ebe2491ba8fa897edc1590de22ffe513a7b1bf814c5e75761d980d651c9d56e5c4f64b16f68d413062a4a9960d53cdd0ae047'
            'SKIP'
            '6e927376938e6687a438b4984e37a23a921fb6b3e87f804b6eb9365af18b04bd5b601fd7be95ae326d8e5f15d82552ae7dd5367a1eb834bd0d7d77600e05c82e'
            '8fcfe52808d00b8535dc6d181af0233dc0f3a51e8da69728a04e7f6edc82c54fb11e62ffad05344d9eeb6d203dc323ffa03181c615c7c6f84682f70a02ee4319')

pkgver() {
  cd ${pkgname}
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

prepare() {
  cd ${pkgname}
  mkdir -p contrib/tarballs
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs/
  cp ../dhtnet-$_dhtnetver.tar.gz contrib/tarballs/
  mkdir -p contrib/native

  patch -p1 -i ../ffmpeg-7.patch # Fix build with ffmpeg 7+
  patch -p1 -i ../ffmpeg-8.patch # Fix build with ffmpeg 8
}

build() {
#  export CXXFLAGS+=" -std=c++17"
  cd ${pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject \
      --enable-dhtnet

  make DEPS_pjproject= DEPS_dhtnet=pjproject

  cd "${srcdir}"
  arch-meson ${pkgname} build -D interfaces=library -D opensl=disabled -D portaudio=disabled \
                              $( ((CHECKFUNC)) && echo -D tests=true) \
                              -D pkg_config_path="${PWD}/${pkgname}/contrib/$(cc -dumpmachine)/lib/pkgconfig"
  meson compile -C build
}

#check() {
# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/649
#  meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "${pkgdir}"
}
