# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=diesel-cli
pkgver=2.2.11
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache-2.0')
replaces=('diesel_cli')
depends=(
  'gcc-libs'
  'glibc'
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('fc93056ef1ac7ef70fb96dfe857cb4e574eed435b444a6f8d7daa309bd2b7e55096e000c4b7156d479348f9093b75252a5debe818892e0e391b70fbf3c9963c7'
            '11c13d9325de83f34b945803cb255bfab29c87b3290f5e2234b1d56771d482e8e8497aaf9759ac179777a5421a32ade65fdd64cc2133787455a89963a148adb2')
b2sums=('70374b6c7b2506b4f8a6f4ad856835bf97f0c30b1ed96bfbbe1591de5ea0cef8b9a648574851521b74b4cf4e08a3ca0ca95870e9852af28dfe717abd4f4e09ab'
        'a89f083b655eae19592a893a214a858e91d965f96958776c6c8716644e1684c72f6c4a9c39074c57da9527cc574889c85a32d51d673d8f8f702b7acf09fc6271')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
