# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=diesel-cli
pkgver=2.2.12
pkgrel=2
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache-2.0')
replaces=('diesel_cli')
depends=(
  'gcc-libs'
  'glibc'
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('f3297197e5787336215460cd7e51f69c74429c3f2526c02e0eb1335457a507dbc126c777e17c767e44a97277daf87f5e0114a64049a3966cd823ddee86711597'
            '74ff1e3e06048b981430551759c0c42d2fb51aa26286077f9bbcc031d8abe95319d90a3fe40d272038dec0c44019d88426260040cf23c856b66af25316a86e27')
b2sums=('08c1053aebc5576f8256e7f3fc46361dd8a06b501c156b119324f60b33a5757cbdb24b4bc97a713305e765b69c3958c79fe896365ca0b185b3010672fd846881'
        '7413f488fed759d151e3e486cefd36cc58791c8c9ae278191f34debdf16018b79b1b5ab7ae70cf418ddbeb60142eb77ead8a5f367d50a75892edd25fd68303d5')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
