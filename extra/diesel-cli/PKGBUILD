# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=diesel-cli
pkgver=2.2.0
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache-2.0')
replaces=('diesel_cli')
depends=(
  'gcc-libs'
  'glibc'
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('6cbab167a922f757e6744b85f788ce2aa3802659c4a200527a6533eb3fbb4bd70a8b3cc3b2a48cd48d5e642dff9d16305adfa9ed7c3bbb60f0bd59b510601e7e'
            '2db5888e07fd1db6940e9cbbf0aed4f499f8801a82236555b31e7216c4e7c926967f06c7d89894e6f95e2a01a1f6733562d7b0fe319558493c197f9cd3614b72')
b2sums=('3bddb1e46a722f5063fe783688eb3eeec365603bfa90180acddb6b220211c5aa272c771cc520353dc1140039f245ee8d645ec80b73e7560a656823858936a1da'
        '052af36f77723c9134570a7abcfddae97bcd02684ade809d1457eb25f78888efd82af1781757837c6b023944c6d5a212fa47e9fc3e1e341e8f1177f33ea983a6')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
