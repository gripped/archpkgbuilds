# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=diesel-cli
pkgver=2.2.8
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache-2.0')
replaces=('diesel_cli')
depends=(
  'gcc-libs'
  'glibc'
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('f1534ee67784dd5995372b88e1b7451bcfe77fbf167e0fe1bda699b763fa5acab02c9d2ce6abb402a749853680461b779b371feb9c39c9b4662f6c76595a28ff'
            'a0e1af073232bf5ac6d1cbc84a9f4f7992a456b2cfbb1d22239912d203e86fb9acb3ff268ad8620d2270f9e8cd5df280d938a0af29927c043cd7f725e21e93de')
b2sums=('146e8be2b2e580a92cb7730bc718a424c9fee4ffdfece9985d9bd1e66feb5b34d64765c8e10bce0e3237dbb0a375f26f7670bc0625e26ffd8e554a43f91eb386'
        'a946151a32776159b618d911f0389b54bf0c8c38c9d670076e94947478748be559e556301e7428234d2e3e9ec3c126d1ea404a51f0d72d5ea8806ce508185808')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
