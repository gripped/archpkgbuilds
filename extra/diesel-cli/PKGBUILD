# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=diesel-cli
pkgver=2.3.6
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache-2.0')
replaces=('diesel_cli')
depends=(
  'gcc-libs'
  'glibc'
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('ee6fd3388aa4b7b5c9b66e317b10349054f653a29807d710adf971a345a91f7d6d1a84c028714a3914412acf4fc5b17ce376781dab18a4c277141c2148bcc86d'
            '35195e7aeae661256fda6c7f7449bb8e0ef022a66eec7a637301f3c0c01fa91c100e53ec89191f78e189209103e24ee914a5dde6766aad8ebcef4d748b34e0ba')
b2sums=('fb3d0950c65d7131d1dd1f94cd0776d386b92d179a8ad15d02594b341f03b930e5c65324e313498e6f13e9044e040c2acbc3f73535caeac61a73b4254b137e9e'
        'f6ce3a505c4e8939a180efbe5fe3896fd6c8aff03f7651f2835be54b65a1e4c2a46be394593cd717810d422dd57d9a0c26e071f817c4485a69fa7aa9bf445195')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
