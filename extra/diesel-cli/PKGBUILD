# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=diesel-cli
pkgver=2.1.3
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=(
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('d184300cccc8c0179c251dc7629c6a4fddd5887f6632373e044b5cd475cdb55b90b2654456bd9bbc0ab94dda1ec5575451f65259dc0260c2c70b8f6d59b7c913'
            'ae124fbc27822b306b8129e207cb88fc894f9fe6db4a8f490ce7c982974ab753769de3f286a2f7f09c1e4aadbe44c542d09f136176bf7088cca3fa04ff55d807')
b2sums=('7feb664bbced8904272fb8feedda7f5c9a4a00d5755f04dbbc10bc920e8f13616f781cd1764f124c638c30166de52e01128576939683d3f10b3a2b7a3c005050'
        '5ab8b93c8a76032ef5b5b71d5c80e8ed9df3ed1f005774adbfa73cba6aff149341b3dc2f1fe4d3ba7e82bb60c69e8a44eac01aa9b402263283e08e371b88a027')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
