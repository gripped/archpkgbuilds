# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=5.2.0
pkgrel=1
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('x86_64')
url="https://kmymoney.org/"
license=('GPL')
depends=('aqbanking' 'gwenhywfar' 'libalkimia' 'sqlcipher' 'qt6-base' 'gpgmepp' 'karchive'
         'kcoreaddons' 'kconfig' 'kwidgetsaddons' 'ki18n' 'kcompletion' 'kcmutils' 'kitemmodels'
         'kitemviews' 'kxmlgui' 'ktextwidgets' 'kio' 'kholidays'
         'kdiagram' 'libical' 'shared-mime-info' 'libofx' 'qtkeychain-qt6')
makedepends=('extra-cmake-modules' 'kdoctools' 'doxygen')
optdepends=('perl: for financequote.pl')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('D69A745A55331F44F404D8258D4DE062AA2EB01C')
sha256sums=('c99d83c0a33efcebcb81196890b5c6e4366fc285840d136e13e622fbad4e9876'
            'SKIP')

prepare() {
# it docs fail to build
  rm -r $pkgname-$pkgver/po/it/docs
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_WITH_QT6=ON \
    -DCMAKE_SKIP_RPATH=YES \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
