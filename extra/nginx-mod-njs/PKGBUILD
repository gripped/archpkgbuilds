# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-njs
pkgver=0.8.10
pkgrel=3

_modname="${pkgname#nginx-mod-}"

pkgdesc='nginScript module for nginx'
arch=('x86_64')
depends=('glibc' 'libxslt' 'openssl' 'zlib' 'pcre2' 'libxml2')
makedepends=('nginx' 'nginx-src')
url="https://nginx.org/en/docs/njs_about.html"
license=('BSD-2-Clause')

source=(https://github.com/nginx/njs/archive/refs/tags/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0be4bdd79184cd43f55a0377e59107fddfeef33e54f97c890894f0fe72628d26')

prepare() {
  mkdir -p build
  cd build
  ln -sf /usr/src/nginx/auto
  ln -sf /usr/src/nginx/src
}

build() {
  cd "$srcdir/njs-$pkgver"
  CFLAGS="$CFLAGS -Wno-dangling-pointer -Wno-maybe-uninitialized" ./configure
  make njs

  # next configure cleans the build directory
  mv build/njs ../build/

  cd "$srcdir/build"
  /usr/src/nginx/configure \
    --with-ld-opt="$LDFLAGS" \
    --with-compat \
    --with-stream \
    --with-http_ssl_module \
    --with-http_v2_module \
    --add-dynamic-module=../njs-$pkgver/nginx
  make modules
}

package() {
  if [[ "$BUILDTOOL" == devtools ]]; then
    local nginx_dep="nginx=$(nginx -v 2>&1 | sed 's|.*/||')"
    depends+=($nginx_dep)
  else
    depends+=(nginx)
  fi

  install -Dm644 "$srcdir"/njs-$pkgver/LICENSE \
              "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -Dm755 build/njs "$pkgdir"/usr/bin/njs

  cd build/objs
  for mod in *.so; do
      install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done
}
