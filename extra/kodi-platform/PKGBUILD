# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-platform
_commit=280ff8a
pkgver=20250416.280ff8a
pkgrel=2
pkgdesc="platform library for external kodi addons"
arch=('x86_64')
url='https://github.com/xbmc/kodi-platform'
license=('GPL')
makedepends=('git' 'cmake' 'kodi-dev')
options=(!lto)
source=("$pkgname::git+https://github.com/xbmc/kodi-platform.git#commit=$_commit")
sha256sums=('a6045d005b467b0a3640bc68978afd63178ea0a5d08e97ad854235f3f38fa8c6')

pkgver() {
    cd "$pkgname"
    git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

build() {
    cd "$pkgname"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'p8-platform')
    cd "$pkgname"
    make DESTDIR="$pkgdir/" install
}

