# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-launcher
pkgver=1.0.0.beta.4
pkgrel=1
pkgdesc='Layer Shell frontend for Pop Launcher'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-launcher
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  pop-launcher
  wayland
)
makedepends=(
  cargo
  clang
  git
  intltool
  just
  lld
)
_tag=e92477a79b2decf9a434fb174f53592ff54a8ece
source=(
  git+https://github.com/pop-os/cosmic-launcher.git#tag=${_tag}
  cosmic-launcher-lto.patch
)
b2sums=('770aed294fd5a382ba2edb9715a8dfa2346516bcf2dfe99b9f3894ad3e26f1b33aa8b4c2959d1988b234cfc3d8ea1cc47ffbc6fce34c1c37906ca65bf1b0a4c2'
        'eb4bf64cbec00b5e6c2d3a332b38ec37dca374c0da16882c421fdea296a0c2a5c741ccd357b4d8cc1af6e82e1c06e5363121b6c03344e6af47025535a127fb8b')

prepare() {
  cd cosmic-launcher
  patch -Np1 -i ../cosmic-launcher-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-launcher
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}
build() {
  cd cosmic-launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
