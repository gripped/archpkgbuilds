# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-launcher
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='Layer Shell frontend for Pop Launcher'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-launcher
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  pop-launcher
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=5c9757b80803d26d20897baf003bcd2e458e14f8
source=(
  git+https://github.com/pop-os/cosmic-launcher.git#tag=${_tag}
  cosmic-launcher-lto.patch
)
b2sums=('831eccebfdaff8f6a1889eb6ce346d2905cef5d33a75bd9f43a36fb30d260044fc4d90b5fa53e2899fd6c26fb9ecc2fc2f742a6994b178a2d4f6a0a877cff049'
        'a27c9dbcf8912640bd5a8464f41bab0ae851254286a9a8c2d99d56aa1879745141da393fa6bc641bbe1fcdd614475d1f8d23cc937736d4cd3c5a0483d38cbabe')

prepare() {
  cd cosmic-launcher
  patch -Np1 -i ../cosmic-launcher-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-launcher
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}
build() {
  cd cosmic-launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
