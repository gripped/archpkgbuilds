# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-launcher
pkgver=1.0.0.alpha.6
pkgrel=1
pkgdesc='Layer Shell frontend for Pop Launcher'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-launcher
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  pop-launcher
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=b617a9dc68b2365e65b7820a6c81980159956a1c
source=(
  git+https://github.com/pop-os/cosmic-launcher.git#tag=${_tag}
  cosmic-launcher-lto.patch
)
b2sums=('e3e9fa81e16c816c9b77f07531df2ddefa98764c86ccb4b473bb2e4d86f35b6f51c3580a2ae3edac227f722af7f222ab42385607ebc3fb6aa8c8dba8fb97b076'
        '49035b03c601f05c322a0bfd3d837b09e34aaa3416a903fc1791e0eb75a07d1f53ded52c0bbcbd6c4c47d38fa32cb2ce89f3358e66d03cd4b9e3ce1b9d162894')

prepare() {
  cd cosmic-launcher
  patch -Np1 -i ../cosmic-launcher-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-launcher
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}
build() {
  cd cosmic-launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
