# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-launcher
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='Layer Shell frontend for Pop Launcher'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-launcher
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  pop-launcher
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=77ca2085554f1d8641f58d7954fd57be232e5006
source=(
  git+https://github.com/pop-os/cosmic-launcher.git#tag=${_tag}
  cosmic-launcher-lto.patch
)
b2sums=('27e653e1e546acc17785f64dda74c328fcaf04bb9d0371a9d3eb4143d3f745adafb4fa913126d1291697e6c8b4f74f6e28292cbeac1cb6dd107fbaa7877c6d7a'
        '49035b03c601f05c322a0bfd3d837b09e34aaa3416a903fc1791e0eb75a07d1f53ded52c0bbcbd6c4c47d38fa32cb2ce89f3358e66d03cd4b9e3ce1b9d162894')

prepare() {
  cd cosmic-launcher
  patch -Np1 -i ../cosmic-launcher-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-launcher
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}
build() {
  cd cosmic-launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
