# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-launcher
pkgver=1.0.0.alpha.5.1
pkgrel=1
pkgdesc='Layer Shell frontend for Pop Launcher'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-launcher
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  pop-launcher
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=70153f4021daa233e9d02d2d3a9b94f3e6fe3b96
source=(
  git+https://github.com/pop-os/cosmic-launcher.git#tag=${_tag}
  cosmic-launcher-lto.patch
)
b2sums=('a3de57f16d659a1c95fda4ea693ff3fd2f86dd69871fc6a2251feb83f9577c8f399919de71a5bfaa629acd0739660b9bf1887e4a3d64ef9c3a4afc09609017ef'
        '49035b03c601f05c322a0bfd3d837b09e34aaa3416a903fc1791e0eb75a07d1f53ded52c0bbcbd6c4c47d38fa32cb2ce89f3358e66d03cd4b9e3ce1b9d162894')

prepare() {
  cd cosmic-launcher
  patch -Np1 -i ../cosmic-launcher-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-launcher
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}
build() {
  cd cosmic-launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
