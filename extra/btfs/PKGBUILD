# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=3.1
pkgrel=1
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('x86_64')
url="https://github.com/johang/btfs"
license=('GPL-3.0-or-later')
depends=('fuse3' 'libtorrent-rasterbar' 'curl')
makedepends=('boost')
optdepends=('python: for btplay')
source=("$pkgname-$pkgver.tar.gz::https://github.com/johang/btfs/archive/v$pkgver.tar.gz")
sha512sums=('e21e1c56ce2cc171f046746cbae17060abde1269f5c48b4c1484ccba6afc486d969c1402ef5282c1de4e019960f48922e70c9c120ecc254798c19d15ee270380')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -i
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
