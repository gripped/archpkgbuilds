# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=forgejo
pkgver=13.0.3
pkgrel=2
pkgdesc='A lightweight software forge'
arch=(x86_64)
url='https://forgejo.org'
license=(GPL-3.0-or-later)
depends=(
  glibc
  git
)
makedepends=(
  go
  nodejs
  npm
)
checkdepends=(openssh)
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'valkey: Redis support'
  'sqlite: SQLite support'
)
backup=(etc/forgejo/app.ini)
options=(!lto !debug)
source=(
  "$pkgname::git+https://codeberg.org/forgejo/forgejo#tag=v$pkgver"
  systemd.service
  sysusers.conf
  tmpfiles.conf
  use-correct-variables.patch
)
sha512sums=('81b7bc899c92b1fb9fe93577ff71697802ea3c8c48a354e4a87d3bc63cb8c4d129373c529ef1d98941e6fdd721a5704bb36b5d4ae83bcd567131d0bb66810eb5'
            '2dff4cc368a9b1c222dad51ca9cc476b1585ef214bd5563941124129278c709d1c6513417c4fdc38550dea78d1a4cfa6aaab7a1ad882369621057947b683f55d'
            '7eb2766c06bb0e104da5860c16fbb9743220b01eec2760a84353b315d0d91a03a10939a105a8b06c6e074782cb76d26e0af8bf8f10881fef6e942dc43300208a'
            '2adee9a7a942a79fb6683699c58d4e256f5431540e1a374d20fcb4f384d06ed118d284f7ddca35f8620afdd211d6c83492485cd01477124f13111d8c65462f29'
            '650ffb26a854ee42384fc4c9f322d64b94179bb4a9ddae4e2324e30ad8c57252099a3f3d952bf63de479ea67ad0fa88abe3b1b40a21c4949c7a9943f16af291a')
b2sums=('a2214b12b52628a752939a4239434c96cb91a5ba8647a41a84c5084ffc5564c73488a15cbe8d7de1d57a7b42206094c284e0b2031f8f8979fb7fe388e5a54d7d'
        '4f8ce1a1e726540eee9ac807b5177db61f824c98f36ea03ca1f4d4ac474a600083156c090986948fe049e4433ffac9355518e1e463cf8714aca8b1f83d8b69bf'
        '939ba4a1cc6862cbe592b930a613694e79457a51c21c258ec01a0baa84d358286db2a457f78e8cc18daac1f8bb7df170d00e57ce877ab486a31a44e707f5e7e8'
        'f16e689ced1834b4638ab2f5cfe9870148d7f4e943a753014a27122e5bd08beecbc84164b6e88e6e171eab30d75220ffa4cb8891b9012c8fc4312e58ff6be08b'
        '3f536bc7d624647a5aa695ebea86a96f23186541e5c02deac714b6b932e9dc67960c38d83028e578afa8a2aa390481c621698cd1f848cf8f5b1a4946b48105f4')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/use-correct-variables.patch"

  make deps-frontend deps-backend deps-tools
}

build() {
  cd "$pkgname"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export EXTRA_GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export LDFLAGS="-X 'code.gitea.io/gitea/modules/setting.AppWorkPath=/var/lib/forgejo/' -X 'code.gitea.io/gitea/modules/setting.CustomConf=/etc/forgejo/app.ini'"
  export TAGS="bindata sqlite sqlite_unlock_notify pam"

  make build
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd "$pkgname"

  # binary
  install -vDm755 gitea "$pkgdir/usr/bin/$pkgname"

  # configuration
  install -vDm644 custom/conf/app.example.ini "$pkgdir/etc/$pkgname/app.ini"
}
