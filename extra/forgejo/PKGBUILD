# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=forgejo
pkgver=13.0.4
pkgrel=1
pkgdesc='A lightweight software forge'
arch=(x86_64)
url='https://forgejo.org'
license=(GPL-3.0-or-later)
depends=(
  glibc
  git
)
makedepends=(
  go
  nodejs
  npm
)
checkdepends=(openssh)
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'valkey: Redis support'
  'sqlite: SQLite support'
)
backup=(etc/forgejo/app.ini)
options=(!lto !debug)
source=(
  "$pkgname::git+https://codeberg.org/forgejo/forgejo#tag=v$pkgver"
  systemd.service
  sysusers.conf
  tmpfiles.conf
  use-correct-variables.patch
)
sha512sums=('febbd7e4846b47616164786e7ad103b3ab375cacd29628ef1985678824e461d93334cd3f115af39854f54014e997b982a9d400b3823644619a42afdc24ae003e'
            'a88aefed4aaee9c4d356444a2b3cbad384e07d738da4c87c7298744222ed784c6d5079d74596b8440605da0ad85699ced20106897c345a12994bd1d3ee84da37'
            '7eb2766c06bb0e104da5860c16fbb9743220b01eec2760a84353b315d0d91a03a10939a105a8b06c6e074782cb76d26e0af8bf8f10881fef6e942dc43300208a'
            'f18cbfa60912221b29e3b3b824c77571797ac76b90cacd28f2014f295c90c964990d4d5079822d280681fa6b0bb80248cd98d0479a3ac6441a3eaf8179f640a3'
            'bbf13b108070b18af7c6aaa86166f886d6b4fa4f2aaa09e66d234a660c7637a812a515d7200fbe2900f77d1e8a2265286e14979c287382d355b8ad6551fbaa9f')
b2sums=('1b06c41416aef4d8604ea30f83d74397a4297fb7abf14fbf243d5a7e725cfd946ae50545c9adfdefc0d02b1b1bf2b860ac8e8f9de5ac799702d5df31b6244889'
        '8233177c7931b8baa046f58b086f733b3dd36ab42e50173724f6d4ab6be85dda2c674a9d0c6f3d7393a5c4f751af889972a0633828f40dc446f1b276a8c9fba2'
        '939ba4a1cc6862cbe592b930a613694e79457a51c21c258ec01a0baa84d358286db2a457f78e8cc18daac1f8bb7df170d00e57ce877ab486a31a44e707f5e7e8'
        '1e1b1fa2d31a87913625f0a235524d9131e26108414d06f91e61bc5134ebf8f570ee599ddb80956821dbbfa565dba09d4b046bafd336d4b0a097bb859da3e183'
        '1e487218206a9a914f503f369d4975bb0e312b4c39cb29ef3910e50ca81f0ec61ce07e70eff2ba4ccbbcafcade0bb4d261db2d3fccc04afbbe831b474ba9ea91')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/use-correct-variables.patch"

  make deps-frontend deps-backend deps-tools
}

build() {
  cd "$pkgname"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export EXTRA_GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export LDFLAGS="-X 'code.gitea.io/gitea/modules/setting.AppWorkPath=/var/lib/forgejo/' -X 'code.gitea.io/gitea/modules/setting.CustomConf=/etc/forgejo/app.ini'"
  export TAGS="bindata sqlite sqlite_unlock_notify pam"

  make build
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd "$pkgname"

  # binary
  install -vDm755 gitea "$pkgdir/usr/bin/$pkgname"

  # configuration
  install -vDm644 custom/conf/app.example.ini "$pkgdir/etc/$pkgname/app.ini"
}
