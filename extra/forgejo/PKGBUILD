# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=forgejo
pkgver=12.0.0
pkgrel=1
pkgdesc='A lightweight software forge'
arch=('x86_64')
url='https://forgejo.org'
license=('GPL-3.0-or-later')
depends=('glibc' 'git')
makedepends=('go' 'nodejs' 'npm')
checkdepends=('openssh')
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'valkey: Redis support'
  'sqlite: SQLite support'
)
backup=('etc/forgejo/app.ini')
options=('!lto' '!debug')
source=(
  "$pkgname::git+https://codeberg.org/forgejo/forgejo#tag=v$pkgver"
  'systemd.service'
  'sysusers.conf'
  'tmpfiles.conf'
)
sha512sums=('4d9195146901c514652d4988a17b4af04cd12c4efca33f7842380db7632b3c293def9bc518ffb46c19b6dfdcc3999a8215b1660233a30434478e3537840a1e1e'
            '91a5333ac5d9b249568aa21d93a46bc390755ece4d6aa437089c4509bcd4ad3e9617bd128faff724f1e8825b9728ee52e4805e82094224c58799f32118b50833'
            '7eb2766c06bb0e104da5860c16fbb9743220b01eec2760a84353b315d0d91a03a10939a105a8b06c6e074782cb76d26e0af8bf8f10881fef6e942dc43300208a'
            '2adee9a7a942a79fb6683699c58d4e256f5431540e1a374d20fcb4f384d06ed118d284f7ddca35f8620afdd211d6c83492485cd01477124f13111d8c65462f29')
b2sums=('f40371ee3298e6b8d4edb4910a2482701f2cf33c2f493f697f1c32029ff0c08cf224ca88c4769148460828e38946338fc244d99d42f03a786ab8532f2fa58151'
        'e1222485c0dfe63fffeb6ce03c09bd025b8c170c829c48289ce5edfdafca315097b6d532a35b6a1f423866a2982afb6416e52441e2b38e78f1e468b966ad152b'
        '939ba4a1cc6862cbe592b930a613694e79457a51c21c258ec01a0baa84d358286db2a457f78e8cc18daac1f8bb7df170d00e57ce877ab486a31a44e707f5e7e8'
        'f16e689ced1834b4638ab2f5cfe9870148d7f4e943a753014a27122e5bd08beecbc84164b6e88e6e171eab30d75220ffa4cb8891b9012c8fc4312e58ff6be08b')

prepare() {
  cd "$pkgname"
  make deps-frontend deps-backend deps-tools
}

build() {
  cd "$pkgname"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export EXTRA_GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export LDFLAGS="-X 'code.gitea.io/gitea/modules/setting.AppWorkPath=/var/lib/forgejo/' -X 'code.gitea.io/gitea/modules/setting.CustomConf=/etc/forgejo/app.ini'"
  export TAGS="bindata sqlite sqlite_unlock_notify pam"

  make build
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd "$pkgname"

  # binary
  install -vDm755 gitea "$pkgdir/usr/bin/$pkgname"

  # configuration
  install -vDm644 custom/conf/app.example.ini "$pkgdir/etc/$pkgname/app.ini"
}
