# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=forgejo
pkgver=10.0.3
pkgrel=2
pkgdesc='A lightweight software forge'
arch=('x86_64')
url='https://forgejo.org'
license=('GPL-3.0-or-later')
depends=('glibc' 'git')
makedepends=('go' 'nodejs' 'npm')
checkdepends=('openssh')
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'valkey: Redis support'
  'sqlite: SQLite support'
)
backup=('etc/forgejo/app.ini')
options=('!lto' '!debug')
source=(
  "$pkgname::git+https://codeberg.org/forgejo/forgejo#tag=v$pkgver"
  'systemd.service'
  'sysusers.conf'
  'tmpfiles.conf'
)
sha512sums=('ba0f67a247951d253f0916106dd02dc7f15c251157ebb4cb7808c6cf1a636314eb7c55305fadb7212fd9f710d2e5f60046889cfe6525528fb3c5acd57fe642e6'
            '91a5333ac5d9b249568aa21d93a46bc390755ece4d6aa437089c4509bcd4ad3e9617bd128faff724f1e8825b9728ee52e4805e82094224c58799f32118b50833'
            '7eb2766c06bb0e104da5860c16fbb9743220b01eec2760a84353b315d0d91a03a10939a105a8b06c6e074782cb76d26e0af8bf8f10881fef6e942dc43300208a'
            '9a3aa163892eaa889e74d066db9d620db098535b08fa51df689e7aa5885393a14b820308364196db54d7ce502791ea56b662d8aede17fad99f8f62d1a3ca6776')
b2sums=('35def55f66d786e94e83b7b32fe489c6da4f5fd4131b45201d030db19f2c54e20e2c7cf458678742a7c403ffc4fb01d9204e0592fa2bfc7e96eaf374355e29b7'
        'e1222485c0dfe63fffeb6ce03c09bd025b8c170c829c48289ce5edfdafca315097b6d532a35b6a1f423866a2982afb6416e52441e2b38e78f1e468b966ad152b'
        '939ba4a1cc6862cbe592b930a613694e79457a51c21c258ec01a0baa84d358286db2a457f78e8cc18daac1f8bb7df170d00e57ce877ab486a31a44e707f5e7e8'
        'f48abdd1d207ee61b1f688e2050adaac30eb19514d1d7ef095cf2e92326f4218a16e37a8910d9eeabc17fda2dbf2e4709961560b5ecd3f06187eddde3312702c')

prepare() {
  cd "$pkgname"
  make deps-frontend deps-backend deps-tools
}

build() {
  cd "$pkgname"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export EXTRA_GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export LDFLAGS="-X 'code.gitea.io/gitea/modules/setting.AppWorkPath=/var/lib/forgejo/' -X 'code.gitea.io/gitea/modules/setting.CustomConf=/etc/forgejo/app.ini'"
  export TAGS="bindata sqlite sqlite_unlock_notify pam"

  make build
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd "$pkgname"

  # binary
  install -vDm755 gitea "$pkgdir/usr/bin/$pkgname"

  # configuration
  install -vDm644 custom/conf/app.example.ini "$pkgdir/etc/$pkgname/app.ini"
}
