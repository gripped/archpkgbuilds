# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-terminal
pkgver=4.0.1
pkgrel=1
pkgdesc='Terminal support components for Maui applications'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         kpty
         mauikit
         qt6-5compat
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7e69f8ec5eb12e842e4465b4f9eb269aad4af2965cd67715658072dc55944ee1'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
