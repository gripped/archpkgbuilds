# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Dmytro Meleshko <qzlgeb.zryrfuxb@tznvy.pbz(rot13)>

pkgname=eslint-language-server
pkgver=3.0.20
pkgrel=1
pkgdesc='Language server for ESLint'
arch=(any)
url=https://github.com/microsoft/vscode-eslint
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  npm
)
provides=("vscode-$pkgname")
source=("git+$url.git#tag=release/$pkgver")
b2sums=('23c3d8836cb9c11447eaef1fa483b75510487dd2ed31ce1d0877ef0af76c419e34c0944d0921413290203ddb063cddaa1a14aa884018c5e4c1ff796242a29ec4')

prepare() {
  cd vscode-eslint
  npm ci
}

build() {
  cd vscode-eslint
  npm run compile:server
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  printf '%s\n' '#!/usr/bin/env node' "require('$mod_dir/out/eslintServer.js');" >"$pkgdir"/usr/bin/$pkgname
  chmod +x "$pkgdir"/usr/bin/$pkgname
  ln -s $pkgname "$pkgdir"/usr/bin/vscode-$pkgname

  cd vscode-eslint
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname License.txt

  cd server
  cp -r node_modules out package.json "$pkgdir"/$mod_dir
}
