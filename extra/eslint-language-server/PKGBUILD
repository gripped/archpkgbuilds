# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Dmytro Meleshko <qzlgeb.zryrfuxb@tznvy.pbz(rot13)>

pkgname=eslint-language-server
pkgver=3.0.16
pkgrel=1
pkgdesc='Language server for ESLint'
arch=(any)
url=https://github.com/microsoft/vscode-eslint
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  npm
)
provides=("vscode-$pkgname")
source=("git+$url.git#tag=release/$pkgver")
b2sums=('b2c8f6764329bf5f1760bff5500febbb310361d206464b929318081b88aa02ffba02d0107d1de5d8b7e81298e4865757442f71adbc3991275365130017595806')

prepare() {
  cd vscode-eslint
  npm ci
}

build() {
  cd vscode-eslint
  npm run compile:server
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  printf '%s\n' '#!/usr/bin/env node' "require('$mod_dir/out/eslintServer.js');" >"$pkgdir"/usr/bin/$pkgname
  chmod +x "$pkgdir"/usr/bin/$pkgname
  ln -s $pkgname "$pkgdir"/usr/bin/vscode-$pkgname

  cd vscode-eslint
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname License.txt

  cd server
  cp -r node_modules out package.json "$pkgdir"/$mod_dir
}
