# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-logs
pkgver=45.0
pkgrel=2
pkgdesc="A log viewer for the systemd journal"
url="https://apps.gnome.org/Logs"
arch=(x86_64)
license=(GPL)
depends=(
  dconf
  glib2
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  systemd
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-logs.git#tag=$pkgname-$pkgver"
        gnome-logs-window-icon.patch
        gnome-logs-quit.patch
        gnome-logs-info-bar.patch
        gnome-logs-invalid-open.patch
        gnome-logs-new-window.patch)
b2sums=('e6aae0af3137c6c96014b00da25fe27b32d90b0b227fb36c376e583f17ce1e9faebc1baeaa51e7ce62c79620f9c16b1831c9c20be55f09d9de9b700a802c9f35'
        '81bb65eda96215d04d5798341c1eca0cdb8ff262c1cf3e2c2f5abae25f8a000bda59015f5314aefce93ac3c47bb58e997b692de5acc1880b9dfbbb919c1165e9'
        '809655260a1fca0bb6870c8fd5c05b06b89f518d038bfb2143ee9ee68eb3cb21d5ecb7288e6ba48b3de59b80219c4515f60558afb53cea0660ba3d5f8e02caa1'
        '552bf39c8609dcfc9cdc18f113181d315eaecedf14413f696888d9e0e5848e760d299f3f7ea8c91899c302b7368871f2734bad1823bcceca980799bcc082a2e5'
        'b6f56246b865a6c72a55b7db4a78fb584b3d1b71a67a33c79eb87ebabdd52be0a5bd408cb83d6456227af2b92121a22f6ee971d4c2fe48653f11e04334da84a8'
        '2d1d84369c4980c5160108c92e3b7e5dd52492d5c4bfd0a463ee016aac227da467676d8621ffb6b9a53250d3e4c3ef9e0a472ce8534e208c2075811e44d719da')

prepare() {
  cd $pkgname

  # Fix icon name
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/66
  git apply -3 ../gnome-logs-window-icon.patch

  # Add CTRL+Q to quit
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/71
  git apply -3 ../gnome-logs-quit.patch

  # Fix GtkInfoBar being blank
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/73
  git apply -3 ../gnome-logs-info-bar.patch

  # Fix crash when invalid journal opened
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/77
  git apply -3 ../gnome-logs-invalid-open.patch

  # Add "new-window" command-line option
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/78
  git apply -3 ../gnome-logs-new-window.patch
}

build() {
  local meson_options=(
    -D man=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
