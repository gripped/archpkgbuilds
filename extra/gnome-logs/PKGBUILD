# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-logs
pkgver=49.0
pkgrel=1
pkgdesc="A log viewer for the systemd journal"
url="https://apps.gnome.org/Logs"
arch=(x86_64)
license=(GPL-3.0-only)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  systemd
  systemd-libs
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-logs.git#tag=${pkgver/[a-z]/.&}"
  0001-gl-application-Add-CTRL-Q-to-quit.patch
  0002-gl-window-Fix-GtkInfoBar-being-blank.patch
  0003-Fix-crash-when-invalid-journal-opened.patch
  0004-Add-new-window-command-line-option.patch
)
b2sums=('b234d768ddc1c6e051ff9a0ec8702860ee937f95fd0a04e12616076988af8c0263c602cfc5cdee16948c38202ad04b57adec363bd58de6f1eeecd0d60a869700'
        'de9c538a53c0dd7fda5f38bc38360a8564df8b10a65201e08718e03b3795b90db8d8162bbfe1255929eba2728d679375d92e46f0e7e4bc430720a1c2567d771c'
        '88925a3383b6ad2a564b5aa6d2450e0f3976c07f7e1011ab7f72f50aae9b8d3195985e57a08ed8e4d148d9fbb0ff9357e4a27e88739630291eda6d56299ed831'
        '45ec10e5f31d25f27b4ad28422432ef7c7e7a0a2697bbbc9c09a5f12b3a703cff9255080c5d5d42fbeda1b2927216fecf9393fc6d27cb3873238e9653c6a0bd3'
        '2cfb6e1b842098bd616992d175317127e8f6744ec23c7a85a4471b561a31e465be2b4c40ce35716bc9ef075953ecdd3ad911fb898ad3739707af0ce3bb902d93')

prepare() {
  cd $pkgname

  # Add CTRL+Q to quit
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/71
  git apply -3 ../0001-gl-application-Add-CTRL-Q-to-quit.patch

  # Fix GtkInfoBar being blank
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/73
  git apply -3 ../0002-gl-window-Fix-GtkInfoBar-being-blank.patch

  # Fix crash when invalid journal opened
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/77
  git apply -3 ../0003-Fix-crash-when-invalid-journal-opened.patch

  # Add "new-window" command-line option
  # https://gitlab.gnome.org/GNOME/gnome-logs/-/merge_requests/78
  git apply -3 ../0004-Add-new-window-command-line-option.patch
}

build() {
  local meson_options=(
    -D man=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
