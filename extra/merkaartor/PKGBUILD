# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Michael Kanis <mkanis gmx de>
# Contributor: Paulo Freire <paulofreire gmail com>
# Contributor: Brice Maron <brice bmaron net>

pkgname=merkaartor
pkgver=0.20.0
pkgrel=19
pkgdesc='OpenStreetMap editor'
arch=('x86_64')
url='http://merkaartor.be/'
license=('GPL-2.0-or-later')
depends=('exiv2' 'gdal' 'gpsd' 'hicolor-icon-theme' 'qt6-svg' 'qt6-networkauth' 'qt6-5compat' 'protobuf')
makedepends=('qt6-tools' 'cmake')
source=("https://github.com/openstreetmap/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        'no-credentials.patch'
         https://github.com/openstreetmap/merkaartor/commit/28cca84e.patch)
sha256sums=('31b73a9d50cb6366a7c15ab36e030467ffe9f7de2be38bad5f3832314d4a5751'
            'facdf3c18e504bd5ac44a3cb9f2a9525e26ff55d66276f8249eb067406730a2a'
            '4e74718b786e12ad3751532e5be7cccd9353e5a7fb9fe550d9a7982ceabb03e3')

prepare() {
  cd $pkgname-$pkgver

  # Fixed a crash on download when no server is loaded from config
  # https://github.com/openstreetmap/merkaartor/pull/308
  patch -Np1 -i ../no-credentials.patch
  # Fix build with gdal 3.12
  patch -p1 -i ../28cca84e.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGPSD=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
