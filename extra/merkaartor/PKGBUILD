# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Michael Kanis <mkanis gmx de>
# Contributor: Paulo Freire <paulofreire gmail com>
# Contributor: Brice Maron <brice bmaron net>

pkgname=merkaartor
pkgver=0.20.0
pkgrel=12
pkgdesc='OpenStreetMap editor'
arch=('x86_64')
url='http://merkaartor.be/'
license=('GPL2')
depends=('exiv2' 'gdal' 'gpsd' 'hicolor-icon-theme' 'qt6-svg' 'qt6-networkauth' 'qt6-5compat' 'protobuf')
makedepends=('qt6-tools' 'cmake')
source=("https://github.com/openstreetmap/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('31b73a9d50cb6366a7c15ab36e030467ffe9f7de2be38bad5f3832314d4a5751')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGPSD=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
