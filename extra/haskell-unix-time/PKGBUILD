# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=unix-time
pkgname=haskell-unix-time
pkgver=0.4.17
pkgrel=37
pkgdesc="Unix time parser/formatter and utilities"
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-old-time')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-hspec' 'haskell-old-locale'
             'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('9e1da62120c0eeb7dd5bc53d24214f9a1007304f957761933d7c74fc03335056b7be5520461bf53bd5fea87221c2a2590edf648c631887c641f6b264b522a32e')

build() {
    cd $_hkgname-$pkgver
    
    LDFLAGS="${LDFLAGS/-Wl,-z,pack-relative-relocs/}" \
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
