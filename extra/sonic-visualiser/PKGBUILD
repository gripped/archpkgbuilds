# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

pkgname=sonic-visualiser
pkgver=5.0.1
pkgrel=4
pkgdesc="A viewer and analyser of music audio files."
arch=(x86_64)
url="https://www.sonicvisualiser.org/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libmad
  opusfile
  qt6-base
  qt6-svg
  vamp-plugin-sdk
)
makedepends=(
  alsa-lib
  bzip2
  capnproto
  fftw
  libfishsound
  libid3tag
  liblo
  liblrdf
  liboggz
  libpulse
  libsamplerate
  libsndfile
  meson
  portaudio
  rubberband
  serd
  sord
)
provides=(vamp-host)
source=(https://github.com/$pkgname/$pkgname/releases/download/sv_v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('eb58403b3d680bbe9b6b30a2ee1899db71f245d6b7b153caa04953268bff1bb52da069d63d2e062ebb14e01a4474214de6770e389b6d611990db7fab8fd6ecc6')
b2sums=('8c3b87aaae14d2ec3284c75b7a4c7a977bd54ede14a6c8c6d8a4f1bd679d051c6abc12045003940537d2ce86f20c33103fee6a90b56947b17f917e7deff7faad')

build() {
  arch-meson build $pkgname-$pkgver
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    alsa-lib libasound.so
    bzip2 libbz2.so
    capnproto libcapnp.so libkj.so
    fftw libfftw3.so
    libfishsound libfishsound.so
    libid3tag libid3tag.so
    liblo liblo.so
    liblrdf liblrdf.so
    liboggz liboggz.so
    libpulse libpulse.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
    portaudio libportaudio.so
    rubberband librubberband.so
    serd libserd-0.so
    sord libsord-0.so
  )

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/samples/* -t "$pkgdir/usr/share/$pkgname/samples"
  install -vDm 644 $pkgname-$pkgver/templates/* -t "$pkgdir/usr/share/$pkgname/templates"
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,CITATION,README{,_OSC}.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
