# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=zita-rev1
pkgver=0.2.2
pkgrel=4
pkgdesc="A reworked jack version of the reverb originally developed for Aeolus."
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio/zita-rev1-doc/quickguide.html"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  cairo
  clxclient
  clthreads
  gcc-libs
  glibc
  jack
  libpng
  libx11
)
makedepends=(gendesk)
source=(https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2)
sha512sums=('7ba145da6eb3163c84c8efac59c2bff3973d215caa1e4ffc9d6b9bf4eab50cc57133a439bde216724edbb0268366891bc2ddb1950c46804debde7404ce171279')
b2sums=('f65c63bfaa660c1d63a7156953f7279e3c4f796bba12f1458b8ece0dac74d4f6117c080a5c9af805eebd30bb4f169f9837c0db1cad5bc6f7fdba9dc2d538478b')

prepare() {
  # strip march=native
  sed -e '/native/d' \
      -i $pkgname-$pkgver/source/Makefile
  # XDG desktop files
  gendesk -n \
          --pkgname $pkgname \
          --pkgdesc "$pkgdesc" \
          --name zita-rev1 \
          --categories "AudioVideo;Audio"
}

build() {
  make PREFIX=/usr -C $pkgname-$pkgver/source
}

package() {
  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname-$pkgver/source
  # XDG desktop integration
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # docs
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,README,doc/*} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
