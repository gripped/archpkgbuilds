# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-class-methodmaker
pkgver=2.25
pkgrel=1
pkgdesc="Create generic class methods"
arch=('x86_64')
url="https://search.cpan.org/dist/Class-MethodMaker"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SC/SCHWIGON/class-methodmaker/Class-MethodMaker-${pkgver}.tar.gz")
sha256sums=('70bd3a6595cc40e54a9521eae3247e7d69166e6783ea5faebd59b84537e1b588')

build() {
  cd  "$srcdir"/Class-MethodMaker-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor 
  make
}

package() {
  cd  "$srcdir"/Class-MethodMaker-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
