# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Marcos Heredia <chelqo@gmail.com>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: orelien <aurelien.foret@wanadoo.fr>
# Contributor: Muflone <webreg@vbsimple.net>

pkgname=httrack
pkgver=3.49.5
pkgrel=1
pkgdesc="An easy-to-use offline browser utility"
arch=('x86_64')
url="https://www.httrack.com/"
license=('GPL-3.0-or-later')
depends=('bash' 'xdg-utils' 'hicolor-icon-theme' 'openssl')
makedepends=('git' 'patch')
source=($pkgname::git+https://github.com/xroche/httrack#tag=$pkgver
        missing-file.patch::https://github.com/xroche/httrack/commit/ce2d2ce810cb1bbaf1c587a3947c31adb47d5244.patch)
# validpgpkeys=('BB71C7E6CB1AD8FAF53FE42A60C3AA7180598EFB') # Xavier Roche
sha256sums=('834b168b28892cfb8bd10d4f3ebe931cdb07a9979028aab7e8d72541549f4de0'
            '6d221b14289c916ea4e71431af57e6ceae3702cebc0bbcbb7253e23ea46844d9')

prepare() {
  cd "${srcdir}"/$pkgname

  git submodule update --init --recursive

  patch -p1 -i ../missing-file.patch
}

build() {
  cd "${srcdir}"/$pkgname

  ./configure --prefix=/usr --enable-static=no
  make
}

package() {
  cd "${srcdir}"/$pkgname

  make DESTDIR="${pkgdir}" install
}
