# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=crate
pkgname=python-$_pyname
pkgver=1.0.1
pkgrel=3
pkgdesc='A Python client library for CrateDB'
arch=(any)
url="https://github.com/crate/crate-python"
license=(Apache-2.0)
depends=(python
         python-urllib3)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
optdepends=(python-sqlalchemy)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('943326ec591d4a0bf237483199964777d59b4973d951e8a5d7933358edeeca7b')

build() {
	cd "$_archive"
	python -m build -wn
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" {CONTRIBUTING,DEVELOP,README}.rst
}
