# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=crate
pkgname=python-$_pyname
pkgver=2.0.0
pkgrel=3
pkgdesc='A Python client library for CrateDB'
arch=(any)
url="https://github.com/crate/crate-python"
license=(Apache-2.0)
depends=(python
         python-orjson
         python-urllib3
         python-verlib2)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
optdepends=(python-sqlalchemy)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('b9f08842f2ee0dd2fa5c6b7d7f6d627e9c86d8cf8894cf56d50b817d679860c6')

build() {
	cd "$_archive"
	python -m build -wn
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" {CONTRIBUTING,DEVELOP,README}.rst
}
