# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-flask-marshmallow
pkgver=1.2.1
pkgrel=4
pkgdesc='Flask + marshmallow for beautiful APIs'
url=https://flask-marshmallow.readthedocs.io/
arch=(any)
license=(MIT)
depends=(
  python
  python-flask
  python-marshmallow
  python-werkzeug
)
makedepends=(
  git
  python-build
  python-flit-core
  python-installer
)
checkdepends=(
  python-flask-sqlalchemy
  python-marshmallow-sqlalchemy
  python-pytest
)
optdepends=(
  'python-flask-sqlalchemy: SQLAlchemy integration'
  'python-marshmallow-sqlalchemy: SQLAlchemy integration'
)
_tag=7ea089d21d39f1d4abc549a4e7c8622c872c152b
source=(git+https://github.com/marshmallow-code/flask-marshmallow.git#tag=${_tag})
b2sums=('a650a9002d8e6f862e38fc34d7072cb73043b3110c4eada5029b0f1a5f88278ae1a6abb84844235fd91c41c36d2901358173ca538609bb8d4d1d6258edef9a91')

pkgver() {
  cd flask-marshmallow
  git describe --tags
}

build() {
  cd flask-marshmallow
  python -m build --wheel --no-isolation
}

check() {
  cd flask-marshmallow
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  python -m installer --destdir="${pkgdir}" flask-marshmallow/dist/*.whl
  install -Dm 644 flask-marshmallow/LICENSE -t "${pkgdir}"/usr/share/licenses/python-flask-marshmallow/
}

# vim: ts=2 sw=2 et:
