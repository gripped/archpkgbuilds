# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=1.94
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=('x86_64')
url='https://16-bits.org/ft2.php'
license=(
  'BSD-3-Clause'
  'MIT'
  'CC-BY-NC-SA-4.0'
)
depends=(
  'glibc'
  'gcc-libs'
  'alsa-lib'
  'sdl2'
  'flac'
  'hicolor-icon-theme'
)
makedepends=(
  'git'
  'libicns'
  'cmake'
  'gendesk'
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  'templatify-build-date.patch'
)
sha512sums=('12627368995e236a3edcce136fdc01f95e121613af5c2964c257553bbed802f4229c67f5d3c7b620b2d998ab78f1f8275a1d29d9fe4ec6f133869c1623437af7'
            'b39b1ccd2750869e8acc1d1517d62926ebab384defdc45abe7b6c13d26e9f8c0d2cca760cf6d42ee5f89af1a4b21bc8eeb3e3aa6a1b4a5642f1f9fc39df24ee4')
b2sums=('891cfeaee0a7713be7d040f5ecbd17d73b1d5df2563d27afc884eee07b2a5ee4905cd6fc1884bf59e718eaa7940bb934f59732c0d14e10d05e0054a214161178'
        '47222515fb150c156810297e6bf0c51a73e7ddda76289d3b39577957f85e8a1afea1bfda03988cdfd5abf92e77ea0f2907a77e37ff34cf9f354fd270241cc078')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
