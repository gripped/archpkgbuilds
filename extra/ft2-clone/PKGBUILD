# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=2.01
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=(x86_64)
url='https://16-bits.org/ft2.php'
license=(
  BSD-3-Clause
  MIT
  CC-BY-NC-SA-4.0
)
depends=(
  glibc
  gcc-libs
  alsa-lib
  sdl2
  flac
  hicolor-icon-theme
)
makedepends=(
  git
  libicns
  cmake
  gendesk
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  templatify-build-date.patch
)
sha512sums=('cd7e0fc4defa04bcadfa841acf0c32b7f023db7ce859c80ef445e69c225b416882e87e7b4e6059f124bf88dcbc416d7eddf204732da946843b22c979e7a75c7c'
            '1c1dfe84342cd8de6a1718272cfc62f912f6ccebaf63e0681183f749da8d4670dc757823a13a244ad987a1d4ec416b2902db8bceb48bda1e1c1d741953c04606')
b2sums=('7caf706c9b2fe2b5e06a0c411648ec06a4b01b188326d75eceae3e843c8bb73f8f2bcece413f511cee89131d8168158b10b80cd6eef53d9161633e65780ed699'
        '91a421a4ebac110393b90b2ea62776277783bc5a49a7c9ce9d3631c93795b4d617e961917e8deb1d4779d5b9dec734094df90ca5fb7064726ce7af6690520344')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
