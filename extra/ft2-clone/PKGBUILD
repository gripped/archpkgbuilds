# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=2.04
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=(x86_64)
url='https://16-bits.org/ft2.php'
license=(
  BSD-3-Clause
  MIT
  CC-BY-NC-SA-4.0
)
depends=(
  glibc
  libgcc
  libstdc++
  alsa-lib
  sdl2
  flac
  hicolor-icon-theme
)
makedepends=(
  git
  libicns
  cmake
  gendesk
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  templatify-build-date.patch
)
sha512sums=('54a23ca428729bf4c7700db392d43529e10971b7f9eef4f190ec022ff9fcd0745bfbd6135ac7a1ae966b2afe4a52c4128f05995c3cfa18726104db7bb84aa82c'
            '0934175a704ec32dee6eb3c4f8eaff9b3c6df5bd91026d94fd215e9a494cc65939dadd26a58471c90e2a40cdf6b3bd1bc6b9cbe962ec31f9e5bec3fb244eda6e')
b2sums=('6b431171e93b3203c0b3a8abfe4cc209c80f4b1c2fb7ead15a83014ddead710f0c51d35feacafb327e418415069131e8f74b7e4a46274bac92eb231e2ebd581b'
        '5c7db51a052f3dab7b4d45ddb688415400dd37cf2b76fc08a3083368cfb8dd58a83445c8d67c5b610d1a371c4aa2c06d080bb74952053f1a55762cb227fed9eb')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
