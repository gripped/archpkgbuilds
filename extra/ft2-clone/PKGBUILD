# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=2.03
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=(x86_64)
url='https://16-bits.org/ft2.php'
license=(
  BSD-3-Clause
  MIT
  CC-BY-NC-SA-4.0
)
depends=(
  glibc
  gcc-libs
  alsa-lib
  sdl2
  flac
  hicolor-icon-theme
)
makedepends=(
  git
  libicns
  cmake
  gendesk
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  templatify-build-date.patch
)
sha512sums=('a2b5b05da765b01a6510cca79768a5e7bc309d1d02a2f6c88c4e6203ef368b7dff0d6b54cd4a1189bd5093fad2836762c1881c4bc1711e8ccb0a33a700016efb'
            '4b4bbf70b49d7713327ef596dca24a93a8c7bc0784f7ee4fc6b2aa363a7a4ce1125904139a13ee1bc5f379d415f1b039410bf55c81bc73d979ee079d00febc65')
b2sums=('fa8ff59a472640e724efcb17b95b7e6d6fbdc084e0ff0c36b48ba788c548b82ed1777603a4035d29277f8336858cb6ac5476b9a08d4432b2bb0f8deefda742fc'
        'a75524b0f27e601a7b25338e497bcf52bcca39c35ef8a0c369050530214335f262d7b1d744ef7a34020cc2cae05c3ac7a099cfd007781d50b32e6082bc2ef877')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
