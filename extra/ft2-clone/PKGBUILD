# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=1.96
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=('x86_64')
url='https://16-bits.org/ft2.php'
license=(
  'BSD-3-Clause'
  'MIT'
  'CC-BY-NC-SA-4.0'
)
depends=(
  'glibc'
  'gcc-libs'
  'alsa-lib'
  'sdl2'
  'flac'
  'hicolor-icon-theme'
)
makedepends=(
  'git'
  'libicns'
  'cmake'
  'gendesk'
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  'templatify-build-date.patch'
)
sha512sums=('9518a5db0a4d4603eadd6fd3f01eb59e67a66c993a8ec4f55d0a0f40f87f5d4358210657e0269e038f3e5cb45e2001fa38be559d41bc6082fa12fadb8056d454'
            '0806a66261518afab361810b0a990fe0696a39b3e6f2adef8242198251f301f7e08d3518e5b47f8c03c315c6a48224f4bcbd29104f6953a8089e4b62b3fec56e')
b2sums=('57191d7de1f6ab326f0c0a445a2a0ff4642579d8f5e7a5cd838fafc5a5e6fa8ee01a8092b950e212669cffd66485f3ab302c012b91bdca23fd1a2c594b7971a7'
        'ea84f25835672f5928688e3ec4a12242b0ef893e6c26a24822a2aec197669be37955f25f296a44173d61b64acbd504b5d738f9a146386834e92e7db2f090f35f')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
