# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=2.00
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=('x86_64')
url='https://16-bits.org/ft2.php'
license=(
  'BSD-3-Clause'
  'MIT'
  'CC-BY-NC-SA-4.0'
)
depends=(
  'glibc'
  'gcc-libs'
  'alsa-lib'
  'sdl2'
  'flac'
  'hicolor-icon-theme'
)
makedepends=(
  'git'
  'libicns'
  'cmake'
  'gendesk'
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  'templatify-build-date.patch'
)
sha512sums=('446ccc42eef2a7070e228bd0adbccd28563272029633ca72a74f802fd0986f3e39ced919043372d55a1c932ef3e6018d2026a33aa6811085760df91bea7e091e'
            'bcba6393258fa64291c39a65f2e429b9e074b70e4227d48b61361adb08259eaed342cf2d64cbbde98ebb96659eb3fff2c4de3ca2cbf62001dcd73862964a5af7')
b2sums=('336b8acf7307ffa14b1ba8655537fb7fff63744b5285dfdc95dc9df598047cfaeaa2048eb92b446a718a3b0483d0b691a4f99feeb5d8f8e9fcc1f1714efd031f'
        '40f1941a46ceda56ddae5df5460d7742b809290f71f510b2f6df8eb2d7391c7298522885884ce4a047c39c3df581e4fc0240025bfdc8c0b6518da35712ce1bb4')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
