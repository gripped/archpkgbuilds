# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=2.05
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=(x86_64)
url='https://16-bits.org/ft2.php'
license=(
  BSD-3-Clause
  MIT
  CC-BY-NC-SA-4.0
)
depends=(
  glibc
  libgcc
  libstdc++
  alsa-lib
  sdl2
  flac
  hicolor-icon-theme
)
makedepends=(
  git
  libicns
  cmake
  gendesk
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  templatify-build-date.patch
)
sha512sums=('adfe512f08a926d065cc48c31264c49900bd8b934777f6482616b4caea48b716c3b2b3a030daac1fda17618adbcb764dfb09420d23f2cc526c7414cf505d9bf9'
            '3b434e7acf36230e7db570422e7c18430537334031641a14c3da8f5133dc780c6baab3c457409a86e189741820f0c69cb54fd7c28f01017135921deea2c218cd')
b2sums=('ed9ffa059a23fb634dd58757d2e216f77dc1c3be642a5b7bd438aca6260b9e22f491d26b22bb8060131ed2a91b8cf5c1996f516b3a2de3916e85f4218ae6fee9'
        '8d0a03a3cb744fe2f2bd98fd2b45afd9e9b4916db0c59eb64ae91cef2a61e67bdd6af5c41a0f89a75714099665df18ed726441d103d400f4751d9986643a863b')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
