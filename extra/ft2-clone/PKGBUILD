# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=2.02
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=(x86_64)
url='https://16-bits.org/ft2.php'
license=(
  BSD-3-Clause
  MIT
  CC-BY-NC-SA-4.0
)
depends=(
  glibc
  gcc-libs
  alsa-lib
  sdl2
  flac
  hicolor-icon-theme
)
makedepends=(
  git
  libicns
  cmake
  gendesk
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  templatify-build-date.patch
)
sha512sums=('3af9c52f7471eba1bf6432a89f17eabadbe09cf852b12a1ba47b908090bf38fc2849c576a7e86a1184cb4eb14da5f3e0ee20d5216b6d8a142677f9368088cee5'
            'a29bded8ea86b92ec6b105307cbe2d8066eb9c2fab02acd73ccab980492c793703ec5e35f00945e71379584d3f773fac8c19747ba9af36655d5a4aa1d14f088e')
b2sums=('5c00bcd022e28dac7cbccea4439fc5a1680edb9ddd1999fb5a2d82bbc42b6e81eeb79b4140e366aa2924a22a97caa6640fe9ba88793643fb18a56e0642220b96'
        '52381cf2b48be22853b0bfbfa5d79145bb22af86cf0af39153e03c4c3aabc6155fb313cae475cfd38330da638262403237f6f455aba1c9281f6b6066d1d8bb13')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
