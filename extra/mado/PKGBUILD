# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mado
pkgver=0.2.2
pkgrel=1
pkgdesc="A fast Markdown linter written in Rust"
arch=(x86_64)
url="https://github.com/akiomik/mado"
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
options=(!lto)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-0.2.2-disable-failing-unit-tests.patch
  $pkgname-0.2.2-disable-failing-command-tests.patch
)
sha512sums=('577482c435564a01d5622262a815c1fcb8989942c9c02b54ce5e1539bc8d514978b6fd9d7246b2bfc30a174e1e6e1eae06852d17a531a909c8e583dbf99d4368'
            '383fff6c0ab17f3e0c46044316302f9a5ca6ba0545a072331502179d426f05ee4489e49169b6806369c1877e17580d8defbbba4742faa039c1000236633bda40'
            'bc13b30aa975ad71121b357728960b0a39f16146123b81a020cb753e4aaf24ddc414f34cd0f33a1304c932b0b05ad57c06d6193022096c3e04dbf2565717f0da')
b2sums=('3ceedaf6383c916462449896f99bc6b8eb837855b7e7f49586ed1b2f161d2733eba73e094dd8abd3d198c3f54e6c663ef34091979d6599ae3bbd8eb4feb42880'
        'e16f04efef43fb16caf18d008d4f0376a8ac9279d04cef72cc56c508ed7d4c73dc55bb5dba1174f50b3ca1a50b58b94d29405e3521e33248e93e476d3d0d9ace'
        '6464261aac9e47826c08aa243fc9278c46e74b73c9a1af54b2f939689314e914fd3dc0c59d83db1558d053ac5747ddcdeaba961f4b2f07b9b804af21cf44957c')

prepare() {
  # Disable failing unit tests: https://github.com/akiomik/mado/issues/140
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.2.2-disable-failing-unit-tests.patch
  # Disable failing command tests: https://github.com/akiomik/mado/issues/141
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.2.2-disable-failing-command-tests.patch

  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 {{CHANGELOG,README}.md,$pkgname.toml} -t "$pkgdir/usr/share/doc/$pkgname/"
}
