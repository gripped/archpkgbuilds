# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Mike Swanson <mikeonthecomputer@gmail.com>

pkgname=disorderfs
pkgver=0.6.0
pkgrel=1
pkgdesc="FUSE filesystem that introduces non-determinism"
arch=('x86_64')
url="https://reproducible-builds.org/"
license=('GPL3')
depends=('fuse' 'gcc-libs')
makedepends=('asciidoc' 'git')
source=(https://reproducible-builds.org/_lfs/releases/disorderfs/disorderfs-${pkgver}.tar.gz{,.asc})
sha512sums=('42858b334b0a8b932817c69d8b2fb5d754a6277a352b407a35d46982bfcebe41c15fa842a83485efc5447a68cd3eca2802cba6827a72a8032ef2d802e9cf7310'
            'SKIP')
# Holger Levsen, Chris Lamb
validpgpkeys=('B8BF54137B09D35CF026FE9D091AB856069AAA1C' 'C2FE4BD271C139B86C533E461E953E27D4311E58')

build() {
  #cd $pkgname-$pkgver
  make
}

# make sure to run with /dev/fuse mounted.
# in extra-*-build this will be with "-- -d /dev/fuse"
check() {
  #cd $pkgname-$pkgver
  test -e /dev/fuse && make test || echo "WARNING: Skipping tests due to missing /dev/fuse mount"
}

package() {
  #cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" PREFIX=/usr install
}
