# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Miles McLean <mills00013@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Brad Pitcher <bradpitcher@gmail.com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=networkmanager-l2tp
pkgver=1.20.12
_pppver=2.5.0
pkgrel=1
pkgdesc='L2TP support for NetworkManager'
arch=(x86_64)
url='https://github.com/nm-l2tp/NetworkManager-l2tp'
license=(GPL2)
conflicts=(networkmanager-l2tp-gtk3 networkmanager-l2tp-gtk4)
replaces=(networkmanager-l2tp-gtk3 networkmanager-l2tp-gtk4)
depends=(ppp=$_pppver xl2tpd)
makedepends=(intltool libnma libnma-gtk4 python)
optdepends=(
  'libnma: GUI support (GTK 3)'
  'libnma-gtk4: GUI support (GTK 4)'
  'strongswan: IPSec support')
source=("$pkgname-$pkgver.tar.gz"::"$url/archive/$pkgver.tar.gz")
b2sums=('8dea49a5d1ef6ebdb74a8606d53c0891ae9ddda077ee26b6a2d5028d7fbfd3c7969ba37ab9fe7218e863802471f0b5797b9f31519b98857f8110424afe6d5584')

prepare() {
  ln -sf NetworkManager-l2tp-$pkgver $pkgname-$pkgver
}

build() {
  export NOCONFIGURE=1
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure \
    --libexecdir=/usr/lib/NetworkManager \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --with-gtk4
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  install -Dm644 $pkgname-$pkgver/NEWS "$pkgdir/usr/share/doc/$pkgname/NEWS"
}
