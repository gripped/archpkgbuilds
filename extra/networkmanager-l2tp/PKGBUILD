# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Miles McLean <mills00013@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Brad Pitcher <bradpitcher@gmail.com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=networkmanager-l2tp
pkgver=1.20.20
pkgrel=3
pkgdesc='L2TP support for NetworkManager'
arch=(x86_64)
url='https://github.com/nm-l2tp/NetworkManager-l2tp'
license=(GPL-2.0-or-later)
conflicts=(networkmanager-l2tp-gtk3 networkmanager-l2tp-gtk4)
replaces=(networkmanager-l2tp-gtk3 networkmanager-l2tp-gtk4)
depends=(ppp xl2tpd)
makedepends=(intltool libnma libnma-gtk4 python-packaging glib2-devel expac)
optdepends=('libnma: GUI support (GTK 3)'
            'libnma-gtk4: GUI support (GTK 4)'
            'strongswan: IPSec support')
source=("$pkgname-$pkgver.tar.gz"::"$url/archive/$pkgver.tar.gz")
b2sums=('0c7f5f3dfab98659b4aa8a51edffb5c061d72735fa6186c0cb308e2f2714efc026cf6409b81c85d04d42c585342ab34737d90456e577b37ec55dd4f37dd41269')

prepare() {
  ln -sf NetworkManager-l2tp-$pkgver $pkgname
}

build() {
  cd $pkgname
  export NOCONFIGURE=1
  ./autogen.sh
  ./configure \
    --libexecdir=/usr/lib/NetworkManager \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pppd-plugin-dir=/usr/lib/pppd/$(expac %v ppp | cut -d - -f 1) \
    --with-gtk4
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" install
  install -Dm644 $pkgname/NEWS "$pkgdir/usr/share/doc/$pkgname/NEWS"
}
