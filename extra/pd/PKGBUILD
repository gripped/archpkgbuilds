# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

_resolutions=( 16 32 48 64 96 128 256 512 1024 )
_name=pure-data
pkgname=pd
pkgver=0.55.2
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=(x86_64)
url="http://msp.ucsd.edu/software.html"
license=(BSD-3-Clause)
groups=(pro-audio)
depends=(
  glibc
  hicolor-icon-theme
  sh
  tcl
  tk
)
makedepends=(
  alsa-lib
  fftw
  gendesk
  inkscape
  jack
  portaudio
  portmidi
)
provides=(
  puredata
  libpd.so
)
source=(
  $pkgname-$_ver.tar.gz::https://github.com/pure-data/pure-data/archive/refs/tags/$_ver.tar.gz
  $pkgname-$pkgver.svg::https://raw.githubusercontent.com/pure-data/pd-icon/9e4f117ade0bc1da951981128998ad82fff83b0c/masters/icon.svg
  $pkgname-$pkgver-icon-LICENSE.txt::https://raw.githubusercontent.com/pure-data/pd-icon/9e4f117ade0bc1da951981128998ad82fff83b0c/LICENSE.txt
)
sha512sums=('f768170280f7b9ce9a468037ae54dfd2570013b10ad843e76655523f5e4a2c8dfb44be243834324b9cef331324d7bee6b85c9ebd75875bb4804bc1bc0cd5d0cf'
            '8d8b274e159bd4cc6a83d5d9d5019b106a74fa61943b36e2a56336b9c3c2c1b1092ce7f79f5264eb69ca43a9360404b800df338b2ad738ca3c237a8ae3c9c41d'
            'dfc919b56ffc8020ffa4e9b16677a6a710f78a73b22d5bb418414f3196a5cbbe5d6cc9cfbf1389811129f219cb21c0eafdfa6dd90e172d73db2452e5c3fd4f5c')
b2sums=('b19315ca6a18112a702b928bc64e673693984122e613cbb3cf6426b63df1e8c365ed7c7603be3f84c92a00c13f24a863b3f763f091f21d413f4018fee2cba142'
        '04f131540fd7ae9b4547a081df61a5a98265c6c9b283587aeda976834d9bbb4486cd30470d5bfb37b6f760d13705074c71cfeb1a9843613e93f3d1e0ca198af9'
        'c3fabeeb8e5c45716fbf5d3e6338b41d5adc0fbefb385e3b42d94c732522e99a4045c88e8b8e2e16a72fc5795dbc0933f07078d0eba80b76fb274450c7518dc7')

prepare() {
  cd $_name-$_ver

  autoreconf -fiv

  # XDG desktop files
  gendesk -n \
          --pkgname $pkgname \
          --pkgdesc "$pkgdesc" \
          --exec $pkgname \
          --name "Pure Data" \
          --categories "AudioVideo;Audio"
  for _res in ${_resolutions[@]}; do
    inkscape -o pd-$_res.png \
             -w $_res \
             -h $_res \
             ../$pkgname-$pkgver.svg
  done
}

build() {
  local configure_options=(
    --prefix=/usr
    --enable-alsa
    --enable-fftw
    --enable-jack
    --enable-libpd
    --enable-libpd-utils
    --enable-libpd-instance
    --enable-libpd-extra
    --enable-portaudio
    --enable-portmidi
    --disable-oss
    --without-local-portaudio
    --without-local-portmidi
  )

  cd $_name-$_ver
  export EXTERNAL_LDFLAGS="$LDFLAGS"
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(
    alsa-lib libasound.so
    fftw libfftw3f.so
    jack libjack.so
    portaudio libportaudio.so
    portmidi libportmidi.so
  )

  cd $_name-$_ver
  # use one job as install target is flaky otherwise: https://github.com/pure-data/pure-data/issues/2345
  make DESTDIR="$pkgdir" install -j1

  # licenses
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 ../$pkgname-$pkgver-icon-LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/icon-LICENSE.txt"

  # desktop file
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # icons
  install -vDm 644 $pkgname-32.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -vDm 644 ../$pkgname-$pkgver.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  for _res in ${_resolutions[@]}; do
    install -vDm 644 $pkgname-$_res.png "$pkgdir/usr/share/icons/hicolor/${_res}x${_res}/apps/$pkgname.png"
  done
  # readme
  install -vDm 644 README.txt -t "$pkgdir/usr/share/doc/$pkgname/"
  # fix broken symlink
  rm -v "$pkgdir/usr/lib/$pkgname/bin/$pkgname"
  ln -sv /usr/bin/$pkgname "$pkgdir/usr/lib/$pkgname/bin/$pkgname"
}
