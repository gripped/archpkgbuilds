# Maintainer:
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=pony
pkgname=python-$_name
pkgver=0.7.17
pkgrel=1
pkgdesc='Pony Object Relational Mapper'
arch=(any)
url=https://github.com/ponyorm/pony
license=(Apache)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
)
sha512sums=('f9284b2fe69eb19c3f2448e181f1a24735c5f7d74662799580dfc12c58de8dd47acebfdb4d00eb6b76a8745b88b8f33bb20b56cc84754773bae0e1cc226bf13e')
b2sums=('302ba72f4630439b3f2ba5b2dbd248794011381f12c9eaaf0c7a085302c232041670058e0440881a8d5fc3bea2bda6ef687c204560f14d869fd0ff05c8d7222c')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  rm pony/orm/tests/test_json.py # https://github.com/ponyorm/pony/issues/704
  python -m unittest discover -vs pony/orm/tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -rf "$pkgdir"/usr/lib/python*/site-packages/pony/orm/tests
}
