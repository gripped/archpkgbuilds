# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Lucas Hoffmann
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Kazuo Teramoto <kaz.rag at gmail.com>

pkgname=afew
_commit=23b5aeaa43572a59e95fb00732292087b091d4a1
pkgver=3.0.1.r82.g23b5aea
pkgrel=1
pkgdesc='Initial tagging script for notmuch mail'
arch=(any)
url=https://github.com/afewmail/afew
license=(ISC)
depends=(
  notmuch
  python-chardet
  python-cffi
  python-dkim
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-sphinx
  python-wheel
)
optdepends=('python-pyinotify: for --watch')
checkdepends=(python-freezegun)
source=("git+$url.git#commit=$_commit")
b2sums=('d119e461d5975ce1770f47355558d8f15ff4b66cb2f55b3b7a99f1f6a7dca3f0d159bc482534259bce58044a8599d0d6ed7b20a6d73b070c4a8f81bb97c218aa')

pkgver() {
  cd $pkgname
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
  PYTHONPATH="build:$PYTHONPATH" sphinx-build -b man docs build
}

check() {
  cd $pkgname
  python -m unittest discover $pkgname/tests
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 build/$pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
