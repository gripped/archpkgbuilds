# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Lucas Hoffmann
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Kazuo Teramoto <kaz.rag at gmail.com>

pkgname=afew
_commit=bc09b1481d8b7e10f5bd90bae663664107e8552d
pkgver=3.0.1.r78.gbc09b14
pkgrel=2
pkgdesc='Initial tagging script for notmuch mail'
arch=(any)
url=https://github.com/afewmail/afew
license=(ISC)
depends=(
  notmuch
  python-chardet
  python-cffi
  python-dkim
  python-setuptools
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-sphinx
  python-wheel
)
optdepends=('python-pyinotify: for --watch')
checkdepends=(python-freezegun)
source=("git+$url.git#commit=$_commit")
b2sums=('f3bdbce60e1fbeed287dfce77ba413860e6d73585adacb81e02410ab51d06376f165f2198524e4f2465776ad2dd32fb7e7d379038b7c5dda452b122caa7e9a15')

pkgver() {
  cd $pkgname
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
  PYTHONPATH="build:$PYTHONPATH" sphinx-build -b man docs build
}

check() {
  cd $pkgname
  python -m unittest discover $pkgname/tests
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 build/$pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
