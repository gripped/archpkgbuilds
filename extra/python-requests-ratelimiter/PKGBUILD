# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-requests-ratelimiter
pkgver=0.9.0
pkgrel=1
pkgdesc='A general-purpose rate-limiting library for python-requests'
arch=(any)
url='https://github.com/JWCook/requests-ratelimiter'
license=(MIT)
depends=(
  python
  python-requests
  python-pyrate-limiter
)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
)
checkdepends=(
  python-pytest
  python-requests-mock
  python-requests-cache
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('fac8eb58fef4fcd75dfc6b084a7533a3cbc142d49abf74e14a04834b627b02161820f9c50230ab7793fd5bfdc46f9e30ef859ee8907b7334504ce24a07d0102f')
b2sums=('8923e4f25a883eaaf8a31322f5ef067b1d303cacbff31f719f3aba2ecdf98fa5d725245e4c697ff7dc4b046965a093b14d1efce5ff1bbfa51c7e99339c117723')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
