# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-requests-ratelimiter
pkgver=0.9.1
pkgrel=1
pkgdesc='A general-purpose rate-limiting library for python-requests'
arch=(any)
url='https://github.com/JWCook/requests-ratelimiter'
license=(MIT)
depends=(
  python
  python-requests
  python-pyrate-limiter
)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
)
checkdepends=(
  python-pytest
  python-requests-mock
  python-requests-cache
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('64ae1561b76994738b85c63924b140d303ed48300773cbfd890fdac447dad80bea45711a7df8b478adb69a9549d2d6bbc760bd7fefd6d62fc2ababc216b0cd5d')
b2sums=('70a07fb3a19e8e4532dc69392c3868ad49ec659ef31d4dae789a286aef8a526a451f3ee090c586a52b00225b8cd551d6bdf01aa994440b828a9433767b7103d9')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
