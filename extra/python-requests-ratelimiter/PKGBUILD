# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-requests-ratelimiter
pkgver=0.9.2
pkgrel=1
pkgdesc='A general-purpose rate-limiting library for python-requests'
arch=(any)
url='https://github.com/JWCook/requests-ratelimiter'
license=(MIT)
depends=(
  python
  python-requests
  python-pyrate-limiter
)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
)
checkdepends=(
  python-pytest
  python-requests-mock
  python-requests-cache
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('79e24f4ff2c312334863f0586dd81fd1ab695de7b1f15ee0aced6172fb7977d73c84609a9ce719115677ab752ca8f486b997a7a4fa687e8c863ceb5538ba6a0f')
b2sums=('b778e9dc3da3931edb4d8acdff1fe58f2717a84db63316c96193687a7e478727878c7bef7de525333c134a758d448a5026c7789d99943fb0bed2de2cb6dad4cb')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
