# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=dnf5
pkgver=5.2.14.0
pkgrel=2
pkgdesc="Next-generation RPM package management system"
arch=('x86_64')
url="https://github.com/rpm-software-management/$pkgname"
license=('GPL-2.0-or-later')
depends=('appstream' 'curl' 'fmt' 'glib2' 'json-c'
         'libmodulemd' 'librepo' 'libsolv' 'libxml2'
         'rpm-tools' 'sdbus-cpp' 'sqlite' 'systemd-libs'
         'util-linux-libs' 'zlib')
makedepends=('bash-completion' 'cmake' 'doxygen' 'gettext' 'perl'
             'python' 'python-breathe' 'python-sphinx' 'python-sphinx_rtd_theme'
             'toml11' 'swig' 'systemd')
checkdepends=('cppunit' 'createrepo_c' 'perl-test-exception')
backup=('etc/dnf/dnf.conf'
        'etc/dnf/libdnf5-plugins/actions.conf'
        'etc/dnf/libdnf5-plugins/appstream.conf'
        'etc/dnf/libdnf5-plugins/expired-pgp-keys.conf')
options=('!emptydirs')
conflicts=(dnf)
replaces=(dnf)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
	"allow-absolute-path-exception.patch")
sha256sums=('2d4c7bd4f26c415221ad7d8c52a0bc3b71fbf492bdd5c1a128a37873f7d4a0f9'
            'adbac106f0253622737e0f9647744c7cfa0c76919af8b8f2a94182b191b9f502')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "${srcdir}/allow-absolute-path-exception.patch"
}

build() {
  cd "$pkgname-$pkgver"

  cmake -B build \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -DCMAKE_INSTALL_SBINDIR='bin' \
      -DWITH_DNF5_OBSOLETES_DNF='OFF' \
      -DWITH_RUBY='OFF'  # https://github.com/rpm-software-management/dnf5/issues/562

  cmake --build build
  cmake --build build --target doc-man
}

check() {
  cd "$pkgname-$pkgver"

  LC_ALL=C.UTF-8 ctest --test-dir build --output-on-failure
}

package() {
  cd "$pkgname-$pkgver"

  DESTDIR="$pkgdir" cmake --install build
  install -Dp -m644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
