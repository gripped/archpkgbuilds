# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.2.1
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('55a2daef85ba550a06a9c0f9c5317aec907708c67a99b3d0f00f5dec2d1c7db1ea1b8c42e7a7988a1bc9ffa668550abc91502f071378fc88fb142c46ff640486')
b2sums=('726372d844764c22f3a7be9a975cded2454e0e7203e2da0a98a77aa9402517a00c0a4df2aabdb30dc6b3d44d14660e981bf379a5bab326cfc89d5566fa7cff62')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
