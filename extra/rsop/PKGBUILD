# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.7.1
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('51f4305b06700549212d71d0b0ba10314b1bda0b97c8504c2336571b97473b451f84ab96b20d4b684418a254c739b3d881f72cf5e00139428cde9efa2f46f290')
b2sums=('022203578944f53cb83fa066c6377de2611d45494fc2478159f21cf966ab22f052ab58c07874fbfa9946824b347c0bc2f805a55931271e90ab965e07d30539f6')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
