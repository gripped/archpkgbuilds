# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.5.0
pkgrel=2
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('dd75e7d925e4b8fda85897be4e41dc42af79aef726e410fe6aad4a8ba7548c60c12722d31d95f5fee491fbccaba4527a5a723bd4957b27c861bd63cb8453c551')
b2sums=('4b44b450b6cbdb2199f19ba8b0516c39e91238d33fb8fb9b61b30c1ea2392de1ec14b24d629e54c63a5b9f8fc72d64d81bbf66475bf2dc6d51a60c78d58df604')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
