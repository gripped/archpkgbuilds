# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.9.3
pkgrel=2
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('ba9328b5ca0d0091e7b2308180b0e579ab67edfb3a3cc9e8b9efd60262786c6304c55632a876b4bdf46b9bdef6706e2412a0319c8ee1f734eb09439683054ff3')
b2sums=('9264e56114816a8d7efbb125ac3321e97204711b247950ba3c18bde602f20910d0c4150204d828d7a608328b9070a86a84de612fdfff68e57c3d4e72db8ac0dc')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  export CARGO_TARGET_DIR="$PWD/target"
  cargo build --bin rsop -p $pkgname --frozen --release --features=cli
  cargo build --bin rsopv -p $pkgname --frozen --release --features=cliv
}

check() {
  cd $pkgname
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 target/$pkgname*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 0644 target/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 0644 target/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 0644 target/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
