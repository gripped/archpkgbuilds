# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.3.4
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('c07156be94d14749fb86db0cad8a4d91969273a2f9307911e63ed9d9fea6d2ec45a4f8f32606da39ad0186a2b865511014d6f75d6c6b18e5f98908ab7e79d74a')
b2sums=('150dfc37563da191fb04a15e983f0c9efdae588a891054a7cbf9fa5c3c1f8c9c17c4594efd9e4c791d46c7b1a2b14460edbca80d942f4271ab89aaa5e26395a4')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
