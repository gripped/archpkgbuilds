# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.10.0
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('8d2b8de259d193a0b93b22e391bf7efd4480b8930bc37645a4e3a85bb4e8fb856099a7a962fb78216ba1f162a82e866dc9daf86c1fff3a03b9b51ccd261bd0e5')
b2sums=('16e9ed1890c6aa5c1fc1a4df4064b7851fb8832ccb483373e84d8123467d804bc23d524326af7b8808ad48519dc4d45b37f2487dd48d6e448c2692fe3af7e842')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  export CARGO_TARGET_DIR="$PWD/target"
  cargo build --bin rsop -p $pkgname --frozen --release --features=cli
  cargo build --bin rsopv -p $pkgname --frozen --release --features=cliv
}

check() {
  cd $pkgname
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 target/$pkgname*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 0644 target/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 0644 target/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 0644 target/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
