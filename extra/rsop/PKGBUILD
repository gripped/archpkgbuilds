# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.3.6
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('5dc5143b8f03b1faa3e945364287e6469d72c8809a9c16d160dbc1dd7acfebb15622568b7185f85a976043838d7ca0ad6edb8e673a321af9d0ccf67a338ad0d6')
b2sums=('2c7e7f05c6eb369666f5dae91a1f51dd428fd8d361965e90b739af06a9f4e4600838bda4cce2632d1d0fa7534c6235be4d7ca2b40bfbdf64cb5460d784918b84')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
