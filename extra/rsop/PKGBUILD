# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.6.3
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('4efde74b8f046ae205f341f1a201adc35bd7ff49629aa13ea96e7b9a18b698c0bd0d51fd69f4c8247c5bb3ca337bfa4fc8c95b3788cdebbd4733daf056cad674')
b2sums=('dee09dcec61990d3d3eaae3a51ec4bc2cdfe745ce7481b95d254889d885683c26caaeb993e8b6d719b7b630b5acc76bbcfd4157291fb559b36dec0e6438de95d')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
