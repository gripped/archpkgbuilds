# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.6.2
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('48bd55a4f573a14257fa2a7800a533232bf4761bdfc63f030041345fe5788625779ad82c9dacd078a23388bda19ffb7dccf51daa75ccf7c83baef03a88228e97')
b2sums=('56d2b1191be87e1540ef9e1eb69fb0e133128f0b5d096d4bd194b3a3028d0c13bb4e2c6f7b9bf6d908f39a12626f5b7ec521fd6440216bf0731f66181f529db6')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
