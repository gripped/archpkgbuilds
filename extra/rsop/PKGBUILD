# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.9.0
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('dfb0735c013de6f92d2727cc83b4fd6e42f7407878e4daf526a07e16e9460a6456743de4e1f00c3488e6ff0afb849d5abbe476ab10b3f552ff323b52ee6c5979')
b2sums=('c6b00adc03fc4dce0d0ed8454f65dbb9e8c46c121f4f88836d76ecdd5bf90d0d91399130423fc9bf3f7e652b5bc77ec6662cc3e47be386bad673e2e066929a0e')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
