# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.9.2
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('cbafe5e8d756a740655f67c60c3ec76bba1f9dd681e4dff18ed23f6f73b6eb11cfbbdc8e56759093b11723d87c9e626e5ba0009181167de040d309ad9bce5e5f')
b2sums=('0e7637afb69b7c2bafca78f686fd5c3e4e2cc4a8d4c549be9649782fc8189807ce491a8114f4a2c05b7ef898e2f15ad0549b76593b4507265990103f3ab9bb29')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
