# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.2.0
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('a2f3cf90602b321475c999e8e07776087449b7f1bdf4a31b7b340bdd314de9d43bc25f6e5fd3ca1b446507ddc12f499e2ac8c8d8b2b367e61831bebaccfc2058')
b2sums=('5b1b0e085781b1b3ae20ccd355b53f38dbc6a70ddc10080e50440c2da07be50f6546feaa2b933501d26a405ddf12a30ec26f20bda08f7ed4205a3022a69f2827')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
