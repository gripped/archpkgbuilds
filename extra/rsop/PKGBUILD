# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.7.2
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('e0d274ef2ee932be94490e3a0d3273917132f664e3d480a0835c587eb4bc5d771b12989938b088a548b3f04c640f7081de923688ec81fe6414661464428dd19a')
b2sums=('24d407b931f539e6d8395a80cd206cc23033183f86ff1024a0a96ac4614b54fd8ca98e289a9866abcdfc70e008ef30abeaaf65569d5d6cabac9d4d9da70b0123')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build -p $pkgname --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test -p $pkgname --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 0755 "target/release/$pkgname"{,v} -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
