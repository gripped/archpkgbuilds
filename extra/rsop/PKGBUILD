# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.3.0
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('3a958d8cecc6d1ddedfab918d4b69ee9f963dce2309c597381fbf6f38478230ae6e0c82a419355486ffc179e880aedb4f55d14f2389d8374ed5ce5ddbc5eb210')
b2sums=('7442c1720ababa5ee10dae0e8dee7f84c71d0df9652dcc75f2d7ada1dfbb7514bcbb50673ac0bf93c7f0091dc52b4c40a039e6395c84ce0b7fbd0dfbde9deb60')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
