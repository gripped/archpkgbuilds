# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.4.3
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  dbus
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('ed91ea4fc45e75dedcc61001733bae398f50698ab631d3107654902e2288a6a3750f2243677a794b858c46c9002d5953dbdb9df6a454ce0f5e46da9ef2e0409a')
b2sums=('aaa58f4788e9de4f8ed58dbeb5cc9049e6e123e1050dbbcc3274d65474c680fc498529889bd1b5f25d0353193cee84df1c3dabae482f1c71a2be24a8f1ea4917')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
