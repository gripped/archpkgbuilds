# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=opengl-man-pages
pkgver=20240713
_commit=9813a5569411cad5250d719a9efed2fac732e56f
pkgrel=2
pkgdesc="OpenGL Man Pages - OpenGL 4.x"
arch=('any')
url="https://github.com/KhronosGroup/OpenGL-Refpages"
license=('SGI-B-2.0' 'OPL-1.0')
makedepends=('docbook5-xml' 'docbook-xsl' 'libxslt' 'docbook-mathml' 'w3c-mathml2' 'python' 'git')
source=(${pkgname}::git+https://github.com/KhronosGroup/OpenGL-Refpages#commit=$_commit
        'license-from-upstream')
sha256sums=('848033ced8fb614dd10cff688dcff878d4758544c654a541a72f362f1746b49e'
            'a7b2f6669d7ead91dcaf5a03620cdf9d37c54d83fd1899b4ef84683c7e6d4024')

build() {
  for manpages in gl4; do
    cd "${srcdir}/${pkgname}/${manpages}"
      for file in *.xml; do
        xsltproc --xinclude --noout --nonet /usr/share/xml/docbook/xsl-stylesheets/manpages/docbook.xsl ${file}
      done
  done
}

package() {
  install -d "${pkgdir}/usr/share/man/man3"
  for manpages in gl4; do
    cd "${srcdir}/${pkgname}/${manpages}"
    install -m644 *.3G "${pkgdir}/usr/share/man/man3/"
  done
  # license
  install -D -m644 "${srcdir}/license-from-upstream" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
