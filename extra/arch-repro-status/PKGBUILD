# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=arch-repro-status
pkgver=1.3.11
pkgrel=4
pkgdesc="Check the reproducibility status of your Arch Linux packages"
arch=('x86_64')
url="https://gitlab.archlinux.org/archlinux/arch-repro-status"
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'libalpm.so'
  'libssl.so'
)
makedepends=('cargo')
groups=('archlinux-tools')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz"
        "alpm.patch")
sha512sums=('16fa85c6bd1990363f7129d7c7b2229fa682e0032cef176f22f5cdc1dd03fd6089470984a7fc50fb526ca53cfaa877696f5712155a61d6d24191cb16afdd30d0'
            '7533afdf709bac29eeac88e7cc4b5b49b9e07d4325872d96ba32d8956081a0f42ae2af0eadf743d0dc17412d5d8fcb80b8b042811cba87e667866d2543887ab1')

prepare() {
  cd "$pkgname-v$pkgver"
  mkdir completions/
  patch -Np1 -i ../alpm.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-v$pkgver"
  cargo build --frozen --release
  OUT_DIR=completions/ ./target/release/$pkgname-completions
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-v$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 "completions/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions"
}
