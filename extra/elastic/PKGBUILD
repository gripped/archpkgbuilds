# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=elastic
pkgver=0.1.7
pkgrel=1
pkgdesc='Design and export spring physics-based animations to use with libadwaita'
arch=(x86_64)
url='https://apps.gnome.org/Elastic/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  template-glib
)
makedepends=(
  appstream
  git
  meson
  vala
)
source=("git+https://gitlab.gnome.org/World/elastic.git#tag=$pkgver")
b2sums=(5ca67854bd0761dc5fee0ffe2c01dd3045aec8d16aed7cd3b888f59afe12848a3282bb35cc554098f9faa2f8667f7c5c585df9ee1d361d156947cfc5be9fa3c5)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s app.drey.Elastic "$pkgdir/usr/bin/elastic"
}
