# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=elastic
pkgver=0.1.6
pkgrel=1
pkgdesc='Design and export spring physics-based animations to use with libadwaita'
arch=(x86_64)
url='https://apps.gnome.org/Elastic/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  template-glib
)
makedepends=(
  appstream
  git
  meson
  vala
)
source=(
  "git+https://gitlab.gnome.org/World/elastic.git#tag=$pkgver"
  elastic-window-icon.patch
)
b2sums=(
  ee7ae3fde380bdd1b0b8a89f60ee17ca3b28d6e11697ba4e6df58a78538ce1b541f930f349eb4d3bf72d35451296c40adac614a7605be2e14c72538c6d884b2f
  cc74b79dbc4d4e420ce438cc8299b06ffa8dfb5c0e065fcc408f386fda62f8ffd925379912303f151a278ff25d05baf9fd86a063948322ec1d3b9a7bfe50fb4e
)

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/World/elastic/-/merge_requests/27
  git apply -3 ../elastic-window-icon.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s app.drey.Elastic "$pkgdir/usr/bin/elastic"
}
