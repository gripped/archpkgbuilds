# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=elastic
pkgver=1.0
pkgrel=1
pkgdesc='Design and export spring physics-based animations to use with libadwaita'
arch=(x86_64)
url='https://apps.gnome.org/Elastic/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  template-glib
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  vala
)
source=("git+https://gitlab.gnome.org/World/elastic.git#tag=$pkgver")
b2sums=(4b52f0713c6c72b6c2789db6eb6a9f44ad6c6600fb5f7632529b9456d715f2090912749f0bc61adabdf59accbd0a0c840ec49ace33216f661aac3d1c46e31e30)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s app.drey.Elastic "$pkgdir/usr/bin/elastic"
}
