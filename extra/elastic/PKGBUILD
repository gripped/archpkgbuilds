# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=elastic
pkgver=0.1.9
pkgrel=1
pkgdesc='Design and export spring physics-based animations to use with libadwaita'
arch=(x86_64)
url='https://apps.gnome.org/Elastic/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  template-glib
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  vala
)
source=("git+https://gitlab.gnome.org/World/elastic.git#tag=$pkgver")
b2sums=(604b2fb58131f7e66b005da2505e69d62476732d9dbbbf90952dce112ea94ce3994fec7bcd704882bffdaf5ac29251051c3f75ee94ed63e789bb9be92b1363c4)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s app.drey.Elastic "$pkgdir/usr/bin/elastic"
}
