# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=elastic
pkgver=1.0.1
pkgrel=1
pkgdesc='Design and export spring physics-based animations to use with libadwaita'
arch=(x86_64)
url='https://apps.gnome.org/Elastic/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  template-glib
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  vala
)
source=("git+https://gitlab.gnome.org/World/elastic.git#tag=$pkgver")
b2sums=(884f62c7c6ebc8390c119edd8bb355cac503d7619c4ffefb8f0965c9a8c73ae74cf79b7dcc736ec102ab2dee08123f02e4d612e2d0093a74d40a24e76b3b79f7)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s app.drey.Elastic "$pkgdir/usr/bin/elastic"
}
