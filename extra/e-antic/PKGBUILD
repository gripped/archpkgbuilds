# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=e-antic
pkgver=2.1.0
pkgrel=1
pkgdesc='Embedded algebraic number fields (on top of antic)'
arch=(x86_64)
url='https://github.com/flatsurf/e-antic/'
license=(LGPL-3.0-or-later)
depends=(flint
         gcc-libs
         glibc
         gmp)
makedepends=(boost)
source=(https://github.com/flatsurf/e-antic/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e3e78701d054b441f95d83b6fb55cd84bfd931f5d4a61a2822dc977a20c46f80')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --without-benchmark \
    --without-byexample \
    --without-pytest \
    --without-doc
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libeantic/libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
