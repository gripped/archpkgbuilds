# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=add-determinism
pkgver=0.4.2
pkgrel=1
pkgdesc="Build postprocessor to reset metadata fields for build reproducibility"
url='https://github.com/keszybz/add-determinism'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'zlib' 'libz.so'
)
makedepends=('cargo')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/keszybz/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('f34e5e674b8caec89265b8af7e596fd2cec8e61f7f82dcd203ea553c73f6c598'
            '712c17c7d1b1a5b6da9c7e10f872fe825330976217afb84eaba2957ce5d398e5')
b2sums=('543cd857eab9c72511e8c2dad0ef5c4dd1bf8ea8ff7f54100d0a8b22bc606f712cf589971d8f59648a5a1729456b33701acdce8d0f020331f0c8f616f604e1eb'
        '7f8bc8f9084f877274e49d3b8f44d8ec962ec439d73c5b2f20e0914d390e478d9d6d55433fffdb884d17f53b01264c2d988ba406a45ca31a0d3ea48d6ffe9ae0')

prepare() {
  cd "${pkgname}-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "${pkgname}-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip test_handlers::test_pyc
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
}

# vim: ts=2 sw=2 et:
