# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=add-determinism
pkgver=0.6.0
pkgrel=2
pkgdesc="Build postprocessor to reset metadata fields for build reproducibility"
url='https://github.com/keszybz/add-determinism'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'zlib' 'libz.so'
)
makedepends=('cargo')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/keszybz/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('289e3586bc9234fc3612a7f7c521b75511fa27f5dcba8b04825249ca03880f80'
            '0fbaf88c9d2be33a1d561d01f9f91600c5c49fd751507c1c19de78f85923dfac')
b2sums=('1d5b87bbf8ace6cee70a599b2f3497bec21f7b893eb21fb5317eeb913dca179a6e7c877756b1756b742654977e2d90c8a5bed5482ecf0e5d9d39dd5ba8c4e8b2'
        'a7f924828aec5a88fa9621904feb07aca23e1c029eba6543df2e23c3ab2f0a0c09a5901afa79d6b218455cabb46d51d6a11819936ef97193d32ca74a0e3a996a')

prepare() {
  cd "${pkgname}-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "${pkgname}-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip test_handlers::test_pyc
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
}

# vim: ts=2 sw=2 et:
