# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=add-determinism
pkgver=0.5.0
pkgrel=1
pkgdesc="Build postprocessor to reset metadata fields for build reproducibility"
url='https://github.com/keszybz/add-determinism'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'zlib' 'libz.so'
)
makedepends=('cargo')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/keszybz/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('eb5d8028900c8de500724e84917a45ba59f35db716b65bf2cd21ce10041a02a7'
            '7f68505a48d862aee17640f0ff8cb53506dcdd293da6e2dea3056b47e8a6c588')
b2sums=('b559ae5080b670895f3368cc4cbada85aef9d13fb46e8260b9f47d3d792027cab34315c3b32e815213e1e7243da2751634dbbd4a4358b2b6d6f52acd2216092a'
        '96fa994c57916764581bd6dd46b9ea567124a3ee782d227906154f574d09b01f9d848113508b3a40624fc40e5e63ed2584e1b65692b7b30fedb3983ede3614d7')

prepare() {
  cd "${pkgname}-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "${pkgname}-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip test_handlers::test_pyc
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
}

# vim: ts=2 sw=2 et:
