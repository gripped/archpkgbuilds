# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=syft
pkgver=1.42.1
pkgrel=1
pkgdesc="CLI tool and library for generating a Software Bill of Materials from container images and filesystems"
arch=('x86_64')
url="https://github.com/anchore/syft"
license=('Apache-2.0')
depends=(
  'glibc'
)
makedepends=(
  'git'
  'go'
)
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/anchore/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('68d658c28dcd072717468913731d77c48fa8bb031b3aaf52d8414b93f58a80b3f18ddb6d924db05a65c54165538329f10718e9ab1bea2f6bb118163f9703f662')
b2sums=('a037982a17fb473a33af469a6fc43ade32825aa7922d0e94f7afd10a173d718a6b90faad7d0d4f0b5745c6c21bffe03ccc3e0a32e7fa26c3af776349d0d0dc49')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -o build/ \
    -buildmode=pie \
    -mod=readonly \
    -ldflags "-compressdwarf=false -linkmode=external -X main.version=${pkgver}" \
    ./...
}

check() {
  cd "${pkgname}-${pkgver}"
  build/syft --help
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/syft "${pkgdir}/usr/bin/${pkgname}"

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
