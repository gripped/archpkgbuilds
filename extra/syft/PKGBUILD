# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=syft
pkgver=1.38.0
pkgrel=2
pkgdesc="CLI tool and library for generating a Software Bill of Materials from container images and filesystems"
arch=('x86_64')
url="https://github.com/anchore/syft"
license=('Apache')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/anchore/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('e21f6106a61295faf79eda97ce22336de7f3fc830d6e763cc53f152a8071cf6f050e9bada7e8226b2ac0180fa63cc852125e8e26a03a4f3855d406fac45e989a')
b2sums=('1b84380c74b75939de7aac2980d31b3ff0f89340db89d6227c7a7cf3c9aa854008063dbb35fe57f5d94034f50793ba6412d83a4c28c92bbba38fee08b1166df7')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -modcacherw"
  mkdir build
  go mod tidy
  go build -o build/ ./...
}

# disabled, needs docker
# check() {
#  cd "${pkgname}-${pkgver}"
#  go test -v ./...
# }

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/syft "${pkgdir}/usr/bin/${pkgname}"
}
