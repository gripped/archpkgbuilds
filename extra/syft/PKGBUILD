# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=syft
pkgver=1.33.0
pkgrel=1
pkgdesc="CLI tool and library for generating a Software Bill of Materials from container images and filesystems"
arch=('x86_64')
url="https://github.com/anchore/syft"
license=('Apache')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/anchore/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f7a55e1ca6c022ba4ba6bcfac886df2e9dc4e2f51ea5e5d87e821a000f2f6d4e743a4d0632834a4c67c28ce543fc18463c2243deb186ad6c02af9caf22af4485')
b2sums=('b2f93d1714ffb42e573067adbb497c947bd2f600c32a7d77dee0646a5a6eede588721a7a3949f91dbc47cbe906915eb7f40f3da56252c5de5c043f61922c1f6a')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -modcacherw"
  mkdir build
  go mod tidy
  go build -o build/ ./...
}

# disabled, needs docker
# check() {
#  cd "${pkgname}-${pkgver}"
#  go test -v ./...
# }

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/syft "${pkgdir}/usr/bin/${pkgname}"
}

