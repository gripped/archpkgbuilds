# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=sigrok-cli
pkgver=0.7.2
pkgrel=3
pkgdesc='Command-line client for the sigrok software'
arch=('x86_64')
url='https://www.sigrok.org/wiki/Sigrok-cli'
license=('GPL-3.0-or-later')
depends=(
  'glib2'
  'glibc'
  'libsigrok'
  'libsigrokdecode'
)
makedepends=('git')
source=("git+git://sigrok.org/sigrok-cli#tag=sigrok-cli-$pkgver")
sha512sums=('c871829410093a0a2bff1b8795c2f27d3dd6227c5b0b8308e36f2b5b762e4db3185e17a88b4663d272e8f803be1bcf2f601a928893a053fe90bb4272f5a92f76')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
