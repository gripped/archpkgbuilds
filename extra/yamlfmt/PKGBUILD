# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=yamlfmt
pkgver=0.17.0
pkgrel=1
pkgdesc='An extensible command line tool or library to format yaml files.'
arch=('x86_64')
url='https://github.com/google/yamlfmt'
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('b62f74f565a3a87bf080750fe2f1c3eeb5a4f31cd11b5db969e44bb9acb60234ec3198a36bd7c7edc9066c31012c26cddc7ceb28e5f544b01a954f6164ef1f46')

prepare() {
  cd ${pkgname}-${pkgver}
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="${srcdir}"
  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
  "

  cd ${pkgname}-${pkgver}
  go build -v -ldflags="$ld_flags" -o yamlfmt ./cmd/yamlfmt
}

# check() {
#   cd ${pkgname}-${pkgver}
#   go test -v ./...
# }

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 yamlfmt "${pkgdir}/usr/bin/yamlfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
