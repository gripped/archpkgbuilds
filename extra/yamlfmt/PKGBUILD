# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=yamlfmt
pkgver=0.20.0
pkgrel=1
pkgdesc='An extensible command line tool or library to format yaml files.'
arch=('x86_64')
url='https://github.com/google/yamlfmt'
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b0a0f2a72309f1de81c1e66ae84771dbd4d45544b3e46fce979f906119b08db48c5d1b7e3c69a34a57c29c91ce0cf32afcd4aa5b9f5ff2128b461a5f2ef9ee36')

prepare() {
  cd ${pkgname}-${pkgver}
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="${srcdir}"
  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
  "

  cd ${pkgname}-${pkgver}
  go build -v -ldflags="$ld_flags" -o yamlfmt ./cmd/yamlfmt
}

check() {
  cd ${pkgname}-${pkgver}
  go test -v ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -vDm755 -t "${pkgdir}/usr/bin" yamlfmt
  install -vDm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
