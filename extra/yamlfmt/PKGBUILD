# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=yamlfmt
pkgver=0.15.0
pkgrel=1
pkgdesc='An extensible command line tool or library to format yaml files.'
arch=('x86_64')
url='https://github.com/google/yamlfmt'
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('6ca1f766eeac10a9d322a6bf15474ed0f6441a9ab04462183ddd4696448fcb888c7cad2c6fe8af890095960dd7e4b59dbacc7778935d57c43e0c8930835cfb4a')

prepare() {
  cd ${pkgname}-${pkgver}
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="${srcdir}"
  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
  "

  cd ${pkgname}-${pkgver}
  go build -v -ldflags="$ld_flags" -o yamlfmt ./cmd/yamlfmt
}

check() {
  cd ${pkgname}-${pkgver}
  go test -v ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 yamlfmt "${pkgdir}/usr/bin/yamlfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
