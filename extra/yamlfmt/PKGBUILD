# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=yamlfmt
pkgver=0.18.0
pkgrel=1
pkgdesc='An extensible command line tool or library to format yaml files.'
arch=('x86_64')
url='https://github.com/google/yamlfmt'
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('be9282053b1e6f41abb4c22f12c41c9f13de691b932fa9c602364161dccacd6722e0d4a6202edf25b982275cf009667903488c40dd13b73632613cdc583c1174')

prepare() {
  cd ${pkgname}-${pkgver}
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="${srcdir}"
  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
  "

  cd ${pkgname}-${pkgver}
  go build -v -ldflags="$ld_flags" -o yamlfmt ./cmd/yamlfmt
}

# check() {
#   cd ${pkgname}-${pkgver}
#   go test -v ./...
# }

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 yamlfmt "${pkgdir}/usr/bin/yamlfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
