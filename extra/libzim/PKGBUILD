# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=libzim
pkgver=9.4.0
# https://github.com/openzim/zim-testing-suite/releases
_testing_suite_commit=91bb9768c2e433e78885082375811f763b9a0c1f # v0.8
pkgrel=2
pkgdesc="Reference implementation of the ZIM specification by the openZIM project"
license=('GPL-2.0-or-later')
arch=('x86_64')
depends=('xapian-core' 'xz' 'icu' 'zstd')
makedepends=('meson' 'gtest' 'git')
url='https://github.com/openzim/libzim'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	"git+https://github.com/openzim/zim-testing-suite#commit=${_testing_suite_commit}")
sha256sums=('000d6e413963754c0e28327fbdd0a04afd05ea2a728f6438ef96795a2aa3edb8'
            'bd166b5e169ba6961cc55d2fc648475a31834590392f9cc8e6c6cc2e77e52664')

build() {
	cd "${pkgname}-${pkgver}"
	# https://github.com/openzim/libzim/issues/757
	arch-meson build \
		-Dtest_data_dir="${srcdir}/zim-testing-suite/data" \
		-Dcpp_std=c++17
	ninja -C build
}

check() {
	cd "${pkgname}-${pkgver}/build"
	SKIP_BIG_MEMORY_TEST=1 meson test --verbose
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
