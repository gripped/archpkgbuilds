# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=ocrad
pkgver=0.29
pkgrel=2
pkgdesc='OCR (Optical Character Recognition) program based on a feature extraction method'
arch=(x86_64)
url='https://www.gnu.org/software/ocrad/'
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  libpng
)
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz"{,.sig})
b2sums=(
  b5acb8fa3b3f02b7a0e5697b3ab4aa4ecc115c9d668192d6a4f533b787092c91449366df3603c73b926be61e5cb6b4e2d5406896ec79f931eb42dce104a424c5
  SKIP
)
validpgpkeys=(1D41C14B272A2219A739FA4F8FE99503132D7742) # Antonio Diaz Diaz <antonio@gnu.org>

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    CXXFLAGS="$CXXFLAGS" LDFLAGS="$LDFLAGS"
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
