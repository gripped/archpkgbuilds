# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=mission-center
pkgver=1.0.2
pkgrel=2
pkgdesc="Monitor your CPU, Memory, Disk, Network and GPU usage"
arch=(x86_64)
url="https://missioncenter.io"
license=(GPL-3.0-or-later)
depends=(
  dconf
  dmidecode
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  libadwaita
  mesa
  nvtop
  protobuf
  systemd
  systemd-libs
)
makedepends=(
  blueprint-compiler
  cargo
  cmake
  git
  meson
)
checkdepends=(
  appstream-glib
)
source=("git+https://gitlab.com/mission-center-devs/mission-center.git#tag=v${pkgver}"
        "git+https://gitlab.com/mission-center-devs/gng.git")
sha256sums=('e3564e863f06b15392e0eb6457c616112a14744008d2bc93d217119d883aa325'
            'SKIP')

prepare() {
  cd "$pkgname"

  git submodule init
  git config submodule.subprojects/magpie.url "${srcdir}/gng"
  git -c protocol.file.allow=always submodule update

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"

  cd subprojects/magpie
  CARGO_HOME="$srcdir/build/subprojects/magpie/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=" -ffat-lto-objects"
  arch-meson "$pkgname" build --buildtype=release

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
