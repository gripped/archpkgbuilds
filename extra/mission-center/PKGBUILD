# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=mission-center
pkgver=0.6.2
pkgrel=3
pkgdesc="Monitor your CPU, Memory, Disk, Network and GPU usage"
arch=(x86_64)
url="https://missioncenter.io"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  libadwaita
  mesa
  systemd
)
makedepends=(
  blueprint-compiler
  cargo
  git
  meson
)
checkdepends=(
  appstream-glib
)
source=("git+https://gitlab.com/mission-center-devs/mission-center.git#tag=v${pkgver}")
sha256sums=('06ab2e9199dbfe74de5925f6009971ff8c1ad655bc8cd8eedb7e6186e5d661f8')

prepare() {
  cd "$pkgname"
  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  cd src/sys_info_v2/gatherer
  CARGO_HOME="$srcdir/build/src/sys_info_v2/gatherer/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=" -ffat-lto-objects"
  arch-meson "$pkgname" build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
