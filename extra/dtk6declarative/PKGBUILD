# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtk6declarative
pkgver=6.0.19
pkgrel=2
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6declarative"
license=('LGPL3')
# qt6-5compat: used in QML
# qt6-shadertools: used in QML (optdep of qt6-5compat for Qt5Compat.GraphicalEffects)
depends=('dtk6core' 'dtk6gui' 'qt6-declarative' 'qt6-base' 'qt6-5compat'
         'qt6-shadertools' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-shadertools' 'doxygen' 'gtest' 'gmock')
source=("git+https://github.com/linuxdeepin/dtk6declarative.git#tag=$pkgver"
        fix-build-on-qt-6.8.patch)
sha512sums=('845569df3c6ea496cbb72b47dbe3fe6ba65e0fe72c15010e2f8972d4073051fda225255c212647fd96fe5c24ea1f87a90e3ef840d9b1063eeac9d61b2dc44432'
            '83941cb60b15cb934259a826486cd34e7810f5fa6ed71a016d6299b81c6cb302cfa6d382ddb4bf263caf09d31e59ac95cfd5218633c1d94698b6a7f7aacc58f0')

prepare() {
  cd $pkgname
  patch -Np1 -i ../fix-build-on-qt-6.8.patch
}

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver \
    -DBUILD_DOCS=ON
  ninja
}

check() {
  cd $pkgname
  ninja test || echo "Tests failed"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
