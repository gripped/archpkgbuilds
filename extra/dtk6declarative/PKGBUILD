# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtk6declarative
pkgver=6.0.43
pkgrel=2
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6declarative"
license=('LGPL-3.0-or-later')
# qt6-5compat: used in QML
# qt6-shadertools: used in QML (optdep of qt6-5compat for Qt5Compat.GraphicalEffects)
depends=('dtk6core' 'dtk6gui' 'qt6-declarative' 'qt6-base' 'qt6-5compat'
         'qt6-shadertools' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-shadertools' 'doxygen' 'gtest' 'gmock' 'vulkan-headers')
source=("git+https://github.com/linuxdeepin/dtk6declarative.git#tag=$pkgver"
         qt-6.10.patch)
sha512sums=('90a650652afedb6f555ddff4b1c7cfade6b732c914899c76752a0ec430ee194155e0072754d5e14ee0934300a2c9ddc087b69bf4aa065ed2132c7c399bf7e911'
            '0f8641bcd5ae6f21f8617af492df77016c7c497892d9d852c5e0873710e391d52a0f1f54c30b72042479ab6e95ecc4f36d06e2a6d3e3cb19e62d444d26f360ca')

prepare() {
  cd $pkgname
  patch -p1 -i ../qt-6.10.patch
}

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOCS=ON
  ninja
}

check() {
  cd $pkgname
  ninja test || echo "Tests failed"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
