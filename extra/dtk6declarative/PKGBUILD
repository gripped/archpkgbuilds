# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtk6declarative
pkgver=6.0.14
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6declarative"
license=('LGPL3')
# qt6-5compat: used in QML
# qt6-shadertools: used in QML (optdep of qt6-5compat for Qt5Compat.GraphicalEffects)
depends=('dtk6core' 'dtk6gui' 'qt6-declarative' 'qt6-quickcontrols2' 'qt6-base' 'qt6-5compat'
         'qt6-shadertools' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-shadertools' 'doxygen' 'gtest' 'gmock')
source=("git+https://github.com/linuxdeepin/dtk6declarative.git#tag=$pkgver")
sha512sums=('9a2f0626e9b5cb0496e69215afff0e502d022b8d590111cd54d6c4110c38a478f9eaff28bb0c23858ed7e23eca66470c7c9060627f6c4902c5b2692970315045')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver \
    -DBUILD_DOCS=ON
  ninja
}

check() {
  cd $pkgname
  ninja test || echo "Tests failed"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
