# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtk6declarative
pkgver=6.0.38
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6declarative"
license=('LGPL-3.0-or-later')
# qt6-5compat: used in QML
# qt6-shadertools: used in QML (optdep of qt6-5compat for Qt5Compat.GraphicalEffects)
depends=('dtk6core' 'dtk6gui' 'qt6-declarative' 'qt6-base' 'qt6-5compat'
         'qt6-shadertools' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-shadertools' 'doxygen' 'gtest' 'gmock' 'vulkan-headers')
source=("git+https://github.com/linuxdeepin/dtk6declarative.git#tag=$pkgver")
sha512sums=('1faf47665c747c1bdba35e3b2213833997f101a0f541b06561b67b818b582b1d1d10dd69b8df423bb5f051a9f2de3bf0e51534ee20d2f6a8be9080f73bdb6310')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOCS=ON
  ninja
}

check() {
  cd $pkgname
  ninja test || echo "Tests failed"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
