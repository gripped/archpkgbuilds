# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtk6declarative
pkgver=6.0.8
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6declarative"
license=('LGPL3')
# qt6-5compat: used in QML
# qt6-shadertools: used in QML (optdep of qt6-5compat for Qt5Compat.GraphicalEffects)
depends=('dtk6core' 'dtk6gui' 'qt6-declarative' 'qt6-quickcontrols2' 'qt6-base' 'qt6-5compat'
         'qt6-shadertools' 'libgl' 'gcc-libs')
makedepends=('cmake' 'ninja' 'qt6-tools' 'qt6-shadertools' 'doxygen' 'gtest' 'gmock')
source=("https://github.com/linuxdeepin/dtk6declarative/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d955c6b98f9d80b432ff9eafa4aa1aae91bff6408617bf818c8b58b88eef9b87975a168214b2a0696aad38d25a11c4393b0aed04397133e70df3b8f95f84ea53')

build() {
  cd $pkgname-$pkgver
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver \
    -DBUILD_DOCS=ON
  ninja
}

check() {
  cd $pkgname-$pkgver
  # ninja test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
