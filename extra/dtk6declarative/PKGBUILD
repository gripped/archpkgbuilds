# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtk6declarative
pkgver=6.0.47
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6declarative"
license=('LGPL-3.0-or-later')
# qt6-5compat: used in QML
# qt6-shadertools: used in QML (optdep of qt6-5compat for Qt5Compat.GraphicalEffects)
depends=('dtk6core' 'dtk6gui' 'qt6-declarative' 'qt6-base' 'qt6-5compat'
         'qt6-shadertools' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-shadertools' 'doxygen' 'gtest' 'gmock' 'vulkan-headers')
source=("git+https://github.com/linuxdeepin/dtk6declarative.git#tag=$pkgver")
sha512sums=('94f087670b55a742cfae9601a976e1b65eb9914685459c816c092d8024d63e21ab93875b8219ae886d5b09d582b55fda0f4e6830d65b647c0b2aee4ff44fb165')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOCS=ON
  ninja
}

check() {
  cd $pkgname
  ninja test || echo "Tests failed"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
