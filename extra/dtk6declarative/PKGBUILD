# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtk6declarative
pkgver=6.0.39
pkgrel=1
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6declarative"
license=('LGPL-3.0-or-later')
# qt6-5compat: used in QML
# qt6-shadertools: used in QML (optdep of qt6-5compat for Qt5Compat.GraphicalEffects)
depends=('dtk6core' 'dtk6gui' 'qt6-declarative' 'qt6-base' 'qt6-5compat'
         'qt6-shadertools' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-shadertools' 'doxygen' 'gtest' 'gmock' 'vulkan-headers')
source=("git+https://github.com/linuxdeepin/dtk6declarative.git#tag=$pkgver")
sha512sums=('bd0296fabf54cbf7ae9aae2c8a5e21be1bd060e46b1fd666a2318ac368280af30996fe3d5dfaadecf7c2b8289fb5fb1a7f230920956133c7119171039035c2e4')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOCS=ON
  ninja
}

check() {
  cd $pkgname
  ninja test || echo "Tests failed"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
