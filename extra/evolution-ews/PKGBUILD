# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=evolution-ews
pkgver=3.58.3
pkgrel=1
pkgdesc="MS Exchange integration through Exchange Web Services"
url="https://gitlab.gnome.org/GNOME/evolution/-/wikis/home"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  evolution
  evolution-data-server
  gcc-libs
  glib2
  glibc
  gtk3
  json-glib
  libical
  libmspack
  libsoup3
  libxml2
  pango
)
makedepends=(
  cmake
  git
  glib2-devel
  intltool
  ninja
  python
  systemd
)
source=("git+https://gitlab.gnome.org/GNOME/evolution-ews.git#tag=$pkgver")
b2sums=('0196338fda5ba3c6b7fd249c93aba07613e25cdbe2df31a546ee99bad7d442dd3b7417a8db9efce31a69933b6ff8cfb655c451ca0de712cdc5a06456b5091509')

prepare() {
  cd $pkgname
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBEXEC_INSTALL_DIR=/usr/lib
    -DSYSCONF_INSTALL_DIR=/etc
  )

  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
