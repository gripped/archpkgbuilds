# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=evolution-ews
pkgver=3.56.2
pkgrel=1
pkgdesc="MS Exchange integration through Exchange Web Services"
url="https://gitlab.gnome.org/GNOME/evolution/-/wikis/home"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  evolution
  evolution-data-server
  gcc-libs
  glib2
  glibc
  gtk3
  json-glib
  libical
  libmspack
  libsoup3
  libxml2
  pango
)
makedepends=(
  cmake
  git
  glib2-devel
  intltool
  ninja
  python
  systemd
)
source=("git+https://gitlab.gnome.org/GNOME/evolution-ews.git#tag=$pkgver")
b2sums=('c09c93af2e46570c205f046acfba8dcded5fe4ef4ab4e519e5e4ea7098fb3f11684a62a241042797c9699b44a5c726827ae2e59e6f958f9cf3c1dc8bf8852145')

prepare() {
  cd $pkgname
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBEXEC_INSTALL_DIR=/usr/lib
    -DSYSCONF_INSTALL_DIR=/etc
  )

  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
