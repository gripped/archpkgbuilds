# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=evolution-ews
pkgver=3.54.3
pkgrel=1
pkgdesc="MS Exchange integration through Exchange Web Services"
url="https://gitlab.gnome.org/GNOME/evolution/-/wikis/home"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  evolution
  evolution-data-server
  gcc-libs
  glib2
  glibc
  gtk3
  json-glib
  libical
  libmspack
  libsoup3
  libxml2
  pango
)
makedepends=(
  cmake
  git
  glib2-devel
  intltool
  ninja
  python
  systemd
)
source=("git+https://gitlab.gnome.org/GNOME/evolution-ews.git#tag=$pkgver")
b2sums=('522190678975deb545e851edc0ac83a7ae8b131a1bbd4ad1289a51763e38a403b5ae8767cf977df193f966e2a5f61c4c787699ca43e6006769fb53c4c3695941')

prepare() {
  cd $pkgname
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBEXEC_INSTALL_DIR=/usr/lib
    -DSYSCONF_INSTALL_DIR=/etc
  )

  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
