# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Hao Long <imlonghao@archlinuxcn.org>

pkgname=trivy
pkgver=0.52.1
pkgrel=1
pkgdesc='A Simple and Comprehensive Vulnerability Scanner for Containers, Suitable for CI'
arch=('x86_64')
url='https://github.com/aquasecurity/trivy'
license=('Apache')
depends=('glibc')
optdepends=('rpm: RHEL/CentOS based image support')
makedepends=('go' 'btrfs-progs')
provides=('trivy')
conflicts=('trivy')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('40f05e116b90ca71dcefb13d53602030c190059338f5aa94d0a6ba1732c3283d8511ce0c4e1fddb58026490231363e8ee90fc8b06235cf2eba60ae37953a6410')

prepare() {
  cd ${pkgname}-${pkgver}

  find docs -type f -name "*.md" \
    -exec sed "s|\(../\)*imgs/\(.*\.[a-z]\{3\}\)|${url}/raw/main/docs/imgs/\2|g" -i {} \;

  rm -Rf docs/{build,imgs}
}

build() {
  cd ${pkgname}-${pkgver}/cmd/trivy

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  GO_LDFLAGS="-linkmode=external -s -w -X=github.com/aquasecurity/trivy/pkg/version.ver=${pkgver}"

  go build -ldflags "${GO_LDFLAGS}" .
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 "cmd/trivy/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  "$pkgdir/usr/bin/${pkgname}" completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/${pkgname}"
  "$pkgdir/usr/bin/${pkgname}" completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_${pkgname}"
  "$pkgdir/usr/bin/${pkgname}" completion fish | install -Dm644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/${pkgname}.fish"

  mkdir -p "${pkgdir}/usr/share/doc"
  cp -r docs "${pkgdir}/usr/share/doc/${pkgname}"
}
