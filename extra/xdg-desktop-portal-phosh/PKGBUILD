# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=xdg-desktop-portal-phosh
pkgver=0.51.0
pkgrel=1
pkgdesc='Backend implementation for xdg-desktop-portal that is using GTK/GNOME/Phosh'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/xdg-desktop-portal-phosh'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  fontconfig
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  graphene
  gtk4
  libadwaita
  libx11
  pango
  xdg-desktop-portal
)
makedepends=(
  git
  glib2-devel
  meson
  python-docutils
  rust
)
provides=(xdg-desktop-portal-impl)
source=(
  "git+https://gitlab.gnome.org/World/Phosh/xdg-desktop-portal-phosh.git?signed#tag=v$pkgver"
  git+https://gitlab.gnome.org/guidog/pfs.git
)
b2sums=(
  a27e040664cd7d62bfd445859d8094e5fb43dc3903e0bf71614b092788ff276d9c3c63f7c4140cdd1fdb3aa3ad09948a966ca24fe24f3f065ca521e77d3e71ca
  SKIP
)
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido Günther <guido@xcpu.ch>

prepare() {
  ln -s "$srcdir/pfs" "$pkgname/subprojects/pfs"
  meson subprojects checkout --sourcedir $pkgname

  cd $pkgname
  CARGO_HOME="$srcdir/build/cargo" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  cd subprojects/pfs
  CARGO_HOME="$srcdir/build/subprojects/pfs/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
