# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=xdg-desktop-portal-phosh
pkgver=0.52.0
pkgrel=1
pkgdesc='Backend implementation for xdg-desktop-portal that is using GTK/GNOME/Phosh'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Phosh/xdg-desktop-portal-phosh'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  fontconfig
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  gtk4
  libadwaita
  libx11
  pango
  xdg-desktop-portal
)
makedepends=(
  git
  glib2-devel
  meson
  python-docutils
  rust
)
provides=(xdg-desktop-portal-impl)
source=(
  "git+https://gitlab.gnome.org/World/Phosh/xdg-desktop-portal-phosh.git?signed#tag=v$pkgver"
  git+https://gitlab.gnome.org/guidog/pfs.git
)
b2sums=(
  e2ba82a3e80b48c50df34cd9bfd28ad7300256100dac413eae045f997b525c683c919116e6476782ea0fbd47c8d348547b42129de78161137ea4e94ca47de07f
  SKIP
)
validpgpkeys=(0DB3932762F78E592F6522AFBB5A2C77584122D3) # Guido Günther <guido@xcpu.ch>

prepare() {
  ln -s "$srcdir/pfs" "$pkgname/subprojects/pfs"
  meson subprojects checkout --sourcedir $pkgname

  cd $pkgname
  CARGO_HOME="$srcdir/build/cargo" \
    cargo fetch --locked --target "$(rustc --print host-tuple)"

  cd subprojects/pfs
  CARGO_HOME="$srcdir/build/subprojects/pfs/cargo-home" \
    cargo fetch --locked --target "$(rustc --print host-tuple)"

  # Don't do debug build unless requested
  sed -i "s/if get_option('buildtype') == 'release'/if not get_option('debug')/" src/meson.build
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
