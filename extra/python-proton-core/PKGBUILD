# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-core
pkgver=0.7.0
pkgrel=3
pkgdesc="The proton-core component contains core logic used by the other Proton components."
arch=(any)
url="https://github.com/ProtonVPN/python-proton-core"
license=(GPL-3.0-or-later)
depends=(
  python-aiohttp
  python-bcrypt
  python-gnupg
  python-importlib-metadata
  python-pyopenssl
  python-requests
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("git+https://github.com/ProtonVPN/${pkgname}.git#tag=v${pkgver}")
sha256sums=('146cb8829d98f55334d862c9af5f2ef9ada9c9edd5368d5107d0a4003c014a23')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
