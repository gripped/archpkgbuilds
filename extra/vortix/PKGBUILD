# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=vortix
pkgver=0.1.4
pkgrel=1
pkgdesc="A TUI for WireGuard and OpenVPN with real-time telemetry and leak guarding"
arch=('x86_64')
url="https://github.com/Harry-kp/vortix"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('3e8e816bd83ddda581baed3664538bd6bab766a473c41e425417f5c4470f26ec')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen -- --skip "app"
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
