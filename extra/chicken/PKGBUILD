# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: David <david@pako.st>

pkgname=chicken
pkgver=5.4.0
pkgrel=1
pkgdesc='Feature rich R5RS Scheme compiler and interpreter'
arch=(x86_64)
url='https://call-cc.org/'
license=(BSD-3-Clause GPL-2.0-or-later custom:Public-Domain)
makedepends=(chicken)
optdepends=('tk: for the tcl/tk egg')
options=(staticlibs)
source=("https://code.call-cc.org/releases/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('75dda6891992a7eeae8ac7624678c62c9aff61a712c9b8812b008f3369b11297019eaed259c28999a8e4f9aa587583cdef4284b5ada2bc971ef823ffa5508b35')

build() {
  make -C $pkgname-$pkgver \
    C_COMPILER_OPTIONS=" -fno-strict-aliasing -fwrapv -DHAVE_CHICKEN_CONFIG_H -Wl,--as-needed -ffat-lto-objects $CFLAGS -w" \
    CSC_PROGRAM=chicken-csc \
    CSI_PROGRAM=chicken-csi \
    LINKER_OPTIONS=" -Wl,--as-needed $LDFLAGS" \
    OPTIMIZE_FOR_SPEED=1 \
    PLATFORM=linux \
    PREFIX=/usr
}

package() {
  cd $pkgname-$pkgver
  make -j1 \
    C_COMPILER_OPTIONS=" -fno-strict-aliasing -fwrapv -DHAVE_CHICKEN_CONFIG_H -Wl,--as-needed -ffat-lto-objects $CFLAGS -w" \
    CSC_PROGRAM=chicken-csc \
    CSI_PROGRAM=chicken-csi \
    DESTDIR="$pkgdir" \
    LINKER_OPTIONS=" -Wl,--as-needed $LDFLAGS" \
    OPTIMIZE_FOR_SPEED=1 \
    PLATFORM=linux \
    PREFIX=/usr \
    install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
