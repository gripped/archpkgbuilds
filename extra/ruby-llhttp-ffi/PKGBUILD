# Maintainer: Felix Yan <felixonmars@archlinux.org>

_gemname='llhttp-ffi'
pkgname="ruby-${_gemname}"
_tag="2025-03-11"
pkgver=0.5.1
pkgrel=4
pkgdesc='Ruby FFI bindings for llhttp'
arch=(x86_64)
url='https://github.com/bryanp/llhttp'
license=(Apache)
depends=(
  ruby
  ruby-ffi-compiler
  ruby-rake
)
makedepends=(
  ruby-rspec
)
checkdepends=(
  ruby-async
  ruby-bundler
  ruby-io-endpoint
  ruby-rake-compiler
)
options=(!emptydirs)
source=("${url}/archive/${_tag}/${pkgname}-${_tag}.tar.gz")
sha512sums=('7c9c71a6a873882f3f8a2d6e379aa73e8fb675240e81b3395845f32f74941dfaaa0170719a2e00817eca9c1040b30cf4113d0d0561cb74fc17bf84fb5cd52fca')
b2sums=('a95f1ec359edf82ee67ddd6bf96b68ec11d8452287174017a423214b8cfe496df932966c1ab0decffe48be5fc1b70b9eaf345881eab2c01d755fff6c818d07ea')

prepare() {
  cd "llhttp-${_tag}/ffi"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "llhttp-${_tag}/ffi"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "llhttp-${_tag}/ffi"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "llhttp-${_tag}/ffi"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
