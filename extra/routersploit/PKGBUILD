# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>

pkgname=routersploit
pkgver=3.4.6
pkgrel=1
pkgdesc='Open-source exploitation framework dedicated to embedded devices'
url='https://github.com/threat9/routersploit'
arch=(any)
license=(BSD-3-Clause)
depends=(
  python
  python-bluepy
  python-cryptography
  python-paramiko
  python-pycryptodome
  python-pysnmp
  python-requests
  python-setuptools
  python-urllib3
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  flake8
  python-pytest
  python-pytest-forked
  python-pytest-xdist
  python-threat9-test-bed
)
source=(
  https://github.com/threat9/routersploit/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
  routersploit-drop-python-future.patch
)
sha512sums=('24f10a09f5e03e13fe9ef9925244045cf9ef35b26ff05ca4563682a584ec066d94c26dafe7ef6de836ccec18a092e61757c27a30e0fea1848703cc9f0cf3a937'
            '9eff1bc7b579cb51451874de35cd38fa4662349e3b568440ae16e339aea9348766c8586d05eb75c254412cec50130dbc14f4be721705b7b844c76a70de274b9e')
b2sums=('88d80104887d2dea7e0ae3b958303ce8e3dd67dbc31f8eaedac314940deb8693f8332f6933843a6b4dcb73c495a8e21f5415b091b0df29dcc5af9f291dc853c2'
        'ec08359478f7d3f716ca52045026f101d9a355a4bc69ff18ade75a468b902bb43ac2bd796e0b0012040dad7a6380955a444d5c9c635bb620df89953c4493847a')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 < ../routersploit-drop-python-future.patch

  # Remove failing tests
  rm tests/exploits/misc/miele/test_pg8528_path_traversal.py
  rm tests/exploits/cameras/xiongmai/test_uc_httpd_path_traversal.py
  rm tests/exploits/routers/dlink/test_dns_320l_327l_rce.py
  rm tests/exploits/routers/tplink/test_wdr740nd_wdr740n_path_traversal.py
  rm tests/exploits/routers/3com/test_officeconnect_rce.py
  rm tests/exploits/cameras/multi/test_jvc_vanderbilt_honeywell_path_traversal.py
  rm tests/exploits/routers/linksys/test_1500_2500_rce.py
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}

  python -m installer --destdir="${pkgdir}" dist/*.whl
  rm -rf "${pkgdir}"/usr/lib/python*/site-packages/tests
  mv "${pkgdir}/usr/bin/rsf"{.py,}

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
