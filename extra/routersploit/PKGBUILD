# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>

pkgname=routersploit
pkgver=3.4.7
pkgrel=2
pkgdesc='Open-source exploitation framework dedicated to embedded devices'
arch=(any)
url='https://github.com/threat9/routersploit'
license=(BSD-3-Clause)
depends=(
  python
  python-bluepy
  python-cryptography
  python-paramiko
  python-pycryptodome
  python-pysnmp
  python-requests
  python-setuptools
  python-urllib3
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-forked
  python-pytest-xdist
  python-threat9-test-bed
)
source=(
  "$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "$pkgname-python-3.13-compat-remove-telnet-support.patch"
)
sha512sums=('e2a649b9c49b9eb564f93d0525b5221bff931571e5add7ff8ef2f30bd2c34f1bba5285b073d807671b6ff527da1bbfdb7887d1b785bba729b984b8be62628e5e'
            'a8d777e2a97205ce4ed7ab36541db4648bb5c3bc43a46806a76eceb98b1a158da0dd0647cdeb2ae2e7a1041c31102ff22cb46af6ab4ab4256ff9e2bc72db8230')
b2sums=('289afca5403ba0e1b9361a613034bfd0bb9e248e0d2ce18d005579fcdc74e783de252ae69dfbf4ae042fc82ec7d9202f86dbc7dcde0b17ec994b8dc87fd464c1'
        'cb806ef3235d61f4cd98a7979bffe192e232fd1127a05bf6acc49d435bd01290c5fcddc066fc336f6e5e8f4cf076156bf58253f3e76406ae3e4941488f74b9e8')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../$pkgname-python-3.13-compat-remove-telnet-support.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  local pytest_args=(
    -n auto
    -k 'not telnet'

    # python-threat9-test-bed uses deprecated asyncio.get_event_loop() which
    # raises RuntimeError in Python 3.14.
    --ignore=tests/creds/

    # ssh_auth_keys uses paramiko.DSSKey which was removed (DSA is obsolete).
    --deselect=tests/test_exploit_scenarios.py::test_exploit_trash_response[Exploit23]
    --deselect=tests/test_exploit_scenarios.py::test_exploit_empty_response[Exploit23]
    --deselect=tests/test_exploit_scenarios.py::test_exploit_not_found_response[Exploit23]
    --deselect=tests/test_exploit_scenarios.py::test_exploit_error_response[Exploit23]
    --deselect=tests/test_exploit_scenarios.py::test_exploit_redirect_response[Exploit23]
    --deselect=tests/test_exploit_scenarios.py::test_exploit_timeout_response[Exploit23]

    # Flaky tests.
    --deselect=tests/exploits/cameras/multi/test_cctv_dvr_rce.py::test_check_success
    --deselect=tests/exploits/cameras/multi/test_jvc_vanderbilt_honeywell_path_traversal.py::test_check_v2_success
    --deselect=tests/exploits/cameras/xiongmai/test_uc_httpd_path_traversal.py::test_check_success
    --deselect=tests/exploits/misc/miele/test_pg8528_path_traversal.py::test_check_success
    --deselect=tests/exploits/routers/3com/test_officeconnect_rce.py::test_check_success
    --deselect=tests/exploits/routers/linksys/test_1500_2500_rce.py::test_check_success
    --deselect=tests/exploits/routers/tplink/test_wdr740nd_wdr740n_path_traversal.py::test_check_success
  )
  pytest "${pytest_args[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  rm -vrf "$pkgdir"/usr/lib/python*/site-packages/tests
  mv -v "$pkgdir/usr/bin/rsf"{.py,}

  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr -t "$pkgdir/usr/share/doc/$pkgname" docs/*
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim: ts=2 sw=2 et:
