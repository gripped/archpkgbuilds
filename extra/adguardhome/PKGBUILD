# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Pavers_Career <pavers_career_0d AT Ã­cloud DOT com>

pkgname=adguardhome
_name=AdGuardHome
pkgver=0.107.59
pkgrel=1
epoch=1
pkgdesc='Network-wide ads and trackers blocking DNS server'
arch=(x86_64)
url='https://github.com/AdguardTeam/AdGuardHome'
license=(GPL-2.0-only)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        $pkgname.service)
makedepends=(go nodejs npm git)
depends=(glibc)
b2sums=('f0071eab28194d36874cfaeef08a0bf159e76ad7a09fe7456e5372e4a338fec63a131566914cf6f8ebf889c867259e268d1ebaa28f79e9aeb85385890939c3a2'
        '161152f91e09fe491db631eb6ed603c0c975453b682467945fdade6091bf427ec932230f3a10e40e2f054dc01567930ecc27343c04882fb0e736b4f6becc96da')

prepare() {
  cd $_name-$pkgver
  npm --prefix client ci
  go mod download
}

build() {
  cd $_name-$pkgver
  npm --prefix client run build-prod

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X 'github.com/AdguardTeam/AdGuardHome/internal/version.version=v$pkgver' -X 'github.com/AdguardTeam/AdGuardHome/internal/version.channel=release'" \
    -o $pkgname
}

package() {
  install -Dm755 $_name-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 $pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -dm755 "$pkgdir"/etc
  ln -s /var/lib/$pkgname/$_name.yaml "$pkgdir"/etc/$pkgname.yaml
}

# vim:set ts=2 sw=2 et:
