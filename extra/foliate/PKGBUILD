# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=foliate
pkgver=3.2.0
pkgrel=1
pkgdesc='A simple and modern GTK eBook reader'
arch=(any)
url="https://johnfactotum.github.io/$pkgname/"
_url="https://github.com/johnfactotum/$pkgname"
license=(GPL-3.0-or-later)
depends=(gjs
         gtk4
         libadwaita
         # iso-codes
         webkitgtk-6.0)
makedepends=(gettext
             meson
             ninja)
optdepends=(#'dictd: Offline dictionary support'
            'espeak-ng: Text-to-speech support; also voice package needed'
            'festival: Text-to-speech support; also voice package needed'
            'gspell: Spell-checking support'
            'hyphen: Auto-hyphenation support'
            'tracker3')
_archive="com.github.johnfactotum.${pkgname^}-$pkgver"
source=("$_url/releases/download/$pkgver/$_archive.tar.xz")
sha256sums=('714d42d54e103f4a3c367bf18186a56401228b7ee1175224e760ae59f204e1e6')

build() {
	cd "$_archive"
	meson build --prefix=/usr
	ninja -C build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" ninja -C build install
}
