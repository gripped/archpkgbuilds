# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=zenith
pkgver=0.14.3
pkgrel=1
pkgdesc="Terminal system monitor with histograms"
arch=('x86_64')
url="https://github.com/bvaisvil/zenith"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo' 'clang')
optdepends=('nvidia-utils')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('b092048d1a9ce7234584d928e4b103aaaa7e47589923cf4e48dfa8919b3f8d88')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 "README.md" -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 "LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "assets/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
  install -Dm 644 "assets/$pkgname.png" -t "$pkgdir/usr/share/pixmaps"
}

# vim: ts=2 sw=2 et:
