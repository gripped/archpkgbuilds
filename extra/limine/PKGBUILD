# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Mintsuki <mintsuki@protonmail.com>

pkgname=limine
pkgver=5.20230917.0
pkgrel=1
pkgdesc="An advanced, portable, multiprotocol bootloader"
arch=('x86_64')
url="https://limine-bootloader.org/"
license=('BSD')
depends=('glibc')
makedepends=('nasm' 'mtools' 'llvm' 'lld' 'clang')
source=("https://github.com/limine-bootloader/limine/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2d9b55e798e951ebd3d4266303e9cefda2ee9f7055a3c877dc7e306f0e94ec91')

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-all TOOLCHAIN_FOR_TARGET=llvm
	make
}

package() {
	cd "${pkgname}-${pkgver}"
  	make DESTDIR="${pkgdir}" install
  	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
