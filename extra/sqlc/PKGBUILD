# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alkindi42

pkgname=sqlc
pkgver=1.27.0
pkgrel=1
pkgdesc='Compile SQL to type-safe Go'
arch=('x86_64')
url='https://sqlc.dev'
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/sqlc-dev/sqlc.git#tag=v$pkgver")
sha512sums=('fb88f9fd10544871418dfe2f0e961123634c76454b6c77f4ad9462dfe8d5177a8ac1179a509435c943587d8d39bdb9d7611c7ddb2bd4fd4b72a51bb5e6ac1f91')
b2sums=('e7d15c961a75aa20d214f9945fc6c362ed881bce89506053464ea52d08c3775b8e426817899dd3abcfe5b15a05c9c7d4479b5d0150132bd2f235141e67d572b7')

prepare() {
  cd "$pkgname"

  # create folder for build output
  mkdir -p build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # build binary
  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./cmd/...

  # generate shell completions
  for sh in bash fish zsh; do
    ./build/sqlc completion "$sh" > "build/$sh"
  done
}

check() {
  cd "$pkgname"

  # skip internal/endtoend
  rm -f internal/endtoend/endtoend_test.go

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/sqlc

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell completions
  install -vDm644 build/bash "$pkgdir/usr/share/bash-completion/completions/sqlc"
  install -vDm644 build/fish "$pkgdir/usr/share/fish/vendor_completions.d/sqlc.fish"
  install -vDm644 build/zsh "$pkgdir/usr/share/zsh/site-functions/_sqlc"
}
