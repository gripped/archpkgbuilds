# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alkindi42

pkgname=sqlc
pkgver=1.30.0
pkgrel=1
pkgdesc='Compile SQL to type-safe Go'
arch=(x86_64)
url='https://sqlc.dev'
license=(MIT)
depends=(glibc gcc-libs)
makedepends=(git go)
options=(!lto)
source=("$pkgname::git+https://github.com/sqlc-dev/sqlc.git#tag=v$pkgver")
sha512sums=('43219cc4792801dc6ec01231724a3a21d6cc3079bbda0f3e008d246d5265da325d6e88f4d8b589e785c22d49315db2cdf8d1765001a22a2447b0a781fc7fdbb0')
b2sums=('c3c2751b7bc9e6f5410649d4d416a4604ffae87a1db3acf98dfe554ca5ea311a3d36b3c7d1e331a6e979898b6b53489fd97fd271957b9c8a811886cc9241f4eb')

prepare() {
  cd "$pkgname"

  # create folder for build output
  mkdir -p build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # build binary
  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./cmd/...

  # generate shell completions
  for sh in bash fish zsh; do
    ./build/sqlc completion "$sh" > "build/$sh"
  done
}

check() {
  cd "$pkgname"

  # skip internal/endtoend
  rm -f internal/endtoend/endtoend_test.go

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/sqlc

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell completions
  install -vDm644 build/bash "$pkgdir/usr/share/bash-completion/completions/sqlc"
  install -vDm644 build/fish "$pkgdir/usr/share/fish/vendor_completions.d/sqlc.fish"
  install -vDm644 build/zsh "$pkgdir/usr/share/zsh/site-functions/_sqlc"
}
