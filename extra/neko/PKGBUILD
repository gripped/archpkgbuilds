# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Dwight Schauer <dschauer@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Caleb McCombs <erdrick016+aur@gmail.com>
# Contributor: Christian Hesse <arch@eworm.de>

pkgname=neko
pkgver=2.4.1
pkgrel=1
pkgdesc='High-level dynamically typed programming language'
url='https://nekovm.org/'
license=(MIT)
arch=(x86_64)
depends=(gc gtk3 mariadb-libs mbedtls2 sqlite)
makedepends=(apache apr cmake git)
optdepends=('apache: for extending Apache with mod_neko')
options=(!strip)
source=(neko-mbedtls2.patch
        "git+https://github.com/HaxeFoundation/neko#tag=v${pkgver//./-}")
b2sums=('ea9c86b2f6ac8e943f0c70406a73a055468e71de8740adc2116e0cfd9e68d08ac0d071ce94119a52954fed08319176308395ae20715ce7bd0d38fb538c95f765'
        '6062a1232314a3de960968618ffb95b69178e0a53d5658ea780afb0ae1289d9bb2b79f080ec46761f9a1317e0a2a9261bdf2e2e7dac33888bb47d585e67a456a')

prepare() {
  cd $pkgname
  sed -i '/xlocale.h/d' libs/std/sys.c
  patch -p1 -i ../neko-mbedtls2.patch
}

build() {
  cmake \
    -B build \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D RUN_LDCONFIG=OFF \
    -S $pkgname
  make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
