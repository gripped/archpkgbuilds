# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=grml-zsh-config
pkgver=0.19.12
pkgrel=1
pkgdesc="grml's zsh setup"
arch=('any')
url='https://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh' 'coreutils' 'grep' 'sed' 'procps')
makedepends=('git' 'txt2tags')
validpgpkeys=('7D1ACFFAD9E0806C9C4CD3925C13D6DB93052E03'  # Christian Hofstaedtler <christian@hofstaedtler.name>
              '33CCB136401AFEC843A3876396A87872B7EA3737') # Michael Prokop <mail@michael-prokop.at>
source=("git+https://github.com/grml/grml-etc-core.git#tag=v${pkgver}?signed")
sha256sums=('a66c7f4f3667fabc7cb724aebe9598f1b17b59622eb80a31d81c96953ddc995c')

build() {
	cd "${srcdir}/grml-etc-core/doc"
	make
}

package() {
	cd "${srcdir}/grml-etc-core"
	install -D -m644 etc/skel/.zshrc "${pkgdir}/etc/skel/.zshrc"
	install -D -m644 etc/zsh/keephack "${pkgdir}/etc/zsh/keephack"
	install -D -m644 etc/zsh/zshrc "${pkgdir}/etc/zsh/zshrc"

	install -D -m644 doc/grmlzshrc.5 "${pkgdir}/usr/share/man/man5/grmlzshrc.5"
	ln -sf grmlzshrc.5.gz "${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz"
}
