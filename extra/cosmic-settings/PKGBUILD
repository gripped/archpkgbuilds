# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-settings
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='The settings application for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-settings
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  accountsservice
  cosmic-icon-theme
  cosmic-randr
  fontconfig
  gcc-libs
  glibc
  iso-codes
  libinput
  libpipewire
  libpulse
  libxkbcommon
  networkmanager
  nm-connection-editor
  pipewire-pulse
  systemd-libs
  ttf-opensans
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
optdepends=('power-profiles-daemon: power profiles support')
_tag=bc70106fe568c5abf54fa18befe65b2b3f521e27
source=(git+https://github.com/pop-os/cosmic-settings.git#tag=${_tag})
b2sums=('fffacc202efb89767f01fc1435a4645c408f866aeff8003924c5077a1437618eaef0f223c71862736741292fd559264812fcdd96d57686cec82d8b327eb81866')

prepare() {
  cd cosmic-settings
  cargo fetch --locked
}

pkgver() {
  cd cosmic-settings
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-settings
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-settings
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
