# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-settings
pkgver=1.0.0.alpha.4
pkgrel=1
pkgdesc='The settings application for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-settings
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  accountsservice
  cosmic-icon-theme
  cosmic-randr
  fontconfig
  gcc-libs
  glibc
  iso-codes
  libinput
  libpipewire
  libpulse
  libxkbcommon
  otf-fira-mono
  otf-fira-sans
  systemd-libs
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
optdepends=('power-profiles-daemon: power profiles support')
_tag=d01e0d85fbd2836489d8092381fc4637fc51ce25
source=(git+https://github.com/pop-os/cosmic-settings.git#tag=${_tag})
b2sums=('79c7fb8b0fea728b407f5248783e965ba80488c253bf19b5a5566ede87d87ac6c848b29e0afc72de77cbfc556ea88962151cef7e49e07ddf2aaf8a7e8f2798df')

prepare() {
  cd cosmic-settings
  cargo fetch --locked
}

pkgver() {
  cd cosmic-settings
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-settings
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-settings
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
