# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-settings
pkgver=1.0.0.alpha.3
pkgrel=1
pkgdesc='The settings application for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-settings
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  accountsservice
  cosmic-icon-theme
  cosmic-randr
  fontconfig
  gcc-libs
  glibc
  iso-codes
  libinput
  libpipewire
  libpulse
  libxkbcommon
  otf-fira-mono
  otf-fira-sans
  systemd-libs
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
optdepends=('power-profiles-daemon: power profiles support')
_tag=ef051c3b13718969e92ade81d98871688ddd8bfe
source=(git+https://github.com/pop-os/cosmic-settings.git#tag=${_tag})
b2sums=('d600f96f0a6560be7be5f8649c5f12a1b2a76b4669249507041378039080658dff978c1de7b49106bd34d40f360af0a6bdd1a1702dffffb8ae1f98789d78605b')

prepare() {
  cd cosmic-settings
  cargo fetch --locked
}

pkgver() {
  cd cosmic-settings
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-settings
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-settings
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
