# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-jaconv
pkgver=0.4.0
pkgrel=1
pkgdesc='Japanese character interconverter for Hiragana, Katakana, Hankaku, and Zenkaku'
url='https://ikegami-yukino.github.io/jaconv/jaconv.html'
license=(MIT)
arch=(any)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-nose
  python-pytest
)
#source=("$pkgname::git+https://github.com/ikegami-yukino/jaconv#tag=v$pkgver")
source=("$pkgname::git+https://github.com/ikegami-yukino/jaconv#commit=1d8aca73a72a4615b165602af9890517444e45d9")
sha512sums=('d32c04fa0e58a09b555dba0c8f2ff6ea6e6028a09a9c3423f3f159f3fd513c10085498fc7972831d829609bfd3bf8404cf1cdd46ec9b3213e4102cb94d8f0bc1')
b2sums=('bd5aca923db9f49d5ba74ca7da271f1b241b75b5b0ca5650457735df83afa1d8f044a16cfb99c49b19b669ce6bda1fd2ce1e5ce6996b3c9943bd3367e408483b')

prepare() {
  cd "$pkgname"
  # Don't install {CHANGES,README}.rst to /usr
  # Upstream bug report: https://github.com/ikegami-yukino/jaconv/issues/28
  git cherry-pick --no-commit 6a1c0fc05f1eac50d5a47d83e988efa1f9a84cf7
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}
  
package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
