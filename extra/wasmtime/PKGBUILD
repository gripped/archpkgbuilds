# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=wasmtime
pkgver=23.0.1
pkgrel=1
pkgdesc='Standalone JIT-style runtime for WebAssembly, using Cranelift'
arch=('x86_64')
url='https://github.com/bytecodealliance/wasmtime'
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo' 'git')
options=('!lto')
source=("git+https://github.com/bytecodealliance/wasmtime.git#commit=v${pkgver}"
        git+https://github.com/WebAssembly/testsuite.git
        git+https://github.com/WebAssembly/wasm-c-api.git
        git+https://github.com/WebAssembly/WASI.git
        git+https://github.com/WebAssembly/wasi-nn.git
        git+https://github.com/WebAssembly/wasi-crypto.git)
b2sums=('7ab640ce1274c089bb41239c86efb873d09d5fd816f966457c426b0a72e54e1a1edaed1a801d7e05d94f6100c43819c27ab6cdb200b2e78940869366b4a5d12e'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule.spec_testsuite.src "${srcdir}"/testsuite
  git config submodule.crates/c-api/examples/wasm-c-api.src "${srcdir}"/wasm-c-api
  git config submodule.WASI.src "${srcdir}"/WASI
  git config submodule.crates/wasi-nn/spec.src "${srcdir}"/wasi-nn
  git config submodule.crates/wasi-crypto/spec.src "${srcdir}"/wasi-crypto
  git submodule update
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --locked --release
  cargo build --locked --release --manifest-path crates/c-api/Cargo.toml
}

package() {
  cd ${pkgname}
  install -Dm755 target/release/${pkgname} "${pkgdir}"/usr/bin/$pkgname
}
