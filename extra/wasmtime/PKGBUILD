# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=wasmtime
pkgver=40.0.2
pkgrel=1
pkgdesc='Standalone JIT-style runtime for WebAssembly, using Cranelift'
arch=('x86_64')
url='https://github.com/bytecodealliance/wasmtime'
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo' 'cmake' 'git')
options=('!lto')
source=("git+https://github.com/bytecodealliance/wasmtime.git#commit=v${pkgver}"
        git+https://github.com/WebAssembly/testsuite.git
        git+https://github.com/WebAssembly/wasm-c-api.git
        git+https://github.com/WebAssembly/WASI.git
        git+https://github.com/WebAssembly/wasi-nn.git
        git+https://github.com/WebAssembly/wasi-crypto.git)
b2sums=('41a44290b5343ca738e2263ddd06e2c03b58890aef644df3c45359966704e33660791ea19a9e31add46f170760d2fee676b6fa39c9e53531f16f4ca37f8b217f'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule.spec_testsuite.src "${srcdir}"/testsuite
  git config submodule.crates/c-api/examples/wasm-c-api.src "${srcdir}"/wasm-c-api
  git config submodule.WASI.src "${srcdir}"/WASI
  git config submodule.crates/wasi-nn/spec.src "${srcdir}"/wasi-nn
  git config submodule.crates/wasi-crypto/spec.src "${srcdir}"/wasi-crypto
  git submodule update
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}
  cargo build --locked --release
  cargo build --locked --release --manifest-path crates/c-api/Cargo.toml
}

package() {
  cd ${pkgname}
  install -Dm755 target/release/${pkgname} "${pkgdir}"/usr/bin/$pkgname
}
