# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Maintainer: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-api-core
pkgver=4.14.1
pkgrel=4
pkgdesc="The proton-vpn-core-api acts as a facade to the other Proton VPN components, exposing a uniform API to the available Proton VPN services."
arch=("any")
url="https://github.com/ProtonVPN/python-proton-vpn-api-core"
license=(GPL-3.0-or-later)
depends=(
  networkmanager-openvpn
  libnm
  python
  python-cryptography
  python-distro
  python-jinja
  python-proton-core
  python-proton-vpn-local-agent
  python-cairo
  python-gobject
  python-pynacl
  python-packaging
  python-sentry_sdk
  python-fido2
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
provides=(python-proton-vpn-network-manager)
replaces=(python-proton-vpn-network-manager)
source=("git+https://github.com/ProtonVPN/${pkgname}.git#tag=v${pkgver}")
sha256sums=('dcac6fb893b1d4b5fd62e0e9de8106ddaa309f40c6b388b01384eab9e7f2e10f')
optdepends=("network-manager-applet: password prompt on Gnome and other GTK DEs"
            "plasma-nm: password prompt on KDE Plasma")
build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
