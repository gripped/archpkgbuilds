# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=wl-clip-persist
pkgver=0.5.0
pkgrel=2
pkgdesc="Keep Wayland clipboard even after programs close"
url="https://github.com/Linus789/wl-clip-persist"
arch=('x86_64')
license=('MIT')
depends=('glibc' 'libgcc')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('fdd2506e6556dda943a164d891fe498985838fdd0e94c54e595a8f1cd8c49b66')

prepare() {
	cd "${pkgname}-${pkgver}"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
	cd "${pkgname}-${pkgver}"
	cargo build --frozen --release --all-features
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
