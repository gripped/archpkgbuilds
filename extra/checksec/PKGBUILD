# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>

pkgname=checksec
pkgver=3.1.0
pkgrel=2
pkgdesc='Tool designed to test which standard Linux OS and PaX security features are being used'
url='https://slimm609.github.io/checksec/'
arch=(any)
license=(BSD-3-Clause)
depends=(
  glibc
)
makedepends=(
  go
)
options=('!lto')
source=("https://github.com/slimm609/checksec/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7a7338b1451161cf5bcc4c44a0b04e5351c830008c2f82ef0699b2cf630a061d06ee51647c8d61259ede4631fe2c77e203f3b9574041e6b2fb93cc5ad9100506')
b2sums=('a8aeb49dd03c1a6f4cf227f5f9de50a08e5f55615a04a4545228c9ad44bce8b43d173e0f860c97f247deb1ac0b37b700024d84c5a85f8dcec2a547c8402d7d38')

prepare() {
  cd ${pkgname}-${pkgver}

  GOPATH="${srcdir}"
  go mod download
}

build() {
  cd ${pkgname}-${pkgver}

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  GOPATH="${srcdir}"
  export GOPROXY=off
  go build \
    -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags " \
      -compressdwarf=false \
      -linkmode external \
      -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 checksec -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGELOG.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 extras/man/checksec.1 -t "${pkgdir}/usr/share/man/man1"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/checksec" completion bash > "${pkgdir}/usr/share/bash-completion/completions/checksec"
  "${pkgdir}/usr/bin/checksec" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_checksec"
  "${pkgdir}/usr/bin/checksec" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/checksec.fish"
}

# vim: ts=2 sw=2 et:
