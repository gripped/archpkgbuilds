# Maintainer: Sven-Hendrik Haase
pkgname=vkmark
pkgdesc='Vulkan benchmark'
epoch=1
pkgver=2025.01
pkgrel=2
url='https://github.com/vkmark/vkmark'
arch=('x86_64')
license=('LGPL-2.1-only')
depends=('vulkan-icd-loader' 'assimp')
makedepends=('vulkan-headers' 'meson' 'ninja' 'wayland-protocols' 'glm' 'xcb-util-wm'
             'xorgproto' 'wayland')
optdepends=('xcb-util-wm: for X11 support'
            'wayland: for Wayland support')
source=($pkgname-$pkgver.tar.gz::https://github.com/vkmark/vkmark/archive/refs/tags/$pkgver.tar.gz)
sha256sums=('1ae362844344d0f9878b7a3f13005f77eae705108892a4e8abf237d452d37edc')

build() {
  cd vkmark-$pkgver

  meson --prefix=/usr build
  ninja -C build
}

package() {
  cd vkmark-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
}

