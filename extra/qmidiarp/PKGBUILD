# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgbase=qmidiarp
pkgname=(
  qmidiarp
  qmidiarp-{lv2,standalone}
)
pkgver=0.7.4
pkgrel=1
pkgdesc="A MIDI arpeggiator, phrase generator and controller LFO for the ALSA sequencer."
arch=(x86_64)
url="https://sourceforge.net/projects/qmidiarp/"
license=(GPL-2.0-or-later)
makedepends=(
  alsa-lib
  gcc-libs
  glibc
  jack
  liblo
  lv2
  qt5-base
  qt5-tools
)
checkdepends=(lv2lint)
source=(https://downloads.sourceforge.net/project/$pkgbase/$pkgbase/$pkgver/$pkgbase-$pkgver.tar.bz2)
sha512sums=('344d8cd8f1c5ae9d736d6661c2db38fe3726348a909e303de870a805d003afdd4f7dd925f8c21a25226fa503a6ace3393541f33b22cdcf36a7d81424e958825b')
b2sums=('df07d58f087c55463f28d0b29a984d4778f05957b8c4d9ae26e7f97666cba64db58c6e16c6eda908a4a0d60a1a617176b6b6ca5b67d557be2daf13a9c5660ca0')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  cd $pkgbase-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --enable-qt5
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  local _name

  cd $pkgbase-$pkgver
  # NOTE: we are not running tests for lfo and seq variants because they make lv2lint segfault...
  for _name in arp; do
    cp -v "src/.libs/${pkgbase}_$_name"{,_ui}.so "${pkgbase}_$_name.lv2/"
    # disable failing tests: https://sourceforge.net/p/qmidiarp/bugs/33/
    lv2lint -t "UI Symbols" -t "Plugin Symbols" -Mpack -I "${pkgbase}_${_name}.lv2/" "https://git.code.sf.net/p/qmidiarp/$_name"
  done
}

package_qmidiarp() {
  depends=($pkgbase-{lv2,standalone}=$pkgver)

  make DESTDIR="$pkgdir/" install -C $pkgbase-$pkgver

  (
    cd "$pkgdir"
    _pick $pkgbase-lv2 usr/lib
    _pick $pkgbase-standalone usr/bin
    _pick $pkgbase-standalone usr/share
  )
}

package_qmidiarp-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    gcc-libs
    glibc
    lv2-host
    qt5-base
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_qmidiarp-standalone() {
  pkgdesc+=" - standalone"
  groups=(
    pro-audio
  )
  depends=(
    alsa-lib
    gcc-libs
    glibc
    hicolor-icon-theme
    jack
    liblo
    qt5-base
  )
  optdepends=('new-session-manager: for session management')

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
