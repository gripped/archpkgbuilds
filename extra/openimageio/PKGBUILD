# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=openimageio
pkgver=3.1.10.0
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('x86_64')
url="http://www.openimageio.org/"
license=('Apache-2.0')
depends=('openexr' 'boost-libs' 'giflib' 'openjpeg2' 'glew' 'libtiff' 'libjpeg-turbo'
         'opencolorio' 'libpng' 'libraw' 'libwebp' 'pugixml' 'pybind11'
         'libheif' 'hdf5' 'freetype2' 'libstdc++' 'glibc' 'zlib' 'libgcc') # TODO: Consider adding these deps: 'openvdb' 'ffmpeg' 'ptex' 'opencv'
makedepends=('cmake' 'qt5-base' 'python' 'boost' 'mesa' 'fontconfig' 'libxrender' 'ninja' 'robin-map' 'fmt')
optdepends=('qt5-base: iv image viewer'
            'python: bindings support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenImageIO/oiio/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('7e3b295562c97d39d50e8c820686baea2b8e7bd257d85a2660d63f7515a88b58bad5e97b3890a9a99e0263888745884831f9f10021752fe5f60d38ad607f3390')

build() {
  cd OpenImageIO-$pkgver

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cmake \
    -Bbuild \
    -GNinja \
    -DUSE_PYTHON=ON \
    -DPYTHON_VERSION=$python_version \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOIIO_BUILD_TESTS=ON \
    -DOIIO_BUILD_TOOLS=ON \
    -DOIIO_INTERNALIZE_FMT=OFF \
    -DUSE_EXTERNAL_PUGIXML=ON \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenImageIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install

  # Remove vendored fonts
  rm -r "$pkgdir"/usr/share/fonts

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE".md
}
