# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=legba
pkgver=1.2.0
pkgrel=1
pkgdesc="A multiprotocol credentials bruteforcer / password sprayer and enumerator"
url='https://github.com/evilsocket/legba'
arch=('x86_64')
license=(
  'AGPL-3.0-only'
  'GPL-3.0-only'
)
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
  'clang'
  'cmake'
)
options=('!lto')
source=(https://github.com/evilsocket/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('d618c59060b76fcff26c066c1a2385c85f8c808bc07e2a1f30c777255de2a400')
b2sums=('f849e1dd1ffd713e08f274ded5b7c7b50bd844118df73ee0a6438ae74037f38f792691fa3b14844eb18c39898f53457af4e1eecf58cc01bdc565fe72907be77d')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/legba
}

# vim: ts=2 sw=2 et:
