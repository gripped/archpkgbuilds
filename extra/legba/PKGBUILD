# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=legba
pkgver=1.1.1
pkgrel=1
pkgdesc="A multiprotocol credentials bruteforcer / password sprayer and enumerator"
url='https://github.com/evilsocket/legba'
arch=('x86_64')
license=(
  'AGPL-3.0-only'
  'GPL-3.0-only'
)
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
  'clang'
  'cmake'
)
options=('!lto')
source=(https://github.com/evilsocket/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a5c7254b19910e2b1816a9e4af3720b772e35d1b0491cb42f90564ca01699d6f')
b2sums=('71dd498db4e71113a964ab1fa00a7b99c532ac310759f002f6cac69d2c4965569f07071d931f145a0f93fb718018a3ede41371c64df08e419d8ac8dd5f0175c2')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/legba
}

# vim: ts=2 sw=2 et:
