# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=legba
pkgver=0.10.0
pkgrel=1
pkgdesc="A multiprotocol credentials bruteforcer / password sprayer and enumerator"
url='https://github.com/evilsocket/legba'
arch=('x86_64')
license=(
  'AGPL-3.0-only'
  'GPL-3.0-only'
)
depends=(
  'gcc-libs'
  'glibc'
  'libssl.so'
  'smbclient'
)
makedepends=(
  'cargo'
  'cmake'
)
options=('!lto')
source=(https://github.com/evilsocket/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('9755ec21539ec31dfc6c314dde1416c9b2bc79199f5aceb937e84bafc445b208')
b2sums=('81522d57e0088e33dd7d154e1154d4e3775bab4429d60b7a00ed6fe49b22d85e588cd25efa8aa7f86ee9246b4c8987186f14bae9e7f0bf6e9417e6d9aa655fcb')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/legba
}

# vim: ts=2 sw=2 et:
