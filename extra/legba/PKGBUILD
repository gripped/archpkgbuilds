# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=legba
pkgver=0.11.0
pkgrel=2
pkgdesc="A multiprotocol credentials bruteforcer / password sprayer and enumerator"
url='https://github.com/evilsocket/legba'
arch=('x86_64')
license=(
  'AGPL-3.0-only'
  'GPL-3.0-only'
)
depends=(
  'gcc-libs'
  'glibc'
  'libssl.so'
  'smbclient'
)
makedepends=(
  'cargo'
  'cmake'
)
options=('!lto')
source=(https://github.com/evilsocket/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c5e0cf14d372792ac99d692894d407911106b97f1307494bfa68e791ef2273c7')
b2sums=('79d9efae82bfcd0631912bb64fdca8261898854ee54b371ecce5c02ab8c8e4a0a704163dddbfaa67e721429b6d8ed02832fdd384b9fa9a5c7c45a070894bbab1')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/legba
}

# vim: ts=2 sw=2 et:
