# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgname=python-sympy
pkgver=1.14.0
pkgrel=5
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='https://sympy.org/en/index.html'
license=('BSD')
depends=('python-mpmath')
optdepends=('ipython: user friendly interface for isympy'
            'python-symengine: optimized backend, set USE_SYMENGINE=1 to use')
makedepends=('git'
             'python-build'
             'python-installer'
             'python-setuptools'
             'python-wheel')
checkdepends=('python-hypothesis'
              'python-pytest')
source=(git+https://github.com/sympy/sympy#tag=sympy-$pkgver
        mpmath-1.4.patch)
sha512sums=('4c9db723b3aef259f6f7adaa02701060c53a80d560451f0b38362ebf7931cec4ff94e2b9d88915d4f831cc71e5a64dec80f668b818735f6c3aa0e6048a3e59ca'
            'fa62846da4be417522c9826becaea3891a5837091c7b2a6e0b08ce0fb6619616ec83a87de1a4cda6c0058f667c6c51fd4d0a198b1d0ff59645e487bbd8d37c24')

prepare() {
  cd sympy
  git cherry-pick -n 2bfc02884645447072ebb02afa93acd648eb1970 # Port away from deprecated mpmath functions
  patch -p1 -i ../mpmath-1.4.patch
}

build() {
  cd sympy
  python -m build --wheel --no-isolation
}

check() {
  cd sympy
  bin/test -v
}

package() {
  cd sympy

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
