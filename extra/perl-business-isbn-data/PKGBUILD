# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Anton Leontiev <bunder /at/ t-25.ru>

pkgname=perl-business-isbn-data
pkgver=20250912.001
pkgrel=1
pkgdesc='Data pack for Business::ISBN'
arch=(any)
url='https://github.com/briandfoy/business-isbn-data'
license=(Artistic-2.0)
depends=(perl perl-business-isbn-data)
makedepends=(git)
checkdepends=(perl-test-pod)
optdepends=('perl-libwww: for access to the xISBN service')
options=(purge !emptydirs)
source=("$pkgname::git+$url#tag=release-${pkgver}")
sha512sums=('bb93a7e8537c56a8cb51cc1ed6059e1fc26ec95095c8e8caea4eedf6e0eec63c025ff6f621b01037db56623316947ecb7ec52adb9b4845b47556a2fd28305e43')
b2sums=('6a4078ba014ccf655f452aa56469952df4b3e9c37bd57aa8b0bec2e2dcb88bfc4162aded91b7de8338e9a1323e8846d8c498815cc8d3547d5d04d8c27f6138ea')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
