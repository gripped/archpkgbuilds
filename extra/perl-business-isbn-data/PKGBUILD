# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Anton Leontiev <bunder /at/ t-25.ru>

pkgname=perl-business-isbn-data
pkgver=20250829.001
pkgrel=1
pkgdesc='Data pack for Business::ISBN'
arch=(any)
url='https://github.com/briandfoy/business-isbn-data'
license=(Artistic-2.0)
depends=(perl perl-business-isbn-data)
makedepends=(git)
checkdepends=(perl-test-pod)
optdepends=('perl-libwww: for access to the xISBN service')
options=(purge !emptydirs)
source=("$pkgname::git+$url#tag=release-${pkgver}")
sha512sums=('3f86d4c52acbfa93495be5b7afd95415d66fcbf1e6725eff41ef40dc926104be322a3e4a0054d4a6246da5330280c3266b2d43fba599c329585df209c1e7a0a0')
b2sums=('f0d2365a17519d0763cd7ec5e5255ad2b4c8ce5cade47cfaa1281271ca7d8524713d3329153a3f23d540628514f04a2081fe695481354b32e53f68d3eb313aaa')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
