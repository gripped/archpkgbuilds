# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Anton Leontiev <bunder /at/ t-25.ru>

pkgname=perl-business-isbn-data
pkgver=20250904.001
pkgrel=1
pkgdesc='Data pack for Business::ISBN'
arch=(any)
url='https://github.com/briandfoy/business-isbn-data'
license=(Artistic-2.0)
depends=(perl perl-business-isbn-data)
makedepends=(git)
checkdepends=(perl-test-pod)
optdepends=('perl-libwww: for access to the xISBN service')
options=(purge !emptydirs)
source=("$pkgname::git+$url#tag=release-${pkgver}")
sha512sums=('731f7f3359e558164d1f0adf45ee196d6666020427179f16e3669e131ca4553e6e8550ee1828ece12649addf174d3093ba3fe0eec1c1f3e9a93fb8cfbbcdfc58')
b2sums=('ff240cc364e46a9bc4bde84bad5ac708da4976cc31ee66fc88bec007c08e43c81a519152180aba1cd54f1a2a19d90d9a2507202bcaea9c0d3d3768cfa25b4ff2')

build() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$pkgname"

  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
