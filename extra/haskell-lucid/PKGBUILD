# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lucid
pkgname=haskell-lucid
pkgver=2.11.20250303
pkgrel=16
pkgdesc="Clear to write, read and edit DSL for HTML"
url="https://github.com/chrisdone/lucid"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-hashable' 'haskell-mmorph')
makedepends=('ghc' 'haskell-bifunctors' 'haskell-hspec' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('b737bdbce45cf92848b711ecc0d91e4e9cb2065c7f0d9b4013c1cbdf599355d25429bb7f4982ab079c2563b388c9504c9dea4ce57a1e0cd14861d7f58df0b6e9')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # Test result depending on version of hashable
    runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
