# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=typography
pkgver=0.3.0
pkgrel=1
pkgdesc='Tool for working with the GNOME typography design guidelines'
arch=(x86_64)
url='https://gitlab.gnome.org/World/design/typography'
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
)
source=("git+https://gitlab.gnome.org/World/design/typography.git#tag=$pkgver")
b2sums=(24880058532e691ed9d50935abeb9884cd50562f4ff74357aa798301253cef237fc82173112857246a66973a8fa70b6f95b5d1c5c67b3213e137a5a5b233f192)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s org.gnome.design.Typography "$pkgdir/usr/bin/typography"
}
