# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Beej Jorgensen <beej@beej.us>

pkgname=wavemon
pkgver=0.9.6
pkgrel=1
pkgdesc='Ncurses-based monitoring application for wireless network devices'
url='https://github.com/uoaerg/wavemon'
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  glibc
  libcap
  libcap.so
  libncursesw.so
  libnl
  ncurses
)
source=(https://github.com/uoaerg/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1a96c8283678daf5c09cc60b5e264047c8f1075b1f43d95376550ccb016b75b2abaf00cd9b67873833d5834e0f86fa59e23186eaf9cda6040c66ebb913f8a729')
b2sums=('08eeeca4638de0a2964b1dc68a9e530da7e6f1c875b3c29ace77b95dba0511ae3c1f1c2c00ad4403b9ad10e805d386b4acc0236247c2bb4af3ca7970ca7f8437')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e '/^CFLAGS=/d' -i configure.ac
  sed -r 's|(/share)|\1/doc|g' -i Makefile.in
  sed -r 's|\?=|=|g' -i Makefile.in
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
