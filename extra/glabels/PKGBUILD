# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=glabels
pkgver=3.4.1
pkgrel=11
pkgdesc='Creating labels and business cards the very easy way'
arch=(x86_64)
url='https://github.com/jimevins/glabels'
license=('GPL-3.0-or-later AND LGPL-3.0-or-later AND CC-BY-SA-3.0 AND MIT')
depends=(
  cairo
  dconf
  evolution-data-server
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  iec16022
  librsvg
  libxml2
  pango
  qrencode
)
makedepends=(
  git
  glib2-devel
  gtk-doc
  intltool
  yelp-tools
)
source=(
  "git+https://github.com/jimevins/glabels.git?signed#tag=$pkgname-${pkgver//./_}"
  glabels-externs.patch
  glabels-incompatible-pointer.patch
  glabels-desktop-metainfo.patch
)
b2sums=(
  23ee742a0654e872fee5a1d3a591e9654b2aa3fd6cf90aa5cea40dccef25d2af6be19bb3d7f42964b6d88391235db9415556f1b5f5135d152a19eafcc1cc7a31
  3df1dbfedaf8773626c11c44676268a33696588dc91a87f7607a9e7ede7ed1883adc5a34ca0632d513cdd99782f58b0225dde1c5ca65b374d0c41e20eaacc469
  ae3b0400a13a49fd2d43b4fd91589eafefed5896d43f39fae4eee0a0eab749e4eee37833aee77d2acc1b470639e755c59c97e8dbe8beb686a30693cb7f6e4111
  380107802dc3a1cdfa2ed5a913ab6f6f1ec5044b1322e0babf195ff89df561c0ee2813cdbbecebb728f5ed1ba37f6870ad561b2847855ab9c346b18a9d3c97e7
)
validpgpkeys=(17B6E6AA6DCE0EA8DE4864502C2EF9A1272BB12F) # Jim Evins <evins@snaught.com>

prepare() {
  cd $pkgname

  # Fix build
  git apply -3 ../glabels-externs.patch
  git apply -3 ../glabels-incompatible-pointer.patch

  # Rename desktop file to match with program name and fix metainfo
  git apply -3 ../glabels-desktop-metainfo.patch

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING*
}
