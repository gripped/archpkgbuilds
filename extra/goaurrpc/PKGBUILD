# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=goaurrpc
pkgver=1.2.4
pkgrel=1
license=('MIT')
pkgdesc="An implementation of the aurweb /rpc service in go"
depends=('glibc')
makedepends=('go' 'git')
arch=('x86_64')
url="https://gitlab.archlinux.org/archlinux/goaurrpc"
source=("git+https://gitlab.archlinux.org/archlinux/goaurrpc.git#tag=v${pkgver}?signed"
        goaurrpc.service
        goaurrpc.sysusers)
sha256sums=('e3ff617c8b2be3cf8de6e55c1921d4411f6b302874ed41eaf8b55066575f833a'
            'e6a298e647fcf3b255d9886180b992f0dc447e763509c50513473f6d9b9ac5e3'
            '85b94030647aefedf69b02764729b11fc8287301c8682599c063397e214eebc0')
validpgpkeys=(
  D5AD89388A7C6C9C22E790994A4760AB4EE15296  # Mario Oenning <moson-mo@users.noreply.github.com>, retrieved from https://github.com/moson-mo.gpg
  F00B96D15228013FFC9C9D0393B11DAA4C197E3D  # Christian Heusel <gromit@archlinux.org>
)

prepare() {
  cd ${pkgname}
  mkdir -p dist/
}

build() {
  cd ${pkgname}
  local _BUILDINFO="-X 'main.version=v${pkgver}'"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o dist/goaurrpc \
    .
}

check() {
  cd ${pkgname}
  go test ./...
}

package() {
  cd ${pkgname}
  install -vDm 755 dist/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 LICENSE  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 "${srcdir}"/${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -vDm 644 "${srcdir}"/${pkgname}.sysusers "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 fromfile.conf "${pkgdir}/usr/share/doc/${pkgname}/fromfile.conf.sample"
  install -vDm 644 sample.conf "${pkgdir}/usr/share/doc/${pkgname}/sample.conf.sample"
}

# vim:set ts=2 sw=2 et:
