# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pynormaliz
pkgname=(python-pynormaliz)
pkgver=2.23
pkgrel=2
pkgdesc='A Python interface to Normaliz'
arch=(x86_64)
url='https://github.com/Normaliz/PyNormaliz'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         normaliz
         python)
makedepends=(boost
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/Normaliz/PyNormaliz#tag=v$pkgver)
sha256sums=('ac4cdc0d7e552e426175d041ffd08472ab6592245cbcbe9a8f773d2ec4cbf273')

build() {
  cd PyNormaliz
  python -m build --wheel --no-isolation
}

package_python-pynormaliz() {
  cd PyNormaliz
  
  python -m installer --destdir="$pkgdir" dist/*.whl
}
