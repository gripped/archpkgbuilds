# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: hexchain <i at hexchain dot org>

pkgname=yaml-language-server
pkgver=1.17.0
pkgrel=2
pkgdesc='YAML Language Server'
url=https://github.com/redhat-developer/yaml-language-server
license=(MIT)
arch=(any)
depends=(nodejs)
makedepends=(
  git
  jq
  yarn
)
options=(!emptydirs)
source=("git+$url.git#tag=$pkgver")
b2sums=('8a4465006c9a130fcd1bdaaec7e82715b19679eef3d52efb63d89db6f41e4ae45150ed6908885d51265215ff348c300c0a2715aeeaf3263974fb0dabe077d8a3')

prepare() {
  cd $pkgname
  # https://github.com/redhat-developer/yaml-language-server/issues/1018
  git cherry-pick --no-commit f6bccf52b204579e2a47647a40868a15644f7d1d
}

build() {
  cd $pkgname
  yarn --frozen-lockfile
  yarn compile
}

check() {
  cd $pkgname
  yarn test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/bin/$pkgname "$pkgdir"/usr/bin/$pkgname

  cd $pkgname

  # Emulate `npm prune --omit=dev`
  cp package.json{,.bak}
  read -ra devDependencies < <(jq -r '.devDependencies | keys | join(" ")' package.json)
  yarn remove --frozen-lockfile "${devDependencies[@]}"
  mv package.json{.bak,}

  cp -r bin node_modules out package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname {CHANGELOG,README}.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
