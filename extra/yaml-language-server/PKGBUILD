# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: hexchain <i at hexchain dot org>

pkgname=yaml-language-server
_commit=b707580afd61837d19fb8f896a9ea4a3c596ad34
pkgver=1.19.0
pkgrel=1
pkgdesc='YAML Language Server'
url=https://github.com/redhat-developer/yaml-language-server
license=(MIT)
arch=(any)
depends=(nodejs)
makedepends=(
  git
  jq
  yarn
)
options=(!emptydirs)
source=("git+$url.git#commit=$_commit")
b2sums=('027bf73003ffbbf4df2455ba6140290fa54a4d4a40d56d8b74592a88dce7f15aaff611aea183b4b66c861cac972950a41f2f07f35e0f8b51063b1faac4a4b985')

prepare() {
  cd $pkgname
  yarn --frozen-lockfile
}

build() {
  cd $pkgname
  yarn compile
}

check() {
  cd $pkgname
  yarn test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/bin/$pkgname "$pkgdir"/usr/bin/$pkgname

  cd $pkgname

  # Emulate `npm prune --omit=dev`
  cp package.json{,.bak}
  read -ra devDependencies < <(jq -r '.devDependencies | keys | join(" ")' package.json)
  yarn remove --frozen-lockfile "${devDependencies[@]}"
  mv package.json{.bak,}

  cp -r bin node_modules out package.json "$pkgdir"/$mod_dir
  cp -r l10n "$pkgdir"/$mod_dir/out/server
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname {CHANGELOG,README}.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
