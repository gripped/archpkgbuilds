# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: hexchain <i at hexchain dot org>

pkgname=yaml-language-server
pkgver=1.21.0
pkgrel=1
pkgdesc='YAML Language Server'
url=https://github.com/redhat-developer/yaml-language-server
license=(MIT)
arch=(any)
depends=(nodejs)
makedepends=(
  git
  npm
)
options=(!emptydirs)
source=("git+$url.git#tag=$pkgver")
b2sums=('0df930a52119018123bf082c948abef8c5f6d0e0d59001f1b9a0c3855d84073dc9c1cbaae1faa40a0ab805a4d016e36254085ed53f04f5ae06a4aac51d18b3ff')

prepare() {
  cd $pkgname
  npm ci
}

build() {
  cd $pkgname
  npm run compile
}

check() {
  cd $pkgname
  npm test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/bin/$pkgname "$pkgdir"/usr/bin/$pkgname

  cd $pkgname
  npm prune --omit=dev

  cp -r bin l10n node_modules out package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname {CHANGELOG,README}.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
