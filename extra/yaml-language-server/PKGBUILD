# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: hexchain <i at hexchain dot org>

pkgname=yaml-language-server
pkgver=1.19.2
pkgrel=1
pkgdesc='YAML Language Server'
url=https://github.com/redhat-developer/yaml-language-server
license=(MIT)
arch=(any)
depends=(nodejs)
makedepends=(
  git
  npm
)
options=(!emptydirs)
source=("git+$url.git#tag=$pkgver")
b2sums=('aae78e495fc9945125dc2a14d9bef8b3a2a2f44647eba6b1453c1798455f1591112f9e95c97fbe32e722e63444afe85a49b3f5fe42d4ccb06097a9e47eeed887')

prepare() {
  cd $pkgname
  npm ci
}

build() {
  cd $pkgname
  npm run compile
}

check() {
  cd $pkgname
  npm test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/bin/$pkgname "$pkgdir"/usr/bin/$pkgname

  cd $pkgname
  npm prune --omit=dev

  cp -r bin l10n node_modules out package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname {CHANGELOG,README}.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
