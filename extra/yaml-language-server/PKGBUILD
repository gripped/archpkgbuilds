# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: hexchain <i at hexchain dot org>

pkgname=yaml-language-server
pkgver=1.18.0
pkgrel=1
pkgdesc='YAML Language Server'
url=https://github.com/redhat-developer/yaml-language-server
license=(MIT)
arch=(any)
depends=(nodejs)
makedepends=(
  git
  jq
  yarn
)
options=(!emptydirs)
source=("git+$url.git#tag=$pkgver")
b2sums=('025b2b08dba61668b49e8483c7d2aca53d72fa05a512b4c32a1b6c4da871528bd9122fcf864e72bd2705c745508b02895bdb317899072b56996fd87b4aa7441f')

build() {
  cd $pkgname
  yarn --frozen-lockfile
  yarn compile
}

check() {
  cd $pkgname
  yarn test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/bin/$pkgname "$pkgdir"/usr/bin/$pkgname

  cd $pkgname

  # Emulate `npm prune --omit=dev`
  cp package.json{,.bak}
  read -ra devDependencies < <(jq -r '.devDependencies | keys | join(" ")' package.json)
  yarn remove --frozen-lockfile "${devDependencies[@]}"
  mv package.json{.bak,}

  cp -r bin node_modules out package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname {CHANGELOG,README}.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
