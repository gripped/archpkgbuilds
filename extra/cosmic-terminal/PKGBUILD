# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Kyuzial <kyuzial@protonmail.com>

pkgname=cosmic-terminal
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='Cosmic Terminal Emulator'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-term
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
  cosmic-icon-theme
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=2c22dc8afa981e90964b51b4fb855ce493b71491
source=(
  git+https://github.com/pop-os/cosmic-term.git#tag=${_tag}
  cosmic-terminal-lto.patch
)
b2sums=('62fe1565ecf9ccdcd695d9d740d8be7def3dfbd4088585ac7d541bddea91b1566db18cf95db7d0fccd8580e379401874b9a5d98a7ec55584fa467608df54071d'
        '4e28ab726f474f064a99dbb4dc75faa7ddfe00d7f5e238f57ba80ac75e44a2f6ed00ae2f3499fac94d40ec4dd773fc9eeb93c4543f8e63699ba437ba45f8783b')

prepare() {
  cd cosmic-term
  patch -Np1 -i ../cosmic-terminal-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-term
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-term
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-term
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
