# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kyuzial <kyuzial@protonmail.com>

pkgname=cosmic-terminal
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='Cosmic Terminal Emulator'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-term
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
  cosmic-icon-theme
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=2e2a2376dfe0c6959e4e7d115f7b7d19405737ad
source=(
  git+https://github.com/pop-os/cosmic-term.git#tag=${_tag}
  cosmic-terminal-lto.patch
)
b2sums=('5dd518b9824428279b0645dd4b535581dacba42f76459e087f89fa2193c36c71b850864419dde7cea11cd63149bafceff09b6319594c92d8ce08f9cc0f0207dd'
        '4e28ab726f474f064a99dbb4dc75faa7ddfe00d7f5e238f57ba80ac75e44a2f6ed00ae2f3499fac94d40ec4dd773fc9eeb93c4543f8e63699ba437ba45f8783b')

prepare() {
  cd cosmic-term
  patch -Np1 -i ../cosmic-terminal-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-term
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-term
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-term
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
