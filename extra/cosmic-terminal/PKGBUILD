# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Kyuzial <kyuzial@protonmail.com>

pkgname=cosmic-terminal
pkgver=1.0.0.beta.1
pkgrel=1
pkgdesc='Cosmic Terminal Emulator'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-term
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
  cosmic-icon-theme
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=42f566007fbb246338d1b45927f368c09abb5ae4
source=(
  git+https://github.com/pop-os/cosmic-term.git#tag=${_tag}
  cosmic-terminal-lto.patch
)
b2sums=('bebf84f9ef9f26e37b96f09741cd51b8b5512c1d0a783ea558650d944d0602e58facbcd80a3361bde5075f7cddd6b468592fa99933e69c6923b02e7a91e9569a'
        '4e28ab726f474f064a99dbb4dc75faa7ddfe00d7f5e238f57ba80ac75e44a2f6ed00ae2f3499fac94d40ec4dd773fc9eeb93c4543f8e63699ba437ba45f8783b')

prepare() {
  cd cosmic-term
  patch -Np1 -i ../cosmic-terminal-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-term
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-term
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-term
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
