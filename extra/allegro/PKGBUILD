# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=5.2.10.0
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=(x86_64)
url='https://liballeg.org/'
license=(custom)
makedepends=(cmake glu libtheora mesa-libgl ninja opusfile pandoc xorgproto)
depends=(dumb gtk3 jack libgl libpulse libtheora libwebp libxpm opusfile physfs)
source=("https://github.com/liballeg/allegro5/releases/download/$pkgver/allegro-$pkgver.tar.gz")
b2sums=('4a4d68b02d14dd129cf67f9fd363105f4fd6e2fe88cfa9740adce6e7c3013addd3e301e4dc714e8c2f970b8193f7e62d60c4df1057536447bf807f065e6a32bf')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D WANT_DOCS_HTML=OFF \
    -G Ninja
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "$pkgname-$pkgver/LICENSE.txt" \
    -t "$pkgdir/usr/share/licenses/$pkgname"
  # Fix man path
  mv -v -f "$pkgdir/usr/man" "$pkgdir/usr/share/man"
}
