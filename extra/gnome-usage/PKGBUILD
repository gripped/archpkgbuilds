# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-usage
pkgver=45.1
pkgrel=1
pkgdesc="View information about use of system resources"
url="https://wiki.gnome.org/Apps/Usage"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  glib2
  gtk4
  libadwaita
  libgee
  libgtop
  tracker3
  xdg-user-dirs
)
makedepends=(
  accountsservice
  gamemode
  git
  meson
  vala
)
optdepends=(
  'accountsservice: show user tags for processes'
  'gamemode: show processes requesting game mode'
)
_commit=bda1849a90d5b897a6cbb6d72ee51cec894dd47b  # tags/45.1+1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-usage.git#commit=$_commit")
b2sums=('c1637bd19a0e7021bd4dd08ae07b110b27053ef9820f09ca9ebc0ace902ee1487781cccedbc96d14919f1a1e9652b95d9f0d3a9c86141cedaaf4bc000d4e763d')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/+1//;s/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
