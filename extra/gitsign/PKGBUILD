# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contriubutor: Jamie Magee <jamie dot magee at gmail dot com>

pkgname=gitsign
pkgver=0.14.0
pkgrel=2
pkgdesc='Keyless Git signing using Sigstore'
arch=('x86_64')
url='https://github.com/sigstore/gitsign'
license=("APACHE")
makedepends=(go)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('235d46799b7fc2895c277bca420a361acf93b0c106c7deecc7b3b9a0ec6dbf7881e89e23e1c1fd5e18c6d5ce94c8d1a453ba99fb8ddfed428da5df3322c97ad8')

build() {
  cd $pkgname-$pkgver
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make build-all
}

package() {
  cd $pkgname-$pkgver

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
