# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=hurl
pkgver=6.1.1
pkgrel=2
pkgdesc="Run and test HTTP requests with plain text"
url='https://github.com/Orange-OpenSource/hurl'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'curl'
  'gcc-libs'
  'glibc'
  'libxml2'
)
makedepends=(
  'cargo'
)
options=('!lto')
source=(
  "https://github.com/Orange-OpenSource/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
  '0001-Replace-deprecated-libxml2-initGenericErrorDefaultFu.patch'
)
sha256sums=('26ef1ed685f4b94190914a0e03127f7b7f6a488abf65758c19092dc6b9034b2c'
            '7833e73a99bf14c6ef8ca567f97c352aa5315ccf8bc6942bc06d6b1618420f28')
b2sums=('1cc8a10256f2ba1bd0710a1c5865cee53713448204476b73628de57a6c748ecd638cbf286cbd45e8d0b3b76353213a010eb61493f2b9fa0299739fbfaf6fe4a7'
        'f2b3f047fbbc75ab62d1cd462c4bb62058ffa1edc3dc39848c5df86c6d5bce9776f908c5f6255cb8334f998159fbf547ea7dfea1464651ea119bb870c2ed2f1b')

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix build with libxml2 2.14
  # https://github.com/Orange-OpenSource/hurl/pull/3977
  patch -Np1 -i ../0001-Replace-deprecated-libxml2-initGenericErrorDefaultFu.patch

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip simple_sample --skip runner::hurl_file::run
  target/release/hurl --help
}

package() {
  depends+=(
    'libcurl.so'
    'libxml2.so'
  )

  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/hurl \
    target/release/hurlfmt

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/manual/hurl.1 \
    docs/manual/hurlfmt.1

  # install completions
  install -Dm 644 completions/hurl.bash "${pkgdir}/usr/share/bash-completion/completions/hurl"
  install -Dm 644 completions/hurlfmt.bash "${pkgdir}/usr/share/bash-completion/completions/hurlfmt"

  install -Dm 644 -t "${pkgdir}/usr/share/zsh/site-functions" \
    completions/_hurl \
    completions/_hurlfmt

  install -Dm 644 -t "${pkgdir}/usr/share/fish/vendor_completions.d" \
    completions/hurl.fish \
    completions/hurlfmt.fish
}

# vim: ts=2 sw=2 et:
