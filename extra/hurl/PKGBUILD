# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=hurl
pkgver=5.0.1
pkgrel=1
pkgdesc="Run and test HTTP requests with plain text"
url='https://github.com/Orange-OpenSource/hurl'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'curl' 'libcurl.so'
  'gcc-libs'
  'glibc'
  'libxml2' 'libxml2.so'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/Orange-OpenSource/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2b5a42fc95b74c876257a35d13b603e06f1f2c58e0ca44a2c0bb23d023227c29')
b2sums=('b699f2c501e13e5bcf65c33971fae5f8417f95a7262f9107b966674bf02b82841a774673b05f3117eb762b19772c40db39c4653b7c1342cf50025197f5db2d3b')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip simple_sample --skip runner::hurl_file::run
  target/release/hurl --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/hurl

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/manual/hurl.1 \
    docs/manual/hurlfmt.1

  # install completions
  install -Dm 644 completions/hurl.bash "${pkgdir}/usr/share/bash-completion/completions/hurl"
  install -Dm 644 completions/hurlfmt.bash "${pkgdir}/usr/share/bash-completion/completions/hurlfmt"

  install -Dm 644 -t "${pkgdir}/usr/share/zsh/site-functions" \
    completions/_hurl \
    completions/_hurlfmt

  install -Dm 644 -t "${pkgdir}/usr/share/fish/vendor_completions.d" \
    completions/hurl.fish \
    completions/hurlfmt.fish
}

# vim: ts=2 sw=2 et:
