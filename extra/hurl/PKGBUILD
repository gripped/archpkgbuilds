# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=hurl
pkgver=7.0.0
pkgrel=1
pkgdesc="Run and test HTTP requests with plain text"
url='https://github.com/Orange-OpenSource/hurl'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'curl'
  'gcc-libs'
  'glibc'
  'libxml2'
)
makedepends=(
  'cargo'
  'clang'
)
options=('!lto')
source=("https://github.com/Orange-OpenSource/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('3f505848aabd9eec78360928b0fcbfea237a049b5c8d25b8d9e0aeb3f9d8dfed')
b2sums=('12e6b39e3f9b69e6b129b69b6d9a90feeada0d99fa9e6078de39a4994c30929ba901ad4bc908b2785a7433544695e2357853ae96a2c45d5bdb973528ae61a5dd')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip simple_sample --skip runner::hurl_file::run
  target/release/hurl --help
}

package() {
  depends+=(
    'libcurl.so'
    'libxml2.so'
  )

  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/hurl \
    target/release/hurlfmt

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/manual/hurl.1 \
    docs/manual/hurlfmt.1

  # install completions
  install -Dm 644 completions/hurl.bash "${pkgdir}/usr/share/bash-completion/completions/hurl"
  install -Dm 644 completions/hurlfmt.bash "${pkgdir}/usr/share/bash-completion/completions/hurlfmt"

  install -Dm 644 -t "${pkgdir}/usr/share/zsh/site-functions" \
    completions/_hurl \
    completions/_hurlfmt

  install -Dm 644 -t "${pkgdir}/usr/share/fish/vendor_completions.d" \
    completions/hurl.fish \
    completions/hurlfmt.fish
}

# vim: ts=2 sw=2 et:
