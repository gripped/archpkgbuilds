# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=hurl
pkgver=4.2.0
pkgrel=1
pkgdesc="Run and test HTTP requests with plain text"
url='https://github.com/Orange-OpenSource/hurl'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'curl' 'libcurl.so'
  'gcc-libs'
  'glibc'
  'libxml2' 'libxml2.so'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/Orange-OpenSource/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8ede2b3e9e1e1fb80000362750814b0fd07911506c1ea13e38e6c2fe80f447f0')
b2sums=('79b2ff72561f7e177be0b4de056d31026e55227e5325452559ca6003f983ce4e34cbff8b9a53fa1db581589744a4e237e98d4dea507888400a99027966f0c5b4')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip simple_sample --skip runner::hurl_file::run
  target/release/hurl --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/hurl

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/manual/hurl.1 \
    docs/manual/hurlfmt.1

  # install completions (available in next release)
  if [ -d completions ]; then
    echo "TODO: add completions"
  fi
#  install -Dm 644 completions/hurl.bash "${pkgdir}/usr/share/bash-completion/completions/hurl"
#  install -Dm 644 completions/hurlfmt.bash "${pkgdir}/usr/share/bash-completion/completions/hurlfmt"
#
#  install -Dm 644 -t "${pkgdir}/usr/share/zsh/site-functions" \
#    _hurl \
#    _hurlfmt
#
#  install -Dm 644 -t "${pkgdir}/usr/share/fish/vendor_completions.d" \
#    hurl.fish \
#    hurlfmt.fish
}

# vim: ts=2 sw=2 et:
