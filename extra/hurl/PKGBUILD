# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=hurl
pkgver=7.1.0
pkgrel=1
pkgdesc="Run and test HTTP requests with plain text"
url='https://github.com/Orange-OpenSource/hurl'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'curl' 'libcurl.so'
  'gcc-libs'
  'glibc'
  'libxml2' 'libxml2.so'
)
makedepends=(
  'cargo'
  'clang'
)
options=('!lto')
source=("https://github.com/Orange-OpenSource/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('1bbe1e9f2736209bc1c0ce3082d3debac08b1aec7c6203e0b6698669c8abc3f2')
b2sums=('5a2a4b4156341bfb301ca265bb92cd12902deb253e711e4fa809ce1a5aa097d34163120fe1248842d40dafdb5bd4675675ed507177f77926da645783de83b2e5')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip simple_sample --skip runner::hurl_file::run
  target/release/hurl --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/hurl \
    target/release/hurlfmt

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/manual/hurl.1 \
    docs/manual/hurlfmt.1

  # install completions
  install -Dm 644 completions/hurl.bash "${pkgdir}/usr/share/bash-completion/completions/hurl"
  install -Dm 644 completions/hurlfmt.bash "${pkgdir}/usr/share/bash-completion/completions/hurlfmt"

  install -Dm 644 -t "${pkgdir}/usr/share/zsh/site-functions" \
    completions/_hurl \
    completions/_hurlfmt

  install -Dm 644 -t "${pkgdir}/usr/share/fish/vendor_completions.d" \
    completions/hurl.fish \
    completions/hurlfmt.fish
}

# vim: ts=2 sw=2 et:
