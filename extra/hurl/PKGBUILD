# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=hurl
pkgver=6.1.0
pkgrel=1
pkgdesc="Run and test HTTP requests with plain text"
url='https://github.com/Orange-OpenSource/hurl'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'curl' 'libcurl.so'
  'gcc-libs'
  'glibc'
  'libxml2' 'libxml2.so'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/Orange-OpenSource/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7ac2cca05a3a22cd92bd10b46c39a4277fd64d10ab989e46a2e8aca112d842f0')
b2sums=('bbbc69a6842234bdef78127d773018f0fd3c7602300038889172a9c4215a15af382960ae2d03e46dcfa3acf8fc002c47040d3f0280c29da40f49dca7f1bc6a76')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip simple_sample --skip runner::hurl_file::run
  target/release/hurl --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/hurl

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/manual/hurl.1 \
    docs/manual/hurlfmt.1

  # install completions
  install -Dm 644 completions/hurl.bash "${pkgdir}/usr/share/bash-completion/completions/hurl"
  install -Dm 644 completions/hurlfmt.bash "${pkgdir}/usr/share/bash-completion/completions/hurlfmt"

  install -Dm 644 -t "${pkgdir}/usr/share/zsh/site-functions" \
    completions/_hurl \
    completions/_hurlfmt

  install -Dm 644 -t "${pkgdir}/usr/share/fish/vendor_completions.d" \
    completions/hurl.fish \
    completions/hurlfmt.fish
}

# vim: ts=2 sw=2 et:
