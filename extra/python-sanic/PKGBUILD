# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Qirui Wang <wqr.prg@gmail.com>
# Contributor: Carl George < arch at cgtx dot us >

pkgname=python-sanic
pkgver=24.6.0
pkgrel=1
pkgdesc='Async Python 3.6+ web server/framework | Build fast. Run fast.'
arch=("any")
url='https://sanic.dev/'
license=("MIT")
depends=('python-sanic-routing' 'python-httptools' 'python-uvloop' 'python-ujson' 'python-aiofiles'
         'python-websockets' 'python-multidict')
makedepends=('python-setuptools')
source=("https://github.com/sanic-org/sanic/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('7d92b5a8169308d544fb2e19cdd9cee395c2fefa68a054e8025e070d44519065')

build() {
    cd sanic-$pkgver
    python setup.py build
}

# TODO: tests

package() {
    cd sanic-$pkgver
    python setup.py install --skip-build --root="$pkgdir" --optimize=1
    install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
