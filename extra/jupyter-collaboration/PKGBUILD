# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-collaboration
pkgver=4.1.0
pkgrel=1
pkgdesc='A Jupyter Server Extension Providing Support for Y Documents'
arch=(any)
url='https://jupyterlab-realtime-collaboration.readthedocs.io/en/latest/'
license=(custom)
depends=(jupyter-server
         jupyter-server-fileid
         python
         python-jupyter-events
         python-jupyter-ydoc
         python-pycrdt
         python-pycrdt-websocket
         python-tornado
         python-traitlets)
makedepends=(git
             jupyterlab
             nodejs
             python-build
             python-hatch-jupyter-builder
             python-hatch-nodejs-version
             python-hatchling
             python-installer)
checkdepends=(python-dirty-equals
              python-httpx-ws
              python-jupyter-server-terminals
              python-pytest
              python-pytest-asyncio
              python-pytest-jupyter
              python-pytest-timeout
              python-websockets)
conflicts=(jupyter-server-ydoc)
replaces=(jupyter-server-ydoc)
source=(git+https://github.com/jupyterlab/jupyter-collaboration#tag=v$pkgver
        pycrdt-websocket-0.16.patch)
sha256sums=('c78b473f7ed0e796388d58c62cd400bfb2bfb513348ed119039e9adec8bb95c7'
            'b40832a2e0c8aabe6c3489fbf01b85bfbf9f697762694e5fe1f9985d89e2c241')

prepare() {
  cd $pkgname
  patch -p1 -i ../pycrdt-websocket-0.16.patch # Support pycrdt-websocket 0.16
}

build() {
  for _proj in jupyter-server-ydoc jupyter-docprovider jupyter-collaboration-ui; do
    cd "$srcdir"/$pkgname/projects/$_proj
    python -m build --wheel --no-isolation --skip-dependency-check
  done
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  for _wheel in projects/*/dist/*.whl; do
    test-env/bin/python -m installer $_wheel
  done
  test-env/bin/python -m pytest -v
}

package() {
  cd $pkgname
  for _wheel in projects/*/dist/*.whl; do
    python -m installer --destdir="$pkgdir" $_wheel
  done
  mv "$pkgdir"/{usr/,}etc
  install -Dm644 "$srcdir"/$pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
