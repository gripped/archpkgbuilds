# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-collaboration
pkgver=4.2.0
pkgrel=2
pkgdesc='A Jupyter Server Extension Providing Support for Y Documents'
arch=(any)
url='https://jupyterlab-realtime-collaboration.readthedocs.io/en/latest/'
license=(BSD-3-Clause)
depends=(jupyter-server
         jupyter-server-fileid
         python
         python-jupyter-events
         python-jupyter-ydoc
         python-pycrdt
         python-pycrdt-websocket
         python-tornado
         python-traitlets)
makedepends=(git
             jupyterlab
             nodejs
             python-build
             python-hatch-jupyter-builder
             python-hatch-nodejs-version
             python-hatchling
             python-installer)
checkdepends=(python-dirty-equals
              python-httpx-ws
              python-jupyter-server-terminals
              python-pytest
              python-pytest-asyncio
              python-pytest-jupyter
              python-pytest-timeout
              python-websockets)
conflicts=(jupyter-server-ydoc)
replaces=(jupyter-server-ydoc)
source=(git+https://github.com/jupyterlab/jupyter-collaboration#tag=v$pkgver)
sha256sums=('61614d639cf9b7d641839006311eae47a50258c206b50c8d3c260a404e565fae')

prepare() {
  cd $pkgname
  git cherry-pick -n 50bfd3678b36d481526afb972eec50a6db0e252d # Fix tests with new pycrdt-store
}

build() {
  for _proj in jupyter-server-ydoc jupyter-docprovider jupyter-collaboration-ui; do
    cd "$srcdir"/$pkgname/projects/$_proj
    python -m build --wheel --no-isolation --skip-dependency-check
  done
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  for _wheel in projects/*/dist/*.whl; do
    test-env/bin/python -m installer $_wheel
  done
  test-env/bin/python -m pytest -v
}

package() {
  cd $pkgname
  for _wheel in projects/*/dist/*.whl; do
    python -m installer --destdir="$pkgdir" $_wheel
  done
  mv "$pkgdir"/{usr/,}etc
  install -Dm644 "$srcdir"/$pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
