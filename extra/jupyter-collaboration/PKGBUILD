# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=jupyter_collaboration
pkgname=${_pyname/_/-}
pkgver=2.0.11
pkgrel=2
pkgdesc='A Jupyter Server Extension Providing Support for Y Documents'
arch=(any)
url='https://jupyterlab-realtime-collaboration.readthedocs.io/en/latest/'
license=(custom)
depends=(jupyter-server
         jupyter-server-fileid
         python
         python-jupyter-events
         python-jupyter-ydoc
         python-pycrdt-websocket
         python-tornado
         python-traitlets)
makedepends=(python-build
             python-hatch-jupyter-builder
             python-hatch-nodejs-version
             python-hatchling
             python-installer)
checkdepends=(python-jupyter-server-terminals
              python-pytest
              python-pytest-asyncio
              python-pytest-jupyter
              python-pytest-timeout
              python-websockets)
source=(https://pypi.io/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('2bb1cde72cda758e526fa49f9ff2bf402ceed7801c0c4187abe4d21e42e18136')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pyname-$pkgver
  pytest -v -k 'not test_room_sequential_opening' # https://github.com/jupyterlab/jupyter-collaboration/issues/252
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"{/usr,}/etc

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
