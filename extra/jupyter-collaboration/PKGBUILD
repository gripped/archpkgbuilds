# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=jupyter_collaboration
pkgname=${_pyname/_/-}
pkgver=1.0.0
pkgrel=2
pkgdesc='A Jupyter Server Extension Providing Support for Y Documents'
arch=(any)
url='https://jupyterlab-realtime-collaboration.readthedocs.io/en/latest/'
license=(custom)
depends=(jupyter-server python-ypy-websocket jupyter-server-ydoc)
makedepends=(python-build python-installer python-hatchling python-hatch-nodejs-version python-hatch-jupyter-builder)
checkdepends=(python-pytest-jupyter python-pytest-timeout python-pytest-asyncio)
source=(https://pypi.io/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz
        03f1f006.patch)
sha256sums=('7a79651d17898564fe13ad4c31bde50059e7087346339045159126e64dd565bd'
            '1a0ab0c3dd24d02de3f84ec2f87bc69405495f6a8247119d8a9c46b9ec7c59b6')

prepare() {
  cd $_pyname-$pkgver
  patch -p1 -i ../03f1f006.patch # Support ypy-websocket 0.12
}

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pyname-$pkgver
  pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"{/usr,}/etc

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
