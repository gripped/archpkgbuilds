# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.16.0
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='https://profanity-im.github.io'
arch=('x86_64')
license=('GPL3')
_clidepends=(
  'libcurl.so' 'libncursesw.so' 'expat' 'glib2' 'libotr'
  'libnotify.so' 'libgpgme.so' 'python' 'libgcrypt' 'libsignal-protocol-c'
  'glibc' 'libreadline.so' 'libassuan' 'libgpg-error' 'libstrophe.so'
  )
_gtkdepends=(
  'libgtk-3.so' 'libxss' 'libx11' 'gdk-pixbuf2' 'cairo'
  )
makedepends=(
  ${_clidepends[@]} ${_gtkdepends[@]} 'autoconf-archive'
  )
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('3e9c9f2f6c13ed000eae4705f87c1fef4ec78a82c355addca6dde358b37659e4')
sha512sums=('de0fa6bb5627cff57578a8441716dcafecd56fdc4d87105f955be2702da578b15581617813a504da6801faf9ba5689fcd55570135b0cc5109634c7333b888e24')
b2sums=('7ec3457c2ebc08215dae9abf896b28b9503abd4a5fd0247ff82eb151970d20d9f316da6fc816c0806f92977cbd942e368346aacfe7198ac2054a17dadb0b341a')

prepare() {
  cd ${pkgname}-${pkgver}

  mkdir -p m4
  autoreconf -fiv
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons-and-clipboard \
      --without-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
  )
  echo "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons-and-clipboard \
      --with-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
  )
}

package_profanity() {
  depends=(
    ${_clidepends[@]}
  )
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc+=' (gtk support)'
  depends=(
    ${_clidepends[@]} ${_gtkdepends[@]}
  )
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
