# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hindent
pkgver=6.1.1
pkgrel=1
pkgdesc="Extensible Haskell pretty printer"
url="https://github.com/mihaimaruseac/hindent"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc-lib-parser-ex' 'haskell-monad-loops'
         'haskell-optparse-applicative' 'haskell-path' 'haskell-path-io' 'haskell-regex-tdfa'
         'haskell-split' 'haskell-syb' 'haskell-unicode-show' 'haskell-utf8-string' 'haskell-yaml')
makedepends=('ghc' 'uusi' 'haskell-diff' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ec7db62d5da636ef075fdd168ff9eb886759fee42da1470e67f2e712304df60e7de1183b0dfd7678512fe2f9ddd542ad4e232a85e7114a64dfa2cba2af8fda5d')

prepare() {
    cd $pkgname-$pkgver
    uusi -r ghc-lib-parser:ghc,ghc-boot
    sed -e 's/"ghc-lib-parser" //' -e 's/ghc_lib_parser/ghc/' -i \
        src/HIndent/Pretty.hs \
        src/HIndent/Pretty/Combinators/Outputable.hs \
        src/HIndent/Pretty/Import/Sort.hs \
        src/HIndent/Pretty/NodeComments.hs \
        src/HIndent/Pretty/Pragma.hs \
        src/HIndent/Pretty/Types.hs \
        src/HIndent/Parse.hs \
        src/HIndent/ModulePreprocessing.hs \
        src/HIndent/ModulePreprocessing/CommentRelocation.hs \
        src/HIndent/GhcLibParserWrapper/GHC/Hs.hs

}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
