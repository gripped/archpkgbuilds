# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hindent
pkgver=6.0.0
pkgrel=7
pkgdesc="Extensible Haskell pretty printer"
url="https://github.com/mihaimaruseac/hindent"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc-lib-parser-ex' 'haskell-monad-loops'
         'haskell-optparse-applicative' 'haskell-path' 'haskell-path-io' 'haskell-regex-tdfa'
         'haskell-split' 'haskell-syb' 'haskell-unicode-show' 'haskell-utf8-string' 'haskell-yaml')
makedepends=('ghc' 'uusi' 'haskell-diff' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f1aa15523b0329ada3590bb46596a4ec358afba0847884af50a8a8602155aa3dca86dbc88525a26eb076e4a94cb6ed8064e2b2db1f971bcbcb7d57274681ba18')

prepare() {
    cd $pkgname-$pkgver
    uusi -r ghc-lib-parser:ghc,ghc-boot
    sed -e 's/"ghc-lib-parser" //' -e 's/ghc_lib_parser/ghc/' -i \
        src/HIndent/Pretty.hs \
        src/HIndent/Pretty/NodeComments.hs \
        src/HIndent/Pretty/Types.hs \
        src/HIndent/Parse.hs \
        src/HIndent/ModulePreprocessing.hs
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
