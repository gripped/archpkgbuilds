# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-unicode-segmentation-rs
pkgver=0.2.1
pkgrel=1
pkgdesc='Unicode segmentation and width for Python using Rust'
arch=('x86_64')
url='https://github.com/WeblateOrg/unicode-segmentation-rs'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-maturin'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3e4f11f6e106a7cf847ba3920178d107062e585248fb86dd0bf9853e34ccc61b5fb2a551730a729b1d328313b908fe9f76b225cfefce3ff76421576a3d53acb3')
b2sums=('1a10cc10a0bef9b870bb5950bdbdd73ad45190b14a6dd9f926b8d70a548814046d88d0a338e733427343f93f27bd36a453ce574fe8f55e33fee13fcc9ab42ee7')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
