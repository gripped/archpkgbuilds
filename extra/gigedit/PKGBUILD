# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=gigedit
pkgver=1.2.4
pkgrel=1
pkgdesc="Gigasampler instrument editor"
arch=(x86_64)
url="https://www.linuxsampler.org/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  atkmm
  cairomm
  docbook-xsl
  gendesk
  glib2
  glibmm
  gtk3
  gtkmm3
  intltool
  libgig
  libsigc++
  libsndfile
  libxslt
  linuxsampler
  pangomm
)
provides=(libgigedit.so)
source=(https://download.linuxsampler.org/packages/$pkgname-$pkgver.tar.bz2)
sha512sums=('ace36bdb9ec12186e9b1932e66712d0760c837385664f898aaa1a7e079a13697bed725bac071a9692e2c31e8f31783fdc2c0d86615479ed702c5b543c1e898e9')
b2sums=('de9eebc9c78d87e116800a789f91dc25599cb3a00a06a5a23f5b93a8c4cab7a88af3ed7ae401c0905aa7a4b03047e89363eaa8e8198065e9706d200f7799a6bb')

prepare() {
  # generate XDG desktop file
  gendesk -n \
          --pkgname "$pkgname" \
          --name "$pkgname" \
          --genericname "Instrument Editor" \
          --categories "AudioVideo;Audio"

  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(
    atkmm libatkmm-1.6.so
    cairomm libcairomm-1.0.so
    glib2 libgobject-2.0.so libglib-2.0.so
    glibmm libglibmm-2.4.so
    gtk3 libgtk-3.so
    gtkmm3 libgtkmm-3.0.so
    libgig libgig.so
    libsigc++ libsigc-2.0.so
    libsndfile libsndfile.so
    linuxsampler liblinuxsampler.so
    pangomm libpangomm-1.4.so
  )

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  # NOTE: Due to autotools obfuscating how to properly configure the installation location of the shared libraries in /usr/lib/, we have to move files manually... :(
  #       In the past we have used patches to the Makefile.in files, but with running `autoreconf`, this no longer works.
  mv -v "$pkgdir/usr/lib/$pkgname/lib$pkgname"* "$pkgdir/usr/lib/"
  rmdir -v "$pkgdir/usr/lib/$pkgname/"

  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
