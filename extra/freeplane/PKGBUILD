# Maintainer: Leonidas Spyropoulos (artafinde@archlinux.org)
# Contributor: scrawler@gmail.com

pkgname=freeplane
pkgver=1.12.16
pkgrel=1
pkgdesc="A Java mindmapping tool"
arch=('any')
url="http://freeplane.sourceforge.net"
license=('GPL-2.0-only')
_jrever=11
_jdkver=21
makedepends=(git 'gradle' "java-environment-openjdk=${_jdkver}" 'gnu-free-fonts' 'fontconfig' 'unzip')
depends=("java-runtime>=${_jrever}" "java-runtime<=${_jdkver}")
source=("git+https://github.com/freeplane/freeplane.git#tag=release-${pkgver}"
        "freeplane.desktop" "freeplane.run")
b2sums=('28eeba6cca6377d84a4e6740348fe4cb15b651afc9fcb012ebef748f5e06073ee343fed8213e5df11aa254df9f7a6da178d9a74605a5989b22ee7a28dde138ff'
        '87c25331e01823e38668e4b394a51a279c05b24b088f4ffc1482d3783e24018da8f9e51b3ad1a62c5a863f85a6ccb30bbe8999cb861dc1b93d5483019644cfa5'
        '24ca56b7c7894b9bb38600b4d37973769243e1bdb221f33125b60bf4f878a3b630775710fab9dee97fa45a69319455037e294860ba7fbd608529982c6b0b1538')

build() {
  cd ${pkgname}
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  /usr/bin/gradle \
    -Dorg.gradle.java.home="/usr/lib/jvm/java-${_jdkver}-openjdk" \
    -Dorg.gradle.daemon=false \
    binZip
}

check() {
  cd ${pkgname}
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  /usr/bin/gradle \
    -Dorg.gradle.java.home="/usr/lib/jvm/java-${_jdkver}-openjdk" \
    -Dorg.gradle.daemon=false \
    check
}

package() {
  install -dm755 "$srcdir/${pkgname}/dist"
  unzip -d "$srcdir/${pkgname}/dist" \
    "${srcdir}/${pkgname}/DIST/${pkgname}_bin-${pkgver}.zip"

  install -dm755 "${pkgdir}/usr/share/freeplane/"
  cd "$srcdir/${pkgname}/dist/${pkgname}-${pkgver}"
  cp -dr --no-preserve='ownership' core doc plugins resources scripts framework.jar freeplane.l4j.ini \
    freeplane.png freeplane.policy freeplane.sh freeplaneConsole.l4j.ini freeplaneIcons.dll \
    freeplanelauncher.jar init.xargs license.txt props.xargs \
    "${pkgdir}/usr/share/freeplane/"

  # Install license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  mv license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  # Install icons
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  mv freeplane.svg "${pkgdir}"/usr/share/pixmaps/freeplane.svg

  # Install the desktop entry
  install -Dm644 "${srcdir}/freeplane.desktop" "${pkgdir}"/usr/share/applications/${pkgname}.desktop

  # Install the executable script
  install -Dm755 "${srcdir}/freeplane.run" "${pkgdir}"/usr/bin/freeplane
}

# vim:set ft=sh sw=2 sts=2 et:
