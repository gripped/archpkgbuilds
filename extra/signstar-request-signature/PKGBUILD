# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=signstar-request-signature
_name=signstar
pkgver=0.1.3
pkgrel=1
pkgdesc="A commandline tool for creating, reading and writing of signing requests for files"
arch=(x86_64)
url="https://signstar.archlinux.page/signstar-request-signature"
_url="https://gitlab.archlinux.org/archlinux/signstar"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  mold
  rust
  rust-script
)
source=($_name::git+$_url.git?signed#tag=$pkgname/$pkgver)
sha512sums=('e3978e2f8ea883ed25f36002ee84d984c9ab72e633a41050e794fc303f804d396942d6f7b1eff17c270b7cbe52eff47123ee4023abe62caa73e6de188d9a738e')
b2sums=('889781cd27aa2ea7efc263978dc424d91b870628e9b5cbfc1a3b6692860cd98615f2fd2ad2b2138cba13e043cf46820c131a657d216d853ae793cec073dc1f17')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release --package $pkgname
  just generate shell_completions $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --package $pkgname
}

package() {
  install -vDm 755 $_name/target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $_name/$pkgname/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $_name/$pkgname/resources/docs/design.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $_name/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name/target/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 $_name/target/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm 644 $_name/target/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 $_name/target/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
}
