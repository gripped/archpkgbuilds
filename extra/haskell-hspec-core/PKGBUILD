# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-core
pkgname=haskell-hspec-core
pkgver=2.11.4
pkgrel=22
pkgdesc="A Testing Framework for Haskell"
url="https://hspec.github.io/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-call-stack' 'haskell-hspec-expectations'
         'haskell-hunit' 'haskell-lexer' 'haskell-quickcheck' 'haskell-quickcheck-io'
         'haskell-random' 'haskell-tf-random')
makedepends=('ghc')
checkdepends=('haskell-base-orphans' 'haskell-hspec-meta' 'haskell-silently' 'haskell-temporary')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        quickcheck-2.15.patch)
sha512sums=('4169813a4fe6b38de431b04f2befe7fae59873392d215a226eccd29aa1eb503a62dd5a5d4e1b7789bb227530f47543d4c6c5bd03755845a01aad1aa50495a09e'
            '17285bade065737816274c53919ae626804095484653fbb2240947818dda4527f668a45163929c3ad64f8f1c9d9c8de9259e107c1480cb53333bd5cbf3a6f033')

prepare() {
    cd $_hkgname-$pkgver
    patch -p2 -i ../quickcheck-2.15.patch
}

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
