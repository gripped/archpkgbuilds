# Maintainer: Torsten Ke√üler <tpkessler@archlinux.org>

pkgname=python-onnxscript
pkgver=0.6.2
pkgrel=1
pkgdesc='Author ONNX functions and models'
arch=('any')
url='https://microsoft.github.io/onnxscript/'
license=('MIT')
depends=(
  python
  python-numpy
  python-ml-dtypes
  python-onnx
  python-onnx-ir
  python-typing_extensions
  python-rich
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${pkgname}::git+https://github.com/microsoft/onnxscript.git#tag=v${pkgver}")
sha512sums=('515dbe6f174b376e5eab4fd3989a9609b3ed48bad71b4d7c05323e26fdc010cd61325ae6d44bf0abdaff6e32fd0243acd4b5b9f446cafc4f06d4cdfac27cb4e6')


build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
# vim:set ts=2 sw=2 et:
