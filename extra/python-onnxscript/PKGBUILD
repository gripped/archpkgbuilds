# Maintainer: Torsten Ke√üler <tpkessler@archlinux.org>

pkgname=python-onnxscript
pkgver=0.5.6
pkgrel=1
pkgdesc='Author ONNX functions and models'
arch=('any')
url='https://microsoft.github.io/onnxscript/'
license=('MIT')
depends=(
  python
  python-numpy
  python-ml-dtypes
  python-onnx
  python-onnx-ir
  python-typing_extensions
  python-rich
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${pkgname}::git+https://github.com/microsoft/onnxscript.git#tag=v${pkgver}")
sha512sums=('e1a7a2c760e56b0c7720f41b5e4158e43763bd5abae67935d35752a5a6bee3779ad7d5ac3ee8b02d3af634c8185a2f992fd6f1795516f53382fb1187c9ef5bed')


build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
# vim:set ts=2 sw=2 et:
