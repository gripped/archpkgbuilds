# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jonni Westphalen <jonny.westphalen@googlemail.com>

pkgname=python-pyclibrary
pkgver=0.3.0
pkgrel=1
pkgdesc="C parser and bindings automation for Python"
arch=(any)
url="https://github.com/MatthieuDartiailh/pyclibrary"
license=(MIT)
depends=(
  python
  python-pyparsing
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
)
checkdepends=(
  python-pytest
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
b2sums=('1676e1208713c14bb0e53811e4ba070fcac6c45c1ffb41b607734c9b231e82c8d8359d24d0de5114c98b61b11aae45ba2cae86de7e2a040b8e566a45fa6fd0fe')

build() {
  cd pyclibrary-$pkgver

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver \
  python -m build --wheel --no-isolation
}

check() {
  cd pyclibrary-$pkgver

  # FIXME: For some reason pytest does not execute the xunit-style setup functions (setup_module, setup_class, etc.)
  # although it is documented: https://docs.pytest.org/en/stable/how-to/xunit_setup.html
  # Hence, we get errors like AttributeError: 'TestParsing' object has no attribute 'parser'
  #python -m venv --system-site-packages test-env
  #test-env/bin/python -m installer dist/*.whl
  #test-env/bin/python -m pytest tests
}

package() {
  cd pyclibrary-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
