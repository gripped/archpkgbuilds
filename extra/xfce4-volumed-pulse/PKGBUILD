# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: kj_sh604 <406hs_jk@proton.me>
# Contributor: Bernhard Landauer <oberon@manjaro.org>

pkgname=xfce4-volumed-pulse
pkgver=0.2.5
pkgrel=1
pkgdesc="A volume keys control daemon for Xfce using pulseaudio"
arch=('x86_64')
url="https://gitlab.xfce.org/apps/xfce4-volumed-pulse"
license=('GPL-3.0-only')
depends=('libnotify' 'pulse-native-provider' 'libkeybinder3' 'xfconf')
makedepends=('git' 'xfce4-dev-tools')
optdepends=('xfce4-notifyd: for OSD notifications')
source=("git+https://gitlab.xfce.org/apps/xfce4-volumed-pulse.git#tag=$pkgname-$pkgver")
sha256sums=('23d047252a24d0ff5868b3a4931368a7ca1966d0a45ecf91179e0be9bad5c382')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
