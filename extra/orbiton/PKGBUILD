# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgbase=orbiton
pkgname=(orbiton orbiton-gtk3 orbiton-nano)
pkgver=2.72.1
pkgrel=1
pkgdesc='Configuration-free text editor and IDE'
arch=(x86_64)
url='https://roboticoverlords.org/orbiton/'
license=(BSD-3-Clause)
makedepends=(git go vte3)
depends=(glibc)
options=(!lto)
source=("git+https://github.com/xyproto/orbiton#tag=v$pkgver")
b2sums=('c7ed5ef708721c4e8cd3b13067f603ff2ba11c7ee7001df094377297a0ba6106812b72bb6f029158a7d859034d6d856e1b57c5c04bf4990e539737a3bdf8e537')

build() {
  export LDFLAGS="$(echo $LDFLAGS | sed 's/ -Wl//g;s/,--sort-common//')"
  make -C $pkgbase o gui
}

package_orbiton() {
  # TODO: consider moving one or more of these from AUR:
  # 'cxx: for compiling C and C++'
  # 'elm-bin: for compiling Elm'
  # 'gnucobol: for compiling COBOL'
  # 'google-java-format: for formatting Java'
  # 'hare: for compiling Hare'
  # 'lua-format: for formatting Lua'
  # 'scala: for compiling Scala'
  # 'vlang: for compiling and formatting V'
  optdepends=('abcmidi: for writing music in the ABC notation'
              'agda: for compiling Agda'
              'asciidoctor: for writing man pages'
              'astyle: for formatting C#'
              'clang: for formatting C++ code with clang-format'
              'clojure: for compiling Clojure'
              'crystal: for compiling Crystal'
              'dart: for compiling and formatting Dart'
              'fpc: for compiling Object Pascal'
              'gcc-d: for compiling D'
              'gdb: for debugging C'
              'ghc: for compiling Haskell'
              'gopls: for Tab completing Go'
              'haskell-brittany: for formatting Haskell'
              'jad: for decompiling .class files on the fly'
              'java-environment: for compiling Java'
              'just: for editing justfiles'
              'kotlin: for compiling Kotlin'
              'ktlint: for formatting Kotlin'
              'lua: for compiling Lua'
              'mlton: for compiling Standard ML'
              'mono: for compiling C#'
              'nasm: for compiling Assembly'
              'ocaml: for compiling and formatting OCaml'
              'odin: for compiling Odin'
              'odinfmt: for formatting Odin'
              'ollama: for explaining code'
              'pandoc: for exporting Markdown to PDF'
              'perl-tidy: for formatting Perl'
              'prettier: for formatting JavaScript, TypeScript and CSS'
              'pylsp: for Tab completing Python'
              'python-ruff: for formatting Python'
              'rust-analyzer: for Tab completing Rust'
              'rustup: for compiling and formatting Rust'
              'scdoc: for writing man pages'
              'shfmt: for formattig Shell scripts'
              'tidy: for formatting HTML'
              'timidity: for playing music in the ABC notation'
              'yasm: for compiling Assembly'
              'zig: for compiling and formatting Zig'
              'zls: for Tab completing Zig')

  DESTDIR="$pkgdir" make -C $pkgbase install symlinks easteregg license
}

package_orbiton-gtk3() {
  pkgdesc='Configuration-free text editor and IDE (GTK3/VTE3)'
  depends+=(orbiton vte3)
  DESTDIR="$pkgdir" PROJECT=$pkgname make -C $pkgbase gui-install gui-symlinks license
}

package_orbiton-nano() {
  pkgdesc='Configuration-free text editor and IDE (Nano/Pico Mode)'
  depends+=(orbiton)
  conflicts=(nano)
  provides=(nano)
  install -d "$pkgdir/usr/bin"
  DESTDIR="$pkgdir" PROJECT=$pkgname make -C $pkgbase nano-symlink license
}
