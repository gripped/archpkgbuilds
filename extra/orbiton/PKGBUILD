# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgbase=orbiton
pkgname=(orbiton orbiton-gtk3 orbiton-nano)
pkgver=2.68.7
pkgrel=1
pkgdesc='Fast and configuration-free text editor and IDE limited by VT100'
arch=(x86_64)
url='https://roboticoverlords.org/orbiton/'
license=(BSD-3-Clause)
makedepends=(git go vte3)
options=(!lto)
source=("git+https://github.com/xyproto/orbiton#tag=v$pkgver")
b2sums=('3bc2ac54bda684f79a0906c7975f49d72da6aba99f4f05e90af9ce6d2301843a2fcc665b25b2cec93b92e29c9819bd9e1392cc1bf097204c4c258f5754c09a86')

build() {
  export LDFLAGS="$(echo $LDFLAGS | sed 's/ -Wl//g;s/,--sort-common//')"
  make -C $pkgbase o gui
}

package_orbiton() {
  # TODO: consider moving one or more of these from AUR:
  # 'cxx: for compiling C and C++'
  # 'elm-bin: for compiling Elm'
  # 'google-java-format: for formatting Java'
  # 'lua-format: for formatting Lua'
  # 'hare: for compiling Hare'
  # 'scala: for compiling Scala'
  # 'vlang: for compiling and formatting V'
  optdepends=('abcmidi: for writing music in the ABC notation'
              'asciidoctor: for writing man pages'
              'agda: for compiling Agda'
              'astyle: for formatting C#'
              'clang: for formatting C++ code with clang-format'
              'clojure: for compiling Clojure'
              'crystal: for compiling Crystal'
              'dart: for compiling and formatting Dart'
              'fpc: for compiling Object Pascal'
              'gdb: for debugging C'
              'gcc-d: for compiling D'
              'ghc: for compiling Haskell'
              'godot: for using GDScript'
              'haskell-brittany: for formatting Haskell'
              'jad: for decompiling .class files on the fly'
              'java-environment: for compiling Java'
              'just: for editing justfiles'
              'kotlin: for compiling Kotlin'
              'ktlint: for formatting Kotlin'
              'lua: for compiling Lua'
              'mlton: for compiling Standard ML'
              'mono: for compiling C#'
              'nasm: for compiling Assembly'
              'ocaml: for compiling and formatting OCaml'
              'odin: for compiling Odin'
              'odinfmt: for formatting Odin'
              'ollama: for experimental tab completion'
              'perl-tidy: for formatting Perl'
              'pandoc: for exporting Markdown to PDF'
              'prettier: for formatting JavaScript, TypeScript and CSS'
              'python-black: for formatting Python'
              'rustup: for compiling and formatting Rust'
              'scdoc: for writing man pages'
              'shfmt: for formattig Shell scripts'
              'tidy: for formatting HTML'
              'timidity: for playing music in the ABC notation'
              'yasm: for compiling Assembly'
              'zig: for compiling and formatting Zig')
  DESTDIR="$pkgdir" make -C $pkgbase install symlinks easteregg license
}

package_orbiton-gtk3() {
  pkgdesc='Fast and configuration-free text editor and IDE limited by VT100 (GTK3/VTE3)'
  depends=(orbiton vte3)
  DESTDIR="$pkgdir" PROJECT=$pkgname make -C $pkgbase gui-install gui-symlinks license
}

package_orbiton-nano() {
  pkgdesc='Fast and configuration-free text editor and IDE limited by VT100 (Nano/Pico Mode)'
  depends=(orbiton)
  conflicts=(nano)
  provides=(nano)
  install -d "$pkgdir/usr/bin"
  DESTDIR="$pkgdir" PROJECT=$pkgname make -C $pkgbase nano-symlink license
}
