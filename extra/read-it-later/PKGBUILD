# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=read-it-later
pkgver=0.6.2
pkgrel=2
pkgdesc='Simple Wallabag client'
arch=(x86_64)
url='https://gitlab.gnome.org/World/read-it-later'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  sqlite
  webkitgtk-6.0
  zlib
)
makedepends=(
  appstream
  git
  meson
  rust
)
options=(!lto)
source=(
  "git+https://gitlab.gnome.org/World/read-it-later.git#tag=$pkgver"
  read-it-later-dbus-activatable.patch
)
b2sums=(
  62f306e82e4a62d951b41e74a7a9a74fb96cd6bd506ca5d3fbad89a984cc9631e3ffe2db79b8659a5f75144d92145f7bff10ca0d9bc86d0cfd3c8c25c6113066
  41511c867a29a24b51937076c97d684fa284f1af7c3b76fd67a6c31b36009cc11612e87787a33d68734079a13b78098e4af4424c072ee2ef8d78be86f394f458
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/read-it-later/-/merge_requests/134
  git apply -3 ../read-it-later-dbus-activatable.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
