# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openfec
pkgver=1.4.2.10
pkgrel=1
pkgdesc="Open Forward Erasure Codecs"
arch=(x86_64)
url="http://openfec.org/"
license=(
  CECILL-2.0
  CECILL-C
)
depends=(glibc)
makedepends=(cmake)
provides=(libopenfec.so)
# NOTE: we are using roc-streaming's version, because the original upstream is effectively dead:
# https://github.com/roc-streaming/openfec
source=(https://github.com/roc-streaming/openfec/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('8ee892241c9196d588519e2d42a48d057607d20812c7a585ff27ac34959012dfd24d89a6f5a3c6534d47d6a79714b6075baa45329bb3747c376eecc2c4c28b6c')
b2sums=('ac59165820e1f68f2fd3d8007c53146c33a051d95ff6b51ddcf71ef0dda19108ed4f043c5ff83fc867ebc0ee71f2a944e6be3e1fa3c6cc0e326dc84e34e02d3d')

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DBUILD_SHARED_LIBS=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{LICENCE,Licence}* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
