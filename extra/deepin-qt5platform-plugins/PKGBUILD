# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.7.22
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('LGPL-3.0-or-later')
depends=('qt5-base' 'qt5-x11extras' 'libx11' 'libxi' 'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'qt5-xcb-private-headers' 'libglvnd' 'gtest'
             'extra-cmake-modules' 'dtkcommon' 'cairo')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt5platform-plugins.git#tag=$pkgver")
sha512sums=('f309b685859a4f07f9450db0c88bc3b96ce6321605a0e2335ec2a8a22620b2be115a15d1db312488fdd6ca822c312b3999c43d39c6774a9d2831b6d4611e6adb')

prepare() {
  cd qt5platform-plugins
  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev
  sed -i 's|qt${QT_VERSION_MAJOR}/plugins|qt/plugins|' xcb/CMakeLists.txt

  # Disable wayland, unsupported upstream
  sed -i '/wayland/d' CMakeLists.txt
}

build() {
  cd qt5platform-plugins
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qtxcb-private
  ninja
}

package() {
  cd qt5platform-plugins
  DESTDIR="$pkgdir" ninja install
}
