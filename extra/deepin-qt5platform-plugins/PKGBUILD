# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.7.27
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('LGPL-3.0-or-later')
depends=('qt5-base' 'qt5-x11extras' 'libx11' 'libxi' 'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs' 'libsm' 'libice')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'qt5-xcb-private-headers' 'libglvnd' 'gtest'
             'extra-cmake-modules' 'dtkcommon' 'cairo')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt5platform-plugins.git#tag=$pkgver")
sha512sums=('6cc567582f167e8ebe4b68cf7b5579c2cf1acf9206323c271064f0f34f2d7bc0a216f27440e93053e7dda75efb8970f9844b1193057cb58564663d4bf0cff7a9')

prepare() {
  cd qt5platform-plugins
  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev
  sed -i 's|qt${QT_VERSION_MAJOR}/plugins|qt/plugins|' xcb/CMakeLists.txt

  # Disable wayland, unsupported upstream
  sed -i '/wayland/d' CMakeLists.txt
}

build() {
  cd qt5platform-plugins
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qtxcb-private
  ninja
}

package() {
  cd qt5platform-plugins
  DESTDIR="$pkgdir" ninja install
}
