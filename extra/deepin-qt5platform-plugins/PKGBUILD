# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.7.23
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('LGPL-3.0-or-later')
depends=('qt5-base' 'qt5-x11extras' 'libx11' 'libxi' 'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'qt5-xcb-private-headers' 'libglvnd' 'gtest'
             'extra-cmake-modules' 'dtkcommon' 'cairo')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt5platform-plugins.git#tag=$pkgver")
sha512sums=('3591d447c287c14da958dd8bb30421f160cf3749579242bd630d3adce9ee3155704c59549346c508fdca3f9deed933ae873a285336a0e06bceae3ba303c4cc96')

prepare() {
  cd qt5platform-plugins
  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev
  sed -i 's|qt${QT_VERSION_MAJOR}/plugins|qt/plugins|' xcb/CMakeLists.txt

  # Disable wayland, unsupported upstream
  sed -i '/wayland/d' CMakeLists.txt
}

build() {
  cd qt5platform-plugins
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qtxcb-private
  ninja
}

package() {
  cd qt5platform-plugins
  DESTDIR="$pkgdir" ninja install
}
