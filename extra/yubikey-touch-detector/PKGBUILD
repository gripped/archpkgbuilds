# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>

pkgname=yubikey-touch-detector
pkgver=1.13.0
pkgrel=1
pkgdesc="A tool that can detect when your YubiKey is waiting for a touch"
url="https://github.com/maximbaz/yubikey-touch-detector"
arch=('x86_64')
license=('ISC')
depends=('glibc' 'gpgme' 'hicolor-icon-theme')
makedepends=('go' 'scdoc')
optdepends=('gnupg: for GPG'
            'openssh: for SSH')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
        "${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc")
b2sums=('f73f03ffe19feee97dc9329542d8e91d92f5bc96482358b59a88674febe02c7e0cb8539976d18e18010b7b3f3228b3ee69e0cc8764a6733cb66cdf0b305b7ab2'
        'SKIP')
validpgpkeys=('56C3E775E72B0C8B1C0C1BD0B5DB77409B11B601')

prepare(){
	cd "${pkgname}-${pkgver}"
	mkdir -p build/
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o "build/${pkgname}"

	scdoc < "${pkgname}.1.scd" > "${pkgname}.1"
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm 755 "build/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 "${pkgname}.service" "${pkgdir}/usr/lib/systemd/user/${pkgname}.service"
	install -Dm 644 "${pkgname}.socket" "${pkgdir}/usr/lib/systemd/user/${pkgname}.socket"
	install -Dm 644 "${pkgname}.png" "${pkgdir}/usr/share/icons/hicolor/128x128/apps/${pkgname}.png"
	install -Dm 644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
	install -Dm 644 service.conf.example "${pkgdir}/usr/share/doc/${pkgname}/service.conf.example"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md" 
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
