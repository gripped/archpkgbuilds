# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=hex
_pkgname=hx
pkgver=0.7.0
pkgrel=1
pkgdesc="Futuristic take on hexdump"
arch=('x86_64')
url="https://github.com/sitkevij/hex"
license=(MIT)
depends=('gcc-libs')
makedepends=('cargo')
source=($pkgname-$pkgver.tar.gz::https://github.com/sitkevij/hex/archive/v${pkgver}.tar.gz)
sha512sums=('e4d30a4c582e26d13e85caaba6e1de19fc6fa0f04f11f59a91c4cc7c9510600bfbe67d68301c7dcf8ffaf0cf1fbfa3a340f87791e2423d7a16feab0427b8f3dc')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo fetch --locked
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --frozen
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --frozen
  cargo test --frozen
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
  install -Dm644 "$_pkgname.1" "$pkgdir/usr/share/man/man1/$_pkgname.1"
}

# vim:set ts=2 sw=2 et:
