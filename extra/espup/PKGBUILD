# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=espup
pkgver=0.8.0
pkgrel=1
pkgdesc="Tool for installing and maintaining ESP Rust ecosystem"
url='https://github.com/esp-rs/espup'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=('curl' 'git' 'ldproxy' 'libbz2.so' 'liblzma.so' 'pkg-config' 'python' 'python-pip' 'udev')
makedepends=('cargo')
options=(!lto)
source=(https://github.com/esp-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('eabe00af217b0cba400ec766f2a047f199c935b2dbe7b5c987f332deeb7140de')
b2sums=('7c3154886486d463e7057d9ad5857d08e03345578e227b8aa49d3b8431b5cd0f6bf47dfd89c5264083cb39f70979d68e342996a49607e5162b4672a7094bbcbe')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/${pkgname} --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
}

# vim: ts=2 sw=2 et:
