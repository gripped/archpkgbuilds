# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=espup
pkgver=0.15.0
pkgrel=1
pkgdesc="Tool for installing and maintaining ESP Rust ecosystem"
url='https://github.com/esp-rs/espup'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'curl'
  'gcc-libs'
  'git'
  'glibc'
  'ldproxy'
  'libbz2.so'
  'liblzma.so'
  'libssl.so'
  'pkg-config'
  'python'
  'python-pip'
  'udev'
)
makedepends=('cargo')
options=(!lto)
source=(
  https://github.com/esp-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
)
sha256sums=('a395a51d2786853d880ced4467a0bdf7afb7c5a4b3f1f10b007b52e13ab9e0a9')
b2sums=('4d0e772a1501b7df204b5be86b0c3976f5e85291714eab78f69d4ddf00e52b5dfd33e2cea26ec4a82371c004304c5328bc33aff3c8b382421462e6dd6e048150')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo test --frozen
  target/release/${pkgname} --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-*
}

# vim: ts=2 sw=2 et:
