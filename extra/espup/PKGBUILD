# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=espup
pkgver=0.15.1
pkgrel=1
pkgdesc="Tool for installing and maintaining ESP Rust ecosystem"
url='https://github.com/esp-rs/espup'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'curl'
  'gcc-libs'
  'git'
  'glibc'
  'ldproxy'
  'libbz2.so'
  'liblzma.so'
  'libssl.so'
  'pkg-config'
  'python'
  'python-pip'
  'udev'
)
makedepends=('cargo')
options=(!lto)
source=(
  https://github.com/esp-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
)
sha256sums=('a015939e757520d6f5ed028abdf54f9d74a33b138493258079d45b10714ff28f')
b2sums=('d9bee11c13265ba8e161a0941c95c8b96b17a88c300e4806cadfcc3586e6af7d48154de9c0005327684ffa4817a74e1d9f2ab43fb7e47769695dc030a0046b6b')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo test --frozen
  target/release/${pkgname} --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-*
}

# vim: ts=2 sw=2 et:
