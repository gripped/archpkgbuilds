# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=espup
pkgver=0.14.1
pkgrel=1
pkgdesc="Tool for installing and maintaining ESP Rust ecosystem"
url='https://github.com/esp-rs/espup'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'curl'
  'gcc-libs'
  'git'
  'glibc'
  'ldproxy'
  'libbz2.so'
  'liblzma.so'
  'libssl.so'
  'pkg-config'
  'python'
  'python-pip'
  'udev'
)
makedepends=('cargo')
options=(!lto)
source=(
  https://github.com/esp-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
)
sha256sums=('ebc8b9cd0fdcc805660229cc077d1b9b4dd5062ea8319b7ef494424bee0323a7')
b2sums=('aef6c3910e3ce040a0c6c503e0eb3cec2c8fcbde54af745371432e678dd0e20b031f9b722e14eb66c666230590cfcd6cc866fbd7c92ac7b2e96b624fd977def3')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo test --frozen
  target/release/${pkgname} --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-*
}

# vim: ts=2 sw=2 et:
