# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=espup
pkgver=0.12.0
pkgrel=1
pkgdesc="Tool for installing and maintaining ESP Rust ecosystem"
url='https://github.com/esp-rs/espup'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'curl'
  'gcc-libs'
  'git'
  'glibc'
  'ldproxy'
  'libbz2.so'
  'liblzma.so'
  'libssl.so'
  'pkg-config'
  'python'
  'python-pip'
  'udev'
)
makedepends=('cargo')
options=(!lto)
source=(https://github.com/esp-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2cf88ace2d03c87e63f8d58f52105c79adf12388e14498ecedb40f392158b6b9')
b2sums=('30159b16000f956c2669da664b8e7fb5b2f72e78b38c783673d5bd3d31ff83565ea5deb6390255580f42b56b7f4be806aae4240a92118d6af59885d17b4cdf41')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo test --frozen
  target/release/${pkgname} --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-*
}

# vim: ts=2 sw=2 et:
