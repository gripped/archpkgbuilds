# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=espup
pkgver=0.16.0
pkgrel=1
pkgdesc="Tool for installing and maintaining ESP Rust ecosystem"
url='https://github.com/esp-rs/espup'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'git'
  'glibc'
  'ldproxy'
  'openssl' 'libssl.so'
  'pkg-config'
  'python'
  'python-pip'
  'udev'
  'xz' 'liblzma.so'
)
makedepends=('cargo')
options=(!lto)
source=(
  https://github.com/esp-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
)
sha256sums=('9e1e9c1effa860e41dc12a1cb1a2a5974c0849bc8cf330a3f7c5eb892d845e5e')
b2sums=('e5a104ceacbaf1e590b46a39793cae5dba3f3c82bc8fd3969fc3ec441a850173686e21335fbf26d5e2ac0ed2846c6aaf77c085b97ebf88524c4cdbf308ba1555')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo test --frozen
  target/release/${pkgname} --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE-*
}

# vim: ts=2 sw=2 et:
