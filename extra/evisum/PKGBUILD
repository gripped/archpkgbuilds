# Maintainer: Carsten Haitzler <raster@archlinux.org>

pkgname=evisum
pkgver=0.6.1
pkgrel=3
pkgdesc="GUI top/htop-like process viewer using EFL"
arch=('x86_64')
url="https://www.enlightenment.org"
license=('BSD')
depends=('efl')
makedepends=('meson' 'ninja')
source=("https://download.enlightenment.org/rel/apps/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('832f20b8de13e290890810267cf41ed84cbb0c84e2e20a14f6783632612dadb9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -fvisibility=hidden"
  meson build --prefix=/usr
  ninja -C build
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}" ninja -C build install

  # install license files
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" AUTHORS
}
