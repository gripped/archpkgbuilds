# Maintainer: Carsten Haitzler <raster@archlinux.org>

pkgname=evisum
pkgver=0.6.4
pkgrel=1
pkgdesc="GUI top/htop-like process viewer using EFL"
arch=('x86_64')
url="https://www.enlightenment.org"
license=('BSD')
depends=('efl')
makedepends=('meson' 'ninja')
source=("https://download.enlightenment.org/rel/apps/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('865ca8b565137c32a412302f0eb944ef1604306ed792c0a0dd7f5792c75c6bf8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -fvisibility=hidden"
  meson build --prefix=/usr
  ninja -C build
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}" ninja -C build install

  # install license files
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" AUTHORS
}
