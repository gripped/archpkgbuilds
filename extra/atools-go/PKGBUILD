# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=atools-go
pkgver=0.6.0
pkgrel=1
pkgdesc="Alpine Linux APKBUILD linter"
url="https://gitlab.alpinelinux.org/alpine/infra/atools-go"
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('redo-python' 'go')
conflicts=('atools')
source=("${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('530477f16b93877e6fcee10e8029b389acb9c701f679bd55ea1a6118521488901e848c06d614f7aeab38697b1bd28259a5be40b54f86f392d7fb88dc83d3b35b')

build() {
	cd "${pkgname}-v${pkgver}"
	./configure --prefix /usr
	redo
}

check() {
	cd "${pkgname}-v${pkgver}"
	redo test
}

package() {
	cd "${pkgname}-v${pkgver}"
	DESTDIR="${pkgdir}" redo install
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
