# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=atools-go
pkgver=0.6.1
pkgrel=1
pkgdesc="Alpine Linux APKBUILD linter"
url="https://gitlab.alpinelinux.org/alpine/infra/atools-go"
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('redo-python' 'go')
conflicts=('atools')
source=("${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('be4addb408fec9e43fe2fdfdb0db6f3c682bfea1394628352e74e148c947378a0dcbfd61ee278887e8f792958d1f8a57f9f7462d1e901167359534f8679d4455')

build() {
	cd "${pkgname}-v${pkgver}"
	./configure --prefix /usr
	redo
}

check() {
	cd "${pkgname}-v${pkgver}"
	redo test
}

package() {
	cd "${pkgname}-v${pkgver}"
	DESTDIR="${pkgdir}" redo install
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
