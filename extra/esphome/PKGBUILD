# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: sgar < swhaat at github >

pkgname=esphome
pkgver=2024.9.2
pkgrel=1
pkgdesc='Solution for your ESP8266/ESP32 projects with Home Assistant'
url='https://github.com/esphome/ESPHome'
depends=(
  python
	python-voluptuous
	python-yaml
	python-paho-mqtt
	python-icmplib
	python-tornado
	python-tzlocal
	python-tzdata
	python-pyserial
	platformio-core
	esptool
	python-click
	python-aioesphomeapi
	python-zeroconf
	python-magic
	python-ruamel-yaml
	python-kconfiglib
	python-pyparsing
	python-argcomplete
	python-pillow
	python-cairosvg
	python-async-timeout
	python-requests
	python-colorama
	python-packaging
	python-cryptography
)
makedepends=(python-build python-installer python-wheel)
license=('MIT AND GPL-3.0-only')
arch=(any)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('c2f0958b423a8cc27e953f4ebcba39cb1149b75e25601722b835e2293139dd676f9087a6e20395afd152227f40c56cb6c462761ea08a58dd270727314887ea15')

prepare() {
	cd $pkgname-$pkgver

	# relax requirements
	sed -i 's/==.*//' requirements.txt
	sed -i 's/setuptools==[0-9.]\+/setuptools/' pyproject.toml
	sed -i 's/wheel~=[0-9.]\+/wheel/' pyproject.toml
}

build() {
	cd $pkgname-$pkgver
	python -m build --wheel --no-isolation
}

package() {
	cd $pkgname-$pkgver
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
