# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: sgar < swhaat at github >

pkgname=esphome
pkgver=2024.10.2
pkgrel=1
pkgdesc='Solution for your ESP8266/ESP32 projects with Home Assistant'
url='https://github.com/esphome/ESPHome'
depends=(
  python
	python-voluptuous
	python-yaml
	python-paho-mqtt
	python-icmplib
	python-tornado
	python-tzlocal
	python-tzdata
	python-pyserial
	platformio-core
	esptool
	python-click
	python-aioesphomeapi
	python-zeroconf
	python-magic
	python-ruamel-yaml
	python-kconfiglib
	python-pyparsing
	python-argcomplete
	python-pillow
	python-cairosvg
	python-async-timeout
	python-requests
	python-colorama
	python-packaging
	python-cryptography
)
makedepends=(python-build python-installer python-wheel)
license=('MIT AND GPL-3.0-only')
arch=(any)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('cbc9ac65bed36cc2a3ef127ebac0dd5682c6136144cc4b5708751203be26376509e77ce16246e7697f2e214af1ba5ed7d82a559fc9868f2f5baa8b168b4f965b')

prepare() {
	cd $pkgname-$pkgver

	# relax requirements
	sed -i 's/==.*//' requirements.txt
	sed -i 's/setuptools==[0-9.]\+/setuptools/' pyproject.toml
	sed -i 's/wheel~=[0-9.]\+/wheel/' pyproject.toml
}

build() {
	cd $pkgname-$pkgver
	python -m build --wheel --no-isolation
}

package() {
	cd $pkgname-$pkgver
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
