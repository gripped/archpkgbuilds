# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: sgar < swhaat at github >

pkgname=esphome
pkgver=2025.3.2
pkgrel=1
pkgdesc='Solution for your ESP8266/ESP32 projects with Home Assistant'
url='https://github.com/esphome/ESPHome'
depends=(
  python
	python-voluptuous
	python-yaml
	python-paho-mqtt
	python-icmplib
	python-tornado
	python-tzlocal
	python-tzdata
	python-pyserial
	platformio-core
	esptool
	python-click
	python-aioesphomeapi
	python-zeroconf
	python-magic
	python-ruamel-yaml
	python-kconfiglib
	python-pyparsing
	python-argcomplete
	python-pillow
	python-cairosvg
	python-requests
	python-colorama
	python-packaging
	python-cryptography
	python-puremagic
	python-freetype-py
	python-esphome-glyphsets
)
makedepends=(python-build python-installer python-wheel)
optdepends=('python-esphome-dashboard: ESPHome dashboard')
license=('MIT AND GPL-3.0-only')
arch=(any)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('b4173c74a215def81aa2cd11a9a58a3009c5355c15ee893183214d39229cf8ccbee4f9001d29e6bffdfbc43c8cee2d51ae0f5af7a7ab67ecb37e14be64361a6e')

prepare() {
	cd $pkgname-$pkgver

	# relax requirements
	sed -i 's/==.*//' requirements.txt
	sed -i 's/setuptools==[0-9.]\+/setuptools/' pyproject.toml
	sed -i 's/wheel~=[0-9.]\+/wheel/' pyproject.toml
}

build() {
	cd $pkgname-$pkgver
	python -m build --wheel --no-isolation
}

package() {
	cd $pkgname-$pkgver
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
