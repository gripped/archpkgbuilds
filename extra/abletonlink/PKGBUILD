# Maintainer: David Runge <dvzrv@archlinux.org>

_name=link
pkgname=abletonlink
pkgver=3.1.2
pkgrel=2
pkgdesc="Synchronizes musical beat, tempo, and phase across multiple applications"
arch=(any)
url="https://github.com/ableton/link"
license=(GPL-2.0-or-later)
depends=(asio)
makedepends=(
  catch2
  cmake
  git
  jack
  portaudio
  qt5-tools
  qt5-quickcontrols
)
source=(
  $_name::git+$url#tag=Link-$pkgver
  asio::git+https://github.com/chriskohlhoff/asio.git
  $pkgname-3.0.5-cmake_system_paths.patch
  $pkgname-3.1.2-devendor_catch2.patch
)
sha512sums=('cbe5d459683c4b97aae6f8c10e46a5e2d39824f585267be9d72c2a8a4246d022cd88c5d18986d62a59bb9fda80f43e664521e3b57728bcf70eb06cb4b67baf8c'
            'SKIP'
            '826a6e83b6ad859197475551b82f8391844d8d3717a1f2df4a7b16d77d47a1a557ce4c7231a68fa11b7d1112a10746e23875d387efa50af8735df150bb26a6cd'
            '4563f7fd1279537cd5494a00f243ac4e53b40c4f363ac91cbb881479984903fd977aad43d314bff1dadaa9b28cc53c6525d3864007da2eb8f45a81418a018fde')
b2sums=('a519377045ad5b7d09de85155a9caa0daa839747c1e81d898b033340a56fdb8cdbd838376432bdbd3b6f46c59a745757e194f4fb366f49be4ed1ef9fd3d3463d'
        'SKIP'
        '63c23dddf60ebe055f9087f7fe5c7519773a3af055059025d283bb02581a25d3fd0ffec8be2aecde43fb1427400bdaadaee9d8387364d81725484ea5f38bd13a'
        'abb60e55b0e8d8d74857564bc00fd13115741557d24d64dde71c37126d60f97b64ec74fe0eae85612b7eafd456716749a7a83e120ec675d9cc4bbf7bece294ba')

prepare() {
  (
    cd $_name
    git submodule init
    git config submodule.modules/asio-standalone.url ../asio
    git -c protocol.file.allow=always submodule update
  )

  # use system provided catch2
  patch -Np1 -d $_name -i ../$pkgname-3.1.2-devendor_catch2.patch

  # the cmake integration is used to build tests and examples, so duplicating
  # before patching for later providing it system-wide
  cp -av $_name $_name-system

  # provide cmake integration with system-wide paths (for other projects)
  patch -Np1 -d $_name-system -i ../$pkgname-3.0.5-cmake_system_paths.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D LINK_BUILD_ASIO=ON
    -D LINK_BUILD_JACK=ON
    -D LINK_BUILD_QT_EXAMPLES=ON
    -S $_name
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  build/bin/LinkCoreTest
  build/bin/LinkDiscoveryTest
}

package() {
  (
  cd $_name
  find include -type f \( -iname "*.ipp" -o -iname "*.hpp" \) -exec install -vDm 644 {} "$pkgdir/usr/"{} \;
  install -vDm 644 {{README,CONTRIBUTING}.md,*.pdf} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 cmake_include/AsioStandaloneConfig.cmake -t "$pkgdir/usr/lib/cmake/$pkgname/cmake_include"

  # NOTE: vendor asio 1.28 into default location as link is not compatible with newer asio: https://github.com/Ableton/link/issues/153
  install -vdm 755 "$pkgdir/usr/lib/cmake/$pkgname/modules/asio-standalone/asio/include/"
  cp -av modules/asio-standalone/asio/include/* "$pkgdir/usr/lib/cmake/$pkgname/modules/asio-standalone/asio/include/"
  )

  # modified cmake integration for system-wide use
  install -vDm 644 $_name-system/AbletonLinkConfig.cmake -t "$pkgdir/usr/lib/cmake/$pkgname/"
}
