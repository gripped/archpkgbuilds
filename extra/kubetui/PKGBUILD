# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=kubetui
pkgver=1.11.0
pkgrel=1
pkgdesc="A TUI for real-time monitoring and exploration of Kubernetes resources"
arch=('x86_64')
url="https://github.com/sarub0b0/kubetui"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('352e446f0b5a7d8aa9690af80d213474c2e75741028711859478a6ed8944c3cdd1d993dc056f59b812f5ba435c5c0cfd127be7f222f337b30b40a9771e992dfc')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  mkdir completions
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
  local compgen="target/release/$pkgname completion"
  $compgen bash >"completions/$pkgbase"
  $compgen zsh >"completions/_$pkgbase"
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "completions/$pkgname" -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions/"
}

# vim: ts=2 sw=2 et:
