# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=kubetui
pkgver=1.6.2
pkgrel=1
pkgdesc="A TUI for real-time monitoring and exploration of Kubernetes resources"
arch=('x86_64')
url="https://github.com/sarub0b0/kubetui"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('2a32cf371d21eb35977b04fc4dd45a7b9bbc29c6d381e5604d20222c2d0d7e49ee333cd4aa3358013e5fdadcd4600df6939fdb54f5f1e496135a24b52994036d')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
