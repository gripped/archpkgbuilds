# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=kubetui
pkgver=1.12.0
pkgrel=1
pkgdesc="A TUI for real-time monitoring and exploration of Kubernetes resources"
arch=('x86_64')
url="https://github.com/sarub0b0/kubetui"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('22181f62f5229b5136c32b5fd882e6939d72833dc2f80f1f83c729be52c13564d440422dd8005f4cde846d38b42b97c6182937306038a77a843f24b72022098e')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
  mkdir completions
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
  local compgen="target/release/$pkgname completion"
  $compgen bash >"completions/$pkgbase"
  $compgen zsh >"completions/_$pkgbase"
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "completions/$pkgname" -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions/"
}

# vim: ts=2 sw=2 et:
