# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

_pyname=igraph
pkgname=python-$_pyname
pkgver=0.11.8
pkgrel=2
url='https://igraph.org/python/'
license=(GPL-2.0-or-later)
arch=(x86_64)
pkgdesc='Python bindings for the igraph library'
depends=(glibc
         igraph
         python
         python-numpy
         python-texttable)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/igraph/python-igraph#tag=$pkgver)
sha256sums=('de5c02c3330edec9aae514eea143df1ba5ebd5188c9a4a284cab4b33a1248221')

build() {
  cd $pkgname
  IGRAPH_USE_PKG_CONFIG=1 \
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname
  PYTHONPATH="$PWD"/`ls -d build/lib.*` python -m unittest
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl 
}
