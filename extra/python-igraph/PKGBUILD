# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

_pyname=igraph
pkgname=python-$_pyname
pkgver=1.0dev20250919
pkgrel=1
_commit=f6e6bce784e478e4b105bf085a157f726a6ef5b5
url='https://igraph.org/python/'
license=(GPL-2.0-or-later)
arch=(x86_64)
pkgdesc='Python bindings for the igraph library'
depends=(glibc
         igraph
         python
         python-numpy
         python-texttable)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/igraph/python-igraph#commit=$_commit)
sha256sums=('8095b8c285859fdb2a3123ce66d7b3ca5b2441b6f4eeacd0fb0e0ba0a44e648a')

build() {
  cd $pkgname
  IGRAPH_USE_PKG_CONFIG=1 \
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname
  PYTHONPATH="$PWD"/`ls -d build/lib.*` python -m unittest
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl 
}
