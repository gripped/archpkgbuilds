# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

_pyname=igraph
pkgname=python-$_pyname
pkgver=0.11.4
pkgrel=2
url='https://igraph.org/python/'
license=(GPL)
arch=(x86_64)
pkgdesc='Python bindings for the igraph library'
depends=(glibc
         igraph
         python
         python-numpy
         python-texttable)
makedepends=(python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://github.com/igraph/python-igraph/releases/download/$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('2437ae0157af6824e2e65a23f7a1fa4fbf0f3664333c72aeca4fc01b83e18483')

build() {
  cd igraph-$pkgver
  IGRAPH_USE_PKG_CONFIG=1 \
  python -m build --wheel --no-isolation
}

check() {
  cd igraph-$pkgver
  PYTHONPATH="$PWD"/`ls -d build/lib.*` python -m unittest
}

package() {
  cd igraph-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl 
}
