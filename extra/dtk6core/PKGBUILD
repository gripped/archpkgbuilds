# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6core
pkgver=6.0.22
pkgrel=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6core"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'dtk6log' 'gcc-libs' 'glibc' 'icu' 'lshw' 'qt6-base'
         'uchardet')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6core.git#tag=$pkgver")
sha512sums=('934f3e61e4a358471e8c75dd21afd14e668222610b7959526ec14f2d338df0bb8c56e39dd4b79ee3ea49a63e3688d85b9e52c73a0966e41ba05420d50844ae22')

prepare() {
  cd dtk6core
  # upstream qt 6.8.1 fix
  git cherry-pick -n f8f1e7ccf297182aef12af19897f32f23ba9d555
  git cherry-pick -n a711231b5cbd253fdb240a8f35c08a677f22e811
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtk6core
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6core
  ninja test
}

package() {
  cd dtk6core
  DESTDIR="$pkgdir" ninja install
}
