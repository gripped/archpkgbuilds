# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6core
pkgver=6.0.17
pkgrel=2
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6core"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'gcc-libs' 'glibc' 'icu' 'libsystemd' 'lshw' 'qt6-base'
         'uchardet' 'spdlog')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6core.git#tag=$pkgver")
sha512sums=('cff9e5c9815089eb8b678013ef56fcadd170052ddff9f0a5eac1fb0ee9260945e5b99210b20cbef03218b1bab943195e7aa8fff6af44ef5959577ad1322fdbac')

prepare() {
  cd dtk6core
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtk6core
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DBUILD_WITH_SYSTEMD=ON \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6core
  ninja test
}

package() {
  cd dtk6core
  DESTDIR="$pkgdir" ninja install
}
