# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6core
pkgver=6.0.19
pkgrel=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6core"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'dtk6log' 'gcc-libs' 'glibc' 'icu' 'lshw' 'qt6-base'
         'uchardet')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6core.git#tag=$pkgver"
         qt-6.8.patch)
sha512sums=('c42e88e152e685b260ce85b97c7bf094ca81b4354bc07470f388f6f2f072352db8d7a4af86d394daa480e3ece22348c5a863ae5d5dcd04de657e5bba7de3d1e3'
            'ecc648232a032288ac7ca02615f861675bab5cebfe2123d9a0e8b790f3ab66af740e316aae5286920a523d0bb12c10cf9054aa7d43a097f1fc8e4c38c5dccda5')

prepare() {
  cd dtk6core
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
  patch -p1 -i ../qt-6.8.patch
}

build() {
  cd dtk6core
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6core
  ninja test
}

package() {
  cd dtk6core
  DESTDIR="$pkgdir" ninja install
}
