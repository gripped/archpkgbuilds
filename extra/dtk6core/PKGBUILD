# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6core
pkgver=6.0.32
pkgrel=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6core"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'dtk6log' 'gcc-libs' 'glibc' 'icu' 'lshw' 'qt6-base'
         'uchardet')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6core.git#tag=$pkgver"
         $pkgname-qt-6.9.patch::https://github.com/linuxdeepin/dtkcore/commit/8db10c91452d25fc3d380f685847bd53b769d3ad.patch
         $pkgname-qt-6.9-new.patch::https://github.com/linuxdeepin/dtkcore/commit/c250cf2111e5e3fb619f7507ac58e6d08150d89e.patch)
sha512sums=('60a5972bbef855feb13c14cef883987781ae576576177acf9fb1661a8c5e5689a3a329ac196671f31ca5ca1febc9edbe8eaae9fc1bf441ee7cc800592bfabd78'
            '37c2e476d47e2fd2b10b19e6ff81994ffa8b961dcd54c4056ec5c00a07684d44df4ab2dc57d617a6ee300dfd8bec88485d933485076cd200f420df36bc6ae674'
            'd116a8425c4c27ed423aff6be485775ae7df391d0ae568c81f24fc71cc6b4cb08bf4bd47a936d7387e6580d166bc3e4cc58827c6a1bed9399f5bc265aa9c1047')

prepare() {
  cd dtk6core
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
  patch -p1 -i ../$pkgname-qt-6.9.patch
  patch -p1 -i ../$pkgname-qt-6.9-new.patch
}

build() {
  cd dtk6core
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6core
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6core
  DESTDIR="$pkgdir" ninja install
}
