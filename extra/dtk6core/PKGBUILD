# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6core
pkgver=6.0.2
pkgrel=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6core"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'gcc-libs' 'glibc' 'icu' 'libsystemd' 'lshw' 'qt6-base'
         'uchardet' 'spdlog')
makedepends=('cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtk6core/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1f5aa610966848db8409732691fc6a7d578fba06893fb6c5d03c2a69ddcdbe412eae69ba67ad6e82b3fde0ead1264bbad0c5fe483aa8564f3f9561691216273e')

prepare() {
  cd dtk6core-$pkgver
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtk6core-$pkgver
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DFEATURES_INSTALL_DIR=lib/qt/mkspecs/features/ \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DBUILD_WITH_SYSTEMD=ON \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver

  ninja
}

check() {
  cd dtk6core-$pkgver
  ninja test
}

package() {
  cd dtk6core-$pkgver
  DESTDIR="$pkgdir" ninja install
}
