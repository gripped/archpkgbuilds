# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6core
pkgver=6.0.46
pkgrel=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtk6core"
license=('LGPL-3.0-or-later')
depends=('deepin-desktop-base' 'dtkcommon' 'dtk6log' 'gcc-libs' 'glibc' 'icu' 'lshw' 'qt6-base'
         'uchardet')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6core.git#tag=$pkgver")
sha512sums=('36e39c3aad4ac261b1b4761190ebfc68648c5de27aa2b856899afe629d5a31c5aab3885e9510e110843379d840c99e2c9ae10100b7da4f6daf34672479269a9f')

build() {
  cd dtk6core
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

check() {
  cd dtk6core
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6core
  DESTDIR="$pkgdir" ninja install
}
