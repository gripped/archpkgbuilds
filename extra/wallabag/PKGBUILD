# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Cedric Girard <cgirard [dot] archlinux [at] valinor [dot] fr>
# Contributor: Philipp Schmitt (philipp<at>schmitt<dot>co)

pkgname=wallabag
pkgver=2.6.12
pkgrel=1
pkgdesc='Self hostable application for saving web pages'
arch=('any')
url='https://www.wallabag.org/'
license=('MIT')
makedepends=('systemd')
depends=(
  'php'
  'php-gd'
  'php-tidy'
  'php-intl'
)
optdepends=(
  'php-fpm: to run in FastCGI process manager'
  'php-pgsql: to use a local PostgreSQL server'
  'php-sqlite: to use the SQLite database backend'
  'rabbitmq: to use a local RabbitMQ server for async import'
  'valkey: to use a local Redis server for async import'
)
options=('!strip')
backup=("etc/$pkgname/parameters.yml")
source=(
  "https://github.com/wallabag/wallabag/releases/download/$pkgver/wallabag-$pkgver.tar.gz"
  'sysusers.conf'
  'tmpfiles.conf'
)
sha512sums=('7f7a94d395cffd5bbad23450f6c1a809bc31a7d3120285f649e8ca4a1c0f9e85c7e8333d9943d80c9b01e228a7434b007f7b4e3ac647c92f96a66a717614f86d'
            '9fdd353baeb34937cc81b8849a97d073c52713703f15931e18a5a7204c2e3ba7263df64174f222487d481f05f3dceeb020dcd8257ee2288fc0b49e60dcd5876d'
            '5f7afac354f496d0f3507b771774fe192f124365c92b016f752b02bacccfe785f189374ed68a3bb9adc7024d4878490c279318642d3b1a6ccdc90939631f1d3e')
b2sums=('f2b87b04ea6fc2a8bf63d68dab8fb2be7f7d78813557847dbd576e2b104f8a656f7291e5f77d3816b77ac2910a65108e093d7bebd18f61b00a5d9d678448955b'
        '6004189a052c29ec71e3d41f0cafbd18070f794aeeb9e26d0ab48d5fbfd6a5ece8f6a7af5a4282bdb47a78a30150963c756db1f1da6f21f2ceb8c5df81915189'
        '3105875aae0131ae9728f96bde67917ad2906a08dd9dfe908dd193e48158cad600e4225523c088d6ce99aa8f9b5e779b3a6bb469b3f083acabe23004eac6ff12')

package() {
  # create required directories
  install -vd \
    "$pkgdir/usr/share" \
    "$pkgdir/etc/$pkgname" \
    "$pkgdir/var/lib/$pkgname"

  cp -r "$pkgname-$pkgver" "$pkgdir/usr/share/$pkgname"

  # setup configuration file
  mv -v "$pkgdir/usr/share/$pkgname/app/config/parameters.yml" "$pkgdir/etc/$pkgname"
  ln -vsf "/etc/$pkgname/parameters.yml" "$pkgdir/usr/share/$pkgname/app/config/parameters.yml"

  # setup data/var directories
  mv -v "$pkgdir/usr/share/$pkgname/"{data,var} "$pkgdir/var/lib/$pkgname"
  ln -vsf "/var/lib/$pkgname/"{data,var} "$pkgdir/usr/share/$pkgname/"

  # allow 'download images locally' option
  mv -v "$pkgdir/usr/share/$pkgname/web/assets/images" "$pkgdir/var/lib/$pkgname"
  ln -vsf "/var/lib/$pkgname/images" "$pkgdir/usr/share/$pkgname/web/assets/images"

  # systemd integration
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/COPYING.md"

  # remove unnecessary files/folders
  find "$pkgdir" -type f -name '.git*' -delete
  find "$pkgdir" -type d -name '.github' -exec rm -rf {} +
}
