# Maintainer: SÃ©bastien Luttringer
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=iperf
pkgver=2.2.1
pkgrel=2
pkgdesc='A tool to measure maximum TCP bandwidth'
arch=('x86_64')
url='https://sourceforge.net/projects/iperf2/'
license=('LicenseRef-IPerf')
depends=(
  'glibc'
  'libgcc'
  'libstdc++'
)
install=iperf.install
source=(
  "https://downloads.sourceforge.net/iperf2/iperf-$pkgver.tar.gz"
  'iperf-tcp.service'
  'iperf-udp.service'
)
sha256sums=('754ab0a7e28033dbea81308ef424bc7df4d6e2fe31b60cc536b61b51fefbd8fb'
            '5bfeaf4b7fc9bcc424cfc2257c6eefd21ed491bd140e9ae5562b270dcf84d794'
            'fec34a6f352619caaf4ed3bc23f75a3ffb6b3ab5c2947cd2c24c6be36851738c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-ipv6 \
    --enable-multicast \
    --enable-threads \
    --enable-fastsampling
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/iperf/LICENSE"
  popd
  # systemd
  install -Dm644 iperf-tcp.service "$pkgdir/usr/lib/systemd/system/iperf-tcp.service"
  install -Dm644 iperf-udp.service "$pkgdir/usr/lib/systemd/system/iperf-udp.service"
}

# vim:set ts=2 sw=2 et:
