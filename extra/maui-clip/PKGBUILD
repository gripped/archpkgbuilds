# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maui-clip
_pkgname=clip
pkgver=4.0.1
pkgrel=1
pkgdesc='Video player and video collection manager'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(ffmpeg
         gcc-libs
         glibc
         kcoreaddons
         ki18n
         mauikit
         mauikit-filebrowsing
         taglib
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-multimedia)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$_pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1b1b9d547b0d30d0447c0fb3806765bf4ad3cf68bd85dcfadf73934ccd01a918'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
