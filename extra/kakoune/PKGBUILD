# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Frank Lenormand <lenormf at gmail dot com>
pkgname=kakoune
pkgver=2024.05.18
pkgrel=3
pkgdesc="Multiple-selection, UNIX-flavored modal editor"
arch=("x86_64")
url="https://kakoune.org/"
license=("custom:unlicense")
depends=("gcc-libs")
makedepends=("git")
optdepends=(
    "aspell: spell check, correct text"
    "clang: error reporting and diagnostics, completion"
    "editorconfig-core-c: set formatting options project-wide"
    "git: display and cycle through hunks, blame lines, handle file status"
    "kakoune-lsp: Language Server Protocol (LSP) client"
    "tmux: split windows, spawn tabs"
    "xdotool: X11 window management"
    "xorg-xmessage: print detailed crash information in a separate window"
)
source=("https://github.com/mawww/kakoune/releases/download/v${pkgver}/kakoune-${pkgver}.tar.bz2"
        "custom-libexecdir.patch")
b2sums=('3c551d1ccdccbfcc64e52d5e4b32e37f2d4f68188ac3aedd651bfc0af9324c458992eff79d37c2f5ec58135b0923aecfb8d1308be3d6878fe15c8b44fcbd6472'
        '2c97785427851a01503bb99e2d0b725e4c11a42411669454f8bdfece00196d63c8dcd84e978840a2a83deed594b0c198e76d964fe8adcf21b6fe4eedb9c987e2')

prepare() {
    git -C ${pkgname}-${pkgver} apply ../custom-libexecdir.patch
}

build() {
    cd "${pkgname}-${pkgver}"
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make test
}

package() {
    cd "${pkgname}-${pkgver}"
    make install-strip DESTDIR="${pkgdir}" PREFIX=/usr libexecdir="${pkgdir}/usr/lib/kak"
    install -D UNLICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
