# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)

pkgbase=drumgizmo
pkgname=(drumgizmo drumgizmo-{lv2,standalone})
pkgver=0.9.20
pkgrel=3
pkgdesc="Multichannel, multilayered, cross-platform drum plugin and stand-alone application"
arch=(x86_64)
url="https://www.drumgizmo.org"
license=(GPL-3.0-or-later)
makedepends=(
  alsa-lib
  gcc-libs
  glibc
  jack
  libsmf
  libsndfile
  libxext
  libx11
  libxml2
  lv2
  zita-resampler
)
source=(
  $url/releases/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz{,.asc}
  $pkgbase-0.9.20-add-missing-cstdint-include.patch
)
sha512sums=('3444201961d74e7787bebe4006df6c257f6c1a57dd81d272b203a4a56ad609d3fa4e88224dfe4ea6a67f4db94b340666d018cb79fc376dd264398f98d66433e8'
            'SKIP'
            '05e5492ebef77b49ed3043b4e5976811e6b8f736be2c5cfde08e9519156419d6ef74ef0d834bd2b5fbc9742c985f79eb122261068cfeb411655bf451fbcdab47')
b2sums=('c92c41fd9cfd09a42d6500ec7d5f3bffd7dd646ef11b88212393f91a06d0af596c4f627be037c3eb0d19fd3ae11448e43d7a40ae8928dea7fbdfb61cad057cdb'
        'SKIP'
        '0850aa903cd3e80d22353d2b08e9af5eed3da60fd37ee839b51443e7de2cb4d7daecfa8215ef42a9db65d313597f7db22c48eeb325648036e9c73d85a30fbc91')
validpgpkeys=('F39C94D556CCB995B1AE540E9EB89445BF071867') # DrumGizmo <info@drumgizmo.org>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  patch -Np1 -d $pkgbase-$pkgver -i ../$pkgbase-0.9.20-add-missing-cstdint-include.patch
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --enable-lv2
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_drumgizmo() {
  depends=(
    drumgizmo-lv2=$pkgver
    drumgizmo-standalone=$pkgver
  )

  make DESTDIR="$pkgdir/" install -C $pkgbase-$pkgver
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgbase/"

  (
    cd "$pkgdir"
    _pick $pkgbase-lv2 usr/lib/lv2

    _pick $pkgbase-standalone usr/bin
    _pick $pkgbase-standalone usr/share
  )
}

package_drumgizmo-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    gcc-libs
    glibc
    libsndfile
    libxext
    libx11
    lv2-host
  )

  mv -v $pkgname/* "$pkgdir"
}

package_drumgizmo-standalone() {
  pkgdesc+=" - standalone"
  groups=(
    pro-audio
  )
  depends=(
    alsa-lib
    gcc-libs
    glibc
    jack
    libsmf
    libsndfile
  )

  mv -v $pkgname/* "$pkgdir"
}
