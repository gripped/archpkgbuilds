# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=uwsm
pkgver=0.23.2
pkgrel=1
pkgdesc='A standalone Wayland session manager'
url="https://github.com/Vladimir-csp/$pkgname"
arch=(any)
license=(MIT)
depends=(bash
         python
         python-pyxdg
         python-dbus)
optdepends=('bemenu: optional picker for uuctl support'
            'dmenu: optional picker for uuctl support'
            'fuzzel: optional picker for uuctl support'
            'libnewt: for "uwsm start select"'
            'libnotify: for feeback from "uswm app" commands or fmon service'
            'rofi-wayland: optional picker for uuctl support'
          # 'tofi: optional picker for uuctl support' # not packaged
          # 'walker: optional picker for uuctl support' # AUR only
            'wmenu: optional picker for uuctl support'
            'wofi: optional picker for uuctl support')
makedepends=(meson
             scdoc)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('b04809e5514f654fe3bde19200d437e418d004592b5f8314d05219c312a87b12')

build() {
	local meson_opts=(
		uuctl=enabled
		fumon=enabled
		uwsm-app=enabled
	)
	arch-meson "$_archive" build ${meson_opts[@]/#/-D }
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
