# Maintainer: David Runge <dvzrv@archlinux.org>

_name=micawber
pkgname=python-micawber
pkgver=0.5.6
pkgrel=1
pkgdesc="A small library for extracting rich content from urls"
arch=(any)
url="https://github.com/coleifer/micawber"
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-beautifulsoup4
  python-django
)
optdepends=(
  'python-beautifulsoup4: for text parser'
  'python-django: django support'
  'python-flask: flask support'
  'python-redis: redis caching support'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('a9f5eea1fd137a8088f796a54b8a318efac27ced05c98ef2dbb8059ddd1b48c92e1c82f72d9dd6ac4024a72f04fcf3940a6d82219439da67c22e263ea14aa716')
b2sums=('b89dca369c1f35fd56a3475ca091c4e2b5a848942cb6b4f81be7e30b15b2ad63bc281df92b16fc80ecc4db2e2975dd66eb8f18fe562023c141f00eb39d302461')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python runtests.py
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
