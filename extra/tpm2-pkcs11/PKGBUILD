# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=tpm2-pkcs11
pkgver=1.9.2
pkgrel=2
pkgdesc='PKCS#11 interface for Trusted Platform Module 2.0 hardware'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-pkcs11'
license=('BSD')
depends=('glibc' 'libyaml' 'openssl' 'python' 'python-bcrypt' 'python-cryptography' 'python-pyasn1'
         'python-pyasn1-modules' 'python-tpm2-pytss' 'python-yaml' 'sqlite' 'tpm2-tools' 'tpm2-tss'
         'libtss2-esys.so' 'libtss2-fapi.so' 'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-tctildr.so')
makedepends=('cmocka' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('expect' 'iproute2' 'java-hamcrest' 'junit' 'libp11' 'opensc' 'openssh'
              'python-python-pkcs11' 'swtpm' 'tpm2-abrmd' 'tpm2-openssl' 'wget' 'xxd')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        'p11kit-module-install.patch')
sha512sums=('b997c45041acecdcbd2d2cee2d41d4b641a6eb735e5cbb34be368c5aff9e4517b83e02ccebd4775d10e78d129b315611465b46a52139fb3feb11d19c2a7ed58a'
            'SKIP'
            'SKIP')
validpgpkeys=('5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1' # William Roberts (Bill Roberts) <william.c.roberts@intel.com>
              'D91A82DB310E8E07519C298A9877C26A3CD36409') # Bill Roberts (Bills Personal Dev Key) <bill.c.roberts@gmail.com>

prepare() {
	cd "$pkgname-$pkgver"
	
	# Fix p11-kit module installation when using --prefix
	# See: https://github.com/tpm2-software/tpm2-pkcs11/pull/818
	patch -Np1 -i "$srcdir/p11kit-module-install.patch"
	
	autoreconf -fiv

	# disable broken test: https://github.com/tpm2-software/tpm2-pkcs11/issues/836
	sed -e 's|test/integration/pkcs11-tool.sh||' -i Makefile*
}

build() {
	cd "$pkgname-$pkgver"
	export CLASSPATH="/usr/share/java/junit.jar:/usr/share/java/hamcrest-core.jar:$CLASSPATH"
	./configure --prefix=/usr --enable-unit $( ((CHECKFUNC)) && echo --enable-integration)
	make
	cd tools
	python -m build --wheel --no-isolation
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
	cd tools
	python -m installer --destdir="$pkgdir" dist/*.whl
}
