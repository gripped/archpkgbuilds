# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Dmitry Kharitonov <arch[at]nano-model[dot]com>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=ruby-ascii85
_pkgname=Ascii85
pkgver=2.0.1
pkgrel=4
pkgdesc="Methods for encoding/decoding Adobeâ€™s binary-to-text encoding of the same name"
arch=(any)
url="https://github.com/datawraith/ascii85gem"
license=(MIT)
depends=(ruby)
makedepends=(
  rubygems
  git
)
checkdepends=(
  ruby-bundler
  ruby-minitest
  ruby-rake
)
options=(!emptydirs)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('dd3b79e2e19674fb9aef083aacc6f2a3a305c9260bc14ed986e26e62780a6949')

prepare() {
  cd ascii85gem
  sed -i -E 's|~>|>=|g' "$_pkgname.gemspec"
}

build() {
  cd ascii85gem
  local gemdir="$(gem env gemdir)"

  gem build "$_pkgname.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "$_pkgname-$pkgver.gem"

  rm --force --recursive --verbose \
    "tmp_install/$gemdir/cache/" \
    "tmp_install/$gemdir/gems/$_pkgname-$pkgver/vendor/" \
    "tmp_install/$gemdir/doc/$_pkgname-$pkgver/ri/ext/"

  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
    -iname "*.o" -o \
    -iname "*.c" -o \
    -iname "*.so" -o \
    -iname "*.time" -o \
    -iname "gem.build_complete" -o \
    -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/$gemdir/extensions/" \
    -type f \
    \( \
    -iname "mkmf.log" -o \
    -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd ascii85gem
  GEM_HOME="tmp_install/$(gem env gemdir)" rake test
}

package() {
  cd ascii85gem
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
