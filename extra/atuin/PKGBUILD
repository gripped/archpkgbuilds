# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Ellie Huxtable <e@elm.sh>

pkgbase=atuin
pkgname=(atuin atuin-server)
pkgver=18.12.1
pkgrel=1
pkgdesc='Magical shell history'
arch=('x86_64')
url="https://github.com/atuinsh/$pkgbase"
license=(MIT)
depends=(gcc-libs
         glibc)
makedepends=(cargo)
options=(!lto)
_archive="$pkgbase-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('8643a7df1e366e9ad3134514b9bcaf4d6440accb13c64340ec9ec1923d58eb6e')

_srcenv () {
	cd "$_archive"

	# Use debug
	export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

	# Use LTO
	export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1
}

prepare() {
	_srcenv

	# Fully lock down user
	sed -i 's/^u /u! /' systemd/atuin-server.sysusers

	cargo fetch --locked --target host-tuple
	mkdir -p completions/
}

build() {
	_srcenv
	cargo build --release --frozen --all-features
	for sh in 'bash' 'fish' 'zsh'; do
		"target/release/$pkgbase" gen-completions -s "$sh" -o completions/
	done
}

check() {
	_srcenv
	cargo test --frozen --all-features --workspace --lib
}

package_atuin() {
	cd "$_archive"
	optdepends=('atuin-server: sync server'
	            'bash-preexec: bash integration')
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 "completions/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "completions/$pkgname.fish"
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "completions/_$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_atuin-server() {
	cd "$_archive"
	pkgdesc+=' - sync server'
	depends+=(atuin)
	optdepends=('postgresql: server database')
	mkdir -p "$pkgdir/etc/$pkgbase"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/lib/systemd/system/" "systemd/$pkgname.service"
	install -Dm0644 "systemd/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md "crates/$pkgname/server.toml"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
