# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=tabiew
pkgver=0.11.1
pkgrel=1
pkgdesc="A lightweight TUI to view and query CSV files"
arch=('x86_64')
url="https://github.com/shshemi/tabiew"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cargo')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('dc54cfd71317892812bfdda508f6f4cc7e688d00a1e896bc6e6bd462d69856bab22b17a32c87edc1e5401f8d2a3e5e6d77a6c4f1109130676b3654061ea0b661')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/tw" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
