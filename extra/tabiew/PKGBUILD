# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=tabiew
pkgver=0.6.2
pkgrel=1
pkgdesc="A lightweight TUI to view and query CSV files"
arch=('x86_64')
url="https://github.com/shshemi/tabiew"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cargo')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('97e5de8c7d613c849b6b128bd8e89c6f7e7d8aa7ae491265f30960908657dff86fccb52521108a5b378920ebbe75bf46859476da769a783950a614ada79ebafb')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/tw" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
