# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=hydra
pkgver=9.6
pkgrel=1
pkgdesc='Very fast network logon cracker which support many different services'
url='https://github.com/vanhauser-thc/thc-hydra'
arch=('x86_64')
license=('AGPL-3.0-or-later')
depends=(
  afpfs-ng
  apr
  bash
  glibc
  libfbclient
  libgcrypt
  libidn
  libmemcached
  mongo-c-driver
  openssl
  pcre2
  subversion
  zlib
)
makedepends=(
  freerdp
  libssh
  mariadb-libs
  ncurses
  postgresql-libs
)
source=(${url}/archive/v${pkgver}/thc-hydra-v${pkgver}.tar.gz)
sha512sums=('588f33b818dd475f19ab538644d54a3a08fa16fd2c119b8baf25ff9de8f15140d24438d2105e3cc1096fe5a6d12c51879992664409e97102c83ba1422b8b5e05')
b2sums=('c55401fe5c7a18789c131739951b2bb43442beb13e2f5ff9f0d83bd3d6a70272a7f97b444c290ec0a9ecc6959793b1aa70faabc1f6b28496ada19469230b0f89')

build() {
  cd thc-hydra-${pkgver}
  ./configure --fhs
  make
}

package() {
  depends+=(
    freerdp libfreerdp3.so libwinpr3.so
    libssh libssh.so
    mariadb-libs libmariadb.so
    ncurses libncursesw.so
    postgresql-libs libpq.so
  )

  cd thc-hydra-${pkgver}
  make DESTDIR="${pkgdir}" MANDIR=/share/man/man1 DATADIR=/share/hydra install
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -v "${pkgdir}"/usr/share/man/man1/xhydra.1
}

# vim: ts=2 sw=2 et:
