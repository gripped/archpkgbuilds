# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.187
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('0ebe9224de8a7520d59695b3906d0e195fda0f0a4976d373e1c03ec3951ce7d0'
            'd5fe1d30d9ec3cd0bdabafee60e766f9235af879eb2a272225e593aad40f0b27')
b2sums=('09a5b4e6513da32916253c62382f1d4e3b787fc97ba5c49935d1bec9cf8240a1427cd46e5cfa3b8418d4e74138a27e18f1a89eaaa9d756cee39e99f8b7b24180'
        '7fd2d9bbdb4eafe20c9a55267e86141a98fb7c12e9d07e60df6f1d89a937216f4d79c17d4785cb96548cc5527de2bd6b1d66b946a4161e7b244b764dd3970864')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
