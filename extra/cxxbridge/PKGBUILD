# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.176
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('4f8ba4417a7a2ee75eacf999eae4331b77dd9457d3d0226322264af0e0bda4b7'
            '9ab13f80a86178a21eb6bc8ccde7fbe091c440269fb838d53d6ab6a2a3010609')
b2sums=('205bfbe3ce6e85d823ca3f25f9b7b8d599454e6cdeeb59dbee7d887c3bb59045fe803b881b5138c23d4bc80e619a05a7267e178363ec1ad6ef5a96d6920ca946'
        'fec101e0be55396a6fa53ae4df8de87e9e2158f7215539edcac38bf0049fb91a26142da6e07dd1a5730c1c7865d059cc4fc9ae60641edd5fd5f0a854238643d6')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
