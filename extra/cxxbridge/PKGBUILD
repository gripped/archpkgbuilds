# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.128
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('7aa61d128d75cbfb4713e0c0803efb4da6c88180327f9e2f095641b55a5d0e06'
            'fc5237b55b6e25e4a590002fb9b5f9c7f2a4ab6895a594da9768c247046ce6ba')
b2sums=('2fc7ee783a6999110eea91f31904063ddec3111ef5ad15e9ab3a324d5f8d36b62d28aa7137afff1dc1f84e7ac9eb0e0a2d97afb685db79a5ecd5596405df8b91'
        'db461d1022ee13d53f5ae6e6825c9c1849feadcc9d19095ce82680486f9af8191236904542d36605b1ef9d1615f7a3e3c2e3b9d79cfd873e40e5ab8bb42dae29')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
