# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.158
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('58a3c7b28033caeeb8a5e1f74c5124d63b1bfc8a67a5740c475c6d12d1bc7f4b'
            '130655a8012a8303363be2013d020942f052e3a65ca616ef58812d101501a507')
b2sums=('b858aedfb650d8d014cf248992f63041c433878285a859c23ccae5e5cfe6184719b6d0450069d392379b3c61e5b938cc48d4f52fc8a507177b95a729472358e7'
        '169e077f2a2fe0dcdecbf8120c79c82aaa17bfa9940556f7e208b724ded77554a34f904fa3c41ebe1e32786672cd1c41fb18eb8dacdcb91b20abb81506e3e4a0')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
