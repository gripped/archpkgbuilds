# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.141
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('8db6d9c82f3ce4a0ce0cc273c1fbd7f35dfa8f159bab7e29e187d601f0a049ab'
            '259f2eebc3720aa7852921ad9cdccadf4bac0db6d74ea62c195bca56aeeb6baa')
b2sums=('5f5c06a31c62d718237018b7935d0d3204d05019aa84792c0d3998a41559692587c60690e20fd35651477b1b6d71954bae13b44e22ba3560e9511b600a80b5eb'
        '61ba3bbce7152357f923efac04f5c598ec277fb7959a6073a076e6719db16d5c1892c6fd23d16ba939016200b364a6e2b6dabb595806efa85121213bf79688f2')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
