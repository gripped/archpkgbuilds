# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=timescaledb-tune
pkgver=0.18.1
pkgrel=1
pkgdesc='A tool for tuning TimescaleDB for better performance'
arch=('x86_64')
url='https://github.com/timescale/timescaledb-tune'
license=('Apache-2.0')
depends=('glibc' 'postgresql')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0e5b6c0b341639fc59823a1c30db6799f51eabf54b169a5c8c12950269053ad12f07ff0208dda59d021107274f0c2d4c4b660fc0bcebb3c8b2be8b73bf99b3db')
b2sums=('e47b910c509fdab3bbfd9bac1cdd27845e52eb968ad85806f6738f1de2a76c7d0270a2163818e8083906a88046c21d90c944fe2e9f3b210e0d75cb4f26bdadd2')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -buildmode=pie \
    -trimpath \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags '${LDFLAGS}'" \
    -o build \
    ./cmd/...
}

package() {
  install -Dm755 -t "$pkgdir/usr/bin" "$pkgname/build/$pkgname"
}
