# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgname=empy
pkgname=python-empy
pkgver=4.2
pkgrel=2
pkgdesc='Powerful and robust templating system for Python'
arch=(any)
url='http://www.alcyone.com/software/empy/'
license=(BSD-3-Clause)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("http://www.alcyone.com/software/empy/$_pkgname-$pkgver.tar.gz")
b2sums=(6460ae0f249d17a8467b6ac51268fec40c4cc6a1606fb36dc56650556598f0c57fd4a815132dcf265c5e51a20f987580b8b337dedff245eb24424072105597ac)

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.md
}
