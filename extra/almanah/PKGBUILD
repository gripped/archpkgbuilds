# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=almanah
pkgver=0.12.3
pkgrel=8
pkgdesc='Small GTK application to allow you to keep a diary of your life'
arch=(x86_64)
url='https://wiki.gnome.org/Apps/Almanah_Diary'
license=(GPL-3.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  evolution-data-server
  gcr-4
  glib2
  glibc
  gpgme
  gtk3
  gtksourceview4
  gtkspell3
  hicolor-icon-theme
  libcryptui
  libical
  pango
  sqlite
)
makedepends=(
  appstream-glib
  git
  glib2-devel
  meson
)
source=(
  "git+https://gitlab.gnome.org/GNOME/almanah.git?signed#tag=$pkgver"
  almanah-sourceview-include.patch
  almanah-setlocale.patch
)
b2sums=(
  1a7f0c3c706efa6043ea7c2710a0df1868aa9376793bb23facca366cb9956689bc56d3e057bc28b03d4ae45188083de8aee7a39ec83eb21e38cbf17854e8bf5c
  cc5f079b573297c067be1fce20e3ec7984b5d3f02e7ffa74752875b9731680dea161360a1aa2edb97f3d320314ec5b272a845a183d8a5de62af4bb5233600d6e
  0fccdd30f60c34166d2c45b1c670b891e9911117b1ca4c6af490a727d18945ff093121d9607c9ad4e940bf6688bd1b6cefa878da4ac86094d67c99d9e6af082f
)
validpgpkeys=(923B7025EE03C1C59F42684CF0942E894B2EAFA0) # Philip Withnall

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/GNOME/almanah/-/merge_requests/13
  git cherry-pick -n 8c42a67695621d1e30cec933a04e633e6030bbaf

  # https://gitlab.gnome.org/GNOME/almanah/-/merge_requests/14
  git cherry-pick -n cd44b476f4ffbf37c5d5f5b996ecd711db925576

  # https://gitlab.gnome.org/GNOME/almanah/-/merge_requests/15
  git cherry-pick -n 0ba7f05cba7feaf2ae2c220596aead5dfc676675

  # https://gitlab.gnome.org/GNOME/almanah/-/merge_requests/23
  git apply -3 ../almanah-sourceview-include.patch

  # https://gitlab.gnome.org/GNOME/almanah/-/merge_requests/24
  git apply -3 ../almanah-setlocale.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
