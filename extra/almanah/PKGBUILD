# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=almanah
pkgver=0.12.4
pkgrel=3
pkgdesc='Small GTK application to allow you to keep a diary of your life'
arch=(x86_64)
url='https://wiki.gnome.org/Apps/Almanah_Diary'
license=(GPL-3.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  evolution-data-server
  gcc-libs
  gcr-4
  glib2
  glibc
  gpgme
  gtk3
  gtksourceview4
  gtkspell3
  hicolor-icon-theme
  libcryptui
  libical
  pango
  sqlite
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
)
# Jan Tojnar uses SSH signatures which makepkg doesn't understand
source=(
  "git+https://gitlab.gnome.org/GNOME/almanah.git#tag=$pkgver"
  almanah-no-appmenu.patch
  almanah-command-line.patch
  almanah-dbus-activatable.patch
)
b2sums=(
  b2903482386f19664534bb0d2827a60c60d398478a6ccd858107330625fb1b8ddf54e0479ca49fc6669f588ff6fd79bd2fba837d2ed887fa28b650ce76950a40
  959f039b4c3231313eefe2c47a4e838e6eb3fc1b960e9c0e497bebfdf107d8d1fedec4b83ad5ee8c353565a1a832a056a466a4c0c0733d15f0a75a39da060f9e
  47809176067eca273c587b46a147ede09e72051eef5597e2663b228caa6780ccd6cccc8f03733bcda1340e1962371e3ea55bd27487f1d47da6447085c630c515
  3ef13f2795c5e8690ebbe9fb1d1de3b52d9f42d8d2e4a4642ac5c93d614977149124231cf2a198591ee3357cce3d360929a6e2d23d8c575aaf0b9d5a353da333
)
validpgpkeys=(923B7025EE03C1C59F42684CF0942E894B2EAFA0) # Philip Withnall

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/GNOME/almanah/-/merge_requests/38
  git apply -3 ../almanah-no-appmenu.patch

  # https://gitlab.gnome.org/GNOME/almanah/-/merge_requests/39
  git apply -3 ../almanah-command-line.patch

  # https://gitlab.gnome.org/GNOME/almanah/-/merge_requests/40
  git apply -3 ../almanah-dbus-activatable.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
