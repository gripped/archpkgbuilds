# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=topology-toolkit
pkgver=1.3.0
pkgrel=2
pkgdesc="An open-source library and software collection for topological data analysis and visualization"
arch=(x86_64)
url="https://github.com/topology-tool-kit/ttk"
license=(BSD-3-Clause-Attribution)
depends=(
  embree3
  gcc-libs
  glibc
  graphviz
  openmpi libmpi.so
  paraview
  python
  python-numpy
  python-scikit-learn
  qhull
  sqlite libsqlite3.so
  vtk
  zfp
  zlib libz.so
)
makedepends=(
  anari-sdk
  boost
  cli11
  cmake
  eigen
  fast_float
  libharu
  ninja
  nlohmann-json
  openvr
  openxr
  qt6-declarative
  utf8cpp
)
source=(
  $pkgname-$pkgver::$url/archive/refs/tags/$pkgver.tar.gz
  fix-cmake-config.patch
  fix-wrlexporter.patch
)
b2sums=('f7b4f70baff93608f1255ab9978c569825a8f592004ef552ca049e327d6f81253d267a0704deb3278227f3598d5e181cc31a320474d9f3118f1ba5c04643fbba'
        '22e0df09eaf623d65cf0c7ca9401b19e3521879046dc7e6b02305ea1d616ebaa1df99341f987290715784f352dabacfd76b41fc20e02836c9a54921c7ca88974'
        '96dd256e1cd245425e403de4f2c9cb763c79f1fde4b8ee2683c26050727881e9c3909295f5785c6d7f6d32feb6dd5fd1c30b41de35d9b43063600b07d3247392')

prepare() {
  cd ttk-$pkgver
  # fix finding Eigen 5.0
  patch -p1 < ../fix-cmake-config.patch

  # ttkWRLExporter: Update for new vtkVRMLExporter API
  # https://github.com/topology-tool-kit/ttk/commit/6a7ed5024da5dc9fec8cada7dda476aa70870476
  patch -p1 < ../fix-wrlexporter.patch
}

build() {
  # Directory where the ParaView plugin will be installed - this must be done with an environment variable
  # https://github.com/topology-tool-kit/ttk/blob/531ccfb154f5057b9c0b95b9c6d4818028309bf7/CMakeLists.txt#L152C16-L158
  export PV_PLUGIN_PATH=/usr/lib/paraview/plugins

  local cmake_options=(
    -B build
    -S ttk-$pkgver
    -G Ninja
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DTTK_SCRIPTS_PATH=/usr/share/ttk/scripts
    -DTTK_ENABLE_CPU_OPTIMIZATION=OFF
    -DTTK_ENABLE_64BIT_IDS=ON
    -DTTK_ENABLE_MPI=ON
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 ttk-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  # remove duplicate install path
  rm -frv "$pkgdir"/usr/bin/plugins/
}
