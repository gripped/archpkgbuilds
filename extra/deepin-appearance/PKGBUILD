# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-appearance
pkgver=1.1.55
pkgrel=1
pkgdesc='Application resource management and control services for the dde desktop environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-appearance"
license=('GPL-3.0-or-later')
# startdde: org.deepin.dde.XSettings1
depends=('qt6-base' 'dtk6core' 'kconfig' 'kwindowsystem' 'kglobalaccel' 'libxcursor' 'libxfixes'
         'libx11' 'gtk3' 'libxcb' 'xcb-util-cursor' 'fontconfig' 'openssl' 'glib2' 'dtk6gui'
         'glibc' 'gcc-libs' 'startdde' 'deepin-service-manager')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-appearance.git#tag=$pkgver")
sha512sums=('fdfd3553451adb459452b9866d054d7c2f807c71277f01c138a41134550628198b2741533ffa19ed99a043b95dc8f9c24a7da64de58be6ab93d0860a221c39f9')

build() {
  cd dde-appearance
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-appearance
  DESTDIR="$pkgdir" ninja install
}
