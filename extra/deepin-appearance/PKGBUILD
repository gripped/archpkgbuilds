# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-appearance
pkgver=1.1.46
pkgrel=1
pkgdesc='Application resource management and control services for the dde desktop environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-appearance"
license=('GPL3')
# startdde: org.deepin.dde.XSettings1
depends=('qt5-base' 'dtkcore' 'kconfig5' 'kwindowsystem5' 'kglobalaccel5' 'libxcursor' 'libxfixes'
         'libx11' 'gtk3' 'libxcb' 'xcb-util-cursor' 'fontconfig' 'openssl' 'gsettings-qt' 'glib2'
         'dtkgui' 'glibc' 'gcc-libs' 'startdde' 'deepin-service-manager')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-appearance.git#tag=$pkgver")
sha512sums=('bb6a0c64f1952aa61dd26bad1e1f4f36b4ed9727d95f17e3b3796cb3b63bf88005727d2a07bc59e70b2cbe631af5250e7d19157cabe99fa92459a384d658af17')

build() {
  cd dde-appearance
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-appearance
  DESTDIR="$pkgdir" ninja install
}
