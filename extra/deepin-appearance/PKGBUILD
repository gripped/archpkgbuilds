# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-appearance
pkgver=1.1.61
pkgrel=1
pkgdesc='Application resource management and control services for the dde desktop environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-appearance"
license=('GPL-3.0-or-later')
# deepin-daemon: org.deepin.dde.XSettings1
depends=('qt6-base' 'dtk6core' 'kconfig' 'kwindowsystem' 'kglobalaccel' 'libxcursor' 'libxfixes'
         'libx11' 'gtk3' 'libxcb' 'xcb-util-cursor' 'fontconfig' 'openssl' 'glib2' 'dtk6gui'
         'glibc' 'gcc-libs' 'deepin-daemon' 'deepin-service-manager')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-appearance.git#tag=$pkgver")
sha512sums=('474b14d3a0539b428c585e08ed563440b46c2f67e378e98662aa6476eb99409312eebd670262838445dbfd176c51be59e27ecf5454df664229c7d31b018f73e5')

build() {
  cd dde-appearance
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-appearance
  DESTDIR="$pkgdir" ninja install
}
