# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-appearance
pkgver=1.1.44
pkgrel=1
pkgdesc='Application resource management and control services for the dde desktop environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-appearance"
license=('GPL3')
# startdde: org.deepin.dde.XSettings1
depends=('qt5-base' 'dtkcore' 'kconfig5' 'kwindowsystem5' 'kglobalaccel5' 'libxcursor' 'libxfixes'
         'libx11' 'gtk3' 'libxcb' 'xcb-util-cursor' 'fontconfig' 'openssl' 'gsettings-qt' 'glib2'
         'dtkgui' 'glibc' 'gcc-libs' 'startdde' 'deepin-service-manager')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-appearance.git#tag=$pkgver")
sha512sums=('6af97469a1e7c83163f48beab57e2b9633293fa22dbc419960e8bf3e946800fb90294ecddb21f7247f4eafbf0a1e4f9c977877bbd5405141f91bec8264159626')

build() {
  cd dde-appearance
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-appearance
  DESTDIR="$pkgdir" ninja install
}
