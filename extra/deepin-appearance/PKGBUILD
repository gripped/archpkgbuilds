# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-appearance
pkgver=1.1.8
pkgrel=1
pkgdesc='Application resource management and control services for the dde desktop environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-appearance"
license=('GPL3')
# startdde: org.deepin.dde.XSettings1
depends=('qt5-base' 'dtkcore' 'kconfig5' 'kwindowsystem5' 'kglobalaccel5' 'libxcursor' 'libxfixes'
         'libx11' 'gtk3' 'libxcb' 'xcb-util-cursor' 'fontconfig' 'openssl' 'gsettings-qt' 'glib2'
         'dtkgui' 'glibc' 'gcc-libs' 'startdde')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-appearance/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('908c5b62d013e9fcfe40ae6cd2f81cb8f49b0b78427b296e35de325c164c89f9cf94fa3473edb3045dad2bdbb944e6148bc54745cdd5ca7977f494c825b35a72')

build() {
  cd dde-appearance-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-appearance-$pkgver
  DESTDIR="$pkgdir" ninja install
}
