# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-videos
pkgver=8.0.2
pkgrel=1
pkgdesc='The Pantheon Video Player'
arch=(x86_64)
url='https://github.com/elementary/videos'
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgee
  libgranite-7.so
)
makedepends=(
  cmake
  git
  intltool
  meson
  vala
)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
)
_tag=ad7906835fe12839e0bc04cfbcc091aacb3a8b8e
source=(pantheon-videos::git+https://github.com/elementary/videos.git#tag=${_tag})
sha256sums=('adc2f22e19b678031efa17ffc8431550b6f2d231a341ce2f43a6f58dd069c2ec')

pkgver() {
  cd pantheon-videos
  git describe --tags
}

build() {
  arch-meson pantheon-videos build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
