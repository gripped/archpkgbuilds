# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-videos
pkgver=8.0.1
pkgrel=1
pkgdesc='The Pantheon Video Player'
arch=(x86_64)
url='https://github.com/elementary/videos'
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgee
  libgranite-7.so
)
makedepends=(
  cmake
  git
  intltool
  meson
  vala
)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
)
_tag=95c92a4effb6a52a9ee16af17c39d3791219d9bf
source=(pantheon-videos::git+https://github.com/elementary/videos.git#tag=${_tag})
sha256sums=('572f95db2b87c116acc4920c34a7fbbb5874b101c388f09603646182f3c92656')

pkgver() {
  cd pantheon-videos
  git describe --tags
}

build() {
  arch-meson pantheon-videos build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
