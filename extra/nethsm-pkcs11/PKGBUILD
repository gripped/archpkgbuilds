# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nethsm-pkcs11
pkgver=1.7.0
pkgrel=1
pkgdesc="PKCS#11 driver for NetHSM"
arch=(x86_64)
url="https://github.com/Nitrokey/nethsm-pkcs11"
license=(Apache-2.0)
depends=(glibc)
makedepends=(rust)
options=(!lto)  # NOTE: as ring is used we can not use LTO
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('c5380aac27d9d988ddae212a2738f929b5f67e57cc3a2b051dc3338fb1e62ede32a358ec0a72b38156f8cc7c3624cff55c413b8ea34e8fc67b8a1259a4431993')
b2sums=('13e4d2c3fafa514548ee2f328b8988d6077e674c8598fd2770393fd8986b4aaade3bb0e29bf757b7103a9d2eba294589e5c55ad02ef73f78054eeaca9bb5c11a')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  # using all features pulls in pkcs11-full-tests which are integration tests
  cargo test --frozen --features=default
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/lib${pkgname/-/_}.so -t "$pkgdir/usr/lib/"
  install -vDm 644 {{README,features}.md,p11nethsm*.conf} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vdm 700 "$pkgdir/etc/nitrokey/"
}
