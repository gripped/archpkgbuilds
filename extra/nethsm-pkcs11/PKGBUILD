# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nethsm-pkcs11
pkgver=1.4.0
pkgrel=1
pkgdesc="PKCS#11 driver for NetHSM"
arch=(x86_64)
url="https://github.com/Nitrokey/nethsm-pkcs11"
license=(Apache-2.0)
depends=(glibc)
makedepends=(rust)
options=(!lto)  # NOTE: as ring is used we can not use LTO
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('175bb3fd6d27258c853e36b3166c568b39829bacc1026c8cfdac5f809d4f6049f4a452ede8a90b0aedf0e5a8dddc093ec91147fdcf446ab8d62f2d2140da8b61')
b2sums=('a1d2687592c7b6d9addd2ae94aaab79a39c7c340d374792754917a0bad4307b0cedc92d06dda965f2a7c47874da8693d0623ffd696464dfaa88111ee09683125')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/lib${pkgname/-/_}.so -t "$pkgdir/usr/lib/"
  install -vDm 644 {{README,features}.md,p11nethsm*.conf} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vdm 700 "$pkgdir/etc/nitrokey/"
}
