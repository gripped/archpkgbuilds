# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nethsm-pkcs11
pkgver=2.0.0
pkgrel=1
pkgdesc="PKCS#11 driver for NetHSM"
arch=(x86_64)
url="https://github.com/Nitrokey/nethsm-pkcs11"
license=(Apache-2.0)
depends=(glibc)
makedepends=(rust)
options=(!lto)  # NOTE: as ring is used we can not use LTO
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('2daea3b2aec40304bdf7bc3037862ac9b21169056734601af0e468c3f3a262061fc8ef18a0ad1f37d095ec5c5e380ed38bed91d06307f7d8c2d197f1479b89af')
b2sums=('97bf28108b73fe41bd3186da2858aee3c94353c2dc4b1c95774d55e56f1e624de333af301e01386d1576b82bc89b59d5e99fc4092605518b976a831e42e1318a')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  # using all features pulls in pkcs11-full-tests which are integration tests
  cargo test --frozen --features=default
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/lib${pkgname/-/_}.so -t "$pkgdir/usr/lib/"
  install -vDm 644 {{README,features}.md,p11nethsm*.conf} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vdm 700 "$pkgdir/etc/nitrokey/"
}
