# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=psalm
pkgver=6.2.0
pkgrel=1
pkgdesc='A static analysis tool for finding errors in PHP applications'
url="https://psalm.dev/"
arch=('any')
license=('MIT')
depends=('php')
makedepends=('composer' 'git')
source=("git+https://github.com/vimeo/psalm.git#tag=${pkgver}"
        "composer.lock")
b2sums=('8417726eed4e7358d7d4e1ac9076114c8499f4e0a09ae853399ce0db626be8cdf144b4c4846d6c69379ba804d7851d4993acc4d11fda9596f95fbe98a5c2fdf9'
        '79a12c26e08d6dd8146d56c9ca4c36cc64ee27df2aacd32c52aea51fe2386ff4eec58907759d10d08c12a1b9570fc6f1f791b6319543632eb3eb1da7be64e442')

prepare() {
  cd ${pkgname}
  cp ../composer.lock .
  composer install
  composer bin box install
  php bin/improve_class_alias.php
  php -r 'require "vendor/autoload.php"; Psalm\Internal\VersionUtils::dump();'
}

updlockfiles() {
  cd ${pkgname}
  rm -f composer.lock
  composer update --no-install
  cp composer.lock "${outdir}/"
}

build() {
  cd ${pkgname}
  vendor/bin/box compile
}

check() {
  rm -rf testing/
  mkdir -p testing/src
  cd testing/

  composer init -n --name test/test
  composer install
  ../${pkgname}/build/psalm.phar --init
  ../${pkgname}/build/psalm.phar

  cat > src/index.php <<EOF
<?php echo(phpversion());
EOF
  ../${pkgname}/build/psalm.phar

  cat > src/index.php <<EOF
<?php echo(this_is_invalid_to_provoke_an_error());
EOF
  if ../${pkgname}/build/psalm.phar; then
    echo 'did not detect invalid php as invalid'
    exit 1
  fi
}

package() {
  cd ${pkgname}
  install -Dm 755 ./build/psalm.phar "${pkgdir}/usr/bin/psalm"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
