# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=psalm
pkgver=6.9.4
pkgrel=1
pkgdesc='A static analysis tool for finding errors in PHP applications'
url="https://psalm.dev/"
arch=('any')
license=('MIT')
depends=('php')
makedepends=('composer' 'git')
source=("git+https://github.com/vimeo/psalm.git#tag=${pkgver}"
        "composer.lock")
b2sums=('881e782998bd446272332083a8d1eb8e71b75fb9447813288221572875dde95edd5fcf388d597d25b9d55422d6b2a21f836d9f4a7fc0790af6d1a0d2d7869f54'
        'd028b2395b46127dcf6a6e861670164e7b33cd8b1e57ccfc64d0f0bf712516c99b65938dccc26a1f0ecd4b33b0601e080e37fa63d073f5a9ec874f54d323b2fd')

prepare() {
  cd ${pkgname}
  cp ../composer.lock .
  composer install
  composer bin box install
  php bin/ci/improve_class_alias.php
  php -r 'require "vendor/autoload.php"; Psalm\Internal\VersionUtils::dump();'
}

updlockfiles() {
  cd ${pkgname}
  rm -f composer.lock
  composer update --no-install
  cp composer.lock "${outdir}/"
}

build() {
  cd ${pkgname}
  vendor/bin/box compile
}

check() {
  rm -rf testing/
  mkdir -p testing/src
  cd testing/

  composer init -n --name test/test
  composer update
  composer install
  ../${pkgname}/build/psalm.phar --init
  ../${pkgname}/build/psalm.phar

  cat > src/index.php <<EOF
<?php echo(phpversion());
EOF
  ../${pkgname}/build/psalm.phar

  cat > src/index.php <<EOF
<?php echo(this_is_invalid_to_provoke_an_error());
EOF
  if ../${pkgname}/build/psalm.phar; then
    echo 'did not detect invalid php as invalid'
    exit 1
  fi
}

package() {
  cd ${pkgname}
  install -Dm 755 ./build/psalm.phar "${pkgdir}/usr/bin/psalm"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
