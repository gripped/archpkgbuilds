# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=psalm
pkgver=6.10.3
pkgrel=1
pkgdesc='A static analysis tool for finding errors in PHP applications'
url="https://psalm.dev/"
arch=('any')
license=('MIT')
depends=('php')
makedepends=('composer' 'git')
source=("git+https://github.com/vimeo/psalm.git#tag=${pkgver}"
        "composer.lock")
b2sums=('290aff015e6da9c084291e289e06ca44ccb80ef872042b42c8ac5791787e0be558903fb151de19bdb03aab77a9256b0cfc41970cedcaa004c2383c4bbbde28c7'
        '7e93401296d8133d14e8e3f4166afedd7cc4733cf6fa517654c63463b134c550ca36880de1553ec0ec87894e24406fb37a357032da234727b6b217be0ab5c6e8')

prepare() {
  cd ${pkgname}
  cp ../composer.lock .
  composer install
  composer bin box install
  php bin/ci/improve_class_alias.php
  php -r 'require "vendor/autoload.php"; Psalm\Internal\VersionUtils::dump();'
}

updlockfiles() {
  cd ${pkgname}
  rm -f composer.lock
  composer update --no-install
  cp composer.lock "${outdir}/"
}

build() {
  cd ${pkgname}
  vendor/bin/box compile
}

check() {
  rm -rf testing/
  mkdir -p testing/src
  cd testing/

  composer init -n --name test/test
  composer update
  composer install
  ../${pkgname}/build/psalm.phar --init
  ../${pkgname}/build/psalm.phar

  cat > src/index.php <<EOF
<?php echo(phpversion());
EOF
  ../${pkgname}/build/psalm.phar

  cat > src/index.php <<EOF
<?php echo(this_is_invalid_to_provoke_an_error());
EOF
  if ../${pkgname}/build/psalm.phar; then
    echo 'did not detect invalid php as invalid'
    exit 1
  fi
}

package() {
  cd ${pkgname}
  install -Dm 755 ./build/psalm.phar "${pkgdir}/usr/bin/psalm"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
