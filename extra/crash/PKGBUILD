# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Contributor: Mathieu Pasquet <mathieui@mathieui.net>
# Contributor: Qu Wenruo <wqu@suse.com>

pkgname=crash
pkgver=9.0.1
pkgrel=1
pkgdesc='Linux kernel crashdump analysis tool'
url='https://crash-utility.github.io/'
arch=(x86_64)
license=(GPL-3.0-only)
depends=(
  boost # Boost is needed for the GDB with extra compression support
  gcc-libs
  glibc
  gmp
  guile
  libelf
  lzo
  mpfr
  ncurses
  xxhash
  xz
  zstd
)
makedepends=(wget)
optdepends=('binutils: offline vmcore loading functionality')
source=("https://github.com/crash-utility/crash/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('73dcdc633ec75ea2fc5329a1c3760407cceb9171428f4d0b885874f943a16797')

build() {
  cd $pkgname-$pkgver
  # Add lzo and zstd support to match makedumpfile's capacity
  make LDFLAGS="$LDFLAGS -llzo2 -lzstd" lzo zstd
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" crash
  install -vDm644 -t "$pkgdir/usr/share/man/man8" crash.8
}
