# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-session
pkgver=1.0.0.alpha.3
pkgrel=3
pkgdesc='Session manager for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-session
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  bash
  cosmic-applets
  cosmic-app-library
  cosmic-bg
  cosmic-comp
  cosmic-files
  cosmic-greeter
  cosmic-icon-theme
  cosmic-idle
  cosmic-launcher
  cosmic-notifications
  cosmic-osd
  cosmic-panel
  cosmic-randr
  cosmic-screenshot
  cosmic-settings-daemon
  cosmic-settings
  cosmic-workspaces
  gcc-libs
  glibc
  otf-fira-mono
  otf-fira-sans
  switcheroo-control
  vulkan-driver
  xdg-desktop-portal-cosmic
  xorg-xwayland
)
makedepends=(
  cargo
  git
  just
  lld
)
optdepends=(
  'cosmic-files: COSMIC file manager'
  'cosmic-store: COSMIC store'
  'cosmic-terminal: COSMIC terminal'
  'cosmic-text-editor: COSMIC text editor'
  'cosmic-wallpapers: COSMIC wallpapers'
)
_tag=5a0df6afb6c9c5e9c29e33a756bf7e600c3d0264
source=(
  git+https://github.com/pop-os/cosmic-session.git#tag=${_tag}
  cosmic-session-lto.patch
)
b2sums=('e576c29b7a575f2c178eb8a966537ed4aaa4ee595f0728479719a4a959f9392c6d8e346b2d5514cbb75b1c9bf9b72cb4fd0d0b4911742d5f5744ee58704460d7'
        '9265d8218817b047b9d645b78abd1ad8e9cc30ae2c84a041058c2457b631bb9d4b4068144c019536cadfea6aa301ec72166a4bf4029305c1f6e67f05bed99882')

prepare() {
  cd cosmic-session
  patch -Np1 -i ../cosmic-session-lto.patch
  cargo fetch --locked
  sed -i 's/libexec/lib/g' Justfile src/main.rs
}

pkgver() {
  cd cosmic-session
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-session
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  cargo build --release --frozen
}

package() {
  cd cosmic-session
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
