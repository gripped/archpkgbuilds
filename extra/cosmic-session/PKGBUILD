# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-session
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='Session manager for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-session
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  bash
  cosmic-applets
  cosmic-app-library
  cosmic-bg
  cosmic-comp
  cosmic-greeter
  cosmic-icon-theme
  cosmic-launcher
  cosmic-notifications
  cosmic-osd
  cosmic-panel
  cosmic-randr
  cosmic-screenshot
  cosmic-settings-daemon
  cosmic-settings
  cosmic-workspaces
  gcc-libs
  glibc
  otf-fira-mono
  otf-fira-sans
  switcheroo-control
  vulkan-driver
  xdg-desktop-portal-cosmic
  xorg-xwayland
)
makedepends=(
  cargo
  git
  just
  lld
)
optdepends=(
  'cosmic-files: COSMIC file manager'
  'cosmic-store: COSMIC store'
  'cosmic-terminal: COSMIC terminal'
  'cosmic-text-editor: COSMIC text editor'
  'cosmic-wallpapers: COSMIC wallpapers'
)
_tag=d06f94a8a01c47b01e0b490c98e0f6d7242ceadd
source=(
  git+https://github.com/pop-os/cosmic-session.git#tag=${_tag}
  cosmic-session-lto.patch
)
b2sums=('a3075cd805dacceafa0aebfa24f7e3d3621713f7a2a7674133165582459ecef20c7ba0d2c618ed27e8eff8bbd126c3072f7da8d7192322992169330014f7ed3e'
        '9265d8218817b047b9d645b78abd1ad8e9cc30ae2c84a041058c2457b631bb9d4b4068144c019536cadfea6aa301ec72166a4bf4029305c1f6e67f05bed99882')

prepare() {
  cd cosmic-session
  patch -Np1 -i ../cosmic-session-lto.patch
  cargo fetch --locked
  sed -i 's/libexec/lib/g' Justfile src/main.rs
}

pkgver() {
  cd cosmic-session
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-session
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  cargo build --release --frozen
}

package() {
  cd cosmic-session
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
