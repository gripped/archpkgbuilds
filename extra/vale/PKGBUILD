# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Rafael Fontenelle <rafaelff@gnome.org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Achilleas Pipinellis <axilleas at archlinux dot gr>

pkgname=vale
pkgver=3.11.2
pkgrel=1
pkgdesc="A markup-aware linter for prose built with speed and extensibility in mind"
url="https://vale.sh"
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('go')
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/errata-ai/vale/archive/v${pkgver}.tar.gz")
sha256sums=('12795c72a5628ebc22d46a33b878519c5ba18e6d665271893bfa24cb1f864f73')

prepare(){
	cd "${pkgname}-${pkgver}"
	mkdir -p build/
}

build() {
	cd "${pkgname}-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOPATH="${srcdir}"
	export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
	go build -ldflags="-compressdwarf=false -linkmode external -s -w -X main.version=${pkgver}" -o build ./cmd/...
}

check() {
	cd "${pkgname}-${pkgver}"
	# The "TestSymlinkFixture" test needs to run `vale`
	export PATH="${PWD}/build:${PATH}"
	go test ./...
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm 755 "build/${pkgname}"  "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 LICENSE   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
