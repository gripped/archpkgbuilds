# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=ocicl
pkgver=2.16.0
pkgrel=1
pkgdesc='An OCI-based ASDF system distribution and management tool for Common Lisp'
arch=(x86_64)
url='https://github.com/ocicl/ocicl'
license=(MIT)
depends=(glibc zstd sbcl)
makedepends=(git libfixposix)
options=(!debug !strip)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('eaa31705ce373abd099647480dcb923b28d3e2d3eb671d7f18c64f987ab9c6660c3fa0cf1aac1f27f0efe183342170bb44d543ded6c0d81a406a7dede107e169')
b2sums=('16d3ac2eb254c35c210b23f2eb0d10ce7f2edc707abbd32ca3e9177d523d6c6a1d97f6eddaff703a2fa8d40e35816d512ccc0285a2ae6d962c80c8da2e1f5e7b')

build() {
  cd "$pkgname"

  sbcl --load setup.lisp --eval "(sb-ext:quit)"

  # generate licenses for vendored libraries
  ./ocicl collect-licenses > VENDORED-LICENSES.txt
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" ocicl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE \
    VENDORED-LICENSES.txt
}
