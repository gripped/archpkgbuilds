# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=ocicl
pkgver=2.16.4
pkgrel=1
pkgdesc='An OCI-based ASDF system distribution and management tool for Common Lisp'
arch=(x86_64)
url='https://github.com/ocicl/ocicl'
license=(MIT)
depends=(glibc zstd sbcl)
makedepends=(git libfixposix)
options=(!debug !strip)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('91662e4574f0669a2bbca3ef43e88ce3937965bb250d1bd4d5064e6171a9ac4f43ab5703a7e593d85c1cba7439a4b49de59d1872dcbce3e444bf3c36e6f6ebfd')
b2sums=('831bc606bfdbf2cce8f8a18aa4d3e6b92e05e094e0b8692f2516328684e12c8e36201fe54c2a7bc32d99a8d0bee270d9af914d6387b4063706610c60568f2892')

build() {
  cd "$pkgname"

  sbcl --load setup.lisp --eval "(sb-ext:quit)"

  # generate licenses for vendored libraries
  ./ocicl collect-licenses > VENDORED-LICENSES.txt
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" ocicl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE \
    VENDORED-LICENSES.txt
}
