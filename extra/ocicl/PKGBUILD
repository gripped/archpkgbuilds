# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=ocicl
pkgver=2.16.5
pkgrel=1
pkgdesc='An OCI-based ASDF system distribution and management tool for Common Lisp'
arch=(x86_64)
url='https://github.com/ocicl/ocicl'
license=(MIT)
depends=(glibc zstd sbcl)
makedepends=(git libfixposix)
options=(!debug !strip)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('85130919e9f93bac0e55f7afffce98b439ec04434e6c30848257fd2576b94fb33330052816c1aa88868143e673df64b09c9ccb22f891552a2f034013bf5d9946')
b2sums=('eab3f8bce4799baa82a1cc8301940a3e5b2d46ed36267b62f91b8dc7d203512cda5371f2bc7950746261060bd032132d6a8ba43d066463919001dadc6b5dd9de')

build() {
  cd "$pkgname"

  sbcl --load setup.lisp --eval "(sb-ext:quit)"

  # generate licenses for vendored libraries
  ./ocicl collect-licenses > VENDORED-LICENSES.txt
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" ocicl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE \
    VENDORED-LICENSES.txt
}
