# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-pyphen
_name="${pkgname#python-}"
pkgver=0.17.2
pkgrel=2
pkgdesc="Pure Python module to hyphenate text"
arch=(any)
url="https://pyphen.org/"
_url=https://github.com/Kozea/Pyphen
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
  MPL-1.1
)
depends=(python)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$_url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('870deec4cd459060f4a8c6e61248b7813a9820cd98cf7d631461d675860f116485ed9ed3404a3ef0dafb6e6f44c162ade2791928ec5f368556cc6d2ff3c89432')
b2sums=('ab283a7336ecaf090f49fe2975375e9e0be39baea2176495e46d3619e265ec404d1c4890eb606a93491e4c636ffc2ac68bb524899e42c889ca50dcdb94f56240')

build() {
  cd ${_name^}-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${_name^}-$pkgver
  pytest -vv
}

package() {
  cd ${_name^}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
