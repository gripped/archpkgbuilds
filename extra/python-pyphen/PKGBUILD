# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-pyphen
_name="${pkgname#python-}"
pkgver=0.17.0
pkgrel=3
pkgdesc="Pure Python module to hyphenate text"
arch=(any)
url="https://pyphen.org/"
_url=https://github.com/Kozea/Pyphen
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
  MPL-1.1
)
depends=(python)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$_url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('3b3bc4a2ec81d5642c93b31c27b0138f154f3a6c9002da6eb440ab953116e1f5269cf81f8daef08fb684d64d952cfe86f00b84ea79efdab8163cdf25f6ec8a11')
b2sums=('ead6eae356ba16b3a7112b2a7f8a6937d017e03178574cf726e084c315a3b12eec541e0cfd923c85c0fc97730994b98b1b2815378c8f76b4287613b70b0e0b9f')

build() {
  cd ${_name^}-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${_name^}-$pkgver
  pytest -vv
}

package() {
  cd ${_name^}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
