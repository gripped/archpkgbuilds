# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: mortdeus <mortdeus@gocos2d.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

pkgname=htmldoc
pkgver=1.9.20
pkgrel=2
pkgdesc='HTML Conversion Software'
arch=('x86_64')
url='https://www.msweet.org/htmldoc/'
license=('GPL-2.0-only')
depends=('fltk' 'libcups' 'libjpeg-turbo' 'libpng' 'libxpm' 'zlib'
         'hicolor-icon-theme')
source=("https://github.com/michaelrsweet/htmldoc/releases/download/v${pkgver}/htmldoc-${pkgver}-source.tar.gz"{,.sig})
sha256sums=('f458f3476acf73b791e3c0c71497c42d094b5d6bf96a4572d755b8c9e180609a'
            'SKIP')
validpgpkeys=('9086C3CDC66C3F563CF8F405BE67C75EC81F3244') # Michael R Sweet

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix='/usr' --with-gui
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
