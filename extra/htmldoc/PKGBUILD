# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: mortdeus <mortdeus@gocos2d.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

pkgname=htmldoc
pkgver=1.9.21
pkgrel=1
pkgdesc='HTML Conversion Software'
arch=('x86_64')
url='https://www.msweet.org/htmldoc/'
license=('GPL-2.0-only')
depends=(
    'fltk'
    'gcc-libs'
    'glibc'
    'hicolor-icon-theme'
    'libcups'
    'libjpeg-turbo'
    'libpng'
    'libxpm'
    'zlib')
source=("https://github.com/michaelrsweet/htmldoc/releases/download/v${pkgver}/htmldoc-${pkgver}-source.tar.gz"{,.sig})
sha256sums=('50fe7fc88285a48f36ecb5dc9869c3a07b0adf2ace1d0d71b15afdaf695238ff'
            'SKIP')
validpgpkeys=('9086C3CDC66C3F563CF8F405BE67C75EC81F3244') # Michael R Sweet

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix='/usr' --with-gui
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
