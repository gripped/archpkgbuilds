# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=3.1.0
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('68e4e65a8af3061ca77159dc9d2db61642bbb117f385e2bbe0a5651d9f1ad526')
b2sums=('40993eb166f96c2ba6175cfc11906642f7f5adc32dd08f673b809ca5a56372735708ed7762d07ec13e53a6cf11beb1d0bb7613cc81b5081ed4f899bd4d18bd4b')

prepare() {
  cd ${pkgname}-${pkgver}
  go mod download
}

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .

  # generate shell completions
  mkdir -p completions
  ./vultr-cli completion bash > completions/vultr-cli.bash
  ./vultr-cli completion zsh > completions/vultr-cli.zsh
  ./vultr-cli completion fish > completions/vultr-cli.fish
}

check() {
  cd ${pkgname}-${pkgver}
  ./vultr-cli --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 completions/vultr-cli.bash "${pkgdir}/usr/share/bash-completion/completions/vultr-cli"
  install -Dm 644 completions/vultr-cli.zsh "${pkgdir}/usr/share/zsh/site-functions/_vultr-cli"
  install -Dm 644 completions/vultr-cli.fish "${pkgdir}/usr/share/fish/vendor_completions.d/vultr-cli.fish"
}

# vim: ts=2 sw=2 et:
