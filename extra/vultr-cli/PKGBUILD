# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=3.2.0
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f979ed556bf55c0cbd4cd9aa6afd8a4c7a8a6a34414bbaddc039b294d25dfac3')
b2sums=('31f1aaefa088f031cb8be44e27cf4e69110a5a1007f9648ab3bcb6f940ce69835f8cd9ee8ccf61ca5a043b2fac1088bec227ad2e40b375d43c8a6a2cde394d7a')

prepare() {
  cd ${pkgname}-${pkgver}
  go mod download
}

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .

  # generate shell completions
  mkdir -p completions
  ./vultr-cli completion bash > completions/vultr-cli.bash
  ./vultr-cli completion zsh > completions/vultr-cli.zsh
  ./vultr-cli completion fish > completions/vultr-cli.fish
}

check() {
  cd ${pkgname}-${pkgver}
  ./vultr-cli --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 completions/vultr-cli.bash "${pkgdir}/usr/share/bash-completion/completions/vultr-cli"
  install -Dm 644 completions/vultr-cli.zsh "${pkgdir}/usr/share/zsh/site-functions/_vultr-cli"
  install -Dm 644 completions/vultr-cli.fish "${pkgdir}/usr/share/fish/vendor_completions.d/vultr-cli.fish"
}

# vim: ts=2 sw=2 et:
