# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=3.7.0
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a90039d703688dae876fa3851274a5893333986e609a04b2129ed32704039a91')
b2sums=('0158b20dbd48afde45bd66967204112a72fbf4444fc39066157997efa948f1eb0c4f0d9cb3aaea68b6e014da60a4bc024d574be43c05ce421118100d0c59491f')

prepare() {
  cd ${pkgname}-${pkgver}
  go mod download
}

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .

  # generate shell completions
  mkdir -p completions
  ./vultr-cli completion bash > completions/vultr-cli.bash
  ./vultr-cli completion zsh > completions/vultr-cli.zsh
  ./vultr-cli completion fish > completions/vultr-cli.fish
}

check() {
  cd ${pkgname}-${pkgver}
  ./vultr-cli --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 completions/vultr-cli.bash "${pkgdir}/usr/share/bash-completion/completions/vultr-cli"
  install -Dm 644 completions/vultr-cli.zsh "${pkgdir}/usr/share/zsh/site-functions/_vultr-cli"
  install -Dm 644 completions/vultr-cli.fish "${pkgdir}/usr/share/fish/vendor_completions.d/vultr-cli.fish"
}

# vim: ts=2 sw=2 et:
