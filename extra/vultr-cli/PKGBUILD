# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=3.0.1
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ae258a579564e857ce20d9073875747ff057a3cdfb5effa0ef24443286d2fd0a')
b2sums=('99c5cfcc6fa9389e5f6bb088ffab7f2be417449e5aa9f58bcf71c7f7d08c0dd5ce566b9e3d5ac21729dc4a058450814ac5b7d33c0b1327be8b5c60fcfc621567')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

check() {
  cd ${pkgname}-${pkgver}
  ./vultr-cli --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  ./vultr-cli completion bash > "${pkgdir}/usr/share/bash-completion/completions/vultr-cli"
  ./vultr-cli completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_vultr-cli"
  ./vultr-cli completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/vultr-cli.fish"
}

# vim: ts=2 sw=2 et:
