# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=3.4.0
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('966161efc0f65c6f836503dfba9a3e2240ad6e54c76d83817fc99532808cf049')
b2sums=('de3a197424025a596462f065f9cbe564eb5f555599441a8bb7788a3fab823630affae7dd0e89687f507f068af9444426c2a41b76221969052cd23a020623555d')

prepare() {
  cd ${pkgname}-${pkgver}
  go mod download
}

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .

  # generate shell completions
  mkdir -p completions
  ./vultr-cli completion bash > completions/vultr-cli.bash
  ./vultr-cli completion zsh > completions/vultr-cli.zsh
  ./vultr-cli completion fish > completions/vultr-cli.fish
}

check() {
  cd ${pkgname}-${pkgver}
  ./vultr-cli --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 completions/vultr-cli.bash "${pkgdir}/usr/share/bash-completion/completions/vultr-cli"
  install -Dm 644 completions/vultr-cli.zsh "${pkgdir}/usr/share/zsh/site-functions/_vultr-cli"
  install -Dm 644 completions/vultr-cli.fish "${pkgdir}/usr/share/fish/vendor_completions.d/vultr-cli.fish"
}

# vim: ts=2 sw=2 et:
