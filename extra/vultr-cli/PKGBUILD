# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=3.5.0
pkgrel=2
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('da6caf170ef61627022d4558e704d9f269a716aa583820ce335d93d2c70d9117')
b2sums=('cf364234bfe98613514a04aa8d5776e00852796a6c70475763afccd191d3f6e73ac923557dfe126564369484107b3419601e182f56347bb4a7936d0129c82af5')

prepare() {
  cd ${pkgname}-${pkgver}
  go mod download
}

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .

  # generate shell completions
  mkdir -p completions
  ./vultr-cli completion bash > completions/vultr-cli.bash
  ./vultr-cli completion zsh > completions/vultr-cli.zsh
  ./vultr-cli completion fish > completions/vultr-cli.fish
}

check() {
  cd ${pkgname}-${pkgver}
  ./vultr-cli --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  install -Dm 644 completions/vultr-cli.bash "${pkgdir}/usr/share/bash-completion/completions/vultr-cli"
  install -Dm 644 completions/vultr-cli.zsh "${pkgdir}/usr/share/zsh/site-functions/_vultr-cli"
  install -Dm 644 completions/vultr-cli.fish "${pkgdir}/usr/share/fish/vendor_completions.d/vultr-cli.fish"
}

# vim: ts=2 sw=2 et:
