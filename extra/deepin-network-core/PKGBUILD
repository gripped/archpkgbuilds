# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.28
pkgrel=1
pkgdesc='DDE network library and plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('GPL3')
depends=(dtkcore dtkgui dtkwidget deepin-control-center glibc gcc-libs gsettings-qt
         networkmanager-qt5 qt5-base)
makedepends=(git cmake deepin-session-shell gtest ninja qt5-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-network-core.git#tag=$pkgver)
sha512sums=('ec7ff37bf47d975afd78793505a9a99c03860f581e61f482ab611b03f57d53da6a7d0363edc465d9c3beff5e98c686851099b31f9a868dfdf86b245805902b19')

prepare() {
  cd dde-network-core
  # dde-tray-loader replaces this
  git cherry-pick -n a51c4020114596a7b41ac378c24584d794bcb395
}

build() {
  cd dde-network-core
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core
  DESTDIR="$pkgdir" ninja install
}
