# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.38
pkgrel=1
pkgdesc='DDE network library and plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('LGPL-3.0-or-later')
depends=(deepin-control-center glibc gcc-libs dtk6core qt6-base qt6-declarative networkmanager-qt)
makedepends=(git cmake deepin-session-shell gtest ninja qt6-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-network-core.git#tag=$pkgver)
sha512sums=('be80e5b001c9fdb7e945422223e9a3ed358ec70322b51f32a33ee7c02046faca3b03c6a62b8c407073beb4719aa5a73add68b025c5f16ce8ecca085679c3b9cb')

prepare() {
  cd dde-network-core
  # remove qt5 components
  git cherry-pick -n 36db3cdb78af14bde08c4a797a890b8e276794c6
  # disable tests
  git cherry-pick -n e1841f0bf240402ad49c1c595c39cc51a1b9197c
  # dss_example & dss-network-plugin needs qt6 version of dde-session-shell
  sed -i '/dss-network-plugin/d;/dss_example/d' CMakeLists.txt
}

build() {
  cd dde-network-core
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core
  DESTDIR="$pkgdir" ninja install
}
