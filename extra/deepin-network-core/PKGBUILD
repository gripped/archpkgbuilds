# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.35
pkgrel=1
pkgdesc='DDE network library and plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('LGPL-3.0-or-later')
depends=(deepin-control-center glibc gcc-libs dtk6core qt6-base qt6-declarative networkmanager-qt)
makedepends=(git cmake deepin-session-shell gtest ninja qt6-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-network-core.git#tag=$pkgver)
sha512sums=('d2ffa6716239a5309be1cc15d1c4349052566591aaa22b41ac25d07b36941b25db295e533422c8bf020bd6e50b864bebb06dd102abb2987f65182dcc46abb96c')

prepare() {
  cd dde-network-core
  # remove qt5 components
  git cherry-pick -n 36db3cdb78af14bde08c4a797a890b8e276794c6
  # disable tests
  git cherry-pick -n e1841f0bf240402ad49c1c595c39cc51a1b9197c
}

build() {
  cd dde-network-core
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core
  DESTDIR="$pkgdir" ninja install
}
