# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.26
pkgrel=1
pkgdesc='DDE network library and plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('GPL3')
depends=(dtkcore dtkgui dtkwidget deepin-control-center glibc gcc-libs gsettings-qt
         networkmanager-qt5 qt5-base)
makedepends=(git cmake deepin-session-shell gtest ninja qt5-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-network-core.git#tag=$pkgver)
sha512sums=('1521c58f1daec415e3778e1b1ab4ebf103fff197075436401f042ea06e09fa4d379cccbf523ead414afacde5566072c6720ea1cc192c7b6c4573c9abce82b85f')

prepare() {
  cd dde-network-core
  # dde-tray-loader replaces this
  git cherry-pick -n a51c4020114596a7b41ac378c24584d794bcb395
}

build() {
  cd dde-network-core
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core
  DESTDIR="$pkgdir" ninja install
}
