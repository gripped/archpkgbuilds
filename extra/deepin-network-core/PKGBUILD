# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.19
pkgrel=1
pkgdesc='DDE network library and plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('GPL3')
depends=(dtkcore dtkgui dtkwidget deepin-control-center glibc gcc-libs gsettings-qt
         networkmanager-qt5 qt5-base)
makedepends=(git cmake deepin-session-shell gtest ninja qt5-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-network-core.git#tag=$pkgver)
sha512sums=('9a161595e63255626d53d5d03dc8cbc552d4c10fad7def33df5e69d499639ab577bf2b385dc4e0dc0e55eea2244417d97781192221cb8bf15e9d1f02f45529cd')

prepare() {
  cd dde-network-core
  # dde-tray-loader replaces this
  git cherry-pick -n a51c4020114596a7b41ac378c24584d794bcb395
}

build() {
  cd dde-network-core
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core
  DESTDIR="$pkgdir" ninja install
}
