# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.39
pkgrel=1
pkgdesc='DDE network library and plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('LGPL-3.0-or-later')
depends=(deepin-control-center glibc gcc-libs dtk6core qt6-base qt6-declarative networkmanager-qt)
makedepends=(git cmake deepin-session-shell gtest ninja qt6-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-network-core.git#tag=$pkgver)
sha512sums=('a7a55bebc6715e6d1b01581deb747e33a3c2c0cf782c7c8cfef15e3c1e42d52f054a329040eb801ea8ea666085eb04291f31b16f278a1a355b1e008deb13b85d')

prepare() {
  cd dde-network-core
  # remove qt5 components
  git cherry-pick -n 36db3cdb78af14bde08c4a797a890b8e276794c6
  # disable tests
  git cherry-pick -n e1841f0bf240402ad49c1c595c39cc51a1b9197c
  # dss_example & dss-network-plugin needs qt6 version of dde-session-shell
  sed -i '/dss-network-plugin/d;/dss_example/d' CMakeLists.txt
}

build() {
  cd dde-network-core
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core
  DESTDIR="$pkgdir" ninja install
}
