# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.18
pkgrel=1
pkgdesc='DDE network library and plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('GPL3')
depends=(dtkcore dtkgui dtkwidget deepin-control-center glibc gcc-libs gsettings-qt
         networkmanager-qt5 qt5-base)
makedepends=(git cmake deepin-session-shell gtest ninja qt5-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-network-core.git#tag=$pkgver)
sha512sums=('565f57f2ea1c3f4b392a526596eadd16337ee6bf8b2a285274afd530c18bf759b0b71bb20829aa1889c58d2cf796c67645aaa080161c052bd543bad0742d35ef')

prepare() {
  cd dde-network-core
  # dde-tray-loader replaces this
  git cherry-pick -n a51c4020114596a7b41ac378c24584d794bcb395
}

build() {
  cd dde-network-core
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core
  DESTDIR="$pkgdir" ninja install
}
