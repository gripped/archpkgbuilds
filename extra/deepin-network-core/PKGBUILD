# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.20
pkgrel=1
pkgdesc='DDE network library and plugins'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('GPL3')
depends=(dtkcore dtkgui dtkwidget deepin-control-center glibc gcc-libs gsettings-qt
         networkmanager-qt5 qt5-base)
makedepends=(git cmake deepin-session-shell gtest ninja qt5-tools)
groups=(deepin)
source=(git+https://github.com/linuxdeepin/dde-network-core.git#tag=$pkgver)
sha512sums=('9c6104e77a5b329f2410d595edab80c47b53371f69c80c916749fc44cf4a90ae55dac55cd139355ef6be25da6f69a0ff117073b75240294be3802cd0ea0065ad')

prepare() {
  cd dde-network-core
  # dde-tray-loader replaces this
  git cherry-pick -n a51c4020114596a7b41ac378c24584d794bcb395
}

build() {
  cd dde-network-core
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core
  DESTDIR="$pkgdir" ninja install
}
