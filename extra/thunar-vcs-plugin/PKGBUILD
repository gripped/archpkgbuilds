# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=thunar-vcs-plugin
pkgver=0.2.0
pkgrel=2
pkgdesc="SVN and GIT integration for Thunar"
arch=('x86_64')
url="https://docs.xfce.org/xfce/thunar/thunar-vcs-plugin"
license=('GPL-2.0-or-later')
depends=('thunar' 'git' 'subversion')
makedepends=('intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/thunar-plugins/thunar-vcs-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('86511d7433eade7f4f9867b37bd57d7d2133f05d678b48b78f71a40dc45014d7')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
