# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-network-manager
pkgver=0.13.4
pkgrel=2
pkgdesc="The proton-vpn-network-manager component provides the necessary functionality for other components to interact with NetworkManager."
arch=(any)
url="https://github.com/ProtonVPN/python-proton-vpn-network-manager"
license=(GPL-3.0-or-later)
depends=(
  networkmanager-openvpn
  libnm
  python
  python-jinja
  python-proton-core
  python-proton-vpn-api-core
  python-proton-vpn-local-agent
  python-cairo
  python-gobject
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("git+https://github.com/ProtonVPN/${pkgname}.git#tag=v${pkgver}")
optdepends=("network-manager-applet: password prompt on Gnome and other GTK DEs"
            "plasma-nm: password prompt on KDE Plasma")
sha256sums=('4effdc61dedaf5a33663aaba8f7c85dd0ec6fbcdb070727930481e83665d4d73')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
