# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-network-manager
pkgver=0.12.10
pkgrel=1
pkgdesc="The proton-vpn-network-manager component provides the necessary functionality for other components to interact with NetworkManager."
arch=(any)
url="https://github.com/ProtonVPN/python-proton-vpn-network-manager"
license=(GPL-3.0-or-later)
depends=(
  networkmanager-openvpn
  libnm
  python
  python-proton-core
  python-proton-vpn-api-core
  python-cairo
  python-gobject
)
makedepends=(
  git
  python-setuptools
)
source=("git+https://github.com/ProtonVPN/${pkgname}.git#tag=v${pkgver}")
optdepends=("network-manager-applet: password prompt on Gnome and other GTK DEs"
            "plasma-nm: password prompt on KDE Plasma")
sha256sums=('eb76796e3687778614ea25ed29148e3675109fffdc41a7f79f47a2895bc8f674')

build() {
  cd "$pkgname"
  python setup.py build
}

package() {
  cd "$pkgname"
  python setup.py install --root="$pkgdir" --optimize=1
}
