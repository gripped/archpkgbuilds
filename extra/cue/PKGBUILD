# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=cue
pkgver=0.11.2
pkgrel=1
pkgdesc="Validate and define text-based and dynamic configuration"
arch=('x86_64')
url="https://cuelang.org/"
license=('Apache-2.0')
makedepends=('go')
checkdepends=('git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cue-lang/cue/archive/v${pkgver}.tar.gz")
sha512sums=('2cc94185c51561f1f45352aa5a50e60397bd6eb7396029e8e2e1fa36cc660d81c021c2e46270bb9fd53461d922894bcdd2aa056b5e2eaa70cf25a5ad188ba014')

prepare(){
  cd "${pkgname}-${pkgver}"
  mkdir -p build/
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build -ldflags "-X cuelang.org/go/cmd/cue/cmd.version=${pkgver}" ./cmd/...
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dsm755 build/cue "${pkgdir}/usr/bin/cue"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./build/cue completion bash  > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  ./build/cue completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
