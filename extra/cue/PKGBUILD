# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=cue
pkgver=0.15.1
pkgrel=1
pkgdesc="Validate and define text-based and dynamic configuration"
arch=('x86_64')
url="https://github.com/cue-lang/cue"
license=('Apache-2.0')
makedepends=('go')
checkdepends=('git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cue-lang/cue/archive/v${pkgver}.tar.gz")
sha512sums=('1859f0098ec6754d164e4b0473d70627ab03018a6a99d1b6707b5b2267f2efafc63c6e857da99cd8cba5aa66e1e81f64c89a6a7df8b5a4bc4a3eb46ac28a3e10')

prepare(){
  cd "${pkgname}-${pkgver}"
  mkdir -p build/
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build -ldflags "-X cuelang.org/go/cmd/cue/cmd.version=${pkgver}" ./cmd/...
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dsm755 build/cue "${pkgdir}/usr/bin/cue"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./build/cue completion bash  > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  ./build/cue completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
