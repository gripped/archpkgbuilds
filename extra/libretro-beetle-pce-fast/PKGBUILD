# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-beetle-pce-fast
pkgver=1278
pkgrel=1
pkgdesc='NEC PC Engine/CD core'
arch=(x86_64)
url=https://github.com/libretro/beetle-pce-fast-libretro
license=(GPL2)
groups=(libretro)
depends=(
  gcc-libs
  glibc
  libretro-core-info
  zlib
)
makedepends=(git)
_commit=71675959c7df0f4235a90e6eeb271bf7c1bde019
source=(libretro-beetle-pce-fast::git+https://github.com/libretro/beetle-pce-fast-libretro.git#commit=${_commit})
sha256sums=('bdca9efd234342fb0b88622825273dfbe2690661bef860fc03555e653d49336a')

pkgver() {
  cd libretro-beetle-pce-fast

  git rev-list --count HEAD
}

build() {
  make \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-pce-fast
}

package() {
  install -Dm 644 libretro-beetle-pce-fast/mednafen_pce_fast_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
