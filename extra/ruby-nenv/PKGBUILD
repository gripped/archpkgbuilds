# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-nenv
pkgver=0.3.0
pkgrel=8
pkgdesc="Convenient wrapper for Ruby's ENV"
arch=(any)
url='https://github.com/e2/nenv'
license=(MIT)
depends=(ruby)
makedepends=(git)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=("git+${url}#tag=v${pkgver}")
sha512sums=('30e8ae3f0da58e086341c9e2c1febc849d26490f1d1b4b912d655208319a317ca37ceec6906f9cf315720be6cfb174ff574d1b0eb33e7119cbca84a6b7ee6d03')
b2sums=('b5c509e52404870205bb40c2e1a3df825315ecd6c3f1749acfe1b78a1da560ee43788bc43d8ce35876c390b45c37a0e64c3658552e1d2a64d8e8ff7d4a65910d')

prepare() {
  cd nenv

  sed -i '/[cC]overalls/d' spec/spec_helper.rb
  sed -i '/rubocop/d' Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd nenv
  gem build nenv.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    nenv-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd nenv
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd nenv
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
