# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-pgspecial
pkgver=2.2.1
pkgrel=2
pkgdesc='Meta-commands handler for PostgreSQL'
arch=(any)
url='https://github.com/dbcli/pgspecial'
license=(BSD-3-Clause)
depends=(
  python
  python-click
  python-sqlparse
  python-psycopg
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(python-pytest python-configobj)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('b8b3d8b38b60ba04a88ac1062d8bd870b1c096da46f7f1e29961f1b3bfdf0fa075636838a4ac461a1d303b136f6e2d991f57ef6ee58f2b66e5328108f0113b38')
b2sums=('35e43fe7e823d9b9028950cb7e9c04aa6eee0443bd36b9329d2156cba04b4a4bac88507ad6ffdd5b294d8248fd65788f23fad5e77c984f84aea7917ec41e382e')

build() {
  cd "$pkgname"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd):$PYTHONPATH" pytest --ignore tests/test_specials.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" License.txt
}
