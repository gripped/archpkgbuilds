# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=tifffile
pkgname=python-$_name
pkgver=2026.2.20
pkgrel=1
pkgdesc="Read and write TIFF files"
arch=(any)
url="https://github.com/cgohlke/tifffile"
license=(BSD-3-Clause)
depends=(
  python
  python-numpy
)
makedepends=(
  python-build
  python-installer
  python-setuptools
)
checkdepends=(
  python-fsspec
  python-lxml
  python-pytest
)
optdepends=(
  'python-fsspec: for opening ReferenceFileSystem files'
  'python-imagecodecs: for encoding or decoding LZW, JPEG, etc. compressed segments'
  'python-lxml: for validating and printing XML'
  'python-matplotlib: for plotting'
  'python-zarr: for opening Zarr stores'
  'tk: for Tkinter file open dialog'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('61cdadbdc0cdd9a274c49d6db56ab04e033f6fc6c7ead77bcdbcbeacee10dd13e68723a8d1b8fb5831e38c712a94cce3f0c3100690b87fa102edb18fc9e8944e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # see https://github.com/cgohlke/tifffile/blob/master/tests/test_tifffile.py for the available env variables
  SKIP_LARGE=1 SKIP_HTTP=1 \
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 README.rst -t "$pkgdir"/usr/share/doc/$pkgname/
}
