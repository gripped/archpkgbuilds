# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-ui
pkgver=1.32.1
pkgrel=207
pkgdesc="Curses-style terminal interface for the hledger accounting system"
url="http://hledger.org"
license=('GPL-3.0-or-later')
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-ansi-terminal' 'haskell-async'
         'haskell-cmdargs' 'haskell-data-default' 'haskell-doclayout' 'haskell-extra'
         'haskell-fsnotify' 'haskell-microlens' 'haskell-microlens-platform' 'haskell-megaparsec'
         'haskell-safe' 'haskell-split' 'haskell-text-zipper' 'haskell-vector' 'haskell-brick'
         'haskell-vty')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        vty6.patch)
sha512sums=('f28fa45e6db0c6f2853470aabd72eb2436fa577c74eac74dbcc3284a8f0058e75741f702b46a1f283c5e5510467ac9895fc4d63ff1c27a03da27ad9dba7bb71f'
            '1bd0d4c1ab204b85cb67f9b1271d809771ce9e5ce2f9c6d41fca31b7dad8cc8a7761852cb49f1066aa8a7bb3e12cef2b630e63e57da95b82cf1a9ca5d8766c07')

prepare() {
    cd $pkgname-$pkgver
    patch -p2 -i ../vty6.patch
    uusi -u megaparsec
}

build() {
    cd $pkgname-$pkgver
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir=$pkgname --enable-tests \
            -fthreaded
    runhaskell Setup build $MAKEFLAGS
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

    install -Dm644 hledger-ui.1 -t "$pkgdir"/usr/share/man/man1/
}
