# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-ui
pkgver=1.51.2
pkgrel=4
pkgdesc="Curses-style terminal interface for the hledger accounting system"
url="http://hledger.org"
license=('GPL-3.0-or-later')
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-ansi-terminal' 'haskell-async'
         'haskell-cmdargs' 'haskell-data-default' 'haskell-doclayout' 'haskell-extra'
         'haskell-fsnotify' 'haskell-githash' 'haskell-microlens' 'haskell-microlens-platform'
         'haskell-megaparsec' 'haskell-safe' 'haskell-split' 'haskell-text-zipper' 'haskell-vector'
         'haskell-brick' 'haskell-vty' 'haskell-vty-crossplatform')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('381741f92e1842249717b7322216f2cdef82d5b4ab6418ba33aa2041166991fe8a95ac5a5d5e5cd7653e3d9ceca76b40da7e8209b3c4a30cc04d8bfec8b87e31')

build() {
    cd $pkgname-$pkgver
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir=$pkgname --enable-tests \
            -fthreaded
    runhaskell Setup build $MAKEFLAGS
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

    install -Dm644 hledger-ui.1 -t "$pkgdir"/usr/share/man/man1/
}
