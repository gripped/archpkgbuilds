# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=morphosis
pkgver=49.0
pkgrel=2
pkgdesc='Document conversion app using Pandoc'
arch=(any)
url='https://gitlab.gnome.org/World/morphosis'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pandoc-cli
  python
  python-gobject
  python-weasyprint
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://gitlab.gnome.org/World/morphosis.git#tag=$pkgver")
b2sums=(c9f8dbeb487a23999d4a45c13eb296612a837e2489956d8daba1fb2cf112802ab100765e1a9b2b2a8d922a8e763b5bde0f4e3fa82bd4ea2031bb05195c999b5a)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
