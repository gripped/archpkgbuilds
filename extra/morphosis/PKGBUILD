# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=morphosis
pkgver=48.1
pkgrel=1
pkgdesc='Document conversion app using Pandoc and WebKitGTK'
arch=(any)
url='https://gitlab.gnome.org/World/morphosis'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pandoc-cli
  python
  python-gobject
  python-weasyprint
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://gitlab.gnome.org/World/morphosis.git#tag=$pkgver")
b2sums=(4514c5d7459e9dfc54ca48c33b6cff1d58b8869ab4d7a513d554f3e85be9838ee8a354bd559831e5e76c20ee160769c0d1653ed712f7a1c14da941b8c52f4f1a)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
