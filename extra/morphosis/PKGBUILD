# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=morphosis
pkgver=48.2
pkgrel=1
pkgdesc='Document conversion app using Pandoc'
arch=(any)
url='https://gitlab.gnome.org/World/morphosis'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pandoc-cli
  python
  python-gobject
  python-weasyprint
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://gitlab.gnome.org/World/morphosis.git#tag=$pkgver")
b2sums=(76847341fd85c47aac8585f714f7005c3a7fb659c172bf58edb3ea016fb2eca1c6caaca9582d3cfad48f318c6cc7129466eba99c425c4613ef13f8349147bb99)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
