# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>

pkgname=awxkit
_pkgname=awx
_commit=94e5795dfc37b95c576d61f3e3b4e936c021548c
pkgver=24.6.1
pkgrel=5
pkgdesc="The official command line interface for Ansible AWX"
arch=('any')
url="https://github.com/ansible/awx"
license=('Apache-2.0')
depends=(
  'python'
  'python-pytest'  # unreferenced, required in awxkit/yaml_file.py
  'python-pyyaml'
  'python-requests'
  'python-setuptools'  # unreferenced, required in awxkit/cli/client.py
  'python-websocket-client'
)
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-setuptools-scm' 'python-wheel')
checkdepends=(
  'jq'
  'python-pytest'
  'python-cryptography'
  'python-websocket-client'
)
optdepends=(
  'jq: for formatting'
  'python-cryptography: for cryptography support'
)
options=(!emptydirs)
source=(
    "${_pkgname}::git+https://github.com/ansible/${_pkgname}#tag=$_commit"
    "0001-Replace-pkg_resources-with-importlib.metadata-15441.patch"
    "0002-cli-fix-TypeError-in-HelpfulArgumentParser-for-pytho.patch"
)
sha512sums=('b59d1710e5cfdeb19c194af77d5e9045c2ab1201b4f55fdb3b446de80dceb0d3a01de34b73dcfe009e71cf4610e95b63be1ae4b8b998a771ebc0b3c2764621ea'
            '894ebd3aac7c00c0fcd8cbfc6154085b2e10c95d26f8b8e6b7cfbf6c5507f0d4be6c7a4722564890ea3c70ed568837908f7c7ea806017b4eb7ad65d5617b90ff'
            '4c097a0d197111bdd3de1006d3b78d0765c5ad9802bd5500619bbefa3dfb62e218b026d2d42082e3ce6feffcf6fa906ccce64da95264deaac1dd9bb012573c6b')

prepare() {
    cd "${_pkgname}"
    patch -p1 -i "${srcdir}/0001-Replace-pkg_resources-with-importlib.metadata-15441.patch"
    patch -p1 -i "${srcdir}/0002-cli-fix-TypeError-in-HelpfulArgumentParser-for-pytho.patch"
}

build() {
    cd "${_pkgname}/${pkgname}"
    python -m build --wheel --no-isolation
}

check() {
    local deselected=(
      # Python 3.11 test failure https://github.com/ansible/awx/issues/13820
      --deselect test/cli/test_options.py::TestOptions::test_actions_with_primary_key
    )
    cd "${_pkgname}/${pkgname}"
    PYTHONPATH="$PYTHONPATH:." pytest -vv "${deselected[@]}"
}

package() {
    cd "${_pkgname}/${pkgname}"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
