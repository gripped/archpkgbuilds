# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daenyth <Daenyth [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

pkgname=rtorrent
pkgver=0.9.8
pkgrel=5
pkgdesc='Ncurses BitTorrent client based on libTorrent'
url='https://rakshasa.github.io/rtorrent/'
license=('GPL')
arch=('x86_64')
makedepends=('git')
depends=("libtorrent=0.13.${pkgver##*.}" 'curl' 'xmlrpc-c')
source=("git+https://github.com/rakshasa/rtorrent.git#tag=v${pkgver}")
sha256sums=('36c91dce07420840d429a649596776cc6fa669c476e2dee732c7c59736293027')

prepare() {
	cd ${pkgname}
	./autogen.sh
}

build() {
	cd ${pkgname}
	export CXXFLAGS+=' -fno-strict-aliasing'
	./configure \
		--prefix=/usr \
		--disable-debug \
		--with-xmlrpc-c \

	make
}

package() {
	cd ${pkgname}
	make DESTDIR="${pkgdir}" install
	install -D doc/rtorrent.rc "${pkgdir}"/usr/share/doc/rtorrent/rtorrent.rc
}
