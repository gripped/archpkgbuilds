# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-settings-daemon
pkgver=8.1.0
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(x86_64)
url=https://github.com/elementary/settings-daemon.git
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  fwupd
  gdk-pixbuf2
  geoclue
  glib2
  glibc
  granite
  gtk3
  libgexiv2
  libpackagekit-glib2.so
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=1b0e0c7b2aadc0e56581c45e55a0be7c61652eb8
source=(pantheon-settings-daemon::git+https://github.com/elementary/settings-daemon.git#tag=${_tag})
b2sums=('350f906fc86c319ebec4fed5d2b373f11dccd387a8ffbb96c0400589f07c607a3133e3f66b0d7075ead98929c111fcc18a637e2185683c557df08edf3ccd48a2')

pkgver() {
  cd pantheon-settings-daemon
  git describe --tags
}

build() {
  arch-meson pantheon-settings-daemon build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
