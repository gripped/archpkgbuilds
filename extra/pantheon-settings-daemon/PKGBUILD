# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-settings-daemon
pkgver=8.3.0
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(x86_64)
url=https://github.com/elementary/settings-daemon.git
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  fwupd
  gdk-pixbuf2
  geoclue
  glib2
  glibc
  granite
  gtk3
  libgexiv2
  libpackagekit-glib2.so
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=f4a30e80e07121f80022ed85e112e52fdbda7835
source=(pantheon-settings-daemon::git+https://github.com/elementary/settings-daemon.git#tag=${_tag})
b2sums=('2adb095b63389cab695e2f1324cb0b3af06ee648cc0e53789ebdeb8fab4e44bd4abc895c8afb62291a53947a30eb91a46f3407955976dd720220f2b821f962a8')

pkgver() {
  cd pantheon-settings-daemon
  git describe --tags
}

build() {
  arch-meson pantheon-settings-daemon build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
