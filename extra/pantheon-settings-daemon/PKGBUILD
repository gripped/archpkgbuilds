# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-settings-daemon
pkgver=8.1.1
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(x86_64)
url=https://github.com/elementary/settings-daemon.git
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  fwupd
  gdk-pixbuf2
  geoclue
  glib2
  glibc
  granite
  gtk3
  libgexiv2
  libpackagekit-glib2.so
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=b70a1f353d7827a8950b648535dd6920292223ae
source=(pantheon-settings-daemon::git+https://github.com/elementary/settings-daemon.git#tag=${_tag})
b2sums=('1a72ab1821c7872523d3ad26aba5d4b3c8ed1e474c9dfe56449d4103151f1c3f3d80ba1b1e2e4f7ec237770c2f1bda865bf39526fd0b687e6d4fbedd656960e0')

pkgver() {
  cd pantheon-settings-daemon
  git describe --tags
}

build() {
  arch-meson pantheon-settings-daemon build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
