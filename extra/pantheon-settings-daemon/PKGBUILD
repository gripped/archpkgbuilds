# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-settings-daemon
pkgver=8.4.0
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(x86_64)
url=https://github.com/elementary/settings-daemon.git
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  fwupd
  gdk-pixbuf2
  geoclue
  glib2
  glibc
  granite
  gtk3
  libgexiv2
  libpackagekit-glib2.so
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=6c5bb6fa1c095ed184f03721e065131dcda7e41a
source=(pantheon-settings-daemon::git+https://github.com/elementary/settings-daemon.git#tag=${_tag})
b2sums=('4cb8f9698764a6ecb3a37bc83109a033723e524174f9a782745fe66d62fc45cfa8242568479130f3ea34bf7465e8a9a176514908f78fa877cfa632029eadc6bf')

pkgver() {
  cd pantheon-settings-daemon
  git describe --tags
}

build() {
  arch-meson pantheon-settings-daemon build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
