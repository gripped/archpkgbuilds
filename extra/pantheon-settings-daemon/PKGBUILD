# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-settings-daemon
pkgver=8.1.0
pkgrel=2
pkgdesc='Default settings for Pantheon'
arch=(x86_64)
url=https://github.com/elementary/settings-daemon.git
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  fwupd
  gdk-pixbuf2
  geoclue
  glib2
  glibc
  granite
  gtk3
  libgexiv2
  libpackagekit-glib2.so
)
makedepends=(
  cmake
  git
  meson
  vala
)
_tag=1b0e0c7b2aadc0e56581c45e55a0be7c61652eb8
source=(
  pantheon-settings-daemon::git+https://github.com/elementary/settings-daemon.git#tag=${_tag}
  fwupd-2.0-compat.patch
)
b2sums=('350f906fc86c319ebec4fed5d2b373f11dccd387a8ffbb96c0400589f07c607a3133e3f66b0d7075ead98929c111fcc18a637e2185683c557df08edf3ccd48a2'
        'b167b0e10347fea95acfee3a15e9c917abd35f157dc01c938c52acbbfae6b7a70538e9d6f6cd0ba87ca15e89cf416c3046708d7c0be671d5c9c2915e0f7d5a13')

pkgver() {
  cd pantheon-settings-daemon
  git describe --tags
}

prepare() {
  cd pantheon-settings-daemon
  patch -Np1 -i ../fwupd-2.0-compat.patch
}

build() {
  arch-meson pantheon-settings-daemon build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
