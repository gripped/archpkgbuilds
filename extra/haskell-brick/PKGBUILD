# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=brick
pkgname=haskell-brick
pkgver=2.1.1
pkgrel=7
pkgdesc="A declarative terminal user interface library"
url="https://github.com/jtdaugherty/brick"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bimap' 'haskell-config-ini' 'haskell-data-clist' 'haskell-microlens'
         'haskell-microlens-mtl' 'haskell-microlens-th' 'haskell-text-zipper' 'haskell-unix-compat'
         'haskell-vector' 'haskell-vty' 'haskell-vty-crossplatform' 'haskell-word-wrap')
makedepends=('ghc' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        $pkgname-testsuite-vty-crossplatform.patch::https://github.com/jtdaugherty/brick/commit/31f94e1b76aaddf0cc5ff49be27ae6c3a82dc98b.patch)
sha512sums=('a87ef2faec32c5eafe9ff89e69f61711b7ac89d9f84414b218b6c4e02ea0b64e8291698075a60639b0329b34756eb1d3bed14a94b2d64505ffa181ef0de0e811'
            '898d233cce1cc31690b8644b7b7877d9af52a34c785a709ffeb68ff0150731077ab6add5b6c39170977b5816634569426b5584d004b839b4167b86bbcad444da')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../$pkgname-testsuite-vty-crossplatform.patch
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-demos
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    TERM=screen runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
