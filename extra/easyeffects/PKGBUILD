# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Wellington <wellingtonwallace@gmail.com>
# Contributor: robertfoster

pkgname=easyeffects
pkgver=7.2.3
pkgrel=2
pkgdesc='Audio Effects for Pipewire applications'
arch=('x86_64')
url='https://github.com/wwmm/easyeffects'
license=('GPL-3.0-only')
depends=('fftw' 'libfmt.so' 'gsl' 'gtk4' 'ladspa' 'libadwaita-1.so'
         'libbs2b.so' 'libebur128' 'libsamplerate.so'
         'libsigc-3.0.so' 'libsndfile.so' 'liblilv-0.so'
         'pipewire' 'rnnoise' 'soundtouch'
         'speexdsp' 'tbb' 'zita-convolver')
makedepends=('appstream-glib' 'itstool' 'meson' 'nlohmann-json' 'speex' 'lv2')
optdepends=(
  'calf: limiter, exciter, bass enhancer and others'
  'lsp-plugins-lv2: equalizer, compressor, delay, loudness'
  'zam-plugins-lv2: maximizer'
  'mda.lv2: bass loudness plugin'
  'yelp: in-app help'
)
replaces=('pulseeffects')
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('4bb7fd19f51fd503773ab584ad1b376bd71bf5e43751dc8e220ac4191a3d6237002b1cf105efc38f596c0046006d36649f73778497cd32c9c024a88ed4f433d3')

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja install -C build
}
