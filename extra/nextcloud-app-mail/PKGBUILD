# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-mail
_name=mail
pkgver=5.1.8
pkgrel=1
pkgdesc="An email app for NextCloud"
arch=(any)
url="https://github.com/nextcloud/mail"
license=(AGPL-3.0-or-later)
makedepends=(
  nextcloud
  yq
)
optdepends=('kitinerary: for kitinerary-extractor')
groups=(nextcloud-apps)
options=(!strip)
# bizarre source tarball naming: https://github.com/nextcloud/mail/issues/10896
source=($_name-$pkgver.tar.gz::https://github.com/nextcloud-releases/mail/releases/download/v$pkgver/${_name}-v${pkgver}.tar.gz)
sha512sums=('8c6b5e3e440b62fd283686b6f85462ec4bead61e1e6a4a399911f5d60312dd03681cc6427a99c06fed5cb78853d91d41e0d90975001ee3bf522046974779de09')
b2sums=('f2b9d2e0c1135a7ee8e430094ab621a9f3f5be139e39a21178251bf04648882981c3d25f40b306964c8c5882e98d04cd84c777f6b80926524cbb88e12c36f652')

# BEGIN boilerplate nextcloud app version clamping, see also other packages in group
# 1. Call respective function helpers in check() and package() *after* cd'ing to the source directory
# 2. Add makedepends+=(nextcloud yq)
_phps=(php php-legacy)
_get_supported_ranges() {
  _app_min_nextcloud="$(< appinfo/info.xml xq -r '.info.dependencies.nextcloud["@min-version"] | values')"
  _app_max_nextcloud="$(< appinfo/info.xml xq -r '.info.dependencies.nextcloud["@max-version"] | values | tonumber | .+1')"
  _app_min_php="$(< appinfo/info.xml xq -r '.info.dependencies.php["@min-version"] | values')"
  _app_max_php="$(< appinfo/info.xml xq -r '.info.dependencies.php["@max-version"] | values | tonumber | .+0.1')"
}
_unsupported_range() {
  printf "%s requires %s %s, but %s %s is provided.\n" "$pkgname" "$1" "$2" "$1" "$3"
  exit 1
}
_nextcloud_app_check() {
  _get_supported_ranges
  for _php in "${_phps[@]}"; do command -v "$_php" > /dev/null && break; done
  local _nextcloud_ver="$("$_php" <(cat /usr/share/webapps/nextcloud/version.php; echo 'print($OC_VersionString);'))"
  local _php_ver="$("$_php" -r 'print(phpversion());')"
  [[ "$(vercmp "${_app_min_nextcloud:-0}" "$_nextcloud_ver")" -le 0 ]] || \
    _unsupported_range nextcloud "=> $_app_min_nextcloud" "$_nextcloud_ver"
  [[ "$(vercmp "${_app_max_nextcloud:-999}" "$_nextcloud_ver")" -gt 0 ]] || \
    _unsupported_range nextcloud "< $_app_max_nextcloud" "$_nextcloud_ver"
  [[ "$(vercmp "${_app_min_php:-0}" "$_php_ver")" -le 0 ]] || \
    _unsupported_range php ">= $_app_min_php" "$_php_ver"
  [[ "$(vercmp "${_app_max_php:-999}" "$_php_ver")" -gt 0 ]] || \
    _unsupported_range php "< $_app_max_php" "$_php_ver"
}
_nextcloud_app_package() {
  _get_supported_ranges
  depends+=("nextcloud>=${_app_min_nextcloud:-0}" "nextcloud<${_app_max_nextcloud:-999}")
  depends+=("php-interpreter${_app_min_php:+>=$_app_min_php}" ${_app_max_php:+"php-interpreter<$_app_max_php"})
}
# END boilerplate nextcloud app version clamping

check() {
  cd $_name
  _nextcloud_app_check
}

package() {
  install -vdm 755 "$pkgdir/usr/share/webapps/nextcloud/apps/"
  cp -av $_name "$pkgdir/usr/share/webapps/nextcloud/apps/"
  # replace vendored version of kitinerary-extractor: https://github.com/nextcloud/mail/issues/8867
  ln -fsv /usr/lib/kf6/kitinerary-extractor "$pkgdir/usr/share/webapps/nextcloud/apps/mail/vendor/nextcloud/kitinerary-bin/bin/kitinerary-extractor"
  cd $_name
  _nextcloud_app_package
}
