# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-sitemap
pkgname=python-sphinx-sitemap
pkgver=2.8.0
pkgrel=1
pkgdesc="Sphinx extension to generate a multi-lingual, multi-version sitemap for HTML builds"
arch=(any)
url="https://github.com/jdillard/sphinx-sitemap"
license=(MIT)
depends=(
  python
  python-sphinx
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-defusedxml
  python-gitpython
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('6413a12d5777a54181e94b4c7b12291003caa3824573be9b3fcac646669e2074c08973d28a849cf421a38500c2dffb12a0829c4ff4b2b4448db721a13249a29f')
b2sums=('881772326cf4cf993c58c07e486fa428e53c744178e1f906dece590a4e0c51cb32d661c70b3419376c2485dfefca0761bf8a23cc19e72d9423be1805f90142ab')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGELOG,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
