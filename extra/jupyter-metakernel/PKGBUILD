# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=metakernel
pkgname=jupyter-$_name
pkgver=0.30.4
pkgrel=2
pkgdesc="A Jupyter/IPython kernel template"
arch=(any)
url="https://github.com/Calysto/metakernel"
license=(BSD-3-Clause)
depends=(
  python
  python-ipykernel
  python-jedi
  python-jupyter-core
  python-pexpect
  # implicitly satisfied
  ipython
  python-jupyter-client
  python-parso
  python-pyzmq
  python-traitlets
)
makedepends=(
  python-build
  python-hatchling
  python-installer
)
checkdepends=(
  python-pytest
  python-pytest-timeout
  python-requests
  man-db
)
optdepends=(
  'python-ipywidgets: for activity magics'
  'python-ipyparallel: for parallel magics'
  'python-matplotlib: for matplotlib magics'
  'python-pydot: for dot magics'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('00f6147b2915c8ab2e087664e1718dd51cb1eee9812f88e2739ae88ac0eb0165009607dcb221253ca9d923f4434efea2c729f1296f5692e9afab4dfa63c96870')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    --override-ini="addopts="
    # AssertionError: Checking indents [...]
    --deselect metakernel/tests/test_magic.py::test_get_help 
    # AssertionError: assert 'bashrc' in []
    --deselect metakernel/tests/test_parser.py::test_path_completions 
    # ValueError: Stdin Requested but not stdin handler available
    --deselect metakernel/tests/test_replwrap.py::REPLWrapTestCase::test_python
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" metakernel/tests metakernel/magics/tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
