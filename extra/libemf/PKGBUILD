# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: lily wilson <hotaru@thinkindifferent.net>

pkgname=libemf
pkgver=1.0.13
pkgrel=5
pkgdesc='Library implementation of ECMA-234 API for the generation of enhanced metafiles'
arch=('x86_64')
url='https://libemf.sourceforge.net/'
license=('GPL-2.0-or-later' 'LGPL-2.1-or-later')
depends=(
    'glibc'
    'libgcc'
    'libstdc++')
source=("https://sourceforge.net/projects/libemf/files/libemf/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('74d92c017e8beb41730a8be07c2c6e4ff6547660c84bf91f832d8f325dd0cf82')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix='/usr' \
        --enable-static='no' \
        --enable-shared='yes' \
        --enable-threads \
        --enable-editing
    make
}

check() {
    make -C "${pkgname}-${pkgver}" check
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
