# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Victor HÃ¤ggqvist <aur@snilius.com>

pkgname=python-pyfiglet
_name=${pkgname#python-}
pkgver=1.0.4
pkgrel=2
pkgdesc='Pure-python FIGlet implementation'
url=https://github.com/pwaller/pyfiglet
license=(MIT)
arch=(any)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  figlet
  python-pytest
  toilet
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('6b5f4050246ef03d05c86b9de2c3ca7eaa5189dcc65982091a88d23089dfd484797383e14c4ad497e734cf68dc4dda050aace72c08ed985f30549f879f9b53e2')

build() {
  cd "$_name"
  mkdir pyfiglet/fonts
  cp pyfiglet/fonts-standard/* pyfiglet/fonts
  cp pyfiglet/fonts-contrib/* pyfiglet/fonts
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PYTHONPATH=. python pyfiglet/test.py
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/licenses/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname

  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
