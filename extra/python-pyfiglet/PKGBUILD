# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Victor HÃ¤ggqvist <aur@snilius.com>

pkgname=python-pyfiglet
_name=${pkgname#python-}
pkgver=1.0.0
pkgrel=1
pkgdesc='Pure-python FIGlet implementation'
url=https://github.com/pwaller/pyfiglet
license=('GPL')
arch=('any')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('figlet' 'python-pytest' 'toilet')
source=(
  "git+$url.git#tag=v$pkgver"
  "pyfiglet-subprocess32-removal.patch::$url/commit/560acfacc85ab64c2d5cba52b6731c8a80bcf879.patch"
)
b2sums=('SKIP'
        '3243d718e0a33d41ac797743f968831942f6e04a2076b20197f78e955929b73ae016e997048798fba036a3f0a5cf2466d55a126bd9fd01209bf58843d50b2182')

prepare() {
  cd "$_name"
  git apply ../pyfiglet-subprocess32-removal.patch
}

build() {
  cd "$_name"
  mkdir pyfiglet/fonts
  cp pyfiglet/fonts-standard/* pyfiglet/fonts
  cp pyfiglet/fonts-contrib/* pyfiglet/fonts
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PYTHONPATH=. python pyfiglet/test.py
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
