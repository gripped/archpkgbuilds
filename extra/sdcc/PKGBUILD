# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jose Negron <josenj.arch@mailnull.net>

pkgname=sdcc
pkgver=4.5.0
pkgrel=1
pkgdesc='Retargettable ANSI C compiler'
arch=('x86_64')
url='https://sdcc.sourceforge.net/'
license=('GPL')
depends=('gcc-libs' 'bash')
makedepends=('gputils' 'boost' 'patchutils')
optdepends=('python')
options=('!strip' 'staticlibs' '!debug')
source=(
  "$pkgname-$pkgver.tar.bz2::https://sourceforge.net/projects/sdcc/files/sdcc/$pkgver/$pkgname-src-$pkgver.tar.bz2"
  'build-all-mcs51-models.patch'
)
sha512sums=('46edd0456d82ebeacc6e7b2ecf5038aac38ce161dc938662c438cca9c41f73b2004589788c18a0f1dd295033c9c35c202c052b463ec7bd8c605065d440d617f4'
            '265982849af52f5d84069ef9dd43f6d768eb46b2ff07e1652c3036d69e59fd366481a4447b6c376d2c74123cf6cf5f08afe957fa18a8038a4b0602f4593238f9')
b2sums=('8f7e79224e8c0692b8bc1377dbeb9c743f959ebb83bc444a215edf20f7d8c9faece7d0898ad130d141e81256cde00a74e1263f2fd9b65df0cca379524af2f93a'
        '28bca84a8e1c2eda552efb0129941fdf1b17cdc2e888c26bf896295d392fd31f729136e76794913770b052f5171091bddbb944cde7407fd65e0df370654faf29')

prepare() {
  cd "$pkgname-$pkgver"

  # FS#79070: Build all models for mcs51 by default
  patch -p1 -i "$srcdir/build-all-mcs51-models.patch"
}

build() {
  cd "$pkgname-$pkgver"

  CFLAGS+=" -Wno-incompatible-pointer-types" \
  ./configure \
    --prefix=/usr \
    --includedir=/usr/include/sdcc \
    --libdir=/usr/lib/sdcc \
    --libexecdir=/usr/lib \
    --disable-werror

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
