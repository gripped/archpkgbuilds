# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=system-fileio
pkgname=haskell-system-fileio
pkgver=0.3.16.6
pkgrel=17
pkgdesc="Consistent filesystem interaction across GHC versions"
url="https://github.com/fpco/haskell-filesystem"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-system-filepath')
makedepends=('ghc' 'haskell-chell' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        $pkgname-c23.patch::https://github.com/fpco/haskell-filesystem/commit/3c5b3dee1810ad25ce7e0c7a52bc4552b7cd9d98.patch)
sha512sums=('7eac27ded5634eaa396999b458d4e4230ddac694bb5ae276a22e3e8acef8028ed547c789e67504f76a649b0dd6baa3c6c010f0e76278de7571bc38a7a403adcf'
            '5201d838a885107d178cce6feabd9d86a3e809787e860afde6b6a01fea3e471fab151f2c160a3f697fd11faeb550a9beecf574056b52214273d3b552907f031c')

prepare() {
    cd $_hkgname-$pkgver
    patch -p2 < ../$pkgname-c23.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
