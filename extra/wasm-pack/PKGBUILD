# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=wasm-pack
pkgver=0.13.1
pkgrel=1
pkgdesc="Your favorite rust -> wasm workflow tool!"
arch=('x86_64')
url="https://github.com/rustwasm/wasm-pack"
license=('MIT' 'Apache')
depends=('bzip2' 'curl')
makedepends=('cargo' 'cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha384sums=('0524f53216575dd81acc8127b89890080efeaba5e8588016a2194fc4140ef75940a9506e04746d7818f5b4d71b70f4de')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
