# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=wasm-pack
pkgver=0.14.0
pkgrel=1
pkgdesc="Your favorite rust -> wasm workflow tool!"
arch=('x86_64')
url="https://github.com/rustwasm/wasm-pack"
license=('MIT' 'Apache')
depends=('bzip2' 'curl')
makedepends=('cargo' 'cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha384sums=('bd92f7470f2789e3876ea4a8a7343106a89b8964921bf4a4328f928a9fcd46c4e9e594cab909a2b68c24be0682a63505')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
