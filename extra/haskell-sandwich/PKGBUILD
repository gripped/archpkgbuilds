# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=sandwich
pkgname=haskell-sandwich
pkgver=0.1.5.2
pkgrel=55
pkgdesc="Yet another test framework for Haskell"
url="https://codedownio.github.io/sandwich"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-async' 'haskell-brick'
         'haskell-colour' 'haskell-free' 'haskell-src-exts' 'haskell-lifted-async'
         'haskell-microlens' 'haskell-microlens-th' 'haskell-monad-control' 'haskell-monad-logger'
         'haskell-optparse-applicative' 'haskell-pretty-show' 'haskell-safe'
         'haskell-safe-exceptions' 'haskell-string-interpolate' 'haskell-transformers-base'
         'haskell-unliftio-core' 'haskell-vector' 'haskell-vty5')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('214f74ecbed388d68a268969e28b7083a0f34568ccde52b827db59b4b1b12a77')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
