# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
# Contributor: dschauer <dschauerATgmailDOTcom>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>

pkgname=tcc
_commit='b39cbc70c4ccc0540c2ef65010b8241caf005ad4'
pkgver=0.9.27.r1337.gb39cbc70
pkgrel=1
pkgdesc='Tiny C Compiler (tinycc)'
arch=('x86_64')
url='https://bellard.org/tcc/'
license=('LGPL-2.0-or-later')
depends=('glibc')
makedepends=('git')
options=('docs' 'staticlibs')
source=("git+https://repo.or.cz/tinycc.git#commit=${_commit}")
sha256sums=('1f247760aebaf9d093be38ee500d860a22bd6b5d091a4735046e4e172c4d5b5d')

pkgver() {
  cd tinycc

  GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"
  printf '%s.r%s.g%s' \
    "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG})" \
    "$(git rev-list --count ${GITTAG}..)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd tinycc

  ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/tcc \
    --disable-static
  make
}

package() {
  cd tinycc

  make DESTDIR="${pkgdir}" install
}
