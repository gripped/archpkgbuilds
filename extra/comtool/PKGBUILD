# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=comtool
pkgver=2.2.1
pkgrel=5
pkgdesc="A cross platform serial debug tools written by python"
arch=('any')
url="https://github.com/Neutree/COMTool"
license=('LGPL-3.0-only')
depends=('python-pyqt5' 'python-pyserial' 'python-requests' 'python-babel' 'python-qtawesome')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("git+https://github.com/Neutree/COMTool.git#tag=v$pkgver")
sha512sums=('880116a7feaf374312868323aed75982e45f377f49aab85b095873ae022651b8cd3a3af50dd0e51214f7e10f5a9f38922c4da0a1d93b8cca18c42d904c387eeb')

prepare() {
  cd COMTool
  sed -i "s/'pyqt5>=5',//" setup.py
}

build() {
  cd COMTool
  python -m build --wheel --no-isolation
}

package() {
  cd COMTool
  python -m installer --destdir="$pkgdir" dist/*.whl

  rm "$pkgdir"/usr/{LICENSE,README.MD}
}

# vim:set ts=2 sw=2 et:
