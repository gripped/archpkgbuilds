# Maintainer: Peter Jung <ptr1337@archlinux.org>

pkgname=dotslash
pkgver=0.5.8
pkgrel=1
pkgdesc="Command-line tool to facilitate fetching an executable, caching it, and then running it"
arch=(x86_64)
url="https://github.com/facebook/dotslash"
license=('MIT' 'Apache-2.0')
depends=(
  glibc
)
makedepends=(
  cargo
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/facebook/dotslash/archive/refs/tags/v$pkgver.tar.gz")
options=(!lto)
sha256sums=('c80f7ce8b659c8f510659298ef5427bfaf7ee3258760bcffe2c45f7628de858e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 target/release/dotslash "$pkgdir/usr/bin/dotslash"
  install -Dm644 LICENSE-MIT LICENSE-APACHE \
    -t "$pkgdir/usr/share/licenses/$pkgname/"
}
