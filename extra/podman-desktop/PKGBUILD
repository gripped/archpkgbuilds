# Maintainer: Quentin Michaud <mh4ckt3mh4ckt1c4s@archlinux.org>
# Contributor: ZhangHua <zhanghuadedn at gmail dot com>
# Contributor: Juliette Cordor 

pkgname=podman-desktop
pkgver=1.18.0
pkgrel=1
pkgdesc="Manage Podman and other container engines from a single UI and tray."
arch=("x86_64")
url="https://podman-desktop.io"
license=('Apache-2.0')
depends=("electron34" "hicolor-icon-theme")
makedepends=(
    'pnpm' 'node-gyp' 'npm' 'nodejs-lts-iron' 'libvips' 'lcms2'
    'openjpeg2' 'glib2' 'python-setuptools'
)
optdepends=(
    "podman: podman plugin"
    "docker: docker plugin"
)
source=(${pkgname}-${pkgver}.tar.gz"::https://github.com/${pkgname}/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('d43ecec051d8a7c167bca080672a938b4fdad810b4560e3f1433c91371c8576171d2a990a17f92b1102956ac6fdb6e8a2e053741f85263057a9795a15666bb70')

prepare(){
    cd "${srcdir}/podman-desktop-$pkgver"
    sed -i 's/run.sh/podman-desktop/' '.flatpak.desktop'
    pnpm install --ignore-scripts --no-frozen-lockfile
}

build(){
    cd "${srcdir}/podman-desktop-$pkgver"
    pnpm run compile:current --dir
}

package(){
    cd "${srcdir}/podman-desktop-$pkgver"
    mkdir -p "${pkgdir}/opt" "${pkgdir}/usr/bin"
    cp -r "dist/linux-unpacked" "${pkgdir}/opt/podman-desktop"
    install -Dm644 ./.flatpak-appdata.xml \
        "${pkgdir}/usr/share/metainfo/io.podman_desktop.PodmanDesktop.xml"
    install -Dm644 ./.flatpak.desktop \
        "${pkgdir}/usr/share/applications/io.podman_desktop.PodmanDesktop.desktop"
    install -Dm644 ./buildResources/icon.svg \
        "${pkgdir}/usr/share/icons/hicolor/scalable/apps/io.podman_desktop.PodmanDesktop.svg"
    install -Dm644 ./buildResources/icon-512x512.png \
        "${pkgdir}/usr/share/icons/hicolor/512x512/apps/io.podman_desktop.PodmanDesktop.png"
    install -Dm644 ./buildResources/icon.png \
        "${pkgdir}/usr/share/icons/hicolor/1024x1024/apps/io.podman_desktop.PodmanDesktop.png"
    ln -s /opt/podman-desktop/podman-desktop "${pkgdir}/usr/bin/podman-desktop"
}
