# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Saghm Rossi <saghmrossi@gmail.com>
# Contributor: Peter Hoeg <first name at last name dot com>
# Contributer: Vincent Demeester <vincent@sbr.io>
# Contributer: Alex Ogier <alex.ogier@gmail.com>

pkgname=ruby-build
pkgver=20250716
pkgrel=1
pkgdesc="A tool to download, compile, and install Ruby on Unix-like systems"
arch=('any')
url="https://github.com/rbenv/ruby-build"
license=('MIT')
depends=('bash')
makedepends=('git')
checkdepends=('bats')
optdepends=(
  'git: install ruby from git'
  'libyaml: install recent versions of mri'
  'rust: install ruby with YJIT enabled'
)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('e31bec70ccc90ed7044fe91b2e0282bb5390915133dcbc1376088d5c4be595ee')

prepare() {
  cd $pkgname
  # The unstub openssl function fails for some reason, tests pass without it.
  sed -i '/unstub openssl/d' test/build.bats
}

check() {
  cd $pkgname
  bats test
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" bin/*
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" share/$pkgname/*
  install -vDm644 -t "$pkgdir/usr/share/man/man1" share/man/man1/*
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
