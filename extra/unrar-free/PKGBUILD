# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bastian Germann <bastian.germann[at]gmx[dot]de>
# Contributor: hcartiaux
# Contributor: Chris Mann <cshclm[at]gmail[dot]com>

pkgname=unrar-free
pkgver=0.3.3
pkgrel=1
epoch=1
pkgdesc="Free utility to extract files from RAR archives"
url="https://gitlab.com/bgermann/unrar-free"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('glibc' 'libarchive')
source=("${url}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e0d52bded7863b81cbc4313929d0ad2e4d2e449bf96c85b436658eda02c506a7')

prepare() {
	cd "${pkgname}-${pkgver}"
	autoreconf -fvi
}

build() {
	cd "${pkgname}-${pkgver}"
	export CFLAGS="${CFLAGS} -Wno-format-security"
	./configure --prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
