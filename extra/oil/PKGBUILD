# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.24.0
pkgrel=1
pkgdesc='A new Unix shell'
arch=('x86_64')
url='https://www.oilshell.org'
license=('Apache-2.0')
depends=('glibc' 'gcc-libs' 'readline')
makedepends=('bash')
replaces=('osh')
install=oil.install
source=("$url/download/oils-for-unix-$pkgver.tar.gz")
sha256sums=('df4afed94d53b303a782ce0380c393d60f6d21921ef2a25922b400828add98f3')
b2sums=('d9c65be8494956afb0f838890b7ebc7f755d5f71bddd110688a0f015e13f648dfda3d4bdaf6ba845c22556a90af578a701872fbd249c335f225bcf9c732afcce')
noextract=("oils-for-unix-$pkgver.tar.gz")
# https://www.oilshell.org/release/latest/

prepare() {
  # fix: LTO + strip
  mkdir -p path
  cat >path/c++ <<END
#!/bin/sh
exec g++ \$CXXFLAGS \$LDFLAGS -fPIC "\$@"
END
  cat >path/strip <<END
#!/bin/sh
exec cp -p "\$3" "\$2"
END
  chmod +x path/*

  # fix: bsdtar choking on hardlinked files pointing to self, e.g.:
  # _gen/frontend/option.asdl.h: Skipping hardlink pointing to itself: _gen/frontend/option.asdl.h: No such file or directory
  tar -xf "oils-for-unix-$pkgver.tar.gz"
}

build() {
  cd "oils-for-unix-$pkgver"

  ./configure \
    --prefix=/usr \
    --datarootdir=/usr/share

  PATH="$srcdir/path:$PATH" _build/oils.sh
}

package() {
  cd "oils-for-unix-$pkgver"

  DESTDIR="$pkgdir" ./install
}
