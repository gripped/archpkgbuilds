# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.26.0
pkgrel=1
pkgdesc='A new Unix shell'
arch=('x86_64')
url='https://oils.pub/'
license=('Apache-2.0')
depends=('glibc' 'gcc-libs' 'readline')
makedepends=('bash')
replaces=('osh')
install=oil.install
source=("$url/download/oils-for-unix-$pkgver.tar.gz")
sha256sums=('2b5b295a577a2763814203b4a34880ca03067a29eeb80af4857b6092314d6eed')
b2sums=('a46db9dc75603b12b8acd2e5b1636dad880184861b8708a9c8b3cbf248bb670df37018e72845ecea526943f6ec6f8daf27d21b25b4bc344f743d7f23f4774ce6')
noextract=("oils-for-unix-$pkgver.tar.gz")

prepare() {
  # fix: LTO + strip
  mkdir -p path
  cat >path/c++ <<END
#!/bin/sh
exec g++ \$CXXFLAGS \$LDFLAGS -fPIC "\$@"
END
  cat >path/strip <<END
#!/bin/sh
exec cp -p "\$3" "\$2"
END
  chmod +x path/*

  # fix: bsdtar choking on hardlinked files pointing to self, e.g.:
  # _gen/frontend/option.asdl.h: Skipping hardlink pointing to itself: _gen/frontend/option.asdl.h: No such file or directory
  tar -xf "oils-for-unix-$pkgver.tar.gz"
}

build() {
  cd "oils-for-unix-$pkgver"

  ./configure \
    --prefix=/usr \
    --datarootdir=/usr/share

  PATH="$srcdir/path:$PATH" _build/oils.sh
}

package() {
  cd "oils-for-unix-$pkgver"

  DESTDIR="$pkgdir" ./install
}
