# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.36.0
pkgrel=1
pkgdesc='Oils for Unix: OSH and YSH shells'
arch=('x86_64')
url='https://oils.pub/'
license=('Apache-2.0')
depends=('glibc' 'gcc-libs' 'readline')
makedepends=('bash')
provides=('oils-for-unix')
replaces=('osh')
install=oil.install
source=("$url/download/oils-for-unix-$pkgver.tar.gz")
sha256sums=('9b65fc7333708dc1dc777e8a6145ae868eac6fbe0c444a8b12bb6d6d29652902')
b2sums=('c5a3bf950a175ed15fffa49ebb1cc1263ebb6f355ab7cedbabc73299a9d6aeded4a44cdccafccc31098a7b5858b6d510635bfd42955f9d1d090e72ce23295fc3')
noextract=("oils-for-unix-$pkgver.tar.gz")

prepare() {
  # fix: LTO + strip
  mkdir -p path
  cat >path/c++ <<END
#!/bin/sh
exec g++ \$CXXFLAGS \$LDFLAGS -fPIC "\$@"
END
  cat >path/strip <<END
#!/bin/sh
exec cp -p "\$3" "\$2"
END
  chmod +x path/*

  # fix: bsdtar choking on hardlinked files pointing to self, e.g.:
  # _gen/frontend/option.asdl.h: Skipping hardlink pointing to itself: _gen/frontend/option.asdl.h: No such file or directory
  tar -xf "oils-for-unix-$pkgver.tar.gz"
}

build() {
  cd "oils-for-unix-$pkgver"

  ./configure \
    --prefix=/usr \
    --datarootdir=/usr/share

  PATH="$srcdir/path:$PATH" _build/oils.sh
}

package() {
  cd "oils-for-unix-$pkgver"

  DESTDIR="$pkgdir" ./install
}
