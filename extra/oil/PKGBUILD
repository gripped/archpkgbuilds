# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.23.0
pkgrel=1
pkgdesc='A new Unix shell'
arch=('x86_64')
url='https://www.oilshell.org'
license=('Apache-2.0')
depends=('glibc' 'gcc-libs' 'readline')
makedepends=('bash')
replaces=('osh')
install=oil.install
source=("$url/download/oils-for-unix-$pkgver.tar.gz")
sha256sums=('c9d35ca78b4a08eeafac8bc6439e2bb40bccd3370db2226487faed6348a17521')
b2sums=('b12919f4ae73c032d0041589c63e24874011e4ffc28edc2a60cc3fe136ef1e25c713fa6a1f0b8f9444cc4abab40abf344286667ba88e9db09609c6673565afa6')
noextract=("oils-for-unix-$pkgver.tar.gz")
# https://www.oilshell.org/release/latest/

prepare() {
  # fix: LTO + strip
  mkdir -p path
  cat >path/c++ <<END
#!/bin/sh
exec g++ \$CXXFLAGS \$LDFLAGS -fPIC "\$@"
END
  cat >path/strip <<END
#!/bin/sh
exec cp -p "\$3" "\$2"
END
  chmod +x path/*

  # fix: bsdtar choking on hardlinked files pointing to self, e.g.:
  # _gen/frontend/option.asdl.h: Skipping hardlink pointing to itself: _gen/frontend/option.asdl.h: No such file or directory
  tar -xf "oils-for-unix-$pkgver.tar.gz"
}

build() {
  cd "oils-for-unix-$pkgver"

  ./configure \
    --prefix=/usr \
    --datarootdir=/usr/share

  PATH="$srcdir/path:$PATH" _build/oils.sh
}

package() {
  cd "oils-for-unix-$pkgver"

  DESTDIR="$pkgdir" ./install
}
