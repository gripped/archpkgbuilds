# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=wlr-protocols
pkgver=r107.ffb89ac
_commit=ffb89ac790096f6e6272822c8d5df7d0cc6fcdfa
pkgrel=1
pkgdesc='Wayland protocols designed for use in wlroots (and other compositors).'
arch=('any')
url="https://gitlab.freedesktop.org/wlroots/wlr-protocols"
license=('MIT')
makedepends=('git')
checkdepends=('wayland')
source=("git+https://gitlab.freedesktop.org/wlroots/wlr-protocols.git#commit=$_commit")
sha512sums=('ee7996c6aa01ae42bb8a7e6def7d0ba363ca07d21aebc1420f452f2a46470ced025a9c7fefc54ec2edd54fca13df1c5853cd66393fd5904253f165e383010c6d')

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" make install
}
