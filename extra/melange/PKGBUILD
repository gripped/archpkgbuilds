# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=melange
pkgver=0.34.1
pkgrel=1
pkgdesc='Build APKs from source code'
url='https://github.com/chainguard-dev/melange'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'alpine-keyring'
  'apk-tools'
  'bubblewrap'
  'glibc'
)
optdepends=(
  'qemu-user-static-binfmt: build packages for other architectures'
)
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/chainguard-dev/melange/archive/v${pkgver}.tar.gz")
sha256sums=('c8956c0fd6e6f554f627af6a781ce66e1086070b80a0d6206f9fef68d46b3560')
b2sums=('4b45a65aeadaf6dc8be58604d4a63bb2486ac9d68ff03e18998e5fb2d742324b606ab951d19c46145631b544def79724a9fe2fc6fccd7c21ff0bd8f409bc8362')

build() {
  cd ${pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .

  for shell in bash fish zsh; do
    ./melange completion ${shell} > ./melange.${shell}
  done
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 melange -t "${pkgdir}/usr/bin"
  install -dm 755 "${pkgdir}/usr/share/melange"
  cp -a pkg/build/pipelines/ "${pkgdir}/usr/share/melange"

  install -Dm644 melange.bash "${pkgdir}"/usr/share/bash-completion/completions/melange
  install -Dm644 melange.zsh "${pkgdir}"/usr/share/zsh/site-functions/_melange
  install -Dm644 melange.fish "${pkgdir}"/usr/share/fish/vendor_completions.d/melange.fish
}

# vim: ts=2 sw=2 et:
