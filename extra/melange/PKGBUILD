# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=melange
pkgver=0.13.5
pkgrel=1
pkgdesc='Build APKs from source code'
url='https://github.com/chainguard-dev/melange'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'alpine-keyring'
  'apk-tools'
  'bubblewrap'
  'glibc'
)
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/chainguard-dev/melange/archive/v${pkgver}.tar.gz")
sha256sums=('b4d2e9275dce91947d879e716a51a05ca59fee335055b37cea7cc01b44dc63a7')
b2sums=('1b26fbbf78030c41e164e3d9c0cc94664f8157aedde8815ff3162916f5202b62edceb48d54579c120d881fef60cd2f2a8f581b83faaa887f022e9258d3f2e103')

build() {
  cd ${pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 melange -t "${pkgdir}/usr/bin"
  install -dm 755 "${pkgdir}/usr/share/melange"
  cp -a pkg/build/pipelines/ "${pkgdir}/usr/share/melange"
}

# vim: ts=2 sw=2 et:
