# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=melange
pkgver=0.6.4
pkgrel=1
pkgdesc='Build APKs from source code'
url='https://github.com/chainguard-dev/melange'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'alpine-keyring'
  'apk-tools'
  'bubblewrap'
  'glibc'
)
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/chainguard-dev/melange/archive/v${pkgver}.tar.gz")
sha256sums=('7a5eca716d123782b5017b629440d3da15c5ab134e35f8f00125226a879df99b')
b2sums=('a80b4c53a14ef11ee3ab416a176b00effbd397bfcf3c76d135dbcb7ab45e5ff810de2c39527b093b8ea65f953b7a80be48daa023c90dd6705390bdad092d931c')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 melange -t "${pkgdir}/usr/bin"
  install -dm 755 "${pkgdir}/usr/share/melange"
  cp -a pkg/build/pipelines/ "${pkgdir}/usr/share/melange"
}

# vim: ts=2 sw=2 et:
