# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-table-extra
pkgver=5.1.2
pkgrel=1
pkgdesc="Extra tables for Fcitx5"
arch=('any')
url="https://github.com/fcitx/fcitx5-table-extra"
license=('GPL')
depends=('fcitx5-chinese-addons')
makedepends=('extra-cmake-modules' 'boost')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('adee8d5362ec052d6e0f414799d257c69ab5474f2f0abf7078e3de8b6d417b3f6d5a1de0fe1d7171926e9febcc55d2e535b94fecdd44448386683c2c87176b98'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
