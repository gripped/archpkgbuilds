# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=illuminanced
pkgver=1.0.2
pkgrel=1
pkgdesc='Ambient Light Sensor Daemon for Linux'
license=('GPL-3.0-or-later')
arch=('x86_64')
url='https://github.com/mikhail-m1/illuminanced'
backup=('etc/illuminanced.toml')
depends=('glibc')
makedepends=('rust')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mikhail-m1/illuminanced/archive/refs/tags/${pkgver}.tar.gz"
        "illuminanced-service-dropin.conf")
b2sums=('0ef7e7c84473075122d421aa4100220298cbbe855229d0365ce2ca1e10f47ccd01b2ecd63c723e149bf05d622ae4e3bd8e3cf4ec453ef984952a453c73c9a00f'
        'f7973ba69f33123f38cf9a2b2ab8c5ac5942fd64724592ef0bbe4bd880dca16bb5a569e2abd5132b88faa409d0feff37d9606826451e4b7aa9153dd79e721a80')

build () {
  cd "${pkgname}-${pkgver}"

  cargo build --release
}

package () {
  cd "${pkgname}-${pkgver}"

  install -Dm755 target/release/illuminanced "${pkgdir}/usr/bin/illuminanced"
  install -Dm644 illuminanced.toml "${pkgdir}/etc/illuminanced.toml"
  install -Dm644 illuminanced.service "${pkgdir}/usr/lib/systemd/system/illuminanced.service"

  install -Dm644 "${srcdir}/illuminanced-service-dropin.conf" "${pkgdir}/usr/lib/systemd/system/illuminanced.service.d/override.conf"
}

