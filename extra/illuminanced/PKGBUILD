# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=illuminanced
pkgver=1.0.1
pkgrel=2
pkgdesc='Ambient Light Sensor Daemon for Linux'
license=('GPL-3.0-or-later')
arch=('x86_64')
url='https://github.com/mikhail-m1/illuminanced'
backup=('etc/illuminanced.toml')
depends=('glibc')
makedepends=('rust')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mikhail-m1/illuminanced/archive/refs/tags/${pkgver}.tar.gz"
        "illuminanced-service-dropin.conf")
b2sums=('ee69e2a339861fa575ee26b6e3afb6df61083794e4652b2dec2e2b0c7a4ef3b8543f5789206395b3b3af4353e1cf75982c3964de44f5d6cb58a15a19ba314f28'
        'f7973ba69f33123f38cf9a2b2ab8c5ac5942fd64724592ef0bbe4bd880dca16bb5a569e2abd5132b88faa409d0feff37d9606826451e4b7aa9153dd79e721a80')

build () {
  cd "${pkgname}-${pkgver}"

  cargo build --release
}

package () {
  cd "${pkgname}-${pkgver}"

  install -Dm755 target/release/illuminanced "${pkgdir}/usr/bin/illuminanced"
  install -Dm644 illuminanced.toml "${pkgdir}/etc/illuminanced.toml"
  install -Dm644 illuminanced.service "${pkgdir}/usr/lib/systemd/system/illuminanced.service"

  install -Dm644 "${srcdir}/illuminanced-service-dropin.conf" "${pkgdir}/usr/lib/systemd/system/illuminanced.service.d/override.conf"
}

