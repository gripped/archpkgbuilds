# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: djanku <djanku (at) email (dot) cz>

_pipname=flask-babel
pkgname=python-flask-babel
pkgver=4.0.0
pkgrel=5
pkgdesc="Adds i18n/l10n support to Flask applications with the help of the Babel library."
arch=(any)
url="https://pypi.python.org/pypi/flask-babel"
license=(BSD-3-Clause)
depends=(
  'python'
  'python-babel'
  'python-flask'
  'python-jinja'
  'python-pytz'
  'python-werkzeug'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-poetry-core'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-mock'
)
source=("https://github.com/python-babel/flask-babel/archive/v$pkgver/$_pipname-$pkgver.tar.gz")
sha256sums=('6952ba8a6813a33a5c2ff65ddad61e8215307b5eb060d2d87e413822af9114a7')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
