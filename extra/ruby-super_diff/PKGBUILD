# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='super_diff'
pkgname="ruby-${_gemname}"
pkgver=0.17.0
pkgrel=1
pkgdesc='A gem that hooks into RSpec to intelligently display the differences between two data structures of any type'
arch=('any')
url="https://github.com/splitwise/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-attr_extras
  ruby-diff-lcs
  ruby-patience_diff
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4c29aa8cd22ae8f2cdc338e1b3d9a40c2eea981b70cff0f000b7bec9a39fd04c4fdd70a8ad526600af181735fa096d7242ad39d727de2e41ca41264d41393b53')
b2sums=('bae54754362533d754342671c9463acc9798e14faae9e3e1db5a418b23164cf2e9f53baa37d1ef19412f5da2802645402e6ab82c441540f47f7c47d5138e2a0a')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
