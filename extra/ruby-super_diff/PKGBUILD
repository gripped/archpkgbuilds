# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='super_diff'
pkgname="ruby-${_gemname}"
pkgver=0.15.0
pkgrel=2
pkgdesc='A gem that hooks into RSpec to intelligently display the differences between two data structures of any type'
arch=('any')
url="https://github.com/splitwise/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-attr_extras
  ruby-diff-lcs
  ruby-patience_diff
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('344a7ac1344561144f22bd860e6febc18466402790aa20d50e1fbf61cc8decb36f40f8ba2dad91f8dd528cd6e52a5e9ab9d32d08701d33f096c0e84c612e389d')
b2sums=('43d857979c4e93c6615854563b8116b7fe21d33e92ca8abd6f4a55a78d4077fdc23b451c3f8b13fb702ec89690bc507f5b02df2686e1f58452c4f967d4e8d6c0')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
