# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=rustlings
pkgver=6.4.0
pkgrel=1
pkgdesc="Small exercises to get you used to reading and writing Rust code"
arch=('x86_64')
url="https://github.com/rust-lang/rustlings"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
install="rustlings.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('314b284c611b2938cb43abec186f9fc7fdb5975436f3aa06c58e09457d7cd82a')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
