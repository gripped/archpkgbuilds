# Maintainer: Forest Crossman <cyrozap at gmail dot com>

pkgname=stp
pkgver=2.3.4
pkgrel=6
pkgdesc="Simple Theorem Prover"
arch=('x86_64')
url="https://stp.github.io/"
license=('MIT')
depends=('boost-libs' 'cryptominisat' 'minisat')
makedepends=('boost' 'cmake' 'help2man' 'ninja' 'python' 'python-setuptools')
source=("https://github.com/$pkgname/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        'mandir.patch')
sha256sums=('dc197e337c058dc048451b712169a610f7040b31d0078b6602b831fbdcbec990'
            '4c09301c5a36cd89845a7177e1215008f8bbb23fa6f3c6cc941006825e28c327')

prepare() {
  # Correct the destination for man documentation.
  patch -d $pkgname-$pkgver < mandir.patch

  # Remove RPATH
  sed -i '/RPATH/d' $pkgname-$pkgver/{,tools/stp/,tools/stp_simple/}CMakeLists.txt
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_PYTHON_INTERFACE=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd $pkgname-$pkgver
  install -Dm644 LICENSE LICENSE_COMPONENTS -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
