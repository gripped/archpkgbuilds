# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-bluetooth
pkgver=8.0.0
pkgrel=1
pkgdesc='Wingpanel Bluetooth Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-bluetooth
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  bluez
  dconf
  glib2
  glibc
  gtk3
  libgee
  libgranite.so
  libwingpanel.so
)
makedepends=(
  git
  meson
  vala
)
_tag=7bc59f1f9d8710c28d64ca951d7ca1d558bbfc18
source=(git+https://github.com/elementary/wingpanel-indicator-bluetooth.git#tag=${_tag})
sha256sums=('e9a186ac11ed4e5d1f2468ddeeca24cadba432d425e1feab34f22b848f0c48fa')

pkgver() {
  cd wingpanel-indicator-bluetooth

  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-bluetooth build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
