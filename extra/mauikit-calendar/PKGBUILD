# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-calendar
pkgver=4.0.0
pkgrel=1
pkgdesc='Calendar support components for Maui applications'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(akonadi-calendar
         gcc-libs
         glibc
         kcalendarcore
         kconfig
         kcontacts
         kcoreaddons
         ki18n
         kirigami
         kitemmodels
         libakonadi
         mauikit
         qt6-5compat # GraphicalEffects
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        fix-build.patch)
sha256sums=('0a9169250bab954fb1b7d07195e9e6322c183946b6268aa2983e080bf55095d0'
            'SKIP'
            '5baa80ca61fee38e08b2f242dd255356356ec830e3dd0d315cde1c6e57a02e28')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

prepare() {
  patch -d $pkgname-$pkgver -p1 < fix-build.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
