# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=gsettings-qt
pkgname=('gsettings-qt5' 'gsettings-qt6')
pkgver=1.0.0
pkgrel=1
pkgdesc="Library to access GSettings from Qt"
arch=('x86_64')
url="https://gitlab.com/ubports/development/core/gsettings-qt"
license=('LGPL-3.0-only')
makedepends=('git' 'cmake' 'ninja' 'cmake-extras' 'qt5-declarative' 'qt6-declarative')
source=("git+https://gitlab.com/ubports/development/core/gsettings-qt.git#tag=v$pkgver")
sha256sums=('0bda32a632d301c1728c863aa3d0e2511a90a755022462b9c887a2e9671148e6')

prepare() {
  cp -a gsettings-qt{,5}
  sed -i 's/debian_version/arch-release/' gsettings-qt/GSettings/CMakeLists.txt
}

build() {
  cd gsettings-qt
  cmake -Bbuild -GNinja -DENABLE_QT6=ON -DCMAKE_INSTALL_PREFIX=/usr .
  ninja -C build

  cd ../gsettings-qt5
  cmake -Bbuild -GNinja -DENABLE_QT6=OFF -DCMAKE_INSTALL_PREFIX=/usr .
  ninja -C build
}

package_gsettings-qt5() {
  pkgdesc="Library to access GSettings from Qt5"
  depends=('gcc-libs' 'glib2' 'glibc' 'qt5-base' 'qt5-declarative')
  provides=('gsettings-qt')
  conflicts=('gsettings-qt')
  replaces=('gsettings-qt')

  cd gsettings-qt5
  DESTDIR="$pkgdir" ninja -C build install
}

package_gsettings-qt6() {
  pkgdesc="Library to access GSettings from Qt6"
  depends=('gcc-libs' 'glib2' 'glibc' 'qt6-base' 'qt6-declarative')

  cd gsettings-qt
  DESTDIR="$pkgdir" ninja -C build install
}
