# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qtile
pkgver=0.33.0
pkgrel=1
pkgdesc="A full-featured, pure-Python tiling window manager"
arch=(x86_64)
url="https://qtile.org/"
_url="https://github.com/qtile/qtile"
license=(MIT)
depends=(
  gdk-pixbuf2
  glibc
  libnotify
  librsvg
  pango
  python
  python-cairocffi
  python-cffi
  python-gobject
  python-xcffib
)
makedepends=(
  git
  libpulse
  python-build
  python-installer
  python-pywlroots
  python-setuptools-scm
  python-wheel
  wlroots0.17
)
checkdepends=(
  graphviz
  gtk3
  imagemagick
  lm_sensors
  procps-ng
  python-anyio
  python-bowler
  python-dbus-fast
  python-gobject
  python-isort
  python-libcst
  python-pytest
  python-xdg
  xorg-server-xephyr
  xorg-server-xvfb
  xorg-xrandr
  xorg-xwayland
  xterm
)
optdepends=(
  'alsa-utils: for volume widget'
  'canto-daemon: for canto widget'
  'cmus: for cmus widget'
  'jupyter_console: for interaction with qtile via Jupyter'
  'khal: for khal_calendar widget'
  'libinput: for Wayland backend'
  'libpulse: for pulse_volume and pulseaudio_ffi widget'
  'lm_sensors: for sensors widget'
  'moc: for moc widget'
  'python-bowler: for migrating configuration files'
  'python-dbus-fast: for utils, notifications and several widgets'
  'python-iwlib: for wlan widget'
  'python-keyring: for imapwidget widget'
  'python-libcst: for migrations'
  'python-mpd2: mpd2widget widget'
  'python-psutil: graph, net and memory widget'
  'python-pywayland: for Wayland backend'
  'python-pywlroots: for Wayland backend'
  'python-setproctitle: change process name to qtile'
  'python-xdg: launchbar widget'
  'python-xkbcommon: for Wayland backend'
  'xorg-xwayland: for XWayland support'
)
install=$pkgname.install
source=("git+$_url?signed#tag=v$pkgver")
sha512sums=('27c84c093a272b489baf9cf7227dfcbbf179bddd46002ea4dd4b5d3d79d902e08974f7e277a518b81e8a9c4a9ba566b986dc61c61984144573c3a37162ec4aa0')
b2sums=('f24baa1e7897c34d4eb767d19dbddd697aea0a9a69ffd70a6e2a91ed1c825ec89aa721b182e92eb77de1cf5026eeb512acbc9e9b66ab99490a98e17a20aa8403')
validpgpkeys=(
  '3CCAB226289DE0160C61BDB418D18F1BC464DCA3' # Tycho Andersen <tycho@tycho.pizza>
  '35D92E7CC7357A81173EA1C974F9FDD20984FBEC' # Matt Colligan <mcol@posteo.net>
  'A6BAA1E17D2664ADB97B2C6F58A9AA7C86727DF7' # elParaguayo <elparaguayocode@gmail.com>
)

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  # adjust group used for udev rules from sudo to wheel
  sed 's/sudo/wheel/g' -i $pkgname/resources/99-$pkgname.rules
}

build() {
  export CFLAGS="$CFLAGS -I/usr/include/wlroots0.17"
  export LDFLAGS="$LDFLAGS -L/usr/lib/wlroots0.17"
  cd $pkgname
  PYTHONPATH="$PWD" ./scripts/ffibuild -v
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --backend x11
    --backend wayland
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  LC_TYPE=en_US.UTF-8 \
  PYTHONPATH="$PWD/test_dir/$_site_packages:$PYTHONPATH" \
  PATH="$PWD/test_dir/usr/bin:$PATH" \
  pytest "${pytest_options[@]}"
}

package() {
  depends+=(
    wlroots0.17 libwlroots.so
  )

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  # license
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  # config
  install -vDm 644 libqtile/resources/default_config.py -t "$pkgdir/usr/share/doc/$pkgname/"
  # desktop files
  install -vDm 644 "resources/$pkgname.desktop" -t "$pkgdir/usr/share/xsessions/"
  install -vDm 644 "resources/$pkgname-wayland.desktop" -t "$pkgdir/usr/share/wayland-sessions/"
  # udev rules
  install -vDm 644 resources/99-$pkgname.rules -t "$pkgdir/usr/lib/udev/rules.d/"
  # docs
  install -vDm 644 {CHANGELOG,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
