# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qtile
pkgver=0.34.1
pkgrel=2
pkgdesc="A full-featured, pure-Python tiling window manager"
arch=(x86_64)
url="https://qtile.org/"
_url="https://github.com/qtile/qtile"
license=(MIT)
depends=(
  cairo
  gdk-pixbuf2
  glibc
  libinput
  libnotify
  librsvg
  pango
  python
  python-cairocffi
  python-cffi
  python-gobject
  python-xcffib
  wayland
  wlroots0.19
)
makedepends=(
  git
  libpulse
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
  wayland-protocols
)
checkdepends=(
  graphviz
  gtk3
  imagemagick
  lm_sensors
  procps-ng
  python-aiohttp
  python-anyio
  python-bowler
  python-dbus-fast
  python-gobject
  python-isort
  python-libcst
  python-pytest
  python-pytest-asyncio
  python-pytest-httpbin
  python-xdg
  xorg-server-xephyr
  xorg-server-xvfb
  xorg-xrandr
  xorg-xwayland
  xterm
)
optdepends=(
  'alsa-utils: for volume widget'
  'canto-daemon: for canto widget'
  'cmus: for cmus widget'
  'jupyter_console: for interaction with qtile via Jupyter'
  'khal: for khal_calendar widget'
  'libpulse: for pulse_volume and pulseaudio_ffi widget'
  'lm_sensors: for sensors widget'
  'moc: for moc widget'
  'python-aiohttp: for generic poll text widget'
  'python-bowler: for migrating configuration files'
  'python-dbus-fast: for utils, notifications and several widgets'
  'python-isort: for migrating configuration files'
  'python-iwlib: for wlan widget'
  'python-keyring: for imapwidget widget'
  'python-libcst: for migrations'
  'python-mpd2: mpd2widget widget'
  'python-prompt_toolkit: for repl script'
  'python-psutil: graph, net and memory widget'
  'python-pytz: clock widget'
  'python-setproctitle: change process name to qtile'
  'python-xdg: launchbar widget'
  'xorg-xwayland: for XWayland support'
)
install=$pkgname.install
source=(
  "git+$_url?signed#tag=v$pkgver"
  "$pkgname-fix-test-fd-leak.patch"
)
sha512sums=('62955251b255109f0552ae272ef72d12ea8416c0c4032d7199770c5789b12103417eaf8bf15d25697a79a2898886a2b2ccccf89f995fae2874331020c7d61ca2'
            '76bb61721332066809d503b45aa6c81aea2a89c1bded668913190b8354ae2b4ec06a4cb5c39f7821e942f010609c6566edb24fc86ad6b418d0ff49f307c5f3ae')
b2sums=('336f9ef541ff5e0a8cbe33474e187d3b428117a06e2459e88085011523b4e960f58339e1184e8b9571c531fa311aaf909567aaf2b49c78092ecab4bc40063fb7'
        '81c79fc4f5b9a0ef5f1ec86314f091ff77c830415cee0da44bbee694fe165932642e085280a5de17f87d8d869e8b2d71f72883ef780a221383af7902f892d2c4')
validpgpkeys=(
  '3CCAB226289DE0160C61BDB418D18F1BC464DCA3' # Tycho Andersen <tycho@tycho.pizza>
  '35D92E7CC7357A81173EA1C974F9FDD20984FBEC' # Matt Colligan <mcol@posteo.net>
  'A6BAA1E17D2664ADB97B2C6F58A9AA7C86727DF7' # elParaguayo <elparaguayocode@gmail.com>
)

prepare() {
  patch -d $pkgname -Np1 -i ../$pkgname-fix-test-fd-leak.patch
  # adjust group used for udev rules from sudo to wheel
  sed 's/sudo/wheel/g' -i $pkgname/resources/99-$pkgname.rules
}

build() {
  export CFLAGS="$CFLAGS -I/usr/include/wlroots0.19"
  export LDFLAGS="$LDFLAGS -L/usr/lib/wlroots0.19"
  cd $pkgname
  PYTHONPATH="$PWD" python ./libqtile/backend/wayland/cffi/build.py
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --backend x11
    --backend wayland
    # Failing inhibitor tests: https://github.com/qtile/qtile/issues/5723
    --deselect 'test/backend/wayland/test_idle_inhibit.py::test_inhibitor_open[1-x11-InhibitorConfig]'
    --deselect 'test/backend/wayland/test_idle_inhibit.py::test_inhibitor_visible[1-x11-InhibitorConfig]'
    --deselect 'test/backend/wayland/test_idle_inhibit.py::test_inhibitor_focus[1-x11-InhibitorConfig]'
    --deselect 'test/backend/wayland/test_idle_inhibit.py::test_inhibitor_fullscreen[1-x11-InhibitorConfig]'
    --deselect 'test/backend/wayland/test_idle_inhibit.py::test_inhibitor_global[1-x11-InhibitorConfig]'
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  LC_TYPE=en_US.UTF-8 \
  PYTHONPATH="$PWD/test_dir/$_site_packages:$PYTHONPATH" \
  PATH="$PWD/test_dir/usr/bin:$PATH" \
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  # license
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  # config
  install -vDm 644 libqtile/resources/default_config.py -t "$pkgdir/usr/share/doc/$pkgname/"
  # desktop files
  install -vDm 644 "resources/$pkgname.desktop" -t "$pkgdir/usr/share/xsessions/"
  install -vDm 644 "resources/$pkgname-wayland.desktop" -t "$pkgdir/usr/share/wayland-sessions/"
  # udev rules
  install -vDm 644 resources/99-$pkgname.rules -t "$pkgdir/usr/lib/udev/rules.d/"
  # docs
  install -vDm 644 {CHANGELOG,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
