# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Erik Reider <erik.reider@protonmail.com>

pkgname=swaync
_pkgname=SwayNotificationCenter
pkgver=0.10.1
pkgrel=4
pkgdesc="A simple GTK based notification daemon for Sway"
url="https://github.com/ErikReider/SwayNotificationCenter"
arch=('x86_64')
license=('GPL-3.0-only')
depends=('gtk3' 'gtk-layer-shell' 'glib2' 'libgee' 'json-glib' 'libhandy' 'libpulse' 'granite')
makedepends=('vala' 'meson' 'git' 'scdoc' 'sassc' 'gobject-introspection')
optdepends=('gvfs: Get album art in MPRIS widget')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        'fix_notification_icons_with_libnotify_0.8.4.patch')
sha256sums=('5586d8a679dde5e530cb8b6f0c86abdd0d5e41362fc1c4e56e2211edea0f7a13'
            '5feeef8b4b6c819942e3ab16492f5b9e4e9448051e3c96f5fff80257b12ee921')

prepare () {
	cd "${_pkgname}-${pkgver}"
	# Fix notification icons with libnotify >= 0.8.4
	# See https://github.com/ErikReider/SwayNotificationCenter/issues/511
	patch -Np1 -i "${srcdir}/fix_notification_icons_with_libnotify_0.8.4.patch"
}

build() {
	arch-meson "${_pkgname}-${pkgver}" build -Dscripting=true
	ninja -C build
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
	install -Dm 644 "${_pkgname}-${pkgver}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
