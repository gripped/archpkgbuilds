# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-gwebsockets
_pkgname=gwebsockets
pkgver=0.7
pkgrel=10
pkgdesc='GLib based websocket server written in Python'
arch=(any)
url='https://github.com/sugarlabs/gwebsockets'
license=(Apache-2.0)
depends=(
  glib2
  python
  python-gobject
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("git+https://github.com/sugarlabs/$_pkgname.git#tag=v$pkgver")
b2sums=(e9536085a57545be72b1fac01be68eca26f7e50573415ed328e1015f30f56a9b8624bec852caa85d0d461f542889699875d6f9daf726392dada05ab66d4b24ff)

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
