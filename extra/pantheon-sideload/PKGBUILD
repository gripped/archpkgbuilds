# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-sideload
pkgver=6.3.0
pkgrel=2
pkgdesc='Sideload Flatpaks on elementary OS'
arch=(x86_64)
url=https://github.com/elementary/sideload
license=(GPL3)
depends=(
  flatpak
  gtk4
  libgranite-7.so
  libxml2
)
makedepends=(
  git
  intltool
  meson
  vala
)
groups=(pantheon)
_tag=22880ed52332e1078e8e165f57402b30d67c7f26
source=(pantheon-sideload::git+https://github.com/elementary/sideload.git#tag=${_tag})
sha256sums=('dd34dfbb6ebb40eeaefb84611b4e9da50fb477243b844ec7517daac0d0f95223')

pkgver() {
  cd pantheon-sideload
  git describe --tags
}

build() {
  arch-meson pantheon-sideload build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
