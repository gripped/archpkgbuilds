# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgname=qstardict
pkgver=3.0.0
pkgrel=1
pkgdesc="Qt clone of StarDict with full support of StarDict dictionaries"
provides=('stardict')
depends=('hicolor-icon-theme' 'qt6-base')
makedepends=('qt6-tools')
arch=('x86_64')
url="http://qstardict.ylsoftware.com/index.php"
license=('GPL2')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/a-rodin/qstardict/archive/$pkgver.tar.gz)
sha256sums=('dbaf26711ad8ffa36c2befd3406b59c5c8d94d02ce35f377122aad5582f85df9')

build() {
  cd $pkgname-$pkgver

  qmake6 ENABLED_PLUGINS="stardict swac web"
  make
}

package() {
  cd $pkgname-$pkgver

  make INSTALL_ROOT="${pkgdir}" install
}

