# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=decasify
pkgname=("$pkgbase" "lua-$pkgbase" "lua53-$pkgbase" "lua52-$pkgbase" "lua51-$pkgbase" "neovim-$pkgbase" "python-$pkgbase" "sile-$pkgbase")
pkgver=0.9.0
_rockrel=1
pkgrel=1
pkgdesc='cast strings to title-case according to locale specific style guides including Turkish'
arch=(x86_64)
url="https://github.com/alerque/$pkgbase"
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc)
makedepends=(cargo
             lua{,-luarocks-build-rust-mlua}
             lua51{,-luarocks-build-rust-mlua}
             lua52{,-luarocks-build-rust-mlua}
             lua53{,-luarocks-build-rust-mlua}
             luarocks
             python-{build,installer,wheel}
             python-maturin
             jq)
_archive="$pkgbase-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.zst"{,.asc}
        "$pkgname-$pkgver-typst-Cargo.toml::$url/raw/refs/tags/v$pkgver/typst/Cargo.toml"
        "$pkgname-$pkgver-typst-lib.rs::$url/raw/refs/tags/v$pkgver/typst/src/lib.rs")
sha256sums=('3bec394fa02877e29b025aa24a9d9cd2087a93119904095c43786c1e4e8f1429'
            'SKIP'
            '9c546b09a9a94232e898c424e02fec8918fd14c6609a7f27de1a9a0404ee1886'
            '2fc534d096616462512db93fa5e4dd3558816235e5cebb5f28f1c17eb8421ebf')
validpgpkeys=('9F377DDB6D3153A48EB3EB1E63CC496475267693') # Caleb Maclennan <caleb@alerque.com> (@alerque)

prepare() {
	cd "$_archive"
	# v0.9.0 sources are missing a workspace manifest
	mkdir -p typst/src
	cp ../${source[2]%::*} typst/Cargo.toml
	cp ../${source[3]%::*} typst/src/lib.rs
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	export CARGO_FEATURE_FLAGS==--offline
	./configure --prefix /usr
	make
	python -m build -wn
}

check() {
	cd "$_archive"
	./$pkgbase -l tr -c title 'ılIk ilkİ' | grep -Fx 'Ilık İlki'
	# export CARGO_FEATURE_FLAGS==--offline
	# check requires lua & python developer tooling, needs to be reworked for a simple selfcheck
	# make check
}

package_decasify() {
	cd "$_archive"
	pkgdesc+=' (CLI tool)'
	make DESTDIR="$pkgdir" install
}

_package() {
	cd "$_archive"
	pkgdesc+=" (Rock for Lua $1)"
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		make --deps-mode none --no-manifest "rockspecs/$_archive-$_rockrel.rockspec"
}

package_lua-decasify() {
	_package 5.4
}

package_lua51-decasify() {
	_package 5.1
}

package_lua52-decasify() {
	_package 5.2
}

package_lua53-decasify() {
	_package 5.3
}

package_neovim-decasify() {
	cd "$_archive"
	pkgdesc+=' (Neovim plugin)'
	depends=("lua51-$pkgbase=$pkgver"
	         neovim)
	find plugin -type f -exec \
		install -Dm0644 {} "$pkgdir/usr/share/nvim/site/pack/dist/start/$pkgbase/{}" \;
}

package_python-decasify() {
	cd "$_archive"
	pkgdesc+=' (Python module)'
	depends=(python)
	python -m installer -d "$pkgdir" dist/*.whl
}

package_sile-decasify() {
	cd "$_archive"
	pkgdesc+=' (SILE package)'
	depends=("lua51-$pkgbase=$pkgver"
	         sile)
	luarocks --lua-version 5.1 --tree "$pkgdir/usr/" \
		make --deps-mode none --no-manifest "rockspecs/$pkgbase.sile-$pkgver-$_rockrel.rockspec"
}
