# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=decasify
pkgname=("$pkgbase" "lua-$pkgbase" "lua53-$pkgbase" "lua52-$pkgbase" "lua51-$pkgbase" "neovim-$pkgbase" "python-$pkgbase" "sile-$pkgbase")
pkgver=0.7.3
_rockrel=1
pkgrel=1
pkgdesc='cast strings to title-case according to locale specific style guides including Turkish'
arch=(x86_64)
url="https://github.com/alerque/$pkgbase"
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc)
makedepends=(cargo
             lua{,-luarocks-build-rust-mlua}
             lua51{,-luarocks-build-rust-mlua}
             lua52{,-luarocks-build-rust-mlua}
             lua53{,-luarocks-build-rust-mlua}
             luarocks
             python-{build,installer,wheel}
             python-maturin
             jq)
_archive="$pkgbase-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.zst"{,.asc}
	"https://luarocks.org/manifests/alerque/decasify.sile-$pkgver-$_rockrel.rockspec"
	"https://raw.githubusercontent.com/alerque/decasify/refs/tags/v$pkgver/sile/decasify.lua")
sha256sums=('210cbe39f4fdc6320080d48a3a3c753540a5e810bf041752f962ee0af2862775'
            'SKIP'
            '89a9dd09cbcacf92f94aa16c0d959e80657252ec8dcbe0b43174591e46c7bbbf'
            '8749eb5e5af00806206d6e8879c36ed2bf7bd01e7f43d3cae90e4e733995c01a')
validpgpkeys=('9F377DDB6D3153A48EB3EB1E63CC496475267693') # Caleb Maclennan <caleb@alerque.com> (@alerque)

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
	# Make up for upstream dist package missing files in v0.7.3
	mkdir -p rockspecs sile
	cp ../${source[2]##*/} rockspecs
	cp ../${source[3]##*/} sile
}

build() {
	cd "$_archive"
	export CARGO_FEATURE_FLAGS==--offline
	./configure --prefix /usr
	make
	python -m build -wn
}

check() {
	cd "$_archive"
	./$pkgbase -l tr -c title 'ılIk ilkİ' | grep -Fx 'Ilık İlki'
	# export CARGO_FEATURE_FLAGS==--offline
	# check requires lua & python developer tooling, needs to be reworked for a simple selfcheck
	# make check
}

package_decasify() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}

_package() {
	cd "$_archive"
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		make --deps-mode none --no-manifest "rockspecs/$_archive-$_rockrel.rockspec"
}

package_lua-decasify() {
	_package 5.4
}

package_lua51-decasify() {
	_package 5.1
}

package_lua52-decasify() {
	_package 5.2
}

package_lua53-decasify() {
	_package 5.3
}

package_neovim-decasify() {
	cd "$_archive"
	depends=("lua51-$pkgbase=$pkgver"
	         neovim)
	find plugin -type f -exec \
		install -Dm0644 {} "$pkgdir/usr/share/nvim/site/pack/dist/start/$pkgbase/{}" \;
}

package_python-decasify() {
	depends=(python)
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}

package_sile-decasify() {
	cd "$_archive"
	depends=("lua51-$pkgbase=$pkgver"
	         sile)
	luarocks --lua-version 5.1 --tree "$pkgdir/usr/" \
		make --deps-mode none --no-manifest "rockspecs/$pkgbase.sile-$pkgver-$_rockrel.rockspec"
}
