# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=decasify
pkgname=("$pkgbase" "lua-$pkgbase" "lua53-$pkgbase" "lua52-$pkgbase" "lua51-$pkgbase" "neovim-$pkgbase" "python-$pkgbase" "sile-$pkgbase")
pkgver=0.11.2
_rockrel=1
pkgrel=2
pkgdesc='cast strings to title-case according to locale specific style guides including Turkish'
arch=(x86_64)
url="https://github.com/alerque/$pkgbase"
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc)
makedepends=(cargo
             jq
             lua{,-luarocks-build-rust-mlua}
             lua51{,-luarocks-build-rust-mlua}
             lua52{,-luarocks-build-rust-mlua}
             lua53{,-luarocks-build-rust-mlua}
             luarocks
             python-{build,installer,wheel}
             python-maturin)
_archive="$pkgbase-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.zst"{,.asc})
sha256sums=('f43794dd8e6f4e1a75132a9892d69f8b4f5272d96dc0909c04915be09eda2e1e'
            'SKIP')
validpgpkeys=('9F377DDB6D3153A48EB3EB1E63CC496475267693') # Caleb Maclennan <caleb@alerque.com> (@alerque)

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
	cd "$_archive"
	export CARGO_FEATURE_FLAGS==--offline
	./configure --prefix /usr
	make
	python -m build -wn
}

check() {
	cd "$_archive"
	./$pkgbase -l tr -c title 'ılIk ilkİ' | grep -Fx 'Ilık İlki'
	# export CARGO_FEATURE_FLAGS==--offline
	# check requires lua & python developer tooling, needs to be reworked for a simple selfcheck
	# make check
}

package_decasify() {
	cd "$_archive"
	pkgdesc+=' (CLI tool)'
	make DESTDIR="$pkgdir" install
}

_package() {
	cd "$_archive"
	pkgdesc+=" (Rock for Lua $1)"
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		make --deps-mode none --no-manifest "rockspecs/$_archive-$_rockrel.rockspec"
}

package_lua-decasify() {
	_package 5.4
}

package_lua51-decasify() {
	_package 5.1
}

package_lua52-decasify() {
	_package 5.2
}

package_lua53-decasify() {
	_package 5.3
}

package_neovim-decasify() {
	cd "$_archive"
	pkgdesc+=' (Neovim plugin)'
	depends=("lua51-$pkgbase=$pkgver"
	         neovim)
	find plugin -type f -exec \
		install -Dm0644 {} "$pkgdir/usr/share/nvim/site/pack/dist/start/$pkgbase/{}" \;
}

package_python-decasify() {
	cd "$_archive"
	pkgdesc+=' (Python module)'
	depends=(python)
	python -m installer -d "$pkgdir" dist/*.whl
}

package_sile-decasify() {
	cd "$_archive"
	pkgdesc+=' (SILE package)'
	depends=("lua51-$pkgbase=$pkgver"
	         sile)
	luarocks --lua-version 5.1 --tree "$pkgdir/usr/" \
		make --deps-mode none --no-manifest "rockspecs/$pkgbase.sile-$pkgver-$_rockrel.rockspec"
}
