# Maintainer:  Robin Candau <antiz@archlinux.org>
# Contributor:  twa022 <twa022 at gmail dot com>

pkgname=xfce4-indicator-plugin
pkgver=2.4.2
pkgrel=2
pkgdesc="Plugin to display information from applications in the Xfce4 panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-indicator-plugin/start"
license=('GPL-2.0-or-later')
depends=('hicolor-icon-theme' 'libayatana-indicator' 'xfce4-panel' 'xdg-utils')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-indicator-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('a04ee0baccea7c4873b1a5cc1842c6dc25836cc614a6657b378206f0302a8427')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
