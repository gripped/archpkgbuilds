# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ty
pkgver=0.0.9
pkgrel=1
pkgdesc='Extremely fast Python type checker and language server, written in Rust'
arch=(x86_64)
url=https://github.com/astral-sh/ty
license=(MIT)
depends=(python)
makedepends=(
  git
  maturin
  python-installer
)
options=(!lto)
source=(
  "git+$url.git#tag=$pkgver"
  git+https://github.com/astral-sh/ruff.git
)
b2sums=('c21f5217b2ad9cbf734f4bb442c04d75358d35f23e2058e1c7ffd92e6c71d6524c418fe823de70988ae54a535d07b1418de8bdc760fa000edee0fb97edd51dfb'
        'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.ruff.url ../ruff
  git -c protocol.file.allow=always submodule update
  cargo fetch --manifest-path ruff/crates/$pkgname/Cargo.toml --locked --target "$(rustc --print host-tuple)"
}

build() {
  local target="$(rustc --print host-tuple)"
  local target_binary="ruff/target/$target/release/$pkgname"

  cd $pkgname
  maturin build --locked --release --all-features --target "$target" --strip

  for completion in bash elvish fish nushell zsh; do
    $target_binary generate-shell-completion $completion > $completion-completions
  done
}

check() {
  cd $pkgname
  cargo test --manifest-path ruff/crates/$pkgname/Cargo.toml --frozen --all-features
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/licenses/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname

  cd $pkgname
  python -m installer --destdir="$pkgdir" ruff/target/wheels/*.whl
  install -Dm644 bash-completions "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  install -Dm644 elvish-completions "$pkgdir"/usr/share/elvish/lib/$pkgname.elv
  install -Dm644 fish-completions "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgname.fish
  install -Dm644 nushell-completions "$pkgdir"/usr/share/nushell/vendor/autoload/$pkgname.nu
  install -Dm644 zsh-completions "$pkgdir"/usr/share/zsh/site-functions/_$pkgname
}
