# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
epoch=1
pkgver=3.7
pkgrel=2
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('x86_64')
url="https://www.poedit.net/"
license=('MIT')
depends=(
  'gcc-libs'
  'glib2'
  'glibc'
  'gtk3'
  'gtkspell3'
  'icu'
  'lucene++'
  'webkit2gtk-4.1'
  'wxwidgets-gtk3'
)
makedepends=('boost')
source=(https://github.com/vslavik/poedit/releases/download/v$pkgver-oss/poedit-${pkgver}.tar.gz
        wxwidgets-3.3.patch
        boost-1.89.patch)
sha256sums=('1bb2388f03618991effde4e8d0e8c35db363af6ca58f6d8ebab21daa76731d71'
            'ffc24ca06fc993b33ee277baa261e7e958486398f524366f51c6d670b2100aeb'
            'ff3609e78cda128be854f4a432321d69d0d10d5f5a5a48dd10fd9342607f6157')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../wxwidgets-3.3.patch
  patch -p1 < ../boost-1.89.patch
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --libexecdir=/usr/lib/poedit
  make CPPFLAGS+=' -DUCHAR_TYPE=uint16_t'
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
