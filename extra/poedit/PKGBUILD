# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
epoch=1
pkgver=3.5.2
pkgrel=2
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('x86_64')
url="https://www.poedit.net/"
license=('MIT')
depends=(
  'gcc-libs'
  'glib2'
  'glibc'
  'gtk3'
  'gtkspell3'
  'icu'
  'lucene++'
  'webkit2gtk-4.1'
  'wxwidgets-gtk3'
)
makedepends=('boost')
source=(https://github.com/vslavik/poedit/releases/download/v$pkgver-oss/poedit-${pkgver}.tar.gz)
sha256sums=('06c540ab6023a5f6f054e4b44f391c7e339d12a2d1c1a9a9d53bcab298d77835')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --libexecdir=/usr/lib/poedit
  make CPPFLAGS+=' -DUCHAR_TYPE=uint16_t'
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
