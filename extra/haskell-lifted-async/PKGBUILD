# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lifted-async
pkgname=haskell-lifted-async
pkgver=0.11.0
pkgrel=13
pkgdesc="Run lifted IO operations asynchronously and wait for their results"
url="https://github.com/maoe/lifted-async"
license=('BSD-3-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-constraints' 'haskell-lifted-base'
         'haskell-monad-control' 'haskell-transformers-base')
makedepends=('ghc')
checkdepends=('haskell-tasty' 'haskell-tasty-expected-failure' 'haskell-tasty-hunit'
              'haskell-tasty-th')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('cbe8e22288dfb41e318555f0948e506d80f57a80cf4e49e8ecb39bb1c3bf7f4ff1106a5b571634b4e24092a953e41bfd1725d03a133242dc35c3d6324d42e1b7')

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
