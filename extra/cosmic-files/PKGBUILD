# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-files
pkgver=1.0.0.alpha.4
pkgrel=1
pkgdesc='File manager for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-files
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glib2
  glibc
  cosmic-icon-theme
  libxkbcommon
  xdg-utils
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_tag=2fa8e6adc44448bd5ac749302154f8f670e7f381
source=(
  git+https://github.com/pop-os/cosmic-files.git#tag=${_tag}
  cosmic-files-lto.patch
)
b2sums=('585aca67811144b81c3dccd070f6ea09af5583c765060724c8ca8d7101f105a4edc66bbdc5cef308336ce4d9f815bd291591018bab14778ae4bfc4f7f3bcd461'
        '105231f886affb3274d18bbe95d0fc27ba33cb557245ca567caccc9b0f5fbae0b7d92a81c0b2784130659e81b73e374ac735f8ad9b06adad18b4e4fd227164b5')

prepare() {
  cd cosmic-files
  patch -Np1 -i ../cosmic-files-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-files
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-files
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  CC=clang just build-release --frozen
}

package() {
  cd cosmic-files
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
