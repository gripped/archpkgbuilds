# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-files
pkgver=1.0.0.alpha.3
pkgrel=1
pkgdesc='File manager for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-files
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glib2
  glibc
  cosmic-icon-theme
  libxkbcommon
  xdg-utils
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_tag=20f9292e7925da20086c7f1f3c8e3a8d03d63b99
source=(
  git+https://github.com/pop-os/cosmic-files.git#tag=${_tag}
  cosmic-files-lto.patch
)
b2sums=('e7406f23a802d3daccc4369eb4b99cc5c51d35b616b9515c88c0f3edbc18b04865b5561c87ca416a63d95f035442ad5595f5a000c940b25c0a194912208e1be9'
        '105231f886affb3274d18bbe95d0fc27ba33cb557245ca567caccc9b0f5fbae0b7d92a81c0b2784130659e81b73e374ac735f8ad9b06adad18b4e4fd227164b5')

prepare() {
  cd cosmic-files
  patch -Np1 -i ../cosmic-files-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-files
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-files
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  CC=clang just build-release --frozen
}

package() {
  cd cosmic-files
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
