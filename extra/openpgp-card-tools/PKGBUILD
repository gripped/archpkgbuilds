# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-tools
pkgver=0.11.8
pkgrel=1
pkgdesc="CLI tool to inspect, manage and use OpenPGP cards"
arch=(x86_64)
url="https://codeberg.org/openpgp-card/openpgp-card-tools"
license=('Apache-2.0 OR MIT')
depends=(
  ccid
  gcc-libs
  glibc
)
makedepends=(
  cargo
  pcsclite
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('d86b4f3b003e94d834e99c2319fa41fbba6ab905a05b6511c109dcbfb8f0934f81fa39897b0209076b1a1b02562175d30b05dd57622978380a092e46124f9b47')
b2sums=('da0bf0a67a8d33fa6d8f2bf0504e0cc62fda1f4aeeeb3d8bc9219ca5c875693d8cb3fe8b5c797afd7679f817d6742f220c585d70b93c72d61bb56e47ae4427ad')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
  OCT_MANPAGE_OUTPUT_DIR="$PWD/../man/" cargo run
  OCT_COMPLETION_OUTPUT_DIR="$PWD/../completion/" cargo run
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $pkgname
  install -vDm 755 target/release/oct -t "$pkgdir/usr/bin/"
  install -vDm 644 {scripting,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 ../man/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 ../completion/oct.bash "$pkgdir/usr/share/bash-completion/completions/oct"
  install -vDm 644 ../completion/_oct -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 ../completion/oct.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
}
