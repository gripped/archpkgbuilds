# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Omar Sandoval <osandov AT osandov DOT com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski AT archlinux.org>
# Contributor: Dan McGee <dpmcgee AT gmail.com>

pkgname=sparse
pkgver=0.6.5+rc1
pkgrel=1
pkgdesc='Semantic parser for C'
arch=(x86_64)
url='https://sparse.docs.kernel.org/en/latest/'
license=(MIT)
depends=(
  bash
  glibc
  libgcc
  libxml2
  perl
  sqlite
)
makedepends=(
  git
  gtk3
  llvm
)
optdepends=(
  'gtk3: for test-inspect'
  'llvm-libs: for sparse-llvm'
  'llvm: for sparsec'
)
_commit=37156835e3d725b6d750f000be33ba3814bb2310  # master
source=(
  "git+https://git.kernel.org/pub/scm/devel/sparse/sparse.git#commit=$_commit"
)
sha512sums=('c1f08598818257fd4e3e2d4432d58e1d308cb9d25dc2e0ff1576279596483cf8300108bf1ce6f2217c2486c3a52f5531bd599cd619c7a51f09a46394f20418fa')
b2sums=('e2796781936e36a784671edcb18844a5eccda001ff3a2f173903277badc8a0a39d1cce06189ec94a7d95a420d8ddd3abd8b3d1fce8849982796d95b372635881')

pkgver() {
  cd sparse
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd sparse
}

build() {
  make -C sparse
}

check() {
  make -C sparse check
}

package() {
  cd sparse

  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
