# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=ibus-typing-booster
pkgver=2.30.0
pkgrel=1
pkgdesc="Predictive input method for the IBus platform"
url="https://mike-fabian.github.io/ibus-typing-booster/"
arch=(any)
license=(
  Apache-2.0
  GPL-3.0-or-later
)
depends=(
  bash
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  hicolor-icon-theme
  ibus
  libibus
  libsoup3
  m17n-db
  m17n-lib
  pango
  python
  python-atspi
  python-dbus
  python-distro
  python-gobject
  python-httpx
  python-nltk
  python-pyaudio
  python-pycountry
  python-pyenchant
  python-pykakasi
  python-pyxdg
  python-rapidfuzz
  python-regex
  sqlite
  wl-clipboard
)
makedepends=(
  git
)
source=("git+https://github.com/mike-fabian/ibus-typing-booster#tag=$pkgver")
b2sums=('fa5bc83727bc766773573f04faabc57844f74fc5729646302a2ad81a79d96989fdf7607e892be8a1f816b394a24803475b32955652fa4a9ba5864a3632cf5f0c')

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib/ibus
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  cd "$pkgdir/usr/share/$pkgname/data"
  gzip -nf --best *.{txt,json} annotations{,Derived}/*.xml
}

# vim:set sw=2 sts=-1 et:
