# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=ibus-typing-booster
pkgver=2.27.67
pkgrel=1
pkgdesc="Predictive input method for the IBus platform"
url="https://mike-fabian.github.io/ibus-typing-booster/"
arch=(any)
license=(
  Apache-2.0
  GPL-3.0-or-later
)
depends=(
  bash
  dconf
  gdk-pixbuf2
  glib2
  gtk3
  hicolor-icon-theme
  ibus
  libibus
  m17n-db
  m17n-lib
  pango
  python
  python-atspi
  python-dbus
  python-distro
  python-gobject
  python-nltk
  python-pycountry
  python-pyenchant
  python-pykakasi
  python-rapidfuzz
  python-regex
  sqlite
)
makedepends=(
  git
  python-pygame
)
optdepends=(
  'python-pygame: Audible feedback (sounds)'
)
source=("git+https://github.com/mike-fabian/ibus-typing-booster#tag=$pkgver")
b2sums=('b7f205ffd4b13259b422938d2f59bbaddc9c7ba67770b1a9b14a089661807170c6663d551b08834e1abcc9f4f72e4d74a71e55786e7c084bcaa376c848f93fae')

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib/ibus
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  cd "$pkgdir/usr/share/$pkgname/data"
  gzip -nf --best *.{txt,json} annotations{,Derived}/*.xml
}

# vim:set sw=2 sts=-1 et:
