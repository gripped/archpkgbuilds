# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=ibus-typing-booster
pkgver=2.27.34
pkgrel=1
pkgdesc="Predictive input method for the IBus platform"
url="https://mike-fabian.github.io/ibus-typing-booster/"
arch=(any)
license=(
  Apache-2.0
  GPL-3.0-or-later
)
depends=(
  bash
  dconf
  gdk-pixbuf2
  glib2
  gtk3
  hicolor-icon-theme
  ibus
  libibus
  m17n-db
  m17n-lib
  pango
  python
  python-atspi
  python-dbus
  python-distro
  python-gobject
  python-nltk
  python-pycountry
  python-pyenchant
  python-regex
  sqlite
)
makedepends=(
  git
  python-pygame
)
optdepends=(
  'python-pygame: Audible feedback (sounds)'
)
source=("git+https://github.com/mike-fabian/ibus-typing-booster#tag=$pkgver")
b2sums=('ce347a4b14a94b022a04704e5402dee35fe948c6729c0604569fa3cca4d3b3c431f2c158dd86b40684f07f7c23e493cc8215913c0fda1cedd24e46bc0899d047')

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib/ibus
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  cd "$pkgdir/usr/share/$pkgname/data"
  gzip -nf --best *.{txt,json} annotations{,Derived}/*.xml
}

# vim:set sw=2 sts=-1 et:
