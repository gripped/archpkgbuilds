# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=ibus-typing-booster
pkgver=2.27.33
pkgrel=1
pkgdesc="Predictive input method for the IBus platform"
url="https://mike-fabian.github.io/ibus-typing-booster/"
arch=(any)
license=(
  Apache-2.0
  GPL-3.0-or-later
)
depends=(
  bash
  dconf
  gdk-pixbuf2
  glib2
  gtk3
  hicolor-icon-theme
  ibus
  libibus
  m17n-db
  m17n-lib
  pango
  python
  python-atspi
  python-dbus
  python-distro
  python-gobject
  python-nltk
  python-pycountry
  python-pyenchant
  python-regex
  sqlite
)
makedepends=(
  git
  python-pygame
)
optdepends=(
  'python-pygame: Audible feedback (sounds)'
)
source=("git+https://github.com/mike-fabian/ibus-typing-booster#tag=$pkgver")
b2sums=('710282103d6a2b031c8f142c5c6ae35163cd6161ab855509a9ac2a3787341b89e43c1caec4f204916385d8325ec9e6f9f6ced8c945ecb868082da9dca4a1435c')

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib/ibus
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  cd "$pkgdir/usr/share/$pkgname/data"
  gzip -nf --best *.{txt,json} annotations{,Derived}/*.xml
}

# vim:set sw=2 sts=-1 et:
