# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=hamlib
pkgver=4.6.5
pkgrel=2
pkgdesc='Ham radio equipment control libraries'
arch=('x86_64')
url='https://hamlib.github.io'
license=(
  'GPL-2.0-or-later'
  'LGPL-2.0-or-later'
)
depends=(
  'gcc-libs'
  'glibc'
  'libindi'
  'libusb'
  'readline'
)
makedepends=(
  'lua'
  'python'
  'swig'
  'tcl'
)
optdepends=(
  'lua: Lua bindings'
  'perl: Perl bindings'
  'python: Python bindings'
  'tcl: Tcl bindings'
)
provides=(
  'libhamlib++.so'
  'libhamlib.so'
)
source=("https://github.com/Hamlib/Hamlib/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('02e626866fa1dba63cc83923938d6632d54bdb10fea1970110fa59f0dbf06c1490142de30b66f4a00fa98cee49eda271b3bbdc114c8a753977655544dccfdb66')
validpgpkeys=('82D64F6B0E67CD41F689BBA6FB2C5130D55A8819') # Nate Bargmann <n0nb@n0nb.us>

prepare() {
  cd Hamlib-$pkgver
  sed -i 's/Hamlib-pl.mk install_site/Hamlib-pl.mk install/g' bindings/Makefile.am
  autoreconf -fiv
}

build() {
  cd Hamlib-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-perl-binding \
    --with-python-binding \
    --with-tcl-binding \
    --with-lua-binding \
    --with-xml-support \
    --with-indi \
    --with-readline \
    --disable-static
  make
}

check() {
  cd Hamlib-$pkgver
  make check
}

package() {
  cd Hamlib-$pkgver
  make DESTDIR="$pkgdir" INSTALLDIRS=vendor install
  rm -rv "$pkgdir"/usr/lib/perl5/*/core_perl
}
