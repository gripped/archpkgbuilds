# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=hamlib
pkgver=4.6.3
pkgrel=2
pkgdesc='Ham radio equipment control libraries'
arch=('x86_64')
url='https://hamlib.github.io'
license=('GPL' 'LGPL')
depends=('perl' 'python' 'lua' 'tcl' 'libxml2' 'libusb-compat' 'libtool' 'libuhd' 'libindi')
makedepends=('swig')
#source=("https://github.com/Hamlib/Hamlib/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
source=(https://github.com/Hamlib/Hamlib/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('a96d08745d4677a4c9717746e74379fff197413a0b663f2f3cc64053888c3ed4994ad62a809476ec533a070993976ccf1e78ce5b86c6481e14602e6aee4ef5c0')
validpgpkeys=('82D64F6B0E67CD41F689BBA6FB2C5130D55A8819') # Nate Bargmann <n0nb@n0nb.us>

build() {
  cd Hamlib-$pkgver

  ./bootstrap
  CFLAGS+=" -Wno-implicit-function-declaration" \
  ./configure \
  	--prefix=/usr \
  	--sbindir=/usr/bin \
  	--with-perl-binding \
  	--with-python-binding \
  	--with-tcl-binding \
  	--with-lua-binding \
  	--with-xml-support \
  	--disable-uhd  # https://github.com/Hamlib/Hamlib/issues/355

  make
}

package() {
  cd Hamlib-$pkgver

  make DESTDIR="$pkgdir" install
}
