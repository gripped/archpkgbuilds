# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>
# Contributor: Yassine Oudjana <y.oudjana@protonmail.com>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: GI Jack <GI_Jack@hackermail.com>

pkgname=gnome-calls
pkgver=49.1
pkgrel=1
pkgdesc="Phone dialer and call handler"
url="https://gitlab.gnome.org/GNOME/calls"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  callaudiod
  dconf
  evolution-data-server
  feedbackd
  folks
  gcc-libs
  glib2
  glibc
  gom
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libmm-glib
  libpeas-2
  libsecret
  sofia-sip
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
  python-docutils
  vala
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/calls.git?signed#tag=v${pkgver/[a-z]/_&}"
  "git+https://gitlab.gnome.org/World/Phosh/libcall-ui.git"
)
b2sums=('2281949e84bdc649e400e14f0fb3c424a43112f1e975e2bf82535574093d24b1adfdb86d3c2b14f00673a109a805b453967e4eea7d716dd39e12e3d59eba5203'
        'SKIP')
validpgpkeys=(
  B9386554B7DD266BCB8E29A990F0C9B18A6B4A19 # Evangelos Ribeiro Tzaras <devrtz@fortysixandtwo.eu>
  0DB3932762F78E592F6522AFBB5A2C77584122D3 # Guido GÃ¼nther <agx@sigxcpu.org>
)

prepare() {
  cd calls

  git submodule init
  git submodule set-url subprojects/libcall-ui "$srcdir/libcall-ui"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson calls build "${meson_options[@]}"
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
