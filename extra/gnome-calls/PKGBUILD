# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>
# Contributor: Yassine Oudjana <y.oudjana@protonmail.com>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: GI Jack <GI_Jack@hackermail.com>

pkgname=gnome-calls
pkgver=49.1.1
pkgrel=1
pkgdesc="Phone dialer and call handler"
url="https://gitlab.gnome.org/GNOME/calls"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  callaudiod
  dconf
  evolution-data-server
  feedbackd
  folks
  gcc-libs
  glib2
  glibc
  gom
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libmm-glib
  libpeas-2
  libsecret
  sofia-sip
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
  python-docutils
  vala
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/calls.git?signed#tag=v${pkgver/[a-z]/_&}"
  "git+https://gitlab.gnome.org/World/Phosh/libcall-ui.git"
)
b2sums=('111fbd9eeaff172a17cb58a3ab52dfcc48d76bef87192d8d6648f4d039d5968b9326a5e40c86739fa5433bfd7bf6a2e4708059443d711875f8a89158e7277b8c'
        'SKIP')
validpgpkeys=(
  B9386554B7DD266BCB8E29A990F0C9B18A6B4A19 # Evangelos Ribeiro Tzaras <devrtz@fortysixandtwo.eu>
  0DB3932762F78E592F6522AFBB5A2C77584122D3 # Guido GÃ¼nther <agx@sigxcpu.org>
)

prepare() {
  cd calls

  git submodule init
  git submodule set-url subprojects/libcall-ui "$srcdir/libcall-ui"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson calls build "${meson_options[@]}"
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
