# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=marksman
pkgver=20251213
pkgrel=2
pkgdesc="Write Markdown with code assist and intelligence in the comfort of your favourite editor."
arch=('any')
url="https://github.com/artempyanykh/marksman"
license=('MIT')
makedepends=('dotnet-sdk-9.0' 'git')
depends=('dotnet-runtime-9.0')
options=('!strip')
source=("$pkgname"::git+https://github.com/artempyanykh/marksman.git#tag="${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}")
b2sums=('5b26b7c974ca9b996d4405d0649295d94770b0d5a4ca01864e43371cae128b82dd0eab4270fc185dff14e8f819fdf5053deafaf04ab5ed922c53efd667b10f20')

build() {
  cd "marksman"

  # Disable dotnet telemetry
  export DOTNET_CLI_TELEMETRY_OPTOUT=1

  make build
  make publish
}

package() {
  cd "marksman"
  install -Dm755 Marksman/bin/Release/net*/linux-x64/publish/marksman $pkgdir/usr/bin/marksman
}
