# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=marksman
pkgver=20260128
pkgrel=1
pkgdesc="Write Markdown with code assist and intelligence in the comfort of your favourite editor."
arch=('any')
url="https://github.com/artempyanykh/marksman"
license=('MIT')
makedepends=('dotnet-sdk-9.0' 'git')
depends=('dotnet-runtime-9.0')
options=('!strip')
source=("$pkgname"::git+https://github.com/artempyanykh/marksman.git#tag="${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}")
b2sums=('5d4587a7c65072e5aeceb9bc37d4e67823570f4efd0862ad7630bb440e80fcaf35feacf6605dfa509059bf366d8052623d6a7368dc6c6d49d9556ed7f723b96d')

build() {
  cd "marksman"

  # Disable dotnet telemetry
  export DOTNET_CLI_TELEMETRY_OPTOUT=1

  make build
  make publish
}

package() {
  cd "marksman"
  install -Dm755 Marksman/bin/Release/net*/linux-x64/publish/marksman $pkgdir/usr/bin/marksman
}
