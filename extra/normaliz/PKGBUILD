# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tarn Burton <twburton at gmail dot com>

pkgname=normaliz
pkgver=3.10.4
pkgrel=1
pkgdesc='A tool for computations in affine monoids, vector configurations, lattice polytopes, and rational cones'
arch=(x86_64)
url='https://www.normaliz.uni-osnabrueck.de/'
license=(GPL-3.0-or-later)
depends=(e-antic
         flint
         gcc-libs
         glibc
         gmp
         nauty)
makedepends=(boost
             git)
source=(git+https://github.com/Normaliz/Normaliz#tag=v$pkgver)
sha256sums=('0ec8cc67a947ce2eb74d07cfbe3fcdcef88fb73d7f41fcfaa26dca99a2e5623c')

prepare() {
  cd Normaliz
  autoreconf -vi
}

build() {
  cd Normaliz
  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd Normaliz
  make check
}

package() {
  cd Normaliz
  make DESTDIR="$pkgdir" install
}
