# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tarn Burton <twburton at gmail dot com>

pkgname=normaliz
pkgver=3.10.3
pkgrel=2
pkgdesc='A tool for computations in affine monoids, vector configurations, lattice polytopes, and rational cones'
arch=(x86_64)
url='https://www.normaliz.uni-osnabrueck.de/'
license=(GPL-3.0-or-later)
depends=(e-antic
         flint
         gcc-libs
         glibc
         gmp
         nauty)
makedepends=(boost
             git)
source=(git+https://github.com/Normaliz/Normaliz#tag=v$pkgver)
sha256sums=('a12ba6bbdfa93d8222d0388be3068f6a7de72396a0a96f4835ff1502dc1e1ad5')

prepare() {
  cd Normaliz
  autoreconf -vi
}

build() {
  cd Normaliz
  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd Normaliz
  make check
}

package() {
  cd Normaliz
  make DESTDIR="$pkgdir" install
}
