# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tarn Burton <twburton at gmail dot com>

pkgname=normaliz
pkgver=3.11.0
pkgrel=3
pkgdesc='A tool for computations in affine monoids, vector configurations, lattice polytopes, and rational cones'
arch=(x86_64)
url='https://www.normaliz.uni-osnabrueck.de/'
license=(GPL-3.0-or-later)
depends=(e-antic
         flint
         gcc-libs
         glibc
         gmp
         nauty)
makedepends=(boost
             git)
source=(git+https://github.com/Normaliz/Normaliz#tag=v$pkgver)
sha256sums=('181f45b9c1d33287dcda19afc1c702972f9c3b7252c98ff027a03ce735c28cc8')

prepare() {
  cd Normaliz
  autoreconf -vi
}

build() {
  cd Normaliz
  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd Normaliz
#  make check
#  many test failures with nauty 2.9 due to different permutation order
}

package() {
  cd Normaliz
  make DESTDIR="$pkgdir" install
}
