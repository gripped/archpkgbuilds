# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-mockito
pkgver=1.5.4
pkgrel=2
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest' 'python-numpy')
source=("git+https://github.com/kaste/mockito-python.git#tag=$pkgver")
sha512sums=('56b3674679f4d3cee2028331420d275433f97b62e2a305e1e53ed732b5d20abffd60555dd81f32bfcb1e2b2e52523fba73b94fa44722bb2c256bb7434e0abea0')

build() {
  cd mockito-python
  python -m build -nw
}

check() {
  cd mockito-python
  pytest
}

package() {
  cd mockito-python
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
