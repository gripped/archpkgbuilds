# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-mockito
pkgver=1.5.3
pkgrel=1
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest' 'python-numpy')
source=("git+https://github.com/kaste/mockito-python.git#tag=$pkgver")
sha512sums=('17b211d9376ba8657d066648f200489b1b9da1cdd77e49b5227c01b6b9b36129ee2c7ba3e8190da8d327dd4a5cb4af49ca97df309bb5f7c1d355856e02f5c567')

build() {
  cd mockito-python
  python -m build -nw
}

check() {
  cd mockito-python
  pytest
}

package() {
  cd mockito-python
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
