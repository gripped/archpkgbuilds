# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-mockito
pkgver=1.5.1
pkgrel=1
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest' 'python-numpy')
source=("git+https://github.com/kaste/mockito-python.git#tag=$pkgver")
sha512sums=('13ae57320eb48787ad75ec01f88373feb3ef4efebaefb933fb9201793f8838f0f054b9c408ad75db58ba55bca0afce7d8a6efff131a67ef7a361f2ca0438a6ae')

build() {
  cd mockito-python
  python -m build -nw
}

check() {
  cd mockito-python
  pytest
}

package() {
  cd mockito-python
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
