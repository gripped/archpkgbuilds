# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=2.9
pkgrel=2
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/"
license=(GPL-2.0-or-later)
groups=(realtime)
depends=(glibc)
makedepends=(
  numactl
  python-setuptools
)
optdepends=(
  'bash: for determine_maximum_mpps.sh'
  'numactl: for cyclictest, oslat and signaltest'
  'python: for get_cyclictest_snapshot and hwlatdetect'
)
source=($url/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('23608ea3c20e793defa00f61f81233d99a14ce60d7457899a0a91864a65ec5a474fa85fa392364b87b1fee3437f1c94fecae8edff797018690c81b737bbe691e')
b2sums=('788174bf5bbf95b04a69ce69b54ef6ba2b8ec3bbba2a70bd562839728b822d85c9abdfd77cb9a060167a8654955978f87d04ee8d8f25000a36cc06ed05327887')

build() {
  export PYLIB=$(python -c 'import distutils.sysconfig;  print(distutils.sysconfig.get_python_lib())')
  make -C $pkgname-$pkgver
}

package() {
  make prefix=/usr DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{MAINTAINERS,README.markdown} -t "$pkgdir/usr/share/doc/$pkgname/"
}
