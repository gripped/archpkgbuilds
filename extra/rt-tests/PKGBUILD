# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=2.10
pkgrel=1
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/"
license=(GPL-2.0-or-later)
groups=(realtime)
depends=(glibc)
makedepends=(
  numactl
  python-setuptools
)
optdepends=(
  'bash: for determine_maximum_mpps.sh'
  'numactl: for cyclictest, oslat and signaltest'
  'python: for get_cyclictest_snapshot and hwlatdetect'
)
source=($url/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('9cb8a71e795a2a447e6a9fe4ccd2e9bc2446e016f389fb9c294e6d5b0e0903ab824abf959f1d2632ad9de8699dbbb6af6e6026b812ee0f52c2ae065c7bac4294')
b2sums=('f9c52b43945893a3e9abca635f17eca88c01ee6b093b34623804a1499fd3dcbebefd98e65c5f4ffa2e84b2d19c8787db9eb234e09f28e871549647d0ab5890ea')

build() {
  export PYLIB=$(python -c 'import distutils.sysconfig;  print(distutils.sysconfig.get_python_lib())')
  make -C $pkgname-$pkgver
}

package() {
  make prefix=/usr DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{MAINTAINERS,README.markdown} -t "$pkgdir/usr/share/doc/$pkgname/"
}
