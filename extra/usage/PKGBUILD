# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='usage'
pkgver=2.0.6
pkgrel=1
pkgdesc='A specification for CLIs'
arch=('x86_64')
url='https://github.com/jdx/usage'
license=('MIT')
makedepends=(
  cargo
)
options=(
  '!emptydirs'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('058bf86c24f38dd15bf979b50b2a9a2a1dedc76d61c1ba6ec80f5e804d4c771f8131ae944d4d64046fb6fbb393db034bca40691fdcbb2e6c7bc24925f3d1294b')
b2sums=('c55b5dfcc4d11d04571c892c974c5cb244bc9e9f10ae499dfe8c3694be2bf1806074b69bd52a8f7b36a047c36361245b0c7c6516ca6d1a01710343e82879c208')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  cargo test --frozen --all-features
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin/"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
