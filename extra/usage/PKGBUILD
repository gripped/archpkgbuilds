# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='usage'
pkgver=2.0.7
pkgrel=1
pkgdesc='A specification for CLIs'
arch=('x86_64')
url='https://github.com/jdx/usage'
license=('MIT')
makedepends=(
  cargo
)
options=(
  '!emptydirs'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5e79922b999e588394ccc8017c8a9dc2d98e091a7cf5321f78f9ea1879b142af8ca197aabc406509e7f6ea18d92be2194dc99ccfab4546ff5519e208bc7e0389')
b2sums=('457a9bab58172aa6b8bb4a92a38cbd66a40a0d3a2ba364652d09a580db84f03370056f5c910cbaab5fd5ec316fa4a70c35f99f437c0d39b2d2ede2882a08a69e')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  cargo test --frozen --all-features
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin/"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
