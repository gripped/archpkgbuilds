# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='usage'
pkgver=2.1.1
pkgrel=1
pkgdesc='A specification for CLIs'
arch=('x86_64')
url='https://github.com/jdx/usage'
license=('MIT')
makedepends=(
  cargo
)
options=(
  '!emptydirs'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('71f295ce256eb3a16986ef04f2160bb67ed4cea76acfead96aaedf705f24dab1635869779fc13b81eb823fa62ef4b5c90722a087dc2032398e6266b3f859dd99')
b2sums=('075a31603083903361ed17de57813dd3e4a268657979a3f2bb914296955c5d296152b6259266a1d4f0fe030d8941ef7fbd4a0baaae0cdc25d9adc9f5121436af')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  cargo test --frozen --all-features
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin/"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
