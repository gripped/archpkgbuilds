# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='usage'
pkgver=2.16.2
pkgrel=1
pkgdesc='A specification for CLIs'
arch=('x86_64')
url='https://github.com/jdx/usage'
license=('MIT')
makedepends=(
  cargo
)
checkdepends=(
  nodejs
)
options=(
  '!emptydirs'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('2088dff55d298f88692be052bc4c9f2ec4e4b3435a1aa6bc5ccf6a2f0884519e48733234c70aad71cb122024d18cc088132e072a151828ab95c25ca9b8ef03c1')
b2sums=('fef7e3cb027d2606d5bbf6a0cc756464c89335fe78c786e6a2e826b0ed20114e47825cf527bcecf6713e9bc9a1d1ab5426254d2def183950ad12d71738bd3858')

prepare() {
  cd "${pkgname}-${pkgver}"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"

  cargo build --frozen --release --all-features
}

check() {
  cd "${pkgname}-${pkgver}"

  cargo test --frozen --all-features
}

package() {
  cd "${pkgname}-${pkgver}"

  # package
  install --verbose -D --mode=0755 "target/release/${pkgname}" --target-directory "${pkgdir}/usr/bin/"

  # license
  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # docs
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
