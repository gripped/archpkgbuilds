# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-django-csp
pkgver=4.0
pkgrel=1
pkgdesc="Django-CSP adds Content-Security-Policy headers to Django"
arch=('any')
url="https://github.com/mozilla/django-csp"
license=('BSD-3-Clause')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-pytest-django')
depends=('python-django' 'python-jinja')
source=($pkgname-$pkgver.tar.gz::https://github.com/mozilla/django-csp/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('f7e99db48562fd9f431aa3aadc76808d398c5be15355c3881e246626d0ad1f2876efa86b3490adfd8c5eeaf0e00389b022c205afe694ce69de95bfa4a5a72692')

prepare() {
  cd "django-csp-$pkgver"
}

build() {
  cd "django-csp-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "django-csp-$pkgver"
  # Override addopts so we don't need pytest-ruff
  PYTHONPATH=. pytest -o addopts='' csp/tests
}

package() {
  cd "django-csp-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
