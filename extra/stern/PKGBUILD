# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname=stern
pkgdesc="Multi pod and container log tailing for Kubernetes"
pkgver=1.33.0
pkgrel=1
arch=('x86_64')
url="https://github.com/stern/stern"
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
optdepends=('bash-completion: for bash completion support')
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}/${pkgver}.tar.gz")
sha512sums=('e8eb0fe980f01f2ada334b550ee4e84a23aa6604ba679950a871369cdd0dd7d1b047ce1fc4636ebb7301994ed0ede328bbcf934456e988337ee9bd73a8d573cc')
b2sums=('ec04249875cc0afaaa17bc9e9cc80269c556a23f5243e75abb681b04322fa11aac3c670e75980161d2f1a0c30ff55b18fcd64d6fed88fbb12d5eeda008ce6e8b')

prepare() {
  cd "${pkgname}-${pkgver}"

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
    -X github.com/stern/stern/cmd.version=${pkgver} \
  "
  go build -v -x \
    -ldflags "${ld_flags}" \
    -o "./out/${pkgname}"
}

check() {
  cd "${pkgname}-${pkgver}"

  go test -v ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install --verbose -D --mode=0755 "./out/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  "${pkgdir}/usr/bin/${pkgname}" --completion=bash \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" --completion=fish \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
  "${pkgdir}/usr/bin/${pkgname}" --completion=zsh \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"

  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
