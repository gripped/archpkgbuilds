# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname=stern
pkgdesc="Multi pod and container log tailing for Kubernetes"
pkgver=1.31.0
pkgrel=1
arch=('x86_64')
url="https://github.com/stern/stern"
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
optdepends=('bash-completion: for bash completion support')
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}/${pkgver}.tar.gz")
sha512sums=('6bcd1799b32482b7f45ffa389d42e31c509f8b3a307e26d435c037b2418ebb0f478abd4819e05dcf7a9dad8e5b07753f28f074f6e581fd16deda0a5d4978be49')
b2sums=('62a41b01eeef41ce2737a06e5c1cdbe2ccc91f4bc21cc5a2215fc1041af75dc941afb593b9fd3383d9109485afa467cc18d303004a9e22abec3b7ef0ffd97b71')

prepare() {
  cd "${pkgname}-${pkgver}"

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
    -X github.com/stern/stern/cmd.version=${pkgver} \
  "
  go build -v -x \
    -ldflags "${ld_flags}" \
    -o "./out/${pkgname}"
}

check() {
  cd "${pkgname}-${pkgver}"

  go test -v ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install --verbose -D --mode=0755 "./out/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  "${pkgdir}/usr/bin/${pkgname}" --completion=bash \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" --completion=fish \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
  "${pkgdir}/usr/bin/${pkgname}" --completion=zsh \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"

  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
