# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname=stern
pkgdesc="Multi pod and container log tailing for Kubernetes"
pkgver=1.33.1
pkgrel=1
arch=('x86_64')
url="https://github.com/stern/stern"
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
optdepends=('bash-completion: for bash completion support')
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}/${pkgver}.tar.gz")
sha512sums=('35645d03abd7657d38b8794e571c607ff2e18a1c4a09f7398b50cc0d5cd3907245f973afca8390adf3ff3de7edf5edef0c8f68a54544cfacd7f307bdceb88304')
b2sums=('2af0c2cf0d62b5f865d33941f1390f855d30cc0ffa1a3add0515744986e5ee793a9f1f0ab88011a04bf4e3483d4b66f3b26493983442a566d53c7a0bd3f61ed0')

prepare() {
  cd "${pkgname}-${pkgver}"

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
    -X github.com/stern/stern/cmd.version=${pkgver} \
  "
  go build -v -x \
    -ldflags "${ld_flags}" \
    -o "./out/${pkgname}"
}

check() {
  cd "${pkgname}-${pkgver}"

  go test -v ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install --verbose -D --mode=0755 "./out/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  "${pkgdir}/usr/bin/${pkgname}" --completion=bash \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" --completion=fish \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
  "${pkgdir}/usr/bin/${pkgname}" --completion=zsh \
    | install --verbose -D --mode=0644 /dev/stdin \
      "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"

  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
