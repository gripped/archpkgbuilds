# Maintainer: Antonio Rojas <arojas@archlinux.com>

_pyname=jupyter_server_ydoc
pkgname=jupyter-server-ydoc
pkgver=1.0.0
pkgrel=1
pkgdesc='A Jupyter Server Extension Providing Y Documents'
arch=(any)
url='https://jupyter.org/'
license=(BSD-3-Clause)
depends=(jupyter-nbformat
         jupyter-server
         jupyter-server-fileid
         python
         python-jupyter-events
         python-jupyter-ydoc
         python-pycrdt
         python-pycrdt-websocket
         python-tornado
         python-traitlets
         python-websockets)
makedepends=(python-build
             python-hatchling
             python-installer)
checkdepends=(python-pytest-jupyter)
source=(https://pypi.python.org/packages/source/j/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('3017524c1da068815b748c8f1ebe7ec08eda047fc597ce72c125b18009a391e9')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/{usr/,}etc
}
