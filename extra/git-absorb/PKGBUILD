# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: AsamK <asamk@gmx.de>

pkgname=git-absorb
pkgver=0.8.0
pkgrel=1
pkgdesc="git commit --fixup, but automatic"
arch=('x86_64')
url="https://github.com/tummychow/git-absorb"
license=('BSD')
depends=('gcc-libs' 'glibc' 'libgit2')
makedepends=('cargo' 'asciidoc')
checkdepends=('git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tummychow/git-absorb/archive/${pkgver}.tar.gz")
sha256sums=('9ed6fef801fbfeb7110744cac38ae5b3387d8832749ae20077b9139d032211f1')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked
  mkdir completions/
}

build() {
  cd "${pkgname}-${pkgver}"
  CFLAGS+=' -ffat-lto-objects'
  export LIBGIT2_NO_VENDOR=1
  cargo build --release --frozen
  "target/release/$pkgname" --gen-completions bash >"completions/$pkgname.bash"
  "target/release/$pkgname" --gen-completions fish >"completions/$pkgname.fish"
  "target/release/$pkgname" --gen-completions zsh >"completions/_$pkgname"
  cd "Documentation"
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_NO_VENDOR=1
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  depends+=('libgit2.so')
  install -Dm 755 target/release/"${pkgname}" -t "${pkgdir}/usr/bin/"
  install -Dm 644 "README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 "Documentation/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 "completions/$pkgname.bash" "${pkgdir}/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "completions/$pkgname.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$pkgname" -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:
