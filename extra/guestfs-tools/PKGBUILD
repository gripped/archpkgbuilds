# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=guestfs-tools
pkgver=1.52.3
pkgrel=3
pkgdesc="Tools for accessing and modifying guest disk images"
arch=(x86_64)
url="https://libguestfs.org/"
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  gcc-libs
  glibc
  jansson
  libosinfo
  perl
  perl-libintl-perl
)
makedepends=(
  bash-completion
  cdrtools
  libguestfs
  libisoburn
  libvirt
  libxcrypt
  libxml2
  ncurses
  ocaml-findlib
  openssl
  perl-module-build
  xz
)
checkdepends=(
  linux
  rpm-tools
)
backup=(
  etc/virt-builder/repos.d/libguestfs.conf
  etc/virt-builder/repos.d/libguestfs.gpg
  etc/virt-builder/repos.d/opensuse.conf
  etc/virt-builder/repos.d/opensuse.gpg
)
source=(
  https://download.libguestfs.org/$pkgname/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz{,.sig}
)
sha512sums=('f7a965500a7d1b355d447bd4710c9e38e8f7443eef721fbff9504d862bbf4740b8e5f75134fcc0ca50817872799168a46d2ddcca82c36be1d891d3ed3c47c79b'
            'SKIP')
b2sums=('f6ef8a63f3b9dfee6c7ebb954ae9d3602dd3d96a60f85fc1d6a5304725ebca488937802cff337f18bd4e86e2598ad092be2fab2a59f9580d0849c65fef09d3c1'
        'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')  # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  export LIBGUESTFS_DEBUG=1 LIBGUESTFS_TRACE=1  # verbose libguestfs output
  export SKIP_TEST_VIRT_FILESYSTEMS_SH=1  # mismatch in test 2
  export SKIP_TEST_VIRT_INSPECTOR_SH=1  # requires KVM
  export SKIP_TEST_VIRT_INSPECTOR_LUKS_ON_LVM_SH=1  # requires KVM
  export SKIP_TEST_VIRT_INSPECTOR_LVM_ON_LUKS_SH=1  # requires KVM
  export SKIP_TEST_VIRT_BUILDER_SH=1  # virt-resize: error: /dev/sda2: physical volume not returned by pvs_full
  export SKIP_TEST_VIRT_DRIVERS_LINUX_SH=1  # requires rpm-tools during libguestfs compile time?
  
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    glib2 libgobject-2.0.so libglib-2.0.so
    libguestfs libguestfs.so
    libvirt libvirt.so
    libxcrypt libcrypt.so
    libxml2 libxml2.so
    ncurses libncursesw.so
    pcre2 libpcre2-8.so
    xz liblzma.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
