# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=guestfs-tools
pkgver=1.52.1
pkgrel=1
pkgdesc="Tools for accessing and modifying guest disk images"
arch=(x86_64)
url="https://libguestfs.org/"
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  gcc-libs
  glibc
  jansson
  libosinfo
  perl
  perl-libintl-perl
)
makedepends=(
  bash-completion
  cdrtools
  libguestfs
  libisoburn
  libvirt
  libxcrypt
  libxml2
  ncurses
  ocaml-findlib
  openssl
  perl-module-build
  xz
)
checkdepends=(
  linux
  rpm-tools
)
backup=(
  etc/virt-builder/repos.d/libguestfs.conf
  etc/virt-builder/repos.d/libguestfs.gpg
  etc/virt-builder/repos.d/opensuse.conf
  etc/virt-builder/repos.d/opensuse.gpg
)
source=(
  https://download.libguestfs.org/$pkgname/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz{,.sig}
)
sha512sums=('0db4b6cc49b18d26cb9a3a1e8f76fcb16127d41fac65b2e6e40e823b697a5581be0b85d54d2a36dbf4e45aea72ecddfc2cf6d198093c8cc52ce1a2712cc26d25'
            'SKIP')
b2sums=('dff8ba0cf2f0f76cfb09d4546d17384737d9f2e38029d930ffd638e2066f909dcd6f26868cd1137e3b2bf686bcf563fb1be9b68b60f1a04f6790b9940c14c018'
        'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')  # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  export LIBGUESTFS_DEBUG=1 LIBGUESTFS_TRACE=1  # verbose libguestfs output
  export SKIP_TEST_VIRT_FILESYSTEMS_SH=1  # mismatch in test 2
  export SKIP_TEST_VIRT_INSPECTOR_SH=1  # requires KVM
  export SKIP_TEST_VIRT_INSPECTOR_LUKS_ON_LVM_SH=1  # requires KVM
  export SKIP_TEST_VIRT_INSPECTOR_LVM_ON_LUKS_SH=1  # requires KVM
  export SKIP_TEST_VIRT_BUILDER_SH=1  # virt-resize: error: /dev/sda2: physical volume not returned by pvs_full
  export SKIP_TEST_VIRT_DRIVERS_LINUX_SH=1  # requires rpm-tools during libguestfs compile time?
  
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    glib2 libgobject-2.0.so libglib-2.0.so
    libguestfs libguestfs.so
    libvirt libvirt.so
    libxcrypt libcrypt.so
    libxml2 libxml2.so
    ncurses libncursesw.so
    pcre2 libpcre2-8.so
    xz liblzma.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
