# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-cabal-fmt-plugin
pkgname=haskell-hls-cabal-fmt-plugin
pkgver=2.1.0.0
pkgrel=46
pkgdesc="Integration with the cabal-fmt code formatter"
url="https://hackage.haskell.org/package/hls-cabal-fmt-plugin"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'cabal-fmt' 'haskell-ghcide' 'haskell-hls-plugin-api' 'haskell-lens'
         'haskell-lsp-types')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('6e3f9441aba9642a55d9afdab01ba2f6c111fe84fc8d749007c0780124753263')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -f-isolatetests

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
