# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=15.0
pkgrel=2
pkgdesc='Engine for running Transport Tycoon Deluxe'
arch=(x86_64)
url='https://www.openttd.org'
# squirrel (bundled) is licensed under the Zlib license
# fmt (bundled) is licensed under the MIT license
license=('GPL-2.0-only' 'BSD-3-Clause' 'LGPL-2.1-or-later' 'MIT' 'Zlib')
depends=(fluidsynth sdl2-compat fontconfig hicolor-icon-theme)
makedepends=(cmake)
optdepends=('openttd-opengfx: free graphics'
            'openttd-opensfx: free soundset')
source=("https://cdn.$pkgname.org/$pkgname-releases/$pkgver/$pkgname-$pkgver-source.tar.xz")
b2sums=('4f3210e573e622298c3238e2701e0953826eeb91abce232da4acdd47094508cc30f988899ba204a58c30210fbdd9d37e849ba27bfcc4b7a1dfa10d8647279224')

build() {
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_BINDIR=bin \
    -D CMAKE_INSTALL_DATADIR=/usr/share \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -S $pkgname-$pkgver
  make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install
  install -Dm644 $pkgname-$pkgver/COPYING.md \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
