# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=15.1
pkgrel=1
pkgdesc='Engine for running Transport Tycoon Deluxe'
arch=(x86_64)
url='https://www.openttd.org'
# squirrel (bundled) is licensed under the Zlib license
# fmt (bundled) is licensed under the MIT license
license=('GPL-2.0-only' 'BSD-3-Clause' 'LGPL-2.1-or-later' 'MIT' 'Zlib')
depends=(fluidsynth sdl2-compat fontconfig hicolor-icon-theme)
makedepends=(cmake)
optdepends=('openttd-opengfx: free graphics'
            'openttd-opensfx: free soundset')
source=("https://cdn.$pkgname.org/$pkgname-releases/$pkgver/$pkgname-$pkgver-source.tar.xz")
b2sums=('39f1ba1d8c790ccb9784a40e902aa1c7eaf8198f57b2dadf0122bc11dba235c5451a9877b8852094f226dafec9657767ee2bbf736922f48edc98b695534dd1cb')

build() {
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_BINDIR=bin \
    -D CMAKE_INSTALL_DATADIR=/usr/share \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -S $pkgname-$pkgver
  make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install
  cd $pkgname-$pkgver
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/COPYING.md"
  for f in CREDITS.md known-bugs.md; do
    install -Dm644 $f "$pkgdir/usr/share/doc/$pkgname/$f"
  done
}
