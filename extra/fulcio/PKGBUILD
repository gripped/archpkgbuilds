# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fulcio
pkgver=1.7.1
pkgrel=1
pkgdesc='free Root-CA for code signing certs - issuing certificates based on an OIDC email address.'
arch=('x86_64')
url="https://github.com/sigstore/fulcio"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/fulcio/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('4e90cfacff0f67954cb3f874bc6fd793585db11a56f8bc5c4887ee8800a47d5532cd593090b8e07efb5a6cf6aeeb30145d296188138783da68a4df2e4858f980')
b2sums=('dfc525b6545a21f30c9d420ced10769d79631098f84b7a76150d2c044967d006d4ecaf5109e47cc90347bac18e92020c9d6edacde179181eab0db9681aea5fe8')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

# Disable tests, failing due to missing config
#check() {
#  cd "${pkgname}-${pkgver}"
#  make test
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/fulcio "${pkgdir}/usr/bin/${pkgname}"
}
