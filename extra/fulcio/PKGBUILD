# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fulcio
pkgver=1.8.3
pkgrel=2
pkgdesc='free Root-CA for code signing certs - issuing certificates based on an OIDC email address.'
arch=('x86_64')
url="https://github.com/sigstore/fulcio"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/fulcio/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('86dff816dd6b7b0e32c161f570607607f9025059d16e8db476ec2b155fe12e0da44b72c18d11cc83b96ee31468dba4e6f86562d787d1012dc26b9388d228c135')
b2sums=('ea8a191c0696f002d88c54cb389ec8127a8d994d5e11b7cf67b9c25b724571b677cf5dbb5780dc09910a6bdfc37a49a1c3abaae2e77da3ac93e09b371f7b0ab6')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

# Disable tests, failing due to missing config
#check() {
#  cd "${pkgname}-${pkgver}"
#  make test
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/fulcio "${pkgdir}/usr/bin/${pkgname}"
}
