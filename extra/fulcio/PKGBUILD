# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fulcio
pkgver=1.6.6
pkgrel=2
pkgdesc='free Root-CA for code signing certs - issuing certificates based on an OIDC email address.'
arch=('x86_64')
url="https://github.com/sigstore/fulcio"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/fulcio/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('d6b30e80fcb0fe848167e0239e0765042d9b5e0adb83fab98c5abf9f07e28b35e5dec2cc11116e186b13636986045b9d52ea8aa356dfb3e66bb5e08065b144b6')
b2sums=('29c76493037a4bd7202c158d833638b6896e2862c79320817e33bed2887882a119d38bbc195c1116efe008f584a51d5576e52ea137fe964685a61ccf9fde6ed6')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

# Disable tests, failing due to missing config
#check() {
#  cd "${pkgname}-${pkgver}"
#  make test
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/fulcio "${pkgdir}/usr/bin/${pkgname}"
}
