# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: HurricanePootis <hurricanepootis@protonmail.com>
# Contributor: grimi

pkgname=libxmp
pkgver=4.7.0
pkgrel=1
pkgdesc="Library that supports over 90 module formats (Amiga, Atari, ..)"
arch=('x86_64')
url="http://xmp.sourceforge.net/"
license=('MIT')
depends=('glibc')
options=(!emptydirs)
source=("http://download.sourceforge.net/project/xmp/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('b6251de1859352c6988752563d60983cb8aa9fd7dfe9f81b8bc6688da47f3464')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir/$pkgname-$pkgver/docs/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
