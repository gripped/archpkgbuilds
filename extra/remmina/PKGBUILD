# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=remmina
epoch=1
pkgver=1.4.43
pkgrel=1
pkgdesc="Remote desktop client written in GTK+"
url="https://www.remmina.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('avahi' 'bash' 'cairo' 'curl' 'gcc-libs' 'glib2' 'glibc' 'gtk3'
         'hicolor-icon-theme' 'json-glib' 'libayatana-appindicator' 'libgcrypt'
	 'libsodium' 'libssh' 'libx11' 'openssl' 'pango' 'python' 'vte3')
makedepends=('cmake' 'freerdp' 'gobject-introspection' 'gtk-vnc' 'harfbuzz'
             'kwallet5' 'libvncserver' 'ninja' 'spice-gtk' 'spice-protocol'
             'webkit2gtk-4.1' 'xorgproto')
optdepends=('freerdp: RDP plugin'
            'libsecret: Secret plugin'
            'libvncserver: VNC plugin'
            'spice-gtk: Spice plugin'
            #'pyhoca-cli: X2Go plugin'
            'webkit2gtk-4.1: WWW plugin'
            'gtk-vnc: GVNC plugin'
            'kwallet5: kwallet plugin'
            'gnome-terminal: external tools')
replaces=('remmina-plugins')
provides=('remmina-plugins')
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/Remmina/Remmina/-/archive/v${pkgver}/Remmina-v${pkgver}.tar.gz")
sha512sums=('8d96f4ef89ef36bf6ebb548a5d36a58828c09b54dcc4e76494e6110f3a683d84291f449ed59efc8414b5c4f646fa9f8432d0fa97e9d4ac745d737741c3549c42')

build() {
	cmake -S "Remmina-v${pkgver}" -B build -G Ninja \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-DCMAKE_INSTALL_LIBDIR='/usr/lib' \
		-DWITH_APPINDICATOR='ON' \
		-DWITH_FREERDP3='ON' \
		-DWITH_NEWS='OFF' \
		-DWITH_KF5WALLET='ON' \
		-DWITH_X2GO='ON' \
		-DWITH_GVNC='ON' \
		-Wno-dev
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
