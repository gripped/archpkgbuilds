# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Beno√Æt Allard <benoit dot allard at gmx dot de>
# Contributor: killruana <killruana@gmail.com>

pkgname=yosys
pkgver=0.54
pkgrel=3
pkgdesc='A framework for RTL synthesis'
arch=('x86_64')
url='https://yosyshq.net/yosys/'
license=('custom:ISC')
depends=('abc' 'bash' 'boost-libs' 'tcl' 'libffi' 'python' 'protobuf')
makedepends=('boost' 'git')
checkdepends=('iverilog' 'gtkwave')
optdepends=('graphviz: Schematics display support'
            'xdot: Display netlists'
            'yices: default solver for yosys-smtbmc'
            'cvc4: alternative solver for yosys-smtbmc'
            'z3: alternative solver for yosys-smtbmc')
options=('!lto') # getting SIGSEGV when running the tests with LTO
source=(git+https://github.com/YosysHQ/yosys#tag=v$pkgver
        git+https://github.com/jarro2783/cxxopts
        boost-1.89.patch)
sha512sums=('f9f1698f493855d537e72de0c7c7b13f409f0bdfbad3be0019449e98bec2dd98b1a1cdd0d7d74f978287a28733d84564120a624d297eb9c8be4388f2f48e074c'
            'SKIP'
            'f926dd219f6ea13fee0d215e3305e5c9bab8f06ebb715f7cadcc80e199096d87a3dd9acbdc6f2727bd8e6e8c85e4c63b8068212a81c906ba0694af6f2b394a96')

prepare() {
  cd yosys
  git submodule init
  git submodule set-url libs/cxxopts "$srcdir"/cxxopts
  git -c protocol.file.allow=always submodule update libs/cxxopts

  sed -e '/cp yosys-abc/d' -i Makefile # Fix Makefile trying to install yosys-abc even if using system one
  patch -p1 -i ../boost-1.89.patch
}

_make() {
    local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
    local python_version_combined=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
    make \
        PREFIX="/usr" \
        CONFIG=gcc \
        ENABLE_TCL=1 \
        ENABLE_ABC=1 \
        ENABLE_GLOB=1 \
        ENABLE_PLUGINS=1 \
        ENABLE_LIBYOSYS=1 \
        ENABLE_PROTOBUF=1 \
        ENABLE_ZLIB=1 \
        ENABLE_PYOSYS=1 \
        ABCEXTERNAL=abc \
        BOOST_PYTHON_LIB="-lpython${python_version} -lboost_python${python_version_combined}" $@
}

build() {
    cd $pkgname
    _make
}

check() {
    cd $pkgname
    _make test
}

package() {
    cd $pkgname
    _make DESTDIR="$pkgdir" PYTHON_PREFIX="$pkgdir/usr" install

    install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
