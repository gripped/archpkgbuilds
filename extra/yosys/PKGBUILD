# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Beno√Æt Allard <benoit dot allard at gmx dot de>
# Contributor: killruana <killruana@gmail.com>

pkgname=yosys
pkgver=0.44
pkgrel=3
pkgdesc='A framework for RTL synthesis'
arch=('x86_64')
url='https://yosyshq.net/yosys/'
license=('custom:ISC')
depends=('abc' 'bash' 'boost-libs' 'tcl' 'libffi' 'python' 'protobuf')
makedepends=('boost' 'git')
checkdepends=('iverilog')
optdepends=('graphviz: Schematics display support'
            'xdot: Display netlists'
            'yices: default solver for yosys-smtbmc'
            'cvc4: alternative solver for yosys-smtbmc'
            'z3: alternative solver for yosys-smtbmc')
options=('!lto') # getting SIGSEGV when running the tests with LTO
source=(git+https://github.com/YosysHQ/yosys#tag=yosys-$pkgver)
sha512sums=('5cb36e49e3394a1ba67d98a9fcf9120ec4092c4859a6939aae9473f6143bf6fad9c61d8b00b804004a7828c31c9efc0e4f2d90905e0094f5f1361c5b9335d5f4')

prepare() {
  cd yosys
  git cherry-pick -n fe79a77e398fab8dc962e49556eec5803aa3be74 # Fix build with GCC 15
}

_make() {
    local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
    local python_version_combined=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
    make \
        PREFIX="/usr" \
        CONFIG=gcc \
        ENABLE_TCL=1 \
        ENABLE_ABC=1 \
        ENABLE_GLOB=1 \
        ENABLE_PLUGINS=1 \
        ENABLE_LIBYOSYS=1 \
        ENABLE_PROTOBUF=1 \
        ENABLE_ZLIB=1 \
        ENABLE_PYOSYS=1 \
        ABCEXTERNAL=abc \
        BOOST_PYTHON_LIB="-lpython${python_version} -lboost_python${python_version_combined}" $@
}

build() {
    cd $pkgname
    _make
}

check() {
    cd $pkgname
    _make test
}

package() {
    cd $pkgname
    _make DESTDIR="$pkgdir" PYTHON_PREFIX="$pkgdir/usr" install

    install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
