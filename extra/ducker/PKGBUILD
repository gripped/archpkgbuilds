# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=ducker
pkgver=0.2.0
pkgrel=1
pkgdesc="A terminal app for managing docker containers"
url="https://github.com/robertpsoane/ducker"
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d556e6e53782191d248c42f4056e80fda4b720bc9b7d35b9166f28568b79d25d3d0c58b8c2106e5816047e84dd2d3be8502cb76694c1aec6f035ff31e13ffd11')
options=('!lto')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
