# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=typescript-language-server
pkgver=5.0.1
pkgrel=1
pkgdesc='Language Server Protocol (LSP) implementation for TypeScript using tsserver'
url=https://github.com/typescript-language-server/typescript-language-server
arch=(any)
license=(
  Apache-2.0
  MIT
)
depends=(typescript)
makedepends=(
  git
  jq
  yarn
)
checkdepends=(npm)
source=("git+$url.git#tag=v$pkgver")
b2sums=('f751ef3c5fbc5ed96f4a066719a82bdd8b57075c2ed015de9ce26158e2373b442eff6c072167d82c9a76212372e3bdb6d56a6c28158e1e45d917e85b50dd781d')

prepare() {
  cd $pkgname
  yarn --frozen-lockfile

  cd test-data/yarn-pnp
  mkdir bin
  corepack enable --install-directory bin yarn
  yes | bin/yarn set version stable
  yes | bin/yarn install --immutable
}

build() {
  cd $pkgname
  yarn build # Needs bin links
}

check() {
  cd $pkgname
  yarn test:commit
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/lib/cli.mjs "$pkgdir"/usr/bin/$pkgname

  cd $pkgname
  # Prune unnecessary packages
  cp package.json{,.bak}
  read -ra devDependencies < <(jq -r '.devDependencies | keys | join(" ")' package.json)
  yarn remove --frozen-lockfile "${devDependencies[@]}"
  mv package.json{.bak,}

  cp -r lib package.json "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/lib/cli.mjs
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname {CHANGELOG,README}.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
