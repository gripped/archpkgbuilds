# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=typescript-language-server
pkgver=5.1.1
pkgrel=1
pkgdesc='Language Server Protocol (LSP) implementation for TypeScript using tsserver'
url=https://github.com/typescript-language-server/typescript-language-server
arch=(any)
license=(
  Apache-2.0
  MIT
)
depends=(typescript)
makedepends=(
  git
  jq
  yarn
)
checkdepends=(
  corepack
  npm
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('6f24e9129c402c7b6db721285001c3b037b05ac85e4b9792e7cce8da88efb6bb168256892be90745644785a2585b49fb9d40dc0a3e258c286012479b6d04218a')

prepare() {
  cd $pkgname
  yarn --frozen-lockfile
}

build() {
  cd $pkgname
  yarn build # Needs bin links
}

check() {
  cd $pkgname/test-data/yarn-pnp
  mkdir bin
  corepack enable --install-directory bin yarn
  yes | bin/yarn set version stable
  bin/yarn install --immutable
  cd ../..
  yarn test:commit
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/lib/cli.mjs "$pkgdir"/usr/bin/$pkgname

  cd $pkgname
  # Prune unnecessary packages
  cp package.json{,.bak}
  read -ra devDependencies < <(jq -r '.devDependencies | keys | join(" ")' package.json)
  yarn remove --frozen-lockfile "${devDependencies[@]}"
  mv package.json{.bak,}

  cp -r lib package.json "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/lib/cli.mjs
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname {CHANGELOG,README}.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
