# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Adam Perkowski <adas1per@protonmail.com>

pkgname=tukai
pkgver=0.1.5
pkgrel=1
pkgdesc="Interactive typing tool with switchable templates"
arch=('x86_64')
url="https://github.com/hlsxx/$pkgname"
license=('MIT')
depends=('libx11' 'gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('e93fc9138766e420e329dc8f3bbb32d0710e9bc11878d2b91c615cf3d337ba65')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
