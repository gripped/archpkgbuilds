# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-online-accounts
pkgver=8.0.1
pkgrel=1
pkgdesc='Switchboard Online Accounts Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-onlineaccounts
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libcamel-1.2.so
  libedataserver-1.2.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=655c1158daabd8f0c4242b3317869fe13acd1cbc
source=(switchboard-plug-online-accounts::git+https://github.com/elementary/switchboard-plug-onlineaccounts.git#tag=${_tag})
b2sums=('46ce53fc2b9cbee04ea4513389d1966f2d28dee7aa62e0c36e351192d8af3f0a585d143193861ebf2cfdb01273273058816f57ad8c3033cdc98ee83e94da0cae')

pkgver() {
  cd switchboard-plug-online-accounts
  git describe --tags
}

build() {
  arch-meson switchboard-plug-online-accounts build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
