# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=jc
pkgver=1.25.6
pkgrel=2
pkgdesc='Converts the output of popular command-line tools and file-types to JSON'
arch=('any')
url='https://github.com/kellyjonbrazil/jc'
license=('MIT')
depends=(
  'python'
  'python-pygments'
  'python-ruamel-yaml'
  'python-xmltodict'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('200811068bc11a03f47ac986a73c866710a3240b136faa26e0feba72731b2bca6ceffb12192e1ec10d03a9d372710613ac93ef0ee96e6ab1d9ae87cf3a21d403')
b2sums=('9767cc5c86ec2967caf1b4c964387a2541c6f21583d9c0f0f5a63fc30113a1f9d2faa17de4e2c09ad51dcbfa1acf7f2d07e179a2a7bf1937e78b47aaaaba79c4')

build(){
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

#check() {
#  cd "$pkgname"
#
#  python -m unittest -v
#}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md EXAMPLES.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/jc.1

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
