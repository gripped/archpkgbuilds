# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=jc
pkgver=1.25.4
pkgrel=1
pkgdesc='Converts the output of popular command-line tools and file-types to JSON'
arch=('any')
url='https://github.com/kellyjonbrazil/jc'
license=('MIT')
depends=(
  'python'
  'python-pygments'
  'python-ruamel-yaml'
  'python-xmltodict'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('4427a2ef3663917de082f79ddeef043746bdb66bdf1e31b1651619708f2bba355706cd9040815f9e31a41d5b010c491c3b3cec99466781707fad8aaa2f81c40c')
b2sums=('1d9058041f0b0432ed1449de7760720c94280e51dfbeb940d0495bc0a9a8dc2af4f535a80c64c993490603ca841c0cc18ac0f20b55df3407faa8cbcb41def2c4')

build(){
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

#check() {
#  cd "$pkgname"
#
#  python -m unittest -v
#}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md EXAMPLES.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/jc.1

  # license
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/$pkgname-$pkgver.dist-info/LICENSE.md" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
