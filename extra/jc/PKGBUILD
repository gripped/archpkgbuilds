# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=jc
pkgver=1.25.5
pkgrel=1
pkgdesc='Converts the output of popular command-line tools and file-types to JSON'
arch=('any')
url='https://github.com/kellyjonbrazil/jc'
license=('MIT')
depends=(
  'python'
  'python-pygments'
  'python-ruamel-yaml'
  'python-xmltodict'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0758610133838abfed0e96790407530c04954790aaa8c32968df7eb4d1051e892a09cf28ff5c1d902f2b9c93aaca2c627b9f2ff36df0bc12d0a26725f988e009')
b2sums=('c7aba645233c3fdb836407968eeb1a3d57ec8aad61887e64adcc675ad9b8fe5b064b7b399e1f721c565365234fadb3d76c75d53fc7098525833a8baf7a9dee2b')

build(){
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

#check() {
#  cd "$pkgname"
#
#  python -m unittest -v
#}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md EXAMPLES.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/jc.1

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
