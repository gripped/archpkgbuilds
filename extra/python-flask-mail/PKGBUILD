# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-flask-mail
pkgver=0.10.0
pkgrel=2
pkgdesc='Flask extension providing simple email sending capabilities'
url='https://github.com/pallets-eco/flask-mail/'
arch=('any')
license=('BSD-3-Clause')
depends=(
  'python'
  'python-blinker'
  'python-flask'
)
makedepends=(
  'python-build'
  'python-flit-core'
  'python-installer'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5f0c0360ca47b46bf57229194f36d7c170b7fcd7f8c1b3444c85e4a50dbe6aaf54d566f0f9b00ec537714fe709707ad5ada416f4c82aaa08003cce7f3e3ee2c2')

build() {
  cd ${pkgname#python-}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # Deselect tests failing due to comparing base64 encoded Unicode headers.
  test-env/bin/python -m pytest \
    --deselect tests/test_message.py::test_unicode_sender \
    --deselect tests/test_message.py::test_unicode_sender_tuple
}

package() {
  cd ${pkgname#python-}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md docs/*.py docs/*.md
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}

# vim: ts=2 sw=2 et:
