# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: hexchain <arch at hexchain.org>
pkgname=krunvm
pkgver=0.2.5
pkgrel=1
pkgdesc="Create microVMs from OCI images"
url='https://github.com/containers/krunvm'
arch=('x86_64')
license=('Apache-2.0')
makedepends=('cargo' 'asciidoctor')
depends=('glibc' 'gcc-libs' 'libkrun' 'buildah')
source=("https://github.com/containers/krunvm/archive/refs/tags/v$pkgver/$pkgname-$pkgver.tar.gz"
        krunvm025-fix.patch::https://github.com/containers/krunvm/pull/77.patch)
sha256sums=('1309cbffad87c0348a468ed728a0575cf3187cba0d26543a00174a48ac89ec61'
            '9c02d63cb88b161338426139f8657cb3bd4602330cd80d349a6c3d16429f3831')

prepare() {
    cd "$pkgname-$pkgver"

    patch -Np1 -i "$srcdir"/krunvm025-fix.patch
}

build() {
    cd "$pkgname-$pkgver"

    make
}

package() {
    cd "$pkgname-$pkgver"

    make DESTDIR="$pkgdir" PREFIX=/usr install

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
