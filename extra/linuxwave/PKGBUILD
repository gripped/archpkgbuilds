# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=linuxwave
pkgver=0.2.0
_gitcommit=3a89b233e053e48925ba5171d5738de9b7835699
pkgrel=1
pkgdesc="Generate music from the entropy of Linux"
arch=('x86_64')
url="https://github.com/orhun/linuxwave"
license=('MIT')
makedepends=('zig' 'git')
source=(
  "${pkgname}::git+$url#commit=$_gitcommit"
)
sha256sums=('937622b322572a908b91f254a08658676a41119862ea622999964c0c673f61b2')

build() {
  cd "$pkgname"
  DESTDIR="build" zig build \
    --summary all \
    --global-cache-dir ../zig-global-cache \
    --prefix /usr \
    --search-prefix /usr \
    --release=safe \
    -Dtarget=native-linux.6.1-gnu.2.38 \
    -Dcpu=baseline \
    -Dpie=true
}

check() {
  cd "$pkgname"
  zig build test \
    --summary all \
    --global-cache-dir ../zig-global-cache \
    --prefix /usr \
    --search-prefix /usr \
    --release=safe \
    -Dtarget=native-linux.6.1-gnu.2.38 \
    -Dcpu=baseline \
    -Dpie=true
}

package() {
  cd "$pkgname"
  cp -a build/* "$pkgdir"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
