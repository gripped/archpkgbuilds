# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: yosh64 <yosh64@gmail.com>

pkgname=mtpaint
pkgver=3.50.11
_commit=f37cf09c02b0ebd81d29c67be9741f54d76a9171 # version: 3.50.11, no git tags are available
pkgrel=1
pkgdesc='Paint program for creating icons and pixel art'
arch=(x86_64)
url='https://github.com/wjaguar/mtpaint'
license=(GPL-3.0-only)
depends=(gtk3 libiconv libwebp openjpeg2)
makedepends=(git)
optdepends=('gifsicle: export GIF files from mtPaint')
source=('https://raw.githubusercontent.com/puppylinux-woof-CE/woof-CE/e904dc79b479b3e62c69c65e744a8715fcfed0fa/woof-code/rootfs-petbuilds/mtpaint/wayland.patch'
        "git+$url#commit=$_commit")
b2sums=('e11a93ba95887abbca866db6687c0a2d410dd657649a17a0c97776977fe6c7bf69dccbf746309d7fdec44a5123c0ee5a47f2c093486b353bfed4c11799d167c0'
        'c38ee1fe81a8fcf0f1f4581e23c5f0e5c569469a18ff9fd605d59878be2b0e6cab1be1a498917754c2683a113e9b8d47b8bf91e4486b7a1749df3bf6c335939b')

prepare() {
  cd $pkgname
  patch -p1 -i ../wayland.patch
}

build() {
  cd $pkgname
  export CFLAGS+="$(pkg-config openjp2 --cflags) -w"
  ./configure --mandir=/usr/share/man --prefix=/usr cflags gtk3 jp2v2 jpeg lcms2 man tiff
  make
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" install
}
