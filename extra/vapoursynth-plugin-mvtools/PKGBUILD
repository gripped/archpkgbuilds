# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-mvtools
pkgver=24
pkgrel=1
pkgdesc='MVTools plugin for VapourSynth'
arch=(x86_64)
url=https://github.com/dubhater/vapoursynth-mvtools
license=(GPL2)
depends=(
  fftw
  vapoursynth
)
makedepends=(
  git
  meson
  nasm
)
_tag=d9270bec9a39bc27cde78f3070cc93b2330d62d1
source=(git+https://github.com/dubhater/vapoursynth-mvtools.git#tag=${_tag})
sha256sums=('59347bdc56aa1d8b51142d10fd4b9708a25e737de76140b95e5ce1f90d5fbfe7')

pkgver() {
  cd vapoursynth-mvtools

  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson vapoursynth-mvtools build \
    --libdir=/usr/lib/vapoursynth
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
