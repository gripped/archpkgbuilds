# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=kafka
pkgver=4.0.0
pkgrel=5
_scalaver=2.13
pkgdesc="An open-source distributed event streaming platform"
arch=('any')
url="https://kafka.apache.org/"
license=('Apache-2.0')
depends=(bash 'java-runtime=17')
makedepends=(gradle 'java-environment=17')
backup=(
  'usr/share/kafka/config/broker.properties'
  'usr/share/kafka/config/connect-console-sink.properties'
  'usr/share/kafka/config/connect-console-source.properties'
  'usr/share/kafka/config/connect-distributed.properties'
  'usr/share/kafka/config/connect-file-sink.properties'
  'usr/share/kafka/config/connect-file-source.properties'
  'usr/share/kafka/config/connect-log4j2.yaml'
  'usr/share/kafka/config/connect-mirror-maker.properties'
  'usr/share/kafka/config/connect-standalone.properties'
  'usr/share/kafka/config/consumer.properties'
  'usr/share/kafka/config/controller.properties'
  'usr/share/kafka/config/log4j2.yaml'
  'usr/share/kafka/config/producer.properties'
  'usr/share/kafka/config/server.properties'
  'usr/share/kafka/config/tools-log4j2.yaml'
  'usr/share/kafka/config/trogdor.conf'
)
install=kafka.install
source=(
    https://downloads.apache.org/$pkgname/$pkgver/$pkgname-$pkgver-src.tgz{,.asc}
    $pkgname.service
    $pkgname.sysusers
    $pkgname.tmpfiles
    set-base_dir.patch
)
sha512sums=('f45810ea2202173051086c08cc5ac29bc325c8f23ba4b427deace8e106f6928a89048fa9ef15906cf9d9ccf0b51d9a746f32be5ac4a8ab707dce98b0efbe60ed'
            'SKIP'
            '10dcff52e0c200e7baa908f85406c43af0acf5f584efb697fcb9650f575bf4142c2927ff0efe71a12838416942fcb862bad8a3808f0afccf69963ae462366293'
            '796eb295cddff842d94e5f10d6a2065b1f397873fd1baf3a85b91fa41d3e67364b105ef199f264b7f28d6536a0448d5fa2512339a085bfd72432844820bb59bf'
            'e555db26b8247ddefdb1c81fb1a6f89d7e8fba39ba19b9ce1608e989e957f6c550bd482e4ccbe1f2f288e00d851741c6a9a53c91754696907d72233f56fad8c6'
            '3739ec988aada505a0eccee6ae27a9ca620547b3117a034d9eec7cae5bc2af26f41aa8bd066945946c19c05e8529034b7a1b1f49ea03a6574760bb6c1a64a7ad')
b2sums=('ea866436d3499452360db24568bd60f7cceb5e6969dd247fc68dd8fe5bf3a5c3f96ddbad52d203df7d152549f5d4521375856581e73932166b312d80fbd655a2'
        'SKIP'
        '2cd6b9280018471e2eac925e41180b0a144543a8d80b992d9170e5fdebf98e983d1ab43d9f0e39b8c517c2a2ddb59a26d4e851c89c80ef5d827920195292d26c'
        'fea3464ddc8e058394153385a7031f71c6a85872062a381a807e9a11eebcf0771da82ae8d5d194fff15ca6c5e0dadf72a94061f8d76789227d2b1d9abb4cc774'
        '9cd647c060054051dfbf49004d672945d004db59d03b2f1a287aee28fc62d1aa37f0506e8b54d4e0e0d5e0136405df51df9d88b901324e1f36b80594ea779924'
        'ced95f54e1efc1cf1e94e933470f0ed19bbb787f0602fbbd7edd188943eda98c31b47debdde2a4cd0c79d32c448dbc6f634162f9ac1f977ba18b67b4376f4901')
# list of trusted signing keys: https://downloads.apache.org/kafka/KEYS
validpgpkeys=(
    '5AFBFDA44492C1240E5A45AFF19D6CB551424672' # Chris Egerton (CODE SIGNING KEY) <cegerton@apache.org>
    '6FB0F28835C48C7C56D2F1F6D3360B139CE19FE8' # David Arthur (CODE SIGNING KEY) <davidarthur@apache.org>
    '42EFF58EC9BDFD20FA7DF8B16CCECEFAC04AC304' # Luke Chen (CODE SIGNING KEY) <showuon@apache.org>
    'D3DF932A03037580D5453F498060FC0DA962AFE5' # Mickael Maison (CODE SIGNING KEY) <mimaison@apache.org>
    '9C1C80A9F910DFCDF11F1E2676AAB84D81427E11' # Divij Vaidya (CODE SIGNING KEY) <divijv@apache.org>
    '0BA3A5C83CF58E807E4750EEF65DC3423D4CD7B9' # Satish Duggana (code signing key) <satishd@apache.org>
    '7C38C2F6E7DF40E527C7C996DE0D9D12FB1360DA' # Stanislav Kozlovski <stanislavkozlovski@apache.org>
    '4687E2BC1319B57B321D6F0E39AB5531A7FCB08E' # Igor Soarez (CODE SIGNING KEY) <soarez@apache.org>
    'CF9500821E9557AEB04E026C05EEA67F87749E61' # Josep Prat (Apache) <jlprat@apache.org>
    '7EEAA203132DC48CF650C9248AF5CC6A1653902F' # Colin Patrick McCabe (CODE SIGNING KEY) <cmccabe@apache.org>
    '880BD93BF75EDE553B6A9918B4CC1061B61737F9' # David Jacot (CODE SIGNING KEY) <dajac@apache.org>
)

prepare() {
    cd "$pkgname-$pkgver-src"
    patch -Np1 < "${srcdir}/set-base_dir.patch"
}

build() {
    cd "$pkgname-$pkgver-src"
    /usr/bin/gradle \
        -PscalaVersion=$_scalaver \
        compileJava \
        compileScala \
        compileTestJava \
        compileTestScala \
        releaseTarGz 

    install -vdm 755 "$srcdir/$pkgname-$pkgver-src/dist"
    tar -xf "core/build/distributions/${pkgname}_${_scalaver}-$pkgver.tgz" \
        --strip 1 \
        -C "$srcdir/$pkgname-$pkgver-src/dist"
}

check() {
    cd "$pkgname-$pkgver-src"
    /usr/bin/gradle \
        -PscalaVersion=$_scalaver \
        --continue \
        -PkeepAliveMode=session \
        -PtestLoggingEvents=started,passed,skipped,failed \
        -PignoreFailures=true \
        -PmaxParallelForks=2 \
        -PmaxTestRetries=1 \
        -PmaxTestRetryFailures=10 \
        unitTest
}

package() {
    local _sh_script
    cd "$pkgname-$pkgver-src/dist"

    # remove unneeded and OS specific scripts
    rm -rvf bin/windows

    # make target scripts executable
    chmod 755 bin/*.sh

    # copy application
    install -vdm 755 "$pkgdir/usr/share/$pkgname/"
    cp -dr --no-preserve='ownership' {bin,config,libs,licenses,site-docs} "$pkgdir/usr/share/$pkgname/"

    # symlink config to /etc
    install -vdm 755 "$pkgdir/etc/$pkgname/"
    for _config in config/*; do
        ln -sv /usr/share/$pkgname/${_config} "$pkgdir/etc/$pkgname/${_config#config/}"
    done

    # set log.dirs to /var/lib/kafka
    sed -e "s:log.dirs=.*:log.dirs=/var/lib/kafka:" \
      -i $pkgdir/usr/share/$pkgname/config/broker.properties
    sed -e "s:log.dirs=.*:log.dirs=/var/lib/kafka:" \
      -i $pkgdir/usr/share/$pkgname/config/server.properties
    sed -e "s:log.dirs=.*:log.dirs=/var/lib/kafka:" \
      -i $pkgdir/usr/share/$pkgname/config/controller.properties
    
    # symlink scripts into PATH
    install -vdm 755 "$pkgdir/usr/bin/"

    for _sh_script in bin/*.sh; do
        ln -sv /usr/share/$pkgname/${_sh_script} "$pkgdir/usr/${_sh_script}"
    done

    # logs directory
    install -vdm 755 "$pkgdir/var/log/$pkgname/"
    # docs
    install -vDm 644 {LICENSE,NOTICE} -t "$pkgdir/usr/share/doc/$pkgname/"
    install -vDm 644 $srcdir/$pkgname.service -t "$pkgdir/usr/lib/systemd/system/"
    install -vDm 644 $srcdir/$pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
    install -vDm 644 $srcdir/$pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
