# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-doc
pkgver=10.5
pkgrel=1
pkgdesc='HTML documentation for SageMath'
arch=(any)
url='http://www.sagemath.org'
license=(GPL-2.0-or-later)
depends=(mathjax
         python-pplpy)
makedepends=(cython
             git
             python-docutils
             python-jupyter-client
             python-jupyter-sphinx
             python-pkgconfig
             python-pyzmq
             python-sphinx-copybutton
             python-sphinx-furo
             python-sphinx-inline-tabs
             sagemath)
source=(git+https://github.com/sagemath/sage#tag=$pkgver)
sha256sums=('dc62465ae2d07cde84176979a2211a4a72966827c98f82d3b238c2d94c7f433f')
options=(!strip) # nothing to strip, save packaging time

prepare() {
  cd sage

  SAGE_ROOT="$PWD" PATH="$PWD/build/bin:$PATH" src/doc/bootstrap
  make -C src/doc doc-src

  rm -r src/sage # Use the installed sage, not the one from the source tree
}

build() {
  cd sage/src

  export SAGE_SRC="$PWD" \
         SAGE_ROOT="$PWD/.." \
         SAGE_DOC="$PWD"/doc \
         PYTHONPATH="$PWD" \
         SAGE_NUM_THREADS=10 \
         LANGUAGE=C
  python -m sage_docbuild --no-pdf-links --mathjax all html
}

package() {
  cd sage/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
    rm -r $_dir
    ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}
