# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-doc
pkgver=10.8
pkgrel=1
pkgdesc='HTML documentation for SageMath'
arch=(any)
url='http://www.sagemath.org'
license=(GPL-2.0-or-later)
depends=(mathjax
         python-pplpy)
makedepends=(boost
             cython
             git
             meson
             python-docutils
             python-jupyter-client
             python-jupyter-sphinx
             python-pkgconfig
             python-pyzmq
             python-sphinx-copybutton
             python-sphinx-furo
             python-sphinx-inline-tabs
             sagemath)
source=(git+https://github.com/sagemath/sage#tag=$pkgver
        warnings.patch)
sha256sums=('d7d73c956b6930c0716841565f3d1dd29e21dba2e2ed28b6ff11391d7776e9c3'
            '06ce501d3be154f854bdbabd57f79287aec6b7d1b0ddf3abb904c68f79d51464')
options=(!strip) # nothing to strip, save packaging time

prepare() {
  cd sage

# Fix build errors due to missing pyx sources
  patch -p1 -i ../warnings.patch
}

build() {
  meson setup sage build
  meson compile -C build doc-html
}

package() {
  mkdir -p "$pkgdir"/usr/share/doc/sage
  cp -r build/src/doc/html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
    rm -r $_dir
    ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}
