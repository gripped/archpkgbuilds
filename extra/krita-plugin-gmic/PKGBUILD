# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=krita-plugin-gmic
pkgver=3.6.6.2
pkgrel=1
pkgdesc='GMic plugin for Krita'
arch=(x86_64)
url='https://krita.org/'
license=(CECILL-2.1
         CECILL-C)
depends=(fftw
         gcc-libs
         glibc
         gmic
         kcoreaddons5
         krita
         libx11
         qt5-base
         zlib)
makedepends=(extra-cmake-modules
             git
             qt5-tools)
source=(gmic-krita::git+https://github.com/vanyossi/gmic#tag=v$pkgver)
sha256sums=('9f505c7718d0a1ebcdc226f17f93163124c63e7ed0cce30a600d70b76e8eedc9')

prepare() {
# Remove bundled gmic, make sure to use system one
  rm -r gmic-krita/src
}

build() {
  cmake -B build -S gmic-krita/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_SYSTEM_GMIC=ON \
    -DGMIC_QT_HOST=krita-plugin
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm "$pkgdir"/usr/share/gmic/gmic_cluts.gmz # Provided by gmic
  install -Dm644 gmic-krita/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
