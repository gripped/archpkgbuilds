# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=miller
pkgver=6.15.0
pkgrel=1
pkgdesc='Name-indexed data processing tool'
arch=('x86_64')
url='https://github.com/johnkerl/miller'
license=('BSD-2-Clause')
depends=('glibc')
makedepends=('go' 'git')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('ef9484bf207c92c356d04439018fae4257024a5f5613a6d876629122d49b884778bb9b311e397c0a0113f941509feb2301c7cb260ebf9270f6c37b49f3c46b15')
b2sums=('c8c4ae7bfaae7a0c759530f4535a32d56346c7665323a3386d55864d90965d427298fb0222e1fb20033738fd88e5571aea48cf2f77ec598b4623251710091fae')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    ./cmd/mlr
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" mlr

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/mlr.1
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
