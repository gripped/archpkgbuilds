# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=miller
pkgver=6.14.0
pkgrel=1
pkgdesc='Name-indexed data processing tool'
arch=('x86_64')
url='https://github.com/johnkerl/miller'
license=('BSD-2-Clause')
depends=('glibc')
makedepends=('go' 'git')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('651a48422c01db5a7a6d65a62d2d888cc92f96acc59a5de0ef0160d3b7ba6543e4b01e008198d869b66aa107708e0a826dd974379b93d32ed8e699cca347833d')
b2sums=('0bb10d5cd540ca1ef4b7a1838ab310f955df2e695afa3db65c6a542c94a40560640d5967e79014f994dec7f868ece0827abb816c3c20608ff9a2f18f86c28e3e')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    ./cmd/mlr
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" mlr

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/mlr.1
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
