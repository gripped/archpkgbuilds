# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=miller
pkgver=6.17.0
pkgrel=1
pkgdesc='Name-indexed data processing tool'
arch=(x86_64)
url='https://github.com/johnkerl/miller'
license=(BSD-2-Clause)
depends=(glibc)
makedepends=(go git)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('9ef7c613d2303747902368954f5dabd1a3a31044b35ca4163a9ab13e00a1095e16253259eaaa9a71505ce2528aac088afb53830d77d6d6389080befe3f634b3b')
b2sums=('0269ac90c74425798ce8251f1e8a5eb29b5600564076db4a91a6a82d206d31553e6e590a4e5a792f44ace47823cea9e3bdabcf93c149772cf8d2689885770f88')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    ./cmd/mlr
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" mlr

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/mlr.1
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
