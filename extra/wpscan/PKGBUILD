# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: alexiobash < me (at) alexiobash (dot) com >

pkgname=wpscan
pkgver=3.8.28
pkgrel=3
epoch=1
pkgdesc='Black box WordPress vulnerability scanner'
url='https://wpscan.org'
arch=('x86_64')
license=('custom:WPScan')
depends=(
  curl
  libxml2
  libxslt
  libyaml
  ruby
  ruby-bundler
  ruby-erb
)
makedepends=(
  findutils
  coreutils
)
optdepends=(
  metasploit
)
options=('!strip' '!emptydirs')
install=wpscan.install
source=(https://github.com/wpscanteam/wpscan/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('66e2406182755bd5c10cee29bfbe73203f246f86acbd9061c9fa66ef0a6e9ff97a5d10a01ede9a7cd3146a85ee78e7178918e54ecc0b8640bd3fd94f73b26c78')
b2sums=('49731e8dfca95f7d1ffca901621b6ea072cc854eb1678624c748a4d8995a0c1125a5f0f198072ae545c0b327f268460af49f245e97dcf8ed034f274e71ca6175')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i '/bundler/ s|~>|>=|g' $pkgname.gemspec
  bundle config build.nokogiri --use-system-libraries
  bundle config set --local path vendor/bundle
  bundle config set --local without development test
  bundle config set --local force_ruby_platform true
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" -I/usr/include/libxml2"
  bundle install -j"$(nproc)"
  # reproducible builds: don't leak jobs count
  sed '/BUNDLE_JOBS/d' -i .bundle/config
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/${pkgname}"
  cp -ra --no-preserve=owner . "${pkgdir}/opt/${pkgname}"

  install -d "${pkgdir}/usr/bin"
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/bin/sh
BUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle exec ruby /opt/wpscan/bin/wpscan "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}"/opt/wpscan/spec
  cd "${pkgdir}"/opt/wpscan/vendor/bundle/ruby/*
  rm -rf cache gems/*/{ext,lib/*/*.so} \
    extensions/*/*/*/{mkmf.log,gem_make.out}
  # there are some insane world writable 777 dirs in sys-proctable
  find . -type d -exec chmod 755 {} \;
}

# vim: ts=2 sw=2 et:
