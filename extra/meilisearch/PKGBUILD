# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=meilisearch
epoch=1
pkgver=1.18.0
pkgrel=1
pkgdesc="Lightning Fast, Ultra Relevant, and Typo-Tolerant Search Engine"
arch=(x86_64)
url="https://docs.meilisearch.com/"
license=('MIT')
depends=(gcc-libs)
makedepends=(rust)
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/meilisearch/MeiliSearch/archive/v${pkgver}.tar.gz
        ${pkgname}.service ${pkgname}.tmpfiles ${pkgname}.sysusers ${pkgname}.conf)
sha512sums=('cd33f3a28b34d79ff51dcbb3dc8b15c1ea8c868c08b3e75fc44ed3401f0157ff15b915594d4f68133495c48a0dbbdd2d7acda651e9807a8f0ec442a7e8eb4203'
            'dd936591833fa4ca4832e0574bdfcfd5ccfcbe1c794c6be2f6a5c9f8e4b9588a95b6da24d29ee533043dda200b37e67e3a8ba04339c1e7530a4c95e17ee14268'
            '76284bf4c436a6b3365edd6bd12ffcb186b721d284b29933e77ddc841d333be9eeb2701c89fba1567a8eedda52787be8d3c5f55809a051488b7295fbb6842bce'
            '10066f77a81020ef5c975a26d510d64fdc61582dff1383e8777db0c78c4164a8b489b01e1ced97adf13d024be688d8f4ae970eda952882bae7f20881699dffd6'
            '9d63a9cca6cafb248fffcd4a6e5bac0905d4b87f3eb9793576003d3965e35e217c4c1d6b3206b25ef47143e7d538e71c746bc0352bcdf248ed19e12f58aedc12')
validpgpkeys=()

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked --bins --tests --package meilisearch
}

check() {
  cd "$pkgname-$pkgver"
  ulimit -n 8192
  cargo test --release --locked --package meilisearch
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

  install -D -m644 "${srcdir}/meilisearch.service"  "${pkgdir}/usr/lib/systemd/system/meilisearch.service"
  install -D -m644 "${srcdir}/meilisearch.sysusers" "${pkgdir}/usr/lib/sysusers.d/meilisearch.conf"
  install -D -m644 "${srcdir}/meilisearch.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/meilisearch.conf"
  install -D -m644 "${srcdir}/meilisearch.conf" "${pkgdir}/etc//meilisearch.conf"
}
