# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=brook
pkgver=20230101
pkgrel=1
pkgdesc="A cross-platform proxy/vpn software"
arch=('x86_64')
url="https://github.com/txthinking/brook"
license=('GPL')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/txthinking/brook.git#tag=v$pkgver")
sha512sums=('85845ae440ea5d97370ac85ff0e30d137873dba83724cdb7b929a9520b4fb529a1a71e107a58d1e67355ee62d059c9407010ec0953aa7cccade5982f4ebbcf26')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd brook/cli/brook
  go build -o brook
}

package() {
  cd brook/cli/brook
  install -Dm755 brook -t "$pkgdir"/usr/bin/
}
