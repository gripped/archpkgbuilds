# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=brook
pkgver=20230122
pkgrel=1
pkgdesc="A cross-platform proxy/vpn software"
arch=('x86_64')
url="https://github.com/txthinking/brook"
license=('GPL')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/txthinking/brook.git#tag=v$pkgver")
sha512sums=('2c9706c81a5869ef7770287414e182d0b38b9294f74113c3e8d1ae22516a8a85e8934ef406f16fd3ade690bef8e5fe6405bbd4b7a14b729b8d21e2dc2adf4333')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd brook/cli/brook
  go build -o brook
}

package() {
  cd brook/cli/brook
  install -Dm755 brook -t "$pkgdir"/usr/bin/
}
