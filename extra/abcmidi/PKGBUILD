# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Peter Sutton <peter@foxdogstudios.com>
# Contributor: Christopher Arndt <chris@chrisarndt.de>
# Contributor: Luis Pablo Gasparotto <lpgasparotto@gmail.com>
# Contributor: Phillip Fynan <pfynan@gmail.com>

pkgname=abcmidi
pkgver=2026_02_13
pkgrel=1
pkgdesc='Tools for ABC and MIDI files'
groups=(pro-audio)
arch=(x86_64)
url='https://github.com/sshlien/abcmidi'
license=(GPL-2.0-or-later)
makedepends=(git)
depends=(glibc)
source=("git+$url#tag=${pkgver//_/.}")
b2sums=('38bb403635ce440d8850941b85d11fc70b3e9190e69d90fc8435613f8dc5de25267ab7c075a209abd1f20e5af7e7ff19e5a8bd5dbfe4d1518015760be685cbcf')

build() {
  cd $pkgname
  export CFLAGS+=' -std=c99 -Wno-implicit-function-declaration'
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -m 755 -d "$pkgdir"/usr/share/doc/$pkgname/{programming,samples}
  install -m 644 -t "$pkgdir"/usr/share/doc/$pkgname/programming doc/programming/*.{abc,txt}
  install -m 644 -t "$pkgdir"/usr/share/doc/$pkgname/samples samples/*.abc
}
