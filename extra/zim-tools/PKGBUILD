# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=zim-tools
pkgver=3.3.0
pkgrel=5
pkgdesc="Various ZIM command line tools"
license=('GPL-3.0-or-later')
arch=('x86_64')
provides=('zimwriterfs')
conflicts=('zimwriterfs')
replaces=('zimwriterfs')
depends=('file' 'libzim>=9.0.0' 'docopt' 'gumbo-parser')
makedepends=('meson' 'mustache')
url='https://github.com/openzim/zim-tools'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	# https://github.com/openzim/libzim/pull/936
	"0001-Fix-build-with-ICU-76.patch")
sha256sums=('306bec256418d6e2caac1955076627440c40144f2552957412108c7883b50576'
            'b742b75e14f551f2d7c45957eee7d96ab1c8a32c23005534fafd0bd20c1da904')

prepare() {
	patch -Np1 -d "${pkgname}-${pkgver}" -i ../0001-Fix-build-with-ICU-76.patch
}

build() {
	cd "${pkgname}-${pkgver}"
	arch-meson build
	ninja -C build
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
