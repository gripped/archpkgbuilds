# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=zim-tools
pkgver=3.6.0
pkgrel=2
pkgdesc="Various ZIM command line tools"
license=('GPL-3.0-or-later')
arch=('x86_64')
provides=('zimwriterfs')
conflicts=('zimwriterfs')
replaces=('zimwriterfs')
depends=('file' 'libzim>=9.4.0' 'docopt' 'gumbo-parser')
makedepends=('meson' 'mustache')
url='https://github.com/openzim/zim-tools'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	# https://github.com/openzim/libzim/pull/936
	"0001-Fix-build-with-ICU-76.patch"
	# https://github.com/openzim/zim-tools/issues/477
	"0002-Allow-deprecated-function-calls.patch")
sha256sums=('5e740767d503858af58eab9c687c1c80b9a45882ef25f5d617103c50e9c1dde0'
            '53d50c766f826c89728b48c3b0718d551422e9dc3a7726f80e1c779878805e97'
            'e16e012a3555c75aa45affc49729740175f9d1a67d08a61a7e8dff9e45dc4419')

prepare() {
	patch -Np1 -d "${pkgname}-${pkgver}" -i ../0001-Fix-build-with-ICU-76.patch
	patch -Np1 -d "${pkgname}-${pkgver}" -i ../0002-Allow-deprecated-function-calls.patch
}

build() {
	cd "${pkgname}-${pkgver}"
	arch-meson build
	ninja -C build
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
