# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=8.0.3
pkgrel=1
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter46
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel.so)
_tag=246b2c7888d9138f048d5da08969a5ea3cc21e3c
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
b2sums=('3cd89f54c496e2bf6ab902c66e1e2033950d1c2daa5de7ebe425c2297f952e8ae54f923633e1f2a17c08d273bda10ac8b7a3a53381b13ac02754d5b306f79bbe')

pkgver() {
  cd wingpanel
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
