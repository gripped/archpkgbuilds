# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=8.0.0
pkgrel=2
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter46
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel.so)
_tag=d66d9ae9f97fe91ea8cc3dffaa2e05aab108b013
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
b2sums=('df18c690283a501747894c9bdec73d5bf9afc0a831c0552f4c5bd7f46b573c7a3b20ab93e0906b73b78897c3dc3866713b11456eb058e178207483dd9cb795dd')

pkgver() {
  cd wingpanel
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
