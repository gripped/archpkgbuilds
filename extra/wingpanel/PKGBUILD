# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=8.0.4
pkgrel=1
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter46
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel.so)
_tag=cb055fd0749034965f05c17d40fff80a9e5b9c25
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
b2sums=('12f93ff4d681fee63555a9b07a1384cfc8c82e7d5fe81b326ab8ecac040892ba45bcb31ad535f557d447712b789f88bc893c0a9e34d3380d9199391a57b9c7b0')

pkgver() {
  cd wingpanel
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
