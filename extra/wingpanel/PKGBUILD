# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=8.0.1
pkgrel=1
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter46
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel.so)
_tag=087cb2d67df0c0df0da4008271ac3194d3fbf954
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
b2sums=('42f9688345228155ce00901b70de201217b866196b299085c270fe366c5ed230f2afc523238dc642c199874e1dc25ce6fe497037eef62d198936cd8e97b750f3')

pkgver() {
  cd wingpanel
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
