# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: grawity <grawity@gmail.com>

pkgname=pkcs11-provider
pkgver=0.5
pkgrel=2
pkgdesc="A pkcs#11 provider for OpenSSL 3.0+"
url="https://github.com/latchset/pkcs11-provider"
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc' 'openssl>=3.0.7' 'p11-kit')
makedepends=('gcc' 'git' 'meson')
checkdepends=('expect' 'gnutls' 'nss' 'opensc' 'softhsm')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('ce38a1653e46bb049b9fefff07714e962cab180e7f689935838ad45786d69a89')

build() {
	cd "${pkgname}-${pkgver}"
	meson setup build --wipe --prefix=/usr --sysconfdir=/etc
	meson compile -C build
}

check() {
	cd "${pkgname}-${pkgver}"
	meson test -C build
}

package() {
	cd "${pkgname}-${pkgver}"
	meson install -C build --destdir="${pkgdir}"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/pkcs11-provider/README.md"
}
