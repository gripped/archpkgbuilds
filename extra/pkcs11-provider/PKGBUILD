# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: grawity <grawity@gmail.com>

pkgname=pkcs11-provider
pkgver=1.2.0
pkgrel=1
pkgdesc="A pkcs#11 provider for OpenSSL 3.0+"
url="https://github.com/latchset/pkcs11-provider"
arch=('x86_64')
license=('Apache-2.0')
depends=('glibc' 'openssl>=3.0.7' 'p11-kit')
makedepends=('git' 'meson')
checkdepends=('expect' 'gnutls' 'nss' 'opensc' 'softhsm')
source=("git+${url}.git#tag=v${pkgver}?signed")
sha256sums=('a3bf84c93061db59cb22f5032a9a87b85d78d199b9f9ed81861961b9eb1309e9')
validpgpkeys=('7C7BD146943B206BB645B64594EAD67E004B65AB')

build() {
	cd "${pkgname}"
	meson setup build --wipe --prefix=/usr --sysconfdir=/etc
	meson compile -C build
}

check() {
	cd "${pkgname}"
	meson test -C build
}

package() {
	cd "${pkgname}"
	meson install -C build --destdir="${pkgdir}"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
