# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=torchcodec
pkgname=python-$_name
pkgver=0.8.1
pkgrel=1
license=(BSD-3-Clause)
pkgdesc="PyTorch media decoding and encoding"
arch=(x86_64)
url="https://github.com/pytorch/torchcodec"
depends=(
  ffmpeg
  gcc-libs
  glibc
  google-glog
  python
  python-pytorch
)
makedepends=(
  cmake
  pkg-config
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-numpy
  python-pillow
  python-pytest
  python-torchvision
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('a1e3a3e12d59efade05fcc4129447f778042269acd2e97ee6ef763ca9e587fec632e4217d3cff59b4140798b589ecb9e23886c63b31f2915d574cb66108371f4')

build() {
  cd $_name-$pkgver

  # disable adding -Werror (causes errors even for deprecated symbols)
  export TORCHCODEC_DISABLE_COMPILE_WARNING_AS_ERROR=ON

  # setup.py requires setting this to use the system ffmpeg libraries
  # https://github.com/pytorch/torchcodec/blob/1acff9c4cffe8b65f77cd892162cd9ed1b2d75e2/setup.py#L176-L189
  # The Arch Linux ffmpeg package is GPL-licensed (--enable-gpl)
  export I_CONFIRM_THIS_IS_NOT_A_LICENSE_VIOLATION=1

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" test
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
