# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>

pkgname=archiso
pkgver=81
pkgrel=1
pkgdesc="Tools for creating Arch Linux live and install iso images"
arch=(any)
url="https://gitlab.archlinux.org/archlinux/archiso"
license=(GPL-3.0-or-later)
depends=(
  arch-install-scripts
  bash
  dosfstools
  e2fsprogs
  erofs-utils
  libarchive
  libisoburn
  mtools
  squashfs-tools
)
makedepends=(
  git
  python-docutils
)
checkdepends=(shellcheck)
optdepends=(
  'edk2-ovmf: for emulating UEFI with run_archiso'
  'gnupg: for PGP signature verification of rootfs over PXE'
  'grub: for grub support in the ISO'
  'openssl: for CMS signature verification of PXE artifacts and rootfs over PXE'
  'qemu-desktop: for run_archiso'
)
source=(git+$url.git?signed#tag=v$pkgver)
sha512sums=('13d7b2da69bc2315745b16bf5c1b9e12ae488e147bd4c13d6fc7ca3a1ca92f80a74f0dcd7424af10ae08dada7d1a129e90413dc5d99c5eb00edb8012edfc3c92')
b2sums=('a5496d8e9b91b7b46b41d40ac916b930a56a432e5558f06e14198bcb82818fd0e950ad69e4e085a33dc8daccccd6c7226157538fbaad58f97b77160b5d2e1bf0')
validpgpkeys=(
  '991F6E3F0765CF6295888586139B09DA5BF0D338' # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF' # nl6720 <nl6720@archlinux.org>
)

check() {
  make -k check -C $pkgbase
}

package() {
  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgbase
}
