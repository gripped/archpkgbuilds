# Maintainer : David Runge <dvzrv@archlinux.org>
# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>

pkgname=archiso
pkgver=86
pkgrel=1
pkgdesc="Tools for creating Arch Linux live and install iso images"
arch=(any)
url="https://gitlab.archlinux.org/archlinux/archiso"
license=(GPL-3.0-or-later)
depends=(
  arch-install-scripts
  bash
  dosfstools
  e2fsprogs
  erofs-utils
  libarchive
  libisoburn
  mtools
  squashfs-tools
)
makedepends=(
  git
  python-docutils
)
checkdepends=(shellcheck)
optdepends=(
  'edk2-ovmf: for emulating UEFI with run_archiso'
  'gnupg: for OpenPGP signature verification of rootfs over PXE'
  'grub: for grub support in the ISO'
  'openssl: for CMS signature verification of PXE artifacts and rootfs over PXE'
  'qemu-desktop: for run_archiso'
)
source=(git+$url.git?signed#tag=v$pkgver)
sha512sums=('7d0f349eeb1f0bf41b24d949e966b8a79ef697984dec6c9d82f17eb5fe20eb139e7cb259718045ff3e91a18e65265b2f9498f55bd2f43af9e41cdba2d8bc31d5')
b2sums=('cad0cd12c5efd4bf2ece1fe686108f859bb8db038af5898bdd4fcfcea70d53040e6908c1811c56aa443a09bf1187aa3778e7d362fafdf12811b5b813000b924d')
validpgpkeys=(
  '991F6E3F0765CF6295888586139B09DA5BF0D338' # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF' # nl6720 <nl6720@archlinux.org>
)

check() {
  make -k check -C $pkgbase
}

package() {
  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgbase
}
