# Maintainer : David Runge <dvzrv@archlinux.org>
# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>

pkgname=archiso
pkgver=85
pkgrel=1
pkgdesc="Tools for creating Arch Linux live and install iso images"
arch=(any)
url="https://gitlab.archlinux.org/archlinux/archiso"
license=(GPL-3.0-or-later)
depends=(
  arch-install-scripts
  bash
  dosfstools
  e2fsprogs
  erofs-utils
  libarchive
  libisoburn
  mtools
  squashfs-tools
)
makedepends=(
  git
  python-docutils
)
checkdepends=(shellcheck)
optdepends=(
  'edk2-ovmf: for emulating UEFI with run_archiso'
  'gnupg: for OpenPGP signature verification of rootfs over PXE'
  'grub: for grub support in the ISO'
  'openssl: for CMS signature verification of PXE artifacts and rootfs over PXE'
  'qemu-desktop: for run_archiso'
)
source=(git+$url.git?signed#tag=v$pkgver)
sha512sums=('4ccdeb1d59c36dca5c20fd4dbe615d13a5d15044c7da55eb7c51d074f83186517b46310d81cc814ba29dc168c26ca6ed772fae471c5bd8158533339c70777fa4')
b2sums=('9cc2c4a31940421d4d860acb6543f9a4fcbbc0b22379f538a77e78ea45ca976cd1e3e40d90ac9a273e04021a9655320d588084dfd843df3f5ef81b5408f29b5f')
validpgpkeys=(
  '991F6E3F0765CF6295888586139B09DA5BF0D338' # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF' # nl6720 <nl6720@archlinux.org>
)

check() {
  make -k check -C $pkgbase
}

package() {
  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgbase
}
