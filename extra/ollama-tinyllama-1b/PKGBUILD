# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=ollama-tinyllama-1b
pkgver=0.0.1
pkgrel=2
pkgdesc='The tinyllama (1B) large language model (LLM), for Ollama'
arch=(any)
url='https://github.com/jzhang38/TinyLlama'
license=(Apache-2.0)
provides=(ollama-tinyllama)
depends=(ollama)
optdepends=('llm-manager: for configuring which models to use for which tasks'
            'ollama-cuda: for using the GPU'
            'ollama-rocm: for using the GPU')
install=model.install
source=('manifest.json::https://registry.ollama.ai/v2/library/tinyllama/manifests/latest'
        'https://registry.ollama.ai/v2/library/tinyllama/blobs/sha256:2af3b81862c6be03c769683af18efdadb2c33f60ff32ab6f83e42c043d6c7816'
        'https://registry.ollama.ai/v2/library/tinyllama/blobs/sha256:af0ddbdaaa26f30d54d727f9dd944b76bdb926fdaf9a58f63f78c532f57c191f'
        'https://registry.ollama.ai/v2/library/tinyllama/blobs/sha256:c8472cd9daed5e7c20aa53689e441e10620a002aacd58686aeac2cb188addb5c'
        'https://registry.ollama.ai/v2/library/tinyllama/blobs/sha256:fa956ab37b8c21152f975a7fcdd095c4fee8754674b21d9b44d710435697a00d'
        'https://registry.ollama.ai/v2/library/tinyllama/blobs/sha256:6331358be52a6ebc2fd0755a51ad1175734fd17a628ab5ea6897109396245362')
sha256sums=('6ab90a29e923c7067ce2a12e7cf2abaa90b1c21866ff5fce3c5d38c9f5c36209'
            '2af3b81862c6be03c769683af18efdadb2c33f60ff32ab6f83e42c043d6c7816'
            'af0ddbdaaa26f30d54d727f9dd944b76bdb926fdaf9a58f63f78c532f57c191f'
            'c8472cd9daed5e7c20aa53689e441e10620a002aacd58686aeac2cb188addb5c'
            'fa956ab37b8c21152f975a7fcdd095c4fee8754674b21d9b44d710435697a00d'
            '6331358be52a6ebc2fd0755a51ad1175734fd17a628ab5ea6897109396245362')

package() {
  for f in sha256:*; do
    install -Dm644 "$f" "$pkgdir/var/lib/ollama/blobs/${f/:/-}"
  done
  install -Dm644 manifest.json "$pkgdir/var/lib/ollama/manifests/registry.ollama.ai/library/tinyllama/latest"
}
