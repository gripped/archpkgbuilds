# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=zita-lrx
pkgver=0.1.2
pkgrel=4
pkgdesc="Command line JACK application providing 2, 3, or 4-band, 4th order crossover filters"
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  clthreads
  gcc-libs
  glibc
  jack
)
source=(https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2)
sha512sums=('a3f74f788e2bb9d1ad13aa454552a61e08ca7b56fd286ee070aa746c40f9bc351ce8142dcd384a11b870b2379d531959c896a058e5601020ad82ac8a4cd92f09')
b2sums=('8f7babd3e58aa60996b8b4a9fb8fceca56b58f9e1dbf4065b19deba2589d7db89bfb392f66ae18a00f35573ba0c489a4f99b3d6cb69e95562532866b42f31ddb')

prepare() {
  # remove optimization
  sed -e '/native/d' -i $pkgname-$pkgver/source/Makefile
}

build() {
  make -C $pkgname-$pkgver/source
}

package() {
  make PREFIX=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver/source
  # docs
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,README*} -t "$pkgdir/usr/share/doc/$pkgname/"
  # examples
  install -vDm 644 $pkgname-$pkgver/examples/* -t "$pkgdir/usr/share/$pkgname/examples/" 
}
