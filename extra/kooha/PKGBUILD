# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=kooha
pkgver=2.3.1
pkgrel=1
pkgdesc="Elegantly record your screen"
arch=('x86_64')
url="https://github.com/SeaDve/Kooha"
license=('GPL-3.0-only')
depends=('glib2'
         'gst-plugin-pipewire'
         'gst-plugins-base'
         'gst-plugins-good'
         'gst-plugins-ugly'
         'gstreamer-vaapi'
         'gtk4'
         'libadwaita>=1:1.5.0'
         'libpulse'
         'x264'
         'xdg-desktop-portal')
makedepends=('cargo'
             'meson'
             'pango')
checkdepends=('appstream-glib'
              'xorg-server-xvfb')
optdepends=('gst-plugins-good: Encode captures as mp4')
sha256sums=('33fa92fdd4f5d315f74d59f12fb30cc38c23238b29eb7cad306e8a9b55dd9155')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")

prepare() {
  cd Kooha-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Kooha-$pkgver build
  meson compile -C build
}

check() {
  find -name hooha -executable
  # https://github.com/SeaDve/Kooha/issues/197
  # xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
