# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=alsa-scarlett-gui
pkgver=0.5.0
pkgrel=1
pkgdesc='GUI for the ALSA controls presented by the Focusrite Scarlett Gen 2/3 mixer driver'
arch=('x86_64')
url='https://github.com/geoffreybennett/alsa-scarlett-gui'
license=('GPL-3.0-or-later' 'LGPL-3.0-or-later')
depends=(
  'glibc'
  'glib2'
  'alsa-lib'
  'gtk4'
  'hicolor-icon-theme'
  'cairo'
  'pango'
  'openssl'
)
makedepends=('git')
source=(
  "$pkgname::git+$url#tag=$pkgver"
  'ftbfs-remove-redefinitions.patch'
)
sha512sums=('81e6ee5f21df5abbeec8b5611e277d582d3939d1ec7af6129005b303222175913a893d248945f95cc5ed0cbcc37b7dccca0b0297b6398d1840233af1b2a8b90e'
            '247a7bdb4e341d5da42454ee34119118944910f9ebf53bf87c49cfc077bfbe4b4ba94704e2ccd58c11ef6f5464cb03999fcfe151f55a2295b9e65caf16d483ae')
b2sums=('c4af243cd1b00c677ca427a8b219792af4272d56f4e975421f65eb5005e94f85d79c8652e99dfd90f147bfd9a41afc5d068991a2a261c922106dad18377a68f8'
        '0fe59b743b180bbb76ffa99b4b6a09207f1b4cd6beff60ffbd4ba177adde6a2e0c99840d5537c9a807c320e6773eddfe12d6fc537278c912fab5220f1ba22f3d')

prepare() {
  cd "$pkgname"

  # FTBFS: remove cflags already defined in makepkg.conf
  patch -p1 -i "$srcdir/ftbfs-remove-redefinitions.patch"
}

build() {
  cd "$pkgname"

  make \
    -C src \
    PREFIX=/usr \
    VERSION="$pkgver"
}

package() {
  cd "$pkgname"

  make \
    -C src \
    PREFIX="$pkgdir/usr" \
    VERSION="$pkgver" \
    install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md ./docs/*.md
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/img" img/*
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/demo" demo/*
}
