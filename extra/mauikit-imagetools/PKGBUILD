# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-imagetools
pkgver=4.0.0
pkgrel=3
pkgdesc='MauiKit Image Tools Components'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         exiv2
         kcoreaddons
         ki18n
         kquickimageeditor
         leptonica
         mauikit
         mauikit-filebrowsing
         qt6-base
         qt6-declarative
         qt6-location
         qt6-positioning
         tesseract)
makedepends=(extra-cmake-modules
             opencv)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        fix-path.patch)
sha256sums=('7f1f02924691cc764af5f79c0c227735d75cdc3c68457160bd302ccee1699f02'
            'SKIP'
            'e2bf50fd2859776c49190e791b00e3c352442810a27f5ceceb691a04d5b54d76')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

prepare() {
  patch -d $pkgname-$pkgver -p1 < fix-path.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
# Install missing header
  install -Dm644 $pkgname-$pkgver/src/code/geolocation/kdtree.hpp -t "$pkgdir"/usr/include/MauiKit3/ImageTools
}
