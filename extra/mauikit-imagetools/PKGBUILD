# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-imagetools
pkgver=4.0.2
pkgrel=3
pkgdesc='MauiKit Image Tools Components'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         kquickimageeditor
         leptonica
         libkexiv2
         mauikit
         mauikit-filebrowsing
         opencv
         qt6-base
         qt6-declarative
         qt6-location
         qt6-positioning
         tesseract)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ed636a9566c1fc0424cb59fa048d2c044d1138336dbabf08d227625f84e6a9f4'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
# Install missing header
  install -Dm644 $pkgname-$pkgver/src/code/geolocation/kdtree.hpp -t "$pkgdir"/usr/include/MauiKit3/ImageTools
}
