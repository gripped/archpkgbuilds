# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-imagetools
pkgver=4.0.1
pkgrel=1
pkgdesc='MauiKit Image Tools Components'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         exiv2
         kcoreaddons
         ki18n
         kquickimageeditor
         leptonica
         mauikit
         mauikit-filebrowsing
         qt6-base
         qt6-declarative
         qt6-location
         qt6-positioning
         tesseract)
makedepends=(extra-cmake-modules
             opencv)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('44f1e5f0133a13f3b998489b5936eb373cc529d826fa4a46c8c8ecb0d0d76ae3'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
# Install missing header
  install -Dm644 $pkgname-$pkgver/src/code/geolocation/kdtree.hpp -t "$pkgdir"/usr/include/MauiKit3/ImageTools
}
