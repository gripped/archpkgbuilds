# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=cargo-sort-derives
pkgver=0.11.0
pkgrel=1
pkgdesc="Cargo subcommand to sort derive attributes"
arch=(x86_64)
url="https://github.com/lusingander/cargo-sort-derives"
license=(MIT)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('05205cfaf23db410d6861bb83ddc511dc0aa60bdeba322c587689e162a008a857f40fca6ab4e557b645090b24c9bd8d7ce4c2101b032b7e6579bac28934e991c')
b2sums=('96eeea9918aa1924074dd71420db270dee01190d72a31c2fa84bc521a366efbd8389b03ccf6d4a08a16d79942888df0ed1b8dbe97574f768892b5952e7c4c315')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
