# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=cargo-sort-derives
pkgver=0.12.0
pkgrel=1
pkgdesc="Cargo subcommand to sort derive attributes"
arch=(x86_64)
url="https://github.com/lusingander/cargo-sort-derives"
license=(MIT)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('43979a86f998b8411faf2919ad4f996b042297079384c167ea353aa56acc6bde43b620f0f078e72d2567b49b29504063a98aa988cf3e3c22fda6190331e86c61')
b2sums=('3b497168d9295dc5443e6b688d044908cafcb9b2e28f7957c978e0de28b5762603d186c9f782c37afd1e470367eeec1718b9078565bd4226c58fc3e92f7871fb')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
