# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=cargo-sort-derives
pkgver=0.10.0
pkgrel=1
pkgdesc="Cargo subcommand to sort derive attributes"
arch=(x86_64)
url="https://github.com/lusingander/cargo-sort-derives"
license=(MIT)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('f67be32df6b0edded691ebbd0f979774ce6fc4e48029e3c0abb5f36f558ca9d7d3ab4fa11da208aa550288926b8e416e9ebc76b35f4d3c193b7fdf4111e041bd')
b2sums=('976d0d285f0295f7047c4d2bcdc47b5aade79b003811d289d3f66e06516f37a262d61ec19a9509b4fa06f1051403219f022cb34842aacb1a4f25ab50ac3f8908')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
