# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=cargo-sort-derives
pkgver=0.10.1
pkgrel=1
pkgdesc="Cargo subcommand to sort derive attributes"
arch=(x86_64)
url="https://github.com/lusingander/cargo-sort-derives"
license=(MIT)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('d8c00ac2e0c4410229d4872df029e7d1e18c16ce38fb5c8e0ea9dcd407ace081a3f4c5928ea52b5bc4c3ff376456a6c8a12ce0326e689115ada98f5b7af1576f')
b2sums=('b3053e03d531d4c9961c30e4338374f0d3a9302c58f3448f99437b9b9addc91ead423a0e2782d377d0b4111017994f7c31fa0d73b9c870559c50125cc72b590b')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
