# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-mame
pkgver=93008
pkgrel=1
arch=(x86_64)
pkgdesc='MAME Arcade core'
url=https://github.com/libretro/mame
license=(custom)
groups=(libretro)
depends=(
  alsa-lib
  glibc
  libretro-core-info
  libgl
  zlib
)
makedepends=(
  git
  libglvnd
  python
)
_commit=20db0f242e4e11a476b548dd57d2ef9cc3e84f03
source=(libretro-mame::git+https://github.com/libretro/mame.git#commit=${_commit})
b2sums=('2181c33fcdb51617afd48885e986da61fe0662875d0b6184366660c08bfab29c20daafd53e40bfd277e10eece567991d95f591e7c9177d9e00289fb29b18619a')

pkgver() {
  cd libretro-mame
  git rev-list --count HEAD
}

build() {
  cd libretro-mame
  make -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mame/mame_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mame/docs/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mame/
}

# vim: ts=2 sw=2 et:
