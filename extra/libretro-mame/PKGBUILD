# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-mame
pkgver=91899
pkgrel=1
arch=(x86_64)
pkgdesc='MAME Arcade core'
url=https://github.com/libretro/mame
license=(custom)
groups=(libretro)
depends=(
  alsa-lib
  glibc
  libretro-core-info
  libgl
  zlib
)
makedepends=(
  git
  libglvnd
  python
)
_commit=231b5e6d621ed93168ad19eed6c0e3390d4e81ff
source=(libretro-mame::git+https://github.com/libretro/mame.git#commit=${_commit})
b2sums=('37afcc5fffc4a8a711088cb95398ec5857370f81399af66e6044aa6b69c8db229d8892472ae9bd5f0585417d934d74073be74eceb9c51254adbce999b8357ff9')

pkgver() {
  cd libretro-mame
  git rev-list --count HEAD
}

build() {
  cd libretro-mame
  make -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mame/mame_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mame/docs/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mame/
}

# vim: ts=2 sw=2 et:
