# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-mame
pkgver=92650
pkgrel=1
arch=(x86_64)
pkgdesc='MAME Arcade core'
url=https://github.com/libretro/mame
license=(custom)
groups=(libretro)
depends=(
  alsa-lib
  glibc
  libretro-core-info
  libgl
  zlib
)
makedepends=(
  git
  libglvnd
  python
)
_commit=750b36ca4cc65e7142f97d0ffd0235085bbd20f7
source=(libretro-mame::git+https://github.com/libretro/mame.git#commit=${_commit})
b2sums=('b1c09b69a9c86a9b6d6a85d64f8c81befe2cdfef57177d1c59b990bf277c5ece7165fccfed736740849ee339ed6471de217dc23dafc8e5396bdbe2417e3bdd4a')

pkgver() {
  cd libretro-mame
  git rev-list --count HEAD
}

build() {
  cd libretro-mame
  make -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mame/mame_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mame/docs/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mame/
}

# vim: ts=2 sw=2 et:
