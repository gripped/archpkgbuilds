# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-mame
pkgver=94042
pkgrel=1
arch=(x86_64)
pkgdesc='MAME Arcade core'
url=https://github.com/libretro/mame
license=(custom)
groups=(libretro)
depends=(
  alsa-lib
  glibc
  libretro-core-info
  libgl
  zlib
)
makedepends=(
  git
  libglvnd
  python
)
_commit=a638be8510d061f435e2abd46e514bb6c7e41262
source=(libretro-mame::git+https://github.com/libretro/mame.git#commit=${_commit})
b2sums=('3de39e7346ba7876cb1ac6b81c53e5a9549a42490bd425b61727ef170676e7cfa02a2df69ac00f5af56c812c688fda683cf149031739f93d676b715d66765adb')

pkgver() {
  cd libretro-mame
  git rev-list --count HEAD
}

build() {
  cd libretro-mame
  make -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mame/mame_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mame/docs/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mame/
}

# vim: ts=2 sw=2 et:
