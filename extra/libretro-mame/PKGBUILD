# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-mame
pkgver=93765
pkgrel=1
arch=(x86_64)
pkgdesc='MAME Arcade core'
url=https://github.com/libretro/mame
license=(custom)
groups=(libretro)
depends=(
  alsa-lib
  glibc
  libretro-core-info
  libgl
  zlib
)
makedepends=(
  git
  libglvnd
  python
)
_commit=40edadab2e445bfda9d206def9508b43b11fb96a
source=(libretro-mame::git+https://github.com/libretro/mame.git#commit=${_commit})
b2sums=('267500bbd6e495e25626c802a5287e7fc3b0db092cbbe2aa7f98e565887d6cde69fd8f508926cd1635c5096b1087326e839d2bf6603ef9ade44fa1df295defef')

pkgver() {
  cd libretro-mame
  git rev-list --count HEAD
}

build() {
  cd libretro-mame
  make -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mame/mame_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mame/docs/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mame/
}

# vim: ts=2 sw=2 et:
