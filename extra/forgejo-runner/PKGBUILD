# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=forgejo-runner
pkgver=12.5.1
pkgrel=1
pkgdesc='Continuous integration for Forgejo'
arch=(x86_64)
url='https://code.forgejo.org/forgejo/runner'
license=(MIT Apache-2.0)
depends=(glibc)
makedepends=(git go)
backup=(etc/forgejo-runner/config.yaml)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  systemd-service.patch
  sysusers.conf
  tmpfiles.conf
)
sha512sums=('b806ed35fc83b7b17d217bbd2b83a491d463cfb1931eecf38ad002a94b95b39e5465d6d8edbe8118bb9c9de3837844b50b034340429e232976a6b8f42b7e014b'
            '04d07de2819a6e34339b80a305e4635fae2bce23078d8a9f2057a4bd017b731ef70b3e2b9230d6d8012f619ae7f02b39aba0eaf6924fbc2704dfead2054ddaab'
            '383a5cb2e6c8cf267dc1cd2de2fa97994b332521d449a9d903a639eaa466335756e8dd2fd13a8b3e233f4ceec6da2d293ba54e0fdbd09c54c6bc77d0314a7c49'
            'e96d43feb05007371d1a876f112c5f687fb99f75e90ac41447577dfdd98bab69ff3ddede4350df3022955aba1775feb11d1696fa17084824b688567b01d557fc')
b2sums=('d7ad90d3d6009b72e06472e95c16f79b708bebf0008c8478782a28a28b53555f022092fa09e6377b17afa7abb02e5eab89c48e1fc29999c5e137eac706c2a15c'
        '858c47b0862100ce316ca470eb09da7026c4110397969dcec27e233502a8403fd5d521ced9e2d56e9670d23381eeb99f496d1f7f876f13f625f7f1dd83a24f6b'
        'f3f86bb89290926ffddb76f3362ce3f1f1c880f0685e52995d23860c25a8ce93cff52c9ae06c741f54ca7f8749ff3049bad85db5651edf0a2de3d0e3b41377a5'
        'd900883bb5bf529ccae64a1411e2d0123f9dde633b5ca0091924cc1dcf3d0956630bb40c3aede57924614942b6d95e1626b90f3cfade3daa3cb5a717a8a7921d')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  patch -p1 -i "$srcdir/systemd-service.patch"
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -s -w -buildid='' \
    -extldflags '${LDFLAGS}' \
    -X code.forgejo.org/forgejo/runner/v${pkgver%%.*}/internal/pkg/ver.version=v$pkgver" \
    -o build/forgejo-runner \
    .

  ./build/forgejo-runner generate-config > config.yaml
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/forgejo-runner

  # systemd integration
  install -vDm640 -t "$pkgdir/etc/forgejo-runner" config.yaml
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" contrib/forgejo-runner.service
  install -vDm644 "$srcdir/sysusers.conf" "$pkgdir/usr/lib/sysusers.d/forgejo-runner.conf"
  install -vDm644 "$srcdir/tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/forgejo-runner.conf"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
