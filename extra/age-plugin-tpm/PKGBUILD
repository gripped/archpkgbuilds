# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=age-plugin-tpm
pkgver=1.0.0
pkgrel=1
pkgdesc="TPM plugin for age"
arch=("x86_64")
url="https://github.com/Foxboron/age-plugin-tpm"
license=("MIT")
depends=("glibc")
makedepends=("go" "git")
checkdepends=("age")
source=("$pkgname-$pkgver.tar.gz::https://github.com/Foxboron/$pkgname/archive/v${pkgver}.tar.gz")
sha256sums=('f26cc619c1b3eeeac17f764b23d11904c9ae8e8dcf85965dc0bf8cee8687154d')

build(){
    cd "${pkgname}-${pkgver}"
    go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-X main.Version=${pkgver} -linkmode external -extldflags \"${LDFLAGS}\"" \
        -o . ./...
}

check(){
    cd "${pkgname}-${pkgver}"
    go test ./...
}

package(){
    cd "${pkgname}-${pkgver}"
    install -Dm755 -t "${pkgdir}/usr/bin/" age-plugin-tpm
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
