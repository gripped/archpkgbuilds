# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-network
pkgver=8.0.1
pkgrel=1
pkgdesc='Wingpanel Network Indicator'
arch=(x86_64)
url=https://launchpad.net/wingpanel-indicator-network
license=(GPL3)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk3
  libgee
  libgranite.so
  libnm
  libwingpanel.so
  networkmanager
  nm-connection-editor
)
makedepends=(
  git
  meson
  vala
)
_tag=3071b9ac837b83773cc386501371dcec923154fc
source=(wingpanel-indicator-network::git+https://github.com/elementary/wingpanel-indicator-network.git#tag=${_tag})
b2sums=('74431171019661529c1825a0b94166466ba8f19468cace97d02a18e859c051990c6e39d55ceb699c56bedae46f9ae99e8fd24ec714570eb59677cf5af0e9ffdb')

pkgver() {
  cd wingpanel-indicator-network
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-network build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
