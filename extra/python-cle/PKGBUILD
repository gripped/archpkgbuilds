# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Xeonacid <h.dwwwwww at gmail dot com>

_pyname=cle
pkgname=python-${_pyname}
pkgver=9.2.123
_testbinaries_ver=9.2.123
pkgrel=2
pkgdesc='Loads binaries and their libraries, resolves imports and provides an abstraction of process memory'
url='https://github.com/angr/cle'
arch=('any')
license=('custom:BSD')
depends=('python' 'python-pyelftools' 'python-cffi' 'python-pyvex' 'python-pefile' 'python-sortedcontainers')
optdepends=(
  'python-claripy: symbolic constraints'
  'python-minidump: minidump'
  'python-pyxbe: xbe'
  'python-arpy: ar'
  'python-pyaxmlparser: apk'
)
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-minidump' 'python-pyxbe' 'python-arpy' 'python-claripy' 'python-pyaxmlparser' 'python-cart')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/angr/binaries/archive/v${_testbinaries_ver}/angr-binaries-${_testbinaries_ver}.tar.gz")
sha512sums=('79720a1017a15576803d5284827f5308b520c1261e61ee3735b4d908d200a4cbd54e5947baa110ebd4c7c7fbcc82b121d57f36445cb908d8565676b420fe5411'
            '5f88c4fb5e811426d83fcb7c75fcb666ecd634abdd32e30d458d2cdf1f4cfabf4a16ec6794129c719e0bdd245363f2693a008ba414bb1bddd0d4c4defda36f89')
b2sums=('17558ea60665310c55f0814282952954ebe7e581adb3668acca5b1cbd59fd6a66ec7cb5cf2347f034493e7e2398864d25aac123485652932b11023697017da3c'
        'd8f2d5287f4c5eb8c06ab2a2429fd951998a3a5a6dcb4a1fa3867598c1076cc32461940e92fe281ae8843ad856fc382cd3343ede9153372ccad724cacccd0b6d')

prepare() {
  ln -s binaries-${_testbinaries_ver} binaries
}

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
