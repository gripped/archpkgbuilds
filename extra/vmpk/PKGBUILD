# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.9.1
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=(x86_64)
url="https://vmpk.sourceforge.io/"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
)
makedepends=(
  cmake
  docbook-xsl
  drumstick
  qt6-tools
)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/${pkgver/a/}/$pkgname-$pkgver.tar.bz2)
sha512sums=('fddeb5c87c442eb5aff85a871e0daa05ce2d56f006c8b710bad35152e3696f5f2f7eac5fc713afec461940d45d4a4156e81773aad1ea454494d65e47aac93016')
b2sums=('752f395193509f829e5c259e833a0e41de7cd98f773a7c8c67df6e55d66e25c532530234104a50fffcce632473eeea7fbed1128a2a8417db8eada2759bab6016')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    drumstick libdrumstick-rt.so libdrumstick-widgets.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname"
}
