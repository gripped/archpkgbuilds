# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.9.2
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=(x86_64)
url="https://vmpk.sourceforge.io/"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
)
makedepends=(
  cmake
  docbook-xsl
  drumstick
  qt6-tools
)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/${pkgver/a/}/$pkgname-$pkgver.tar.bz2)
sha512sums=('a585c4e63934a11b834e633133c5dcd407011a669a894b31b72875ee73170aa7e260ab13247b8fbe79acef6e306bbfefcc08b6cd0d4d621d2b80e1d000b60084')
b2sums=('e3700d092958ebea80e28ef3af4aded61cf17179494905490d00b83fbf068bae103908dce8b890e5cb66da7b11ad0088d4b0d1568938ad2d0f8e60dce086a9fc')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    drumstick libdrumstick-rt.so libdrumstick-widgets.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname"
}
