# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-pyzstd
_pyname=${pkgname#python-}
pkgver=0.16.2
pkgrel=2
pkgdesc='Python bindings to Zstandard (zstd) compression library'
arch=(x86_64)
url="https://pypi.org/project/$_pyname"
license=(BSD-3-Clause)
depends=(python
         zstd)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('179c1a2ea1565abf09c5f2fd72f9ce7c54b2764cf7369e05c0bfd8f1f67f63d2')

prepare() {
	cd "$_archive"
	# HACK: workaround upstream setuptools version check specifically for Windows.
	sed -i 's/setuptools>=64,<74/setuptools/' pyproject.toml
}

build() {
	cd "$_archive"
	python -m build -wn -C=--build-option=--dynamic-link-zstd
}

check() {
	cd "$_archive"
	local _pyver=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
	export PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$_pyver"
	python -m unittest discover tests
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
