# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>

pkgname=vnstat
pkgver=2.13
pkgrel=2
pkgdesc="A console-based network traffic monitor"
arch=('x86_64')
url="https://humdi.net/vnstat/"
license=('GPL')
depends=('bash' 'sqlite')
makedepends=('gd' 'git')
optdepends=('gd: image output')
backup=(etc/vnstat.conf)
source=(git+https://github.com/vergoh/vnstat.git#tag=v${pkgver})
sha256sums=('ce4a9fee6bffc7960ba138991c68f5d8c0ad0bc375ed6f949f8d8c6e18b38335')
#validpgpkeys=(23EF1DD76E65248FB055201ADAFE84E63D140114) # Teemu Toivola

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm0755 examples/vnstat.cgi -t "$pkgdir"/usr/share/doc/vnstat/examples
  install -Dm0644 examples/systemd/vnstat.service -t "$pkgdir"/usr/lib/systemd/system
}
