# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jérémie Detrey <Jeremie.Detrey@loria.fr>

_name=treestyletab
pkgname=firefox-tree-style-tab
pkgver=4.0.11
pkgrel=1
pkgdesc='Firefox extension to show tabs like a tree'
arch=('any')
url=https://piro.sakura.ne.jp/xul/_treestyletab.html.en
license=('MIT' 'MPL-1.1' 'MPL-2.0' 'PSF-2.0')
groups=('firefox-addons')
makedepends=('git' 'jq' 'npm' 'strip-nondeterminism' 'zip')
source=(
  "git+https://github.com/piroor/treestyletab.git#tag=$pkgver"
  'git+https://github.com/piroor/webextensions-lib-configs.git'
  'git+https://github.com/piroor/webextensions-lib-dom-updater.git'
  'git+https://github.com/piroor/webextensions-lib-event-listener-manager.git'
  'git+https://github.com/piroor/webextensions-lib-l10n.git'
  'git+https://github.com/piroor/webextensions-lib-menu-ui.git'
  'git+https://github.com/piroor/webextensions-lib-options.git'
  'git+https://github.com/piroor/webextensions-lib-placeholder-parser.git'
  'git+https://github.com/piroor/webextensions-lib-rich-confirm.git'
  'git+https://github.com/piroor/webextensions-lib-shortcut-customize-ui.git'
  'git+https://github.com/piroor/webextensions-lib-tab-favicon-helper.git'
)
b2sums=('7f5e1a1f7c664d11754245e064827a07591e14177f661273ba9f68854e8ad1a5b0b5de637ec7941cca5cb17550a176140cbeedaa43560ada9808cb0c47e564c5'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd $_name
  git submodule init
  for i in ../webextensions*; do
    git config submodule.submodules/"${i##../}".url "$i"
  done
  git -c protocol.file.allow=always submodule update
  cd webextensions
  sed -i 's/^\(install_extlib\|xpi\): .*/\1:/' Makefile
  npm ci
}

build() {
  cd $_name/webextensions
  make install_extlib
  make xpi
  strip-nondeterminism -t zip $_name-we.xpi
}

package() {
  cd $_name/webextensions
  install -Dm644 $_name-we.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/$_name@piro.sakura.ne.jp.xpi
}
