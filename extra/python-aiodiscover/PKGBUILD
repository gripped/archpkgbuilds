# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-aiodiscover
pkgver=2.6.1
pkgrel=1
pkgdesc='Discover Hosts via ARP and PTR lookup'
arch=(any)
url=https://github.com/bdraco/aiodiscover
license=(Apache-2.0)
depends=(
  python
  python-dnspython
  python-ifaddr
  python-netifaces
  python-pyroute2
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
_tag=f76c7915091a9e91d67ed69f5e2ba95de1262380
source=(git+https://github.com/bdraco/aiodiscover.git#tag=${_tag})
b2sums=('7d5bed6a7561d8c4d49dad5e8f5edc53aa1f02a5f456ed6dd65fa505a5de40c4004929f0f9c34553344c046f9625bcd2d0d89c83adf6faf986e6b1e69ea48f73')

pkgver() {
  cd aiodiscover
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiodiscover
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" aiodiscover/dist/*.whl
}

# vim: ts=2 sw=2 et:
