# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-aiodiscover
pkgver=2.7.0
pkgrel=1
pkgdesc='Discover Hosts via ARP and PTR lookup'
arch=(any)
url=https://github.com/bdraco/aiodiscover
license=(Apache-2.0)
depends=(
  python
  python-dnspython
  python-ifaddr
  python-netifaces
  python-pyroute2
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
_tag=8ee4840b3cf6f9762c25db3418ba45899e47d286
source=(git+https://github.com/bdraco/aiodiscover.git#tag=${_tag})
b2sums=('abb0248180883207a90858838213d2f7173143a1b4137809db0b9eb98ac1d6eb05786bc39b7de108265fdc101a08303546f79ac755dd847dec40fad61af8674f')

pkgver() {
  cd aiodiscover
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiodiscover
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" aiodiscover/dist/*.whl
}

# vim: ts=2 sw=2 et:
