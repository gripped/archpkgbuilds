# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-aiodiscover
pkgver=2.7.1
pkgrel=2
pkgdesc='Discover Hosts via ARP and PTR lookup'
arch=(any)
url=https://github.com/bdraco/aiodiscover
license=(Apache-2.0)
depends=(
  python
  python-dnspython
  python-ifaddr
  python-netifaces
  python-pyroute2
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
_tag=60b1fc561e101d4c85f7d5b577c6af021ea6ed5d
source=(git+https://github.com/bdraco/aiodiscover.git#tag=${_tag})
b2sums=('8621acccc8c0d194f4c897e52ed726b748778a94cfb78da0efc9d7fc302290fc31a4aa4a2c558735becd2cd0b43e9b077c1c568ef3d4b26a8e78b172e095adf7')

pkgver() {
  cd aiodiscover
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiodiscover
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" aiodiscover/dist/*.whl
}

# vim: ts=2 sw=2 et:
