# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Maël Lavault <moimael@neuf.fr>

pkgname=abiword
pkgver=3.0.5
pkgrel=5
pkgdesc='Fully-featured word processor'
arch=(x86_64)
url='http://www.nl.abisource.com/'
license=(GPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  enchant
  fontconfig
  fribidi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  goffice
  gtk3
  hicolor-icon-theme
  libgsf
  libical
  libjpeg-turbo
  libpng
  librsvg
  libx11
  libxml2
  libxslt
  pango
  rasqal
  readline
  redland
  wv
  zlib
)
makedepends=(
  autoconf-archive
  boost
  git
  gobject-introspection
  libwmf
  libwpg
  libwps
  link-grammar
  python-gobject
  telepathy-glib
)
optdepends=(
  'libwmf: WMF Import plugin'
  'libwpg: WordPerfect Graphics Importer plugin'
  'libwps: WordPerfect and Microsoft Works Importer plugin'
  'link-grammar: AbiGrammar plugin'
  'telepathy-glib: Collaboration plugin'
)
conflicts=(abiword-plugins)
replaces=(abiword-plugins)
source=(
  "git+https://gitlab.gnome.org/World/AbiWord.git?signed#tag=release-$pkgver"
  abiword-3.0.0-librevenge.patch
  abiword-3.0.1-libwps-0.4.patch
  abiword-fix-appstream-data.patch
)
b2sums=(
  a6be891747551888053d432772a7216d7b07178325c9b85d8cbe41829c7ccd033833b4595fefb98497e523bd20e1c0e7ac455043d1330e332d9c2f500089d8a8
  67a55edc78d4332bde26467c3791103d6c950d7c7f000f8c03783f8d7f49d5706a1fa9787a037198c06df3a0d1c4a52521228a41ae6766cdfb20db019ce4bd09
  7bc9ebf5e6005af2e72ecd7f9db38e9ca3808bc2ee4a2bf64510f35461511ccc9888a24ce7c3e9b84575ec7842288542af669ee0f45a7f7be1558f38fcc8baa5
  cecfa249c504d2b4a342ad6b7140be67d7422c350231e10eb1411e9eef834b7ea2a92a761d1cfced09e0520d820e1188339a3b62f0a07efed70f638502704aa5
)
validpgpkeys=(6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3) # Hubert Figuiere <hub@figuiere.net>

prepare() {
  cd AbiWord

  # Apply all fixes from the stable branch
  git cherry-pick -n 5e9bf43e6a711c918ced5d747b6c24ed3b19e824..2c8b8db64fee3cc155dd78249a614512710c664c

  # Fix python override code to work with Python 3.x
  git cherry-pick -n 51787d61993cb3981c18e4cf174fc229734fba1e

  # Port to enchant 2
  git cherry-pick -n ae05e92df5a5d6151641622c83d35a6fdba47b1f

  # Install AppStream data into metainfo directory
  sed -i 's|/appdata|/metainfo|' Makefile.am

  # Fix build with librevenge based import filters
  patch -Np0 -i ../abiword-3.0.0-librevenge.patch

  # Port to libwps 0.4
  patch -Np1 -i ../abiword-3.0.1-libwps-0.4.patch

  # Fix AppStream data
  patch -Np1 -i ../abiword-fix-appstream-data.patch

  ./autogen-common.sh
  autoreconf -fi
}

build() {
  cd AbiWord
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-plugins \
    --enable-clipart \
    --enable-templates \
    --enable-introspection
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd AbiWord
  make DESTDIR="$pkgdir" install
}
