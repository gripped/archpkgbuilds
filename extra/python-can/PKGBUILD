# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kyle Manna <kyle[at]kylemanna[d0t]com>

pkgname=python-can
pkgver=4.6.1
pkgrel=2
pkgdesc='Controller Area Network support for Python'
arch=(any)
url=https://github.com/hardbyte/python-can
license=(LGPL-3.0-only)
depends=(
  python
  python-msgpack
  python-packaging
  python-typing_extensions
  python-wrapt
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
_tag=702ec3eb79530b9242a1167c049cfe2cfec235e5
source=(git+https://github.com/hardbyte/python-can.git#tag=${_tag})
b2sums=('ad9d69923de97c36f2e52f617b5b929b4a8962d99f44917844f140fb551a020b078f29865f820d94f2c0a7179e6d342e45c8806ebf0ea3cdb3d362190a3ce422')

pkgver() {
  cd python-can
  git describe --tags | sed 's/^v//'
}

build() {
  cd python-can
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" python-can/dist/*.whl
}

# vim: ts=2 sw=2 et:
