# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-service-manager
pkgver=1.0.14
pkgrel=1
pkgdesc='Manage DBus service on Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-service-manager"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'gcc-libs' 'glibc' 'qt6-base' 'systemd-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/deepin-service-manager.git#tag=$pkgver")
sha512sums=('47216609942c7d71d9854b67d5b4ed9ea3740f9a142ba61730aaf1c8954b2b7f581dd94fcdb3d8d9e4abca6ebcf3ef948ca909aac0f0e9b59ae094742cdd4c50')

build() {
  cmake -GNinja -B build -S $pkgname -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  cmake --build build
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
