# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: justforlxz <justforlxz@gmail.com>

pkgname=deepin-service-manager
pkgver=1.0.17
pkgrel=1
pkgdesc='Manage DBus service on Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-service-manager"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'gcc-libs' 'glibc' 'qt6-base' 'systemd-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/linuxdeepin/deepin-service-manager.git#tag=$pkgver")
sha512sums=('b756b9d2d607f7ecef4e18dacf7853df640ecd929030b05b8c0d040917f2ad17120d2252b1e0aa95dd42baaea9c6b876f2545e874a89f079333f66942b040494')

build() {
  cmake -GNinja -B build -S $pkgname -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  cmake --build build
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
