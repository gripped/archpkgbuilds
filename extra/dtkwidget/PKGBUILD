# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.7.15
pkgrel=1
pkgdesc='Deepin base graphical widgets library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
# qt5-imageformats is needed for webp (dci) icons
depends=('dtkcore' 'dtkgui' 'gcc-libs' 'glibc' 'gsettings-qt' 'libx11' 'libxcb' 'libxext' 'libxi'
         'qt5-base' 'qt5-imageformats' 'qt5-svg' 'qt5-x11extras' 'startup-notification' 'xcb-util')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
replaces=('deepin-tool-kit')
source=("git+https://github.com/linuxdeepin/dtkwidget.git#tag=$pkgver")
sha512sums=('017440be5dc0c8ddfa062c52bc1b1647cd3375ad06580d39a6b29b06450103b2af9f9fbd59b98651305405108118a9ad22a533eb11afc5fc5bf485dff6a1384e')

build() {
  cd dtkwidget
  # Disable plugins because they are not going to be installed
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DBUILD_DOCS=ON \
      -DBUILD_PLUGINS=OFF \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver \
      -DCMAKE_BUILD_TYPE=None
  ninja
}

check() {
  cd dtkwidget
  # TODO
  # ninja test || echo "Tests failed"
}

package() {
  cd dtkwidget
  DESTDIR="$pkgdir" ninja install
}
