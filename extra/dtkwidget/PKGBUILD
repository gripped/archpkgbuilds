# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.7.17
pkgrel=1
pkgdesc='Deepin base graphical widgets library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL-3.0-or-later')
# qt5-imageformats is needed for webp (dci) icons
depends=('dtkcore' 'dtkgui' 'gcc-libs' 'glibc' 'gsettings-qt' 'libx11' 'libxcb' 'libxext' 'libxi'
         'qt5-base' 'qt5-imageformats' 'qt5-svg' 'qt5-x11extras' 'startup-notification' 'xcb-util')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
replaces=('deepin-tool-kit')
source=("git+https://github.com/linuxdeepin/dtkwidget.git#tag=$pkgver")
sha512sums=('7cdead554d110c60905f87ed4e0dcd3a0bd5e3e59a4cfb31f50eb9170ec6b60cda5b9638e81649fc42ce5490910953cfe7c6bda9d267d8967adc19e1c75830e7')

build() {
  cd dtkwidget
  # Disable plugins because they are not going to be installed
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DBUILD_DOCS=ON \
      -DBUILD_PLUGINS=OFF \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=None
  ninja
}

check() {
  cd dtkwidget
  # TODO
  # ninja test || echo "Tests failed"
}

package() {
  cd dtkwidget
  DESTDIR="$pkgdir" ninja install
}
