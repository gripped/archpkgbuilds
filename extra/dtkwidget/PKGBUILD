# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.7.30
pkgrel=1
pkgdesc='Deepin base graphical widgets library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL-3.0-or-later')
# qt5-imageformats is needed for webp (dci) icons
depends=('dtkcore' 'dtkgui' 'gcc-libs' 'glibc' 'gsettings-qt' 'libx11' 'libxcb' 'libxext' 'libxi'
         'qt5-base' 'qt5-imageformats' 'qt5-svg' 'qt5-x11extras' 'startup-notification' 'xcb-util')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
replaces=('deepin-tool-kit')
source=("git+https://github.com/linuxdeepin/dtkwidget.git#tag=$pkgver")
sha512sums=('958975da7459f33f3443d121f3472f3554915817708206c0e301ffac6a76b68391ab20062a8e214659c13dfa64eac707d24b8d4a293ac4b7d08eb26563d81a33')

prepare() {
  cd dtkwidget
  # https://github.com/linuxdeepin/dtkwidget/pull/707
  git cherry-pick -n e37d9c1dd6efe6acac68da042f7214e74347d4f2
}

build() {
  cd dtkwidget
  # Disable plugins because they are not going to be installed
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DBUILD_DOCS=ON \
      -DBUILD_PLUGINS=OFF \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=None
  ninja
}

check() {
  cd dtkwidget
  # TODO
  # ninja test || echo "Tests failed"
}

package() {
  cd dtkwidget
  DESTDIR="$pkgdir" ninja install
}
