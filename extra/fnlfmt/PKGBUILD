# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=fnlfmt
pkgver=0.3.2
pkgrel=1
pkgdesc='A formatter for Fennel code'
arch=('any')
url='https://git.sr.ht/~technomancy/fnlfmt'
license=('MIT')
depends=('lua')
makedepends=('git')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('e879145ae08d076e284dbd1ab9ec650c9aa28119784b22fff6c1e21560136a1d5b88307a87c9c29aa25130015e2065f4911f9409efa63233d508623c3f9e82d7')
b2sums=('a57db0dfb6506ba976128291f312961b743d67a53818002897ceed7ff30c0a5a2fc0997b6b49a785910f4cb04722d64d506834a403ac542e36f3d6a576f2151e')

build() {
  cd "$pkgname"

  make
}

check() {
  cd "$pkgname"

  make test
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" fnlfmt

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
