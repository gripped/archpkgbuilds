# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: txtsd <code@ihavea.quest>
# Contributor: Sandy Carter <bwrsandman@gmail.com>

pkgname=openmw
pkgver=0.48.0
pkgrel=12
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64')
url="http://www.openmw.org"
license=('GPL-3.0-or-later' 'OFL-1.1' 'LicenseRef-DejaVuLGCSansMono')
depends=('openal' 'openscenegraph' 'mygui' 'qt5-base' 'ffmpeg4.4' 'sdl2' 'unshield'
         'libxt' 'boost-libs' 'bullet-dp' 'yaml-cpp' 'luajit')
makedepends=('cmake' 'boost' 'doxygen' 'ninja')
source=("https://gitlab.com/OpenMW/openmw/-/archive/openmw-${pkgver}/openmw-openmw-${pkgver}.tar.gz"
         gcc14.patch)
sha512sums=('6f2429253b44a232fc69db7f04a873f7328a5e95d810c36768311bdc23064a96c9963caa7d64a906bd67509e3a2f8d142db005ce093fc2764dd672ae6a617414'
            '552ec48c2b1821229fc81bcaf7f01174b24ffd6ae80d1e59c927a422d8c17eeb435f1501a05c1f105ab87342ad91d89fab0f7e8eb06d5552bb3d8d8675577a11')

prepare() {
  cd openmw-openmw-$pkgver
  patch -p1 -i ../gcc14.patch # Fix build with GCC 14
}

build() {
  cd openmw-openmw-$pkgver

  export CMAKE_POLICY_VERSION_MINIMUM=3.5
  PKG_CONFIG_LIBDIR='/usr/lib/ffmpeg4.4/pkgconfig' \
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DOPENMW_LTO_BUILD=ON \
    -DDESIRED_QT_VERSION=5 \
    -GNinja \
    -Bbuild
  ninja -C build
}

package() {
  cd openmw-openmw-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
