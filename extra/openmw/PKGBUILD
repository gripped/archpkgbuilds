# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: txtsd <code@ihavea.quest>
# Contributor: Sandy Carter <bwrsandman@gmail.com>

pkgname=openmw
pkgver=0.50.0
pkgrel=2
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64')
url="http://www.openmw.org"
license=('GPL-3.0-or-later' 'OFL-1.1' 'LicenseRef-DejaVuLGCSansMono')
depends=('openal' 'openscenegraph' 'mygui' 'qt6-base' 'qt6-tools' 'qt6-svg' 'ffmpeg' 'sdl2' 'unshield'
         'libxt' 'boost-libs' 'bullet-dp' 'yaml-cpp' 'luajit' 'glibc' 'gcc-libs' 'zlib')
makedepends=('cmake' 'boost' 'doxygen' 'ninja')
source=("https://gitlab.com/OpenMW/openmw/-/archive/openmw-${pkgver}/openmw-openmw-${pkgver}.tar.gz")
sha512sums=('9221a3825da265b2145b7cf4a5f197b52ac890e27e0c303358b92c7030e92787d039afb4f66bde39304937bd9f9f00e4f66b10c07abf108eabbd818aab3a513b')

build() {
  cd openmw-openmw-$pkgver

  export CMAKE_POLICY_VERSION_MINIMUM=3.5

# Fix for #4
  CXXFLAGS="${CXXFLAGS/-fno-omit-frame-pointer/}"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DOPENMW_LTO_BUILD=ON \
    -GNinja \
    -Bbuild
  ninja -C build
}

package() {
  cd openmw-openmw-$pkgver
  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
