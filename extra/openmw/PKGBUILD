# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Sandy Carter <bwrsandman@gmail.com>

pkgname=openmw
pkgver=0.48.0
pkgrel=7
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield'
         'libxt' 'boost-libs' 'bullet-dp' 'yaml-cpp' 'luajit')
makedepends=('cmake' 'boost' 'doxygen' 'ninja')
source=("https://github.com/OpenMW/openmw/archive/refs/tags/openmw-${pkgver}.tar.gz"
         gcc14.patch
         ffmpeg-7.patch)
sha512sums=('6f2429253b44a232fc69db7f04a873f7328a5e95d810c36768311bdc23064a96c9963caa7d64a906bd67509e3a2f8d142db005ce093fc2764dd672ae6a617414'
            '4039328c3c587db9fba203156eb0dbacfb6b6bc8015ee1f483c79757b52f85da88adc6640947cf5965e91cb7babb2fabcbcf42ce51e1b0a41a8b0ebac31882e1'
            'cfc9834376b6d15a4fc5d5c26badadead6f355eeabbc5abff56fa7eb2a29de6da0f6367557c71369684c5378309fafe579e2c752649abede7ecbcb3fd57cf720')

prepare() {
  cd openmw-openmw-$pkgver
  patch -p1 -i ../gcc14.patch # Fix build with GCC 14
  patch -p1 -i ../ffmpeg-7.patch # Fix build with ffmpeg 7
}

build() {
  cd openmw-openmw-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DOPENMW_LTO_BUILD=ON \
    -DDESIRED_QT_VERSION=5 \
    -GNinja \
    -Bbuild
  ninja -C build
}

package() {
  cd openmw-openmw-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
