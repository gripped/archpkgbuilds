# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=aisleriot
pkgver=3.22.35
pkgrel=1
pkgdesc="A collection of patience games written in guile scheme"
url="https://wiki.gnome.org/Apps/Aisleriot"
arch=(x86_64)
license=(
  GFDL-1.1-or-later
  GFDL-1.3-only
  GPL-3.0-or-later
  LGPL-3.0-or-later
)
depends=(
  at-spi2-core
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  guile
  hicolor-icon-theme
  libcanberra
  librsvg
)
makedepends=(
  appstream-glib
  git
  glib2-devel
  meson
  yelp-tools
)
optdepends=(
  'pysolfc-cardsets: PySol card sets'
  'pysolfc: PySol card sets'
)
options=(
  # https://gitlab.gnome.org/GNOME/aisleriot/-/commit/ca27295085473111564b6674059351a0b6f170c1
  !lto
)
source=("git+https://gitlab.gnome.org/GNOME/aisleriot.git#tag=$pkgver")
b2sums=('43504f0ff62aefe8e0cbb8eaa9bdbc53fadbb4af33b44950defbcc1dd880674ba58295b19da638a34c328000f9915897d77876a154417c46c2229e5223ef7e6b')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D theme_kde=false
    -D theme_pysol=true
    -D theme_pysol_path=/usr/share/PySolFC
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
