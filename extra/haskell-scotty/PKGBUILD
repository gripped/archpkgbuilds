# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=scotty
pkgname=haskell-scotty
pkgver=0.22
pkgrel=149
pkgdesc="Haskell web framework inspired by Ruby's Sinatra, using WAI and Warp"
url="https://github.com/scotty-web/scotty"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-blaze-builder' 'haskell-case-insensitive'
         'haskell-cookie' 'haskell-data-default-class' 'haskell-http-types' 'haskell-monad-control'
         'haskell-network' 'haskell-regex-compat' 'haskell-resourcet' 'haskell-transformers-base'
         'haskell-unliftio' 'haskell-wai' 'haskell-wai-extra' 'haskell-warp')
makedepends=('ghc' 'haskell-async' 'haskell-doctest' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-hspec-wai' 'haskell-http-client' 'haskell-lifted-base')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('ceb998c12502cd639c8aa59c5b5ea8f2651198e62f2f86cf18b8b5a087b4b81c')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
