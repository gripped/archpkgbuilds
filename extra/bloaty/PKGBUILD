# Maintainer: Bert Peters <bert@bertptrs.nl>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=bloaty
pkgver=1.1
pkgrel=4
pkgdesc='Size profiler for binaries'
arch=(x86_64)
url='https://github.com/google/bloaty'
license=('Apache-2.0')
depends=(abseil-cpp capstone protobuf re2)
makedepends=(cmake git ninja)
source=("git+$url#commit=379d5305670c00c36a57e608079fd253f13bde63") # v1.1 + patches
b2sums=('211837ca607b8599275929f61f41005dbab6801cc66db529394ffe80392ffe6b5ab221e49e0d95d34e4d947f795ab41320d30cb3ac4e529b8254756518094399')

build() {
  cmake \
    -B build \
    -D BLOATY_ENABLE_CMAKETARGETS=OFF \
    -D BUILD_TESTING=OFF \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_EXE_LINKER_FLAGS_RELEASE="$LDFLAGS" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -G Ninja \
    -S $pkgname \
    -W no-dev
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
