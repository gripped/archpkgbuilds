# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kernelshark
pkgver=2.3.2
pkgrel=1
pkgdesc="GUI frontend for trace-cmd based Linux kernel Ftrace captures"
arch=(x86_64)
url="https://kernelshark.org/"
license=(
  GPL-2.0-only
  LGPL-2.0-only
)
depends=(
  freeglut
  gcc-libs
  glibc
  glu
  qt6-base
  qt6-scxml
  ttf-fira-sans
)
makedepends=(
  boost
  cmake
  json-c
  libglvnd
  libtracefs
  libtraceevent
  trace-cmd
)
optdepends=(
  'bash: for kshark-su-record'
)
provides=(
  libkshark.so
  libkshark-plot.so
  libkshark-gui.so
)
source=($pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/utils/trace-cmd/kernel-shark.git/snapshot/kernel-shark-$pkgname-v$pkgver.tar.gz)
sha512sums=('52765091bdc0a0ab4a1eb60d8b054682c538a864dd49f06d8f38c67f3e1bf62de106ef121549b1c76a08b023fe43aee83e0b7d2afaa6693a5a59bfc3885940df')
b2sums=('7a8814c1c47bae7f869561f7296cd7f829bffdb388508dd6a58727672bb9b6538b75c3bfee34abb01b83f416278e589d819a4756bbbddc2623e65a7a85c181d1')

prepare() {
  cd kernel-shark-$pkgname-v$pkgver

  # replace FreeSans with ttf-fira-sans
  sed -e 's/FreeSans/FiraSans-Regular/g' \
      -i CMakeLists.txt
}

build() {
  local cmake_options=(
    -B build
    -D _INSTALL_PREFIX=/usr
    -D _LIBDIR=/usr/lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -S kernel-shark-$pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    json-c libjson-c.so
    libglvnd libGL.so
    libtraceevent libtraceevent.so
    libtracefs libtracefs.so
    trace-cmd libtracecmd.so
  )

  DESTDIR="$pkgdir" cmake --install build
}
