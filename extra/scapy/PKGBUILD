# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ebubekir KARUL <ebubekirkarul@yandex.com>
# Contributor: s1gma,shirokuro

pkgbase=scapy
pkgname=(scapy python-scapy)
pkgver=2.7.0
pkgrel=1
pkgdesc='Powerful interactive packet manipulation program written in Python'
url='http://www.secdev.org/projects/scapy/'
arch=(any)
license=(GPL-2.0-only)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
  tcpdump
)
checkdepends=(
  graphviz
  python-cryptography
  python-matplotlib
  python-pyx
  sox
)
source=(https://github.com/secdev/scapy/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2274261bd3bceb18d815197349031d99fe236649644007a8cb7987280af10d7b')
sha512sums=('fafdf9a1df8668603d72f902833ef1954e780b3da74a5447d20bf125a763129cfb78350ee1e7b9c284214d9e97a02d8fb0f1be1f6c05ed890eee50016ef3d2f1')

build() {
  cd ${pkgbase}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  # Run only main tests, without network access
  cd ${pkgbase}-${pkgver}/test
  ./run_tests -K netaccess -F
}

package_scapy() {
  depends=(
    python
    python-scapy
  )
  pkgdesc+=' (tools)'
  cd ${pkgbase}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d "${pkgdir}/usr/share/doc"
  ln -sf /usr/share/doc/python-scapy "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/lib"
}

package_python-scapy() {
  pkgdesc+=' (library)'
  depends=(
    python
    tcpdump
  )
  # TODO: clenup optdeps
  optdepends=(
    'python-cryptography: WEP, IPsec and SSL/TLS support'
    'python-pyx: psdump() and pdfdump() functions'
    'python-matplotlib: plotting support'
    'graphviz: conversations() method support'
    'sox: for VOIP support'
    'python-prompt_toolkit: expore function'
  )
  cd ${pkgbase}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 doc/scapy/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}"/usr/{bin,share/man}
}

# vim: ts=2 sw=2 et:
