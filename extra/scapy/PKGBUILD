# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ebubekir KARUL <ebubekirkarul@yandex.com>
# Contributor: s1gma,shirokuro

pkgbase=scapy
pkgname=('scapy' 'python-scapy')
pkgver=2.6.0
pkgrel=1
pkgdesc='Powerful interactive packet manipulation program written in Python'
url='http://www.secdev.org/projects/scapy/'
arch=('any')
license=('GPL2')
makedepends=(
  tcpdump
  python-build
  python-installer
  python-setuptools
  python-wheel
  python-sphinx
)
checkdepends=('python-cryptography' 'python-pyx' 'python-matplotlib'
              'graphviz' 'sox')
source=(https://github.com/secdev/scapy/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('cfd1babc5c0008bc021eede72149922c24dfc4a511ced7cc3a8665193b6be5c5')
sha512sums=('a5543b44411fcc22eb909e806e729605616ce98ab15ff208ee90ad606265c6604ef295bc0b9b130f22711ffed0ad9fd4c52783c8df7ef79849f7db38b97d9e1d')

build() {
  cd ${pkgbase}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  # Run only main tests, without network access
  cd ${pkgbase}-${pkgver}/test
  ./run_tests -K netaccess -F
}

package_scapy() {
  depends=('python' 'python-scapy')
  pkgdesc+=' (tools)'
  cd ${pkgbase}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d "${pkgdir}/usr/share/doc"
  ln -sf /usr/share/doc/python-scapy "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/lib"
}

package_python-scapy() {
  pkgdesc+=' (library)'
  depends=('tcpdump' 'python')
  # TODO: clenup optdeps
  optdepends=('python-cryptography: WEP, IPsec and SSL/TLS support'
              'python-pyx: psdump() and pdfdump() functions'
              'python-matplotlib: plotting support'
              'graphviz: conversations() method support'
              'sox: for VOIP support'
              'python-prompt_toolkit: expore function')
  cd ${pkgbase}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 doc/scapy/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}"/usr/bin
}

# vim: ts=2 sw=2 et:
