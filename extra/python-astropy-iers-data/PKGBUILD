# Maintainer:

_pyname=astropy-iers-data
pkgname=python-$_pyname
pkgver=0.2025.9.8.0.36.17
pkgrel=1
pkgdesc="IERS Earth Rotation and Leap Second tables for the astropy core package"
arch=(any)
url="https://github.com/astropy/astropy-iers-data"
license=(BSD-3-Clause)
depends=(python)
makedepends=(python-build python-installer python-hatchling python-hatch-vcs)
source=(https://github.com/astropy/$_pyname/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('208fcbad80a566eccf950393814150e0343376c30ec32da867f9f98524aceabf')

build() {
  cd $_pyname-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname
}
