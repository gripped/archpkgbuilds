# Maintainer:

_pyname=astropy-iers-data
pkgname=python-$_pyname
pkgver=0.2024.12.23.0.33.24
pkgrel=1
pkgdesc="IERS Earth Rotation and Leap Second tables for the astropy core package"
arch=(any)
url="https://github.com/astropy/astropy-iers-data"
license=(BSD-3-Clause)
depends=(python)
makedepends=(python-build python-installer python-wheel python-setuptools-scm)
source=(https://github.com/astropy/$_pyname/archive/v$pkgver/$_pyname-$pkgver.tar.gz
    "python-astropy_0_2024_03_18_00_29_47-issue-1.patch::https://github.com/astropy/astropy-iers-data/pull/24.patch"
)
sha256sums=('a7397530d1068396f14a72b3a7ad6fadc366cc775d2c8f86b06e420f356b0738'
            '79ecf580efcf66bf16816ff47c0b044946c20aae04356677acf7fc3e96b98d82')

prepare() {
  cd $_pyname-$pkgver
  # https://gitlab.archlinux.org/archlinux/packaging/packages/python-astropy-iers-data/-/issues/1
  patch -Np1 < "${srcdir}"/python-astropy_0_2024_03_18_00_29_47-issue-1.patch
}

build() {
  cd $_pyname-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname
}
