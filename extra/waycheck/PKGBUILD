# Maintainer: Campbell Jones <serebit at archlinux dot org>

pkgname=waycheck
pkgver=1.3.0
pkgrel=1
pkgdesc="Simple GUI that displays the protocols implemented by a Wayland compositor"
arch=("x86_64")
url="https://gitlab.freedesktop.org/serebit/$pkgname"
license=("Apache-2.0")
depends=("hicolor-icon-theme" "qt6-base>=6.5" "qt6-wayland>=6.5" "wayland")
makedepends=("meson")
checkdepends=("appstream")
source=("$url/-/archive/v$pkgver/${pkgname}-v${pkgver}.tar.gz"
        "https://gitlab.freedesktop.org/-/project/21343/uploads/6fdb225ee361d939ad94b20ce68b29f3/${pkgname}-v${pkgver}.tar.gz.asc")
b2sums=('738be8b72ddf45aa26dad1c911316038c70b207df85a4e6368bdd7bdc5c8981864f4be698eeac26d33dc85ea48aa012f26e34f65aed5b29088118eeab1f62000'
        'SKIP')
validpgpkeys=("E87E5B39F04A5D889D8C0147F6D84143496F6680")

build() {
	arch-meson "$pkgname-v$pkgver" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
