# Maintainer: Campbell Jones <serebit at archlinux dot org>

pkgname=waycheck
pkgver=1.4.0
pkgrel=1
pkgdesc="Simple GUI that displays the protocols implemented by a Wayland compositor"
arch=("x86_64")
url="https://gitlab.freedesktop.org/serebit/$pkgname"
license=("Apache-2.0")
depends=("hicolor-icon-theme" "qt6-base>=6.5" "qt6-wayland>=6.5" "wayland")
makedepends=("meson")
checkdepends=("appstream")
source=("$url/-/archive/v$pkgver/${pkgname}-v${pkgver}.tar.gz"
        "https://gitlab.freedesktop.org/-/project/21343/uploads/d8c36ec2420644310c011ff93f4c2ee8/${pkgname}-v${pkgver}.tar.gz.asc")
b2sums=('823966776366e61726bc0b84a3d36b9179e7a2ab0c2e1391e8ff846a1190be4f8627b3fb0c18fe6ba1143746d6ef34e38dbf33c5d719d0e6f7defbc67a1ae017'
        'SKIP')
validpgpkeys=("E87E5B39F04A5D889D8C0147F6D84143496F6680")

build() {
	arch-meson "$pkgname-v$pkgver" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
