# Maintainer: Campbell Jones <serebit at archlinux dot org>

pkgname=waycheck
pkgver=1.6.0
pkgrel=1
pkgdesc="Simple GUI that displays the protocols implemented by a Wayland compositor"
arch=("x86_64")
url="https://gitlab.freedesktop.org/serebit/$pkgname"
license=("Apache-2.0")
depends=("hicolor-icon-theme" "qt6-base>=6.5" "qt6-wayland>=6.5" "wayland")
makedepends=("meson")
checkdepends=("appstream")
source=("$url/-/archive/v$pkgver/${pkgname}-v${pkgver}.tar.gz"
        "https://gitlab.freedesktop.org/-/project/21343/uploads/64bcc66cea83ae43f1a6f130b3638c52/${pkgname}-v${pkgver}.tar.gz.asc")
b2sums=('334759daa423d89914f4001fbe42a85ee95a7964b3a3b07bf8a1e9bb0a33aae7c1da528fe1dbc1fbe34dd97d2b69a81fd729318d3f1b21fcc96f884fce8af128'
        'SKIP')
validpgpkeys=("E87E5B39F04A5D889D8C0147F6D84143496F6680")

build() {
	arch-meson "$pkgname-v$pkgver" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
