# Maintainer: Campbell Jones <serebit at archlinux dot org>

pkgname=waycheck
pkgver=1.3.1
pkgrel=1
pkgdesc="Simple GUI that displays the protocols implemented by a Wayland compositor"
arch=("x86_64")
url="https://gitlab.freedesktop.org/serebit/$pkgname"
license=("Apache-2.0")
depends=("hicolor-icon-theme" "qt6-base>=6.5" "qt6-wayland>=6.5" "wayland")
makedepends=("meson")
checkdepends=("appstream")
source=("$url/-/archive/v$pkgver/${pkgname}-v${pkgver}.tar.gz"
        "https://gitlab.freedesktop.org/-/project/21343/uploads/f91604b2841491797d66bd6e5eae9978/${pkgname}-v${pkgver}.tar.gz.asc")
b2sums=('bd0780a2c8554ad2ce76bf563605efb59f1198280e1dce97b479a6dd38ff850ddafb9e3072d9acc76285484e5d574fa0a450fbf616016fcc7da8c818ab766714'
        'SKIP')
validpgpkeys=("E87E5B39F04A5D889D8C0147F6D84143496F6680")

build() {
	arch-meson "$pkgname-v$pkgver" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
