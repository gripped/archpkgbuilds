# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Moritz Lipp <mlq@pwmt.org>
# Contributor: fordprefect <fordprefect@dukun.de>
# Contributor: Thor77 <thor77@thor77.org>

pkgname=radicale
_name=Radicale
pkgver=3.3.3
pkgrel=1
pkgdesc="Simple calendar (CalDAV) and contact (CardDAV) server"
arch=(any)
url="https://radicale.org/"
_url="https://github.com/Kozea/radicale"
license=(GPL-3.0-or-later)
depends=(
  python
  python-bcrypt
  python-dateutil
  python-defusedxml
  python-passlib
  python-pika
  python-vobject
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-waitress
)
backup=(
  'etc/radicale/config'
  'etc/radicale/rights'
  'etc/radicale/users'
)
install=$pkgname.install
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/v$pkgver.tar.gz
  $pkgname-sysusers.conf
  $pkgname-tmpfiles.conf
  $pkgname.service
)
sha512sums=('4d2ac8ea501a4b323901c6891603b87c0603470c59f2a3d72e4915896b5461c96d889fba6a4c7e0703988c072a92b2f01ddd199188d98c990bbee1fa5c87d206'
            '56dffb66e018cfbf158dc5d8fe638b3cb31229945f659aae5623f219bcd1d68ddc375f1633fa8e857a9b2f50c9e05a06efce165370137d6e116a4f187466637f'
            '9d0dd88e4a34e9f97abda1785698e4b2a5e8202063deeb91b84e13c05e00b07e45b8d4d9eca09b9241b1138bbbfdc999dba0135c18f5bc0c08d65b0cd83b367b'
            '9ed8aa69f8a09206ab44b731da9c8638b86926f76547316a5806499f9d462f0ea6d93f3ef7302c8024c2ff2377b19a54530b6363911d59c02645880d608a4427')
b2sums=('cd5f48d73ab764c516554df5282093e1a5ad5d1f29374ee173e66c836875bc7e80c971111df476c8f4216a563cdd7e787f1b10aaab0ea399daf4ddddb6c1b7fd'
        'b3af60e144ef857e42ec672e806e9600265ab7d2ea4a75011de9ab56918a008437afdacb301df210b54424fb7ff1e9a332831c67b2e58fd6bc0a0aa1eebe8909'
        '41916d62f5e3f1060bd21db0722abe837754a4cb915af218c904dafac4b06794f8fde2e34486fb7392777b4738502f3df4c1390b835050045337585b064e23bb'
        'ff518d0602b4c05dc0a5946427759216d97b054ca4e8ca9ec87b2edac7c46e8b05edb734bc4cf52988be606ecbfb58c918cff248fe1c6201114d696b7eeecce4')

prepare() {
  cd $_name-$pkgver
  touch "users"
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -o addopts=""
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  # config
  install -vDm 644 {config,rights,users} -t "$pkgdir/etc/$pkgname/"
  # wsgi
  install -vDm 644 $pkgname.wsgi -t "$pkgdir/usr/share/$pkgname/"
  # systemd service
  install -vDm 644 ../$pkgname.service -t "$pkgdir/usr/lib/systemd/system/"
  # sysusers.d
  install -vDm 644 ../$pkgname-sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  # tmpfiles.d
  install -vDm 644 ../$pkgname-tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  # docs
  install -vDm 644 {CHANGELOG,DOCUMENTATION,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
