# Maintainer: Torsten Ke√üler <t dot kessler at posteo dot de>

pkgname=hipfort
pkgver=6.4.3
pkgrel=1
pkgdesc='Fortran interfaces for ROCm libraries'
arch=('x86_64')
url='https://rocm.docs.amd.com/projects/hipfort/en/latest/'
license=('MIT')
depends=(
    'rocm-core'        
    'hip-runtime-amd'
    'gcc-fortran'
)
makedepends=(
    'cmake'
    'rocm-cmake'
    'rocblas'
    'rocsparse'
    'rocfft'
    'rocrand'
    'rocsolver'
    'hipblas'
    'hipsparse'
    'hipfft'
    'hiprand'
    'hipsolver'
)
optdepends=(
    'rocfft: Fortran bindings'
    'rocrand: Fortran bindings'
    'rocsolver: Fortran bindings'
    'hipblas: Fortran bindings'
    'hipsparse: Fortran bindings'
    'hipfft: Fortran bindings'
    'hiprand: Fortran bindings'
    'hipsolver: Fortran bindings'
)
_git='https://github.com/ROCmSoftwarePlatform/hipfort'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz")
sha256sums=('6cfd4f704ee4f156d15afa30b8e029a4af336a6cb60fe4f6ca6fb85c86a266bd')
options=(!strip)
_dirname="$(basename "$_git")-$(basename "${source[0]}" .tar.gz)"

build() {
    local cmake_args=(
        -Wno-dev
        -B build
        -S "$_dirname"
        -D CMAKE_POLICY_VERSION_MINIMUM=3.5
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX=/opt/rocm
    )
    cmake "${cmake_args[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    install -Dm644 "$_dirname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
