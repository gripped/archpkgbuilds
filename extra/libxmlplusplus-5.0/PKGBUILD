# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libxml++-5.0
pkgname=(
  libxml++-5.0
  libxml++-5.0-docs
)
pkgver=5.2.1
pkgrel=1
pkgdesc="C++ bindings to libxml2"
url="https://libxmlplusplus.github.io/libxmlplusplus/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
  libxml2
)
makedepends=(
  git
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://github.com/libxmlplusplus/libxmlplusplus#tag=$pkgver")
b2sums=('4fbb3d96a68e822d5da2eea559cb76a94e14af21fc97167c523ceb54e25291836f8a3e8658abf3d93b00b41cbc0165eaeb8021fee39e80d2b2e127080c3f257d')

prepare() {
  cd libxmlplusplus
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson libxmlplusplus build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libxml++-5.0() {
  provides=(libxml++-5.0.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_libxml++-5.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
