# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Matt Cuneo <m@cuneo.au>

pkgname=matrix-authentication-service
pkgver=1.6.0
pkgrel=1
pkgdesc="Authentication service for Matrix"
url="https://github.com/element-hq/matrix-authentication-service"
arch=(x86_64)
license=(AGPL-3.0-only)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  nodejs-lts-jod
  npm
  open-policy-agent
  rust
)
options=(!lto)
source=(
  "git+$url#tag=v${pkgver/[a-z]/-&}"
  0001-Arch-Linux-paths.patch
  matrix-authentication-service.service
  matrix-authentication-service-worker.service
  sysusers.conf
  tmpfiles.conf
)
b2sums=('ad435a8cb9543ba064e8f132b59b5f629072ae8f3cea870256ed2fc9fce12d766f8fd6ddaf4b7d8a9f2edaaf42a151441ca22f2ebcf9068f5116257589641988'
        '212a6c6a455b95e2013458cc5b9ae38a9ba7a76c094305061f280864356d32491593910253fb767423e1bed2624002ce1b81cfacd92f35e5c850fe31bea3987a'
        '9f3e6f91897d2aee1409280d440ce4068244b007845e18f338eb43e634b970b8bf99eed2f4f877ec78f706925583136c8ba80e697983b52bd682753839916870'
        'd3fac357d8794feefba7a45f6f8815aa0b2dce17d4200ee0906e2cb3444009fe13f3921e1dbbca6cbd2892f2d45edd75c817a216ec0dfd1eb4334df0d0ec2f74'
        '5996147ed184e2f29e06148e36584462f81f12d5246c91f64eefbe0b82bf03b2509cd9d0c818b733f7d4fd820823095b11a04ff875bc9b996b1909e7925dff52'
        'ec4959bf54b248b3b8e5f4399d0b8b3c437f39e38131ef16eec96532b8ba3b566f182175b6e5eeaede50b3b811f9a82fbeba4d0cdf964a7da0466d1796442c1e')

prepare() {
  cd $pkgname

  # Move 'docker' feature paths from /usr/local to /usr
  git apply -3 ../0001-Arch-Linux-paths.patch

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  cd frontend
  npm ci
}

build() {
  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  cd $pkgname
  cargo build --release --frozen --features docker

  cd frontend
  npm run build

  cd ../policies
  make
}

package() {
  cd $pkgname

  install -D target/release/mas-cli -t "$pkgdir/usr/bin"

  local sharedir="$pkgdir/usr/share/mas-cli"
  install -Dm644 policies/policy.wasm frontend/dist/manifest.json -t "$sharedir"
  cp -a templates translations -t "$sharedir"
  cp -a frontend/dist -T "$sharedir/assets"
  rm "$sharedir"/assets/manifest.json*

  mkdir -p "$pkgdir/usr/share/doc"
  cp -a docs -T "$pkgdir/usr/share/doc/$pkgname"

  cd ..
  mkdir -p "$pkgdir/etc/matrix-authentication-service"
  install -Dm644 *.service -t "$pkgdir/usr/lib/systemd/system"
  install -Dm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set sw=2 sts=-1 et:
