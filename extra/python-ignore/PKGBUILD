# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=ignore-python
pkgname=python-ignore
pkgver=0.3.2
pkgrel=1
pkgdesc="Python bindings for the Rust crate ignore"
arch=(x86_64)
url="https://github.com/borsattoz/ignore-python"
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  cargo
  maturin
  python-installer
)
checkdepends=(
  python-pytest
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('91a5cc60a8750ff27c41bcd1ee285acc69d3d39a2da7ed903e47a32cd5d57526cc9be0616f8681f6d2efe076e70798d765663c965a450a69ea41742b95101b4c')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target host-tuple
}

build() {
  cd $_name-$pkgver
  maturin build --locked --release --target $(rustc --print host-tuple) --strip
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
  )
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer $_name-$pkgver/target/wheels/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" $_name-$pkgver/tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir "$pkgdir" target/wheels/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
