# Maintainer:
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro4
pkgver=4.4.3.1
pkgrel=5
pkgdesc='Portable library aimed at video game and multimedia programming (legacy version)'
arch=(x86_64)
url='https://liballeg.org/'
license=(custom)
depends=(jack libxcursor libxpm libxxf86vm libvorbis)
makedepends=(cmake glu libpng mesa ninja)
options=(staticlibs)
source=("https://github.com/liballeg/allegro5/releases/download/$pkgver/allegro-$pkgver.tar.gz")
sha256sums=('ec19dbc9a021244582b4819b3583ee594b50141f9fcf6944a4ed8069cbf8d4d4')

build() {
  export CMAKE_POLICY_VERSION_MINIMUM=3.5

  mkdir -p build
  cd build
  cmake "$srcdir/allegro-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS=OFF \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "allegro-$pkgver/misc/allegro.m4" \
    "$pkgdir/usr/share/aclocal/allegro.m4"
  install -Dm644 "allegro-$pkgver"/docs/txt/license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
