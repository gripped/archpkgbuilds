# Maintainer: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Andrew Steinke <rkcf@rkcf.me>
# Contributor: Joshua Leahy <jleahy@gmail.com>

pkgname=python-freetype-py
_name=${pkgname#python-}
pkgver=2.5.1
pkgrel=2
pkgdesc='FreeType Python bindings'
arch=(any)
url="https://github.com/rougier/$_name"
license=(BSD-3-Clause)
depends=(python freetype2)
makedepends=(python-build python-installer python-setuptools-scm python-wheel)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
b2sums=('c9017b4e6c258dda25f8e51a399df08a9a6ed5d96a4c7790b86bd8193ba7cc6dec085754ccb13b8d415dda38e707e238f0e1b666de14da2891a90749369c18c3')

build() {
  cd $_name-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
