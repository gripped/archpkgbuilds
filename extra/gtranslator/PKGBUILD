# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=45.2
pkgrel=2
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=('x86_64')
url='https://projects.gnome.org/gtranslator/'
license=('GPL3')
depends=(
  'gettext'
  'gtksourceview5'
  'hicolor-icon-theme'
  'iso-codes'
  'libadwaita'
  'libgda6'
  'libspelling'
)
makedepends=(
  'gobject-introspection'
  'meson'
  'yelp-tools'
)
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('1c946110e6f19013c162a422ca17f1de944c5ab8c29e30389a0df9f33314c8aa')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
