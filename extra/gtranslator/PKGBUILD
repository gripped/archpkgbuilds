# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=49.0
pkgrel=1
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=(x86_64)
url='https://wiki.gnome.org/Apps/Gtranslator'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gettext
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  iso-codes
  json-glib
  libadwaita
  libgda6
  libsoup3
  libspelling
  libxml2
  pango
  sqlite
)
makedepends=(
  appstream
  git
  glib2-devel
  gobject-introspection
  meson
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=${pkgver/[a-z]/.&}")
b2sums=('6c3e9fcef903f6d68c4091ff361a09650d96f7f12598234ddfa001972d1d371a9be30b458904def21c62d3e1812cb028494efe58841c65463269a082a13ac6f2')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
