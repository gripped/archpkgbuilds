# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=47.1
pkgrel=1
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Gtranslator'
license=('GPL-3.0-or-later')
depends=(
  'cairo'
  'dconf'
  'gcc-libs'
  'gettext'
  'glib2'
  'glibc'
  'gtk4'
  'gtksourceview5'
  'hicolor-icon-theme'
  'iso-codes'
  'json-glib'
  'libadwaita'
  'libgda6'
  'libsoup3'
  'libspelling'
  'libxml2'
  'pango'
)
makedepends=(
  'git'
  'glib2-devel'
  'gobject-introspection'
  'meson'
  'yelp-tools'
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=${pkgver/[a-z]/.&}")
b2sums=('c99cf605857cc6b97d449014dc058a9eb102fa7bd14e396f8d06971fece0905a0676512e28b6f54a2885082ee52ec10e02f9230d50545b567c352f0d28b11cab')

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
