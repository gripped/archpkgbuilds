# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=46.0
pkgrel=1
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Gtranslator'
license=('GPL-3.0-or-later')
depends=(
  'gettext'
  'gtksourceview5'
  'hicolor-icon-theme'
  'iso-codes'
  'libadwaita'
  'libgda6'
  'libspelling'
)
makedepends=(
  'gobject-introspection'
  'meson'
  'yelp-tools'
)
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('0a30bf0fd82d88f9a206659e8b5f59eaa93fb80f67530bbf9461c5bf6a1c0beb')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
