# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=48.0
pkgrel=1
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Gtranslator'
license=('GPL-3.0-or-later')
depends=(
  'cairo'
  'dconf'
  'gcc-libs'
  'gettext'
  'glib2'
  'glibc'
  'gtk4'
  'gtksourceview5'
  'hicolor-icon-theme'
  'iso-codes'
  'json-glib'
  'libadwaita'
  'libgda6'
  'libsoup3'
  'libspelling'
  'libxml2'
  'pango'
)
makedepends=(
  'appstream'
  'git'
  'glib2-devel'
  'gobject-introspection'
  'meson'
  'yelp-tools'
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=${pkgver/[a-z]/.&}")
b2sums=('e9aa64fb21d7fd741cccef9b97315f532db140085192fc38c37d8fc2f774d0ea2b97478a8ee538a6965b0424b9980f3f7c3713eb76f4b6c4493447acd73f7c60')

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
