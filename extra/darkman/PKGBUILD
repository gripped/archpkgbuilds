# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=darkman
pkgver=2.2.0
pkgrel=1
pkgdesc='Framework for dark-mode and light-mode transitions on Linux desktop'
arch=(x86_64)
url=https://darkman.whynothugo.nl
license=(ISC)
makedepends=(
  git
  go
  scdoc
)
optdepends=(
  "geoclue: to automatically determine the system's location"
  'xdg-desktop-portal-impl: to expose the current mode via the XDG settings portal D-Bus API'
)
source=("git+https://gitlab.com/WhyNotHugo/darkman.git#tag=v$pkgver?signed")
b2sums=('d61e33f987e518287512800097a2e3321300262e039fff0315d582012f3c0f0542a993f57b4f8bb03629056c462f1159723e5dbeaef86da35f169e128750b2b9')
validpgpkeys=("1204CA9FC2FFADEEDC2961367880733B9D062837") # Hugo Osvaldo Barrera <hugo@whynothugo.nl>

prepare() {
  cd $pkgname
  go mod vendor
}

build() {
  cd $pkgname
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  make build
}

check() {
  cd $pkgname
  go test ./...
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install

  cd "$pkgdir"
  rm -r etc
}
