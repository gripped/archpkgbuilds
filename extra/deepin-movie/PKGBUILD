# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.10.36
pkgrel=1
pkgdesc='Movie player based on mpv'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL-3.0-or-later')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg' 'ffmpegthumbnailer' 'libxtst'
         'libglvnd' 'dconf' 'gsettings-qt' 'libva' 'libxcb' 'libx11' 'qt5-x11extras' 'mpv'
         'qt5-multimedia' 'qtdbusextended' 'qtmpris' 'deepin-daemon')
makedepends=('git' 'cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-movie-reborn.git#tag=$pkgver")
sha512sums=('3a18f333944084272f6e12d6af608cff6a701bb2bfca0fbed5ff818d330d4c7db0e90c895a745374b286d36bfb42c1bb1960a431fc2a46f8504db8559c2329bf')

prepare() {
  cd deepin-movie-reborn
  # new dtkgui
  git cherry-pick -n adbcf2f754f74947f32546f40d488001d34f3051
  # fix ldflag
  git cherry-pick -n b16acf2321be8411b4d0426a6856c74a0b2b260a
  # ffmpeg 7
  git cherry-pick -n 649fc5ebc0b80d8b7b530ae640a2e42280d1cdd6
  sed -i '/include <QDBusReply>/a#include <QTimer>' src/widgets/toolbutton.h
  sed -i 's|QGSettings/QGSettings|qt5/QGSettings/QGSettings|' src/libdmr/compositing_manager.h
}

build() {
  cd deepin-movie-reborn
  export CXXFLAGS+=' -fpermissive'
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_POLICY_VERSION_MINIMUM=3.5 -DVERSION=$pkgver .
  ninja
}

package() {
  cd deepin-movie-reborn
  DESTDIR="$pkgdir" ninja install
}
