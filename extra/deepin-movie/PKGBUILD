# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.10.32
pkgrel=1
pkgdesc='Movie player based on mpv'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg'
         'ffmpegthumbnailer' 'libxtst' 'libglvnd' 'dconf' 'gsettings-qt' 'libva' 'libxcb' 'libx11'
         'qt5-x11extras' 'mpv' 'qt5-multimedia' 'qtdbusextended' 'qtmpris')
makedepends=('cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
options=('!lto')
source=("https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('45378a6abb639f834d1e8c3d391cdefd92bf4d71bfd8fbe95a1250c9f601af44d4504efda6892a6606915219f644d12711695123b6d53a8d293db4a69d9fb90f')

build() {
  cd deepin-movie-reborn-$pkgver
  export CXXFLAGS+=' -fpermissive'
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release -DVERSION=$pkgver .
  ninja
}

package() {
  cd deepin-movie-reborn-$pkgver
  DESTDIR="$pkgdir" ninja install
}
