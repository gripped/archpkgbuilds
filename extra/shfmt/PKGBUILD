# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.12.0
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=(BSD-3-Clause)
arch=(x86_64)
depends=(glibc)
makedepends=(
  go
  scdoc
)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('e3a8bee5b77c5b23eafa883e7498712d1dfa272692d8c413f656e3871150dc435e7660a1657a04f2994421ade6e77c45f450463af455d2c334ac5c566c08c9c5')

prepare() {
  cd "sh-$pkgver"

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "sh-$pkgver"

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags="-compressdwarf=false -linkmode=external -X main.version=$pkgver"
  go build -v \
    -ldflags "$ld_flags" \
    ./cmd/shfmt/...

  scdoc < cmd/shfmt/shfmt.1.scd > shfmt.1
}

check() {
  cd "sh-$pkgver"

  # Ensure the executable reports the correct version.
  test "$(./shfmt --version)" = "$pkgver"

  go test ./cmd/shfmt/...
}

package() {
  cd "sh-$pkgver"

  install -Dm755 -t "$pkgdir/usr/bin" shfmt
  install -Dm644 -t "$pkgdir/usr/share/man/man1" shfmt.1
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
