# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=67.2
pkgrel=1
pkgdesc='Application menu extension for GNOME Shell'
arch=(any)
url='https://gitlab.com/arcmenu/ArcMenu'
license=(GPL-2.0-or-later)
depends=(
  gnome-menus
  gnome-shell
)
makedepends=(git)
source=("git+https://gitlab.com/arcmenu/$_pkgname.git#tag=v$pkgver")
b2sums=(763e6bd83548783bdfe4fc759e377064b18f997c520ae8bfa6cb6adccf073c8dfaa885ee5d9fca8c3ff5e9d9ed4bdd1aff1a25f77a0d3c8b34ce5fb9f614aa13)

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
