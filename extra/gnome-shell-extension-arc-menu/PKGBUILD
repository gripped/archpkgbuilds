# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=64
pkgrel=1
pkgdesc='Application menu extension for GNOME Shell'
arch=(any)
url='https://gitlab.com/arcmenu/ArcMenu'
license=(GPL-2.0-or-later)
depends=(
  gnome-menus
  gnome-shell
)
makedepends=(git)
source=(
  "git+https://gitlab.com/arcmenu/$_pkgname.git#tag=v$pkgver"
  gnome-shell-extension-arch-menu-shutdown-fix.patch
)
b2sums=('44edce656bdff1d947cc0841ff18f2b5bb4f4aa3d7af4a68795ab26d9b28d9ec5176ebcce3df9d7370dd9219f15235bcb97146c04f7c4c7e6d83a1f290a258c5'
        '3bb8fa03d9a0cc1261bb78d626858f17c2ce5c0a449c7dd04db7ac07ede561a396309127d894d93410691ac199bcf6ab5adad1553ef145c57c1ae2d24b846b9b')

prepare() {
  cd $_pkgname
  git apply -3 ../gnome-shell-extension-arch-menu-shutdown-fix.patch
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
