# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=56
pkgrel=2
pkgdesc='Application menu extension for GNOME Shell'
arch=('any')
url='https://gitlab.com/arcmenu/ArcMenu'
license=('GPL-2.0-or-later')
depends=('gnome-menus' 'gnome-shell')
makedepends=('git')
source=("git+https://gitlab.com/arcmenu/$_pkgname.git#tag=v$pkgver"
        '0001-Settings-set-default-size-instead-of-size-request.patch')
b2sums=('9e892b259764943f171087df50efb6720228ee73f7886e45b15a76da36392b65d88bc5ec4e622341bd281547b6ab0696c12081f8711eef43b29ea260c1750436'
        '4296d33fc86360d2a22a5af54a328b8eb783a73efb248a632d294c41c8bc77bf50dec9bf01062d061f00cd25d8719b9aedc89ceede0154892493cbb81101580b')

prepare() {
  cd $_pkgname
  # https://gitlab.com/arcmenu/ArcMenu/-/merge_requests/215
  patch -Np1 -i ../0001-Settings-set-default-size-instead-of-size-request.patch
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
