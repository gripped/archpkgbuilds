# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=67.1
pkgrel=1
pkgdesc='Application menu extension for GNOME Shell'
arch=(any)
url='https://gitlab.com/arcmenu/ArcMenu'
license=(GPL-2.0-or-later)
depends=(
  gnome-menus
  gnome-shell
)
makedepends=(git)
source=("git+https://gitlab.com/arcmenu/$_pkgname.git#tag=v$pkgver")
b2sums=(6c62ecd1b5d9f99a216feae80cabe7362c01b14697610ed7874f08b03f8a757e61a37a8cceaaecfed76e6b87bd65aa593a120007026b50dc66e5019d8074e391)

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
