# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=55
pkgrel=1
pkgdesc='Application menu extension for GNOME Shell'
arch=('any')
url='https://gitlab.com/arcmenu/ArcMenu'
license=('GPL-2.0-or-later')
depends=('gnome-menus' 'gnome-shell')
source=("https://gitlab.com/arcmenu/ArcMenu/-/archive/v$pkgver/$_pkgname-v$pkgver.tar.gz")
sha256sums=('ca7b44e437c9141da1cb5ca7e39855206a80ad681e7261df89cc616d1321f0d9')

build() {
  cd $_pkgname-v$pkgver
  make
}

package() {
  cd $_pkgname-v$pkgver
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
