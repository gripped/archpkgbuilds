# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=57
pkgrel=1
pkgdesc='Application menu extension for GNOME Shell'
arch=('any')
url='https://gitlab.com/arcmenu/ArcMenu'
license=('GPL-2.0-or-later')
depends=('gnome-menus' 'gnome-shell')
makedepends=('git')
source=("git+https://gitlab.com/arcmenu/$_pkgname.git#tag=v$pkgver")
b2sums=('36d552dc110f76a3d334d3cf0b4ff6cea5ad99a431916b156f3d5a8eb98d7491a8c4994a735297623a05ac3b4f3a92b1d199e06ddda7ba43b3c195d7c5c780de')

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
