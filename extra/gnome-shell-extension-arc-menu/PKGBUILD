# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=63
pkgrel=2
pkgdesc='Application menu extension for GNOME Shell'
arch=(any)
url='https://gitlab.com/arcmenu/ArcMenu'
license=(GPL-2.0-or-later)
depends=(
  gnome-menus
  gnome-shell
)
makedepends=(git)
source=(
  "git+https://gitlab.com/arcmenu/$_pkgname.git#tag=v$pkgver"
  gnome-shell-extension-arch-menu-shutdown-fix.patch
)
b2sums=(
  ef9f54040dcb3ccf93969d85f1f69d06f336f8f8c05ac21ab17ceb0358fb2a2642e7559ce79d4bd2a90acbcce5978266c890bf22ad20e97308e34f75fc3814c7
  3bb8fa03d9a0cc1261bb78d626858f17c2ce5c0a449c7dd04db7ac07ede561a396309127d894d93410691ac199bcf6ab5adad1553ef145c57c1ae2d24b846b9b
)

prepare() {
  cd $_pkgname
  git apply -3 ../gnome-shell-extension-arch-menu-shutdown-fix.patch
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
