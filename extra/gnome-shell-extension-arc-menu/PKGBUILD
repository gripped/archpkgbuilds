# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=65
pkgrel=2
pkgdesc='Application menu extension for GNOME Shell'
arch=(any)
url='https://gitlab.com/arcmenu/ArcMenu'
license=(GPL-2.0-or-later)
depends=(
  gnome-menus
  gnome-shell
)
makedepends=(git)
source=("git+https://gitlab.com/arcmenu/$_pkgname.git#tag=v$pkgver")
b2sums=(76c2c511ba9c2d901d0189d6f6434d37ca2a5e8713b56cad638776efc81986f232c06f0cc99f0258eb8f31f94b46a4626d392b69c1ca2ac9611a5e73d7d57322)

prepare() {
  cd $_pkgname

  # Update command for user settings
  # https://gitlab.com/arcmenu/ArcMenu/-/merge_requests/241
  git cherry-pick -n 50ec6ab0456a0591bdacf192973e77fcbefc3322

  # Add option to activate app on launch
  # https://gitlab.com/arcmenu/ArcMenu/-/merge_requests/242
  git cherry-pick -n 840cddab719d8befef1ab523d7f34f6f8113ccb3
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
