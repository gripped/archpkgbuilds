# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-xlsxwriter
pkgver=3.2.7
pkgrel=1
pkgdesc='A Python module for creating Excel XLSX files'
arch=('any')
url='https://github.com/jmcnamara/XlsxWriter'
license=('BSD-2-Clause')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
source=("$pkgname::git+$url#tag=RELEASE_$pkgver")
sha512sums=('6af1fbf2b15b3b776861e4d27536eb5bfb398ee8320cedb46da68a55b0c95a8610d01da18f5b5634cd3df0afbf9fd1e9e86ced6f5c61cc136b071cf24916f3b8')
b2sums=('0ff4c68a4031d3bee57e66998d4518ce5c6c6048c03d485b8a7ca67198db0e0a32789335525ed21177266fdbb45efeb59e81a9f8c322cc5d05dfca32e3f53221')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m unittest discover
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
