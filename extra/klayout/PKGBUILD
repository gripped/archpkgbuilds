# Contributor: Lars Boettcher <lars@newyew.de>
# Maintainer: Aaron Ali <t0nedef@causal.ca>

pkgname=klayout
pkgver=0.30.5
pkgrel=2
pkgdesc="High Performance Layout Viewer And Editor. Support of GDS and OASIS files."
arch=('x86_64')
url="https://www.klayout.de"
license=('GPL-3.0-or-later')
depends=('libgit2' 'qt6-base' 'qt6-tools' 'qt6-multimedia' 'qt6-svg' 'qt6-5compat' 'python' 'ruby')
source=("${url}/downloads/source/${pkgname}-${pkgver}.tar.bz2"
        "klayoutEditor.desktop" "klayoutViewer.desktop")
sha512sums=('a5afa1b5931e9356804f8f01d50f382b3c95046f4549f532c2975dadb722046176b03e2f53d13fc6bbd3eb1f88a47773b52c5fba88d84918da97e112609f4c62'
            'bc6c7621e5cda754509a86664fb3e7ecd15f577dec712f4b0c52c9a7455ba692043a67145f5bf143d8fdc502b84e4212303906d62107537eefb4bebe57b2ffe1'
            '37a3bbb06ab155c1c22e3990cf160383570e8ea945dbf4964bc94b6fd4e6321627d31071a29d3dbeafc9e2fc406b6905761e299bfccdfe0a35663f2021624f4f')

build() {
  cd klayout-$pkgver
  ./build.sh \
    -rpath "/usr/lib/klayout" \
    -qmake qmake6
}

package() {
  install -D -m 644 klayoutEditor.desktop "$pkgdir"/usr/share/applications/klayoutEditor.desktop
  install -D -m 644 klayoutViewer.desktop "$pkgdir"/usr/share/applications/klayoutViewer.desktop
  cd klayout-$pkgver
  install -D -m 644 etc/logo.png "$pkgdir"/usr/share/icons/hicolor/256x256/apps/klayout.png
  install -D -m 755 bin-release/{klayout,strm*} -t "$pkgdir"/usr/bin/
  install -d "$pkgdir"/usr/lib/"$pkgname"/
  cp -a bin-release/* "$pkgdir"/usr/lib/"$pkgname"/

  # Remove ELF executables and Python modules in /usr/lib/klayout
  rm -r "$pkgdir"/usr/lib/"$pkgname"/{klayout,strm*,pymod}

  local site="$(python -c 'import site; print(site.getsitepackages()[0])')"
  install -d "$pkgdir/$site"

  # pya is included for compatibility reasons, but will be deprecated in the future
  # See upstream: https://github.com/KLayout/klayout/issues/2072
  cp -a bin-release/pymod/{klayout,pya} "$pkgdir/$site/"
}
