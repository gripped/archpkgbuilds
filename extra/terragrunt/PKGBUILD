# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.93.0
pkgrel=1
pkgdesc='Thin wrapper for Terraform and OpenTofu for working with multiple modules'
arch=(x86_64)
url='https://github.com/gruntwork-io/terragrunt'
license=(MIT)
depends=(
  glibc
  terragrunt-iac-provider
)
makedepends=(
  git
  go
)
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('f2e2b7789fe301666853e5a4c919da99adee87762743d28a247b9146c50f634f93149123ab4bfd60f5ba038f482708188e764915e6ac349d6719a5e3436a0256')
b2sums=('d9606210c28d7034ddc708b08f8d219c104059881196b21a7e5b8e8b1a9fba31a96c1e11aa8e318ff090e0c6115eb5966b59b774d1423cb54786c491ce05ac5b')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -X github.com/gruntwork-io/go-commons/version.Version=$pkgver \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "$ld_flags" -o build/
}

check() {
  cd $pkgname
  # Deselect integration tests & tests requiring network access
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/gruntwork-io/terragrunt/cli/commands/run \
      | grep -v github.com/gruntwork-io/terragrunt/internal/cas \
      | grep -v github.com/gruntwork-io/terragrunt/internal/github \
      | grep -v github.com/gruntwork-io/terragrunt/test
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" build/$pkgname
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
