# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.90.0
pkgrel=1
pkgdesc='Thin wrapper for Terraform and OpenTofu for working with multiple modules'
arch=(x86_64)
url='https://github.com/gruntwork-io/terragrunt'
license=(MIT)
depends=(
  glibc
  terragrunt-iac-provider
)
makedepends=(
  git
  go
)
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('635079fbc28b8c9b724a2be2a568662ac8c2b2b1818f7ed1128bc1549217c1853739a1147d23d828f512d7ae93d0abf2501ebfc11b5d546bdbc613941125c12e')
b2sums=('3f4f8d1907a130609c0fc7d13d9bf2aaa3c95ce3682fba46a0ec84f8f7d190f44e34a47fb02b20c7f87e6a399aa37010c648463bdb58d43a36c5e12c442f9121')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -X github.com/gruntwork-io/go-commons/version.Version=$pkgver \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "$ld_flags" -o build/
}

check() {
  cd $pkgname
  # Deselect integration tests & tests requiring network access
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/gruntwork-io/terragrunt/cli/commands/run \
      | grep -v github.com/gruntwork-io/terragrunt/internal/cas \
      | grep -v github.com/gruntwork-io/terragrunt/test \
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" build/$pkgname
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
