# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.80.4
pkgrel=1
pkgdesc='Thin wrapper for Terraform and OpenTofu for working with multiple modules'
url='https://github.com/gruntwork-io/terragrunt'
arch=(x86_64)
makedepends=(
  git
  go
)
depends=(
  glibc
  terragrunt-iac-provider
)
license=(MIT)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('b8de3422bb86da3fb6d2170aa4a35e1679aef228985fe5b683120866fa4fb591')
b2sums=('db5140e9bcb6f543a59ddbd826ebbf8beadd1841d9d902b3a917f5dddac7c7768959ff65e012217ce9431a0fa0dec743b95f8667817c3c51de4cc76e31f232fa')
options=('!lto')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X github.com/gruntwork-io/go-commons/version.Version=${pkgver}" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
