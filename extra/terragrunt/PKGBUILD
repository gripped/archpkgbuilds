# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.93.3
pkgrel=1
pkgdesc='Thin wrapper for Terraform and OpenTofu for working with multiple modules'
arch=(x86_64)
url='https://github.com/gruntwork-io/terragrunt'
license=(MIT)
depends=(
  glibc
  terragrunt-iac-provider
)
makedepends=(
  git
  go
)
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('3461e6b236d87cf4524acc9be5eb6921699ca87f9034260bfd95e1a062ff946886161286c35a9deed0ee036d29f72906651d3e2ba1b729ef22525c0c45af9a60')
b2sums=('ff82f36bcb74a08fb1497d49d789848db98831cbd38c19229a0d2ef254e97cc6cab521a736e1e8d1260f5536eb0e560df8aaf405e15cfce50af9d7d68b5f2f99')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -X github.com/gruntwork-io/go-commons/version.Version=$pkgver \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "$ld_flags" -o build/
}

check() {
  cd $pkgname
  # Deselect integration tests & tests requiring network access
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/gruntwork-io/terragrunt/cli/commands/run \
      | grep -v github.com/gruntwork-io/terragrunt/internal/cas \
      | grep -v github.com/gruntwork-io/terragrunt/internal/github \
      | grep -v github.com/gruntwork-io/terragrunt/test
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" build/$pkgname
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
