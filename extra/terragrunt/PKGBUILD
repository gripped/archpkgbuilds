# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.99.4
pkgrel=2
pkgdesc='Thin wrapper for Terraform and OpenTofu for working with multiple modules'
arch=(x86_64)
url='https://github.com/gruntwork-io/terragrunt'
license=(MIT)
depends=(
  glibc
  terragrunt-iac-provider
)
makedepends=(
  git
  go
)
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('4823eedbbc012d8d79612a3bec705201be1697a3465eb5246de5699a1de44fe39a610d61fea4de1a5a24aa262fa58a46c9b75de775dd46ae849750e960c84a86')
b2sums=('c8f747f9360933824858ed5c0df71d845da8f475254c63040592a74682d96d6e27c3d52520f59aeac2c4bee1367c29882b5d2e375dc28f5f715d23dbcc10a68c')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -X github.com/gruntwork-io/go-commons/version.Version=$pkgver \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "$ld_flags" -o build/
}

check() {
  cd $pkgname
  # Deselect integration tests & tests requiring network access
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/gruntwork-io/terragrunt/cli/commands/run \
      | grep -v github.com/gruntwork-io/terragrunt/internal/cas \
      | grep -v github.com/gruntwork-io/terragrunt/internal/git \
      | grep -v github.com/gruntwork-io/terragrunt/internal/github \
      | grep -v github.com/gruntwork-io/terragrunt/internal/report \
      | grep -v github.com/gruntwork-io/terragrunt/internal/runner/run \
      | grep -v github.com/gruntwork-io/terragrunt/test
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" build/$pkgname
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
