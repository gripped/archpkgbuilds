# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.99.1
pkgrel=1
pkgdesc='Thin wrapper for Terraform and OpenTofu for working with multiple modules'
arch=(x86_64)
url='https://github.com/gruntwork-io/terragrunt'
license=(MIT)
depends=(
  glibc
  terragrunt-iac-provider
)
makedepends=(
  git
  go
)
options=('!lto')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('2c5dfec5169f4ba27b9780cdccc9caf330ab2748345c4420c17413db2c4eb57333cead737f7d60b6e8fa19c2096554062d1d3b69f7f326452c99962141841492')
b2sums=('55c2ed4f37d74fd5c906869d3ce7fa53cad2660967580729204f58fdebbe738df2f7082759faafd4d3de49f6b627e1c27623018408c9b468ff2a912a418caf01')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -X github.com/gruntwork-io/go-commons/version.Version=$pkgver \
    -compressdwarf=false \
    -linkmode=external \
  "
  go build -v -ldflags "$ld_flags" -o build/
}

check() {
  cd $pkgname
  # Deselect integration tests & tests requiring network access
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/gruntwork-io/terragrunt/cli/commands/run \
      | grep -v github.com/gruntwork-io/terragrunt/internal/cas \
      | grep -v github.com/gruntwork-io/terragrunt/internal/git \
      | grep -v github.com/gruntwork-io/terragrunt/internal/github \
      | grep -v github.com/gruntwork-io/terragrunt/internal/report \
      | grep -v github.com/gruntwork-io/terragrunt/internal/runner/run \
      | grep -v github.com/gruntwork-io/terragrunt/test
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" build/$pkgname
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
