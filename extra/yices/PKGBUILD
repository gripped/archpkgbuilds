# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Brent Carmer
# Contributor: Alex J. Malozemoff <amaloz@galois.com>

pkgname=yices
pkgver=2.6.5
pkgrel=1
pkgdesc="A solver for Satisfiability Modulo Theories (SMT) problems"
arch=('x86_64')
url="https://yices.csl.sri.com/"
license=('GPL-3.0-or-later')
depends=('cudd' 'gmp' 'libpoly')
makedepends=('gperf')
# Since 2.6.3 sources are not published in https://yices.csl.sri.com/releases/ anymore.
source=("https://github.com/SRI-CSL/yices2/archive/Yices-$pkgver.tar.gz")
sha512sums=('8f01c6d516f2aa901b30e9248c3df78930e670c8e1a795b1bb112561f4e338e6e11eadfc2e6cbfc089f6593561b5874fbe816be677dd09c0834a01829c5cf490')

build() {
  cd yices2-Yices-$pkgver
  autoconf
  ./configure --prefix=/usr --enable-mcsat
  make
}

check() {
  make -C yices2-Yices-$pkgver check
}

package() {
  cd yices2-Yices-$pkgver
  make DESTDIR="$pkgdir/" install
}
