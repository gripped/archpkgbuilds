# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Chad Kunde <Kunde21@gmail.com>

pkgname=gopls
pkgver=0.21.1
pkgrel=2
pkgdesc='Language server for Go programming language'
arch=(x86_64)
url='https://github.com/golang/tools/tree/master/gopls'
license=(BSD-3-Clause)
depends=(go)
makedepends=(git)
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/golang/tools/archive/gopls/v${pkgver}.tar.gz")
sha512sums=('c2a3af645dda3b836eaf9f0d23b067225a57cc0591938e26e8f8af12c2237755094cb538b139e4a0cecaa373b5ce6a45c593624c521c3a897ba750e1780ff930')
b2sums=('879d519fdc40bb49fbf9dcd1074cff27d4757a8ae80339dafac28b85245f7904cf687d3dd0f0d9340100f9a02933142a45b9be2be5e086e65cddd1c7b2a8cd9e')

build() {
  cd "tools-${pkgname}-v${pkgver}/${pkgname}"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -X main.version=v${pkgver} \
    -extldflags \"${LDFLAGS}\"" \
    -o "${pkgname}"
}

package() {
  cd "tools-${pkgname}-v${pkgver}"
  install -Dm755 "${pkgname}/${pkgname}" -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ts=2 sw=2 et:
