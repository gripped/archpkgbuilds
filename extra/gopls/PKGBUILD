# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Chad Kunde <Kunde21@gmail.com>

pkgname=gopls
pkgver=0.21.0
pkgrel=3
pkgdesc='Language server for Go programming language'
arch=('x86_64')
url='https://github.com/golang/tools/tree/master/gopls'
license=('BSD-3-Clause')
depends=('go')
makedepends=('git')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/golang/tools/archive/gopls/v${pkgver}.tar.gz")
sha512sums=('b47906657a813bc747bdced040878de8bf53d5abe48a869d783ff914002aaf591910f027647e5eee50c78c1521fda6ed65079b278f65c8ea1ef0a1c7b30e0888')
b2sums=('66565fd5c4fdbb51bf572e0c28d1003eb15fb11c5af41b2a0690fd929e5840dfcb88f10a420b82d53e6566320101742cead7351237df45a42d1bbdd2a1ae953c')

build() {
  cd "tools-${pkgname}-v${pkgver}/${pkgname}"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -X main.version=v${pkgver} \
    -extldflags \"${LDFLAGS}\"" \
    -o "${pkgname}"
}

package() {
  cd "tools-${pkgname}-v${pkgver}"
  install -Dm755 "${pkgname}/${pkgname}" -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ts=2 sw=2 et:
