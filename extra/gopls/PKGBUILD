# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Chad Kunde <Kunde21@gmail.com>

pkgname=gopls
pkgver=0.18.1
pkgrel=2
pkgdesc='Language server for Go programming language'
arch=('x86_64')
url='https://github.com/golang/tools/tree/master/gopls'
license=('BSD-3-Clause')
depends=('go')
makedepends=('git')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/golang/tools/archive/gopls/v${pkgver}.tar.gz")
sha512sums=('12bf4279cd21c3bc7caba3376710292946e2acf1ae1cc8e598fa7ef07d0ca3061b020a0a5d2397cef190a43daa911072a0c7045d9d4fc54b94450d4b8fe96fd3')
b2sums=('984715eb791213059829fe53ea867c6e262a80806f36962e79edd0649a16684d67f8485fd6f0c1848dc23b92c73b41a89638a55f51466f7f70c132b20cae7389')

build() {
  cd "tools-${pkgname}-v${pkgver}/${pkgname}"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o "${pkgname}"
}

package() {
  cd "tools-${pkgname}-v${pkgver}"
  install -Dm755 "${pkgname}/${pkgname}" -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ts=2 sw=2 et:
