# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Chad Kunde <Kunde21@gmail.com>

pkgname=gopls
pkgver=0.16.2
pkgrel=1
pkgdesc='Language server for Go programming language'
arch=('x86_64' 'aarch64' 'armv7h' 'armv7l')
url='https://github.com/golang/tools/tree/master/gopls'
license=('BSD-3-Clause')
depends=('go')
makedepends=('git')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/golang/tools/archive/gopls/v${pkgver}.tar.gz")
sha512sums=('cdd712a4e316b3f629ceff1a3742da2cf30b94acc45af484addb6ae7cbf0b9272dbbee8ebc252744dd4df38852760ec3fce4b48fecee1a7a751c37d7edcb0424')
b2sums=('b9dff484256368d8b317f6b6e3e6db54b04f14c26b1663d990fec6688a9c9ce4d1f4e6ca075e318630728bfe8cfe886250ef1f57bfe6d50319305cef6dd753b7')

build() {
  cd "tools-${pkgname}-v${pkgver}/${pkgname}"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o "${pkgname}"
}

package() {
  cd "tools-${pkgname}-v${pkgver}"
  install -Dm755 "${pkgname}/${pkgname}" -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ts=2 sw=2 et:
