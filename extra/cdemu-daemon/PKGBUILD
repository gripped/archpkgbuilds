# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-daemon
pkgver=3.3.0
pkgrel=1
pkgdesc="CD/DVD-ROM device emulator daemon"
url="https://cdemu.sourceforge.io/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  VHBA-MODULE
  dbus
  glib2
  glibc
  libao
  libmirage
)
makedepends=(
  cmake
  git
  glib2-devel
  intltool
  ninja
)
optdepends=(
  'alsa-lib: to enable the ALSA audio driver'
  'pulse-native-provider: to enable the PA audio driver'
)
install=cdemu-daemon.install
source=("cdemu-code::git+https://git.code.sf.net/p/cdemu/code#tag=$pkgname-$pkgver")
b2sums=('2efad5bc2d914ed92953a131d66f5ef8c0afe664a8679c9ff6658d9f0f2c9f0b7bdecbbf348a99035cfbda7ce734686d3875e273901cb38e95f4fd46206423d2')

prepare() {
  cd cdemu-code/$pkgname
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
  )

  cmake -S cdemu-code/$pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 cdemu-code/$pkgname/README
  install -Dt "$pkgdir/usr/lib/systemd/user" -m644 \
    cdemu-code/$pkgname/service-example/cdemu-daemon.service
  install -Dt "$pkgdir/usr/share/dbus-1/services" -m644 \
    cdemu-code/$pkgname/service-example/net.sf.cdemu.CDEmuDaemon.service

  echo vhba | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/cdemu.conf"
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"
  install -Dm644 /dev/stdin "$pkgdir/usr/lib/udev/rules.d/60-cdemu.rules" <<END
ACTION!="remove", KERNEL=="vhba_ctl", MODE="0660", OWNER="root", GROUP="cdemu", TAG+="uaccess"
END
}

# vim:set sw=2 sts=-1 et:
