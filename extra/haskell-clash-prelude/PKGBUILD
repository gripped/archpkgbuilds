# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=clash-prelude
pkgname=haskell-clash-prelude
pkgver=1.8.2
pkgrel=37
pkgdesc="Clash: a functional hardware description language - Prelude library"
url="https://github.com/clash-lang/clash-prelude"
license=('BSD-2-Clause')
arch=('x86_64')
depends=('ghc-libs' 'haskell-arrows' 'haskell-constraints' 'haskell-data-binary-ieee754'
         'haskell-data-default-class' 'haskell-extra' 'haskell-ghc-typelits-extra'
         'haskell-ghc-typelits-knownnat' 'haskell-ghc-typelits-natnormalise' 'haskell-half'
         'haskell-hashable' 'haskell-infinite-list' 'haskell-lens' 'haskell-quickcheck'
         'haskell-recursion-schemes' 'haskell-reflection' 'haskell-singletons'
         'haskell-string-interpolate' 'haskell-tagged' 'haskell-th-abstraction' 'haskell-th-lift'
         'haskell-th-orphans' 'haskell-type-errors' 'haskell-uniplate' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-doctest-parallel' 'haskell-hedgehog' 'haskell-hint'
             'haskell-quickcheck-classes-base' 'haskell-tasty' 'haskell-tasty-hedgehog'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-tasty-th')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('7a0dc39145ce61afbc0bde93c459a4a59fac495835b90267fb2fb1f60d5d75ba8b0f7967839f02284e717e4bca60a3a355403f91e85d1d2c9093ee2163ad7818')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # TODO
    runhaskell Setup test --show-details=direct || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
