# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Adrian Groh <adrian[dot]groh[at]t[dash]online[dot]de>
# Contributor: Max Niederman <max@maxniederman.com>

pkgname=ttyper
pkgver=1.6.0
pkgrel=1
pkgdesc="Terminal-based typing test"
arch=('x86_64')
url="https://github.com/max-niederman/ttyper"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('2d3f82b1409651dc5a2e81e10d2cd531a5454dd278d77fa588d1036c192d1a49ad38652580b903d3a121f4a5c1984d46bcdf812684444d2ee80d2336822debf2')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
