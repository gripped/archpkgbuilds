# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hunter
pkgver=3.7.0
pkgrel=3
arch=('x86_64')
pkgdesc='Hunter is a flexible code tracing toolkit'
license=('BSD-2-Clause')
url='https://github.com/ionelmc/python-hunter'
depends=('python-colorama' 'python-cymem' 'python-manhole')
optdepends=('gdb: for remote support')
makedepends=('python-setuptools-scm' 'python-build' 'python-installer' 'python-wheel' 'cython')
checkdepends=('python-pytest-benchmark' 'python-virtualenv' 'python-pytest' 'gdb'
              'python-process-tests' 'python-aspectlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-hunter/archive/v$pkgver.tar.gz")
sha512sums=('ab5df01864830ef7f99f038eb2ca68ad4022a8daf1f2fa7e48b1ceb86d958452158e2262c0f048b5d2c9af3b86b95314d446187ddb2638c4fe262169cca8b25a')

prepare() {
  cd python-hunter-$pkgver
  rm src/hunter/*.c
  rm -r src/hunter/vendor
  rm tests/eviltracer.*

  sed -i 's/from .vendor.colorama/from colorama/' src/hunter/{actions,util}.py
  sed -i 's/from .vendor._cymem.cymem/from cymem.cymem/' src/hunter/_event.pyx
}

build() {
  cd python-hunter-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  # Hack to process .pth files properly
  # ptrace needed to pass the remote tests
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')

  cd python-hunter-$pkgver
  python tests/setup.py build_ext --inplace
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python -m installer dist/*.whl
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-${python_version}" PATH="$srcdir/pyvenv/bin:$PATH" pytest
  ) || :

  # Clean up to prevent it from being installed
  rm build/lib.linux-$CARCH-cpython-${python_version}/*.so
}

package() {
  cd python-hunter-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
