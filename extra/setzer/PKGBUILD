# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Marco Schröder <narco dot schroeder 96 at gmail dot com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=setzer
pkgver=66
pkgrel=4
pkgdesc='LaTeX editor written in Python with Gtk'
arch=(any)
url="https://github.com/cvfosammmm/$pkgname"
license=(GPL-3.0-or-later)
_pydeps=(bibtexparser
         cairo
         gobject
         numpy
         pdfminer
         pexpect
         pillow
         pyxdg)
depends=(gspell
         gtk3
         gtksourceview5
         libhandy
         libportal
         poppler-glib
         python
         ${_pydeps[@]/#/python-}
         texlive-core
         texlive-xetex
         webkitgtk-6.0
         xdg-utils)
makedepends=(appstream
             meson)
_archive="${pkgname^}-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('6064047f18179f8c3b4abf4c31d06f5896051a63af94533fbdb5d8722a42e08a')

build() {
	arch-meson "$_archive" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
