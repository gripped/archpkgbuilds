# Maintainer: George Hu <integral@archlinux.org>

pkgname=apfsprogs
pkgver=0.2.1
pkgrel=2
pkgdesc="Experimental APFS tools for linux"
arch=('x86_64')
url="https://github.com/linux-apfs/${pkgname}"
license=('GPL-2.0-only')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('92cf4beaf0a34182a2ad02a4babf2235c5ae88c819fda22eeca64dc77bb30a52')
_progs=('mkapfs' 'apfs-snap' 'apfsck' 'apfs-label')

build() {
	for prog in "${_progs[@]}"; do
		make -C "${pkgname}-${pkgver}/${prog}"
	done
}

package() {
	for prog in "${_progs[@]}"; do
		make DESTDIR="${pkgdir}" BINDIR=/usr/bin MANDIR=/usr/share/man/man8 -C "${pkgname}-${pkgver}/${prog}" install
	done
}
