# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin
pkgver=2.10.1
pkgrel=1
pkgdesc='Menu for Xfce4'
arch=('x86_64')
url='https://docs.xfce.org/panel-plugins/xfce4-whiskermenu-plugin/start'
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('accountsservice' 'garcon' 'gtk3' 'gtk-layer-shell' 'xfce4-panel')
makedepends=('git' 'meson' 'ninja')
optdepends=('mugshot: Update user details')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-whiskermenu-plugin.git#tag=v${pkgver}")
sha256sums=('9eafb5f1caa550abe0190cae3861fa3ceca6380e10527ccae8b730e889515c13')

build() {
  arch-meson "${pkgname}" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
