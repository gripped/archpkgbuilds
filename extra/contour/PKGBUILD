# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=contour
pkgver=0.6.1.7494
pkgrel=3
pkgdesc="Modern C++ Terminal Emulator"
arch=(x86_64)
url="https://github.com/contour-terminal/contour"
license=('Apache-2.0')
depends=(
  fontconfig
  freetype2
  gcc-libs
  glibc
  harfbuzz
  libssh2
  libunicode
  libutempter
  libxcb
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-multimedia
  qt6-shadertools
  yaml-cpp
  reflection-cpp
)
makedepends=(
  boxed-cpp
  catch2
  cmake
  extra-cmake-modules
  git
  libxml2
  microsoft-gsl
  ninja
  python
  range-v3
)
optdepends=('ncurses: for contour terminfo database')
source=($pkgname-v$pkgver::"$url/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('925169c3ae3b631455f2bdb19c654fb084d32e768e03fc974156a5d7331f2f7be59f7114849237568fb5feacd6d176eab45b2d2f5e2aa7498a05d847c13beb6e')

build() {

  cmake -S"${pkgname}-${pkgver}" -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCONTOUR_QT_VERSION=6 \
        -DCONTOUR_TESTING=ON
  cmake --build build
}

check() {
  # for running tests, it is (currently) expected to be executed
  # from within the source code's project root directory, in order to
  # access some test files.
  cd "${srcdir}/${pkgname}-${pkgver}"

  "${srcdir}"/build/src/contour/contour version
  "${srcdir}"/build/src/crispy/crispy_test
  "${srcdir}"/build/src/vtparser/vtparser_test
  "${srcdir}"/build/src/vtbackend/vtbackend_test
}

package() {
  cd "${srcdir}/build"
  DESTDIR="${pkgdir}" cmake --build . --target install

  # provided by ncurses now
  rm -rf "${pkgdir}/usr/share/terminfo"

  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
