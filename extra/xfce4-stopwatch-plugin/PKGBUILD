# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=xfce4-stopwatch-plugin
pkgver=0.5.0
pkgrel=1
pkgdesc="A simple stopwatch plugin for Xfce panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-stopwatch-plugin/start"
license=('BSD-2-Clause')
depends=('libxfce4ui' 'libxfce4util' 'xfce4-panel')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-stopwatch-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('12c4f524490d59d221830831722d845080693ec804f1f641a8b416c2405768c4')

prepare() {
  cd $pkgname
  ./autogen.sh \
	  --prefix=/usr
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
