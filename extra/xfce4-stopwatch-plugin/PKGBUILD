# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=xfce4-stopwatch-plugin
pkgver=0.5.2
pkgrel=1
pkgdesc="A simple stopwatch plugin for Xfce panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-stopwatch-plugin/start"
license=('BSD-2-Clause')
depends=('libxfce4ui' 'libxfce4util' 'xfce4-panel')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-stopwatch-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('b6331a5b889fafbd33033ce6e6bbded87eeda9553a5c091443e1c800eb0c7ab0')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
