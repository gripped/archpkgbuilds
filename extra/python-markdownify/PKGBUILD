# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-markdownify
pkgver=1.2.2
pkgrel=2
pkgdesc='Convert HTML to Markdown'
arch=(any)
url='https://github.com/matthewwithanm/python-markdownify'
license=(MIT)
depends=(
  python
  python-beautifulsoup4
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=(
  "$pkgname::git+$url#tag=$pkgver"
  yeet-six.patch
)
sha512sums=('e0a6f36ef80aba3da79f019b8fc52053f307e95ffafadc6b6d5d003634bcd252bb06b32f9bd7aad3f828826fb5849a55c8df6625356a89a63219cdc89ce620a8'
            '4c050fdf6aa8305f4f7eded04313a38e5452a8002fbc2028c469b46c5076806fa7cef3033df5d7df597d16a6def809bcc1ea50e424acb554b1829723977cec93')
b2sums=('ce9f73355cacc1c17368682d28ef1a67a823f99d771ac7844c4bc38d964102a3385e8354052b3f86181c47233f8190e4db1822362e1b8a80121fbd87439263af'
        '8c2438d1febdb7dc4683e6ece059429a8253d63d2f06c002c372b71a2281c42fadd98a89e8a8e019296e96db7789c5752af6090a66ad7a53b70c7875f8022904')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/yeet-six.patch"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
