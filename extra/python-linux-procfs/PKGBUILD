# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=python-linux-procfs
pkgver=0.7.3
pkgrel=4
pkgdesc="Linux /proc abstraction classes for python"
arch=(any)
url="https://git.kernel.org/pub/scm/libs/python/python-linux-procfs/python-linux-procfs.git"
license=(GPL-2.0-only)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(
  $url/snapshot/$pkgname-$pkgver.tar.gz
  0001-python-linux-procfs-Remove-use-of-the-six-Python-lib.patch
)
sha512sums=('650c4d965177c2422961d7131a77c23af8cf583194d2ddcc0472ee3369978427b8d2f88a0ce6651e1141274a8f56a33adc4ce8b1f20534c737a09a7672b5818d'
            '619c79aba573794fbdae791060032eebd6832d8e776a9b720d01883788832e25a9dd5cf3bc6f73526affbbd2069d7a1cc8347cbc8cdc981045e99778190608f1')
b2sums=('5a2296310bca86821bfaa9c1da8c036f300a9b551c58ccc4f2ea66da93c9f971f635f99164f3809634c0b24c93bda4e9cb72b76d620f22bff8c69a9c7a1887ab'
        '372422bd61d701e2f1c308240d69cd2a972716f002bd18ad06dfa0fb9063ae8e595c2ba14aab6c9bfb860cd361c6695db0e3d272671eb4fbb03ababd9458fba3')

prepare() {
  # remove the use of python-six (patch sent to upstream maintainer on 2025-02-20)
  patch -Np1 -d $pkgname-$pkgver -i ../0001-python-linux-procfs-Remove-use-of-the-six-Python-lib.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python bitmasklist_test.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
