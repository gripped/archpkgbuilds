# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=rofi-calc
pkgver=2.3.3
pkgrel=1
pkgdesc="Do calculations in rofi"
arch=('x86_64')
url="https://github.com/svenstaro/rofi-calc"
license=(MIT)
depends=(rofi libqalculate)
makedepends=(meson)
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/svenstaro/rofi-calc/archive/v${pkgver}.tar.gz)
sha512sums=('9205f03351296380f430f6d13831d7e4ade04d809674db15b9a8cb3ffb5b16847424de222ca58ba1f0b5ad9ed13117273fc71df1217ae080251c1e0e6d4da5f0')

build() {
  arch-meson rofi-calc-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  install -Dm644 rofi-calc-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
