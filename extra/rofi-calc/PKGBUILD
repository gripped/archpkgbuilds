# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=rofi-calc
pkgver=2.4.0
pkgrel=1
pkgdesc="Do calculations in rofi"
arch=('x86_64')
url="https://github.com/svenstaro/rofi-calc"
license=(MIT)
depends=(rofi libqalculate)
makedepends=(meson)
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/svenstaro/rofi-calc/archive/v${pkgver}.tar.gz)
sha512sums=('02c60915e3b0ff18b54959e3baccf769dda6460cc3e9d955602e606660199a7b058a1b102fdf8b3a010a589f5f0de66dc6f4f49328be03fbee2d3b46482292a7')

build() {
  arch-meson rofi-calc-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  install -Dm644 rofi-calc-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
