# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=rofi-calc
pkgver=2.5.0
pkgrel=1
pkgdesc="Do calculations in rofi"
arch=('x86_64')
url="https://github.com/svenstaro/rofi-calc"
license=(MIT)
depends=(rofi libqalculate)
makedepends=(meson)
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/svenstaro/rofi-calc/archive/v${pkgver}.tar.gz)
sha512sums=('13dccf67e6704b9f67f2964805bb4ff7ce4224c838ff19d5d4c7ff270b7a86d71098900dc7db7308337a93f4c25a5a8a32f8e73620376b97d1b7a240398aef5a')

build() {
  arch-meson rofi-calc-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  install -Dm644 rofi-calc-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
