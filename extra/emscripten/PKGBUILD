# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Vlad Kolotvin <vlad.kolotvin@gmail.com>

pkgname=emscripten
# NOTE: You need to run ./get-compatible-versions.sh after changing the pkgver!
_binaryen_revision=9f496abc5cf1ddbca8c8a4f4e740c412588708ef
_llvm_project_revision=d6344c1cd0d099f8d99ee320f33fc9254dbe8288
# Sadly, upstream currently suggests bundling a binaryen version for the time being:
# https://github.com/emscripten-core/emscripten/issues/12252
# I'm obviously unhappy about that but it appears to be the only practical solution for the time being.
pkgver=3.1.72
pkgrel=1
pkgdesc="Compile C and C++ into highly-optimizable JavaScript for the web"
arch=('x86_64')
url="https://emscripten.org"
license=('custom')
depends=('nodejs' 'python' 'which' 'acorn')
makedepends=('cmake' 'libxml2' 'git' 'ninja' 'npm')
optdepends=('java-environment: for using clojure'
            'ruby: for using websockify addon'
            'cmake: for emcc --show-ports')
install=emscripten.install
options=('!lto' '!debug')
conflicts=('binaryen')
provides=('binaryen')
source=("git+https://github.com/kripken/emscripten#tag=$pkgver"
        git+https://github.com/llvm/llvm-project.git#commit=$_llvm_project_revision
        git+https://github.com/WebAssembly/binaryen.git#commit=$_binaryen_revision
        "emscripten.sh"
        emscripten-config)
sha512sums=('7c311c7dd55008807a5349d0c00dad9c3ba68808b3323b5248cfa0eaa87b71f1827c1d9d51f9f08208440d30da04a9e86e9d88f70f9c1dd493df655b64b2230f'
            'acbf089e950ba446c32f1fe35c1f3eab96381521610ff8126725aff8d3b864cbd4561127adedad53044b01565246aa06e512e6485ab19422ba6da4dc7f52c700'
            '3e9ce0fe916c91a2db5c2cc639eb3c45a1f480cdbe89bb3a6f88d07bd90ff74307310facaaabca8b92a986d418dd52af74c08396d9cb937a70ace41073b9ea16'
            'fbe9b95b8d18e7d0c6ec5fded6f11b72fbe4ddd0391e5704b281ba79c479f3563e82423b790ddf3f0554a23d659193ca898a81fe3db509f16c30c7188b790e4d'
            '8d22f727e64cace0eb33f049a2902b928c29a6448b05098030858590660f31e1dc142dae55a54bbcb4da09650173f571a64789e0a955aefb449ce0081fb438f9')

build() {
  cd binaryen
  cmake \
      -Bbuild \
      -GNinja \
      -DBUILD_TESTS=OFF \
      -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build

  # Inspired from https://github.com/WebAssembly/waterfall/blob/db2ea5eeb11b74cce9b9459be0cc88807744b1b5/src/build.py#L868
  cd "$srcdir"/llvm-project/llvm
  cmake \
    -Bbuild \
    -GNinja \
    -DLLVM_ENABLE_LIBXML2=OFF \
    -DLLVM_INCLUDE_EXAMPLES=OFF \
    -DCOMPILER_RT_BUILD_XRAY=OFF \
    -DCOMPILER_RT_INCLUDE_TESTS=OFF \
    -DCOMPILER_RT_ENABLE_IOS=OFF \
    -DCMAKE_INSTALL_PREFIX=/opt/emscripten-llvm \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DLLVM_BUILD_RUNTIME=OFF \
    -DLLVM_TOOL_LTO_BUILD=ON \
    -DLLVM_INSTALL_TOOLCHAIN_ONLY=ON \
    -DLLVM_INCLUDE_EXAMPLES=OFF \
    -DLLVM_INCLUDE_TESTS=OFF \
    -DLLVM_TARGETS_TO_BUILD="host;WebAssembly" \
    -DLLVM_ENABLE_PROJECTS="lld;clang" \
    -DCLANG_INCLUDE_TESTS=OFF
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C binaryen/build install

  # Install LLVM stuff according to
  # https://github.com/emscripten-core/emscripten/blob/master/docs/packaging.md
  # and
  # https://github.com/WebAssembly/waterfall/blob/d4a504ffee488a68d09b336897c00d404544601d/src/build.py#L915
  DESTDIR="$pkgdir" ninja -C llvm-project/llvm/build install
  cd "$pkgdir"/opt/emscripten-llvm/bin

  # Clean up some unnecessary bins and libs
  rm clang-check clang-cl clang-cpp clang-extdef-mapping clang-format \
      clang-offload-bundler clang-refactor clang-scan-deps \
      lld-link ld.lld llvm-lib
  cd ../lib
  rm libclang.so
  cd ..
  rm -r share

  # Copy some stuff that we need but that wasn't installed by default
  for bin in llvm-as llvm-dis FileCheck llc llvm-link llvm-mc llvm-readobj opt llvm-dwarfdump; do
      install -Dm755 "$srcdir"/llvm-project/llvm/build/bin/$bin "$pkgdir"/opt/emscripten-llvm/bin/$bin
  done

  # Install emscripten
  cd "$srcdir"/emscripten
  DESTDIR="$pkgdir"/usr/lib/emscripten make install

  # Fix permissions messed up by npm
  find "${pkgdir}"/usr/lib/emscripten -type d -exec chmod 755 {} +
  chown -R root:root "${pkgdir}"/usr/lib/emscripten/

  install -Dm644 "$srcdir"/emscripten-config "$pkgdir"/usr/lib/emscripten/.emscripten

  install -d "$pkgdir"/usr/share/doc
  ln -s /usr/lib/emscripten/site/source/docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 "$srcdir"/emscripten.sh "$pkgdir"/etc/profile.d/emscripten.sh
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
