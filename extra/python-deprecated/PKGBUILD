# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-deprecated
pkgver=1.2.17
pkgrel=1
pkgdesc="Python @deprecated decorator to deprecate old python classes, functions or methods"
url="https://github.com/tantale/deprecated"
license=('MIT')
arch=('any')
depends=('python' 'python-wrapt')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/tantale/deprecated.git#tag=v$pkgver")
sha512sums=('c3d5e9403cbd03cdbe0172f0b21d0a8ef233612e695f68b35beb7df67ee99e82cee4368b74508228665ff6eea2e68d62d8dcd4b4430959dcd1961ab28c91fa18')

build() {
  cd deprecated
  python -m build --wheel --no-isolation
}

check() {
  cd deprecated
  pytest
}

package() {
  cd deprecated
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
