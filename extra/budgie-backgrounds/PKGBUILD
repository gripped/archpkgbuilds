# Maintainer: Campbell Jones <serebit at archlinux dot org>

pkgname=budgie-backgrounds
pkgver=3.0
pkgrel=2
pkgdesc="The default background set for the Budgie Desktop"
arch=(any)
url="https://github.com/BuddiesOfBudgie/budgie-backgrounds"
license=(CC0-1.0)
groups=(budgie)
depends=()
makedepends=(
  git
  imagemagick
  jhead
  meson
)
source=("git+https://github.com/BuddiesOfBudgie/budgie-backgrounds.git?signed#tag=v$pkgver")
b2sums=(b083df7bc4af28b236cbfa5abcddd114317a8d65f9f604577dc875ce2d4aef9bf4ce16390b0fbdd3abee198b6cc5ed79249452313a79ce2e4dd667b267613e41)
validpgpkeys=(8A59B135826B2DA453B9707408AD7343E6BD4B56) # Campbell Jones (Git) <git at serebit dot com>

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$pkgname/LICENSE"
}
