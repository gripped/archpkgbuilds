# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=rocm-dbgapi
pkgver=7.1.1
pkgrel=1
pkgdesc="Support library necessary for a debugger of AMD's GPUs"
arch=('x86_64')
url="https://rocm.docs.amd.com/projects/ROCdbgapi/en/latest/"
license=('MIT')
depends=('rocm-core' 'glibc' 'gcc-libs' 'comgr' 'hsa-rocr')
makedepends=('cmake' 'rocm-cmake')
_git='https://github.com/ROCm/ROCdbgapi'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz")
sha256sums=('4c31da40e6da3c81fea8a8b0757daae3d6e95dc86ba32ff55484e7044aaa094f')
_dirname=$(basename "$_git")-$(basename "${source[0]}" ".tar.gz")

build() {
    local cmake_args=(
        -Wno-dev
        -B build
        -S "$_dirname"
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX=/opt/rocm
    )
    cmake "${cmake_args[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 "$srcdir/$_dirname/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
