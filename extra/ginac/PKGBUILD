# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=ginac
pkgver=1.8.9
pkgrel=1
pkgdesc="C++ library for symbolic calculations"
arch=(x86_64)
url='https://www.ginac.de/'
license=(GPL-2.0-or-later)
depends=(
  bash
  cln
  gcc-libs
  glibc
  readline
)
makedepends=(
  doxygen
  fig2dev
  python
  texlive-bin
)
source=(https://www.ginac.de/$pkgname-$pkgver.tar.bz2)
sha256sums=('6cfd46cf4e373690e12d16b772d7aed0f5c433da8c7ecd2477f2e736483bb439')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --libexecdir=/usr/lib/ginac --disable-static
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver

  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
