# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Nicolas F. <aur@fratti.ch>

pkgname=oxipng
pkgver=9.1.0
pkgrel=1
pkgdesc="A lossless PNG compression optimiser"
arch=(x86_64)
url="https://github.com/shssoichiro/oxipng"
license=('MIT')
depends=('gcc-libs')
options=('!lto')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/shssoichiro/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('c6e2d7934b52881fa4b6b71f7e8bce0c32c853d63158fc62b0b079664deb6269')

build() {
  cd "$pkgname-$pkgver"

  cargo build --release --locked
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 target/release/oxipng "$pkgdir/usr/bin/oxipng"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
