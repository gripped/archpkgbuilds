# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.1.5
pkgrel=1
pkgdesc="Fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  glibc
  openssl
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('836d63f3c6cf2e796f3462f71a5970303bd5e50dd2213ce5126d24e88420a17604bea9930af7a5607dc9f043547e652ab56ffff65b175d15b151ff0d6be7a476')
b2sums=('1f29ff7980b04e54cc78f5f3fd50a423c8007ca9f235b4288ac003cacee428c2cf5e4c2d2576f129ea24ff55a8881217df045144072d9fdd899d8ebbfa189e13')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
