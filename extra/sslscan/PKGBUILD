# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.2.0
pkgrel=1
pkgdesc="Fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  glibc
  openssl
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b01cc5fc903cd15d6895675724c3e6f8060a97b1f1536af6e88e28d63cc5cfdd32beaf61f7a717ecfe0e802d61e092d5189f7b6259fbc9866b82562d3e82ad78')
b2sums=('65a43bba582215dcf2aaa54694a6c29b5dcdcbf5286e6549cc31d886738a932dc06d50510dee4c256251d33a275001fc1a3240182ad606e1e0da50657b8ec931')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
