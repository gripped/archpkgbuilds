# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.2.1
pkgrel=1
pkgdesc="Fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  glibc
  openssl
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('83d1faf9f936c565d9e26147b3e239ac2e0166b2ac0a32dc8e92a63e9c28ad37f14612a98dc8a7005f538242fcb18022ae607df0d56c9b9660fbdf3d999079b0')
b2sums=('a317fa42122014007d4699db75a18e94f89d64119c0fb4df578779b52ccc90b4e2a445519d33d430c53c3b86597c3e7e0102d6d90e5e6efffb603dad007477e6')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
