# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.1.4
pkgrel=1
pkgdesc="Fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  glibc
  openssl
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2880df01cf4a9d05e151a67d0f7ec078645286ce467bb4ead97592d4aeb0a5d7373d67ce48cef8d211c5519fee88fbede7b90adee5e4da1feece51bf609a4318')
b2sums=('4a2473a1fe76f276b6f2656cb9c688e4cb8fb829d76e048ddbf8693287ee50b74e5ba2bf7de44c71f4ae75e73e191c5a52e508c32dfb9cb32678dfbcd5247827')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
