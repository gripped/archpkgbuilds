# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-pkginfo
_upstream=alpm
pkgver=0.4.0
pkgrel=1
pkgdesc="A CLI for writing and parsing of ALPM PKGINFO files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-pkginfo/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('6add457fb36ada82548807c080f9f9f45194f310b37ceab42f5d1f9e6bf6c8c660d14abbbf4c1f9955c5736b7be2564004357618641c14d30a64ff71d09babe8')
b2sums=('a3a48ce3037b6a986bb1d9c799d36fbdce45e7ae13dc29ad234a7f621ec6bbf79e9304eb31dfec12f6ce9b510fe969f604704cf6b72e84e48cf9af39facc19a5')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate specifications $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
