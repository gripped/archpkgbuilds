# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=alpm-pkginfo
_upstream=alpm
pkgver=0.6.1
pkgrel=1
pkgdesc="A CLI for writing and parsing of ALPM PKGINFO files"
arch=(x86_64)
url="https://alpm.archlinux.page/alpm-pkginfo/index.html"
_url="https://gitlab.archlinux.org/archlinux/alpm/alpm.git"
license=(
  'Apache-2.0 OR MIT'
  CC-BY-SA-4.0
)
groups=(alpm)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  just
  lowdown
  rust
  rust-script
)
optdepends=(
  'alpm-docs: for additional specifications'
)
source=($_upstream::git+$_url?signed#tag=$pkgname/$pkgver)
sha512sums=('bf1f726ca872b7f352cd9a196802c43ac917ffcaf2acb923d142ab4f5d38a5ad6aae091ba06574c2474b7cb60e8a9c46570314f15044bd8e4f272537e140f0ee')
b2sums=('739385ed2b054e5eeefe24d1fb101a80c4b317a70808a209364b238297e415e832c32b1ba10b7e15751eed4b57bb9a71c62f2ff90201994e76b86857b5b94217')
validpgpkeys=(991F6E3F0765CF6295888586139B09DA5BF0D338)  # David Runge <dvzrv@archlinux.org>

prepare() {
  cd $_upstream
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $_upstream
  cargo build --frozen --release --features cli --package $pkgname

  just generate shell_completions $pkgname
  just generate specifications $pkgname
  just generate manpages $pkgname
}

check() {
  cd $_upstream
  cargo test --frozen --package $pkgname
}

package() {
  cd $_upstream
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 output/manpages/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 output/manpages/*.5 -t "$pkgdir/usr/share/man/man5/"
  install -vDm 644 output/shell_completions/$pkgname.bash "$pkgdir/usr/share/bash-completions/completions/$pkgname"
  install -vDm 644 output/shell_completions/$pkgname.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 output/shell_completions/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
