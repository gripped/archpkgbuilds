# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_pkgname=ginkgo
pkgbase=ginkgo-hpc
pkgname=(ginkgo-hpc ginkgo-hpc-docs ginkgo-hpc-cuda ginkgo-hpc-hip)
pkgver=1.11.0
pkgrel=3
pkgdesc="Numerical linear algebra software package"
arch=(x86_64)
url=https://ginkgo-project.github.io/
license=(BSD-3-Clause)
depends=(
  glibc
  hwloc
  libgcc
  libgomp
  libstdc++
  openmpi
  yaml-cpp
)
# FIXME: system 'rapidjson' does not work due to some C++ issues
# (see the 'declared protected here' error in https://github.com/Tencent/rapidjson/issues/1338#issuecomment-449849215 )
makedepends=(
  cmake
  ninja
  nlohmann-json
  gflags  # for benchmarks (not installed yet)
  gtest
  numactl
  doxygen
  graphviz
  texlive-bin
  texlive-latexextra
  # -cuda
  cuda
  # -hip
  hip-runtime-amd
  hipblas
  hipfft
  hiprand
  hipsparse
  rocthrust
  roctracer
)
source=(
  "$_pkgname-$pkgver.tar.gz::https://github.com/ginkgo-project/ginkgo/archive/refs/tags/v$pkgver.tar.gz"
  split_cuda_library.patch
  split_hip_library.patch
)
b2sums=('a17728e527d40f8c5593a95b467badc58edd89ece01e3418549909f06cb1eee57e626c31c36dee5ad11379b6fb28b1b6452a3e99bd1dfa2f353703bdf601ff2a'
        '1e5edafb792c9e2cb0602673cc0394d50c0d87bc22590a649c04337070c1a6585f915fcea94660323581df566a3fb777b9a9114ad811cb641dccf7dca33029d6'
        '7992eab1a9c0ed69ce6fc2d86be0374b006bb100600a25556143b53c8d4b4af80fb95614edf0c8dea13a693e94326176364274bf250e6851f57446af736ae715')

prepare() {
  # Ginkgo is too big to build all CUDA archs into a single library, we need to split it
  # https://github.com/ginkgo-project/ginkgo/issues/1734#issuecomment-2517784197
  patch --directory=$_pkgname-$pkgver -Np1 < split_cuda_library.patch
  patch --directory=$_pkgname-$pkgver -Np1 < split_hip_library.patch

  # fix build with rocm 7.2.0 https://github.com/ginkgo-project/ginkgo/issues/1983
  sed -i 's|set(GINKGO_HIP_CUSTOM_THRUST_NAMESPACE ON)|set(GINKGO_HIP_CUSTOM_THRUST_NAMESPACE OFF)|' $_pkgname-$pkgver/CMakeLists.txt
}

build() {
  local common_cmake_options=(
    -S $_pkgname-$pkgver
    -G Ninja
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DGINKGO_BUILD_REFERENCE=ON
    -DGINKGO_BUILD_OMP=ON
    -DGINKGO_BUILD_MPI=ON
    -DGINKGO_HAVE_GPU_AWARE_MPI=ON
    -DGINKGO_BUILD_BENCHMARKS=ON
    -DGINKGO_BUILD_EXAMPLES=ON
    -DGINKGO_BUILD_DOC=ON
    -DGINKGO_BUILD_TESTS=ON
  )
  # In general, we want to list all real archs (sm_XX) and the latest virtual arch (compute_XX) for future PTX compatibility.
  # Valid values can be discovered from nvcc --help
  local cuda_archs="75;80;86;87;88;89;90;100;103;110;120;121;121-virtual"
  # archs gfx1010;gfx1012;gfx1030;gfx1100;gfx1101;gfx1102 are not supported: https://github.com/ginkgo-project/ginkgo/issues/1429
  # 128-bit atomics not available on gfx940 and gfx941 https://github.com/ginkgo-project/ginkgo/issues/1965
  #local amdgpu_archs="gfx900,gfx906:xnack-,gfx908:xnack-,gfx90a:xnack+,gfx90a:xnack-,gfx942,gfx950"
  # building for more architectures does not work since rocm 7.2.0
  local amdgpu_archs="gfx908,gfx90a,gfx942,gfx950"

  # base package
  cmake -B build "${common_cmake_options[@]}" \
    -DGINKGO_BUILD_CUDA=OFF \
    -DGINKGO_BUILD_HIP=OFF \
    -DGINKGO_BUILD_SYCL=OFF
  cmake --build build

  # -cuda package
  # Compile source code for supported GPU archs in parallel
  export CUDAFLAGS="--threads 8"
  cmake -B build-cuda "${common_cmake_options[@]}" \
    -DCMAKE_CUDA_ARCHITECTURES="$cuda_archs" \
    -DGINKGO_BUILD_CUDA=ON \
    -DGINKGO_BUILD_HIP=OFF \
    -DGINKGO_BUILD_SYCL=OFF
  cmake --build build-cuda

  # -hip package
  # LTO does not work with HIP
  export HIPFLAGS="${CXXFLAGS/-flto=auto/}"
  export CXXFLAGS="${CXXFLAGS/-flto=auto/}"
  # HIP does not support -fcf-protection
  export HIPFLAGS="${HIPFLAGS/-fcf-protection/}"
  # Ginkgo does not support _GLIBCXX_ASSERTIONS for device builds https://github.com/ginkgo-project/ginkgo/issues/1143#issuecomment-2036957897
  export HIPFLAGS="${HIPFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS/}"
  # Compile source code for supported GPU archs in parallel
  export HIPFLAGS="$HIPFLAGS -parallel-jobs=$(nproc)"
  cmake -B build-hip "${common_cmake_options[@]}" \
    -DCMAKE_CXX_COMPILER=/opt/rocm/lib/llvm/bin/amdclang++ \
    -DCMAKE_HIP_ARCHITECTURES="$amdgpu_archs" \
    -DGINKGO_BUILD_CUDA=OFF \
    -DGINKGO_BUILD_HIP=ON \
    -DGINKGO_BUILD_SYCL=OFF
  cmake --build build-hip
}

check() {
  # FIXME: some tests fail:
  # - https://github.com/ginkgo-project/ginkgo/issues/1567
  local excluded_tests=""
  excluded_tests+="|benchmark_multi_vector_distributed"  #1567
  excluded_tests+="|benchmark_spmv_distributed"  #1567
  excluded_tests+="|benchmark_solver_distributed"  #1567

  # limit parallel execution of tests to 4 threads and 8 processes
  # note that without a GPU we cannot run tests for -cuda and -hip
  (
    export OMP_NUM_THREADS=4
    export CTEST_PARALLEL_LEVEL=8
    export CTEST_OUTPUT_ON_FAILURE=1
    ctest --test-dir build --exclude-regex "$excluded_tests"
  )
}

_package() {
  DESTDIR="$pkgdir" cmake --install build"$1"

  # move gdb script to the right location (/usr/lib is not in the default safe-path)
  install -vdm 755 "$pkgdir"/usr/share/gdb/auto-load/usr/lib/
  mv -v "$pkgdir"/usr/lib/*-gdb.py "$pkgdir"/usr/share/gdb/auto-load/usr/lib/

  # install the license
  install -vDm 644 $_pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # TODO: install the benchmarks and examples: https://github.com/ginkgo-project/ginkgo/issues/1144
}

package_ginkgo-hpc() {
  optdepends=(
    'ginkgo-hpc-docs: for documentation'
  )

  _package ""
}

package_ginkgo-hpc-docs() {
  pkgdesc+=" - documentation"
  depends=()

  # install the documentation
  install -vdm 755 "$pkgdir/usr/share/doc/$pkgbase"
  cp -r build/doc/usr "$pkgdir/usr/share/doc/$pkgbase/html"

  # install the license
  install -vDm 644 $_pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_ginkgo-hpc-cuda() {
  pkgdesc+=" (with CUDA)"
  depends+=(cuda)
  optdepends=(
    'ginkgo-hpc-docs: for documentation'
  )
  conflicts=(ginkgo-hpc)
  provides=(ginkgo-hpc)

  _package -cuda
}

package_ginkgo-hpc-hip() {
  pkgdesc+=" (with ROCm/HIP)"
  depends+=(
    hip-runtime-amd
    hipblas
    hipfft
    hiprand
    hipsparse
    roctracer
  )
  optdepends=(
    'ginkgo-hpc-docs: for documentation'
  )
  conflicts=(ginkgo-hpc)
  provides=(ginkgo-hpc)

  _package -hip
}

# vim:set ts=2 sw=2 et:
