# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=adns
pkgver=1.6.1
pkgrel=1
pkgdesc="An asyncronous replacement resolver library"
arch=('x86_64')
url="https://www.chiark.greenend.org.uk/~ian/adns/"
license=('GPL-3.0-or-later')
depends=('glibc')
validpgpkeys=('559AE46C2D6B6D3265E7CBA1E3E3392348B50D39')
source=(https://www.chiark.greenend.org.uk/~ian/$pkgname/ftp/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('71d8b3368f8055bbf558d7828e772f6689b3370945d7c2061b835c43d4f71971569d1a17c0cfd6e8ebe0602d78cdf22d4915eeb06836e2193f521d51e87fa0a0'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  mkdir -p "$pkgdir"/usr/{lib,include,bin}
  make prefix="$pkgdir"/usr install
}
