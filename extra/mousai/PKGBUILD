# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=mousai
pkgver=0.7.9
pkgrel=1
pkgdesc='Simple application to identify songs'
arch=(x86_64)
url='https://apps.gnome.org/Mousai/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
)
makedepends=(
  git
  meson
  rust
)
options=(!lto)
source=("git+https://github.com/SeaDve/Mousai.git#tag=v$pkgver")
b2sums=(8782aa3081c5259844f2e42e1e7593dac629f8723535720f1d5e82125238016fea1f79271f9920dec3b8d49f7200742ab19b03fad0ab8671b147419a7a4b6bbf)

prepare() {
  cd Mousai

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Mousai build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
