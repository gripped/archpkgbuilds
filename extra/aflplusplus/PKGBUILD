# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor GI Jack <GI_Jack@hackermail.com>

pkgname=afl++
pkgver=4.30c
pkgrel=1
pkgdesc='instrumentation-driven fuzzer for binary format'
url='https://aflplus.plus/'
arch=(x86_64)
license=(Apache-2.0)
depends=(
  bash
  clang
  compiler-rt
  gcc-libs
  glibc
  lld
  llvm
  python
)
makedepends=(
  cpio
  git
  grep
  pax-utils
  python-jinja
  python-msgpack
  rust
)
checkdepends=(
  cmocka
)
provides=(
  afl
)
conflicts=(
  afl
  aflplusplus
)
replaces=(
  afl
  aflplusplus
)
source=(
  "https://github.com/AFLplusplus/AFLplusplus/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  0001-fix-gcc-plugin-test.patch
  0002-unbreak-mutators-with-python-3.13.patch
)
sha256sums=('7c08c81f59b6c1f0bc2428fdee9fb880520e72c50be0683072e66bcde662b480'
            '752b45f0eb74d4618b5c11ab8885bd6ea2eaa51fdbda3347d4bbd6cb912aa2e6'
            '5ed97cdd9a8016b3eee3bc4c1145c002028f4ae0575c803852b4580f083b91ac')
b2sums=('7f9c7d32afc5316cef460a72e7603aee36161559c2bb5c03edc408c16e8ba8bf96ced2c90e5578caa401ace37caf3ff7bba72ab58b7070c7c5b69004dc2d4da7'
        'd6893b5f8a0559ba4702f16e3d5f203c2557b5f1ee84e280cb8b054643f6eb5842a04f1e4be5903b8e793d1fd7f9dfb6e87c939d14e604ddd7e33f530592390c'
        '7ea7d31670fd3097e95e47c05b94ac355370ad8e85bd76d61f73fd87b8a8b04af9101cdbbaa29115dc342116e08366ed31cb01269b22ebcc4cd1f154e127fc15')

prepare() {
  cd "AFLplusplus-${pkgver}"

  # https://github.com/AFLplusplus/AFLplusplus/pull/2272
  patch -Np1 -i ../0001-fix-gcc-plugin-test.patch

  # https://github.com/AFLplusplus/AFLplusplus/issues/2284#issuecomment-2578810564
  patch -Np1 -i ../0002-unbreak-mutators-with-python-3.13.patch
}

build() {
  cd "AFLplusplus-${pkgver}"
  export LLVM_LTO=1
  make PREFIX=/usr
}

check() {
  cd "AFLplusplus-${pkgver}"
  # Unset our CFLAGS/CXXFLAGS for the tests since these may
  # interact in unexpected ways with afl-cc instrumentation.
  CFLAGS= CXXFLAGS= make test
}

package() {
  cd "AFLplusplus-${pkgver}"
  make install PREFIX=/usr DESTDIR="${pkgdir}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
