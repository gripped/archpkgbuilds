# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2023.07
pkgrel=1
pkgdesc='U-Boot bootloader utility tools'
arch=(x86_64)
url='https://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(openssl)
makedepends=(python python-setuptools swig)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2{,.sig})
validpgpkeys=('1A3C7F70E08FAB1707809BBF147C39FF9634B72C')
options=(!lto)
sha1sums=('a24f9c922e092402d9d5df9c0b41ad7f788b5dac'
          'SKIP')
sha256sums=('12e921b466ae731cdbc355e6832b7f22bc90b01aeceef9886f98aaba7b394300'
            'SKIP')

build() {
  cd u-boot-$pkgver
  make defconfig
  make tools-all KBUILD_HOSTLDFLAGS=${LDFLAGS}
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -Dt "$pkgdir"/usr/share/man/man1/ u-boot-$pkgver/doc/{mkimage,dumpimage}.1
}
