# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2025.04
pkgrel=1
pkgdesc='U-Boot bootloader utility tools'
arch=(x86_64)
url='https://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(openssl)
makedepends=(python python-setuptools swig)
source=(https://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2{,.sig})
validpgpkeys=('1A3C7F70E08FAB1707809BBF147C39FF9634B72C')
options=(!lto)
sha1sums=('6bc4fd62d5b135ff2fbb11fcecbb4b8ca055022a'
          'SKIP')
sha256sums=('439d3bef296effd54130be6a731c5b118be7fddd7fcc663ccbc5fb18294d8718'
            'SKIP')

build() {
  cd u-boot-$pkgver
  make defconfig
  make tools-all KBUILD_HOSTLDFLAGS=${LDFLAGS}
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb,kwboot}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -Dt "$pkgdir"/usr/share/man/man1/ u-boot-$pkgver/doc/{mkimage,dumpimage,kwboot}.1
}
