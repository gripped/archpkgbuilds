# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2024.10
pkgrel=1
pkgdesc='U-Boot bootloader utility tools'
arch=(x86_64)
url='https://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(openssl)
makedepends=(python python-setuptools swig)
source=(https://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2{,.sig})
validpgpkeys=('1A3C7F70E08FAB1707809BBF147C39FF9634B72C')
options=(!lto)
sha1sums=('f5998171fd6902d2f29bffc90790b6214cbdf88f'
          'SKIP')
sha256sums=('b28daf4ac17e43156363078bf510297584137f6df50fced9b12df34f61a92fb0'
            'SKIP')

build() {
  cd u-boot-$pkgver
  make defconfig
  make tools-all KBUILD_HOSTLDFLAGS=${LDFLAGS}
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb,kwboot}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -Dt "$pkgdir"/usr/share/man/man1/ u-boot-$pkgver/doc/{mkimage,dumpimage,kwboot}.1
}
