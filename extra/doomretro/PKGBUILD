# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.6
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('x86_64')
url='http://doomretro.com'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('364010f0c7cec960133a2525d3c740db26b461449ebdf23b52eb3da57bf797ca6149d1d5198524dbd244652aacd9ae86298d669826252f719b339807873c50a7')
b2sums=('7acfd9102b281309b6aabe87c9af5cd7c2ae24223307da70d7c6daef2b2d2ec5b1eaa3afa2c9d98683ed0bfd40a6b5c2ef42359da91ccac2995711befa71da29')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
