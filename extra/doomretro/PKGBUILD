# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.4
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('x86_64')
url='http://doomretro.com'
license=('GPL3')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
b2sums=('7abeff1f7450c0990e25b4413954d35f027ea482efef08461173a4afaf76cd03086ac62ab6fd573bb449c6d6bf832fc928b0452bb7efae29df6bbe1a7b4f7597')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
