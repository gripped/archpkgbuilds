# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.8.1
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=(x86_64)
url='http://doomretro.com'
license=(GPL-3.0-only)
depends=(
  glibc
  sdl2
  sdl2_image
  sdl2_mixer
)
makedepends=(git cmake)
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('f946267c5ee414b374c2985544cef261775e6a511207d7b3e91d3b4ffa3676e88da11895b3a07563280af6a25cf02ccce91be9b7c032167cba41fb29ace5f35c')
b2sums=('9984b9aaa9e48cd9ff5b7cd2be1dbe74ab99b1afda657d87b420dd00df7528b54305493fe1ff75c47e721893954c5e8046d042c880d3e12b5212cf83e257fbae')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
