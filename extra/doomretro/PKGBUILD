# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.7.1
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('x86_64')
url='http://doomretro.com'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('85272164329e9b47ff1b7f43125a1a930ad3f49c05e94ec5b9bb8b4249384f3dfa39123f606ea7d66126441ca99690615b7fedecb3df9a96216e72dfcafafa89')
b2sums=('dc206c6f4c660993271f686246ec5107ef0b38ef2f291d364d9750c5f557ecf3bc0a7aad7f0b2cc0a2fff145dc248bef1cf6cdf1689cdcbb8788fe0c3473b4d6')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
