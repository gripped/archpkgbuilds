# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.6.1
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('x86_64')
url='http://doomretro.com'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('a2163e760af6ff9c5e26ed3469ecc7ad35db9c8a54dda3f748b1bf25cdb50dd175ae25ff36696e4d2a4240dcb41ff6323b32648cc5ecee2e54d7729124051998')
b2sums=('0279116ce3f57ce176fef7a1ee887de49a079ca4dfb124aa3fa8da96f21a47a8d71db416383182b0ecc09c31f9f91bc5ea63c83ad59f4d146c72ae65a6518c6e')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
