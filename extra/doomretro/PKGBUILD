# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.8.2
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=(x86_64)
url='http://doomretro.com'
license=(GPL-3.0-only)
depends=(
  glibc
  sdl2
  sdl2_image
  sdl2_mixer
)
makedepends=(git cmake)
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('da45416b0e4eaec2f3111886b2532fe1a4d43c70470ec33c18c5bbc5809044c5b70eee18bdc0f03d6a0d993b624c4a623c40c38defe6f3153019420111b534ee')
b2sums=('da68ed8204034d867c48935d724fa856913005028f56ed40f88116581477e6378b146641f8b444911668c776e4b2015075e5e1dca5fc40ba0ea5198c2983de9f')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
