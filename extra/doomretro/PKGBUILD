# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.5.1
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('x86_64')
url='http://doomretro.com'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('f14b1fe7ce93afca84cd821d5a40470c7e5e6a5791e9b6d68424897addc54e38d804afb95e71a1057d43bd287e1011f3dd6c368ad8f688752905565a1f8a39b3')
b2sums=('bb544acb6837c23ac795dec87dd12df7360a3ea284e3e92e3612b83b0967ffd613957ae5f44da2660435787fe36204e7dfd52f638fa0b24a152898e48623a99e')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
