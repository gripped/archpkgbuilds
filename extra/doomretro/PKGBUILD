# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.8
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=(x86_64)
url='http://doomretro.com'
license=(GPL-3.0-only)
depends=(
  glibc
  sdl2
  sdl2_image
  sdl2_mixer
)
makedepends=(git cmake)
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('0e1192a69fd125b178afb196d70182e33061c401e220fbc17ad7c4c33812351f24d7f27fce7a0ad6eeebd5e29a96fa4483c13993b51a384f1894d5a25af85ba5')
b2sums=('a23c24a3b22b57f74c12e83a2bd720663e4eb79309ad983bdc53ee01d09ffafc1c4ee00bebd402a963ac521af7205888c79df698dd7bb3eba39464c196a488e2')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
