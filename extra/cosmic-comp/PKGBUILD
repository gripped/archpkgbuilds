# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-comp
pkgver=1.0.0.beta.1
pkgrel=1
pkgdesc='Compositor for the COSMIC desktop environment'
arch=(x86_64)
url='https://github.com/pop-os/cosmic-comp'
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  fontconfig
  gcc-libs
  glibc
  libdisplay-info
  libseat.so
  libinput
  libxcb
  libxkbcommon
  mesa
  pixman
  systemd-libs
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=b19f66702fcdc9c4a7ab066e8dbca4e94bb5063e
source=(git+https://github.com/pop-os/cosmic-comp.git#tag=${_tag}
        update_to_libdisplay-info_3.patch)
b2sums=('57bdb31cb5ea9a9ef5878692eae1d4599dd2d765a2ef81fa8807a9d6c314a6b6032551112e69113380d25ed80f3baf67fa7d6fd94811d28ada205bc16f1a9a3e'
        '6ee7439da3cfe9a30ca8bfdb37ed3ed0be8a56eee763fabc7b58e90518f6459ada3559061558765764901a1108854cbffb6bb8303ffb2472513766816a092a5d')

prepare() {
  cd cosmic-comp

  # Update dependencies to libdisplay-info/libdisplay-info-sys to v0.3.0
  patch -Np1 -i "$srcdir/update_to_libdisplay-info_3.patch"

  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

pkgver() {
  cd cosmic-comp
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-comp
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-comp
  make prefix='/usr' libexecdir='/usr/lib' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
