# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-comp
pkgver=1.0.0.alpha.5.1
pkgrel=1
pkgdesc='Compositor for the COSMIC desktop environment'
arch=(x86_64)
url='https://github.com/pop-os/cosmic-comp'
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  fontconfig
  gcc-libs
  glibc
  libdisplay-info
  libseat.so
  libinput
  libxcb
  libxkbcommon
  mesa
  pixman
  systemd-libs
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=b7937807adbb964a09e914bea3e96026e6c4f4e1
source=(git+https://github.com/pop-os/cosmic-comp.git#tag=${_tag})
b2sums=('344e24417dbf3b4903cb3e3394c3e169146c64183611b3bf505638c564b7480d8579dd049af3b34c89b9b31ed215748580b2080c4c7ca8e972f93f62f72cc797')

prepare() {
  cd cosmic-comp
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

pkgver() {
  cd cosmic-comp
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-comp
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-comp
  make prefix='/usr' libexecdir='/usr/lib' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
