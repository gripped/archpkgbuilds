# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-comp
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='Compositor for the COSMIC desktop environment'
arch=(x86_64)
url='https://github.com/pop-os/cosmic-comp'
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  fontconfig
  gcc-libs
  glibc
  libdisplay-info
  libseat.so
  libinput
  libxcb
  libxkbcommon
  mesa
  pixman
  systemd-libs
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=96e9bf3b81718e2a9ac28b2c24bd1149400b286a
source=(git+https://github.com/pop-os/cosmic-comp.git#tag=${_tag})
b2sums=('af165bae4e0032a46408c882ded44e293505c7a32b88db2e8105e5eeca203e09b686cbd8bd4d2f288166cf00ded750d3d4d7828aaa199e22a39bfcbf4384d3aa')

prepare() {
  cd cosmic-comp
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

pkgver() {
  cd cosmic-comp
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-comp
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-comp
  make prefix='/usr' libexecdir='/usr/lib' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
