# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-font-manager
pkgver=6.5.7
pkgrel=1
pkgdesc='A font management tool for Deepin desktop'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-font-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'fontconfig' 'freetype2' 'gcc-libs')
makedepends=('git' 'cmake' 'deepin-file-manager' 'ninja' 'qt5-tools')
optdepends=('deepin-file-manager: font preview in file manager')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-font-manager.git#tag=$pkgver")
sha512sums=('3fc269afe0332f8cb2814044fefc4ff46d9e7547bbf76f72ff5cbfc1a11de2bb5fa058d07b0c14f04297f888a36b090b0101c6849d55915cc5095cfb89a5c6c4')

prepare() {
  cd deepin-font-manager

  # Fix linker flags
  sed -i 's/CMAKE_EXE_LINKER_FLAGS "-pie"/CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -pie"/' deepin-font-manager/CMakeLists.txt
}

build() {
  cd deepin-font-manager
  cmake -GNinja . -DPLUGIN_INSTALL_DIR=  -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-font-manager
  DESTDIR="$pkgdir" ninja install
}
