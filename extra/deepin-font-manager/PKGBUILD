# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-font-manager
pkgver=6.5.3
pkgrel=1
pkgdesc='A font management tool for Deepin desktop'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-font-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'fontconfig' 'freetype2' 'gcc-libs')
makedepends=('git' 'cmake' 'deepin-file-manager' 'ninja' 'qt5-tools')
optdepends=('deepin-file-manager: font preview in file manager')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-font-manager.git#tag=$pkgver")
sha512sums=('e6af205b318e9e4b7935d61b29fc2002fc6398ed3bc1eefffae5328912a8a90926042f3cd0bab2835590caa4c05c0c1bb7b3d93c02e000e614a19313f85c393d')

prepare() {
  cd deepin-font-manager

  # Fix linker flags
  sed -i 's/CMAKE_EXE_LINKER_FLAGS "-pie"/CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -pie"/' deepin-font-manager/CMakeLists.txt
}

build() {
  cd deepin-font-manager
  cmake -GNinja . -DPLUGIN_INSTALL_DIR=  -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-font-manager
  DESTDIR="$pkgdir" ninja install
}
