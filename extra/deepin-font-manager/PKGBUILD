# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-font-manager
pkgver=6.5.4
pkgrel=1
pkgdesc='A font management tool for Deepin desktop'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-font-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'fontconfig' 'freetype2' 'gcc-libs')
makedepends=('git' 'cmake' 'deepin-file-manager' 'ninja' 'qt5-tools')
optdepends=('deepin-file-manager: font preview in file manager')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-font-manager.git#tag=$pkgver")
sha512sums=('7a53a09269c6f75a6c62937f2b8d40278c35a838559d9b36eb6c8cf1613562a6f06e42aa7c61ef4d955ccbfb6fdd34f367a4b5c0c50d4bcf2881e13127fe3bcb')

prepare() {
  cd deepin-font-manager

  # Fix linker flags
  sed -i 's/CMAKE_EXE_LINKER_FLAGS "-pie"/CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -pie"/' deepin-font-manager/CMakeLists.txt
}

build() {
  cd deepin-font-manager
  cmake -GNinja . -DPLUGIN_INSTALL_DIR=  -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-font-manager
  DESTDIR="$pkgdir" ninja install
}
