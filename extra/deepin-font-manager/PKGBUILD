# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-font-manager
pkgver=6.5.12
pkgrel=1
pkgdesc='A font management tool for Deepin desktop'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-font-manager"
license=('GPL-3.0-or-later')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'fontconfig' 'freetype2' 'gcc-libs')
makedepends=('git' 'cmake' 'deepin-file-manager' 'ninja' 'qt6-tools' 'clang')
optdepends=('deepin-file-manager: font preview in file manager')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-font-manager.git#tag=$pkgver")
sha512sums=('55554630b8145a19a17f4b99ee8fd968acb48ef4b80159e79f4a1725bcfaedea08c60717b16dc1765e2b8e75bd916142041d3bb5c74f7e7563d0a885983b21d3')

prepare() {
  cd deepin-font-manager

  # Fix linker flags
  sed -i 's/CMAKE_EXE_LINKER_FLAGS "-pie"/CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -pie"/' deepin-font-manager/CMakeLists.txt
}

build() {
  cd deepin-font-manager
  cmake -GNinja . -DPLUGIN_INSTALL_DIR=  -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-font-manager
  DESTDIR="$pkgdir" ninja install
}
