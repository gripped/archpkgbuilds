# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-font-manager
pkgver=6.5.10
pkgrel=1
pkgdesc='A font management tool for Deepin desktop'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-font-manager"
license=('GPL3')
depends=('dtk6core' 'dtk6gui' 'dtk6widget' 'qt6-base' 'qt6-svg' 'fontconfig' 'freetype2' 'gcc-libs')
makedepends=('git' 'cmake' 'deepin-file-manager' 'ninja' 'qt6-tools')
optdepends=('deepin-file-manager: font preview in file manager')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-font-manager.git#tag=$pkgver")
sha512sums=('cd92d67a60305af0b02b2caeff2092115e0d3a04a5b18df7804f423047ba023c6519942640038bd0c49c10d126db2341adaf54cdbb0db6cf0c93943fd0ed2536')

prepare() {
  cd deepin-font-manager

  # Fix linker flags
  sed -i 's/CMAKE_EXE_LINKER_FLAGS "-pie"/CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -pie"/' deepin-font-manager/CMakeLists.txt

  # lrelease path
  sed -i 's|lrelease|/usr/lib/qt6/bin/lrelease|' deepin-font-manager-assets/translate_generation.sh
}

build() {
  cd deepin-font-manager
  cmake -GNinja . -DPLUGIN_INSTALL_DIR=  -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-font-manager
  DESTDIR="$pkgdir" ninja install
}
