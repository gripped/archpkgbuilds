# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-network
pkgver=8.1.0
pkgrel=1
pkgdesc='Switchboard Network Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-network
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libnm
  libnma-gtk4
  libswitchboard-3.so
  networkmanager
  polkit
)
makedepends=(
  cmake
  git
  gobject-introspection
  meson
  vala
)
_tag=9e16f690d2fa6564717bbabffd5ca764ac07a008
source=(git+https://github.com/elementary/switchboard-plug-network.git#tag=${_tag})
b2sums=('a48ccc9bd3723442095194fe34852b1cc6d906129903f786f3007e397b537edd8f4e56ee43451d88e95bdd11ee53c42fd3da3fa5898eba0c6458d05d1f82b8a9')

pkgver() {
  cd switchboard-plug-network
  git describe --tags
}

build() {
  arch-meson switchboard-plug-network build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
