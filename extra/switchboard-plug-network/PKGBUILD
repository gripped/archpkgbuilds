# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-network
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Network Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-network
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libnm
  libnma-gtk4
  libswitchboard-3.so
  networkmanager
  polkit
)
makedepends=(
  cmake
  git
  gobject-introspection
  meson
  vala
)
_tag=6a328c296b25d12e0e8b8d0affa37c8c476479e4
source=(git+https://github.com/elementary/switchboard-plug-network.git#tag=${_tag})
b2sums=('c197ed74d4d4d95b64d5eee25a398761f9f5fa36a47f1c4ba60f2024ceabfc91c47c978e1d54b02dd36c64417a2d7177fde3cf5e432e462bfae5c4fcf77b7748')

pkgver() {
  cd switchboard-plug-network
  git describe --tags
}

build() {
  arch-meson switchboard-plug-network build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
