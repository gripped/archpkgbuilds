# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=kile
pkgver=2.9.93
pkgrel=7
pkgdesc='A user friendly TeX/LaTeX frontend for KDE'
arch=(x86_64)
license=(GPL2)
url='https://apps.kde.org/kile/'
depends=(gcc-libs
         glibc
         kcodecs5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         kdbusaddons5
         kguiaddons5
         ki18n5
         kiconthemes5
         kinit
         kio5
         kjobwidgets5
         kparts5
         kservice5
         ktexteditor5
         ktextwidgets5
         kwidgetsaddons5
         kwindowsystem5
         kxmlgui5
         okularpart5
         perl
         poppler-qt5
         qt5-base
         qt5-script
         texlive-basic)
makedepends=(extra-cmake-modules
             kdoctools5
             okular)
optdepends=('konsolepart5: embedded terminal' 'imagemagick: for some file type conversions')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        https://invent.kde.org/office/kile/-/commit/7c050b5e.patch)
sha256sums=('04499212ffcb24fb3a6829149a7cae4c6ad5d795985f080800d6df72f88c5df0'
            'e0e6f18535b7823609c2b8e9d06b1b223f72744d75e1fb53670db2a68b5fbd76')

prepare() {
  patch -d $pkgname-$pkgver -p1 < 7c050b5e.patch # Drop KHTML dependency
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
