# Maintainer: Bert Peters <bertptrs@archlinux.org>
pkgname=systemd-bootchart
pkgver=235
pkgrel=2
pkgdesc="Boot performance graphing tool"
arch=("x86_64")
url="https://github.com/systemd/systemd-bootchart"
license=("GPL-2.0-only" "LGPL-2.1-only")
depends=(systemd-libs glibc gcc-libs)
makedepends=(intltool libxslt docbook-xsl)
source=("$pkgname-$pkgver.tar.gz::https://github.com/systemd/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('42aa55fe1d3054ba3c119a4479929913445c48768eb1d36b13db8c6ad1889b81')

build() {
	cd "$pkgname-$pkgver"
	./autogen.sh
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
	make
}

check() {
	cd "$pkgname-$pkgver"
	make -k check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
