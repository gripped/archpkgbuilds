# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-pyahocorasick
_name=pyahocorasick
pkgver=2.2.0
pkgrel=3
pkgdesc='Python module (C extension and plain python) implementing Aho-Corasick algorithm'
url='https://github.com/WojciechMula/pyahocorasick'
arch=('x86_64')
license=('BSD-3-Clause')
depends=(
  glibc
  python
)
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
b2sums=('6ec20ef0979447d53dc9078b1129049e4d2b2c045c8ac2ae22fe7ff27958d4c16e1b2c2f07a664c81fa12f57ad0b42c757fe8c6f865eb9653b0253466238fe38')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${_name}-${pkgver}"
  PYTHONPATH=. python -c 'import src'
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
