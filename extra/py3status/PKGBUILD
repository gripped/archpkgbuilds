# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Alexis "Horgix" Chotard <alexis.horgix.chotard@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=py3status
pkgver=3.63
pkgrel=2
pkgdesc="An extensible i3status replacement/wrapper written in python"
url="https://ultrabug.github.io/py3status/"
arch=('any')
license=('BSD-3-Clause')
depends=('python' 'python-setuptools' 'glib2')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-hatchling')
checkdepends=('python-pytest')
optdepends=('i3status: for i3status modules'
            'i3blocks: for i3blocks modules'
            'iw: for the wifi module'
            'playerctl: for the playerctl module'
            'python-gobject: needed for the playerctl module'
            'python-mpris2: for the mpris module'
            'python-pyudev: for udev event monitoring'
            'python-dbus: for modules that rely on D-Bus'
            'python-pytz: for the clock module'
            'python-tzlocal: for the clock module'
            'pacman-contrib: for the arch_updates module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ultrabug/py3status/archive/refs/tags/$pkgver.tar.gz"
        "$pkgname-refresh-arch_updates.hook")
sha256sums=('0117543796804211447176b3dad24cb6a9041aebc94e9ae1b0aa21486baf7032'
            'af8725c17cd985a60e2801dd62ca32b26dbd547b279c2e1483793096043bc626')

build() {
    cd "$pkgname-$pkgver"
    python -m build --wheel --no-isolation
}

check() {
    cd "$pkgname-$pkgver"
    pytest
}

package() {
    cd "$pkgname-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -d "$pkgdir/usr/share/doc/$pkgname"{,/dev-guide,/user-guide}
    install -m644 docs/*.md README.md CHANGELOG "$pkgdir/usr/share/doc/$pkgname"
    install -m644 docs/dev-guide/* "$pkgdir/usr/share/doc/$pkgname/dev-guide"
    install -m644 docs/user-guide/* "$pkgdir/usr/share/doc/$pkgname/user-guide"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "../$pkgname-refresh-arch_updates.hook" "$pkgdir/usr/share/libalpm/hooks/$pkgname-refresh-arch_updates.hook"
}
