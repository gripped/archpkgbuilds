# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

pkgname=qtractor
pkgver=0.9.37
pkgrel=2
pkgdesc="Audio/MIDI multitrack sequencer"
arch=(x86_64)
url="https://qtractor.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libmad
  libxcb
  qt6-base
  qt6-svg
  zlib
)
makedepends=(
  alsa-lib
  aubio
  clap
  cmake
  dssi
  jack
  ladspa
  liblo
  libogg
  libsamplerate
  libsndfile
  libvorbis
  lilv
  lv2
  qt6-tools
  rubberband
  suil
  vst3sdk
)
optdepends=(
  'new-session-manager: for session management'
  'qt6-wayland: for native wayland support'
)
provides=(
  clap-host
  dssi-host
  ladspa-host
  lv2-host
  vst-host
  vst3-host
)
source=(https://github.com/rncbc/$pkgname/archive/refs/tags/${pkgname}_${pkgver//./_}.tar.gz)
sha512sums=('0c0de24ba728da28297595028dfd428bf72b86bc2a801f714112f2130e4a3f491970e143b2653ed5a8a464470d2eb2ea2618a2a077af4d12b37e6674a96877fc')
b2sums=('ad760229ee1e1261a208954765444cb8ac909607f274ae18e9883d9c2be129c26bdd6799adc15dad6296857363e116cc890fbd2b363927eb7ce24361e46f1be4')

prepare() {
  # symlink vst3sdk into place
  rm -frv $pkgname-${pkgname}_${pkgver//./_}/src/vst3
  ln -fsv /usr/src/vst3sdk $pkgname-${pkgname}_${pkgver//./_}/src/vst3
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -D CONFIG_LV2_UI_GTK2=OFF
    -D CONFIG_LV2_UI_GTKMM2=OFF
    -S $pkgname-${pkgname}_${pkgver//./_}
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
    aubio libaubio.so
    jack libjack.so
    liblo liblo.so
    libogg libogg.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
    libvorbis libvorbisenc.so libvorbisfile.so libvorbis.so
    lilv liblilv-0.so
    rubberband librubberband.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-${pkgname}_${pkgver//./_}/{README,README.VST} -t "$pkgdir/usr/share/doc/$pkgname/"
}
