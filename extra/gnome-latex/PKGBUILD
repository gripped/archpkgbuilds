# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Tobias Bohrmann (PlainTextField) <honeypot.carbage@aechelon.de>
# Contributor: Bernd Prünster (JesusMcCloud) <aur@berndpruenster.org>

pkgname=gnome-latex
pkgver=3.46.0
pkgrel=5
pkgdesc='LaTeX editor for the GNOME desktop'
arch=(x86_64)
url='https://gitlab.gnome.org/swilmet/gnome-latex'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gsettings-desktop-schemas
  gspell
  gtk3
  hicolor-icon-theme
  libgedit-amtk
  libgedit-gtksourceview
  libgedit-tepl
  libgee
  pango
)
makedepends=(
  appstream-glib
  autoconf-archive
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  vala
  yelp-tools
)
optdepends=(
  'texlive-binextra: Build LaTeX documents'
  'texlive-fontsrecommended: Build LaTeX documents'
  'texlive-latex: Build LaTeX documents'
)
source=("git+https://gitlab.gnome.org/swilmet/gnome-latex.git#tag=${pkgver/[a-z]/.&}")
b2sums=(8f74661c98e3f5aba54d1abee3869832380da3c91abf477efc462e45dd0e23f040cee1c0a2c0864934f900c3c69e3021aea7cad6d9d37e8229fc0192749e04ee)

prepare() {
  cd $pkgname

  # Adapt for Tepl -> libgedit-tepl rename
  git cherry-pick -n 41e532c427f43a5eed9081766963d6e29a9975a1

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk-doc
  make
}

check() {
  make -C $pkgname check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
