# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=esp-generate
pkgver=1.1.0
pkgrel=1
pkgdesc="Template generation tool to create no_std applications for Espressif's chips"
arch=('x86_64')
url="https://github.com/esp-rs/esp-generate"
license=('MIT' 'Apache-2.0')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('acfdeb66af4eff3a733b89fffbf880dcc6a4c69beac7240d0aed105544c7583e')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
