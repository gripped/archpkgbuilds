# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=nampa
pkgname=python-${_pyname}
pkgver=1.0
pkgrel=10
pkgdesc='FLIRT signatures for python'
url='https://github.com/thebabush/nampa'
license=('LGPL-3.0-only')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz" "drop-future.patch")
sha512sums=('899ee2786b0d1739f5d9836310e13c7fdc46df14259491acc480db69f291d6c9934d059064a54798fecab84a9e4a731ada71105dee67b967e93d3cb4435a3387'
            '73868648fc6e1e30a293a056b0660808e923f7c7f0d1b681b96d99ea14f3864a9ee43a32353b215548180e8e9c708ff29a3e6fd2348dfae58eea38d20fa27952')
b2sums=('73122bb669f825fd1b50f6fba1b58f0c1232c404590b7ada652e3142bd4bf74aae21dcb3362c6c9ec7bd1904e13bf1654c2eb3e8ec68332f1a92660bee2ac249'
        'e4a96398edfedc044c34b967d5870357ace6dd994baf3c558aa3373c36eee501dd6342a38630ce7e225697f39561e3e4d038fbcb3f14443dfa46fb0bfee1b966')

prepare() {
  cd ${_pyname}-${pkgver}
  patch -Np1 -i ${srcdir}/drop-future.patch
}

build() {
  cd ${_pyname}-${pkgver}
  export EXTRA_CFLAGS="${CFLAGS} ${CPPFLAGS}"
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # requires binaryninja which is not packaged
    --deselect tests/test_crc.py::test_empty
    --deselect tests/test_crc.py::test_empty_start_value
  )

  cd ${_pyname}-${pkgver}
  PYTHONPATH=build/lib pytest "${pytest_options[@]}"
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
