# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pyhamcrest
pkgver=2.1.0
pkgrel=1
pkgdesc="Hamcrest framework for matcher objects"
arch=('any')
license=('BSD-2-Clause')
url="https://github.com/hamcrest/PyHamcrest"
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling' 'python-hatch-vcs')
checkdepends=('python-pytest')
source=("git+https://github.com/hamcrest/PyHamcrest.git#tag=V$pkgver")
sha512sums=('17b8d27cf3134ac854d6f757711fa317d63c6d8f365baaeaa5b16086eddc86207edcb185a61b9143a966209707cffc7aa6ef0cbe57fd7bc228428d1a19e7d57b')

build() {
  cd PyHamcrest
  python -m build -nw
}

check() {
  cd PyHamcrest
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd PyHamcrest
  python -m installer -d "$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/${pkgname#python-}-$pkgver.dist-info/licenses/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
