# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=qalculate-gtk
pkgver=5.5.1
pkgrel=1
pkgdesc='GTK frontend for libqalculate'
arch=(x86_64)
url='https://qalculate.github.io/'
license=(GPL-2.0-only)
depends=(cairo
         gcc-libs
         gdk-pixbuf2
         glib2
         glibc
         gtk3
         libqalculate
         pango)
makedepends=(intltool
             git
             glib2-devel)
source=(git+https://github.com/Qalculate/qalculate-gtk#tag=v$pkgver)
sha256sums=('a7a2055d87a0e74fb8c1c02109dad4ade90dac7a77d90b4c62745c8334998c3a')

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
