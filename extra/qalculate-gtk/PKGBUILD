# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=qalculate-gtk
pkgver=5.6.0
pkgrel=1
pkgdesc='GTK frontend for libqalculate'
arch=(x86_64)
url='https://qalculate.github.io/'
license=(GPL-2.0-only)
depends=(cairo
         gcc-libs
         gdk-pixbuf2
         glib2
         glibc
         gtk3
         libqalculate
         pango)
makedepends=(intltool
             git
             glib2-devel)
source=(git+https://github.com/Qalculate/qalculate-gtk#tag=v$pkgver)
sha256sums=('3d18f771e3a472de99e27df115aa710df2cc143153636daaa4019573e2221a5f')

prepare() {
  cd $pkgname
  autoreconf -vif -I /usr/share/gettext/m4
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
