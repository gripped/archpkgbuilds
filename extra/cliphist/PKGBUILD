# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Luis Aleman Vazquez <luisv@tuta.io>

pkgname=cliphist
pkgver=0.6.1
pkgrel=1
epoch=1
pkgdesc='wayland clipboard manager'
arch=(i686 x86_64)
url="https://github.com/sentriz/$pkgname"
license=(GPL3)
depends=(glibc
         wl-clipboard)
makedepends=(go)
optdepends=('xdg-utils: image and mime inferance')
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('db0f25ba3e9173dcb257d66cb1293e277a8bdfed11cc8d6b38b7473b0947781d')

build() {
	cd "$_archive"
	export CGO_CPPFLAGS="$CPPFLAGS"
	export CGO_CFLAGS="$CFLAGS"
	export CGO_CXXFLAGS="$CXXFLAGS"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	go build \
		-ldflags "-linkmode external -extldflags '$LDFLAGS'" \
		-o $pkgname
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" $pkgname
}
