# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb ][ gmail ? com>
# Contributor: Jonathan Schaeffer <joschaeffer ][ gmail ? com>
# Contributor: chfoxli <lg ][ romandie ? com>

pkgname=kphotoalbum
pkgver=5.13.0
pkgrel=1
pkgdesc="KDE Photo Album, picture collection manager"
url="https://www.kphotoalbum.org/"
arch=('x86_64')
license=('GPL')
depends=('libkdcraw5' 'marble-common' 'exiv2' 'purpose5' 'perl')
makedepends=('extra-cmake-modules' 'kdoctools5')
optdepends=()
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('9881e1ee314efeea4b2488eb473427c4b53a9fbad455e234170650cb64814156'
            'SKIP')
validpgpkeys=('8A53F0CB9F752E6F4F72FEC6F8DECD0DDF0F74A8'  # Tobias Leupold <tobias.leupold@gmx.de> 
              'A53563BC888E6CE5215535992523842AF9681DDF'
              'F277636BA2E409347D503411CAA57B280FE0E9EE')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
