# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=xdg-desktop-portal-dde
pkgver=1.0.7
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal on Deepin desktop environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/xdg-desktop-portal-dde"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'dtk6widget' 'qt6-base' 'qt6-wayland' 'wayland' 'glibc' 'gcc-libs'
         'xdg-desktop-portal')
makedepends=('git' 'cmake' 'ninja' 'wlr-protocols' 'qt6-tools')
provides=(xdg-desktop-portal-impl)
groups=('deepin')
source=("git+https://github.com/linuxdeepin/xdg-desktop-portal-dde.git#tag=$pkgver")
sha512sums=('f241b9f76b91c50203946dfd5707517a5a952274e6ba7af698c80a53e8b7610752b70f62c93392e29d1619dbb099394c9654bc720f036008485e53f88346f513')

prepare() {
  cd xdg-desktop-portal-dde
  sed -i 's|lrelease|/usr/lib/qt6/bin/lrelease|' misc/translations/translate_generation.sh
}

build() {
  cd xdg-desktop-portal-dde
  cmake . -GNinja -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd xdg-desktop-portal-dde
  DESTDIR="$pkgdir" ninja install
}
