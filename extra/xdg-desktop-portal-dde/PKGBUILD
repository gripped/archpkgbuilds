# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=xdg-desktop-portal-dde
pkgver=1.0.12
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal on Deepin desktop environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/xdg-desktop-portal-dde"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'dtk6widget' 'qt6-base' 'qt6-wayland' 'wayland' 'glibc' 'gcc-libs'
         'xdg-desktop-portal')
makedepends=('git' 'cmake' 'ninja' 'wlr-protocols' 'qt6-tools' 'treeland-protocols')
provides=(xdg-desktop-portal-impl)
groups=('deepin')
source=("git+https://github.com/linuxdeepin/xdg-desktop-portal-dde.git#tag=$pkgver")
sha512sums=('3535c353e503e997cf64ab75b2ce41cad7d3faae0998988361e0b9f306faf4e3712f4f71d5b1e18e4740eb8258e93f56d1f238d1221ebe055b2d871d8125c940')

prepare() {
  cd xdg-desktop-portal-dde
  sed -i 's|lrelease|/usr/lib/qt6/bin/lrelease|' misc/translations/translate_generation.sh
}

build() {
  cd xdg-desktop-portal-dde
  cmake . -GNinja -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd xdg-desktop-portal-dde
  DESTDIR="$pkgdir" ninja install
}
