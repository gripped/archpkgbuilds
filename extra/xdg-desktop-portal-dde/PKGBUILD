# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=xdg-desktop-portal-dde
pkgver=1.0.9
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal on Deepin desktop environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/xdg-desktop-portal-dde"
license=('LGPL-3.0-or-later')
depends=('dtk6core' 'dtk6widget' 'qt6-base' 'qt6-wayland' 'wayland' 'glibc' 'gcc-libs'
         'xdg-desktop-portal')
makedepends=('git' 'cmake' 'ninja' 'wlr-protocols' 'qt6-tools' 'treeland-protocols')
provides=(xdg-desktop-portal-impl)
groups=('deepin')
source=("git+https://github.com/linuxdeepin/xdg-desktop-portal-dde.git#tag=$pkgver")
sha512sums=('22d025bfaf5a407da2254691a77ffc6ebf300f4c05ee21407744fa1fca9c882ab9c7211d7a68198249809900f54bb3b035f8f01e569f34ccfaf9449ce5eb1dcd')

prepare() {
  cd xdg-desktop-portal-dde
  sed -i 's|lrelease|/usr/lib/qt6/bin/lrelease|' misc/translations/translate_generation.sh
}

build() {
  cd xdg-desktop-portal-dde
  cmake . -GNinja -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd xdg-desktop-portal-dde
  DESTDIR="$pkgdir" ninja install
}
