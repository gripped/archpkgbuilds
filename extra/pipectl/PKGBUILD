# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pipectl
pkgver=0.5.1
pkgrel=1
pkgdesc="A simple named pipe management utility"
url="https://github.com/Ferdi265/pipectl"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(glibc)
makedepends=(
  cmake
  ninja
  scdoc
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('6eb0d60177264661642bdd27612e55ad9ed4d5ff163135ab85d004ded6fdc6d19990884bc5ee9bdfdd21e3cfffbb7b677ae14b73ce3abbe988df37d72366497b')
b2sums=('c4b9fa53fe0477c0356940cd33fa8eeeaf93a5358effe8b30a4dbbccf20e022f4ddbc92439e70e6869717308de00f91fc3afda67c68914e235479a470d68a8f9')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_DOCUMENTATION=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
