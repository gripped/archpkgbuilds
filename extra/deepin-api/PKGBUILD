# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=6.0.8
pkgrel=1
pkgdesc='Dbus interfaces for miscellaneous desktop services'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-api"
license=('GPL3')
# alsa-utils: alsactl used in sound-theme-player
# bc: used in adjust-grub-theme
# fontconfig: fc-match used in adjust-grub-theme
# librsvg: rsvg-convert used in adjust-grub-theme
# util-linux: rfkill used in device
# deepin-application-manager: org.desktopspec.ApplicationManager1
depends=('alsa-utils' 'bc' 'fontconfig' 'glib2' 'gdk-pixbuf-xlib' 'gtk3' 'libcanberra-pulse'
         'librsvg' 'libxi' 'libxfixes' 'poppler-glib' 'util-linux' 'xcur2png' 'blur-effect'
         'libpulse' 'libxcursor' 'cairo' 'gdk-pixbuf2' 'freetype2' 'alsa-lib' 'libgudev'
         'deepin-application-manager')
makedepends=('git' 'deepin-gettext-tools' 'go')
groups=('deepin')
install=deepin-api.install
source=("https://github.com/linuxdeepin/dde-api/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        deepin-api.sysusers)
sha512sums=('a8598a71b1530c208ce6d7b7133080f6b428d9b803214bdbb9df32d64b57ef8b9fa20c61bf04c40378c5aac884889803e18d009205b9dfde5984ad2803b4df4c'
            'e894eb3928af9e244fa78010fdf16c8abb6ce18df114cf05327d02b18774d6ba5b023e4dfa0d07042f4e44a5c6e2ddb55b07f3e0db466a0e6169b52465fdefd6')

build(){
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd dde-api-$pkgver
  make
}

package() {
  cd dde-api-$pkgver
  make DESTDIR="$pkgdir" SYSTEMD_LIB_DIR=/usr/lib install

  install -Dm644 ../deepin-api.sysusers "$pkgdir"/usr/lib/sysusers.d/deepin-api.conf
}
