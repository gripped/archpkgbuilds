# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=6.0.13
pkgrel=1
pkgdesc='Dbus interfaces for miscellaneous desktop services'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-api"
license=('GPL3')
# alsa-utils: alsactl used in sound-theme-player
# bc: used in adjust-grub-theme
# fontconfig: fc-match used in adjust-grub-theme
# librsvg: rsvg-convert used in adjust-grub-theme
# util-linux: rfkill used in device
# deepin-application-manager: org.desktopspec.ApplicationManager1
depends=('alsa-utils' 'bc' 'fontconfig' 'glib2' 'gdk-pixbuf-xlib' 'gtk3' 'libcanberra-pulse'
         'librsvg' 'libxi' 'libxfixes' 'poppler-glib' 'util-linux' 'xcur2png' 'blur-effect'
         'libpulse' 'libxcursor' 'cairo' 'gdk-pixbuf2' 'freetype2' 'alsa-lib' 'libgudev'
         'deepin-application-manager')
makedepends=('git' 'deepin-gettext-tools' 'go')
groups=('deepin')
install=deepin-api.install
source=("git+https://github.com/linuxdeepin/dde-api#tag=$pkgver"
        deepin-api.sysusers)
sha512sums=('87a251ed38814102250b4c7adca2b3b0d33c6374374f82a0687593d227d98e5270972f37ea5b9d20d6fdd9707c09eab6533248c817960ef9206dd8c42f9a82ff'
            'e894eb3928af9e244fa78010fdf16c8abb6ce18df114cf05327d02b18774d6ba5b023e4dfa0d07042f4e44a5c6e2ddb55b07f3e0db466a0e6169b52465fdefd6')

build(){
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd dde-api
  make
}

package() {
  cd dde-api
  make DESTDIR="$pkgdir" SYSTEMD_LIB_DIR=/usr/lib install

  install -Dm644 ../deepin-api.sysusers "$pkgdir"/usr/lib/sysusers.d/deepin-api.conf
}
