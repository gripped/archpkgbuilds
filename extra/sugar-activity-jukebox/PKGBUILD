# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-jukebox
_pkgname=jukebox-activity
pkgver=36
pkgrel=5
pkgdesc='Media player for Sugar'
arch=(any)
url='https://github.com/sugarlabs/jukebox-activity'
license=(GPL-2.0-or-later)
groups=(sugar-fructose)
depends=(
  glib2
  gst-plugins-base
  gst-plugins-base-libs
  gst-python
  gstreamer
  gtk3
  pango
  python
  python-gobject
  sugar-toolkit-gtk3
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-good: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
makedepends=(git)
source=("git+https://github.com/sugarlabs/$_pkgname.git#tag=v$pkgver")
b2sums=(517c9a6fb93adda71935b8d46e514a77d32635d0c853ad11ec2c36a25709080dd96ffb8315501f04f376e9face72d5ca79811df9b88c0113d95d413d72929907)

prepare() {
  cd $_pkgname

  # Use standard icon name
  sed -i "s/ToolButton('player_rew')/ToolButton('media-seek-backward')/
          s/ToolButton('player_fwd')/ToolButton('media-seek-forward')/" controls.py
}

package() {
  cd $_pkgname
  python setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
