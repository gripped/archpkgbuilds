# Maintainer: dbgbgtf <dudududumaxver@outlook.com>
# Maintainer: RocketDev <ma2014119@outlook.com>
pkgname=ceccomp
pkgver=4.0
pkgrel=2
pkgdesc="A C-based seccomp analysis tool"
arch=(x86_64)
url="https://github.com/dbgbgtf1/Ceccomp"
license=(GPL-3.0-or-later)
depends=(
  glibc
  libseccomp
)
makedepends=(
  asciidoctor # for doc generation
  gettext # for i18n/l10n
  git
  python # for configure
  util-linux # for flock to display a progress
)
checkdepends=(
  python-pytest
)

source=("$pkgname"::git+https://github.com/dbgbgtf1/Ceccomp.git#tag=v${pkgver}?signed)
b2sums=('178eaf32b6d1a17ed55ab00cc986ef3b7e7709fec86b3e1b17853f619a4ed9c37a5dba890ed4f33d95e102814c40465555ed0f7013eafe2e282d42edc3db8abe')

validpgpkeys=(
  '0816A179BB09248F30468BD6542A0969B5CEDCDB' # dbgbgtf1 <dudududumaxver@outlook.com>
  'A7ACCC386C15E3C554D34B3EAB08F98092A456BB' # RocketDev <ma2014119@outlook.com>
)

prepare() {
  cd "$pkgname"
  ./configure --destdir="$pkgdir" --packager="Arch Linux"
  touch include/config.h # work around for 95c4c0e
  make clean
}

build() {
  cd "$pkgname"
  make
}

check() {
  cd "$pkgname"
  make test
  python -m pytest test
}

package() {
  cd "$pkgname"
  make install
}

# vim: ts=2 sw=2 et:
