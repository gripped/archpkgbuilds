# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-parental-controls
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Parental Controls Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-parental-controls
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  flatpak
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libgee
  libgranite-7.so
  libmalcontent-0.so
  libswitchboard-3.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
_tag=c11e81d851b48de015b666ddf9e98eb4e950514b
source=(git+https://github.com/elementary/switchboard-plug-parental-controls.git#tag=${_tag})
b2sums=('ca617f51e849196804493436b169d5e4fe63e1c16f2d091d674edb956ce8bf2e154358dd22d4a84cc838c4c49f9b3d662701725793ccd26bfbb58023f7353e3e')

pkgver() {
  cd switchboard-plug-parental-controls
  git describe --tags
}

prepare() {
  cd switchboard-plug-parental-controls
  sed "s/dbus_dep.get_pkgconfig_variable('sysconfdir')/dbus_dep.get_pkgconfig_variable('datadir')/" -i data/meson.build
}

build() {
  arch-meson switchboard-plug-parental-controls build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
