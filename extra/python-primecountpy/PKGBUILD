# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=primecountpy
pkgname=python-$_pyname
pkgver=0.1.1
pkgrel=1
pkgdesc='Cython interface to the C++ library primecount'
arch=(x86_64)
url='https://github.com/dimpase/primecountpy'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         primecount
         python
         python-cysignals)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/dimpase/primecountpy#tag=v$pkgver)
sha256sums=('4bbdb9e4cff7970634e1d9d565feae387554d6454380c34965a9d59bc6abadbc')

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname 
  python -m installer --destdir="$pkgdir" dist/*.whl
}
