# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: RedTide <redtid3@gmail.com>

pkgbase=sfizz
pkgname=(
  sfizz
  sfizz-{lib,standalone}
)
pkgver=1.2.3
pkgrel=1
pkgdesc="SFZ parser and synth"
arch=(x86_64)
url="https://github.com/sfztools/sfizz"
license=(BSD-2-Clause)
makedepends=(
  abseil-cpp
  cmake
  cxxopts
  gcc-libs
  ghc-filesystem
  glibc
  jack
  pugixml
  simde
)
checkdepends=(
  catch2-v2
)
source=(
  $url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('753df985b0863baf3335cc386075a1f8053800a521725ad57102394883274aa617f1a554da8471444789c985891c604946f2e0627e3a5e2349586532ea0af3cc')
b2sums=('bd41e583ecb0d77831ea20213692c428d958207914252bf8b9cca2d384d12eb80662803c91575e27c543098950df009b01869db65b02b9dd95c20011eb5d0793')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  # symlink tests data to top-level location so that tests can get to them (we build out of tree)
  ln -sv $pkgbase-$pkgver/tests .
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_CXX_STANDARD=17
    -D ENABLE_LTO=ON
    -D SFIZZ_TESTS=ON
    -D SFIZZ_USE_SYSTEM_ABSEIL=ON
    -D SFIZZ_USE_SYSTEM_CATCH=ON
    -D SFIZZ_USE_SYSTEM_CXXOPTS=ON
    -D SFIZZ_USE_SYSTEM_GHC_FS=ON
    -D SFIZZ_USE_SYSTEM_PUGIXML=ON
    -D SFIZZ_USE_SYSTEM_SIMDE=ON
    -S $pkgbase-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package_sfizz() {
  depends=(
    sfizz-{lib,standalone,ui}
  )

  DESTDIR="$pkgdir" cmake --install build

  (
    cd "$pkgdir"

    _pick $pkgbase-lib usr/include
    _pick $pkgbase-lib usr/lib/lib$pkgbase.so*
    _pick $pkgbase-lib usr/lib/pkgconfig/$pkgbase.pc

    _pick $pkgbase-standalone usr/bin
    _pick $pkgbase-standalone usr/share/man/man1
  )

  install -vDm 644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_sfizz-lib() {
  pkgdesc+=" - C++ library"
  depends=(
    abseil-cpp
    gcc-libs
    glibc
    pugixml
  )
  provides=(libsfizz.so)

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,CONTRIBUTING,GOVERNANCE,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_sfizz-standalone() {
  pkgdesc+=" - JACK standalone client"
  groups=(
    pro-audio
  )
  depends=(
    abseil-cpp
    gcc-libs
    glibc
    jack libjack.so
    pugixml
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,CONTRIBUTING,GOVERNANCE,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
