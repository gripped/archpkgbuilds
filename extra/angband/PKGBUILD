# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Adrian Stratulat <adrian.stratulat at inboxcom>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: KillaB <xGenoBlast@gmail.com>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Christian Schmidt <xmucknertx@googlemail.com>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=angband
pkgver=4.2.5
pkgrel=4
pkgdesc='A roguelike dungeon exploration game based on the writings of JRR Tolkien'
arch=(x86_64)
url='https://rephial.org/'
license=(GPL-2.0-only CC-BY-3.0 CC-BY-4.0)
depends=(
  glibc
  sdl2
  sdl2_ttf
  sdl2_image
  sdl2_mixer
  ncurses
  xorg-fonts-misc
  xorg-mkfontscale
  hicolor-icon-theme
  libx11
)
makedepends=(git python-docutils)
source=(
  "$pkgname::git+https://github.com/angband/angband#tag=$pkgver"
  remove-dirty-version.patch
)
sha512sums=('5794ee23934ad76005968f1f2f1bd129e78e974b825e90ca4fe233582aa377a3dd7b9b6f090fc6d3e4318af0856863ad96feccd39469a5ea9b9a6ac38a330a27'
            'de2bddb3824d581cf6827b9e86f409f9100245b9ce800611e982fa4a80986b4b9a01aea59a3b60f430ece5425977ce5074843ebf4e3cc91c52b175778f86dbc5')
b2sums=('9023ad6803a3514ab1b45e5efc04cbce6386532a388d95fa80084a328480f5603747f90273fcd2f828e5989e748a0e7b70a8825655de1851cd0b558d8a326567'
        '84e2a3f0a959072167aaf55718e02643d0b5793da18bc60d5464de0477fe9237e5f6e3374ee0919ca7079fb86a65c011db979e7d9dbe052af8caa883bf16e906')

build() {
  cd "$pkgname"

  # contrary to popular belief, arch linux is *not* dirty.
  patch -p1 -i "$srcdir/remove-dirty-version.patch"

  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sysconfdir=/usr/share/angband \
    --enable-sdl2 \
    --enable-sdl2-mixer

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 docs/copying.rst "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
