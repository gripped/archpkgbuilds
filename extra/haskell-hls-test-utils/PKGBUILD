# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-test-utils
pkgname=haskell-hls-test-utils
pkgver=2.1.0.0
pkgrel=43
pkgdesc="Utilities used in the tests of Haskell Language Server"
url="https://github.com/haskell/haskell-language-server#readme"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-async' 'haskell-blaze-markup' 'haskell-data-default'
         'haskell-extra' 'haskell-ghcide' 'haskell-hls-graph' 'haskell-hls-plugin-api'
         'haskell-lens' 'haskell-lsp' 'haskell-lsp-test' 'haskell-lsp-types' 'haskell-row-types'
         'haskell-tasty' 'haskell-tasty-expected-failure' 'haskell-tasty-golden'
         'haskell-tasty-hunit' 'haskell-tasty-rerun' 'haskell-temporary'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('c99fa77c715419169edb53d8b37938510466abc14298e66204d28308a8d86911335d9ddcc85b8a92d130ac9d815dc85212275e7034010b93a4d8ddd955c2cdcc')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
