# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: GreyXor <greyxor@protonmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Tam <lchris314 at gmail dot com>
# Contributor: Daniel Micay <danielmicay at gmail dotcom>
# Contributor: David Herrmann <dh.herrmann@googlemail dot com>

pkgname=kmscon
pkgver=9.2.1
pkgrel=2
pkgdesc='Kernel Mode Setting (KMS) and DRM based virtual Console Emulator'
arch=('x86_64')
url='https://github.com/kmscon/kmscon'
license=('MIT')
backup=('etc/kmscon/kmscon.conf')
depends=(
  bash
  glibc
  glib2
  libdrm
  libgl
  libtsm
  libxkbcommon
  mesa
  pango
  systemd-libs
)
makedepends=(
  check
  docbook-xsl
  git
  libxslt
  linux-api-headers
  meson)
source=(
  git+${url}.git#tag=v${pkgver}
  8a9cb88a7bb57801c1127ccb26e5d5a1f3706ac9.patch
  bbd427c8fb0a1292f9077224aa223296becfce63.patch
)
b2sums=('4863387062297017824d0c93e61caeb973ea13a8bc860fa7b3fe5ace1151dd2706754b9d0da34ca245945fe6720fda07c315c25103c95772cba6bf4894b0f20b'
        'f0680e52d6b1b48394b49517461ecfc4533c6925dae050c6e9c4e48c72848875f9fd951726611828faa0172c94e96032f793c26be6a78ed4718eea98d5711d88'
        'bd9c8ec2aaac96f8ea1a71e0b1c276d8f903f86991c4c39a4aa018de0526010ac2b90752f5bbbbc30c31f09898e04fbf6af29d6b9bc6a9354041de11967fa823')

prepare() {
  cd $pkgname
  patch -Np1 -i ../8a9cb88a7bb57801c1127ccb26e5d5a1f3706ac9.patch
  patch -Np1 -i ../bbd427c8fb0a1292f9077224aa223296becfce63.patch
}

build() {
  arch-meson "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
