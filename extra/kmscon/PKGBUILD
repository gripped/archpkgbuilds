# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: GreyXor <greyxor@protonmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Tam <lchris314 at gmail dot com>
# Contributor: Daniel Micay <danielmicay at gmail dotcom>
# Contributor: David Herrmann <dh.herrmann@googlemail dot com>

pkgname=kmscon
pkgver=9.3.1
pkgrel=2
pkgdesc='Kernel Mode Setting (KMS) and DRM based virtual Console Emulator'
arch=('x86_64')
url='https://github.com/kmscon/kmscon'
license=('MIT')
backup=('etc/kmscon/kmscon.conf')
depends=(
  bash
  glibc
  glib2
  libdrm
  libgl
  libtsm
  libxkbcommon
  mesa
  pango
  systemd-libs
)
makedepends=(
  check
  docbook-xsl
  git
  libxslt
  linux-api-headers
  meson
)
optdepends=('inotify-tools: for kmscon-launch-gui waiting mechanism')
source=(
  git+${url}.git#tag=v${pkgver}
)
b2sums=('389418768ed59c5715f3616c62eca5a4fd45253cd93941a976caa7702dcfa6e66388f0e39fcb9f4eb24d87f92e9c69fe0f514ede0eb2c2d035c9eadd9ad5861d')

build() {
  arch-meson "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
