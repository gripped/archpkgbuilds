# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: GreyXor <greyxor@protonmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Tam <lchris314 at gmail dot com>
# Contributor: Daniel Micay <danielmicay at gmail dotcom>
# Contributor: David Herrmann <dh.herrmann@googlemail dot com>

pkgname=kmscon
pkgver=9.3.2
pkgrel=2
pkgdesc='Kernel Mode Setting (KMS) and DRM based virtual Console Emulator'
arch=('x86_64')
url='https://github.com/kmscon/kmscon'
license=('MIT')
backup=('etc/kmscon/kmscon.conf')
depends=(
  bash
  glib2
  glibc
  libdrm
  libtsm
  libxkbcommon
  systemd-libs
)
makedepends=(
  check
  docbook-xsl
  freetype2
  git
  libxslt
  linux-api-headers
  libglvnd
  meson
  pango
)
optdepends=(
  'fontconfig: for freetype font rendering'
  'inotify-tools: for kmscon-launch-gui waiting mechanism'
  'libglvnd: for accelerated video output'
  'pango: for pango font rendering'
)
source=(
  git+${url}.git#tag=v${pkgver}
)
b2sums=('9543e7c25b2d61d9c54c26a9ffb665b8e1a26bc4a6e9a4087ec895fd5409ad582a1368806f775f718788e06dc66e40c872eae491b00cac7b4fa8587d3e9bf35f')

prepare() {
  cd "$pkgname"
  git pull origin pull/287/head         
}

build() {
  arch-meson "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
