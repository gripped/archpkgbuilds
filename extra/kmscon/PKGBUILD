# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: GreyXor <greyxor@protonmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Tam <lchris314 at gmail dot com>
# Contributor: Daniel Micay <danielmicay at gmail dotcom>
# Contributor: David Herrmann <dh.herrmann@googlemail dot com>

pkgname=kmscon
pkgver=9.3.0
pkgrel=2
pkgdesc='Kernel Mode Setting (KMS) and DRM based virtual Console Emulator'
arch=('x86_64')
url='https://github.com/kmscon/kmscon'
license=('MIT')
backup=('etc/kmscon/kmscon.conf')
depends=(
  bash
  glibc
  glib2
  libdrm
  libgl
  libtsm
  libxkbcommon
  mesa
  pango
  systemd-libs
)
makedepends=(
  check
  docbook-xsl
  git
  libxslt
  linux-api-headers
  meson)
source=(
  git+${url}.git#tag=v${pkgver}
)
b2sums=('81f905e053e941ddb1fa37a63ce815865c4f0a7fa96f2f0c7c74b683a9d98527e32a12d412ce97698aa51be497848496aefcc5da2d3bbb3b99df6d44c60ac76b')

build() {
  arch-meson "$pkgname" build
  meson compile -C build 
}

package() {
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
