# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=altair
pkgname=python-$_name
pkgver=5.5.0
pkgrel=2
pkgdesc="Declarative statistical visualization library for Python"
arch=(any)
url="https://altair-viz.github.io/"
license=(BSD-3-Clause)
depends=(
  python
  python-jinja
  python-jsonschema
  python-narwhals
  python-packaging
  python-typing_extensions
)
makedepends=(
  python-build
  python-hatchling
  python-installer
  python-wheel
)
checkdepends=(
  ipython
  python-anywidget
  python-numpy
  python-pandas
  python-polars
  python-pytest
  python-pytest-xdist
)
optdepends=(
  'python-anywidget: for using JupyterChart widget'
  'python-numpy: for NumPy and Pandas imports'
  'python-pandas: for Pandas imports'
  'python-polars: for Polars imports'
  'python-pyarrow: for PyArrow imports'
  'python-vl-convert: PNG and SVG export via Vega-Lite specifications'
)
source=($_name-$pkgver.tar.gz::https://github.com/vega/altair/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('104fc70ad3d4af93bf0ed52c24468342ff9acee87091b3c23de68f84c29ea9f3b44fae641bd90b368add05f22b6cf3e0811308729276c31742677977c3de2d96')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    -m "not slow"
    # skip test that requires duckdb
    --ignore tests/vegalite/v5/test_api.py
    # skip tests that require vega_datasets
    --deselect tests/test_examples.py
    --deselect tests/vegalite/v5/test_params.py
    --ignore tests/test_jupyter_chart.py
    --ignore tests/test_transformed_data.py
    --ignore tests/utils/test_schemapi.py
    # skip test that requires vl_convert
    --ignore tests/test_toplevel.py
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
