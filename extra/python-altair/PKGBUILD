# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=altair
pkgname=python-$_name
pkgver=6.0.0
pkgrel=2
pkgdesc="Declarative statistical visualization library for Python"
arch=(any)
url="https://altair-viz.github.io/"
license=(BSD-3-Clause)
depends=(
  python
  python-jinja
  python-jsonschema
  python-narwhals
  python-packaging
  python-typing_extensions
)
makedepends=(
  python-build
  python-hatchling
  python-installer
  python-wheel
)
checkdepends=(
  ipython
  python-anywidget
  python-mistune
  python-numpy
  python-pandas
  python-polars
  python-pytest
  python-pytest-xdist
  python-vl-convert
)
optdepends=(
  'python-anywidget: for using JupyterChart widget'
  'python-numpy: for NumPy and Pandas imports'
  'python-pandas: for Pandas imports'
  'python-polars: for Polars imports'
  'python-pyarrow: for PyArrow imports'
  'python-vl-convert: PNG and SVG export via Vega-Lite specifications'
)
source=($_name-$pkgver.tar.gz::https://github.com/vega/altair/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('5c1fc3364e3901d8cc5fe442510d41fbeb5f161e0e595605ccc22b2d3e0cac6599e1d7f13530ebecda0e1e4edbf246242596ff13ebb94d6a4c20a10bed3a7708')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -W ignore::DeprecationWarning
    -m "not slow"
    # skip dataset tests (remote fetching from vega-datasets)
    --deselect tests/test_datasets.py
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
