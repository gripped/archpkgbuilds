# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=prometheus-systemd-exporter
_pkgname=systemd_exporter
pkgver=0.7.0
pkgrel=1
pkgdesc="Application-level metrics for systemd units"
url="https://github.com/prometheus-community/systemd_exporter"
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/prometheus-community/systemd_exporter/archive/v${pkgver}.tar.gz"
        prometheus-systemd-exporter.conf
        prometheus-systemd-exporter.service
        prometheus-systemd-exporter.sysusers)
sha256sums=('666a26db03844f11504d36ff08526b25155c5239beed9fabd0d9cc96433ea89a'
            '24c91b7bb90cb65bf5edddc93cce810f57f2e06f265cef955303325526508f23'
            '9230d7a3092418da27e0690387700a8d1c143bcc1f1666657c62548be5bd169a'
            'e6a79e1d65cb2b4512c4bbe341a7455111fc585d6f329bff48d97a1ca3d3df59')

build() {
  cd "${_pkgname}-${pkgver}"

  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
      -X github.com/prometheus/common/version.Version=$pkgver \
      -X github.com/prometheus/common/version.Revision=$pkgver \
      -X github.com/prometheus/common/version.Branch=tarball \
      -X github.com/prometheus/common/version.BuildUser=someone@builder \
      -X github.com/prometheus/common/version.BuildDate=$(date --date "@${SOURCE_DATE_EPOCH}" "+%Y%m%d%H%M%S")" \
    .
}

check() {
  cd "${_pkgname}-${pkgver}"

  go test ./...
}

package() {
  install -Dm644 prometheus-systemd-exporter.conf "$pkgdir"/etc/conf.d/prometheus-systemd-exporter
  install -Dm644 prometheus-systemd-exporter.service "$pkgdir"/usr/lib/systemd/system/prometheus-systemd-exporter.service
  install -Dm644 prometheus-systemd-exporter.sysusers "$pkgdir"/usr/lib/sysusers.d/prometheus-systemd-exporter.conf
  
  cd "${_pkgname}-${pkgver}"

  install -Dm755 systemd_exporter "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 LICENSE  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
