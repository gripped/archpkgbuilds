# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

_pyname=json-stream-rs-tokenizer
pkgname=python-${_pyname}
pkgver=0.4.25
pkgrel=1
pkgdesc='Faster tokenizer for the json-stream Python library'
arch=(x86_64)
url="https://github.com/smheidrich/py-json-stream-rs-tokenizer"
license=('MIT')
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools-rust
  python-wheel
)
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('6382f2f71a2ef17cef1cc85165d770a3e09360fe211576824a14089f7cb0e422')
b2sums=('6d0c1f63b25626b54e9a531cf885672f3c59f7fa23ba0a6c44f9e719c961eb8d176a5a0604e6e4141034e9ffbbdde7a142f27261cfe8786173c7ea6ddc4eb1eb')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_pyname}-${pkgver}
  python -m installer --destdir=test_dir dist/*.whl

  PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" python -c 'import json_stream_rs_tokenizer'
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
