# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

_pyname=json-stream-rs-tokenizer
pkgname=python-${_pyname}
pkgver=0.4.27
pkgrel=3
pkgdesc='Faster tokenizer for the json-stream Python library'
arch=(x86_64)
url="https://github.com/smheidrich/py-json-stream-rs-tokenizer"
license=('MIT')
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools-rust
  python-wheel
)
source=("https://github.com/smheidrich/py-json-stream-rs-tokenizer/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('b3aff10a52fe64517ef0dd1260f2f31db9ea395cfb05e1b82996bfb087823c7e')
b2sums=('20af74125da5b69bd703a9e3d5f99401ea29c3abd9407c2161d0250b5d9f827cf92fb33b33d5ab5e4fab1fb82d55ec868ae8c4129b4a85382060f63bf7ac3a8f')

build() {
  cd "py-${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd "py-${_pyname}-${pkgver}"
  python -m installer --destdir=test_dir dist/*.whl

  PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" python -c 'import json_stream_rs_tokenizer'
}

package() {
  cd "py-${_pyname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
