# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

_pyname=json-stream-rs-tokenizer
pkgname=python-${_pyname}
pkgver=0.4.22
pkgrel=1
pkgdesc='Faster tokenizer for the json-stream Python library'
arch=(x86_64)
url="https://github.com/smheidrich/py-json-stream-rs-tokenizer"
license=('MIT')
depends=('python')
makedepends=('python-setuptools-rust')
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
b2sums=('6946e653924716dfe6501ac3d2227122a2beb382354c838d1d335dc22568d7c594b5c008cf82d8d4f5caa95ab5c7e3a15f03b262de5b2097d9c9db0c30be4321')

build() {
  cd "${_pyname}-${pkgver}"
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-311:$PYTHONPATH" python -c 'import json_stream_rs_tokenizer'
}

package() {
  cd "${_pyname}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
