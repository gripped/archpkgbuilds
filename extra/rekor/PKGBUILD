# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=rekor
pkgver=1.3.7
pkgrel=1
pkgdesc="Signature Transparency Log -- Sigstore client and server tools"
arch=('x86_64')
url="https://github.com/sigstore/rekor"
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'git'
  'go'
)
checkdepends=('openssh')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/rekor/archive/v${pkgver}.tar.gz")
sha256sums=('7cc507cbe416677136cfc34012d7ada7d55371e4a6417f4bfe5ba4966f12e841')
b2sums=('dfd422a0d370ae16987ca4a4b492b1b4edcf21da7bd734a610b3cabd8d8947602bf5f07cdc7f59b9dcb86d2654f67b2b950e2683e1136cabe3096a8f64515579')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false -linkmode=external -X=sigs.k8s.io/release-utils/version.gitVersion=v${pkgver}" \
    -o bin/ \
    ./cmd/...
}

check() {
  cd "${pkgname}-${pkgver}"
  go test -v ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 bin/rekor-* -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  "${pkgdir}/usr/bin/rekor-cli" completion bash > "${pkgdir}/usr/share/bash-completion/completions/rekor-cli"
  "${pkgdir}/usr/bin/rekor-cli" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_rekor-cli"
  "${pkgdir}/usr/bin/rekor-cli" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/rekor-cli.fish"

  "${pkgdir}/usr/bin/rekor-server" completion bash > "${pkgdir}/usr/share/bash-completion/completions/rekor-server"
  "${pkgdir}/usr/bin/rekor-server" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_rekor-server"
  "${pkgdir}/usr/bin/rekor-server" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/rekor-server.fish"
}

# vim: ts=2 sw=2 et:
