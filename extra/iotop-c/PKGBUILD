# Maintainer: Chistian Heusel <gromit@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Rumen Jekov <rvjekov@gmail.com>
# Contributor: Boian Bonev <bbonev@ipacct.com>

pkgname=iotop-c
_pkgname=iotop
pkgver=1.27
pkgrel=1
pkgdesc="A top utility for IO"
url="https://github.com/Tomas-M/iotop"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('glibc' 'ncurses')
makedepends=('git')
conflicts=('iotop')
provides=('iotop')
source=("git+${url}.git#tag=v${pkgver}?signed"
        'Makefile.patch')
validpgpkeys=('BA60BC20F37E59444D6D25001365720913D2F22D')
sha256sums=('252b6be613dd0d5c6167cafdb74f07b960442665c083ee01e755bf0b908d709c'
            '53765edb625d544728fdd1dbefb58915b99f6e06827e0e29c3e76bc9393a4a2f')

prepare() {
	cd "${_pkgname}"
	# Use $(PREFIX)/bin rather than $(PREFIX)/sbin
	# and do not strip binary in Makefile
	patch -Np1 -i "${srcdir}/Makefile.patch"
}

build() {
	cd "${_pkgname}"
	make
}

package() {
	cd "${_pkgname}"
	make DESTDIR="${pkgdir}" install
}
