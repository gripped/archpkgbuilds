# Maintainer: Chistian Heusel <gromit@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Rumen Jekov <rvjekov@gmail.com>
# Contributor: Boian Bonev <bbonev@ipacct.com>

pkgname=iotop-c
_pkgname=iotop
pkgver=1.31
pkgrel=1
pkgdesc="A top utility for IO"
url="https://github.com/Tomas-M/iotop"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('glibc' 'ncurses')
makedepends=('git')
conflicts=('iotop')
provides=('iotop')
source=("git+${url}.git#tag=v${pkgver}?signed")
validpgpkeys=('BA60BC20F37E59444D6D25001365720913D2F22D')
sha256sums=('1e58750a89c5977360cc2c1b4330b00094e4da5f1262e0518e5bcf4d3a7e0f5a')

build() {
	cd "${_pkgname}"
	make
}

package() {
	cd "${_pkgname}"
	make STRIP=: DESTDIR="${pkgdir}" BINDIR="${pkgdir}/usr/bin" install
}
