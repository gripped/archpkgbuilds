# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-fabulous
pkgver=0.4.0
pkgrel=6
pkgdesc='Print images, colors, and stylish text to the terminal with Python'
arch=(any)
url='https://github.com/jart/fabulous'
license=(Apache-2.0)
depends=(
  python
  python-pillow
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("git+https://github.com/jart/fabulous.git#tag=$pkgver")
b2sums=(feca83ce2a52a754a818ed2b09a6098c6606bbaa81b293358640fb444cafa89769aa31595c316ae49b5dc3f63d3e2e54613c60e3f7cad292343effc7a1c08bab)

build() {
  cd fabulous
  python -m build --wheel --no-isolation
}

package() {
  cd fabulous
  python -m installer --destdir="$pkgdir" dist/*.whl
}
