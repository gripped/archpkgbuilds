# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-sphinx-autoapi
_name=${pkgname#python-}
pkgver=3.1.2
pkgrel=1
pkgdesc="A new approach to API documentation in Sphinx"
arch=(any)
url=https://github.com/readthedocs/sphinx-autoapi
license=(MIT)
depends=(
  python-astroid
  python-jinja
  python-sphinx
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-beautifulsoup4
  python-pytest
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('145e61dd8b9017c35adfb6ff573d42c2aebc859dfbc0b4816f60aa1d55afc49fc00e4fb41e8ce791b9edb6a193f4f7cd41a1f478e7b51c0e942f00ca2e8622d1')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  PYTHONPATH="$PWD/autoapi:$PYTHONPATH" pytest -v
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"${_name//-/_}"-$pkgver.dist-info/LICENSE.rst \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
