# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil
pkgver=2.2.5
pkgrel=2
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='http://ddcutil.com/'
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('gcc-libs' 'glibc' 'glib2' 'i2c-tools' 'libusb' 'libdrm' 'jansson' 'libx11' 'libxext' 'libxrandr' 'systemd-libs')
makedepends=('git' 'systemd')
source=(git+https://github.com/rockowitz/ddcutil.git#tag=v$pkgver)
sha512sums=('9b610790a8fba7273c1f9870b6f494de5b3b45be9b189667df5d20e3224119db1177f844af6a108ee2b17dbc752861500ddfdd7ed445e5296286771a36adaa07')

prepare() {
  cd ddcutil
  git revert -n 642ddd5785e034eeae51db00a1d1402412471f43 316fcb977070f6161fd5b2779f7f2c8f6dca8593
  autoreconf -fiv
}

build() {
  cd ddcutil
  ./configure --prefix=/usr --disable-build-timestamp
  make
}

package() {
  cd ddcutil
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
