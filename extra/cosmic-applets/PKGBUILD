# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-applets
pkgver=1.0.0.alpha.5
pkgrel=1
pkgdesc='Applets for COSMIC Panel'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applets
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  dbus
  gcc-libs
  glibc
  libinput
  libpulse
  libxkbcommon
  systemd-libs
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=edcf29df9e38fdffc39bd67ee3aed0bbf648c7a6
source=(git+https://github.com/pop-os/cosmic-applets.git#tag=${_tag})
b2sums=('7ab9345fa8ae742354013c8a8c72cc19a89023fb3dca59b34ee9351035ef774aa3c60629bb90a89d32a414c8ae9908ec156a95f335f8d56a5c1c71a8d9eba2cb')

prepare() {
  cd cosmic-applets
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

pkgver() {
  cd cosmic-applets
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applets
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applets
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
