# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-applets
pkgver=1.0.0.alpha.3
pkgrel=1
pkgdesc='Applets for COSMIC Panel'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applets
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  dbus
  gcc-libs
  glibc
  libinput
  libpulse
  libxkbcommon
  systemd-libs
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=11faa567f32e255444682b011d3196b15addeb8d
source=(git+https://github.com/pop-os/cosmic-applets.git#tag=${_tag})
b2sums=('31b210acbbccf03f086f59aa1954e6f3dab457fbbc09b3502bc26330293082d3a3769c1c5cb9ffc0fab468d86cef7d0dfb79c45f36cc885638fc26e40515e9d9')

prepare() {
  cd cosmic-applets
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

pkgver() {
  cd cosmic-applets
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applets
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applets
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
