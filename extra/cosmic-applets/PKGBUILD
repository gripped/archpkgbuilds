# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-applets
pkgver=1.0.3
pkgrel=1
epoch=1
pkgdesc='Applets for COSMIC Panel'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applets
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  dbus
  gcc-libs
  glibc
  libinput
  libpipewire
  libpulse
  libxkbcommon
  systemd-libs
  util-linux
)
makedepends=(
  cargo
  clang
  git
  just
  mold
)
source=(git+https://github.com/pop-os/cosmic-applets.git#tag=epoch-${pkgver})
b2sums=('dbaf2a325108793e85c5d2d8920b9b3cb8a8324df257101e29cb9e512efbb764ade4e6336da48afb1a3652266afb525e78df751b5d972ceb44d2fc83ec5b834c')

prepare() {
  cd cosmic-applets
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

build() {
  cd cosmic-applets
  RUSTFLAGS+=" -C link-arg=-fuse-ld=mold"
  just build-release --frozen
}

package() {
  cd cosmic-applets
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
