# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-applets
pkgver=1.0.6
pkgrel=1
epoch=1
pkgdesc='Applets for COSMIC Panel'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applets
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  dbus
  gcc-libs
  glibc
  libinput
  libpipewire
  libpulse
  libxkbcommon
  systemd-libs
  util-linux
)
makedepends=(
  cargo
  clang
  git
  just
  mold
)
source=(git+https://github.com/pop-os/cosmic-applets.git#tag=epoch-${pkgver})
b2sums=('885a117ff3351600c5b4e972ad18c92fa2ee3f1eb939320bf130028c97e83928ab22f6a06e288957529ff8630fa87c89528cff422a266d874c0c6c9ca0dcffa0')

prepare() {
  cd cosmic-applets
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

build() {
  cd cosmic-applets
  RUSTFLAGS+=" -C link-arg=-fuse-ld=mold"
  just build-release --frozen
}

package() {
  cd cosmic-applets
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
