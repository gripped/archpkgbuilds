# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-applets
pkgver=1.0.0.alpha.5.1
pkgrel=1
pkgdesc='Applets for COSMIC Panel'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applets
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  dbus
  gcc-libs
  glibc
  libinput
  libpulse
  libxkbcommon
  systemd-libs
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=2c79ca44e2da521c3f5987da1fd15cd8e455832c
source=(git+https://github.com/pop-os/cosmic-applets.git#tag=${_tag})
b2sums=('cce4e277f9849dd638dcc8127a0e1efacb8d9069bee6d91ec327f59b94a6ca7a253a05bcc58efa4bbfbb4723028820174c279872ee4a7aee52ebf6621118c342')

prepare() {
  cd cosmic-applets
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

pkgver() {
  cd cosmic-applets
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applets
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applets
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
