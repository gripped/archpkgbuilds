# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-applets
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='Applets for COSMIC Panel'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applets
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  dbus
  gcc-libs
  glibc
  libinput
  libpulse
  libxkbcommon
  systemd-libs
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=3670f124fb51ff730c470398014a38ee166e2dbb
source=(git+https://github.com/pop-os/cosmic-applets.git#tag=${_tag})
b2sums=('6d0bad257e0d06e72b2488ed3d3fffeb61e9e2795b6ae81b48a50e8683f77ccc49ccd1b86c2c283364fac6dea2938ed8d8d60e87ca7da91803fbf87458978226')

prepare() {
  cd cosmic-applets
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

pkgver() {
  cd cosmic-applets
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applets
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applets
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
