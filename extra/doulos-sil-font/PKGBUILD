# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=doulos-sil-font
_fname=DoulosSIL
pkgname=ttf-${pkgbase%-font}
pkgver=7.000
pkgrel=1
pkgdesc='Complete support for the International Phonetic Alphabet'
arch=(any)
url="https://software.sil.org/${pkgbase/-*}"
license=(OFL-1.1-RFN)
_archive="$_fname-$pkgver"
source=("https://software.sil.org/downloads/r/${pkgbase%%-*}/$_archive.zip")
sha256sums=('3d63296575f1f834f785f4a9426a9da6bc3b9083fcc304a5490704f4398bf639')

package_ttf-doulos-sil() {
	cd "$_archive"
	replaces=(ttf-sil-doulos)
	provides=("$pkgbase=$pkgver" ttf-sil-doulos)
	install -Dm0644 -t "$pkgdir/usr/share/fonts/TTF/" *.ttf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" OFL.txt
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.txt FONTLOG.txt documentation/pdf/*
}
