# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: GreyXor <greyxor@protonmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: David Herrmann <dh.herrmann@gmail.com>

pkgname=libtsm
pkgver=4.4.2
pkgrel=1
pkgdesc='Terminal-emulator State Machine'
arch=('x86_64')
url='https://github.com/kmscon/libtsm'
license=('MIT')
depends=('glibc')
makedepends=(
  check
  git
  meson
  libxkbcommon
)
options=(!libtool)
source=(git+${url}.git#tag=v$pkgver)
b2sums=('ae692f980b3eeebf6a176ccf7aa2737be8f30e880b285974be397b25e554e7f1fb9271e900dba10bf01925e06951662279625888398d99c80ee5e6f42d32713c')

build() {
  arch-meson "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
