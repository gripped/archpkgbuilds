# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: GreyXor <greyxor@protonmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: David Herrmann <dh.herrmann@gmail.com>

pkgname=libtsm
pkgver=4.4.1
pkgrel=1
pkgdesc='Terminal-emulator State Machine'
arch=('x86_64')
url='https://github.com/kmscon/libtsm'
license=('MIT')
depends=('glibc')
makedepends=(
  check
  git
  meson
  libxkbcommon
)
options=(!libtool)
source=(git+${url}.git#tag=v$pkgver)
b2sums=('5dd33c89f246e6e887146ac0d104e4a5ecf657595df012d7f08f73b7eab5688aee9378ddb97454f1d9eb95eada5c430ec4d708c0c797daec7913501a2b34d4b1')

build() {
  arch-meson "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
