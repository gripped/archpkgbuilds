# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=difftastic
pkgver=0.67.0
pkgrel=2
pkgdesc='An experimental diff tool that compares files based on their syntax'
arch=(x86_64)
url='https://difftastic.wilfred.me.uk'
license=(MIT)
depends=(glibc # libc.so
         libgcc libgcc_s.so
         libstdc++ libstdc++.so
         shared-mime-info)
makedepends=(cargo git mdbook)
source=(
  "git+https://github.com/Wilfred/difftastic.git#tag=$pkgver"
  remove-makedepends-jq.patch
)
sha512sums=('8c39f1247c51f5c33f9f7f1fac9c951664bc8efc152fb1cb90454fb6d6082c70480be8b26c96866cda72beb04e4fb120796db2b156762be855879024a23891d5'
            '641ad5c9edeaca58b70c1929c7a31df8a1ef666e9810465954a41cb5bed949cd5d50771407d3f1ef5e0fbee63cd4678fe2c3eefcae765e37a97eea7e3777b2de')
b2sums=('18f3d5d10a1dec82b950f54dfcf790ea107d5536852c27907b266c00041d55031534eab8cd609e75fb1c169493c9db0d151670d67f552b92d2ed674c5e845767'
        '467d5fc634b3f1d8fa7fe0c8de843e615b6bbfed340ad5d4167d6d5d374185bcb8d54f31f43995beb43966f8d49497956d4a30186ccb974b2cadc01357c9c2bc')
#LTO cause undefined symbols
options=(!lto)

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target host-tuple

  # remove makedepends on jq
  patch -p1 -i "$srcdir/remove-makedepends-jq.patch"
}

build() {
  cd "$pkgname"
  export CFLAGS+=" -ffat-lto-objects"

  # binary
  cargo build --frozen --release

  # documentation
  cd manual
  sed -i "s/DFT_VERSION_HERE/$pkgver/g" -i src/introduction.md
  mdbook build
}

check() {
  cd "$pkgname"

  # Skip tests that expect to detect interactive terminal
  # Run ignored tests that *are* expected to work if MIME database present
  # c.f. https://github.com/Wilfred/difftastic/commit/34f21c6d
  cargo test --frozen --all-features -- \
    --ignored \
    --skip 'options::tests::test_detect_display_width'
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/difft

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr manual/book "$pkgdir/usr/share/doc/$pkgname/manual"
  install -vDm644 -t "$pkgdir/usr/share/man/man1" difft.1

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
