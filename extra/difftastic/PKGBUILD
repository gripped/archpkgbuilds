# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=difftastic
pkgver=0.65.0
pkgrel=1
pkgdesc='An experimental diff tool that compares files based on their syntax'
arch=(x86_64)
url='https://difftastic.wilfred.me.uk'
license=(MIT)
depends=(glibc gcc-libs shared-mime-info)
makedepends=(cargo git mdbook)
source=(
  "git+https://github.com/Wilfred/difftastic.git#tag=$pkgver"
  remove-makedepends-jq.patch
)
sha512sums=('21b038d1903a5ebd1567405eb5577bc84d2d49cf93870a76728435444aae30f5136d9141cf17924a79659152ea7764dc7e2fb5c0479f1d484b80df772ff36c86'
            '641ad5c9edeaca58b70c1929c7a31df8a1ef666e9810465954a41cb5bed949cd5d50771407d3f1ef5e0fbee63cd4678fe2c3eefcae765e37a97eea7e3777b2de')
b2sums=('eaa79805ebd2ecac096b1600318d79da7551477f4f04a4bb91457efa8a0103bdf3562942e62940d3acee104f5f7258636a817b780c2fcb10d8e9f806ee62749c'
        '467d5fc634b3f1d8fa7fe0c8de843e615b6bbfed340ad5d4167d6d5d374185bcb8d54f31f43995beb43966f8d49497956d4a30186ccb974b2cadc01357c9c2bc')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # remove makedepends on jq
  patch -p1 -i "$srcdir/remove-makedepends-jq.patch"
}

build() {
  cd "$pkgname"
  export CFLAGS+=" -ffat-lto-objects"

  # binary
  cargo build --frozen --release

  # documentation
  cd manual
  sed -i "s/DFT_VERSION_HERE/$pkgver/g" -i src/introduction.md
  mdbook build
}

check() {
  cd "$pkgname"

  # Skip tests that expect to detect interactive terminal
  # Run ignored tests that *are* expected to work if MIME database present
  # c.f. https://github.com/Wilfred/difftastic/commit/34f21c6d
  cargo test --frozen --all-features -- \
    --ignored \
    --skip 'options::tests::test_detect_display_width'
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/difft

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr manual/book "$pkgdir/usr/share/doc/$pkgname/manual"
  install -vDm644 -t "$pkgdir/usr/share/man/man1" difft.1

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
