# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=difftastic
pkgver=0.64.0
pkgrel=2
pkgdesc='An experimental diff tool that compares files based on their syntax'
arch=('x86_64')
url='https://difftastic.wilfred.me.uk'
license=('MIT')
depends=('glibc' 'gcc-libs' 'shared-mime-info')
makedepends=('cargo' 'git' 'mdbook')
source=(
  "git+https://github.com/Wilfred/difftastic.git#tag=$pkgver"
  'remove-makedepends-jq.patch'
)
sha512sums=('78fa3096b6b4a3c1f0f5666462a6a3880bbd8c76100154d2db8343256e0064ef4f2577438849974d49ec85a425fd3817de55809cac150416f25df77c0c669528'
            '641ad5c9edeaca58b70c1929c7a31df8a1ef666e9810465954a41cb5bed949cd5d50771407d3f1ef5e0fbee63cd4678fe2c3eefcae765e37a97eea7e3777b2de')
b2sums=('9aab103f77c131b1f6f7d31b5af0bc78b789691e8749173740888dfa47d77b53b2f72f27d595afe8a7b5839b3aebc47f66141daa02fbae72e1a567b6363c0057'
        '467d5fc634b3f1d8fa7fe0c8de843e615b6bbfed340ad5d4167d6d5d374185bcb8d54f31f43995beb43966f8d49497956d4a30186ccb974b2cadc01357c9c2bc')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # remove makedepends on jq
  patch -p1 -i "$srcdir/remove-makedepends-jq.patch"
}

build() {
  cd "$pkgname"
  export CFLAGS+=" -ffat-lto-objects"

  # binary
  cargo build --frozen --release --all-features

  # documentation
  cd manual
  sed -i "s/DFT_VERSION_HERE/$pkgver/g" -i src/introduction.md
  mdbook build
}

check() {
  cd "$pkgname"

  # Skip tests that expect to detect interactive terminal
  # Run ignored tests that *are* expected to work if MIME database present
  # c.f. https://github.com/Wilfred/difftastic/commit/34f21c6d
  cargo test --frozen --all-features -- \
    --ignored \
    --skip 'options::tests::test_detect_display_width'
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/difft

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr manual/book "$pkgdir/usr/share/doc/$pkgname/manual"
  install -vDm644 -t "$pkgdir/usr/share/man/man1" difft.1

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
