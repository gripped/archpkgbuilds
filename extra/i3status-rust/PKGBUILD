# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=i3status-rust
pkgver=0.34.0
pkgrel=2
pkgdesc='Resourcefriendly and feature-rich replacement for i3status, written in pure Rust'
arch=('x86_64')
url='https://github.com/greshake/i3status-rust'
license=('GPL-3.0-only')
depends=(
  'libpipewire'
  'libpulse'
  'lm_sensors'
)
makedepends=(
  'clang'
  'rust'
  'pandoc'
)
optdepends=('alsa-utils: for the volume block'
            'bluez: for the bluetooth block'
            'fakeroot: for the pacman block to show pending updates'
            'kdeconnect: for the kdeconnect block'
            'powerline-fonts: for all themes using the Powerline arrow char'
            'pulseaudio: for the volume block'
            'speedtest-cli: for the speedtest block'
            'otf-font-awesome: for the awesome icons'
            'upower: for the battery block')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/greshake/i3status-rust/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('ff89f9f6db6021166a78bfd04d89ebf0ccf525916f0295f1b7dc25bc3a28502b54d8e2354dccf7d8512a0dcc71ce4a1f2a12d433b0b53111b595a7b9a0726cb0')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}


build() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --features 'pulseaudio maildir pipewire'
  cargo xtask generate-manpage
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}"/usr/bin target/release/i3status-rs
  install -Dm644 -t "${pkgdir}"/usr/share/man/man1 man/i3status-rs.1

  for icon in files/icons/*.toml; do
    install -Dm644 -t "${pkgdir}"/usr/share/${pkgname}/icons "${icon}"
  done

  for theme in files/themes/*.toml; do
    install -Dm644 -t "${pkgdir}"/usr/share/${pkgname}/themes "${theme}"
  done

  for example in examples/*.toml; do
    install -Dm644 -t "${pkgdir}"/usr/share/doc/${pkgname}/examples/ ${example}
  done
}
