# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=prometheus-snmp-exporter
_pkgname=snmp_exporter
pkgver=0.27.0
pkgrel=1
pkgdesc='Prometheus exporter for SNMP metrics'
arch=('x86_64')
url='https://github.com/prometheus/snmp_exporter'
license=('Apache-2.0')
depends=('glibc' 'net-snmp')
makedepends=('git' 'go')
backup=("etc/conf.d/$pkgname")
install='package.install'
options=('!lto')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'systemd.service'
  'sysusers.conf'
  'config.env'
)
sha512sums=('9886139a8eade750974e17ac718120089ff6cefe2cbcb9d87702bd31a7ea56ba8af255b81f5873cc650ca45d09a534e6c37aaa8882237b4d8a2d8f32179cca5a'
            '9ca53c18fe84d49cf283a402dc3162d0e6a2045f7030a7c05b9d31a39cc16e86557d49361d985e4993993cf59acbe65d12ebedeebdff87c87b647f49499ec321'
            '845bd76b3284b13673d35dde5a2068f48e391baf99baf2287ad1eb65f35c34d3619962992538dd4d01e84e0d78deb7e7ed029b6e89f3db4772e6c232959e4050'
            '3cac11e6c0935b2c123a4ed85791fbab4585d35820e89baa927883619a03c3925876bdc5e385ac7df48f7adec8ee1082aaa347bce2ad6c237f7401eba58e2905')
b2sums=('95b6c8810e6c3535c3e4cbfa3f99aefccc21510505d3f24cf9e2c99ff6a02184d85c5d70471ea782032a1536f110309c346d35bbec06ac90871d2120d53b7b50'
        'ac862a1fecc719c04684836b0c0e0aaff93912ad09f676555fc3212dcb8fa93bd4569550247fb0aaca9e7083e6d4707ebd7f6332d28ab14ba0774625fc176737'
        'e3ee4223014a9db6ba74b45d3e2a5e7fe32bf2628289245a7864d2a5118647de851314c877a4148f9693eca9722a025ed92b23f8389694b196e0af671f618467'
        '88d48f0485a485a7840f9cc9aa6c5c3a0de2c28542bd77f16fa181772532287375e214f214bf925c30705a60ee48ebf15ac6ca707f58a7b3ed74984afb22aded')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X github.com/prometheus/common/version.Version=$pkgver \
    -X github.com/prometheus/common/version.Revision=$pkgver \
    -X github.com/prometheus/common/version.Branch=main \
    -X github.com/prometheus/common/version.BuildUser=archlinux \
    -X github.com/prometheus/common/version.BuildDate=$(date -d@"$SOURCE_DATE_EPOCH" +%Y%m%d-%H:%M:%S)" \
    -o build \
    ./generator .
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm644 config.env "$pkgdir/etc/conf.d/$pkgname"

  cd "$pkgname"

  # binaries
  install -vDm755 "build/$_pkgname" "$pkgdir/usr/bin/$pkgname"
  install -vDm755 build/generator "$pkgdir/usr/bin/${pkgname%exporter}generator"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md auth-split-migration.md
  install -vDm644 snmp.yml "$pkgdir/usr/share/doc/$pkgname/snmp_example.yml"
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/generator" generator/{README.md,generator.yml}

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
