# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaidan
pkgver=0.11.0
pkgrel=3
pkgdesc='A user-friendly XMPP client for every device'
arch=(x86_64)
url='https://www.kaidan.im/'
license=(GPL-3.0-or-later
         MIT)
depends=(gcc-libs
         glibc
         icu
         kcoreaddons
         kcrash
         kio
         kirigami-addons
         kirigami
         knotifications
         kquickimageeditor
         libomemo-c
         prison
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-location
         qt6-multimedia
         qt6-positioning
         qt6-svg
         qxmpp)
makedepends=(extra-cmake-modules
             qt6-tools)
source=(https://download.kde.org/unstable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3feef43d66ff5c30ae81894a7343da1856ca84b0177b8a07922f193ca33776a2'
            'SKIP')
validpgpkeys=(AE08C590A7D112C1979D068B04EFAD0F7A4D9724) # Melvin Keskin <melvo@olomono.de>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
