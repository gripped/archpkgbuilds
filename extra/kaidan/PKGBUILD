# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaidan
pkgver=0.10.0
pkgrel=1
pkgdesc='A user-friendly XMPP client for every device'
arch=(x86_64)
url='https://www.kaidan.im/'
license=(GPL-3.0-or-later
         MIT)
depends=(gcc-libs
         glibc
         kcoreaddons5
         kio5
         kirigami-addons5
         kirigami2
         kitemviews5
         knotifications5
         kquickimageeditor5
         libomemo-c
         qqc2-desktop-style5
         qt5-base
         qt5-declarative
         qt5-location
         qt5-multimedia
         qt5-quickcontrols2
         qt5-svg
         qxmpp-qt5
         zxing-cpp)
makedepends=(extra-cmake-modules
             git
             qt5-tools)
source=(git+https://invent.kde.org/network/kaidan#tag=v$pkgver)
sha256sums=('87592096d12e330919ad8975e76a939d1ed3725d5621fa44619a2d51ace4037a')
validpgpkeys=(03C2D10DC97E5B0BEBB8F3B44663231A91A1E27B) # Linus Jahn <lnj@kaidan.im>

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
