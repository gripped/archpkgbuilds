# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaidan
pkgver=0.11.0
pkgrel=1
pkgdesc='A user-friendly XMPP client for every device'
arch=(x86_64)
url='https://www.kaidan.im/'
license=(GPL-3.0-or-later
         MIT)
depends=(gcc-libs
         glibc
         icu
         kcoreaddons
         kcrash
         kio
         kirigami-addons
         kirigami
         knotifications
         kquickimageeditor
         libomemo-c
         prison
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-location
         qt6-multimedia
         qt6-positioning
         qt6-svg
         qxmpp-qt6)
makedepends=(extra-cmake-modules
             git
             qt6-tools)
source=(git+https://invent.kde.org/network/kaidan#tag=v$pkgver)
sha256sums=('562d6ced023beea94307fb94239950bcb89937a0152a0df197c1431b5a132710')
validpgpkeys=(03C2D10DC97E5B0BEBB8F3B44663231A91A1E27B) # Linus Jahn <lnj@kaidan.im>

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
