# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaidan
pkgver=0.13.0
pkgrel=1
pkgdesc='A user-friendly XMPP client for every device'
arch=(x86_64)
url='https://www.kaidan.im/'
license=(GPL-3.0-or-later
         MIT)
depends=(gcc-libs
         glibc
         icu
         kcoreaddons
         kcrash
         kdsingleapplication
         kio
         kirigami-addons
         kirigami
         knotifications
         kquickimageeditor
         kwindowsystem
         libomemo-c
         prison
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-location
         qt6-multimedia
         qt6-positioning
         qt6-svg
         qtkeychain-qt6
         qxmpp)
makedepends=(extra-cmake-modules
             qt6-tools)
source=(https://download.kde.org/unstable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b5b76cb1e78f0edbb07bfc63e972f64ac1a8322c57fe62a0e0e1992653784b20'
            'SKIP')
validpgpkeys=(AE08C590A7D112C1979D068B04EFAD0F7A4D9724) # Melvin Keskin <melvo@olomono.de>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
