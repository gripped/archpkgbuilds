# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=obfuscate
pkgver=0.0.10
pkgrel=2
pkgdesc='Censor private information from any image'
arch=(x86_64)
url='https://apps.gnome.org/Obfuscate/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
source=(
  "git+https://gitlab.gnome.org/World/obfuscate.git#tag=$pkgver"
  obfuscate-dbus-activatable.patch
  obfuscate-new-window.patch
)
b2sums=(
  5b2ff8e5d07f982e94a77e6a8a1092acbe5f01eb8f82780381a48dee8891a9563fb7b93bd8d157693550de1e8b6b0446bbc3497a3e27739e0c96e6a8dd038fd8
  4b55e18e632915522d21f8af1cb1de1eb908f28d9ab3c7a327ad0197aa4a21c5519ab2eefe890fbeb8ca8b3827cca4d051db21e2f011bdacb91540d15a235645
  f3f4409541a31e52229224fa55be3e42f95251f5e776cb32db61dd9dff0eaffdda219296e9f68caa0902e194ac7f357008bbb13085d16204f16e313defb68296
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/obfuscate/-/merge_requests/37
  git cherry-pick -n c1999aef671e68ed52017047715bb178c13190e2

  # https://gitlab.gnome.org/World/obfuscate/-/merge_requests/43
  git apply -3 ../obfuscate-dbus-activatable.patch

  # https://gitlab.gnome.org/World/obfuscate/-/merge_requests/44
  git apply -3 ../obfuscate-new-window.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
