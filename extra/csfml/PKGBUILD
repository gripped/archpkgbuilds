# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=csfml
pkgver=3.0.0.git1
_gitrev=b607308b3f1e58222e3ba70322f94dfa1ad0ce13
pkgrel=2
pkgdesc='C bindings for sfml'
arch=('x86_64')
url='http://www.sfml-dev.org/'
license=('Zlib')
depends=("sfml")
makedepends=('cmake' 'doxygen' 'ninja')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/SFML/CSFML/archive/${_gitrev}.tar.gz")
sha512sums=('ff7b366f46737d85013fbdb81694aba5e628bfb24880eab347761a11ce4ec26212262718b6adb2f3aea14cc3ea0a145c370c95a8a73256c7c2a826bf08792a83')

build() {
  cd CSFML-*

  cmake \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCSFML_BUILD_DOC=true
  ninja -C build
  ninja -C build doc
}

package() {
  cd CSFML-*

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 license.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

