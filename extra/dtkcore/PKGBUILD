# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=5.6.31
pkgrel=2
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'gcc-libs' 'glibc' 'gsettings-qt' 'icu' 'libsystemd'
         'lshw' 'qt5-base' 'uchardet' 'spdlog')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtkcore.git#tag=$pkgver")
sha512sums=('f3643032bbb1958b0d8b03a78e7420c650d84a595c0796afcb566dedc6194394b4e1ae3f97a299a007d20015303033a5551a60bd4bb0a920e39278e236741651')

prepare() {
  cd dtkcore
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtkcore
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DFEATURES_INSTALL_DIR=lib/qt/mkspecs/features/ \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DBUILD_WITH_SYSTEMD=ON \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver

  ninja
}

check() {
  cd dtkcore
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkcore
  DESTDIR="$pkgdir" ninja install
}
