# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=5.7.13
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'dtklog' 'gcc-libs' 'glibc' 'gsettings-qt' 'icu'
         'lshw' 'qt5-base' 'uchardet')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtkcore.git#tag=$pkgver")
sha512sums=('f8827b78550901270913c600b55e5270acae915b3919923f08ddfb869aa31633bcf3710d7123bdfad5ab9a75351f824898206f4f59b67cbb25ce03c3c9861d6f')

prepare() {
  cd dtkcore
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtkcore
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DFEATURES_INSTALL_DIR=lib/qt/mkspecs/features/ \
      -DBUILD_WITH_SYSTEMD=ON \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver

  ninja
}

check() {
  cd dtkcore
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkcore
  DESTDIR="$pkgdir" ninja install
}
