# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=5.7.4
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'dtklog' 'gcc-libs' 'glibc' 'gsettings-qt' 'icu'
         'lshw' 'qt5-base' 'uchardet')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtkcore.git#tag=$pkgver")
sha512sums=('8f60e60a6327aa7a9eebe617201068701275f185bc561e568a2395865f5e9495b854043fe4a830f6126b6435038946848145b803ab38756e51b070f6298ca0fa')

prepare() {
  cd dtkcore
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtkcore
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DFEATURES_INSTALL_DIR=lib/qt/mkspecs/features/ \
      -DBUILD_WITH_SYSTEMD=ON \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver

  ninja
}

check() {
  cd dtkcore
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtkcore
  DESTDIR="$pkgdir" ninja install
}
