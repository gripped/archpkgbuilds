# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-diff
pkgver=2.1.1
pkgrel=1
pkgdesc=" Utility for comparing two OpenAPI specifications"
arch=('any')
url="https://github.com/OpenAPITools/openapi-diff"
license=(Apache-2.0)
depends=(
  bash
  java-runtime=8
)
makedepends=(
  maven
  java-environment=8
)
source=(
  $url/archive/$pkgver/$pkgname-$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('3de64cead6797a092947e39dbeae97b8ca82147e8aab9b40dd6fd451e6e1a0adc26224eacd0eb050e2223c3398b214283a3dac0f07244182c4670ac50aa720c6'
            'e4143cff09abc59958f50a969ed3f010ab39e640cf77081e47e77cf3e2bb785645100572fd18bc508ea441732bc13a5a811332fdbfdbd7f8a0d6865f301f978e')
b2sums=('ac9a9600a19fc307c4f8ab963c4ca5ae2c586e83ef52552fd5e28498400f579d33bd8f8ab9c9d719ff0355f479bdd81aadcf01a3891ccd1790afd359f55f01a7'
        'a28a194b1b5c731fd3093c22754fc7f6641441ae2acb1ad177b8ee573d8e3787af2b249740ffd1e3472f721e0274d0866c0acf1bf5c85f4a22fe1c2dcf24af7b')

build() {
  cd $pkgname-$pkgver
  mvn clean install
}

package() {
  cd $pkgname-$pkgver
  install -vDm 644  cli/target/openapi-diff-cli-$pkgver-all.jar "$pkgdir/usr/share/java/$pkgname/$pkgname-cli.jar"
  install -vDm 755 ../$pkgname.sh "$pkgdir/usr/bin/$pkgname"
  ln -svf $pkgname "$pkgdir/usr/bin/$pkgname-cli"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
