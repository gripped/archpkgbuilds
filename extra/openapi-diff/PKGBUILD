# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-diff
pkgver=2.1.0
pkgrel=1
pkgdesc=" Utility for comparing two OpenAPI specifications"
arch=('any')
url="https://github.com/OpenAPITools/openapi-diff"
license=(Apache-2.0)
depends=(
  bash
  java-runtime=8
)
makedepends=(
  maven
  java-environment=8
)
source=(
  $url/archive/$pkgver/$pkgname-$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('ee1f7083059cab1efefceff94cb69a4ad3488a8f092a98d687c20b0b0214ec5721f10efe8efe2b576bfb98311493057c3b498f938639ee94bc5aea9273864f12'
            'e4143cff09abc59958f50a969ed3f010ab39e640cf77081e47e77cf3e2bb785645100572fd18bc508ea441732bc13a5a811332fdbfdbd7f8a0d6865f301f978e')
b2sums=('1150f5ecfc555a15e0daba4d8f88f8bb6bd2f315adf8a23112c9876d8313f192d37fc431ba50dd9c37e8dfcd5d26f1ba3005f5adb0cf5c1668850e969fc14504'
        'a28a194b1b5c731fd3093c22754fc7f6641441ae2acb1ad177b8ee573d8e3787af2b249740ffd1e3472f721e0274d0866c0acf1bf5c85f4a22fe1c2dcf24af7b')

build() {
  cd $pkgname-$pkgver
  mvn clean install
}

package() {
  cd $pkgname-$pkgver
  install -vDm 644  cli/target/openapi-diff-cli-$pkgver-all.jar "$pkgdir/usr/share/java/$pkgname/$pkgname-cli.jar"
  install -vDm 755 ../$pkgname.sh "$pkgdir/usr/bin/$pkgname"
  ln -svf $pkgname "$pkgdir/usr/bin/$pkgname-cli"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
