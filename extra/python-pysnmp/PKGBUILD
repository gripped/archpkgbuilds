# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname=python-pysnmp
pkgver=7.1.17
pkgrel=1
pkgdesc='Open source and free implementation of v1/v2c/v3 SNMP engine'
arch=('any')
url='https://www.pysnmp.com/'
license=('BSD-2-Clause')
depends=(
  'python'
  'python-cryptography'
  'python-pyasn1'
  'python-pysmi'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-poetry-core'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-asyncio'
)
source=("https://github.com/lextudio/pysnmp/archive/v$pkgver/${pkgname#python-}-v$pkgver.tar.gz")
sha512sums=('83e1fcbd48d0a3d16ab203a4739c8356e5ff9c33b4bc7129a4e805af79f527a06e35e4b9978317c5f43195b35750e4c9bd1814b5061cca487166e4ed5bc433b0')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  mibdump --generate-mib-texts NET-SNMP-EXAMPLES-MIB
  mibdump --generate-mib-texts IF-MIB
  mibdump --generate-mib-texts LEXTUDIO-TEST-MIB
  mibdump --generate-mib-texts CISCO-ENHANCED-IPSEC-FLOW-MIB
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr -t "$pkgdir/usr/share/doc/$pkgname" examples
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.rst
}

# vim: ts=2 sw=2 et:
