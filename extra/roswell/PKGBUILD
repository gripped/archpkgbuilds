# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <bytecurry.software@gmail.com>

pkgname=roswell
pkgver=26.02.116
pkgrel=1
pkgdesc='Environment for Common Lisp development'
arch=(x86_64)
url='https://roswell.github.io'
license=(MIT)
depends=(glibc bash curl)
makedepends=(git)
source=("$pkgname::git+https://github.com/roswell/roswell#tag=v$pkgver")
sha512sums=('1d19f1abb10c1513464093921d5d59857336c95e487f8aa0e940c51f2886fe8782a380517b2f927ad597f961d3cefd29a72baed8a964907e9911891397837231')
b2sums=('95f51dcbc06377b6fe17d860bba43a4d8e529663f6233d8df167c3e6b4ad91de33622608ff897b6f885a0aa50bc504b8c61f3835cc756a752c3447199888bfc6')

build() {
  cd "$pkgname"

  ./bootstrap

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
