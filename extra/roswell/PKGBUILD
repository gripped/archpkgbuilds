# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <bytecurry.software@gmail.com>

pkgname=roswell
pkgver=24.10.115
pkgrel=1
pkgdesc='Environment for Common Lisp development'
arch=('x86_64')
url='https://roswell.github.io'
license=('MIT')
depends=('glibc' 'bash' 'curl')
makedepends=('git')
source=("$pkgname::git+https://github.com/roswell/roswell#tag=v$pkgver")
sha512sums=('4af2e8e55def5626a2b8d4b07877446e7ae55fa4b0d178bd23f3d7a84eb5b1b896cbf3bafbbf291311b6532dcd0ca0105fd50c9c05afefbc8178f609a8e9ffc1')
b2sums=('bbf54d5fc96ef939602fbb383c730fa9a49f221ea5a5e0b85b6a072748794ce17daf1a8396ef964115cecbbb7487b3914c804cc7290be07b60f97007181b2f32')

build() {
  cd "$pkgname"

  ./bootstrap

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
