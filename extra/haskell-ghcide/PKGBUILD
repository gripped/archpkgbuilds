# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghcide
pkgname=haskell-ghcide
pkgver=2.2.0.0
pkgrel=4
pkgdesc="The core of an IDE"
url="https://github.com/haskell/haskell-language-server/tree/master/ghcide#readme"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-glob' 'haskell-aeson' 'haskell-aeson-pretty'
         'haskell-async' 'haskell-base16-bytestring' 'haskell-case-insensitive'
         'haskell-co-log-core' 'haskell-cryptohash-sha1' 'haskell-data-default'
         'haskell-dependent-map' 'haskell-dependent-sum' 'haskell-dlist' 'haskell-enummapset'
         'haskell-extra' 'haskell-fingertree' 'haskell-ghc' 'haskell-ghc-check' 'haskell-ghc-paths'
         'haskell-ghc-trace-events' 'haskell-gitrev' 'haskell-haddock-library' 'haskell-hashable'
         'haskell-hie-bios' 'haskell-hie-compat' 'haskell-hiedb' 'haskell-hls-graph'
         'haskell-hls-plugin-api' 'haskell-implicit-hie-cradle' 'haskell-lens' 'haskell-list-t'
         'haskell-lsp' 'haskell-lsp-types' 'haskell-opentelemetry' 'haskell-optparse-applicative'
         'haskell-parallel' 'haskell-prettyprinter' 'haskell-prettyprinter-ansi-terminal'
         'haskell-random' 'haskell-regex-tdfa' 'haskell-row-types' 'haskell-safe-exceptions'
         'haskell-sorted-list' 'haskell-sqlite-simple' 'haskell-stm-containers' 'haskell-syb'
         'haskell-text-rope' 'haskell-unliftio' 'haskell-unliftio-core'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-fuzzy' 'haskell-ghc-typelits-knownnat'
             'haskell-implicit-hie' 'haskell-lsp-test' 'haskell-monoid-subclasses'
             'haskell-network-uri' 'haskell-shake' 'haskell-tasty' 'haskell-tasty-expected-failure'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-tasty-rerun')
checkdepends=('cabal-install')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        "$pkgname-ghc9.6.4.patch::https://github.com/haskell/haskell-language-server/commit/f4df1aa736f813eddcd6898f33b9f1fe93cc7e29.patch")
sha512sums=('a919f968c81a158086ebc2da870c510e288ea74ee3e3cf0c9e3ccdb3a31cefd71558560a55331af71b78fb2a10d23a4c377a912c8d3bf1fc2cd3e3e54981b0de'
            'd338de55589aa496634b4e132c64a0bf7ca97d4bc2620a8534c33f7f4f2b712ce729083054059734f751100678c2905b93fc6951babc5f3387b90435c95ae996')

prepare() {
  cd $_hkgname-$pkgver
  patch -Np2 -i ../"${pkgname}"-ghc9.6.4.patch
  gen-setup
  uusi -u Diff -u implicit-hie
  find test/data -name \*.cabal -exec uusi --add-options-all=-dynamic {} \;
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fexecutable

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  cabal update
  PATH="$PWD/dist/build/ghcide:$PWD/dist/build/ghcide-test-preprocessor:$PATH" LD_LIBRARY_PATH="$PWD/dist/build" runhaskell Setup test || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
