# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=libdatovka
pkgver=0.7.1
pkgrel=2
pkgdesc="ISDS client library"
arch=('x86_64')
url="https://www.datovka.cz/cs/pages/libdatovka.html"
license=('LGPL-3.0-only')
depends=('libxml2' 'openssl' 'curl' 'expat')
makedepends=('docbook-xsl')
source=(https://datovka.nic.cz/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('c8d5cbc1009a6d98c507a6537664b7d62f31f372cd1635185d285b79e92ebcff')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr \
    --with-docbook-xsl-stylesheets=/usr/share/xml/docbook/xsl-stylesheets-1.79.2 \
    --enable-openssl-backend  # https://gitlab.nic.cz/datovka/datovka/-/issues/599
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
