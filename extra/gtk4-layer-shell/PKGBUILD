# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Shengyu Zhang <la@archlinuxcn.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgdesc='Library to create panels and other desktop components for Wayland'
pkgname=gtk4-layer-shell
pkgver=1.0.3
pkgrel=1
arch=(x86_64)
license=(MIT)
url="https://github.com/wmww/gtk4-layer-shell"
depends=(
    "gtk4"
    "wayland"
)
makedepends=(
    "gtk-doc"
    "gobject-introspection"
    "meson"
    "ninja"
    "valabind"
    "python"
    "luajit"
)
checkdepends=(
    "lua51-lgi"
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('4d669c30b3dbc68ad69ade9752e6ebbe7be132db21a5a4734d42bc09c5481c34')

build() {
    meson setup \
          --prefix=/usr \
          --wrap-mode=nofallback \
          --buildtype=plain \
          -Dtests=true \
          -Ddocs=true \
          -Dintrospection=true \
          -Dvapi=true \
          -Dexamples=true \
          -Dsmoke-tests=false \
          "$pkgname-$pkgver" \
          build
    ninja -C build
}

# See: https://github.com/wmww/gtk4-layer-shell/issues/28
check() {
    meson test -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    install -D -m 644 "$pkgname-$pkgver/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
