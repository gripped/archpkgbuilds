# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.12.27
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL-3.0-or-later')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'qt5-base' 'glib2' 'karchive5' 'kcodecs5'
         'libarchive' 'libzip' 'minizip' 'util-linux-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-compressor.git#tag=$pkgver")
sha512sums=('9292807f60d3390d56b3d6f15072b4194a213cca345293b03deb97faa0f4fd2cec4e885a9e33622c90fed572755f500a62d751a01996287ac4fdc75c854c5b00')

prepare() {
  cd deepin-compressor
  git cherry-pick -n 9e13248e351dd6db49134e91e33a39d671310d8d
}

build() {
  cd deepin-compressor
  cmake . -GNinja -DCMAKE_INSTALL_LIBDIR=/usr/lib
  ninja
}

package() {
  cd deepin-compressor
  DESTDIR="$pkgdir" ninja install
}
