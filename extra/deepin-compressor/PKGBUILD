# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=6.5.0
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL-3.0-or-later')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'qt5-base' 'glib2' 'karchive5' 'kcodecs5'
         'libarchive' 'libzip' 'minizip' 'util-linux-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-compressor.git#tag=$pkgver")
sha512sums=('33011a765cf0004eb66cf045b384d8ff61774b224ea6add566ade2e052ed1f9489026947dcd4e2da5cefa29d00c332cbe628659ed915ea216a43eccb5347281a')

build() {
  cd deepin-compressor
  cmake . -GNinja -DCMAKE_INSTALL_LIBDIR=/usr/lib
  ninja
}

package() {
  cd deepin-compressor
  DESTDIR="$pkgdir" ninja install
}
