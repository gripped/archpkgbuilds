# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.12.25
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'qt5-base' 'glib2' 'karchive5' 'kcodecs5'
         'libarchive' 'libzip' 'minizip' 'util-linux-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-compressor.git#tag=$pkgver")
sha512sums=('98fdbe0f767184434897de893309a1c7e4a98fe1409ca7c2cd0fa8f54335ffbcec24aeda4209c2b1a42bbffdc856e6560c532c67f67204dfb18fc234dd51551b')

prepare() {
  cd deepin-compressor
  git cherry-pick -n 9e13248e351dd6db49134e91e33a39d671310d8d
  git cherry-pick -n daf74ab78f03151371f0bbf2b9e4355f29afab8d
}

build() {
  cd deepin-compressor
  cmake . -GNinja -DCMAKE_INSTALL_LIBDIR=/usr/lib
  ninja
}

package() {
  cd deepin-compressor
  DESTDIR="$pkgdir" ninja install
}
