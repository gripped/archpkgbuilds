# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=6.0.0
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL-3.0-or-later')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'qt5-base' 'glib2' 'karchive5' 'kcodecs5'
         'libarchive' 'libzip' 'minizip' 'util-linux-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-compressor.git#tag=$pkgver")
sha512sums=('3536eb5750d6d72b1ead1bb6b4004ccbe3d472283f47ac56a1398bd0ec86d0cc44da6915533ac1afc9ee1353d7d114fc439236571bed542a89a226cb6a51dace')

prepare() {
  cd deepin-compressor
  git cherry-pick -n 9e13248e351dd6db49134e91e33a39d671310d8d
  git cherry-pick -n e06fa7ebc4cc28f6138bb7c9e771ea0e1f977d2a
}

build() {
  cd deepin-compressor
  cmake . -GNinja -DCMAKE_INSTALL_LIBDIR=/usr/lib
  ninja
}

package() {
  cd deepin-compressor
  DESTDIR="$pkgdir" ninja install
}
