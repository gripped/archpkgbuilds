# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.12.17
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'gcc-libs' 'qt5-base' 'glib2'
         'disomaster' 'karchive' 'kcodecs' 'libarchive' 'libzip' 'minizip' 'udisks2-qt5'
         'util-linux-libs')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('65f28c50011801e24a98bc02450050ee37a2fcfccc66b6a4625be6954d347816ff7204207dc2398c1569856d4e48df95b0532e0085789e039b955a518ca3fc3a')

build() {
  cd deepin-compressor-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd deepin-compressor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
