# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.12.24
pkgrel=4
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'qt5-base' 'glib2' 'karchive5' 'kcodecs5'
         'libarchive' 'libzip' 'minizip' 'util-linux-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-compressor.git#tag=$pkgver")
sha512sums=('bc8c640c3fbc938596fcd14b9fa6d2967afaa88c8d405a20fb1cb77218602dddd670311aace0c3da0187f93af92da1219dc9d815fb7062c0ddda9de77441db20')

prepare() {
  cd deepin-compressor
  git cherry-pick -n 9e13248e351dd6db49134e91e33a39d671310d8d
  git cherry-pick -n daf74ab78f03151371f0bbf2b9e4355f29afab8d
}

build() {
  cd deepin-compressor
  cmake . -GNinja -DCMAKE_INSTALL_LIBDIR=/usr/lib
  ninja
}

package() {
  cd deepin-compressor
  DESTDIR="$pkgdir" ninja install
}
