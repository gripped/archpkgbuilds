# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=bulletty
pkgver=0.2.1
pkgrel=1
pkgdesc="Pretty feed reader for the terminal that stores the articles as Markdown"
url='https://github.com/CrociDB/bulletty'
arch=('x86_64')
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/CrociDB/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('5c5cc4065e2b9529a7ac2262d4fbc7355065ae3d632dead7dcfa7e3ed385c304')
b2sums=('f5af06ec32ddd9ece3db820ca564c5d24b45adf0b7e0ffc16fe78d89fa4057e8ab94a17809b986ecef671412b60337b216eecdda7e0e6026d673bfce234f937b')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
  target/release/bulletty --help
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/bulletty
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

# vim: ts=2 sw=2 et:
