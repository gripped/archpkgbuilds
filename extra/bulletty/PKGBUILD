# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=bulletty
pkgver=0.2.2
pkgrel=1
pkgdesc="Pretty feed reader for the terminal that stores the articles as Markdown"
url='https://github.com/CrociDB/bulletty'
arch=('x86_64')
license=('MIT')
depends=(
  'glibc'
  'libgcc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/CrociDB/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('9983879c1246779e39ebf7476dedea6174957c2d15f907429e7352e3a2fdcaeb')
b2sums=('3ba91be37f14ed4f5764cf0f05a92e68e25c65f40665923f25fd969cf140231fac558d190a9075787de555f4d45eeee9a85de22479ecb02f099ee2a767900c87')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target host-tuple
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
  target/release/bulletty --help
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/bulletty
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

# vim: ts=2 sw=2 et:
