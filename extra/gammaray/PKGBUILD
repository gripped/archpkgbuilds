# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gammaray
pkgver=3.2.1
pkgrel=1
pkgdesc='A tool for examining the internals of a Qt application and to some extent also manipulate it'
arch=(x86_64)
url='https://www.kdab.com/gammaray/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         libelf
         qt6-base
         qt6-svg
         qt6-tools
         syntax-highlighting)
makedepends=(cmake
             doxygen
             git
             qt6-connectivity
             qt6-location
             qt6-scxml
             qt6-shadertools
             qt6-wayland
             qt6-webengine)
optdepends=('qt6-declarative: QML support'
            'qt6-positioning: Qt Positioning support'
            'qt6-scxml: state machine viewer plugin'
            'qt6-wayland: Wayland compositor inspector plugin'
            'qt6-webengine: web inspector plugin'
            'qt6-connectivity: bluetooth plugin')
source=(git+https://github.com/KDAB/GammaRay#tag=v$pkgver)
sha256sums=('12e84cfb192ca1829cea3f548d396d025f99a6bed0d66bbd539ab711fdf0bd38')
validpgpkeys=(E86C000370B1B9E2A9191AD53DBFB6882C9358FB) # KDAB Products <info@kdab.com>

prepare() {
# Fix plugin install dir
  sed -e 's|plugins/gammaray|lib/qt6/plugins/gammaray|' -i GammaRay/CMakeLists.txt
}

build() {
  cmake -B build -S GammaRay \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_VERSION_MAJOR=6 \
    -DGAMMARAY_INSTALL_QT_LAYOUT=ON \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt6/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt6/plugins/gammaray
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
