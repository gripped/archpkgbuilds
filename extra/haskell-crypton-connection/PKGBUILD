# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=crypton-connection
pkgname=haskell-crypton-connection
pkgver=0.4.0
pkgrel=5
pkgdesc="Simple and easy network connections API"
url="https://github.com/kazu-yamamoto/crypton-connection"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-basement' 'haskell-crypton-x509' 'haskell-crypton-x509-store'
         'haskell-crypton-x509-system' 'haskell-crypton-x509-validation'
         'haskell-data-default-class' 'haskell-network' 'haskell-socks' 'haskell-tls')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('c5bd5358e9240492b5f0e4ee8ae453dad6e3d91fb58c0b15283d082f7290b53e')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
