# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=sorted-list
pkgname=haskell-sorted-list
pkgver=0.2.3.1
pkgrel=49
pkgdesc="Type-enforced sorted lists and related functions"
url="https://hackage.haskell.org/package/sorted-list"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'uusi' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('45752fcc2dc20cc72caef1ba1512421f975804d8512090bc1076a3bcbb786317d8706c200f450e646c1b48cce449a793c2e799a3c0855926d96b17cf4e1d0a31')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 license "$pkgdir"/usr/share/licenses/$pkgname/license
    rm -f "$pkgdir"/usr/share/doc/$pkgname/license
}
