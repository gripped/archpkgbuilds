# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Cedric Girard <cgirard.archlinux@valinor.fr>
# Contributor: kevku <kevku@gmx.com>

pkgname=kodi-addon-inputstream-adaptive
pkgver=21.5.18
_codename=Omega
pkgrel=1
pkgdesc="InputStream client for adaptive streams for Kodi 18+"
arch=('x86_64')
url="https://github.com/peak3d/inputstream.adaptive"
license=('GPL2')
groups=('kodi-addons' 'kodi-addons-inputstream')
makedepends=('cmake' 'kodi-dev' 'gtest' 'rapidjson' 'pugixml')
options=(!lto)

_bento4_version="1.6.0-641-3"

source=("$pkgname-$pkgver.tar.gz::https://github.com/peak3d/inputstream.adaptive/archive/$pkgver-$_codename.tar.gz"
        "$pkgname-bento4-$_bento4_version.tar.gz::https://github.com/xbmc/Bento4/archive/refs/tags/$_bento4_version-$_codename.tar.gz"
    )
noextract=("$pkgname-bento4-$_bento4_version.tar.gz")
sha512sums=('f17297d0917d0342e6117c0ce53bf43d384c8ec398e0a97bd576e91cbe9a253eaf8dbcce321ea16bf542f6fa69cf98370a433be3c4269f25405baab650672f05'
            '401a4a394a6822f1d83cdb2fd61e5b353c5c7e610b095557ec5aad1639fc94b631676d7f335260285d74f8662ddb60863b65ff54711fcf3462458965bf9cc021')

#prepare() {
    #cd "inputstream.adaptive-$pkgver-$_codename"
#}

build() {
    cd "inputstream.adaptive-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        -DENABLE_INTERNAL_BENTO4=1 \
        -DBENTO4_URL="$srcdir/$pkgname-bento4-$_bento4_version.tar.gz" \
        .
    make
}

package() {
    depends=('kodi' 'kodi-platform' 'pugixml')
    cd "inputstream.adaptive-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}
