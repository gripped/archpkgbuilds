# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Cedric Girard <cgirard.archlinux@valinor.fr>
# Contributor: kevku <kevku@gmx.com>

pkgname=kodi-addon-inputstream-adaptive
pkgver=21.5.15
_codename=Omega
pkgrel=1
pkgdesc="InputStream client for adaptive streams for Kodi 18+"
arch=('x86_64')
url="https://github.com/peak3d/inputstream.adaptive"
license=('GPL2')
groups=('kodi-addons' 'kodi-addons-inputstream')
makedepends=('cmake' 'kodi-dev' 'gtest' 'rapidjson' 'pugixml')
options=(!lto)

_bento4_version="1.6.0-641-3"

source=("$pkgname-$pkgver.tar.gz::https://github.com/peak3d/inputstream.adaptive/archive/$pkgver-$_codename.tar.gz"
        "$pkgname-bento4-$_bento4_version.tar.gz::https://github.com/xbmc/Bento4/archive/refs/tags/$_bento4_version-$_codename.tar.gz"
        "0001-gcc-15-build-fixes.patch"
    )
noextract=("$pkgname-bento4-$_bento4_version.tar.gz")
sha512sums=('6c550d1163b0718476d2e50ae537149370ba04124bfd5bdb4a996b3a3d1183b3b337206d40d1b4d8c59282e6362eba33e76b0da3a758df952b8c89d394fb7564'
            '401a4a394a6822f1d83cdb2fd61e5b353c5c7e610b095557ec5aad1639fc94b631676d7f335260285d74f8662ddb60863b65ff54711fcf3462458965bf9cc021'
            'c6c35f698de8e57429e134467d56d11e64e697a522513ae0437f3a99251336c87e9ab0d11a950bae687b1433c7300adc3ba6f3857d9b78849017986a273b44e2')

prepare() {
    cd "inputstream.adaptive-$pkgver-$_codename"
    patch -p1 -i "$srcdir/0001-gcc-15-build-fixes.patch"
}

build() {
    cd "inputstream.adaptive-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        -DENABLE_INTERNAL_BENTO4=1 \
        -DBENTO4_URL="$srcdir/$pkgname-bento4-$_bento4_version.tar.gz" \
        .
    make
}

package() {
    depends=('kodi' 'kodi-platform' 'pugixml')
    cd "inputstream.adaptive-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}
