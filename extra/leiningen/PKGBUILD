# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcel Campello <tucho@prafrentex.com.br>

pkgname=leiningen
pkgver=2.12.0
pkgrel=1
pkgdesc='Automate Clojure projects'
arch=(any)
url='https://codeberg.org/leiningen/leiningen'
license=(EPL-2.0)
depends=(java-environment java-runtime)
makedepends=(git leiningen)
optdepends=('rlwrap: for readline support')
noextract=("$pkgname-$pkgver-standalone.jar")
source=("git+$url#tag=$pkgver")
b2sums=('d5686fb30b1b7facad6d0cafdad4084093b557b981464ae72f3658640142b8f3968359df0f5655e9db6e41b8481e877db18a501ab4abc43503dac88b9ed56533')

prepare() {
  cd $pkgname/$pkgname-core
  lein bootstrap
}

build() {
  cd $pkgname
  chmod +x bin/lein
  bin/lein uberjar
  bin/lein pom
}

package() {
  cd $pkgname
  install -Dm755 bin/lein "$pkgdir/usr/bin/lein-standalone"
  install -Dm755 bin/lein-pkg "$pkgdir/usr/bin/lein"
  install -Dm644 doc/lein.1 "$pkgdir/usr/share/man/man1/lein.1"
  install -Dm644 target/$pkgname-$pkgver-standalone.jar \
    "$pkgdir/usr/share/java/$pkgname-$pkgver-standalone.jar"
}
