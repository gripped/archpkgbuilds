# Maintainer: Your Name <youremail@domain.com>

pkgname=rpacket
pkgver=0.1.2
pkgrel=1
pkgdesc="Low-level OpenPGP packet operations based on rPGP"
arch=(x86_64)
url="https://codeberg.org/heiko/rpacket"
license=('Apache-2.0 OR MIT')
groups=(openpgp-tools)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('860c055aa37a2c90970e273695f1064760fb01b9327c506b39a435ae5f1a72596baa1e0d17f3bfa13821b724bc01c69700ac6b0e5ee8f6d610f0d95936f37be3')
b2sums=('286bcf54792af41994164c236f4c7a448238c099e06d97147ca0a662eabbca0e60074421ed4c46f4a85000879199615f00dac3cda70f7c7a2214831ee1236f51')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
