# Maintainer: Your Name <youremail@domain.com>

pkgname=rpacket
pkgver=0.1.1
pkgrel=1
pkgdesc="Low-level OpenPGP packet operations based on rPGP"
arch=(x86_64)
url="https://codeberg.org/heiko/rpacket"
license=('Apache-2.0 OR MIT')
groups=(openpgp-tools)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('d19051900c68ecb509f6526f0bfeee34c1abb75c01187ef9cad9b9213135028c834492b247a42efdcbdd2d5c6f43bd859db991ae370852cb1b1cdaf363152400')
b2sums=('b7b91f26131d99eb5c3cf976c098e1c2a95aac7d2c9e5bdbaa5f184a5edfbcf0b213ce4ba19351e5b25ae68bd86d0e9e9ba322344159fd106019fee1cd8f0a0e')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
