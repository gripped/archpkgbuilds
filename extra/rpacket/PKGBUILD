# Maintainer: Your Name <youremail@domain.com>

pkgname=rpacket
pkgver=0.1.5
pkgrel=1
pkgdesc="Low-level OpenPGP packet operations based on rPGP"
arch=(x86_64)
url="https://codeberg.org/heiko/rpacket"
license=('Apache-2.0 OR MIT')
groups=(openpgp-tools)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('bff6ff0fb5e98a5118d6f0ad7ce2aee39433b95c85dd30927f823b703dcac97838a520fa37661b7a8988800e7ac3daab46ed4bef2232ccb36d72e9c194086a06')
b2sums=('71886e964f1aee9aaa334ea9568f7d96e74c0e6f3560383b5cbc13c41816ed07484b3d5a84b24b2e1b19ddfae3d453e90d4ee459b4bd923e915c9a922baf2bad')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
