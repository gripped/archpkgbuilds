# Maintainer: Your Name <youremail@domain.com>

pkgname=rpacket
pkgver=0.1.4
pkgrel=1
pkgdesc="Low-level OpenPGP packet operations based on rPGP"
arch=(x86_64)
url="https://codeberg.org/heiko/rpacket"
license=('Apache-2.0 OR MIT')
groups=(openpgp-tools)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('f7344fcff4c5b2f7cab52dcd858c3ef1ea8d4a1aef02326dc81c20ddfbaca626022d443fe726c5f0d21f426e91e6f95b45b82eac1fd98e9c006b10e2b6152de1')
b2sums=('f82b49069105ef221f71f7b1bbc6c0c75bf4baea8bc0d4610a7ccdd174c24fa1156cc2c4a4f979e87fb770e058a16cfc51a57e9bad98ab00412bc5f53679bb93')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
