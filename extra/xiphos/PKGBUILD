# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Christopher Bayliss <christopher.j.bayliss@gmail.com>
# Contributor: Albert De La Fuente Vigliotti
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: vonpupp

pkgname=xiphos
pkgver=4.3.1
pkgrel=2
pkgdesc='A Bible study tool for GTK3'
arch=(x86_64)
url='https://xiphos.org'
license=(GPL-2.0-only)
depends=('biblesync' 'dbus-glib' 'icu' 'libbiblesync.so' 'libicui18n.so'
         'minizip' 'sword>=1.9.0' 'webkit2gtk')
makedepends=('appstream-glib' 'cmake' 'git' 'glib2-devel' 'gnome-common'
             'intltool' 'libffi' 'libxml2' 'python' 'zlib' 'zip')
source=("git+https://github.com/crosswire/xiphos.git#tag=$pkgver"
        https-by-default.patch)
sha256sums=('701bcfab0a06b00d415bbebeb3ea98a2fe6efe57d04c05224b4fd3e6ac3aa101'
            '3630dee5f9f05600e1a726b91a89e2eb17615a73d30548de303e7b29ec2286d2')

prepare() {
  cd "$pkgname"
  # avoid fetching modules over plaintext ftp
  patch -Np1 -i ../https-by-default.patch
}

build() {
  cmake -B build -S "$pkgname" \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DEPUB=OFF \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
