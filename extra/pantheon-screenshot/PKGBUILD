# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-screenshot
pkgver=8.0.1
pkgrel=1
pkgdesc='The Pantheon Screenshot Tool'
arch=(x86_64)
url=https://github.com/elementary/screenshot
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk4
  libcanberra
  libgranite-7.so
  libhandy-1.so
  libportal
)
makedepends=(
  cmake
  git
  intltool
  meson
  vala
)
_tag=fa364dc1971239c05733721b7267bd9ac49b6dff
source=(pantheon-screenshot::git+https://github.com/elementary/screenshot.git#tag=${_tag})
b2sums=('6707b8b501ee7b3cea34bf89d6d5d7ec276c829a4862ef57e190ef2866ac4b565c7b898ffa7946751b6b613968f8372184dc1fab84815fe5e370093f0d25bd4b')

pkgver() {
  cd pantheon-screenshot
  git describe --tags
}

build() {
  arch-meson pantheon-screenshot build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
