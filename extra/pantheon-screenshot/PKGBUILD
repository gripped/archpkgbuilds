# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-screenshot
pkgver=8.0.3
pkgrel=1
pkgdesc='The Pantheon Screenshot Tool'
arch=(x86_64)
url=https://github.com/elementary/screenshot
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk4
  libcanberra
  libgranite-7.so
  libhandy-1.so
  libportal
)
makedepends=(
  cmake
  git
  intltool
  meson
  vala
)
_tag=62db07e3cd72e2a4709d67cb83f44ea939f88982
source=(pantheon-screenshot::git+https://github.com/elementary/screenshot.git#tag=${_tag})
b2sums=('3653f0a534e0b7691f4692e9955d9af8ad196e0d73947b717d0322da833eac8bada153e4e00edea69a2c30611d91c901e81ac6a76d01f6591e359287f9081039')

pkgver() {
  cd pantheon-screenshot
  git describe --tags
}

build() {
  arch-meson pantheon-screenshot build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
