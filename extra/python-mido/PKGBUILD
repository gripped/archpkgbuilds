# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor:  Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=python-mido
pkgver=1.3.3
pkgrel=5
pkgdesc='Python library for working with MIDI 1.0 messages, ports and files'
arch=(any)
url='https://mido.readthedocs.io'
license=(MIT)
depends=(
  python
  python-importlib-metadata
  python-packaging
  python-rtmidi
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-pytest
)
optdepends=(
  'portmidi: for PortMidi support'
  'python-pygame: for PortMidi support'
)
source=("$pkgname::git+https://github.com/mido/mido#tag=$pkgver")
sha512sums=('eb611988854f08de5360a1fbaa897256ac7745ef5a695207ab7defddcb0b3a964074dc1e30848500cf8c611ec4631f7765eb141287ff8460be3f8674c8205c23')
b2sums=('91cff524d8f36126d4c6424d47738b2962092339d595cb95164e93bb491b1d398e811006ea0b77513cdbba408e12ed89f9da5625a066b4156eeb61334aefd36a')

build() {
  cd "$pkgname"

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH=. pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
