# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thor77 <thor77 at thor77 dot org>
# Contributor: Clar Charr <clar@charr.xyz>
# Contributor: somini <dev at somini dot xyz>

pkgname=miniflux
pkgver=2.2.4
pkgrel=1
pkgdesc='Minimalist and opinionated feed reader'
arch=('x86_64')
url='https://miniflux.app'
license=('Apache-2.0')
depends=('glibc')
optdepends=(
  'postgresql: for local PostgreSQL instance'
)
makedepends=('git' 'go')
backup=('etc/miniflux.conf')
options=('!lto')
source=(
  "$pkgname::git+https://github.com/miniflux/v2.git#tag=$pkgver"
  'sysusers.conf'
)
sha512sums=('a4ba61da8b7b60691c3e7fb3bd8076a1dcbaf52c5612e3b96c737ac54746ceaff1144a402985a2b3be39892f80ef978aec4de64784cbfd83c457e59e6e4dce05'
            'eb026d1eb085f0638161a6a75af5e870c7ba4755012bf7f895de5c1bdd9fbdcb44a8aacbb71c482ca101c214f12f65906ddbcbb7044fad051490f44276aba8e5')
b2sums=('d90c2fc0c3e42e1f7e7cad928b2729a17d78f3f69bb582d8f9c409bec2647abb8875a2ea6395069b25634af0627e8119c781834767a157293803af26a9cc972e'
        '71a1cba0e015c6b33897e3492dd014efbfac240d3c6f007f86ff7bade830ce0f2809d4d0ad1d8ca1189627e2961dddc7ff6682cd445587f5145bee0eab8a7bf6')

prepare() {
  cd "$pkgname"

  # revert unnecessary change to systemd unit file
  git revert --no-commit 527c5f49cbe851b53b87c25dd19e8b4760aa7032

  # create directory for build output
  mkdir -p build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # ensure build date is reproducible
  local BUILD_DATE=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d-%H:%M:%S)

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X miniflux.app/v2/internal/version.Version=${pkgver} \
    -X miniflux.app/v2/internal/version.Commit=$(git rev-parse HEAD) \
    -X miniflux.app/v2/internal/version.BuildDate=${BUILD_DATE}" \
    -o build \
    .
}

#check() {
#  cd "$pkgname"
#  go test -v -race -count=1 ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 "build/$pkgname.app" "$pkgdir/usr/bin/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/man/man1" "$pkgname.1"
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md ChangeLog

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" "packaging/systemd/$pkgname.service"
  install -vDm644 "$srcdir/sysusers.conf" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm600 -t "$pkgdir/etc" "packaging/$pkgname.conf"
}
