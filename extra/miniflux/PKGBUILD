# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thor77 <thor77 at thor77 dot org>
# Contributor: Clar Charr <clar@charr.xyz>
# Contributor: somini <dev at somini dot xyz>

pkgname=miniflux
pkgver=2.2.16
pkgrel=1
pkgdesc='Minimalist and opinionated feed reader'
arch=(x86_64)
url='https://miniflux.app'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go)
optdepends=('postgresql: for local PostgreSQL instance')
backup=(etc/miniflux.conf)
source=(
  "$pkgname::git+https://github.com/miniflux/v2.git#tag=$pkgver"
  sysusers.conf
)
sha512sums=('f281b32837af971c363eff738abae6b49508d3adfe87b6135b87f368394616fa318c713af715895302243d183a2c0bc568adce96f9fab8e52c09d8f18d7f7960'
            '4a51cccf15f37da66c56e4721171ab858b894f4f74f3210fb275b437fc7fa99cce21a8a98703462e07163ce3f9ea35d8e2a9f8b5ad147cb819092a037c42c17e')
b2sums=('dfd3ba0f08aceadf9c699e04e4d4c8be37d3228b995f892a20b4a44fa3577cc7d4b6893b5ecd651b69296c254009b5e63a3969d9ba230f4e66b53082c0b875a1'
        '6c1bbc5bebb319d921c9f511a156c4e578811319423e99cead87f2044e6269abf38654733376b83f8dbb7432c816f795918935bae741529d7395f249027b2932')

prepare() {
  cd "$pkgname"

  # revert unnecessary change to systemd unit file
  git revert --no-commit 527c5f49cbe851b53b87c25dd19e8b4760aa7032

  # create directory for build output
  mkdir -p build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # ensure build date is reproducible
  local BUILD_DATE=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d-%H:%M:%S)

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X miniflux.app/v2/internal/version.Version=${pkgver} \
    -X miniflux.app/v2/internal/version.Commit=$(git rev-parse HEAD) \
    -X miniflux.app/v2/internal/version.BuildDate=${BUILD_DATE}" \
    -o build \
    .
}

#check() {
#  cd "$pkgname"
#  go test -v -race -count=1 ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 "build/$pkgname.app" "$pkgdir/usr/bin/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/man/man1" "$pkgname.1"
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" "packaging/systemd/$pkgname.service"
  install -vDm644 "$srcdir/sysusers.conf" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm600 -t "$pkgdir/etc" "packaging/$pkgname.conf"
}
