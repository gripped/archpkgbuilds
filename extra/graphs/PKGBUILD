# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=graphs
pkgver=1.8.3
pkgrel=2
pkgdesc='Tool to plot and manipulate data'
arch=(x86_64)
url='https://apps.gnome.org/Graphs/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  pango
  python
  python-cairo
  python-cycler
  python-gobject
  python-matplotlib
  python-numexpr
  python-numpy
  python-scipy
  python-sympy
)
makedepends=(
  appstream
  blueprint-compiler
  git
  gobject-introspection
  itstool
  meson
  vala
)
source=(
  "git+https://gitlab.gnome.org/World/Graphs.git#tag=v$pkgver"
  graphs-metainfo.patch
  graphs-gschema-install.patch
  graphs-accent-color.patch
  graphs-window-icon.patch
)
b2sums=(
  808b82c95add9896767cf3068eff3f27714f5f269c345511e6b9cc99cb0cf8bb94beab66a3f3b3687ee29c1126d186c2653ddf48beca366957be38024ddc8948
  170a2b12fab2d9713e00fdb043777d12da8785fc4cf2a7b023d7522c8eb141620c735e5b1abdb3dabf4273fb03af0a49a0e56652cd0ab7e5c0c5a36e9103a99e
  3bc65c3b1d5ccc782eadc9dddb934c1e3adcce459cb6fe8140ac9b54b8decab0fae75d675c2ee16deb2fd50583df8925088707c3be3b2e2feffe6a869516b174
  c738d93d6f810639d9d1b4f5f5c877640769cdc99bfd56d287a6660cf763e13897f336d510c90c840b68f53f32e2842e45d982a1842e3f9c52ab402155a7873a
  9c99112b1166e47ab8c04b471b98dbe5d28761ad6459e36f8abdab58e71bf15aefe92582473ee7053b725dc02ba9166ed1f8f4c532733dce6c7a08b54aa7f2bc
)

prepare() {
  cd Graphs

  # https://gitlab.gnome.org/World/Graphs/-/merge_requests/964
  git apply -3 ../graphs-metainfo.patch

  # https://gitlab.gnome.org/World/Graphs/-/merge_requests/966
  git apply -3 ../graphs-gschema-install.patch

  # Use AdwStyleManager (fixes crash on launch)
  git apply -3 ../graphs-accent-color.patch

  # Set icon for the window
  git apply -3 ../graphs-window-icon.patch
}

build() {
  arch-meson Graphs build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
