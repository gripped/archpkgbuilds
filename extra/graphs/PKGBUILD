# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=graphs
pkgver=1.8.7
pkgrel=1
pkgdesc='Tool to plot and manipulate data'
arch=(x86_64)
url='https://apps.gnome.org/Graphs/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  pango
  python
  python-cairo
  python-cycler
  python-gobject
  python-matplotlib
  python-numexpr
  python-numpy
  python-scipy
  python-sympy
)
makedepends=(
  appstream
  blueprint-compiler
  git
  gobject-introspection
  itstool
  meson
  vala
)
source=(
  "git+https://gitlab.gnome.org/World/Graphs.git#tag=v$pkgver"
  graphs-metainfo.patch
  graphs-gschema-install.patch
  graphs-activate.patch
  graphs-dbus-activatable.patch
)
b2sums=(
  02f44b7ebfde13e24f6a32890202b9f80f203f1490091194829509a24d4cd53ed78af94c27a31838b8dd6351e3400908bd4014c5b766128054795ead9673614b
  170a2b12fab2d9713e00fdb043777d12da8785fc4cf2a7b023d7522c8eb141620c735e5b1abdb3dabf4273fb03af0a49a0e56652cd0ab7e5c0c5a36e9103a99e
  3bc65c3b1d5ccc782eadc9dddb934c1e3adcce459cb6fe8140ac9b54b8decab0fae75d675c2ee16deb2fd50583df8925088707c3be3b2e2feffe6a869516b174
  0bbcd5ec640a6e1b036567e2ce82c6a0ed24f0705e8b337e8ec6c19253a4803f7b99e2186596aaa20fd1d935a52ca6bf74f130e480a704417d8dc5f0bfe990dc
  99feb11cc173b7492db19816292ba06eb1d6145c25439935db2e2888c45331449cd396a4b1bba20f7685c54a023316bdd78709f3bde2bd45b051f38b0f4202f3
)

prepare() {
  cd Graphs

  # https://gitlab.gnome.org/World/Graphs/-/merge_requests/964
  git apply -3 ../graphs-metainfo.patch

  # https://gitlab.gnome.org/World/Graphs/-/merge_requests/966
  git apply -3 ../graphs-gschema-install.patch

  # Raise existing window on activation
  git apply -3 ../graphs-activate.patch

  # Add D-Bus service file
  git apply -3 ../graphs-dbus-activatable.patch
}

build() {
  arch-meson Graphs build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
