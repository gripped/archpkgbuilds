# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-floskell-plugin
pkgname=haskell-hls-floskell-plugin
pkgver=2.2.0.0
pkgrel=4
pkgdesc="Integration with the Floskell code formatter"
url="https://hackage.haskell.org/package/hls-floskell-plugin"
license=('Apache-2.0')
arch=('x86_64')
depends=('ghc-libs' 'haskell-floskell' 'haskell-ghcide' 'haskell-hls-plugin-api' 'haskell-lsp-types')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('a9c125e1469fcb3e8d15e24be30b6bcfe4621b739d4666f63a2a77915afb1a7385b7e6445eb7c4468ead2b6a6e6966f69cc6c368a2f51bf00cc85f38c0927abc')

prepare() {
  cd $_hkgname-$pkgver
  sed -i 's/ghc >= 9.5/ghc >= 9.7/' $_hkgname.cabal
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
