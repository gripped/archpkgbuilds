# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Albert Graef <aggraef at gmail.com>
# Contributor: Bernardo Barros <bernardobarros at gmail dot com>
# Contributor: bjoern lindig (bjoern _dot_ lindig _at_ google.com)

pkgname=faust
pkgver=2.75.7
pkgrel=3
pkgdesc="A functional programming language for realtime audio signal processing."
arch=(x86_64)
url="https://faust.grame.fr/"
license=(
  GPL-2.0-or-later
  GPL-3.0-or-later
  LGPL-2.1-or-later
)
groups=(pro-audio)
depends=(
  bash
  gcc-libs
  glibc
  libmicrohttpd
  llvm-libs
)
makedepends=(
  cmake
  libsndfile
  llvm
  xxd
)
optdepends=(
  'clang: for sound2reader'
  'csound: for faust2csound'
  'gradle: for faust2android'
  'graphviz: for faust2sig{,viewer}'
  'gtk2: for faust2{jack,rosgtk}'
  'inkscape: for faust2{pdf,png}'
  'jack: for faust2{api,jack{,console,internal,server,rust},jaqt,netjackqt,nodejs,ros,rosgtk,rpinetjackconsole}'
  'swig: for faust2android'
  'libsndfile: for faust2{dummy,sndfile}'
  'octave: for faust2octave'
  'pd: for faust2puredata'
  'portaudio: for faust2paqt'
  'python: for faust2atomsnippets, faust2md and faust2sc.py'
  'qt5-tools: for faust2{faustvst,jaqt,lv2}'
  'rtaudio: for faust2raqt'
  'ruby-rexml: for faust2sc and scbuilder'
  'rust: for faust2jackrust'
  'supercollider: for faust2supercollider'
  'texlive-fontutils: for faust2pdf'
)
provides=(
  libfaustmachine.so
  libfaust.so
  libOSCFaust.so
  libHTTPDFaust.so
)
options=(
  !lto  # https://github.com/grame-cncm/faust/issues/1033
  staticlibs
)
source=(
  https://github.com/grame-cncm/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz
  $pkgname-2.75.7-link-llvm-dynamically.patch::https://github.com/grame-cncm/faust/commit/b09e2a49f50d0834546b39890e08252dbbf47d18.patch
  llvm-19.patch::https://github.com/grame-cncm/faust/commit/5b275806093fb2e3348043de1150f01ff892d232.patch
)
sha512sums=('2bcf28f445e3582e43a52d38d9a3b1f79f7dc0ec21ea28d215ba87bbcfa42a2f57a7c75a91becf9ab390b1fbd2e42ae828c21f2774433a2eeea9951aef88e52f'
            '4f38bc7561fc3add25136fea6beae2500d7492e2f144d3484e731565068db3f6b3660d5d80a51bfbe7e65a40fbf0019464464fc72e186ddf0a5b484a85321636'
            'f42ec31c76e981e1d1b3b91037a5f8bad13f8606bdd3ff37a6e4a26086025a9be81efabdb0749342e6f9b18067572913931d5f00cc2a69c81367203f4fc7cc63')
b2sums=('fa5cb3a5412135d72fd252e721cc093d41db52f678a4a08e3ba902ae389ca92d8da55524de3ee911ba5bec8f5b1badf680d2fae3f3059a63c0b0cf7cad44ce84'
        '4d89dd3b3d35cd38d24bdd55fb312f5ca50f2b166c011b447d80719102dcf4da8b2e264464a4d4451238db9721a9ecfd15f4802ade1ad03ac698c016163b11c4'
        '68d26c9b8a45b9e749903d66fd0378f8610d94a346231b544306a34617925198752dc8954f92b495aeed37bdae931e6b50ea57e61bab9dc82dd084b1802a33d1')

prepare() {
  # dynamically link against LLVM: https://github.com/grame-cncm/faust/issues/1053
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.75.7-link-llvm-dynamically.patch
  # Fix for LLVM 19
  patch -Np1 -d $pkgname-$pkgver -i ../llvm-19.patch
}

build() {
  local cmake_options=(
    -B build
    -C $pkgname-$pkgver/build/backends/all.cmake
    -C $pkgname-$pkgver/build/targets/all.cmake
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D INCLUDE_DYNAMIC=ON
    -D INCLUDE_ITP=ON
    -D INCLUDE_STATIC=OFF
    -S $pkgname-$pkgver/build
    -W no-dev
  )
  # prevent static libs from being mangled with LTO
  CXXFLAGS+=' -ffat-lto-objects'

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
  make VERBOSE=1 -C $pkgname-$pkgver/tools/sound2faust
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  cd $pkgname-$pkgver
  make VERBOSE=1 PREFIX=/usr DESTDIR="$pkgdir" install -C tools/sound2faust

  # docs
  install -vDm 644 documentation/{,misc/}*.pdf -t "$pkgdir/usr/share/doc/$pkgname"

  # examples
  install -vd "$pkgdir/usr/share/$pkgname/examples"
  cp -vR "examples/"* "$pkgdir/usr/share/$pkgname/examples/"

  cd syntax-highlighting
  # atom
  install -vDm 644 atom/language-$pkgname/package.json -t "$pkgdir/usr/lib/atom/dot-atom/packages/language-$pkgname/"
  install -vDm 644 atom/language-$pkgname/grammars/$pkgname.cson -t "$pkgdir/usr/lib/atom/dot-atom/packages/language-$pkgname/grammars/"
  install -vDm 644 atom/language-$pkgname/settings/language-$pkgname.cson -t "$pkgdir/usr/lib/atom/dot-atom/packages/language-$pkgname/settings/"
  install -vDm 644 atom/language-$pkgname/snippets/* -t "$pkgdir/usr/lib/atom/dot-atom/packages/language-$pkgname/snippets/"
  install -vDm 644 atom/language-$pkgname/process-palette.json.linux "$pkgdir/usr/share/doc/$pkgname/process-palette.json"
  # kate
  install -vDm 644 $pkgname.xml -t "$pkgdir/usr/share/apps/katepart/syntax/"
  # gedit
  install -vDm 644 $pkgname.lang -t "$pkgdir/usr/share/gtksourceview-2.0/language-specs/"
  install -vDm 644 $pkgname.lang -t "$pkgdir/usr/share/gtksourceview-3.0/language-specs/"
  # highlight
  install -vDm 644 dsp.lang -t "$pkgdir/usr/share/highlight/langDefs/"
  # nano
  install -vDm 644 $pkgname.nanorc -t "$pkgdir/usr/share/nano/"
  # vim
  install -vDm 644 $pkgname.vim -t "$pkgdir/usr/share/vim/vimfiles/syntax/"
  # emacs
  install -vDm 644 $pkgname-mode.el -t "$pkgdir/usr/share/emacs/site-lisp/"

  # remove unusable scripts
  rm -v "$pkgdir/usr/bin/faust2au"
  # remove precompiled shared libraries for android:
  # https://github.com/grame-cncm/faust/issues/370
  rm -rvf "$pkgdir/usr/share/faust/android/app/"{lib,oboe} "$pkgdir/usr/share/faust/smartKeyboard/android/app/oboe"
}

