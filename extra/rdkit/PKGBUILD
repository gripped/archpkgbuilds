# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=rdkit
pkgver=2025.03.2
pkgrel=2
pkgdesc="Open-source toolkit for cheminformatics"
arch=(x86_64)
url="https://github.com/rdkit/rdkit"
license=(BSD-3-Clause)
depends=(
  boost-libs
  cairo
  freetype2
  gcc-libs
  glibc
  python
  python-numpy
  python-pillow
)
makedepends=(
  boost
  catch2
  cmake
  eigen
  git
  ninja
)
source=($pkgname-$pkgver.tar.gz::$url/archive/Release_${pkgver//./_}.tar.gz)
b2sums=('3acb2364e074e73a7ba8907ec62f9370e8deb404af623bc32c3ad527aa70489a1bede7db1a1283809466544e970d5cee63f4b08ab17193f7f659f7f0f2b21059')

build() {
  # Fix build with GCC 15 https://github.com/rdkit/rdkit/issues/8491
  CFLAGS+=' -std=gnu17'

  local cmake_options=(
    -B build
    -S $pkgname-Release_${pkgver//./_}
    -G Ninja
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DRDK_BUILD_CAIRO_SUPPORT=ON
    -DRDK_INSTALL_INTREE=OFF
    -DRDK_INSTALL_STATIC_LIBS=OFF
    # TODO: the test suite has too many failures, even due to bad input files
    -DRDK_BUILD_CPP_TESTS=OFF
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

#check() {
#  local excluded_tests=""
#  # skip tests that take too long
#  excluded_tests+="cxsmilesTest"
#  excluded_tests+="|testGrid"
#  excluded_tests+="|testMultithreadedMolSupplier"
#  excluded_tests+="|v2MolSuppliers"
#
#  local ctest_flags=(
#    --test-dir build
#    # show the stdout and stderr when the test fails
#    --output-on-failure
#    # execute tests in parallel
#    --parallel $(nproc)
#    # exclude problematic tests
#    --exclude-regex "$excluded_tests"
#  )
#  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
#
#  # rdkit must be installed "in-tree" before running the test suite
#  # https://github.com/rdkit/rdkit/blob/master/Docs/Book/Install.md#linux-x86_64-python-3-environment
#  DESTDIR=test-install cmake --install build
#  local RDBASE=$pkgname-Release_${pkgver//./_}
#  mv test-install/usr/lib $RDBASE
#  RDBASE=$RDBASE LD_LIBRARY_PATH=$RDBASE/lib PYTHONPATH=test-install/"$site_packages" ctest "${ctest_flags[@]}"
#}

package_rdkit() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-Release_${pkgver//./_}/license.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
