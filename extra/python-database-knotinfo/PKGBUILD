# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-database-knotinfo
_pyname=database_knotinfo
pkgver=2024.11.1
pkgrel=1
arch=(any)
pkgdesc='Snapshot of the complete content of the KnotInfo and LinkInfo databases for Python'
url='https://github.com/soehms/database_knotinfo'
depends=(python)
makedepends=(git
             python-build
             python-installer
             python-setuptools-scm
             python-wheel)
license=(GPL-3.0-only)
source=(git+https://github.com/soehms/database_knotinfo#tag=$pkgver)
sha256sums=('92cbea0ab237f921340de45c0264df907f92090ef0347b3ca8462ece482ddcff')

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
