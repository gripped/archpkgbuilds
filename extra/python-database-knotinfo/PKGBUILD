# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-database-knotinfo
_pyname=database_knotinfo
pkgver=2025.10.1
pkgrel=1
arch=(any)
pkgdesc='Snapshot of the complete content of the KnotInfo and LinkInfo databases for Python'
url='https://github.com/soehms/database_knotinfo'
depends=(python)
makedepends=(git
             python-build
             python-installer
             python-setuptools-scm
             python-wheel)
license=(GPL-3.0-only)
source=(git+https://github.com/soehms/database_knotinfo#tag=$pkgver)
sha256sums=('c834c9c5ce6eff6806eca39761b19112c47e17dc8aaabf19300041612dce6a0a')

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
