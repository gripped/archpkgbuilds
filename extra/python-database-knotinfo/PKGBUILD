# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-database-knotinfo
_pyname=database_knotinfo
pkgver=2026.1.1
pkgrel=2
arch=(any)
pkgdesc='Snapshot of the complete content of the KnotInfo and LinkInfo databases for Python'
url='https://github.com/soehms/database_knotinfo'
depends=(python)
makedepends=(git
             python-build
             python-installer
             python-setuptools-scm
             python-wheel)
license=(GPL-3.0-only)
source=(git+https://github.com/soehms/database_knotinfo#tag=$pkgver)
sha256sums=('0599e64dfc5428f47f008f81ad63100ee35f2f9d2e958c78e6f0e0febeb997f1')

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
