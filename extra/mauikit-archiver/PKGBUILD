# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-archiver
pkgver=4.0.1
pkgrel=1
pkgdesc='QtQuick plugin for online archived/compressed files management'
url='https://mauikit.org/'
arch=(x86_64)
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         karchive
         kcoreaddons
         mauikit
         mauikit-filebrowsing
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('87398f98017c21c2e59be3740d6a0d592f6ca09e9290d665ab7a44dd34ea81fd'
            'SKIP')
validpgpkeys=(023625C8D02C0D8C15C3BABB86C793550794FEB8) # Camilo Higuita <milo.h@aol.com>                                    

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
# Workaround https://invent.kde.org/maui/index-fm/-/issues/76
  sed -e '/prefer/d' -i "$pkgdir"/usr/lib/qt6/qml/org/mauikit/archiver/qmldir
}
