# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=radare2-r2pipe
pkgname=(python-r2pipe)
pkgver=5.9.0
pkgrel=2
pkgdesc='Access radare2 via pipe from any programming language'
url='https://github.com/radareorg/radare2-r2pipe'
arch=('any')
license=('MIT')
makedepends=(
  radare2
  python
  python-build
  python-installer
  python-setuptools
  python-wheel
  python-sphinx
)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a3c53b9b86fa08f081d0762c29041c036a28e4d03c0d6aa7476c72c3e677139cc0860ed3fc413e725d7ad3471f921d92b1b83c269a47001345a10bc8d3cd7cbd')
b2sums=('834547c11a3cd72412d686a0c716abf08dba57638d9e747cf031b86f8eb393d88e5b1190a01c448b58e1372ef80c606b2955bc04dee1eff280c0cf5f37c7a522')

build() {
  cd radare2-r2pipe-${pkgver}/python
  python -m build --wheel --no-isolation
}

package_python-r2pipe() {
  depends=('python' 'radare2')
  cd radare2-r2pipe-${pkgver}/python
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
