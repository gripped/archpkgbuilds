# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=7.0.1
pkgrel=1
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi' 'libxkbcommon' 'llvm' 'glibc')
makedepends=('rustup' 'cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/refs/tags/v${pkgver}.tar.gz"
        reproducible-builds.patch)
sha512sums=('e8cf1c23d9b65d133ff54b6d2ebb73fe415e688f28d6349badd2d5e21334a46c25fc435a34d6c734d442f8605c37307b072f714fa8556f2e3be943f3c957ff14'
            '5e1f26fc8684af4cf53103b1250d986ff0174bddec6eb7107d086c5b82cf1ad9e02bc7b56e47274da444f345b482dea3282de0f5d413fdded84c94f55f59eb6e')
options=('staticlibs' '!lto')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/reproducible-builds.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make ENABLE_CRANELIFT=1 ENABLE_LLVM=1 WASMER_INSTALL_PREFIX=/usr
}

check() {
  cd "$pkgname-$pkgver"
  make test-examples
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir"/usr install
}

# vim:set ts=2 sw=2 et:
