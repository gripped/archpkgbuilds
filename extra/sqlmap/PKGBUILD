# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: fxbru <frxbru[at]gmail>

pkgname=sqlmap
pkgver=1.10.2
pkgrel=1
pkgdesc='Automatic SQL injection and database takeover tool'
url='https://sqlmap.org'
arch=(any)
license=(GPL-2.0-only)
depends=(
  python
  sh
)
optdepends=(
  'perl: icmpsh extra tool'
  'python-sqlalchemy: MySQL/MariaDB connection'
)
backup=(
  'etc/sqlmap.conf'
)
source=(https://github.com/sqlmapproject/sqlmap/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('092aab5def8149657899ee37266ef9b6dc93d2de6983fbd97726546ba8fe3dbd')
b2sums=('4025f4688df984edd36bfe4b5d80a021c1e50dbbf21ea92d9ebb1cb6b21285e46a4a80728f140424ee5c7e0b5802e81b1a09c794328e4c6fbe446f0af9fb42ab')

build() {
  cd ${pkgname}-${pkgver}
  python -m compileall .
  python -O -m compileall .
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/etc"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/opt/${pkgname}"
  install -d "${pkgdir}/usr/share/doc"

  mv doc "${pkgdir}/usr/share/doc/${pkgname}"
  mv sqlmap.conf "${pkgdir}/etc/sqlmap.conf"
  ln -sf /etc/sqlmap.conf "${pkgdir}/opt/${pkgname}/sqlmap.conf"
  cp -a --no-preserve=ownership * "${pkgdir}/opt/${pkgname}"

  cat > "${pkgdir}/usr/bin/sqlmap" << EOF
#!/bin/sh
exec python /opt/${pkgname}/sqlmap.py "\$@"
EOF
  cat > "${pkgdir}/usr/bin/sqlmapapi" << EOF
#!/bin/sh
exec python /opt/${pkgname}/sqlmapapi.py "\$@"
EOF

  chmod 755 "${pkgdir}/usr/bin/sqlmap"
  chmod 755 "${pkgdir}/usr/bin/sqlmapapi"
}

# vim: ts=2 sw=2 et:
