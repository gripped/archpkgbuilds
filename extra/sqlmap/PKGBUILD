# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: fxbru <frxbru[at]gmail>

pkgname=sqlmap
pkgver=1.9.12
pkgrel=2
pkgdesc='Automatic SQL injection and database takeover tool'
url='https://sqlmap.org'
arch=(any)
license=(GPL-2.0-only)
depends=(
  python
  sh
)
optdepends=(
  'perl: icmpsh extra tool'
  'python-sqlalchemy: MySQL/MariaDB connection'
)
backup=(
  'etc/sqlmap.conf'
)
source=(https://github.com/sqlmapproject/sqlmap/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c0d825644222c147c044a9215642515f760f047d690329bbf806dd89e873b68a')
b2sums=('644d4a90caf89c329e2e0a05e5a8d21cd4cd67102fc9888d5b8e17bb9e642796be1d4ff130690d24fc480d793d352a05392bd642345ed5a4c648871dda750825')

build() {
  cd ${pkgname}-${pkgver}
  python -m compileall .
  python -O -m compileall .
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/etc"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/opt/${pkgname}"
  install -d "${pkgdir}/usr/share/doc"

  mv doc "${pkgdir}/usr/share/doc/${pkgname}"
  mv sqlmap.conf "${pkgdir}/etc/sqlmap.conf"
  ln -sf /etc/sqlmap.conf "${pkgdir}/opt/${pkgname}/sqlmap.conf"
  cp -a --no-preserve=ownership * "${pkgdir}/opt/${pkgname}"

  cat > "${pkgdir}/usr/bin/sqlmap" << EOF
#!/bin/sh
exec python /opt/${pkgname}/sqlmap.py "\$@"
EOF
  cat > "${pkgdir}/usr/bin/sqlmapapi" << EOF
#!/bin/sh
exec python /opt/${pkgname}/sqlmapapi.py "\$@"
EOF

  chmod 755 "${pkgdir}/usr/bin/sqlmap"
  chmod 755 "${pkgdir}/usr/bin/sqlmapapi"
}

# vim: ts=2 sw=2 et:
