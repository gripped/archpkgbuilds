# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: fxbru <frxbru[at]gmail>

pkgname=sqlmap
pkgver=1.9.4
pkgrel=2
pkgdesc='Automatic SQL injection and database takeover tool'
url='https://sqlmap.org'
arch=(any)
license=(GPL-2.0-only)
depends=(
  python
  sh
)
optdepends=(
  'perl: icmpsh extra tool'
  'python-sqlalchemy: MySQL/MariaDB connection'
)
backup=(
  'etc/sqlmap.conf'
)
source=(https://github.com/sqlmapproject/sqlmap/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('aa7d89591945efe25ea3a41292630606ba9c8196294f920abe752f430a67437a')
b2sums=('2cc368c540cc62d1c6fe87ffeae3c2f77549557bd55bed034ca25e086ca5d511f155afa09d148ca63e793304ca5d2815bbf1bace18f59c98b3dfd9f8f52bd6ca')

build() {
  cd ${pkgname}-${pkgver}
  python -m compileall .
  python -O -m compileall .
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/etc"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/opt/${pkgname}"
  install -d "${pkgdir}/usr/share/doc"

  mv doc "${pkgdir}/usr/share/doc/${pkgname}"
  mv sqlmap.conf "${pkgdir}/etc/sqlmap.conf"
  ln -sf /etc/sqlmap.conf "${pkgdir}/opt/${pkgname}/sqlmap.conf"
  cp -a --no-preserve=ownership * "${pkgdir}/opt/${pkgname}"

  cat > "${pkgdir}/usr/bin/sqlmap" << EOF
#!/bin/sh
cd /opt/${pkgname}
python sqlmap.py "\$@"
EOF
  cat > "${pkgdir}/usr/bin/sqlmapapi" << EOF
#!/bin/sh
cd /opt/${pkgname}
python sqlmapapi.py "\$@"
EOF

  chmod 755 "${pkgdir}/usr/bin/sqlmap"
  chmod 755 "${pkgdir}/usr/bin/sqlmapapi"
}

# vim: ts=2 sw=2 et:
