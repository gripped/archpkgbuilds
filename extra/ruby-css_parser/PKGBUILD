# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contirbutor: Anatoly Bashmakov <anatoly at posteo dot net>

pkgname=ruby-css_parser
_pkgname=${pkgname#ruby-}
pkgver=1.21.1
pkgrel=3
pkgdesc="Ruby CSS parser"
arch=(any)
url="https://github.com/premailer/css_parser"
license=(MIT)
depends=(
  ruby
  ruby-addressable
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-maxitest
  ruby-rake
  ruby-webrick
)
options=(!emptydirs)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('bfa062cc58c306104a707036e8260312b294678b7248b265e801773dc888f746')

prepare() {
  cd "$_pkgname"
  sed -i -E 's|~>|>=|g' "$_pkgname.gemspec"
  sed -i '/bundler/Id' Rakefile test/test_helper.rb
  sed -i '/require.*bump/Id' Rakefile
  sed -i \
    -e "/require 'rubocop/d" \
    -e '/RuboCop/,/end/d' \
    Rakefile
  echo "require 'pathname'" >> test/test_helper.rb
}

build() {
  cd "$_pkgname"
  local gemdir="$(gem env gemdir)"

  gem build "$_pkgname.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "$_pkgname-$pkgver.gem"

  rm --force --recursive --verbose \
    "tmp_install/$gemdir/cache/" \
    "tmp_install/$gemdir/gems/$_pkgname-$pkgver/vendor/" \
    "tmp_install/$gemdir/doc/$_pkgname-$pkgver/ri/ext/"

  find "tmp_install/$gemdir/gems/" \
    -type f \
    \( \
    -iname "*.o" -o \
    -iname "*.c" -o \
    -iname "*.so" -o \
    -iname "*.time" -o \
    -iname "gem.build_complete" -o \
    -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/$gemdir/extensions/" \
    -type f \
    \( \
    -iname "mkmf.log" -o \
    -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "$_pkgname"
  GEM_HOME="tmp_install/$(gem env gemdir)" rake test
}

package() {
  cd "$_pkgname"
  cp -a -t "$pkgdir" tmp_install/*
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" MIT-LICENSE
}
