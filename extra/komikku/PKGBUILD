# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=komikku
pkgver=1.64.0
pkgrel=1
pkgdesc='Manga and comics reader for GNOME'
arch=(any)
url='https://apps.gnome.org/Komikku/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libnotify
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-brotli
  python-colorthief
  python-cryptography
  python-dateparser
  python-emoji
  python-gobject
  python-keyring
  python-lxml
  python-magic
  python-natsort
  python-piexif
  python-pillow
  python-pillow-heif
  python-pytz
  python-rarfile
  python-regex
  python-requests
  python-tzlocal
  python-unidecode
  python-urllib3
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://codeberg.org/valos/Komikku.git#tag=v$pkgver")
b2sums=(9c07b224f0f12050f9d34dfd7552273ad65c641fd664a4b8d05d5409cb8c0559ece170a08810826c713dec74337e94363f05e8f93ab668b7fd13408eae3af8dd)

build() {
  arch-meson Komikku build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
