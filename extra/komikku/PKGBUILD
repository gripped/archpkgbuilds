# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=komikku
pkgver=1.99.0
pkgrel=1
pkgdesc='Manga and comics reader for GNOME'
arch=(any)
url='https://apps.gnome.org/Komikku/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-brotli
  python-colorthief
  python-cryptography
  python-dateparser
  python-emoji
  python-gobject
  python-keyring
  python-lxml
  python-magic
  python-natsort
  python-piexif
  python-pillow
  python-pyjwt
  python-pypdf
  python-pytz
  python-rarfile
  python-requests
  python-tzlocal
  python-unidecode
  python-urllib3
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://codeberg.org/valos/Komikku.git#tag=v$pkgver")
b2sums=(0a466cadad47bdde494f35091f211a76aa926df658b049849eac8c570f0c070d443efc495b59c7efe85751f32c4683742efc16dba20d83f5ec8731c464def898)

prepare() {
  cd Komikku
}

build() {
  arch-meson Komikku build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
