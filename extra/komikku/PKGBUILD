# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=komikku
pkgver=1.83.0
pkgrel=1
pkgdesc='Manga and comics reader for GNOME'
arch=(any)
url='https://apps.gnome.org/Komikku/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libnotify
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-brotli
  python-cryptography
  python-dateparser
  python-emoji
  python-gobject
  python-keyring
  python-lxml
  python-magic
  python-modern-colorthief
  python-natsort
  python-piexif
  python-pillow
  python-pytz
  python-rarfile
  python-regex
  python-requests
  python-tzlocal
  python-unidecode
  python-urllib3
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://codeberg.org/valos/Komikku.git#tag=v$pkgver")
b2sums=(6b60c08abcd3155391faac7c03d08fc0955a5c9f137ef00ef2d28e211a0d23dfb8d6d7882801c486572cb38aa7c3813c77d4d575496cf70b1a64eedd2a8b5b02)

build() {
  arch-meson Komikku build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
