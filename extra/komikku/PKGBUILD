# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=komikku
pkgver=1.75.0
pkgrel=1
pkgdesc='Manga and comics reader for GNOME'
arch=(any)
url='https://apps.gnome.org/Komikku/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libnotify
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-brotli
  python-colorthief
  python-cryptography
  python-dateparser
  python-emoji
  python-gobject
  python-keyring
  python-lxml
  python-magic
  python-natsort
  python-piexif
  python-pillow
  python-pillow-heif
  python-pytz
  python-rarfile
  python-regex
  python-requests
  python-tzlocal
  python-unidecode
  python-urllib3
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=("git+https://codeberg.org/valos/Komikku.git#tag=v$pkgver")
b2sums=(1985181f1c08fd7c9af3d3e63cf251d0b260c60848ffb58de66861872271b1958531de5fc09ff770bd7680f1f9293a8e5a65311974c753b8f80ebcbfaf5bea5d)

prepare() {
  cd Komikku

  # https://codeberg.org/valos/Komikku/pulls/783
  git cherry-pick -n 40436f913cc8bc6267c065aaaab30bb8299d8719

  # https://codeberg.org/valos/Komikku/pulls/784
  git cherry-pick -n 2099b4533779a3fab3341600094c3d4b286a56e5
}

build() {
  arch-meson Komikku build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
