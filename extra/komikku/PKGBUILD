# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=komikku
pkgver=1.60.0
pkgrel=2
pkgdesc='Manga and comics reader for GNOME'
arch=(any)
url='https://apps.gnome.org/Komikku/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libnotify
  libsoup3
  pango
  python
  python-beautifulsoup4
  python-brotli
  python-colorthief
  python-cryptography
  python-dateparser
  python-emoji
  python-gobject
  python-keyring
  python-lxml
  python-magic
  python-natsort
  python-piexif
  python-pillow
  python-pillow-heif
  python-pytz
  python-rarfile
  python-regex
  python-requests
  python-tzlocal
  python-unidecode
  python-urllib3
  webkitgtk-6.0
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://codeberg.org/valos/Komikku.git#tag=v$pkgver"
  komikku-icon-gresource.patch
)
b2sums=(
  f264b00aa971a56e4ed5e13d248dba590a2f0f251becc748f32f5b2254f6491f6826441fedaad2d3c13b395bb56be5bc74ebc5f79b5bd1b4431e18a454b642e1
  3c04a7475e20b15da06e16359cb62c4841056c5eb483ffce3f83ccdd00cd4b417d553bb7c2055ece730791c5a15676455fb5fd1b2737fc3468039253f7e984e0
)

prepare() {
  cd Komikku

  # https://codeberg.org/valos/Komikku/pulls/745
  git apply -3 ../komikku-icon-gresource.patch
}

build() {
  arch-meson Komikku build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
