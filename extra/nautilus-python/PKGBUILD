# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giorgio Gilestro <giorgio@gilest.ro>

pkgbase=nautilus-python
pkgname=python-nautilus
pkgver=4.0.1
pkgrel=1
pkgdesc="Python bindings for the Nautilus Extension API"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/NautilusPython"
license=(GPL2)
depends=(nautilus python-gobject)
makedepends=(gtk-doc meson)
source=(https://download.gnome.org/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
sha256sums=('fc49c104fb32a0ad195a66b0136784cd19d10d8b3e8d59d5ee7f73e8f94e928f')

build() {
  meson build $pkgbase-$pkgver \
    --prefix=/usr \
    -D docs=enabled
  meson compile -C build
}

package_python-nautilus() {
  meson install -C build --destdir="$pkgdir"
}
