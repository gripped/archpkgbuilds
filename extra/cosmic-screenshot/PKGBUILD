# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-screenshot
pkgver=1.0.0.beta.1.1
pkgrel=1
pkgdesc='Utility for capturing screenshots via XDG Desktop Portal'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-screenshot
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  xdg-desktop-portal-cosmic
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=ba59f95cb1ef28e6836535fc6e1874b5667f76f2
source=(
  git+https://github.com/pop-os/cosmic-screenshot.git#tag=${_tag}
)
b2sums=('d7ca599be98f0716460bcfe00832ba5acd479799370df0456ec14c0bc9c8b4780ca74441e15df39a83c82bb466d54f06c4f8e574eab061aa1d119c594ad85ec1')

prepare() {
  cd cosmic-screenshot
  cargo fetch --locked
}

pkgver() {
  cd cosmic-screenshot
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-screenshot
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-screenshot
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
