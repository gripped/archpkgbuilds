# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-screenshot
pkgver=1.0.0.beta.1
pkgrel=1
pkgdesc='Utility for capturing screenshots via XDG Desktop Portal'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-screenshot
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  xdg-desktop-portal-cosmic
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=5939fbe3a2c53e37973918ba781e165ec9bb495d
source=(
  git+https://github.com/pop-os/cosmic-screenshot.git#tag=${_tag}
)
b2sums=('b359303290a2cc0f61021e524505126370d4094c7a0812d1d6c946b7c1023665ccf60efc59a81cb9ee3fde1688578e1701babce6f66f4dc7c2f714f60123b36a')

prepare() {
  cd cosmic-screenshot
  cargo fetch --locked
}

pkgver() {
  cd cosmic-screenshot
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-screenshot
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-screenshot
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
