# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-screenshot
pkgver=1.0.0.beta.4
pkgrel=1
pkgdesc='Utility for capturing screenshots via XDG Desktop Portal'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-screenshot
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  xdg-desktop-portal-cosmic
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=8ca2c6a662ba1b8a8790cd3caf1a0fe28bb85845
source=(
  git+https://github.com/pop-os/cosmic-screenshot.git#tag=${_tag}
)
b2sums=('31cdc86c9e3eebb6c09efac2619a4785d4649566add793de3b7b4048e2c5a266bd6c60e78d78d08e6e04186c128a205952b7aa42f916442f215db8ec0e622b83')

prepare() {
  cd cosmic-screenshot
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-screenshot
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-screenshot
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-screenshot
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
