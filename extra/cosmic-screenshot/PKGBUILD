# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-screenshot
pkgver=1.0.0.alpha.4
pkgrel=1
pkgdesc='Utility for capturing screenshots via XDG Desktop Portal'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-screenshot
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  xdg-desktop-portal-cosmic
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=031eb6686196e3dd4d7876ae30572522edc110b9
source=(
  git+https://github.com/pop-os/cosmic-screenshot.git#tag=${_tag}
  cosmic-screenshot-lto.patch
)
b2sums=('d5f9f94fbb7cc245851eda93be16f341f798195c19874f205b6e51143ea33f5299c0f8efd2e839e870ee2fb1f6edd177cbc0d6b06eebf0758732261fe28d1dbc'
        'cd1479de7cac5d58fd085ab4a9d694dfdee4cf7577ca531bbbb978cff512f52fec0cb9505a3e51075a0b741a7b3cb1cf88ff6725242d0e220135ef4d9e10b83b')

prepare() {
  cd cosmic-screenshot
  patch -Np1 -i ../cosmic-screenshot-lto.patch
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-screenshot
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-screenshot
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-screenshot
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
