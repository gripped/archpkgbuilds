# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-screenshot
pkgver=1.0.0.alpha.5
pkgrel=1
pkgdesc='Utility for capturing screenshots via XDG Desktop Portal'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-screenshot
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  xdg-desktop-portal-cosmic
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=d920352253cbc7e0861cfca3e7e70a407231d5a8
source=(
  git+https://github.com/pop-os/cosmic-screenshot.git#tag=${_tag}
  cosmic-screenshot-lto.patch
)
b2sums=('340a73f33fa05601cacf666be6ab11abd641ea6e1214fd685e3c4ba85d1732fa16cac3685d55cdb57f479c443b4cc81a656a439c9c58f873cb5dbf9ab51bcc61'
        'cd1479de7cac5d58fd085ab4a9d694dfdee4cf7577ca531bbbb978cff512f52fec0cb9505a3e51075a0b741a7b3cb1cf88ff6725242d0e220135ef4d9e10b83b')

prepare() {
  cd cosmic-screenshot
  patch -Np1 -i ../cosmic-screenshot-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-screenshot
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-screenshot
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-screenshot
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
