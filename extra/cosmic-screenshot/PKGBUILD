# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-screenshot
pkgver=1.0.0.alpha.5.1
pkgrel=1
pkgdesc='Utility for capturing screenshots via XDG Desktop Portal'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-screenshot
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  xdg-desktop-portal-cosmic
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=10a564d9db675b2a0a5c58a3055b0ac578ae9f22
source=(
  git+https://github.com/pop-os/cosmic-screenshot.git#tag=${_tag}
)
b2sums=('41f1daddede460f0df543b1a618aead0cec115b1cfed9981be407120388d7cf87e93810135ded4ce3547c365b0ac1d43178de346d6e412a5ecec2e5536f25326')

prepare() {
  cd cosmic-screenshot
  cargo fetch --locked
}

pkgver() {
  cd cosmic-screenshot
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-screenshot
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-screenshot
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
