# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=0.44.0
pkgrel=1
pkgdesc="The Grammar Checker for Developers"
url='https://github.com/elijah-potter/harper'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/elijah-potter/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('bb5bb86ded921a63629d3db2731282fe1cdfc755bdb6c88a9e6d006970bbdf84')
b2sums=('b8ec499f817ebb37ff61737df9cbc83501dc51339c87e88924716cc8e98020c0ad445e2f7b58dd4cdb83f09e2bd94111ce22f8cb1f07eee30cdde11e7e8dc7b4')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
