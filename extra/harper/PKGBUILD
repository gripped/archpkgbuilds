# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=1.7.0
pkgrel=1
pkgdesc='The Grammar Checker for Developers'
arch=(x86_64)
url='https://github.com/Automattic/harper'
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo
)
options=(!lto)
source=(https://github.com/Automattic/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('da56b2fd6163f7bb5e51b43d8658a402d3773e708eefe8758143301285a16959')
b2sums=('7f8a45583cc9a6a94de22842d9df3dce5e71f676be3e5782e3180e8515b023989c6702e5b84b4a879f3f5ce5c488640a78d225acf694d5b88a6a9e0bf856a199')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
