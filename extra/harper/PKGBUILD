# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=0.29.1
pkgrel=1
pkgdesc="The Grammar Checker for Developers"
url='https://github.com/elijah-potter/harper'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/elijah-potter/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('9ac27ed81ee0b6e076adcb6b08abcc0633ba23df73d982a6f12ee6144534054a')
b2sums=('1de9871548020bf32bc01a8d0b20430578922bdec5de3c1bf626aa194520ef24db6958719ec523d1d572a9adfe06a7d841bff914ddbad0841fb76d7e19e402e9')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
