# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=1.9.0
pkgrel=1
pkgdesc='The Grammar Checker for Developers'
arch=(x86_64)
url='https://github.com/Automattic/harper'
license=(Apache-2.0)
depends=(
  glibc
  libgcc
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0ff3acf18dfcd44f9efa07f36a898cbd8107ede90bcea2aa88c85a07667f930d8381c7b42ae3f07b93d58443b1551e18d9f4e2889b2027fd7adec8cd5eedc633')
b2sums=('708cfe56b82341194fe9a728cb832dd1bc4c2321a669cf614e07d534b7130906ff16474fd6a4ca68c77559e3867bda5be63c2558482c2023b2ae103d14bc5360')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

check() {
  cd "$pkgname"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  install -Dm 755 -t "$pkgdir/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
