# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=1.8.0
pkgrel=1
pkgdesc='The Grammar Checker for Developers'
arch=(x86_64)
url='https://github.com/Automattic/harper'
license=(Apache-2.0)
depends=(
  glibc
  libgcc
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0f848bb3713d5c40178cd4339b4157717d0124fdb1e7cd4c1f0f176cfd03365ffb3de5619cf3f111c0f565e86d0ee842eff22c56f09c17b1deab193e40a3244b')
b2sums=('e9068be5ad0bad30318175097e5b1ce0c10d8465135e2da86100088da4081773bd928cd76f682cf8fa729255126660d3c2b98a66ba3dc4c633c47fe9929a858f')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

check() {
  cd "$pkgname"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  install -Dm 755 -t "$pkgdir/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
