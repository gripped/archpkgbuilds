# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=1.0.0
pkgrel=1
pkgdesc="The Grammar Checker for Developers"
url='https://github.com/Automattic/harper'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/Automattic/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('70f188c5fc1e99187ddaa305d671f9c8e940b8f4368679a70866b55eea66710f')
b2sums=('dc70b0364b7b2d79202385e19b38b162838202ca0eb5865bb91e8f12b44b09a0f2ad920aab497d39bb130b7adca3410ec5b582aaa1807ea14cadec1ae7bc66aa')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
