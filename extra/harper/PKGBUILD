# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=0.14.0
pkgrel=1
pkgdesc="The Grammar Checker for Developers"
url='https://github.com/elijah-potter/harper'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/elijah-potter/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('af07f2a621b876cb77a4dfa376bdab0df2e1b0b210816bb26e73e56878367634')
b2sums=('14de4af3c020f60c1a433d85f130c731cb3c762a01db41476b32d8372e76c9119a95aa06a434789bd03baa2018e5c56f7dacca8df011200f1e787fdbaa0656a8')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
