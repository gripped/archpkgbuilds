# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=0.24.0
pkgrel=1
pkgdesc="The Grammar Checker for Developers"
url='https://github.com/elijah-potter/harper'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/elijah-potter/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0d03374ef03ef189fa3824439fb7075cf309a0284857b6d8d22107f164ad8487')
b2sums=('6f1ea6bc59f400eb1e593846ed24ecf9682a5e67f4afcd5d6132bd7d65f22293cd4de6162579fc17fc1cc25450b1a6cd767fbd1165a04fd2545fe363f4ae0a2b')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
