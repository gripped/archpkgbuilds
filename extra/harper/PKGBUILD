# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=harper
pkgver=1.5.1
pkgrel=1
pkgdesc="The Grammar Checker for Developers"
url='https://github.com/Automattic/harper'
arch=('x86_64')
license=('Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/Automattic/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('37ddaa7a76738d41914b6235054c4aa255b5c029f19e42fd4e44c9a4fa44daf0')
b2sums=('cc7d01802a521c046aebb328832ea87e9d38d98dec5ffdfd8c299ada86498d5cd354ee220628b55af589564c147ce25320b8cda6365bb78e0bf6d929c0751f6e')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/harper-cli \
    target/release/harper-ls
}

# vim: ts=2 sw=2 et:
