# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gupnp-tools
pkgver=0.12.1
pkgrel=3
pkgdesc='Utilities and demos to work with UPnP'
arch=(x86_64)
url='https://wiki.gnome.org/Projects/GUPnP'
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gssdp
  gtk3
  gtksourceview4
  gupnp
  gupnp
  gupnp-av
  hicolor-icon-theme
  libsoup3
  libxml2
)
makedepends=(
  git
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/gupnp-tools.git#tag=$pkgname-$pkgver")
b2sums=(3f43782e88b9a1ca42c2366a4a3a9eac6c4843f89d6ff88165d980e51244cd94f3df958b0f9b519219e47e69376f8bf52f57fdbcc42432f17c07ab10319c18d8)

prepare() {
  cd $pkgname
  sed -i '/-Werror/d' meson.build
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
