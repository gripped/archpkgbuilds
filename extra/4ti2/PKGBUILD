# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=4ti2
pkgver=1.6.14
pkgrel=1
pkgdesc='A software package for algebraic, geometric and combinatorial problems on linear spaces'
arch=(x86_64)
url='https://4ti2.github.io/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         glpk
         gmp
         sh)
makedepends=(git)
source=(git+https://github.com/4ti2/4ti2#tag=Release_${pkgver//./_})
sha256sums=('eb5de78199de6b5d84b7d19650054edf7d2bca3dc9a7203cbacc7a39190bf1b5')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
