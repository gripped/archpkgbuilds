# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=solanum
pkgver=6.0.0
pkgrel=1
pkgdesc='Pomodoro timer for the GNOME desktop'
arch=(x86_64)
url='https://apps.gnome.org/Solanum/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
source=(
  "git+https://gitlab.gnome.org/World/Solanum.git?signed#tag=$pkgver"
  solanum-appdata-path.patch
)
b2sums=('734a59e908bd3198d7f1045a00fa32f6ff991ed67951066f6e67d02d6395c070265918ea5c78c91dbb6e7edbb425f6b3ae7c3058ae68df2b5a52a551704fa654'
        '55c01c0f38f170f84cb223510b761370e7d98da2ea9bc6bce02430719033ec694c8524eea6a3de1d522ab29c81e2528f9ccc5f4a18b264a1ed1ef07015c27f8a')
validpgpkeys=(173547C8EB86C22FF5B6EF790F974ABB3478DC48) # Christopher Davis (disroot.org) <brainblasted@disroot.org>

prepare() {
  cd Solanum

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.gnome.org/World/Solanum/-/merge_requests/84
  git apply -3 ../solanum-appdata-path.patch
}

build() {
  arch-meson Solanum build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
