# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=azote
pkgver=1.15.1
pkgrel=1
pkgdesc="Wallpaper & color manager for wlroots-based compositors and some X11 WMs"
url="https://github.com/nwg-piotr/azote"
arch=('any')
license=('GPL-3.0-or-later')
depends=('python' 'python-setuptools' 'python-gobject' 'python-pillow' 'gtk3' 'python-cairo' 'python-send2trash')
makedepends=('python-build' 'python-installer' 'python-wheel')
optdepends=('imagemagick: for screen color picker'
            'grim: for screen color picker on wlroots'
            'slurp: for screen color picker on wlroots'
            'maim: for screen color picker on X11-based WMs'
            'libappindicator-gtk3: for tray status icon'
            'python-yaml: for alacritty.yml toolbox'
            'swaybg: for setting background on wlroots-based compositors'
            'feh: for setting background on X11-based WMs'
            'python-xlib: for checking outputs on X11-based WMs'
            'python-pillow-jxl-plugin: for JPEG XL support in Pillow'
            'wlr-randr: for checking outputs on wlroots-based compositors other than Sway and Hyprland')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('e7a64c6c14e1cd52222fdcfb23eef5275925ef68711b094bd5f59cf6a465b043')

build() {
	cd "${pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	install -Dm 644 "dist/${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
	install -Dm 644 dist/indicator*.png -t "${pkgdir}/usr/share/${pkgname}/"
	install -Dm 644 "dist/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
