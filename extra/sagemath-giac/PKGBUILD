# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-giac
pkgver=0.1.3
pkgrel=3
pkgdesc='Support for using Giac within SageMath'
arch=(x86_64)
url='https://github.com/sagemath/sagemath-giac'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         giac
         glibc
         python
         sagemath)
makedepends=(cython
             git
             meson-python
             python-build
             python-installer)
checkdepends=(python-pytest)
source=(git+https://github.com/sagemath/sagemath-giac#tag=$pkgver
        cython-3.2.patch)
sha256sums=('cbd61577b139a602da7be66259aa183324faf8fd6b1c0441684351756b50e719'
            '7169a2992b4d59c927f985bd8c6dffd7fda3c289ef7c71f239c630c04987308c')

prepare() {
  cd $pkgname
  patch -p1 -i ../cython-3.2.patch # https://github.com/sagemath/sagemath-giac/issues/5
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
