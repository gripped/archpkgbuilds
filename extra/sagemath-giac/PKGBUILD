# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-giac
pkgver=0.1.1
pkgrel=1
pkgdesc='Support for using Giac within SageMath'
arch=(x86_64)
url='https://github.com/sagemath/sagemath-giac'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         giac
         glibc
         python
         sagemath)
makedepends=(cython
             git
             meson-python
             python-build
             python-installer)
checkdepends=(python-pytest)
source=(git+https://github.com/sagemath/sagemath-giac#tag=$pkgver)
sha256sums=('f2347a9501b39b53865236ff92146867813794c6312261d7a85ae671db2f339a')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
