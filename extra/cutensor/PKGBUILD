# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=cutensor
pkgver=2.4.0.2
_cudaver=13
pkgrel=2
pkgdesc="High-performance CUDA library for tensor primitives"
arch=(x86_64)
url="https://docs.nvidia.com/cuda/cutensor/latest/index.html"
license=(LicenseRef-NVIDIA-cuTENSOR)
depends=(
  "cuda>=$_cudaver"
  gcc-libs
  glibc
)
optdepends=(
  'nccl: for the multi-process extension (cuTENSORMp)'
  'openmpi: for the multi-process extension (cuTENSORMp)'
)
options=(!strip)
source=(https://developer.download.nvidia.com/compute/$pkgname/redist/lib$pkgname/linux-x86_64/lib$pkgname-linux-x86_64-${pkgver}_cuda$_cudaver-archive.tar.xz)
b2sums=('c2912a465fd28fc8ee1315cd7ac131dfffd39bfca0bbdb2a2470c245c1732a292bd0b958b0c15b815da79a8243f03d263083ecde109d5f1e765a561d21fe0221')

package() {
  cd lib$pkgname-linux-x86_64-${pkgver}_cuda${_cudaver}-archive

  install -vdm 755 "$pkgdir"/usr/
  cp -r lib include "$pkgdir"/usr/

  # remove static libs
  rm "$pkgdir"/usr/lib/*.a

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
