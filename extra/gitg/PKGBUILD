# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gitg
pkgver=44
pkgrel=6
epoch=2
pkgdesc="GNOME GUI client to view git repositories"
url="https://wiki.gnome.org/Apps/Gitg"
arch=(x86_64)
license=(GPL)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glibc
  glib2
  gpgme
  gsettings-desktop-schemas
  gspell
  gtk3
  gtksourceview4
  hicolor-icon-theme
  json-glib
  libdazzle
  libgee
  libgirepository
  libgit2-glib
  libhandy
  libpeas
  libsecret
  libxml2
  pango
)
makedepends=(
  git
  gobject-introspection
  meson
  python-gobject
  vala
)
optdepends=('python-gobject: Python extension support')
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gitg.git#tag=v$pkgver")
b2sums=('2db182b0762cef301354397ee40eae3d79697ce80499707aeda9b62f5044074dce527216e6a4032f19564e15d994ea52475f5218738dd15c11732c041cfeb660')

prepare() {
  cd gitg

  # remove illegal positional argument to i18n.merge_file()
  git cherry-pick -n 1978973b12848741b08695ec2020bac98584d636

  # Set prgname to application ID
  # https://gitlab.gnome.org/GNOME/gitg/-/merge_requests/259
  git cherry-pick -n 97fa362910fae57d9d892fd54556075f10d0c2b2

  # Fix icon name
  # https://gitlab.gnome.org/GNOME/gitg/-/merge_requests/257
  git cherry-pick -n cf646c22bde86b4ecbeafd2ebc3c1be8ad3bc8ba

  # Set icon for the window
  # https://gitlab.gnome.org/GNOME/gitg/-/merge_requests/258
  git cherry-pick -n 71e494c96544c56a379678e8a86754a68cdbfb8b
}

build() {
  arch-meson gitg build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

# vim:set sw=2 sts=-1 et:
