# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-task
pkgver=3.43.2
pkgrel=1
pkgdesc='A task runner written in Go'
arch=('x86_64')
url='https://taskfile.dev'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
install=go-task.install
source=(
  "$pkgname::git+https://github.com/go-task/task#tag=v$pkgver"
  'rename-completions.patch'
  'fix-version.patch'
)
sha512sums=('48a99b992df0f07e16693b15cd7f2886d4f8f8eb940af6f0d333ba7297377ddb281302d7c22d1ca079831fce6dc410f7d3f6c495376bde09bf4b7a8590497fa1'
            '1b31bd63e3ce982c9a1714eafbc2c6ac1adc6b50b5187b11dddbfc8c5dbb53bdb0fff2a0200d03f71af61ee3b34f7a6af34585e056b0ae9ad889607c54cb0369'
            '5ca079e3d2875d1efe8bba390ae8960445f42b53f309b50cff8a8c13dc3d5ca06065a0bd9ea4504817bbdccc36f7ff39f937817f4feb28fd1d3006dbf382636a')
b2sums=('8953646c7e7c679bbb81e3e90d16ce66c81af7d2b86da08fb1b9460db78c4ad36897cf919b98168ba0021a408f569706f0058bfae2366ff4b993253b4b93318a'
        '5b515faeaaae296e102ee0309fb6e4cf99a2d1c3d23e2a333da019973b9204a357f288be6ab74a88fc7b85d802983bc8b877a25ed7b578fb2deef379f272d309'
        '99f5fd755334a9c5b0a0375d4cdda6e640444d7550d1c105d41f4b94da9f25f74331465b90bc2f79ab0daa5e3d95d9d27d62cce86646f5c22b6c95947c919a0c')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # rename completions from `task` to `go-task`
  patch -p1 -i "$srcdir/rename-completions.patch"

  # fix version
  patch -p1 -i "$srcdir/fix-version.patch"
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-task/task/v3/internal/version.version=${pkgver} \
    -X github.com/go-task/task/v3/internal/version.commit=$(git rev-parse --short HEAD)" \
    -o go-task \
    ./cmd/task
}

# tests make network calls, yuck
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" go-task

  # shell completions
  install -vDm644 completion/bash/task.bash "$pkgdir/usr/share/bash-completion/completions/go-task"
  install -vDm644 completion/fish/task.fish "$pkgdir/usr/share/fish/vendor_completions.d/go-task.fish"
  install -vDm644 completion/zsh/_task "$pkgdir/usr/share/zsh/site-functions/_go-task"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
