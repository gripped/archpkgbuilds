# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-task
pkgver=3.45.4
pkgrel=2
pkgdesc='A task runner written in Go'
arch=(x86_64)
url='https://taskfile.dev'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
install=go-task.install
source=(
  "$pkgname::git+https://github.com/go-task/task#tag=v$pkgver"
  rename-completions.patch
  fix-version.patch
)
sha512sums=('e7a4bc4256e18b9fc590f308acccb8b7b574040b9dbbc7857715261d15db39677af12c104a89960980d606c77c65c11f5dc56c9f37a8e652ba68ed1aceb5983a'
            '1b31bd63e3ce982c9a1714eafbc2c6ac1adc6b50b5187b11dddbfc8c5dbb53bdb0fff2a0200d03f71af61ee3b34f7a6af34585e056b0ae9ad889607c54cb0369'
            '5ca079e3d2875d1efe8bba390ae8960445f42b53f309b50cff8a8c13dc3d5ca06065a0bd9ea4504817bbdccc36f7ff39f937817f4feb28fd1d3006dbf382636a')
b2sums=('fbcccb0e31db94e802448c0316fa853f0cbe6fe97f90bbe70001e9a49054382cb13af69f754b4d5dca8c6c90e839b43407cc857f7cec1e39459210f954a0166d'
        '5b515faeaaae296e102ee0309fb6e4cf99a2d1c3d23e2a333da019973b9204a357f288be6ab74a88fc7b85d802983bc8b877a25ed7b578fb2deef379f272d309'
        '99f5fd755334a9c5b0a0375d4cdda6e640444d7550d1c105d41f4b94da9f25f74331465b90bc2f79ab0daa5e3d95d9d27d62cce86646f5c22b6c95947c919a0c')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # rename completions from `task` to `go-task`
  patch -p1 -i "$srcdir/rename-completions.patch"

  # fix version
  patch -p1 -i "$srcdir/fix-version.patch"
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -s -w -buildid='' \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-task/task/v3/internal/version.version=${pkgver} \
    -X github.com/go-task/task/v3/internal/version.commit=$(git rev-parse --short HEAD)" \
    -o go-task \
    ./cmd/task
}

# tests make network calls, yuck
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" go-task

  # shell completions
  install -vDm644 completion/bash/task.bash "$pkgdir/usr/share/bash-completion/completions/go-task"
  install -vDm644 completion/fish/task.fish "$pkgdir/usr/share/fish/vendor_completions.d/go-task.fish"
  install -vDm644 completion/zsh/_task "$pkgdir/usr/share/zsh/site-functions/_go-task"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
