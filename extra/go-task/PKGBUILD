# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-task
pkgver=3.48.0
pkgrel=1
pkgdesc='A task runner written in Go'
arch=(x86_64)
url='https://taskfile.dev'
license=(MIT)
depends=(glibc)
makedepends=(git go)
install=go-task.install
source=(
  "$pkgname::git+https://github.com/go-task/task#tag=v$pkgver"
  rename-completions.patch
  fix-version.patch
)
sha512sums=('2b83a69aae11a5570599adf30b684bb2ca60d7f09e56c8d57a12e08a1d674fd01e42ce9c6ce175cfc1a50ea950b01fc3c48a9af102c23f11f3c8fca6c2c5151b'
            '3fe434bd03b8f377b3fb31312ff6f1003f3b0b3e852221782ebe7a11c794eaecbbc3bc77548c4a7326c69c30ffe8d4f4e33c8fe0a3fe1e6785ef3084f8841e39'
            '5ca079e3d2875d1efe8bba390ae8960445f42b53f309b50cff8a8c13dc3d5ca06065a0bd9ea4504817bbdccc36f7ff39f937817f4feb28fd1d3006dbf382636a')
b2sums=('44f3d330aa9cc57da52878c6edf44d481b958b110f7127c3700d12a620ae76181b3bfac48dd002fea4e951bee6d972b3ac7b849b9ef558f364f19158770d7aea'
        'ea350cac916f54688c29dcd9d9e5c69a30fb4b5c37eb62dfaa4f19d59a9cc33f0a03520aa2ac1658eb534ed0fe8c0a552f3cbc8a08041f05986395d04c1ad9e9'
        '99f5fd755334a9c5b0a0375d4cdda6e640444d7550d1c105d41f4b94da9f25f74331465b90bc2f79ab0daa5e3d95d9d27d62cce86646f5c22b6c95947c919a0c')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # rename completions from `task` to `go-task`
  # TODO: set env var? see upstream ref:
  # https://github.com/go-task/task/pull/2495
  patch -p1 -i "$srcdir/rename-completions.patch"

  # fix version
  patch -p1 -i "$srcdir/fix-version.patch"
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -s -w -buildid='' \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-task/task/v3/internal/version.version=${pkgver} \
    -X github.com/go-task/task/v3/internal/version.commit=$(git rev-parse --short HEAD)" \
    -o go-task \
    ./cmd/task
}

# tests make network calls, yuck
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" go-task

  # shell completions
  install -vDm644 completion/bash/task.bash "$pkgdir/usr/share/bash-completion/completions/go-task"
  install -vDm644 completion/fish/task.fish "$pkgdir/usr/share/fish/vendor_completions.d/go-task.fish"
  install -vDm644 completion/zsh/_task "$pkgdir/usr/share/zsh/site-functions/_go-task"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
