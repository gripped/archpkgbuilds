# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-task
pkgver=3.47.0
pkgrel=1
pkgdesc='A task runner written in Go'
arch=(x86_64)
url='https://taskfile.dev'
license=(MIT)
depends=(glibc)
makedepends=(git go)
install=go-task.install
source=(
  "$pkgname::git+https://github.com/go-task/task#tag=v$pkgver"
  rename-completions.patch
  fix-version.patch
)
sha512sums=('b132ec6e6a571176e7fa1364f59e0a2170a228401a9df5577cfc36f9f3728689a116a427af28bcb4b17d4c120ce4424bb8e0ab8e73b88bcda8bcbd3f70048aef'
            '5bade82020c49006a68d38f54bab9e55068dab23222bfebef0498c5e139707977f413309ccd3ee326e4b1573c0d4ebd9783ab6f7c2e0dc0ad2631109867621a8'
            '5ca079e3d2875d1efe8bba390ae8960445f42b53f309b50cff8a8c13dc3d5ca06065a0bd9ea4504817bbdccc36f7ff39f937817f4feb28fd1d3006dbf382636a')
b2sums=('3af7a03e4c1998fb2cc5dfed7a3100332bcb9efab8459fc8c1f2728a852dc3ca16dde0eeb461c8f2c956d753829e02f0cc5907fef4f129ae604d7769b8281952'
        '3fee2c027faf494dcbded0a5aea84d2026fc37da7c7183847aa1982dd4b42450330fcab73f52fcad736d28342115838a15db1d240698ce7d4030dbc814dd27aa'
        '99f5fd755334a9c5b0a0375d4cdda6e640444d7550d1c105d41f4b94da9f25f74331465b90bc2f79ab0daa5e3d95d9d27d62cce86646f5c22b6c95947c919a0c')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # rename completions from `task` to `go-task`
  # TODO: set env var? see upstream ref:
  # https://github.com/go-task/task/pull/2495
  patch -p1 -i "$srcdir/rename-completions.patch"

  # fix version
  patch -p1 -i "$srcdir/fix-version.patch"
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -s -w -buildid='' \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-task/task/v3/internal/version.version=${pkgver} \
    -X github.com/go-task/task/v3/internal/version.commit=$(git rev-parse --short HEAD)" \
    -o go-task \
    ./cmd/task
}

# tests make network calls, yuck
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" go-task

  # shell completions
  install -vDm644 completion/bash/task.bash "$pkgdir/usr/share/bash-completion/completions/go-task"
  install -vDm644 completion/fish/task.fish "$pkgdir/usr/share/fish/vendor_completions.d/go-task.fish"
  install -vDm644 completion/zsh/_task "$pkgdir/usr/share/zsh/site-functions/_go-task"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
