# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-task
pkgver=3.42.1
pkgrel=1
pkgdesc='A task runner written in Go'
arch=('x86_64')
url='https://taskfile.dev'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
install=go-task.install
source=(
  "$pkgname::git+https://github.com/go-task/task#tag=v$pkgver"
  'rename-completions.patch'
)
sha512sums=('64cbba9d078e0991b0ed6c2c83cfd5ca5f08da386203b5b749dac6cd2db8d068ba9eaf3f79543a72e2c0038211916fd3942885a95f816986a732ee3b0e26995a'
            'c0929f3a37b774fcb9e2f28f74541ec27ff94a58a6be50774eb7492480789c2820faedb533bb2a69811396bb8a1863c96d3dbab8ba04afcfbdaf0b4e5fcfaf6b')
b2sums=('87a76aef084a131785a531f98b79aa3f27b98bd71aad1adae78a8a20ee22cf841c2e6a7bd9b72ebdf055a4955b798cf32a966846cee52a114c0b2578fac768ee'
        '76d3c7c88d9716c4f6ec9735139a01c9e7d73086266b1175edd15934062622735234277fefb3fa4774eee6677c38e2e19f0399284e4a43c67433bb24ced70c93')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # rename completions from `task` to `go-task`
  patch -p1 -i "$srcdir/rename-completions.patch"

  # fix version checksum
  # https://github.com/go-task/task/pull/1830
  git cherry-pick --no-commit 460e587c664494995152fa5f701d57743bd91e10
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-task/task/v3/internal/version.version=${pkgver} \
    -X github.com/go-task/task/v3/internal/version.sum=$(git rev-parse --short HEAD)" \
    -o go-task \
    ./cmd/task
}

# tests make network calls, yuck
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" go-task

  # shell completions
  install -vDm644 completion/bash/task.bash "$pkgdir/usr/share/bash-completion/completions/go-task"
  install -vDm644 completion/fish/task.fish "$pkgdir/usr/share/fish/vendor_completions.d/go-task.fish"
  install -vDm644 completion/zsh/_task "$pkgdir/usr/share/zsh/site-functions/_go-task"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
