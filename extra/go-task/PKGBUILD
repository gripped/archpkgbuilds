# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-task
pkgver=3.46.4
pkgrel=1
pkgdesc='A task runner written in Go'
arch=(x86_64)
url='https://taskfile.dev'
license=(MIT)
depends=(glibc)
makedepends=(git go)
install=go-task.install
source=(
  "$pkgname::git+https://github.com/go-task/task#tag=v$pkgver"
  rename-completions.patch
  fix-version.patch
)
sha512sums=('f6b6a7257fc45d53efba2f0ada6f987696514ae45a4b8602493710b8ededa6bef76cff44802a57d26ea6e24ee1a175ff051ce29ab22608e091d2f77297a53ddf'
            '828d43dee9d2514aa2d71983c4b525ab6704703f928f6b4fc4c9a4fc3f941c46fd2261ff6730af99011772b701fd8d8375d1a9419ccdea6df5beb342c804298b'
            '5ca079e3d2875d1efe8bba390ae8960445f42b53f309b50cff8a8c13dc3d5ca06065a0bd9ea4504817bbdccc36f7ff39f937817f4feb28fd1d3006dbf382636a')
b2sums=('e257d9b864c6a1278eb9ce2f60aaacd0bbc15b4201452cb3457b664b62b01b448c8c98b6760aab89df1832e34ff728f11b21e079cc628612dccc9e72f4d80ed4'
        '4b78a151e4652c79b8b53e9fefd752590b440c9fd2d1d92ba747edc13c2fe9cad2d5fbb1342e7cb581d3c2a041ef6f29ceafae1ab7acef9f6779bc9169084c53'
        '99f5fd755334a9c5b0a0375d4cdda6e640444d7550d1c105d41f4b94da9f25f74331465b90bc2f79ab0daa5e3d95d9d27d62cce86646f5c22b6c95947c919a0c')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # rename completions from `task` to `go-task`
  # TODO: set env var? see upstream ref:
  # https://github.com/go-task/task/pull/2495
  patch -p1 -i "$srcdir/rename-completions.patch"

  # fix version
  patch -p1 -i "$srcdir/fix-version.patch"
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -s -w -buildid='' \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-task/task/v3/internal/version.version=${pkgver} \
    -X github.com/go-task/task/v3/internal/version.commit=$(git rev-parse --short HEAD)" \
    -o go-task \
    ./cmd/task
}

# tests make network calls, yuck
#check() {
#  cd "$pkgname"
#
#  go test -v ./...
#}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" go-task

  # shell completions
  install -vDm644 completion/bash/task.bash "$pkgdir/usr/share/bash-completion/completions/go-task"
  install -vDm644 completion/fish/task.fish "$pkgdir/usr/share/fish/vendor_completions.d/go-task.fish"
  install -vDm644 completion/zsh/_task "$pkgdir/usr/share/zsh/site-functions/_go-task"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
