# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-util-dfm
pkgver=1.2.25
pkgrel=1
pkgdesc='Utilities of deepin file manager'
arch=('x86_64')
license=('LGPL')
url="https://github.com/linuxdeepin/util-dfm"
depends=('libisoburn' 'libmediainfo' 'qt5-base' 'udisks2' 'glib2' 'gcc-libs' 'util-linux-libs'
         'libsecret')
makedepends=('cmake' 'ninja')
source=("https://github.com/linuxdeepin/util-dfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9da449eb89c5b8142cf650255a2aa77f097865bfa09f350320fa63d959e0189cb4da93119526cc52b900dcce0d2ae5a106fdd8e29cef56f3e5a12e96bbf3bfc0')

build() {
  cmake -G Ninja -B build -S util-dfm-$pkgver -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
