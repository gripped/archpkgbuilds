# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-util-dfm
pkgver=1.2.16
pkgrel=1
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=('x86_64')
license=('LGPL')
url="https://github.com/linuxdeepin/util-dfm"
depends=('libisoburn' 'libmediainfo' 'qt5-base' 'udisks2' 'glib2' 'gcc-libs' 'util-linux-libs'
         'libsecret')
makedepends=('cmake' 'ninja')
source=("https://github.com/linuxdeepin/util-dfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('83713d949c22d8426e01cfffd466fae8e747a74dcd63747caa3ecc03cbb7bf4d4b5b31db9aad556d601cf3001f4c8355adb4b3155a4e3508de39c89f49d3b286')

build() {
  cmake -G Ninja -B build -S util-dfm-$pkgver -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
