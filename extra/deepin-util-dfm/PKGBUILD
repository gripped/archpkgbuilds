# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-util-dfm
pkgver=1.2.17
pkgrel=1
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=('x86_64')
license=('LGPL')
url="https://github.com/linuxdeepin/util-dfm"
depends=('libisoburn' 'libmediainfo' 'qt5-base' 'udisks2' 'glib2' 'gcc-libs' 'util-linux-libs'
         'libsecret')
makedepends=('cmake' 'ninja')
source=("https://github.com/linuxdeepin/util-dfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6f49a1ef23ddc277e4b7d49b573c10d8d500c77d8d4fa4b4dc97b75974924ecdcd6fc613353731da9fda941ea861ef5a23014abee7411902fad1efe44776b46b')

build() {
  cmake -G Ninja -B build -S util-dfm-$pkgver -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
