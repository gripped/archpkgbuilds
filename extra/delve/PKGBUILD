# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=delve
pkgver=1.24.0
pkgrel=1
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/go-delve/delve"
license=('MIT')
makedepends=('go' 'git')
optdepends=('debuginfod: debuginfod support')
options=(!lto strip)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/go-delve/delve/archive/v${pkgver}.tar.gz")
sha256sums=('940fc50d6338dfe515982ac5fcc3247616f23e2652048ac4f2b439ebd51741c1')

build(){
  cd "${pkgname}-${pkgver}"
  # export CGO_CPPFLAGS="${CPPFLAGS}"
  # export CGO_CFLAGS="${CFLAGS}"
  # export CGO_CXXFLAGS="${CXXFLAGS}"
  # export CGO_LDFLAGS="${LDFLAGS}"
  # export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
  export GOPATH="${srcdir}"
  go build -ldflags='-B gobuildid' ./cmd/dlv
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
