# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=one_gadget
pkgname=${_gemname}
pkgver=1.10.0
pkgrel=4
pkgdesc='The best tool for finding one gadget RCE in libc.so.6'
url='https://github.com/david942j/one_gadget'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-elftools')
makedepends=('ruby-rake' 'ruby-rdoc' 'ruby-bundler' 'ruby-yard' 'ruby-rspec')
provides=('ruby-one_gadget')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha512sums=('6b5060e1165352de358ae84b55299b0bab9b7a41b53973512de32cee99973e7bcea69640f2b8e07354ed12e5cf34e5f4df3c51399e636a9ee20710e903754839')
b2sums=('d6e932c4cbb6dd4dfb8d9b304c45e9c865e02c22fed530beeadee2fde59d98b798caf1f4abee6573b212feec030fdb45e490950272f6b10654429fc2f3bdba79')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed -r "s|, '< .*'||g" -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so,man} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
