# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Dmitry Orlov <me@mosquito.su> <me@mosquito.su>

pkgname=python-cysystemd
_gitname=cysystemd
pkgver=2.0.1
pkgrel=2
pkgdesc="SystemD wrapper on Cython"
arch=(x86_64)
url="http://github.com/mosquito/cysystemd"
license=(Apache-2.0)
depends=(
  glibc
  python
  systemd-libs
)
makedepends=(
  cython
  git
  python-packaging
  python-setuptools
  python-installer
  python-build
)
source=("git+https://github.com/mosquito/cysystemd.git#tag=${pkgver}")
sha256sums=('c14f5e4f42c38587e6405191cc7fca0695b5b1bbe4d3c39d4796abd1d4fc07f8')

build() {
  cd "${srcdir}/${_gitname}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${_gitname}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
