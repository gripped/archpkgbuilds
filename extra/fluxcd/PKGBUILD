# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=fluxcd
pkgver=2.5.0
pkgrel=1
pkgdesc='Open and extensible continuous delivery solution for Kubernetes'
arch=('x86_64')
url='https://fluxcd.io'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go' 'kustomize')
replaces=('fluxctl')
options=('!lto')
source=("$pkgname::git+https://github.com/fluxcd/flux2#tag=v$pkgver")
sha512sums=('0b2c4a5fc625404ff834ddd647a55d20fde0cc3951a1777506698ed0ccfe5083b97eb2acf9c8a53a1ec26ec926d228616e603a4fbaaf0ad3f2babef5630995ce')
b2sums=('635d5ba48f0acc087ee0ae2674fa6c498eedbebe0119d4a9e066bbb2c8369df13950f47a8ddd96cbe7fc69aef5e18dd18b612678faecdf8942f06c98128abc2b')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # generate manifests
  # https://github.com/fluxcd/flux2/blob/c5b5263b1bd826e792b3e5607857b7cd6d981b62/.github/workflows/release.yaml#L55-L57
  make cmd/flux/.manifests.done
  ./manifests/scripts/bundle.sh "" ./output manifests.tar.gz
  kustomize build ./manifests/install > ./output/install.yaml

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.VERSION=${pkgver}" \
    -o build \
    ./cmd/...

  # generate shell completions
  cd build
  for shell in bash fish zsh; do
    ./flux completion "$shell" > "completion.$shell"
  done
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname/build"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" flux

  # shell completions
  install -vDm644 completion.bash "$pkgdir/usr/share/bash-completion/completions/flux"
  install -vDm644 completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/flux.fish"
  install -vDm644 completion.zsh "$pkgdir/usr/share/zsh/site-functions/_flux"
}
