# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=fluxcd
pkgver=2.8.1
pkgrel=1
pkgdesc='Open and extensible continuous delivery solution for Kubernetes'
arch=(x86_64)
url='https://fluxcd.io'
license=(Apache-2.0)
depends=(glibc)
makedepends=(git go kustomize)
replaces=(fluxctl)
source=("$pkgname::git+https://github.com/fluxcd/flux2#tag=v$pkgver")
sha512sums=('e207064643b8e7b8c4411770bbb38a87ef01f09ce0cc955ab06c44d6f13d40324959310226f559e2bad793c3a93bb22f3dfee5c63de977b65ed91af788120679')
b2sums=('91b7af32387a6f606bd1ad57ac5a19ca0d0ac4e9470e9e81d31bfbfd495e7392e2ef65c4f26ce7c0b8e2d6cbff1ec743f1a886b188dd061c204146e5c2e0933f')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # generate manifests
  # https://github.com/fluxcd/flux2/blob/c5b5263b1bd826e792b3e5607857b7cd6d981b62/.github/workflows/release.yaml#L55-L57
  make cmd/flux/.manifests.done
  ./manifests/scripts/bundle.sh "" ./output manifests.tar.gz
  kustomize build ./manifests/install > ./output/install.yaml

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.VERSION=${pkgver}" \
    -o build \
    ./cmd/...

  # generate shell completions
  cd build
  for shell in bash fish zsh; do
    ./flux completion "$shell" > "completion.$shell"
  done
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname/build"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" flux

  # shell completions
  install -vDm644 completion.bash "$pkgdir/usr/share/bash-completion/completions/flux"
  install -vDm644 completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/flux.fish"
  install -vDm644 completion.zsh "$pkgdir/usr/share/zsh/site-functions/_flux"
}
