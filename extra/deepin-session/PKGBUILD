# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-session
pkgver=2.0.9
pkgrel=1
pkgdesc='New deepin session, based on systemd and existing projects'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session"
license=('GPL-3.0-or-later')
# deepin-session-ui: org.deepin.dde.Osd1
# deepin-kwin: dde-session@.service
# deepin-shell: dde-session-initialized.target.wants/dde-shell@DDE.service
# deepin-qt{5,6} plugins/integrations: enabled by dde-session
depends=('glibc' 'gcc-libs' 'glib2' 'libx11' 'libsecret' 'qt6-base' 'dtk6core'
         'libxcursor' 'libxfixes' 'systemd-libs' 'deepin-session-ui' 'deepin-kwin' 'deepin-shell'
         'deepin-qt5platform-plugins' 'deepin-qt5integration'
         'deepin-qt6platform-plugins' 'deepin-qt6integration')
makedepends=('git' 'cmake' 'ninja')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-session.git#tag=$pkgver")
sha512sums=('20023c18a03d75ce43590da9b97bf73b0e3fc67c79aca47ddd8db798c5f93c9b6ee68c3b1056bbf89062490e446fbd8b655578e4fbdc452cf4e48a1f9de9dcca')

prepare(){
  cd dde-session
  # qt 6.10.1
  git cherry-pick -n 8d9927d223ae99b09db7525c8170e550d0ff5226
  sed -i 's|/etc/os-version|/etc/uos-version|' tools/dde-version-checker/main.cpp
}

build() {
  cd dde-session
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-session
  DESTDIR="$pkgdir" ninja install
}
