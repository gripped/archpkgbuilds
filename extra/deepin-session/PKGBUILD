# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-session
pkgver=1.2.14
pkgrel=1
pkgdesc='New deepin session, based on systemd and existing projects'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session"
license=('GPL3')
# deepin-session-ui: org.deepin.dde.Osd1
# deepin-kwin: dde-session@.service
# deepin-shell: dde-session-initialized.target.wants/dde-shell@DDE.service
# deepin-qt{5,6} plugins/integrations: enabled by dde-session
depends=('glibc' 'gcc-libs' 'glib2' 'libx11' 'libsecret' 'qt5-base' 'gsettings-qt' 'dtkcore'
         'libxcursor' 'libxfixes' 'systemd-libs' 'deepin-session-ui' 'deepin-kwin' 'deepin-shell'
         'deepin-qt5platform-plugins' 'deepin-qt5integration'
         'deepin-qt6platform-plugins' 'deepin-qt6integration')
makedepends=('git' 'cmake' 'ninja')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-session.git#tag=$pkgver")
sha512sums=('85c3be2dda660ed291cac91d057b5c920436f2ccd8fd194ab1b285888c3956263dc57bc0c6019e8c16a6b2ccbfb56647e0c894388115cb0aca12fcf1358e7910')

prepare(){
  cd dde-session
  # DDE sets these envvars in /etc/X11/Xsession.d, which is Debian-specific
  # Patch from openSUSE
  git cherry-pick -n 26f7836591cbd9992526b24d42d6198a20c153a7
  sed -i 's|/etc/os-version|/etc/uos-version|' misc/Xsession.d/00deepin-dde-env
}

build() {
  cd dde-session
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-session
  DESTDIR="$pkgdir" ninja install
}
