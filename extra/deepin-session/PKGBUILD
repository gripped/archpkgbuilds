# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-session
pkgver=2.0.1
pkgrel=1
pkgdesc='New deepin session, based on systemd and existing projects'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session"
license=('GPL-3.0-or-later')
# deepin-session-ui: org.deepin.dde.Osd1
# deepin-kwin: dde-session@.service
# deepin-shell: dde-session-initialized.target.wants/dde-shell@DDE.service
# deepin-qt{5,6} plugins/integrations: enabled by dde-session
depends=('glibc' 'gcc-libs' 'glib2' 'libx11' 'libsecret' 'qt6-base' 'dtk6core'
         'libxcursor' 'libxfixes' 'systemd-libs' 'deepin-session-ui' 'deepin-kwin' 'deepin-shell'
         'deepin-qt5platform-plugins' 'deepin-qt5integration'
         'deepin-qt6platform-plugins' 'deepin-qt6integration')
makedepends=('git' 'cmake' 'ninja')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-session.git#tag=$pkgver")
sha512sums=('43630bd9670100ba51745ed8b9c3cc4adcbc0c113a70595a4a49d808274309b66f69539ce5cfed2802f74aaeaf63809ed69029490b14fc41c9910fdd3b07e3e2')

prepare(){
  cd dde-session
  sed -i 's|/etc/os-version|/etc/uos-version|' misc/Xsession.d/00deepin-dde-env
}

build() {
  cd dde-session
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-session
  DESTDIR="$pkgdir" ninja install
}
