# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-session
pkgver=2.0.12
pkgrel=1
pkgdesc='New deepin session, based on systemd and existing projects'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session"
license=('GPL-3.0-or-later')
# deepin-session-ui: org.deepin.dde.Osd1
# deepin-kwin: dde-session@.service
# deepin-shell: dde-session-initialized.target.wants/dde-shell@DDE.service
# deepin-qt{5,6} plugins/integrations: enabled by dde-session
depends=('glibc' 'gcc-libs' 'glib2' 'libx11' 'libsecret' 'qt6-base' 'dtk6core'
         'libxcursor' 'libxfixes' 'systemd-libs' 'deepin-session-ui' 'deepin-kwin' 'deepin-shell'
         'deepin-qt5platform-plugins' 'deepin-qt5integration'
         'deepin-qt6platform-plugins' 'deepin-qt6integration')
makedepends=('git' 'cmake' 'ninja')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-session.git#tag=$pkgver")
sha512sums=('2bc99fbd9e2d4f74af3b0ff5cb41be31e77a95e443ec6adc601bf592b2eee3cad5fa5ce43c72787cc65d0091e2e64719a09ef33660eee894ac2cef2d2b6d24fa')

prepare(){
  cd dde-session
  # qt 6.10.1
  git cherry-pick -n 8d9927d223ae99b09db7525c8170e550d0ff5226
  sed -i 's|/etc/os-version|/etc/uos-version|' tools/dde-version-checker/main.cpp
}

build() {
  cd dde-session
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-session
  DESTDIR="$pkgdir" ninja install
}
