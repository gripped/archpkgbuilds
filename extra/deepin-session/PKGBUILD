# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-session
pkgver=1.2.1
pkgrel=1
pkgdesc='New deepin session, based on systemd and existing projects'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session"
license=('GPL3')
# deepin-session-ui: org.deepin.dde.Osd1
# deepin-kwin: dde-session@.service
# deepin-dock: dde-session-initialized.target.wants/dde-dock.service
depends=('glibc' 'gcc-libs' 'glib2' 'libx11' 'libsecret' 'qt5-base' 'gsettings-qt' 'dtkcore'
         'libxcursor' 'libxfixes' 'systemd-libs' 'deepin-session-ui' 'deepin-kwin' 'deepin-dock')
makedepends=('cmake' 'ninja')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-session/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('75a0ef06c61ed6828c728dd903344e22ffa072edfd58a89dde947976f435408978dfcd67f119449c47a7356e5db202ae179af94c9ded8bbfb3f59878ade22776')

prepare(){
  cd dde-session-$pkgver
  sed -i 's|/etc/os-version|/etc/uos-version|' misc/Xsession.d/00deepin-dde-env
}

build() {
  cd dde-session-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-session-$pkgver
  DESTDIR="$pkgdir" ninja install
}
