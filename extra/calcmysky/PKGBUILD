# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Carlos Aznarán <caznaranl@uni.pe>
# Contributor: Frederik “Freso” S. Olesen <archlinux@freso.dk>

pkgname=calcmysky
pkgver=0.3.5
pkgrel=1
pkgdesc="Simulator of light scattering by planetary atmospheres"
arch=(x86_64)
url="https://github.com/10110111/CalcMySky"
license=(GPL-3.0-or-later)
depends=(gcc-libs qt6-base)
makedepends=(cmake eigen git glm vulkan-headers)
source=(git+$url.git#tag=v$pkgver)
sha512sums=('74484d4fa92e90490e63802607f2d8bf152037a3d7525415ddcef821f0a088d4fe5f40e4d08922d86a109c25e6fbc649e909f13de7f0f664aac32700cde62f95')

build() {
  local cmake_options=(
    -B build
    -S CalcMySky
    -W no-dev
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CPM_LOCAL_PACKAGES_ONLY=ON
    -D QT_VERSION=6
  )
  cmake "${cmake_options[@]}"
  cmake --build build --target all
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
  install -vDm 644 CalcMySky/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
