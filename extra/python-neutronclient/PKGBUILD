# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-neutronclient
pkgver=11.3.1
pkgrel=2
pkgdesc="OpenStack Networking (Neutron) Client"
arch=('any')
url="https://docs.openstack.org/python-neutronclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cliff' 'python-debtcollector' 'python-iso8601' 'python-netaddr'
         'python-openstacksdk' 'python-osc-lib' 'python-oslo-i18n' 'python-oslo-log'
         'python-oslo-serialization' 'python-oslo-utils' 'python-os-client-config'
         'python-keystoneauth1' 'python-keystoneclient' 'python-requests')
makedepends=('git')
checkdepends=('python-oslotest' 'python-osprofiler' 'python-openstackclient' 'python-subunit'
              'python-requests-mock' 'python-stestr' 'python-testtools' 'python-testscenarios')
source=("git+https://github.com/openstack/python-neutronclient.git#tag=$pkgver")
sha512sums=('d3a7782de38388ded4f1dd80dd8fff1a6d4b7a5de953cd15b32ed3a99cfa61e8ff9721049d244c2d7893e27663c6d0628cf49528c7a0db7280629faf57ff50e0')

build() {
  cd python-neutronclient
  python setup.py build
}

check() {
  cd python-neutronclient
  stestr run
}

package() {
  cd python-neutronclient
  python setup.py install --root="$pkgdir" --optimize=1
}
