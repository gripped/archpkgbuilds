# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-neutronclient
pkgver=11.7.0
pkgrel=1
pkgdesc="OpenStack Networking (Neutron) Client"
arch=('any')
url="https://docs.openstack.org/python-neutronclient/latest/"
license=('Apache-2.0')
depends=('python-pbr' 'python-cliff' 'python-debtcollector' 'python-netaddr'
         'python-openstacksdk' 'python-osc-lib' 'python-oslo-i18n' 'python-oslo-log'
         'python-oslo-serialization' 'python-oslo-utils' 'python-keystoneauth1'
         'python-keystoneclient' 'python-requests')
makedepends=('git')
checkdepends=('python-oslotest' 'python-osprofiler' 'python-openstackclient'
              'python-requests-mock' 'python-stestr' 'python-testtools')
source=("git+https://github.com/openstack/python-neutronclient.git#tag=$pkgver")
sha512sums=('19e38a595366bd090962d14aefa37e0add7c762e2ed1f2354760efd90e47042a8419ec39b17ef3852ac0f350e5ab35081ceab01a4405c1641a6375b34da6874c')

build() {
  cd python-neutronclient
  python setup.py build
}

check() {
  cd python-neutronclient
  stestr run
}

package() {
  cd python-neutronclient
  python setup.py install --root="$pkgdir" --optimize=1
}
