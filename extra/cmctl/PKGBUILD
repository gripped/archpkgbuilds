# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: ml <>

pkgname=cmctl
pkgver=2.4.0
pkgrel=1
pkgdesc='CLI tool manage and configure cert-manager resources for Kubernetes'
arch=('x86_64')
url='https://github.com/cert-manager/cmctl'
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'git'
  'go'
)
source=("git+$url.git#tag=v$pkgver")
sha512sums=('6873238c13369bcbb62b81d55fa1adb891e18c7cc28bce220cdd674fec9f02e7423b8dbcba31662d4263a911ee59dfc4b6d0ba550d50dcf50c341485fd34b512')
b2sums=('113550bfa854c874e7da397644034e4748c3903c2f256ece3b2d362700c87a08bbbe9065c95d8c560450a2c10315ed524ccf478098e5317836abdea58b1b8c69')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
    -X 'github.com/cert-manager/cert-manager/pkg/util.AppGitCommit=$(git rev-parse --short HEAD)' \
    -X 'github.com/cert-manager/cert-manager/pkg/util.AppGitState=clean' \
    -X 'github.com/cert-manager/cert-manager/pkg/util.AppVersion=$pkgver' \
  "

  go build -v -ldflags "$ld_flags" -o build/
}

check() {
  cd $pkgname
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/cert-manager/cmctl/v2/test/integration \
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" build/cmctl

  ./build/cmctl completion bash > cmctl.bash
  install -vDm644 cmctl.bash "$pkgdir/usr/share/bash-completion/completions/cmctl"
  ./build/cmctl completion fish > cmctl.fish
  install -vDm644 cmctl.fish "$pkgdir/usr/share/fish/vendor_completions.d/cmctl.fish"
  ./build/cmctl completion zsh > cmctl.zsh
  install -vDm644 cmctl.zsh "$pkgdir/usr/share/zsh/site-functions/_cmctl"
}
