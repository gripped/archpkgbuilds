# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: ml <>

pkgname=cmctl
pkgver=2.3.0
pkgrel=2
pkgdesc='CLI tool manage and configure cert-manager resources for Kubernetes'
arch=('x86_64')
url='https://github.com/cert-manager/cmctl'
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'git'
  'go'
)
source=("git+$url.git#tag=v$pkgver")
sha512sums=('9134bce0de2ea962afc60e5a743d5419bad7fbf0ec2f2c2506227bccacae27dc1581b509ef040aa42f63a63a3e4f41f7e3c9b2b8abcb3b6173df4e4242954576')
b2sums=('2bc9301c9dbea9b9aaf0580e64b320056d1ce47cf2ca335aa006bcccd67db698efe3e22761d417e3004ded6927832a43e7711fb8e9c6591ba9b611a6b8d9ce24')

prepare() {
  cd $pkgname
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
    -X 'github.com/cert-manager/cert-manager/pkg/util.AppGitCommit=$(git rev-parse --short HEAD)' \
    -X 'github.com/cert-manager/cert-manager/pkg/util.AppGitState=clean' \
    -X 'github.com/cert-manager/cert-manager/pkg/util.AppVersion=$pkgver' \
  "

  go build -v -ldflags "$ld_flags" -o build/
}

check() {
  cd $pkgname
  local unit_tests=$(
    go list ./... \
      | grep -v github.com/cert-manager/cmctl/v2/test/integration \
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" build/cmctl

  ./build/cmctl completion bash > cmctl.bash
  install -vDm644 cmctl.bash "$pkgdir/usr/share/bash-completion/completions/cmctl"
  ./build/cmctl completion fish > cmctl.fish
  install -vDm644 cmctl.fish "$pkgdir/usr/share/fish/vendor_completions.d/cmctl.fish"
  ./build/cmctl completion zsh > cmctl.zsh
  install -vDm644 cmctl.zsh "$pkgdir/usr/share/zsh/site-functions/_cmctl"
}
