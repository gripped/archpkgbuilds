# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-libthai
pkgver=5.1.5
pkgrel=1
pkgdesc="Libthai Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-libthai"
license=('GPL')
depends=('fcitx5' 'libthai')
makedepends=('git' 'extra-cmake-modules' 'ninja')
source=("git+https://github.com/fcitx/fcitx5-libthai.git#tag=$pkgver?signed")
sha512sums=('23047f62ef6d8245145930c2677d87c6414a7f392178351aa221a686247b81c25052da249e58a39323ae547d30fb1e799bf1d13239066270abe9b21b02664267')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
