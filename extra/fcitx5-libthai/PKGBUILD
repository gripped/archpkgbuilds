# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-libthai
pkgver=5.1.8
pkgrel=1
pkgdesc="Libthai Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-libthai"
license=('GPL')
depends=('fcitx5' 'libthai')
makedepends=('git' 'extra-cmake-modules' 'ninja')
source=("git+https://github.com/fcitx/fcitx5-libthai.git#tag=$pkgver?signed")
sha512sums=('2f9cc358dd16b747f74865f49b6c6a293d96f27eb8260a48c0c66ccacb228fc2344c12c19fb3e362a979bea3b18ce57ad3d3f91ee1f408ba454852fea0fb3102')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname
  ninja test
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
