# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Felix Yan
# Contributor: Zerial
# Contributor: Patrick Leslie Polzer
# Contributor: Jose Riha

pkgname=dd_rescue
pkgver=1.99.17
pkgrel=1
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('openssl')
optdepends=('lzo: for libddr_lzo')
arch=('x86_64')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.bz2"{,.asc}
        "gzip_reproducible.patch")
sha512sums=('8bdd63ed48413607e1da004f497feec8b8a990488348105d30d4d7e56e342b54b4e6439270e581e434b4b0edcf55ab11490739b0c6563358c59b5a64a390a0df'
            'SKIP'
            '6c29100cfd138ae8c2105161ca03da199ba68d9a2561a6a6876656d138d94df2b17ff048eabddf1bfab5b7a94cd5466845b6395184439ab8be8ae8901b0362c4')
validpgpkeys=('6669F7340D31E95EC5565490DE4F1B3A2BFFC5BF') # Kurt Garloff <kurt@garloff.de>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../gzip_reproducible.patch
  autoreconf -fi

  # Don't install to /usr/lib64
  sed -i '/LIB = lib64/d' Makefile
}

build() {
  cd $pkgname-$pkgver
  ./configure
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}
