# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=letterpress
pkgver=2.1
pkgrel=10
pkgdesc='Convert your images into a picture made up of ASCII characters, using jp2a'
arch=(any)
url='https://apps.gnome.org/Letterpress/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  jp2a
  libadwaita
  python
  python-gobject
  python-pillow
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://gitlab.gnome.org/World/Letterpress.git#tag=$pkgver"
  letterpress-ref-dialog.patch
  letterpress-drop-target.patch
  letterpress-png-convert.patch
  letterpress-clear-tmpfiles.patch
  letterpress-dbus-activatable.patch
)
b2sums=(
  150cec7703733d6466008baa29aa77310749328ae4aab193e23da4dac25cbeb810058551a144069852b0f647b403e89060c65673124032ca37340c1c34ab441b
  85b95ddf80a445a8fd13856086da45e7702d171a780deb62512b27ef03ed56acd30ddf43cd27c67a935961b11cab2fe8a68607d3d6519802cfa4edaaedc6710d
  4030ec1653f77719e00f28435a615a431fd139833db2a706af165866ac0ecc8f5edeb6c0ac66f931a53d50ae57578e1b97fcb7af423dedbb67b5a034fccabd4c
  5f6cc0075f3a78a77e04827805e0f76f645db41f2b2378740586ff6ddc40b8bd39c84f1a3697c856b72cb15e85a214cd6e64cfd1515f8a0008cc0bcd66ea0d9e
  78b5fcc46accc00bc558053bef023cbce2ee57b16c76d84a68bcec325daec6ea7db99dbd92b52a3936a3416620e5d0724f1cd8791519bb23a4dbeb901d26b0ae
  f1e492d0383195f8aa9ccf34fbc1b0b964c6b8c6c80455a2d15087aceb364efd1d3b8231630b9485ac862c74557ed2717ec73819d6d13941818807c52c97bc9b
)

prepare() {
  cd Letterpress

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/16
  git apply -3 ../letterpress-ref-dialog.patch

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/19
  git apply -3 ../letterpress-drop-target.patch

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/27
  git apply -3 ../letterpress-png-convert.patch

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/21
  git apply -3 ../letterpress-clear-tmpfiles.patch

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/30
  git apply -3 ../letterpress-dbus-activatable.patch

  # Don't maximize window by default, reduce output width
  sed -i '/"window-is-maximized"/{n;s/>true</>false</}
          /"output-width"/{n;s/>100</>80</}' data/io.gitlab.gregorni.Letterpress.gschema.xml
}

build() {
  arch-meson Letterpress build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
