# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=letterpress
pkgver=2.1
pkgrel=6
pkgdesc='Convert your images into a picture made up of ASCII characters, using jp2a'
arch=(any)
url='https://apps.gnome.org/Letterpress/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  jp2a
  libadwaita
  python
  python-gobject
  python-pillow
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
)
source=(
  "git+https://gitlab.gnome.org/World/Letterpress.git#tag=$pkgver"
  letterpress-ref-dialog.patch
  letterpress-window-icon.patch
  letterpress-drop-target.patch
  letterpress-clear-tmpfiles.patch
  letterpress-webp-support.patch
)
b2sums=(
  150cec7703733d6466008baa29aa77310749328ae4aab193e23da4dac25cbeb810058551a144069852b0f647b403e89060c65673124032ca37340c1c34ab441b
  85b95ddf80a445a8fd13856086da45e7702d171a780deb62512b27ef03ed56acd30ddf43cd27c67a935961b11cab2fe8a68607d3d6519802cfa4edaaedc6710d
  001ea58a749bc573f69f6a6de235572a07092d99f414f9e66f2a8180ad1df523988536d5b21589e3d6f2c8869fa0cfd08fea8b203abb8043bcbc9d143975cd0f
  4030ec1653f77719e00f28435a615a431fd139833db2a706af165866ac0ecc8f5edeb6c0ac66f931a53d50ae57578e1b97fcb7af423dedbb67b5a034fccabd4c
  6686d328c10e8176a745a1ff616e9abb18fe7f523912fc13bc1c9b0e6439c1945bf8cf0cbffcd67f1f5dd5ccc373bb636bbd8975e06f074720096bc682946216
  1ce45e3b5b837fbae2267799483b876a739c67fabfdc6b3e2d8c5043b5c6d78a9b742b0c6bfe807f5e5f15303f7ee7fcca50c5edf925815b948c64ec57a6b28b
)

prepare() {
  cd Letterpress

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/16
  git apply -3 ../letterpress-ref-dialog.patch

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/18
  git apply -3 ../letterpress-window-icon.patch

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/19
  git apply -3 ../letterpress-drop-target.patch

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/21
  git apply -3 ../letterpress-clear-tmpfiles.patch

  # https://gitlab.gnome.org/World/Letterpress/-/merge_requests/24
  git apply -3 ../letterpress-webp-support.patch

  # Don't maximize window by default, reduce output width
  sed -i '/"window-is-maximized"/{n;s/>true</>false</}
          /"output-width"/{n;s/>100</>80</}' data/io.gitlab.gregorni.Letterpress.gschema.xml
}

build() {
  arch-meson Letterpress build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"
}
