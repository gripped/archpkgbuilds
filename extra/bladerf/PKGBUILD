# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=bladerf
pkgver=2025.10
_FPGAver=0.16.0
_firmver=2.6.0
pkgrel=1
pkgdesc="Driver, userspace, FPGA & firmware for the bladeRF SDR"
arch=('x86_64')
url="https://github.com/Nuand/bladeRF"
license=('MIT AND LGPL-2.1-only AND GPL-2.0-only')
depends=(
  'curl'
  'glibc'
  'libedit'
  'libusb'
  'ncurses'
)
makedepends=(
  'cmake'
  'doxygen'
  'git'
  'help2man'
)
source=(
  "git+$url.git#tag=$pkgver"
  "git+https://github.com/Nuand/bladeRF-fsk.git"
  "git+https://github.com/analogdevicesinc/no-OS.git"
  "hostedxA4-$_FPGAver.rbf::https://nuand.com/fpga/v$_FPGAver/hostedxA4.rbf"
  "hostedxA9-$_FPGAver.rbf::https://nuand.com/fpga/v$_FPGAver/hostedxA9.rbf"
  "hostedx40-$_FPGAver.rbf::https://nuand.com/fpga/v$_FPGAver/hostedx40.rbf"
  "hostedx115-$_FPGAver.rbf::https://nuand.com/fpga/v$_FPGAver/hostedx115.rbf"
  "https://www.nuand.com/fx3/bladeRF_fw_v$_firmver.img"
)
sha512sums=('4f04ccaa6f9e5e45082bd660364c0cd04ec8fc4ee01941338cc5a29ce911d59d68dd3d7061d49e095dfd728e0f9e3fc09b1f5f523fc1f79d7760a2bfe7eb8eb0'
            'SKIP'
            'SKIP'
            '0a641309c5561f1d66e46c1a810963e68b556d4667c5e68e76b63b4b5f5c86af764fb274fbd77202d9324e5cf352512eab10f4583cb2d5a27c49e0bf06138465'
            '4b17f4cb174291cce6b35889dc53c67652b41145c4f7e6a6759a1cb238368299b3deece77115117fc95afbb6176aea426754f59500fa4d3b84b3f72e413e9e63'
            '8bcba343a861d2fabf49d9f5b122ab67a8ce267409ba504bf28073d273918b99e2105ff3b2255d7b03bf37a1bc3d270493aad46e0414076f137371bf579eca04'
            'ee5f51ed1ba17f1b2ab748406b998efe283acdeeb5a00e77863655168b91a5e9d9fbb3e74fdc4d404f08baf3ea3d0c0fd11084cf2366c34fdd82f7e9a4da97f9'
            'ba8dfd4f99903cfeec496dbdfddb73d6037a74ad16337b19900abe925b3c69dcb8fc5d256b4a53beb08d5f8de8bcd36b0a9dab86d12eef135194d119d8b0c9e8')

prepare() {
  cd bladeRF
  # https://bugs.archlinux.org/task/47168
  sed -i 's|MODE.*$|TAG+="uaccess"|' host/misc/udev/*.rules.in

  # https://bugs.archlinux.org/task/54105
  mv -v host/misc/udev/{88,70}-nuand-bladerf1.rules.in
  mv -v host/misc/udev/{88,70}-nuand-bladerf2.rules.in
  mv -v host/misc/udev/{88,70}-nuand-bootloader.rules.in
  sed -i 's|88-nuand-|70-nuand-|' host/misc/udev/CMakeLists.txt

  git submodule init
  git config submodule.host/utilities/bladeRF-fsk.url ../bladeRF-fsk
  git config submodule.thirdparty/analogdevicesinc/no-OS.url ../no-OS
  git -c protocol.file.allow=always submodule update \
    host/utilities/bladeRF-fsk \
    thirdparty/analogdevicesinc/no-OS
}

build() {
  cmake -S bladeRF/host -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DBUILD_DOCUMENTATION=ON \
    -DUDEV_RULES_PATH=/usr/lib/udev/rules.d
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" bladeRF/legal/licenses/LICENSE.MIT.nuand

  install -vDm644 bladeRF_fw_v$_firmver.img "$pkgdir/usr/share/bladerf/firmware/bladeRF_fw_v$_firmver.img"
  install -vDm644 hostedxA4-$_FPGAver.rbf "$pkgdir/usr/share/bladerf/fpga/hostedxA4.rbf"
  install -vDm644 hostedxA9-$_FPGAver.rbf "$pkgdir/usr/share/bladerf/fpga/hostedxA9.rbf"
  install -vDm644 hostedx40-$_FPGAver.rbf "$pkgdir/usr/share/bladerf/fpga/hostedx40.rbf"
  install -vDm644 hostedx115-$_FPGAver.rbf "$pkgdir/usr/share/bladerf/fpga/hostedx115.rbf"
}
