# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=chatty
pkgver=0.8.8
pkgrel=2
pkgdesc='Simple to use SMS and Matrix messaging application'
arch=(x86_64)
url='https://gitlab.gnome.org/World/Chatty'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  evolution-data-server
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libcmatrix
  libmm-glib
  libphonenumber
  libspelling
  pango
  sqlite
)
makedepends=(
  appstream
  git
  glib2-devel
  itstool
  meson
)
checkdepends=(xorg-server-xvfb)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/World/Chatty.git?signed#tag=v$pkgver")
b2sums=('2770375826ad4d67966c8e7551f1c3e7ffeccbc92d57b4338cbe9ec6e6558a268b5731ac630fab13c6cdda45eaa7eaeabe6e313a799dc5bde7bda8c43b52a9a3')
validpgpkeys=(
  FAA79DE7EBDD0D17A3F218E228319315DD47C33B # Evangelos Ribeiro Tzaras <evangelos.tzaras@puri.sm>
  B9386554B7DD266BCB8E29A990F0C9B18A6B4A19 # Evangelos Ribeiro Tzaras <devrtz@fortysixandtwo.eu>
)

build() {
  arch-meson Chatty build \
    -D purple=disabled
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
