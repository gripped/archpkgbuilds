# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackclient
pkgver=7.2.1
_commit=7c6b47b451f30d1d3965358c515baae87955d7dc
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cryptography' 'python-cliff' 'python-iso8601' 'python-openstacksdk'
         'python-osc-lib' 'python-oslo-i18n' 'python-keystoneclient' 'python-novaclient'
         'python-cinderclient' 'python-stevedore')
makedepends=('git')
checkdepends=('python-oslotest' 'python-requests' 'python-requests-mock' 'python-stestr'
              'python-testtools' 'python-wrapt' 'python-ddt')
source=("git+https://github.com/openstack/python-openstackclient.git#commit=$_commit")
sha512sums=('d1511f2464e52e74b95e9726bce8868797571f88eeb2f954f3e9b9dddab36956c0bd033024a64f13e8356b5a62ce398a4635a575b79f2d43d00db192cf0dfbb3')

build() {
  cd python-openstackclient
  python setup.py build
}

check() {
  cd python-openstackclient
  stestr run
}

package() {
  cd python-openstackclient
  python setup.py install --root="$pkgdir" --optimize=1
}
