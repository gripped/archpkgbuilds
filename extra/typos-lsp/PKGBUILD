# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=typos-lsp
pkgver=0.1.48
pkgrel=1
pkgdesc='Language server for the typos source code spell checker'
arch=(x86_64)
url=https://github.com/tekumara/typos-lsp
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('d9a44dd14bd8c9084711dd424ccf9f9d171513e4ac4bcf24c0aa8f4fe5467a03278c50dad734b4efd70cb67dcdb2d0de9e1c3abe13144f1fca22428775f7b59b')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
