# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=typos-lsp
pkgver=0.1.38
pkgrel=1
pkgdesc='Language server for the typos source code spell checker'
arch=(x86_64)
url=https://github.com/tekumara/typos-lsp
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('c7bd7c2d2f70f63c3b728431b96a4a30124de0b51bdc183caa279295cc773795465dfe30c9f7d0187bb36c4d5195b267e1f4cda180b882dcd1737f44a1611e49')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
