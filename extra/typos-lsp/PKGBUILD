# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=typos-lsp
pkgver=0.1.41
pkgrel=1
pkgdesc='Language server for the typos source code spell checker'
arch=(x86_64)
url=https://github.com/tekumara/typos-lsp
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('2566a3cf0d801697348653d4dc2807fa61451b58d314c32d92d06ccfee3847d5725090c36cb383fac47978184d8a3fea5ce03a6ea56cd82db2a3ad60d91bc838')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
