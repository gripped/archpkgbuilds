# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=typos-lsp
pkgver=0.1.49
pkgrel=1
pkgdesc='Language server for the typos source code spell checker'
arch=(x86_64)
url=https://github.com/tekumara/typos-lsp
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('65ea7ede71f8721da97dcf12f5b04a8c6c6e3816b95137a1b56257d577c213f7f395b16b47d7a66a656914d50d49ac2bd576c23d5cbc854ced7e11326c391819')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
