# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=typos-lsp
pkgver=0.1.40
pkgrel=1
pkgdesc='Language server for the typos source code spell checker'
arch=(x86_64)
url=https://github.com/tekumara/typos-lsp
license=(MIT)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('a82a6d365871f47ad99f1fe5113cb6d2dde54a3a9e7dd8fb37ceb01d28256f21b799d03f20b4b8ea465ea3fd06292f2ae7dce0b5b80ec52057534c349891e7c8')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
