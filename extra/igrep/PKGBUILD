# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=igrep
pkgver=1.3.0
pkgrel=2
pkgdesc="Interactive grep"
arch=('x86_64')
url="https://github.com/konradsz/igrep"
license=('MIT')
depends=('libgcc' 'oniguruma')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('30420e9d7d3c833d1415f54e0e315335f87811a514c2eba492ae4542006eb0f300d717525d9e8025c45ba51acf9e32e4ee30635321c3318105b0967195124818')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgname-$pkgver"
  RUSTONIG_DYNAMIC_LIBONIG=1 cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  RUSTONIG_DYNAMIC_LIBONIG=1 cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/ig" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
