# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Camille Monière <draslorus at draslorus dot fr>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Stefan Husmann <stefan-husmann at t-online dot de>
# Contributor: Alexander Rødseth <rodseth at gmail dot com>
# Contributor: William Rea <sillywilly at gmail dot com>

pkgname=libmatio
pkgver=1.5.29
pkgrel=1
pkgdesc='C library with a fortran 90/95 module interface for reading/writing MATLAB MAT-files'
arch=(x86_64)
license=(BSD-2-Clause)
url='https://sourceforge.net/projects/matio'
depends=(glibc
         hdf5
         zlib)
makedepends=(git)
source=(git+https://github.com/tbeu/matio#tag=v$pkgver)
sha256sums=('a8b1f5bf51a41dd8fda7264848df933fa260c61acb69879b9d8c7949785a3311')

prepare() {
  cd matio
  autoreconf -fiv
}

build() {
  cd matio
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --with-hdf5
  make
}

package() {
  cd matio
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
