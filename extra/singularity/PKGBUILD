# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor: sausageandeggs <s_stoakley at hotmail.co.uk>

pkgname=singularity
pkgver=1.1
pkgrel=2
pkgdesc="Endgame: Singularity - simulation of true AI"
arch=('any')
url="http://emhsoft.com/singularity/index.html"
license=('GPL-2.0-or-later' 'CC-BY-SA-3.0' 'CC0-1.0')
depends=('python' 'python-pygame' 'python-numpy' 'python-polib')
makedepends=('python-setuptools' 'git')
source=("git+https://github.com/singularity/singularity.git#tag=v${pkgver}"
        https://github.com/singularity/singularity-music/archive/025e2696638bcc3bf7690679c3a17c0b46823bbe.tar.gz
        "singularity.desktop")
sha512sums=('cd07b3535ee6ba7b415e448c7b092fcb1348a163fc838a65e6957d790a9ad6b6a89708a38deae41dca6e0ebffd0b06a1a8d693b808de3db313f3dfaf60ebd17f'
            '9ec095e8882aa3981a4bbfe551f8d61c876fc6d0ee082f558f8e0f3015bfb65d23d76e5c3557a3e9ab41ddfe896eff618934c043ce8cee58709548f02fa81625'
            'd37e1a2a6d8e832a81114133df0ea5d5aeb531c81ba606b6bd49a6a38db1825660de9248593b9bfb40ddab4e1c9f794b6b5ed11ea273adb8bd9b4a363896b135')

prepare() {
  cd "$pkgname"

  mv "$srcdir"/singularity-music-* singularity/music
  sed -i "s|prune|recursive-include singularity/music *|g" MANIFEST.in
  sed -i "s|replaceme|python$(pacman -Q python | cut -d ' ' -f 2 | cut -d . -f 1,2)|g" "$srcdir"/singularity.desktop
}

build() {
  cd "$pkgname"

  python setup.py build
}

package() {
  cd "$pkgname"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  mv run_singularity.sh "$pkgdir"/usr/bin/singularity

  install -Dm644 "$srcdir/singularity.desktop" "$pkgdir/usr/share/applications/singularity.desktop"
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.cc0.txt LICENSE.CC-BY-SA-3.txt LICENSE.gpl.txt LICENSE.txt
}
# vim: sw=2:ts=2 et:
