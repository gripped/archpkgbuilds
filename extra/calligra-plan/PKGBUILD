# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=calligra-plan
pkgdesc='A project management application intended for managing moderately large projects with multiple resources'
pkgver=4.0.0
pkgrel=6
arch=(x86_64)
url='https://www.calligra-suite.org/'
license=(GPL-2.0-or-later
         LGPL-2.1-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcalendarcore
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kdbusaddons
         kdiagram
         kholidays
         ki18n
         kiconthemes
         kio
         kitemmodels
         kitemviews
         kjobwidgets
         knotifications
         kparts
         kservice
         ktextwidgets
         kwallet
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         plasma-activities
         qca-qt6
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
source=(https://download.kde.org/stable/calligraplan/calligraplan-$pkgver.tar.xz{,.sig}
        fix-kcalendarcore.patch)
sha256sums=('b741f7440e02e96ab152b82a5c66f47b77eec6bdfd97704b0272ea78933f6936'
            'SKIP'
            'd54d0c748a2783d45ae14207f016b554876e6bb63545297ac1093693fdd3ca9a')
validpgpkeys=(ECE3E51E588C721746FFE4B587AD2BE1C0E834B9) # Mickael Sergent <miko53@free.fr>

prepare() {
  patch -d calligraplan-$pkgver -p1 < fix-kcalendarcore.patch # Fix KCalendarCore usage
}

build() {
  cmake -B build -S calligraplan-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
