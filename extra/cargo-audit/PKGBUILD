# Maintainer: kpcyrd <git@rxv.cc>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-audit
pkgver=0.19.0
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
options=(!lto)
source=("https://github.com/RustSec/rustsec/archive/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('0fbf088247ab2aa0da9d42dcf8be4bd10d05296e6599a2ae91a94d25aba6d37b')
b2sums=('696d2ec6d246f1849806e4c8f4e50ebff214b5f06790492e0f32141b79df1ab029163e4961de71562e4731379396d23d42911ff93f6658241711ce2f8017ea86')

prepare() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd rustsec-${pkgname}-v${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --features fix
}

check() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --release --locked
}

package() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  install -Dm755 "../target/release/${pkgname}" -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
