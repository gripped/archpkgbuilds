# Maintainer: kpcyrd <git@rxv.cc>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-audit
pkgver=0.22.1
pkgrel=2
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'cargo'
  'libgcc'
  'glibc'
)
options=(!lto)
source=("https://github.com/RustSec/rustsec/archive/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('262d42fcca5db8629b6220d84e62e7ffda913846a36089a847ffe276e6b09446')
b2sums=('59e20276733875564e7d17750a2617b0f0b9cd33bddb84f5120d2954ed290697a8ca60f0a0233e93f7797c8c7751dfc3dcc4b8544b5c89309477c0e5e2943409')

prepare() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd rustsec-${pkgname}-v${pkgver}
  cargo build --release --locked --features fix
}

check() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo test --release --locked
}

package() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  install -Dm755 "../target/release/${pkgname}" -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
