# Maintainer: kpcyrd <git@rxv.cc>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-audit
pkgver=0.21.2
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'cargo'
  'gcc-libs'
  'glibc'
)
options=(!lto)
source=("https://github.com/RustSec/rustsec/archive/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('caf8914af7f95ebb45590c95b5f9bfd71bd6f9f57c1ffcf69dc9d20f0430e578')
b2sums=('258275e71b101f603f7770d2fdbe8b467e99f0c8dacd527bdd0de91785ee403b0775b48002579b35b8309331d0f0aa7885cd78aad5579915d119f131b5fe9c36')

prepare() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd rustsec-${pkgname}-v${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --features fix
}

check() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --release --locked
}

package() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  install -Dm755 "../target/release/${pkgname}" -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
