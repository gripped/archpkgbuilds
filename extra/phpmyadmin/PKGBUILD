# Maintainer:
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Manolis Tzanidakis <manolis@archlinux.org>
# Contributor: Simon Lackerbauer <calypso "at" strpg.org>
# Contributor: William Desportes <williamdes@wdes.fr>

pkgname=phpmyadmin
pkgver=5.2.2
pkgrel=2
pkgdesc='PHP web-based tool to administrate MySQL and MariaDB databases over the web'
arch=('any')
url='https://www.phpmyadmin.net'
license=('GPL')
depends=('mysql-clients' 'php-interpreter')
backup=(etc/webapps/phpmyadmin/config.inc.php)
source=(https://files.phpmyadmin.net/phpMyAdmin/$pkgver/phpMyAdmin-$pkgver-all-languages.tar.xz)
sha512sums=('f671969ae477aa094fca7f88b27ddc555b67388dd5f971df7e3ec8b09d01506549f86ef90324b3e290b0015debdebe66f87dd5098771c92ba4dac4106b2aeb3d')

package() {
  _instdir="$pkgdir"/usr/share/webapps/phpMyAdmin
  mkdir -p "$_instdir" "$pkgdir"/etc/webapps/phpmyadmin
  cd "$_instdir"

  cp -ra "$srcdir"/phpMyAdmin-$pkgver-all-languages/* .

  ln -s /etc/webapps/phpmyadmin/config.inc.php "$_instdir"/config.inc.php
  cp "$_instdir"/config.sample.inc.php "$pkgdir"/etc/webapps/phpmyadmin/config.inc.php
  
  rm -f "$pkgdir"/usr/share/webapps/phpMyAdmin/vendor/composer/ca-bundle/res/cacert.pem
  ln -s /etc/ssl/certs/ca-certificates.crt "$pkgdir"/usr/share/webapps/phpMyAdmin/vendor/composer/ca-bundle/res/cacert.pem
}
