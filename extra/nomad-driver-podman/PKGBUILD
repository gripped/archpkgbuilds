# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad-driver-podman
pkgver=0.6.2
pkgrel=1
pkgdesc='A nomad taskdriver for podman containers'
arch=('x86_64')
url='https://github.com/hashicorp/nomad-driver-podman'
license=('MPL-2.0')
depends=('nomad' 'podman')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('20bd0ef9784c86c1adf84851c0cb3fef15e562cfc617f386525192661b03f0555cb1a4d695c8ceab405924ccab7b53bdcc2396c8d6b98fcd588127acad56d749')
b2sums=('315c8750c2dbafb2a730768203f26b4d6f9894d580a8dc8f0c38b678eacb9098170ad15d4aec605b4f2c7ebf039d08791853a90d1b2776acd9e3830aa9004bbe')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/lib/nomad/plugins" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examples "$pkgdir/usr/share/doc/$pkgname"
  # note: examples/plugins is an empty folder
  rm -rf "$pkgdir/usr/share/doc/$pkgname/examples/plugins"
}
