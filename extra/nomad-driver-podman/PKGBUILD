# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad-driver-podman
pkgver=0.6.3
pkgrel=1
pkgdesc='A nomad taskdriver for podman containers'
arch=('x86_64')
url='https://github.com/hashicorp/nomad-driver-podman'
license=('MPL-2.0')
depends=('nomad' 'podman')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('db211b4114df94bbb9089ae011a427c76ef1272791390bdf6aadd1fb81af6e3e550c1696e677bf5e78377958e144a5e0f3b216cc7fb6a2108d6bf767a0085c70')
b2sums=('e3adbbbd47c957daea98502cda7790d58ca2c698377a16f6b616c7cec9a8aea45655c13e63aa32861beb26414ee09a2d611fb1d69f667b8d9b1ec2885a2005d7')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/lib/nomad/plugins" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examples "$pkgdir/usr/share/doc/$pkgname"
  # note: examples/plugins is an empty folder
  rm -rf "$pkgdir/usr/share/doc/$pkgname/examples/plugins"
}
