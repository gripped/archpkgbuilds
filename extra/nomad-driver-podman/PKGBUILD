# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad-driver-podman
pkgver=0.6.4
pkgrel=1
pkgdesc='A nomad taskdriver for podman containers'
arch=('x86_64')
url='https://github.com/hashicorp/nomad-driver-podman'
license=('MPL-2.0')
depends=('nomad' 'podman')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('fff27d9f77652c452aebe8a281d8a41b050bd3943f50046a4fc46f7353cdc93a3803776f86a4c74a935b94a62b3b5d17b00aa53b03703cd4878de45ed8379e37')
b2sums=('2cfa4f8ef303f99a1016133fa185956041715222b3095965dd02e000c47a7e927233864d013c414a61a8a789fd0a9502c68c56fa491466077630b731dcff8f98')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/lib/nomad/plugins" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examples "$pkgdir/usr/share/doc/$pkgname"
  # note: examples/plugins is an empty folder
  rm -rf "$pkgdir/usr/share/doc/$pkgname/examples/plugins"
}
