# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gmsynth.lv2
pkgver=0.6.2
pkgrel=1
pkgdesc="General MIDI LV2 Synth"
arch=(x86_64)
url="https://x42-plugins.com/x42/x42-gmsynth"
license=(GPL-2.0-or-later)
groups=(
  pro-audio
  lv2-plugins
)
depends=(glibc)
makedepends=(
  fluidsynth
  lv2
)
checkdepends=(lv2lint)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/x42/$pkgname/archive/v$pkgver.tar.gz
  $pkgname-0.5.1-use_system_fluidsynth.patch
)
sha512sums=('7537fe65bf78f47b0e3da45d3b1f95fd3eaf346578f7a1f7c9b957a79aa3bd43088ffbf81ad9e3e4158fcabc3ab114c981487347c7a567c75ddac83d4b06d44b'
            'a0f2a09a6a839c38dc3234a719a633eb221f2afca2a97bbecf17b7d0c8dc71befec3768b882deb20357d55186e81e342cad73eb5e08426a1e2108949d11b3cf2')
b2sums=('78eb88889514372becd0eae068ae63579e3f19297fad9bf1d1043b6d56f9a3142e3f92feb1bedfc0d8405239a0f6daceab5d8265309a981530c75847e1ff9c9b'
        'cb327b55536006a862540e769497d138a6815bbee83f9365f97778adf8c542f3ce49e83d07dd532e66c39ce57a4c1eedc79c982b8ac350f22311f9443f682954')

prepare() {
  # use system fluidsynth
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.5.1-use_system_fluidsynth.patch
}

build() {
  make -C $pkgname-$pkgver
}

check() {
  lv2lint -Mpack -I $pkgname-$pkgver/build "http://gareus.org/oss/lv2/gmsynth"
}

package() {
  depends+=(
    fluidsynth libfluidsynth.so
  )

  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
