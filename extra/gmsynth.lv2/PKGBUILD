# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gmsynth.lv2
pkgver=0.6.3
pkgrel=1
pkgdesc="General MIDI LV2 Synth"
arch=(x86_64)
url="https://x42-plugins.com/x42/x42-gmsynth"
license=(GPL-2.0-or-later)
groups=(
  pro-audio
  lv2-plugins
)
depends=(glibc)
makedepends=(
  fluidsynth
  lv2
)
checkdepends=(lv2lint)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/x42/$pkgname/archive/v$pkgver.tar.gz
  $pkgname-0.5.1-use_system_fluidsynth.patch
)
sha512sums=('96b7491ebd8f83dfb9e3289c6736ca5518e8513b472733feb91f16199be00e2a7fc433800bcc44fe7dd3d490382492e0b2403a3efaafe8fd219a999ae813d198'
            'a0f2a09a6a839c38dc3234a719a633eb221f2afca2a97bbecf17b7d0c8dc71befec3768b882deb20357d55186e81e342cad73eb5e08426a1e2108949d11b3cf2')
b2sums=('db6d79731d42888e9709730e24acc8be123cc52d69f82c369d9e0f0d962cf825e9e86825a51563bffd5cd8a16ea4e632de3038963bf3236f712803cf66311030'
        'cb327b55536006a862540e769497d138a6815bbee83f9365f97778adf8c542f3ce49e83d07dd532e66c39ce57a4c1eedc79c982b8ac350f22311f9443f682954')

prepare() {
  # use system fluidsynth
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.5.1-use_system_fluidsynth.patch
}

build() {
  make -C $pkgname-$pkgver
}

check() {
  lv2lint -Mpack -I $pkgname-$pkgver/build "http://gareus.org/oss/lv2/gmsynth"
}

package() {
  depends+=(
    fluidsynth libfluidsynth.so
  )

  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
