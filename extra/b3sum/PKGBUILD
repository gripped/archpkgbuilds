# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=BLAKE3
pkgname=b3sum
pkgver=1.5.4
pkgrel=1
pkgdesc='Command line implementation of the BLAKE3 hash function'
arch=(x86_64)
url=https://github.com/BLAKE3-team/BLAKE3
license=('CC0-1.0 OR Apache-2.0 OR Apache-2.0 WITH LLVM-exception')
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=$pkgver")
b2sums=('7ba0954d910bab719a04ccf768ccbfd8b285b2a1d23449da1c1b5809a63a8fd700b03dfe6adb4476dd3d9029fb562fa145c14f6ed5b4a72118030f5bd0fcb703')

prepare() {
  cd $_name/$pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name/$pkgname
  cargo build --release --locked --offline
}

check() {
  cd $_name/$pkgname
  cargo test --locked --offline
}

package() {
  cd $_name/$pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}
