# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=recoll
pkgver=1.37.4
pkgrel=4
pkgdesc="Full text search tool based on Xapian backend"
arch=('x86_64')
url="https://www.lesbonscomptes.com/recoll/"
license=('GPL')
depends=('xapian-core>=1.0.15-1' 'qt6-base' 'openssl' 'hicolor-icon-theme' 'qt6-webengine'
	 'chmlib' 'aspell')
makedepends=('python' 'python-setuptools' 'qt6-tools'
	     'cmake' 'extra-cmake-modules')
optdepends=('libxslt: for XML based formats (fb2,etc)'
	    'unzip: for the OpenOffice.org documents'
	    'poppler: for pdf'
	    # 'pstotext: for postscipt' # Dropped to AUR as upstream has disappeared
	    'antiword: for msword'
	    'catdoc: for ms excel and powerpoint'
	    'unrtf: for RTF'
#	    'untex: for dvi support with dvips'
	    'djvulibre: for djvu'
#	    'id3lib: for mp3 tags support with id3info'
	    'python-mutagen: Audio metadata'
	    'perl-image-exiftool: EXIF data from image files'
	    'python-lxml: indexing spreadsheets'
	    'python-pychm: CHM filter'
#	    'kcoreaddons5: KDE plugins'
#	    'kio5: KDE plugins'
#	    'krunner5: KDE plugins'
	    'aspell-en: English stemming support')
source=("https://www.lesbonscomptes.com/$pkgname/$pkgname-${pkgver/_/}.tar.gz")
sha512sums=('9e5af5d00191658f23e6a5916718014827f12f4553d9429fe385c0bd1c72f2f49ae97e7d7b8121cb174b299ffe482aa547e4a3aabf2da92e9773bf9eb0f57814')

prepare() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  sed -i '1,1i#include <QAbstractItemView>' qtgui/ssearch_w.cpp
}

build() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  QMAKE=qmake6 ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-recollq \
    --disable-webkit \
    --enable-webengine \
    --enable-publiclib
  make

#  KDE Applets not ported to KF6 yet
#  pushd kde/kioslave/kio_recoll
#  mkdir -p build
#  cd build
#  cmake -DCMAKE_INSTALL_PREFIX=/usr -DRECOLL_PUBLIC_LIB=ON -DQT_MAJOR_VERSION=6 ..
#  make
#  popd

#  pushd kde/krunner
#  mkdir -p build
#  cd build
#  cmake -DCMAKE_INSTALL_PREFIX=/usr -DRECOLL_PUBLIC_LIB=ON -DQT_MAJOR_VERSION=6 ..
#  make
#  popd
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  make DESTDIR="$pkgdir" install
#  make -C "kde/kioslave/kio_recoll/build" DESTDIR="$pkgdir" install
#  make -C "kde/krunner/build" DESTDIR="$pkgdir" install
}
