# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-picodrive
pkgver=2805
pkgrel=1
pkgdesc='Sega MS/MD/CD/32X core'
arch=(x86_64)
url=https://github.com/libretro/picodrive
license=(BSD)
groups=(libretro)
depends=(
  glibc
  libretro-core-info
)
makedepends=(git)
_commit=3365b1774bc8680be9899968fe45b224ad2f11c1
source=(
  libretro-picodrive::git+https://github.com/libretro/picodrive.git#commit=${_commit}
  picodrive-cyclone::git+https://github.com/irixxxx/cyclone68000.git
  git+https://github.com/mackron/dr_libs.git
  git+https://github.com/digital-sound-antiques/emu2413.git
  picodrive-libchdr::git+https://github.com/irixxxx/libchdr.git
  git+https://github.com/irixxxx/libpicofe.git
)
b2sums=('17ddb80e73b3cf8d02d01705895bf21a6214e52e6f9f34d8d9843c88cc567708ffdb0869e09538ec9ab399a63fafe8a33afb27750d6644ac98b82f6d7373f941'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd libretro-picodrive
  for submodule in pico/sound/emu2413 platform/{common/dr_libs,libpicofe}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule##*/}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
  for submodule in cpu/cyclone pico/cd/libchdr; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../picodrive-${submodule##*/}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

pkgver() {
  cd libretro-picodrive
  git rev-list --count HEAD
}

build() {
  cd libretro-picodrive
  make -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-picodrive/picodrive_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-picodrive/COPYING -t "${pkgdir}"/usr/share/licenses/libretro-picodrive/
}

# vim: ts=2 sw=2 et:
