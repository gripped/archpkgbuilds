# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: andreas_baumann <abaumann@yahoo.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gengetopt
pkgver=2.23
pkgrel=4
pkgdesc='A tool to write command line option parsing code for C programs'
arch=('x86_64')
url='https://www.gnu.org/software/gengetopt/gengetopt.html'
license=('GPL-3.0-or-later')
options=('!docs' '!makeflags')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'gengen' 'gengetopt' 'help2man')
validpgpkeys=('CD7AB2B200F374043F92E5D42485C3A5CF0FF62F') # Gray Wolf <wolf@wolfsden.cz>
source=("git+https://git.savannah.gnu.org/git/gengetopt.git#tag=rel_${pkgver/./_}?signed")
sha256sums=('acb81a9e7315285e2ef6e3d5ded387e85abe7322d86f509e25644d27ac1a5bd0')

prepare() {
  cd "${pkgname}"

  autoreconf -fi
}

build() {
  cd "${pkgname}"

  ./configure \
    YACC='bison' \
    --prefix=/usr
  make
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install
}

