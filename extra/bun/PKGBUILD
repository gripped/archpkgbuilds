# Contributor: Daniele Basso <d dot bass 05 at proton dot me>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=bun
pkgver=1.3.8
_webkitver=9a2cc42ae1bf693a0fd0ceb9b1d7d965d9cfd3ea
_zigver=c1423ff3fc7064635773a4a4616c5bf986eb00fe
pkgrel=2
pkgdesc="Incredibly fast JavaScript runtime, bundler, test runner, and package manager â€“ all in one"
arch=(x86_64)
url="https://github.com/oven-sh/bun"
license=('MIT')
depends=(
  gcc-libs
  glibc
  icu
  sqlite
)
makedepends=(
  cargo # bun
  clang20
  cmake
  git
  lld20
  llvm20
  ninja
  python          # webkit
  ruby            # webkit
  ruby-getoptlong # webkit
)
options=('!lto')
source=(
  "git+$url.git#tag=bun-v$pkgver"
  # Necessary for code generation
  "$pkgname-linux-x64-$pkgver.zip::https://github.com/oven-sh/bun/releases/download/bun-v$pkgver/bun-linux-x64.zip"
  "$pkgname-WebKit::git+https://github.com/oven-sh/WebKit.git#commit=$_webkitver"
  "$pkgname-zig::git+https://github.com/oven-sh/zig.git#commit=$_zigver"
  "$pkgname-add-cmake-option-zig-local.patch"
  "$pkgname-fix-dangling-pointer-in-remote-debugger.patch"
  "$pkgname-sqlite3-lib-path.diff"
)
b2sums=('62a3ba4da9dad6c81f0d44e9453a53a89e69a0d5c1a2763e2f09531b3c850b5d91383ebc835ef583c7e13e0bf11ecb6b5def2174c2e556eeaebe3e7dffd8e9bd'
        '5a548fe46b5050445d5057b9568b1d22a6270b68f3d2824f9c63d05d12b0a04eddffce2f76c05714b50fe731e81cf9304960625271aee4eba9d623f5ecbbc202'
        '1ee5878d590bd7b8b1e5f5ac7b6e62b63e1cdcdac4cf78a65f3a7a8823e5f85508b2114f50226c051ad1b9857c969208d90bfb2b7054b520a93f7650d93fe57f'
        '8c867cf8d1a4895c366e9230cedd2328f7d6ee73be89cb58e794a8f226bb57c2ca7323650c4f275f8b502df4c1e2eec90c295b61079b6865d09d9ec8d8170483'
        'fb11b321280b79ef2807c8a3947dc8ec3e413bf53471fce7891923f8bb5bcb555c672638143d838d7af6229a147dcd2f8b9a76107a35f09146b7e2da88a2cd45'
        'f08b27d947235dc556275ccce421d7d9de72add117c1482fa62e92a9aa38f3f01349b77488c3e167476955994cccf0e57f691ace92d2b21a2d2503dda398b9c9'
        'f1ad976393570be7f09e0391d68a4f573735ddc13be577f9d3746f8adb71d66b74d705429a8bd5c3582107971a5ded1931303185dae6763251ad7ce4ce9e2cd5')

prepare() {
  cd $pkgname
  grep -q "WEBKIT_VERSION $_webkitver" cmake/tools/SetupWebKit.cmake \
    || (echo "WebKit version mismatch"; exit 1)
  grep -q "ZIG_COMMIT \"$_zigver\"" cmake/tools/SetupZig.cmake \
    || (echo "Zig version mismatch"; exit 1)
  patch -Np1 < ../$pkgname-add-cmake-option-zig-local.patch
  patch -Np1 < ../$pkgname-fix-dangling-pointer-in-remote-debugger.patch
  patch -Np1 < ../$pkgname-sqlite3-lib-path.diff
}

build() {
  local cmake_args_common=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -Wno-dev
    -GNinja
  )

  export PATH="/usr/lib/llvm20/bin:$PATH"
  CC=clang CXX=clang++ \
    cmake -S bun-WebKit -B build-WebKit \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=JSCOnly \
    -DENABLE_STATIC_JSC=ON \
    -DUSE_BUN_JSC_ADDITIONS=ON \
    -DUSE_BUN_EVENT_LOOP=ON \
    -DENABLE_FTL_JIT=ON \
    -DALLOW_LINE_AND_COLUMN_NUMBER_IN_BUILTINS=ON \
    -DENABLE_REMOTE_INSPECTOR=ON
  cmake --build build-WebKit --target jsc

  cmake -S bun-zig -B build-zig \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=None \
    -DZIG_PIE=ON \
    -DZIG_SHARED_LLVM=ON \
    -DZIG_USE_LLVM_CONFIG=ON \
    -DZIG_TARGET_TRIPLE=native-linux.6.6-gnu.2.40 \
    -DZIG_TARGET_MCPU=baseline
  cmake --build build-zig

  rm build-WebKit/JavaScriptCore/DerivedSources/inspector/InspectorProtocolObjects.h

  mkdir -vp bun/vendor/zig
  ln -vsf "$PWD/build-zig/stage3/bin/zig" bun/vendor/zig/zig
  ln -vsf "$PWD/build-zig/stage3/lib/zig" bun/vendor/zig/lib

  # Link system ICU libs - bun expects .a but works with .so
  ln -vs /usr/lib/libicudata.so build-WebKit/lib/libicudata.a
  ln -vs /usr/lib/libicui18n.so build-WebKit/lib/libicui18n.a
  ln -vs /usr/lib/libicuuc.so build-WebKit/lib/libicuuc.a

  export PATH="$PWD/bun-linux-x64:$PATH"
  bun run --cwd=$pkgname scripts/glob-sources.mjs

  local llvmver=$(clang --version | head -1 | awk '{print $3}')
  cmake -S bun -B build-bun \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DLLVM_VERSION="$llvmver" \
    -DENABLE_LTO=ON \
    -DENABLE_ASAN=OFF \
    -DUSE_STATIC_SQLITE=OFF \
    -DUSE_STATIC_LIBATOMIC=OFF \
    -DWEBKIT_LOCAL=ON \
    -DWEBKIT_PATH="$PWD/build-WebKit" \
    -DZIG_LOCAL=ON
  cmake --build build-bun
}

check() {
  # Smoke test
  ./build-bun/bun --version
  echo 'console.log("ok")' | ./build-bun/bun run -
}

package() {
  install -vDm755 build-bun/bun "$pkgdir/usr/bin/bun"
  ln -vs /usr/bin/bun "$pkgdir/usr/bin/bunx"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" bun/LICENSE.md

  install -vDm644 bun/completions/bun.bash "$pkgdir/usr/share/bash-completion/completions/bun"
  install -vDm644 bun/completions/bun.fish "$pkgdir/usr/share/fish/vendor_completions.d/bun.fish"
  install -vDm644 bun/completions/bun.zsh "$pkgdir/usr/share/zsh/site-functions/_bun"
}
