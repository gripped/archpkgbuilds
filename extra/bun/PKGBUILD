# Contributor: Daniele Basso <d dot bass 05 at proton dot me>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=bun
pkgver=1.3.10
_webkitver=4a6a32c32c11ffb9f5a94c310b10f50130bfe6de
_zigver=9a092d3f95a8087c298e847848816cd2d53d2734
pkgrel=1
pkgdesc="Incredibly fast JavaScript runtime, bundler, test runner, and package manager â€“ all in one"
arch=(x86_64)
url="https://github.com/oven-sh/bun"
license=('MIT')
depends=(
  gcc-libs
  glibc
  icu
  sqlite
)
makedepends=(
  cargo   # bun
  clang   # bun, webkit
  clang20 # zig
  cmake
  git
  lld    # bun, webkit
  lld20  # zig
  llvm   # bun
  llvm20 # zig
  ninja
  python          # webkit
  ruby            # webkit
  ruby-getoptlong # webkit
)
options=('!lto')
source=(
  "git+$url.git#tag=bun-v$pkgver"
  # Necessary for code generation
  "$pkgname-linux-x64-$pkgver.zip::https://github.com/oven-sh/bun/releases/download/bun-v$pkgver/bun-linux-x64.zip"
  "$pkgname-WebKit::git+https://github.com/oven-sh/WebKit.git#commit=$_webkitver"
  "$pkgname-zig::git+https://github.com/oven-sh/zig.git#commit=$_zigver"
  "$pkgname-add-cmake-option-zig-local.patch"
  "$pkgname-sqlite3-lib-path.diff"
)
b2sums=('cdc2ec7a83bbe569f63ba2fed03b6f053e6be284d8cc51254121931709924c0e10f8f9008f19d319e765fe209281464b8cd1a8a0b53095c8015a0c8db4eea58f'
        '6e7f47680cf10b233d3d556c2a8974dd18715fda2ab7ff83ec4690a203de676e49d989e0f963c9d081c2340a204db2372e23e11589b9568f4cf4ac75e010abaa'
        '75d5b1a70ffb190018092dd9c936eaa338978476c6b325191699831692e2b9cbac5237ebe969f72a6fd656ba3581c075d4eb724fb3cf5e664f20cf9ed1e51e20'
        'e274a2cbd7e8df8ca26e5fbd6aa54f84a8c713899a4d4ba6f93e5d05345bcde4b8d58e3dc355fb99c3fdbe236c768336f96c126d688e9fcbddc8d763277d2cb6'
        'f2f96e38df6844239d65fe22a4a76bcb86c05496bcbb7923f083acd3a41d1d5d812b68733885b89a5456f73c083655fe098a17599611c9e463b7919199d5f38d'
        'f1ad976393570be7f09e0391d68a4f573735ddc13be577f9d3746f8adb71d66b74d705429a8bd5c3582107971a5ded1931303185dae6763251ad7ce4ce9e2cd5')

prepare() {
  cd $pkgname
  grep -q "WEBKIT_VERSION $_webkitver" cmake/tools/SetupWebKit.cmake \
    || (echo "WebKit version mismatch"; exit 1)
  # The actual commit that is referenced is not in-tree. I picked an equivalent
  # commit that was actually merged. I'm not sure how they could release that
  # but they did.
  # TODO: This check should probably be re-enabled with the next bun release.
  # grep -q "ZIG_COMMIT \"$_zigver\"" cmake/tools/SetupZig.cmake \
  #   || (echo "Zig version mismatch"; exit 1)
  patch -Np1 < ../$pkgname-add-cmake-option-zig-local.patch
  patch -Np1 < ../$pkgname-sqlite3-lib-path.diff
}

build() {
  local cmake_args_common=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -Wno-dev
    -GNinja
  )

  export PATH="/usr/lib/llvm/bin:$PATH"
  CC=clang CXX=clang++ \
    cmake -S bun-WebKit -B build-WebKit \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=JSCOnly \
    -DENABLE_STATIC_JSC=ON \
    -DUSE_BUN_JSC_ADDITIONS=ON \
    -DUSE_BUN_EVENT_LOOP=ON \
    -DENABLE_FTL_JIT=ON \
    -DALLOW_LINE_AND_COLUMN_NUMBER_IN_BUILTINS=ON \
    -DENABLE_REMOTE_INSPECTOR=ON
  cmake --build build-WebKit --target jsc

  PATH="/usr/lib/llvm20/bin:$PATH" \
    cmake -S bun-zig -B build-zig \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=None \
    -DZIG_PIE=ON \
    -DZIG_SHARED_LLVM=ON \
    -DZIG_USE_LLVM_CONFIG=ON \
    -DZIG_TARGET_TRIPLE=native-linux.6.6-gnu.2.40 \
    -DZIG_TARGET_MCPU=baseline
  cmake --build build-zig

  rm build-WebKit/JavaScriptCore/DerivedSources/inspector/InspectorProtocolObjects.h

  mkdir -vp bun/vendor/zig
  ln -vsf "$PWD/build-zig/stage3/bin/zig" bun/vendor/zig/zig
  ln -vsf "$PWD/build-zig/stage3/lib/zig" bun/vendor/zig/lib
  ln -vsf "$PWD/bun-WebKit" bun/vendor/WebKit

  # Link system ICU libs - bun expects .a but works with .so
  ln -vs /usr/lib/libicudata.so build-WebKit/lib/libicudata.a
  ln -vs /usr/lib/libicui18n.so build-WebKit/lib/libicui18n.a
  ln -vs /usr/lib/libicuuc.so build-WebKit/lib/libicuuc.a

  export PATH="$PWD/bun-linux-x64:$PATH"
  bun run --cwd=$pkgname scripts/glob-sources.mjs

  local llvmver=$(clang --version | head -1 | awk '{print $3}')
  cmake -S bun -B build-bun \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_CANARY=OFF \
    -DLLVM_VERSION="$llvmver" \
    -DENABLE_LTO=ON \
    -DENABLE_ASAN=OFF \
    -DUSE_STATIC_SQLITE=OFF \
    -DUSE_STATIC_LIBATOMIC=OFF \
    -DWEBKIT_LOCAL=ON \
    -DWEBKIT_PATH="$PWD/build-WebKit" \
    -DZIG_LOCAL=ON
  cmake --build build-bun
}

check() {
  # Smoke test
  ./build-bun/bun --version
  echo 'console.log("ok")' | ./build-bun/bun run -
}

package() {
  install -vDm755 build-bun/bun "$pkgdir/usr/bin/bun"
  ln -vs /usr/bin/bun "$pkgdir/usr/bin/bunx"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" bun/LICENSE.md

  install -vDm644 bun/completions/bun.bash "$pkgdir/usr/share/bash-completion/completions/bun"
  install -vDm644 bun/completions/bun.fish "$pkgdir/usr/share/fish/vendor_completions.d/bun.fish"
  install -vDm644 bun/completions/bun.zsh "$pkgdir/usr/share/zsh/site-functions/_bun"
}
