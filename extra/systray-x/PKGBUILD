# Maintainer: Robin Candau <antiz@archlinux.org>

pkgbase=systray-x
_pkgbase=SysTray-X
pkgname=('systray-x-common' 'systray-x-kde')
pkgver=0.9.10
pkgrel=4
pkgdesc="A system tray extension for Thunderbird"
url="https://github.com/Ximi1970/systray-x"
arch=('x86_64')
license=('MPL-2.0')
depends=('qt6-base' 'qt6-wayland' 'thunderbird')
makedepends=('git' 'zip' 'unzip' 'libx11' 'kstatusnotifieritem' 'strip-nondeterminism')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	'Makefile.patch'
	'binary_path.patch'
	'fix_compatibility_with_tb_136.patch')
sha256sums=('7a9bc146f341288b0b41e65d56190dcead694681b1bed25ad36a35768abf3029'
            '62affd86e6dbad6e9cf851768d99fd79f76f65ac2fd5e45ef5e89c5dcb63dbab'
            'aa0a5a7e8b062452416ec9b4472ee4d1a9d7d17fd5ecf94698c557cd504514da'
            '9071bee84fb4eb3030401222eb9944b49ecd4eb8644615b778cdaba5ef35aa48')

prepare() {
	cd "${pkgbase}-${pkgver}"

	# Add the correct path to the binary in the json file
	patch -Np1 -i "${srcdir}/binary_path.patch"

	# Patch the Makefile to build both versions of the app into separate directories
	patch -Np1 -i "${srcdir}/Makefile.patch"

	# Backported patch set to fix compatibility with Thunderbird >= 136
	# See https://github.com/Ximi1970/systray-x/commit/cd49f4c1d5a5e06faab4b61ee46e67a972001eb0
	# And https://github.com/Ximi1970/systray-x/commit/28e4a4a348207ca9f398dc3faabffc24b5178820
	# And https://github.com/Ximi1970/systray-x/commit/d47b7f20ba0dfdc502c81c4eeb1f07d7037f9aa4
	patch -Np1 -i "${srcdir}/fix_compatibility_with_tb_136.patch"
}

_build() {
	if [ "${1}" == "systray-x-common" ]; then
		make OPTIONS="DEFINES+=NO_KDE_INTEGRATION" APP="${1}"
	else
		make APP="${1}"
	fi
}

build() {
	cd "${pkgbase}-${pkgver}"
	_build systray-x-common
	_build systray-x-kde

	# Strip timestamps for zip archive for reproducibility
	strip-nondeterminism -t zip systray-x@Ximi1970.xpi
}

package_systray-x-common() {
pkgdesc="${pkgdesc} - Common version"
optdepends=('gnome-shell-extension-appindicator: Gnome integration')
provides=("${pkgbase}")
conflicts=("${pkgbase}")

	cd "${pkgbase}-${pkgver}"
	install -Dm 755 "app/systray-x-common-build/${_pkgbase}-app/${_pkgbase}" "${pkgdir}/usr/bin/${_pkgbase}"
	install -Dm 644 "app/${_pkgbase//-/_}.json" "${pkgdir}/usr/lib/mozilla/native-messaging-hosts/${_pkgbase//-/_}.json"
	install -Dm 644 "${pkgbase}@Ximi1970.xpi" "${pkgdir}/usr/lib/thunderbird/extensions/${pkgbase}@Ximi1970.xpi"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgbase}/README.md"
}

package_systray-x-kde() {
pkgdesc="${pkgdesc} - KDE version"
depends=('kstatusnotifieritem')
provides=("${pkgbase}")
conflicts=("${pkgbase}")

	cd "${pkgbase}-${pkgver}"
	install -Dm 755 "app/systray-x-kde-build/${_pkgbase}-app/${_pkgbase}" "${pkgdir}/usr/bin/${_pkgbase}"
	install -Dm 644 "app/${_pkgbase//-/_}.json" "${pkgdir}/usr/lib/mozilla/native-messaging-hosts/${_pkgbase//-/_}.json"
	install -Dm 644 "${pkgbase}@Ximi1970.xpi" "${pkgdir}/usr/lib/thunderbird/extensions/${pkgbase}@Ximi1970.xpi"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgbase}/README.md"
}
