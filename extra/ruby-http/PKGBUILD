# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-http
pkgver=5.2.0
pkgrel=4
pkgdesc='A fast Ruby HTTP client with a chainable API, streaming support, and timeouts'
arch=(any)
url='https://github.com/httprb/http'
license=(MIT)
depends=(
  ruby
  ruby-addressable
  ruby-base64
  ruby-http-cookie
  ruby-http-form_data
  ruby-llhttp-ffi
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-certificate_authority
  ruby-rake
  ruby-rspec
  ruby-rspec-its
  ruby-simplecov
  ruby-webrick
)
options=(!emptydirs)
source=(
  "git+https://github.com/httprb/http.git#tag=v$pkgver"
  "${pkgname}_fix_tests.patch"
)
sha256sums=('e76b8462e6d5e4779d5775e0593f6b16f3b24bd29192bca3193e98444cc413c3'
            '616c7c4c361b44072aaf518cba82c3d7669634a1c890043a7bc5fb85ab734fcc')

prepare() {
  cd http

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"

  sed -i 's|~>|>=|' http.gemspec
  sed -i '/fuubar/d' spec/spec_helper.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd http
  gem build http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd http
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd http
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
