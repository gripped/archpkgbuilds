# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Carl Smedstad <carl.smedstad at protonmail dot com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thomas <thomas at 6f dot io>

pkgname=jujutsu
_pkgname=jj
pkgver=0.25.0
pkgrel=1
pkgdesc='Git-compatible VCS that is both simple and powerful'
arch=(x86_64)
url="https://github.com/jj-vcs/$_pkgname"
license=(Apache-2.0)
depends=(gcc-libs # libgcc_s.so ld-linux-x86-64.so
         glibc # libc.so libm.so
         libgit2
         libssh2
         openssl
         # zlib-ng # https://github.com/rust-lang/libz-sys/issues/83
         zstd)
replaces=("$pkgname-bin")
makedepends=(cargo
             cmake) # builds vendored libz-ng
_archive="$_pkgname-$pkgver"
source=("$url/archive/refs/tags/v$pkgver/$_archive.tar.gz")
sha256sums=('3a99528539e414a3373f24eb46a0f153d4e52f7035bb06df47bd317a19912ea3')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
	mkdir -p completions
}

build() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
	export LIBGIT2_NO_VENDOR=1
	export LIBSSH2_SYS_USE_PKG_CONFIG=1
	cargo build --frozen --release --all-features --package jj-cli
	local jj=target/release/jj
	$jj util completion bash > completions/jj
	$jj util completion elvish > completions/jj.elv
	$jj util completion fish > completions/jj.fish
	$jj util completion zsh > completions/_jj
	$jj util mangen > jj.1
}

check() {
	cd "$_archive"
	export useNextest=false
	local skipped=(
		test_diff_command # relies on config of external command
		test_acls::test_diff # relies on assumptions about tty
	)
	cargo test --frozen --all-features --package jj-cli -- \
		${skipped[@]/#/--skip }
}

package() {
	depends+=(libgit2.so
	          libssh2.so)
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin" target/release/jj
	install -Dm0644 -t "$pkgdir/usr/share/bash-completion/completions/" completions/jj
	install -Dm0644 -t "$pkgdir/usr/share/elvish/lib/" completions/jj.elv
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" completions/jj.fish
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" completions/_jj
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" jj.1
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
	cp -at "$pkgdir/usr/share/doc/$pkgname" docs
	# Avoid namcap warning for dead symlink
	rm "$pkgdir/usr/share/doc/jujutsu/docs/cli-reference.md"
}
