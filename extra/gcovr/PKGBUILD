# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=gcovr
pkgver=8.6
pkgrel=1
pkgdesc='provides a utility for managing the use of the GNU gcov utility'
arch=(any)
url="https://gcovr.com/"
license=(BSD-3-Clause)
depends=('python' 'python-jinja' 'python-lxml' 'gcc' 'python-colorlog' 'python-pygments')
makedepends=('python-hatchling' 'python-hatch-vcs' 'python-hatch-fancy-pypi-readme'
             'python-build' 'python-installer' 'python-wheel' 'git')
checkdepends=('python-pytest')
source=("git+https://github.com/gcovr/gcovr.git#tag=${pkgver}")
sha256sums=('256fa0cd538daa7f2719c8f539d878dc93a18d8381f0d3d7f5411c2d2c566a0a')

prepare() {
  cd ${pkgname}
  # Remove unnecessary dependency version pinning
  sed -i 's/hatchling==1.27.0/hatchling/' pyproject.toml
  sed -i 's/hatch-fancy-pypi-readme==24.1.0/hatch-fancy-pypi-readme/' pyproject.toml
  sed -i 's/hatch-vcs==0.4.0/hatch-vcs/' pyproject.toml
}

build() {
  cd ${pkgname}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}
  # requires pyutillib, ply
  #pytest gcovr
}

package() {
  cd ${pkgname}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
