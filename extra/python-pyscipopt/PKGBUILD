# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maximilian Stahlberg <maximilian.stahlberg tu-berlin de>

pkgname=python-pyscipopt
pkgver=6.0.0
pkgrel=2
pkgdesc='A Python interface to the SCIP Optimization Suite'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(MIT)
depends=(glibc
         python
         scip)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/SCIP-Interfaces/PySCIPOpt#tag=v$pkgver)
sha256sums=('57d26b3e0411c92ea1ec2141551f1046bc26b9ba17e01a85c3345f7bca5e3008')

build() {
  cd PySCIPOpt
  python -m build --wheel --no-isolation
}

package() {
  cd PySCIPOpt
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
