# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maximilian Stahlberg <maximilian.stahlberg tu-berlin de>

pkgname=python-pyscipopt
pkgver=5.2.1
pkgrel=2
pkgdesc='A Python interface to the SCIP Optimization Suite'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(MIT)
depends=(glibc
         python
         scip)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/SCIP-Interfaces/PySCIPOpt#tag=v$pkgver)
sha256sums=('fffe5fee5e455adca98b4d90b405c98bebac3bc7bc26685008a0ce369a3c1381')

build() {
  cd PySCIPOpt
  python -m build --wheel --no-isolation
}

package() {
  cd PySCIPOpt
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
