# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maximilian Stahlberg <maximilian.stahlberg tu-berlin de>

pkgname=python-pyscipopt
pkgver=6.1.0
pkgrel=1
pkgdesc='A Python interface to the SCIP Optimization Suite'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(MIT)
depends=(glibc
         python
         python-numpy
         scip)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/SCIP-Interfaces/PySCIPOpt#tag=v$pkgver)
sha256sums=('a8eebe462926984bc71eb6938c6d26f7a09551e3ee4830d445d800513bf13b9b')

build() {
  cd PySCIPOpt
  python -m build --wheel --no-isolation
}

package() {
  cd PySCIPOpt
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
