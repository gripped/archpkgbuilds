# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maximilian Stahlberg <maximilian.stahlberg tu-berlin de>

pkgname=python-pyscipopt
pkgver=4.4.0
pkgrel=1
pkgdesc='A Python interface to the SCIP Optimization Suite'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(MIT)
depends=(python
         scip)
makedepends=(cython0
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://github.com/SCIP-Interfaces/PySCIPOpt/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('5d3733abb2e7183db383cbb63dc8064ea855acec1468d3cf7148bdaf36b4ad6b')

build() {
  cd PySCIPOpt-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd PySCIPOpt-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
