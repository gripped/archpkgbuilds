# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=isomorphism-class
pkgname=haskell-isomorphism-class
pkgver=0.1.1
pkgrel=52
pkgdesc="Isomorphism typeclass solving the conversion problem"
url="https://github.com/nikita-volkov/isomorphism-class"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-primitive' 'haskell-unordered-containers'
         'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-rebase'
             'haskell-tasty' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        $pkgname-quickcheck-instances-0.3.32.patch::https://github.com/nikita-volkov/isomorphism-class/commit/eadf18f469a40c0531f5750cbd73acc454a3b6d4.patch)
sha256sums=('d32e2f1a1cf9f748a7b8594a72f71479ad79b5d93517d73eed372b5dd14407c3'
            '0a22ff1126c65adfd7e650396ba1b3ea4d928dfba67184162569132d36764a5d')

prepare() {
  cd $_hkgname-$pkgver
  patch -p1 -i ../$pkgname-quickcheck-instances-0.3.32.patch
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-debug-info --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
