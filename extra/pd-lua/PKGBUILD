# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.12.15
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL-2.0-or-later)
groups=(
  pd-externals
  pro-audio
)
depends=(
  glibc
  lua
  pd
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('1473ef9961114036d86343937cd0a3e3bc5a4cfdbcff5dda8adf254f11ba24df44b760d29a3d90c767180df94e046ba6fd2a240b31cc3a7662a20c68ccfc6377')
b2sums=('9726c8544b38d47e702436457a1428ada78a99196f20d1e0c4582f2d183a069d14f1252bb86f302ba021f016e4e91517e97490bf1dea33e2b2689400666ce110')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
