# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.12.19
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL-2.0-or-later)
groups=(
  pd-externals
  pro-audio
)
depends=(
  glibc
  lua
  pd
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('58e68ebd24ae996fd4e353c2cab14cb20cb1f1c70667da5f7ca83db6cbd7ff57a8e7acc9c492b4fa89fa95056053723d3a0514796b6cacc73d735b2487822ce9')
b2sums=('7e42ba0dd77d512247fb60c6cc5bf87bf2851d122640d1209677b4ed15594ea53060d33c8f6136def3743ac34a03116ffdfb17db8b43f7e2b7861bf21dc76915')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
