# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.12.17
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL-2.0-or-later)
groups=(
  pd-externals
  pro-audio
)
depends=(
  glibc
  lua
  pd
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('eaf5c4045e52188d7c5531902b65e42fa39df7441708162683439b6e5778e67f7cbc95a217d8ce7ecc48c55aae3024f8df27b20e5ae6e5a23ce65ead8b365c78')
b2sums=('e36f18ba08b1727fc14b31d32b2f3ab4dd7b0088a9afbacafc5c4aab6e23cb962fc83b976dc263eeb7516de5e19bcd3dd2573d22efda2cac0b1d7a11afde2177')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
