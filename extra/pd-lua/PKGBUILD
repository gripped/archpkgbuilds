# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.12.21
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL-2.0-or-later)
groups=(
  pd-externals
  pro-audio
)
depends=(
  glibc
  lua
  pd
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('eede555f89a2b88ed168808a23ea7937d57566c11e4e9177797b7c99fcf376d6dbc16bdcfb5443d055e5b894ae148bc360d3f14a55c0a49435ae5ec2631cddfb')
b2sums=('e879dd08c61b0e5bb4282d221f12209d057291e8f6ed925adcba40f3c1411b34e37c43a0d51704de8829f79320cbbed803c7601a0c24cbbddede057c4cbddd56')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
