# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.12.16
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL-2.0-or-later)
groups=(
  pd-externals
  pro-audio
)
depends=(
  glibc
  lua
  pd
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('8d0a418c19989519d93778541a739af299d77a3ba0921e18f283d55cb60948d26688bdc83e5172cb419889a0d824ede99a19ebd3d40eebc33ec64fc11d734fe0')
b2sums=('1ff251681ef73ec1e5bfb0c59cd9102dd22158c34999d5a8a199b528fb4d681f062f15c8209082fafdf2cabbb0a3047ab0e26ad0bbe7e4b24bd72e270c7f85fe')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
