# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.12.10
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL-2.0-or-later)
groups=(
  pd-externals
  pro-audio
)
depends=(
  glibc
  lua
  pd
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('3dd7c1f2f08d56f5dadf3d4af38e6a7dc2f4c6d8ab72bc36bc3e421d6b6c71a1246b7ed899f33636ca70b0e71267a26455720d2319413dddad281bb4eacd3bf8')
b2sums=('1513ea69a70fa66da95ef4e8f6b01d98227933a6866039c62051bfcb70f368ea187997b6eff569fd4726d9df16b693cac8a6e1f9c1861435dadb266c4bab61b4')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
