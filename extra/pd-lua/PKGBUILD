# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.12.7
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL-2.0-or-later)
groups=(
  pd-externals
  pro-audio
)
depends=(
  glibc
  lua
  pd
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('ae7fa4f17ecb279facd011e8211593db6bcfb33c7c6c6142ff5db28a32e37ea721dc8e7dead53418325b012725d4852175863961bcd3180574e5177344847551')
b2sums=('51dae7b65450df1a28c28db3d543104fbb8399568a086ae7a121c538bb92421b72350972f6a9a48a66a1b1f2d6f5f2f8d8a00f462c2a78417f3ca0d3b5a03a71')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
