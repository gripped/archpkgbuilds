# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.12.0
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL-2.0-or-later)
groups=(
  pd-externals
  pro-audio
)
depends=(
  glibc
  lua
  pd
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('c71341aaffcbd528a46b4f07f919dbbf349f688fb7a2753a8bbed30f0af4bab70df0548cad286e961a3029ff1c88c0ba990de3ff5aec7ad28bbecb291ff703f5')
b2sums=('e4b2b4c93876200d524585b12fac203f5da869030534b4a9550e0a064035f548ee56900394fd71908935d49458ee7dd5dc2e8e8f9db46dc306ecc66916a4aed1')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
