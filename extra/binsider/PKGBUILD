# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=binsider
pkgver=0.2.0
pkgrel=1
pkgdesc="A TUI for analyzing binaries like a boss"
arch=('x86_64')
url="https://github.com/orhun/binsider"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('5f31e7c7c936c3451af62b010cdebaaf6bc51691158adbaaf1ce57a518d77c3a26ed9d65a32a41368ffe54f595ff0722086a5ebabcd4539f7d4aacf9d5d135bf')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
