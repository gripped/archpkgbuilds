# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=binsider
pkgver=0.2.1
pkgrel=1
pkgdesc="A TUI for analyzing binaries like a boss"
arch=('x86_64')
url="https://github.com/orhun/binsider"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f7b122be78b46ec480849c4bb4c4e36dd1cdbeec562cd6bb9a43826b06a80d482cfa424ff74c206fb230efe0c5b97b10244ddac7f312ae97370b76e014c6b8c1')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
