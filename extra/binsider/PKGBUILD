# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=binsider
pkgver=0.3.0
pkgrel=1
pkgdesc="A TUI for analyzing binaries like a boss"
arch=('x86_64')
url="https://github.com/orhun/binsider"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d37939435ad30ad5fdae1d61ac17c686a89a68186936c4c625d5c4785636faf966118ceff53ca31c58318beeab8b278afa3771fd9afca8ff0d44f451cb8db70a')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
