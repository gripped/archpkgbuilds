# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kent Hou Man <knthmn0 at gmail dot com>

_name=monaspace
pkgbase=$_name-font
pkgname=(otf-$_name ttf-$_name-variable)
pkgver=1.200
pkgrel=1
pkgdesc='An innovative superfamily of fonts for code, by GitHub'
arch=(any)
url="https://github.com/githubnext/$_name"
license=(OFL)
_archive="$_name-v$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.zip")
sha256sums=('544b94719be76dd2d7e69f8464c5f8c0d612e59e9bde8d9ecc8867c9aa4940af')

package_otf-monaspace() {
	provides=("$pkgbase")
	cd "$_archive"
	install -Dm0644 -t "$pkgdir/usr/share/fonts/OTF/" fonts/otf/*.otf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_ttf-monaspace-variable() {
	provides=("$pkgbase-variable")
	cd "$_archive"
	install -Dm0644 -t "$pkgdir/usr/share/fonts/OTF/" fonts/variable/*.ttf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
