# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-rebulk
pkgver=3.3.0
pkgrel=2
pkgdesc='Define simple search patterns in bulk to perform advanced matching on any string'
arch=(any)
url='https://github.com/Toilal/rebulk'
license=(MIT)
depends=(python)
# python-regex is not a dependency, as it will be deprecated by upstream at some point
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('3cc916d104a4380667b89218044c84763d5dd511f5acba28f96c6e7eb9291a3cbf972e7f460291994b55eaf2447f85ffafef60276832002888796be460060a06')
b2sums=('9d11c04f62bf67c52bd6524674cb1d2efc32db71ceb9a42986fbaf32d0305b4c32a7a8601d32eaacc8669d8ad85a45b1f53b57ecb38183a3f54b86c8d42eea42')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$PWD:$PYTHONPATH" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # delete tests from package
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$site_packages/rebulk/test"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
