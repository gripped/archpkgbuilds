# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=rocq-stdlib
pkgver=9.1.0
pkgrel=1
pkgdesc="Stdlib for the Rocq Prover"
arch=('any')
url="https://rocq-prover.org/"
license=('LGPL-2.1-only')
depends=('rocq')
makedepends=(
  'dune'
  'ocaml'
  'ocaml-compiler-libs'
  'ocaml-findlib'
  'ocaml-zarith'
  'pango'
)
options=(
  '!debug'
  'staticlibs'
)
source=("https://github.com/rocq-prover/stdlib/archive/V$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('e69b7159a56c1ee74577ca34f94dff3198023069e403fd686f4f215ad3db76058c36a657ff26a033bcd732e4993cdefd807fbd5b6963850524aa51cba5e55195')

build() {
  cd stdlib-$pkgver
  dune build -p rocq-stdlib
}

package() {
  cd stdlib-$pkgver
  dune install rocq-stdlib \
    --prefix=/usr \
    --destdir="$pkgdir" \
    --mandir=/usr/share/man \
    --docdir=/usr/share/doc \
    --libdir=/usr/lib/ocaml
}
