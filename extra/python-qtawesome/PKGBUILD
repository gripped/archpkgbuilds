# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=qtawesome
pkgname=python-${_pkg}
pkgver=1.4.0
pkgrel=1
pkgdesc="Iconic fonts in PyQt and PySide applications"
arch=(any)
url="https://github.com/spyder-ide/qtawesome/"
license=(MIT)
depends=(python-qtpy)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-qt xorg-server-xvfb python-pyqt5)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4b05beb43a5144e1221e0be2ee55a4fe363be89fd74a635f9172dffc35798fb4')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  xvfb-run --auto-servernum python example.py
  PYTHONPATH="${PWD}"/build/lib xvfb-run --auto-servernum pytest -vv --color=yes qtawesome \
    --deselect qtawesome/tests/test_qtawesome.py::test_get_fonts_info
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
