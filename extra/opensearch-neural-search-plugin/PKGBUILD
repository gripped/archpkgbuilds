# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-neural-search-plugin
pkgver=3.1.0.0
pkgrel=1
_opensearchver=3.1.0
_jdkver=21
pkgdesc="OpenSearch Neural Search Plugin"
arch=('x86_64')
# No docs website available yet
url="https://github.com/opensearch-project/neural-search"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/neural-search/archive/${pkgver}.tar.gz"
  "ignore-failed-tests.patch"
)
sha256sums=('45f44a84838af26799935110df8f690c3c7b76980730cd9876a86bbd6a9c192e'
            'fcf4ae60d5bf7e1eceb71aadc144a0ea23bed09ac24a70dda83e8b71cc564157')

prepare(){
  cd "neural-search-${pkgver}"
  patch -p1 -i "$srcdir/ignore-failed-tests.patch"
}

build() {
  cd "neural-search-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew build \
    --exclude-task "integTest" \
    --exclude-task "jacocoTestReport" \
    --exclude-task "jacocoTestCoverageVerification"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-neural-search"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-neural-search"
  unzip "${srcdir}/neural-search-${pkgver}/build/distributions/opensearch-neural-search-${pkgver}.zip"

  install -Dm644 "${srcdir}/neural-search-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
