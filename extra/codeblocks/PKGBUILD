# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Roman Mikhayloff <rimf@inbox.ru>

pkgname=codeblocks
pkgver=25.03
pkgrel=2
pkgdesc='Cross-platform C/C++ IDE'
arch=('x86_64')
url='http://codeblocks.org/'
license=('GPL3')
depends=('wxwidgets-gtk3' 'hunspell')
makedepends=('boost' 'imagemagick' 'zip')
source=("https://downloads.sourceforge.net/codeblocks/${pkgname}_$pkgver.tar.xz"
         sc_wxtypes-normalize.patch)
sha256sums=('b0f6aa5908d336d7f41f9576b2418ac7d27efbc59282aa8c9171d88cea74049e'
            'a4d39d3016fe128e521c38a23b7b25ce94ea18263498a60d6224729dee528564')

prepare() {
  cd ${pkgname}_$pkgver
  magick src/mime/codeblocks.png +set date:create +set date:modify -background none -extent 64x64 src/mime/codeblocks.png
  sed -i 's|$(datadir)/pixmaps|$(datadir)/icons/hicolor/64x64/apps|' src/mime/Makefile.{am,in}
  sed -i 's|$(datarootdir)/appdata|$(datarootdir)/metainfo|' Makefile.{am,in} src/plugins/contrib/appdata/Makefile.{am,in}
}

build() {
  cd ${pkgname}_$pkgver
  ./configure --prefix=/usr --with-contrib-plugins=all,-FileManager
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}_$pkgver
  make DESTDIR="$pkgdir" install
}
