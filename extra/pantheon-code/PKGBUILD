# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=8.1.0
pkgrel=1
pkgdesc='The Pantheon Text Editor'
arch=(x86_64)
url=https://github.com/elementary/code
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  editorconfig-core-c
  glib2
  gtk3
  gtksourceview4
  libgee
  libgit2-glib
  libgranite.so
  libhandy-1.so
  libpeas-2.so
  pango
  polkit
  vala
)
makedepends=(
  cmake
  git
  gobject-introspection
  gtkspell3
  intltool
  libsoup
  meson
  vte3
)
optdepends=(
  'gtkspell3: Spell Check plugin'
  'libsoup: Pastebin plugin'
  'vala: Outline plugin'
  'vte3: Terminal plugin'
)
_tag=27de240aae6e8217968ebbdc0d6a57b47bed08f9
source=(pantheon-code::git+https://github.com/elementary/code.git#tag=${_tag})
b2sums=('8ae9d147bce6b2ee541ea107e87d7c2d57f1ce32ce3872cb295c7dbc2c3d15f41597a9d7aebd40397291657af702b5268c8eac542f4cd6b97ae767d67031dea3')

pkgver() {
  cd pantheon-code
  git describe --tags
}

build() {
  arch-meson pantheon-code build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set ts=2 sw=2 et:
