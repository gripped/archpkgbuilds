# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=7.4.0
pkgrel=2
pkgdesc='The Pantheon Text Editor'
arch=(x86_64)
url=https://github.com/elementary/code
license=(GPL3)
groups=(pantheon)
depends=(
  editorconfig-core-c
  glib2
  gtk3
  gtksourceview4
  libgee
  libgit2-glib
  libgranite.so
  libhandy-1.so
  libpeas
  pango
  polkit
  vala
)
makedepends=(
  cmake
  git
  gobject-introspection
  gtkspell3
  intltool
  libsoup
  meson
  vte3
)
optdepends=(
  'gtkspell3: Spell Check plugin'
  'libsoup: Pastebin plugin'
  'vala: Outline plugin'
  'vte3: Terminal plugin'
)
_tag=4ea63087971b859893f87270fad5259ab07df581
source=(pantheon-code::git+https://github.com/elementary/code.git#tag=${_tag})
b2sums=('227132d0dd7afa6c9faf50d03d62d178170a152fb779e53bcc4a6983641d13c6c2ae359a9afb2aaa6fb02c850969d5fdbbbd9ccb5afd9a6fd9141d0b2a38edb4')

pkgver() {
  cd pantheon-code
  git describe --tags
}

build() {
  arch-meson pantheon-code build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set ts=2 sw=2 et:
