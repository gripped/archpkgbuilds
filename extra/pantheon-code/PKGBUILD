# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=8.1.2
pkgrel=1
pkgdesc='The Pantheon Text Editor'
arch=(x86_64)
url=https://github.com/elementary/code
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  editorconfig-core-c
  glib2
  gtk3
  gtksourceview4
  libgee
  libgit2-glib
  libgranite.so
  libhandy-1.so
  libpeas-2.so
  pango
  polkit
  vala
)
makedepends=(
  cmake
  git
  gobject-introspection
  gtkspell3
  intltool
  libsoup
  meson
  vte3
)
optdepends=(
  'gtkspell3: Spell Check plugin'
  'libsoup: Pastebin plugin'
  'vala: Outline plugin'
  'vte3: Terminal plugin'
)
_tag=1293fb3f478fe39381ecdf916903819a57665bb7
source=(pantheon-code::git+https://github.com/elementary/code.git#tag=${_tag})
b2sums=('da1e55c94745237451981b09f7057a40e95f1dca0ea7b37e6fccb257ccec6bbc36aa698439e3294ec5aaaed524b5b13f164ce9ca9396b1b7eb25e9b31d9e6053')

pkgver() {
  cd pantheon-code
  git describe --tags
}

build() {
  arch-meson pantheon-code build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set ts=2 sw=2 et:
