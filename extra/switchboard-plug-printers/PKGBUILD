# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-printers
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard Printers Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-printers
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libcups
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=cfdeac98397d7e5bcb794d79d9233cd6180720a6
source=(git+https://github.com/elementary/switchboard-plug-printers.git#tag=${_tag})
b2sums=('186d6a1830e22f7e48bfcd95e93b19b8437cad679fa0dac561e5f7920a9e8522517d7c1dc6734527ecb458bf8c32390cf45deb8e28962f2f9ab36551a8a7f00d')

pkgver() {
  cd switchboard-plug-printers
  git describe --tags
}

build() {
  arch-meson switchboard-plug-printers build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
