# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-printers
pkgver=8.0.2
pkgrel=1
pkgdesc='Switchboard Printers Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-printers
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libcups
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_tag=96f7b71d79a878de099574ecbefe31ecc25a7171
source=(git+https://github.com/elementary/switchboard-plug-printers.git#tag=${_tag})
b2sums=('dee5aa9f7c1e477697955779a1cf0550f71073e853de6a17a3df1a07939a06f9fb5bd870e6f60a48eb52d223dc80815f24f1efc3780a1e8837065cd793506afb')

pkgver() {
  cd switchboard-plug-printers
  git describe --tags
}

build() {
  arch-meson switchboard-plug-printers build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
