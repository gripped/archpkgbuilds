# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-b2sdk
pkgver=2.7.0
pkgrel=2
pkgdesc='Python module to access B2 cloud storage'
arch=('any')
url='https://github.com/Backblaze/b2-sdk-python'
license=('MIT')
depends=(
  'python'
  'python-annotated-types'
  'python-logfury'
  'python-requests'
  'python-urllib3'
  'python-importlib-metadata'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-pdm-backend'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-mock'
  'python-pytest-lazy-fixtures'
  'python-pytest-timeout'
  'python-pyfakefs'
  'python-tqdm'
  'python-pydantic'
)
optdepends=(
  'python-tqdm: for TqdmProgressListener class'
  'python-pydantic: for improved type hints'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('5cd09260593246789bfb36ba269122cde33571acee31dc44109e513d16782a4a9d388d98620c6b1b235f304d46fa9cf19756420e48ba27f4668cab7b78b80a7f')
b2sums=('87384f1654c27470a55cd121406e22416d6842f08220bd0b41bba1fba4a7424cc67e77aa64b5381bf039593330e52145c05925b97b497c264ca90d350680b02c')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install so pytest can pick up the compiled extension
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v \
    --ignore=test/integration \
    --deselect test/static/test_licenses.py::test_files_headers
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
