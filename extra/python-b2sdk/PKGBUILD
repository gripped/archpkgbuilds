# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-b2sdk
pkgver=2.10.1
pkgrel=1
pkgdesc='Python module to access B2 cloud storage'
arch=(any)
url='https://github.com/Backblaze/b2-sdk-python'
license=(MIT)
depends=(
  python
  python-annotated-types
  python-logfury
  python-requests
  python-urllib3
  python-importlib-metadata
)
makedepends=(
  git
  python-build
  python-installer
  python-pdm-backend
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-pytest-lazy-fixtures
  python-pytest-timeout
  python-pyfakefs
  python-tqdm
  python-pydantic
)
optdepends=(
  'python-tqdm: for TqdmProgressListener class'
  'python-pydantic: for improved type hints'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('1512f73649c4e5c9864d417607fd04ce55de5da1256518b1c7d1492892f68bc1a684b1db0960f65ce65b3d55cfaf8e872187ddb2a8a0453fbdd285dd63f40e7f')
b2sums=('32a650baedfc1f6b70ccf979fa527a208812493a408d3cfca89756c6b8cbfc6dd41f91658cbc8ba208adc01411f90e029315a30ca6ff2cdea7928e497daaa3da')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install so pytest can pick up the compiled extension
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v \
    --ignore=test/integration \
    --deselect test/static/test_licenses.py::test_files_headers
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
