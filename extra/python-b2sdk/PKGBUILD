# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-b2sdk
pkgver=2.9.4
pkgrel=1
pkgdesc='Python module to access B2 cloud storage'
arch=(any)
url='https://github.com/Backblaze/b2-sdk-python'
license=(MIT)
depends=(
  python
  python-annotated-types
  python-logfury
  python-requests
  python-urllib3
  python-importlib-metadata
)
makedepends=(
  git
  python-build
  python-installer
  python-pdm-backend
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-pytest-lazy-fixtures
  python-pytest-timeout
  python-pyfakefs
  python-tqdm
  python-pydantic
)
optdepends=(
  'python-tqdm: for TqdmProgressListener class'
  'python-pydantic: for improved type hints'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('ccaa24c0b22060a5141fdad50b89ea69beb9d8e1828a363f339907cb91e41efcb3ce3b0620284e0519320891bd05bff83053f44c30157a936f8dd4541c74ed99')
b2sums=('682a43eacfa49bcc5db1df0fcbe5bd1f012d3f6d54bec551fb928247b34f32295cd74e718337f496228370ea493ccac8074c6b5c6f1e138abafe74e7f956b0ef')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install so pytest can pick up the compiled extension
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v \
    --ignore=test/integration \
    --deselect test/static/test_licenses.py::test_files_headers
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
