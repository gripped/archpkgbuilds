# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-b2sdk
pkgver=2.5.1
pkgrel=1
pkgdesc='Python module to access B2 cloud storage'
arch=('any')
url='https://github.com/Backblaze/b2-sdk-python'
license=('MIT')
depends=(
  'python'
  'python-logfury'
  'python-requests'
  'python-urllib3'
  'python-importlib-metadata'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-pdm-backend'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-mock'
  'python-pytest-lazy-fixture'
  'python-pyfakefs'
  'python-tqdm'
  'python-pydantic'
)
optdepends=(
  'python-tqdm: for TqdmProgressListener class'
  'python-pydantic: for improved type hints'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0ef1a1bf3f35d3c3f02be3fb9fb28f76fb981bddb90481a0b7e17e551fd96e1abde0eb63a03f09cf8751a3129d2711313ac938bad01c78975275e6c6fc49c8cd')
b2sums=('56f6d6b3d8840b8440bcde49897fef6c12315138320f65ffea4501d92d64cef757b414aa82fc403492e7e03304e36e2590c8b471f850e43ef930f8583c1504c4')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install so pytest can pick up the compiled extension
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v \
    --ignore=test/integration \
    --deselect test/static/test_licenses.py::test_files_headers
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
