# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-b2sdk
pkgver=2.3.0
pkgrel=1
pkgdesc='Python module to access B2 cloud storage'
arch=('any')
url='https://github.com/Backblaze/b2-sdk-python'
license=('MIT')
depends=(
  'python'
  'python-logfury'
  'python-requests'
  'python-urllib3'
  'python-setuptools'
  'python-importlib-metadata'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-pdm-backend'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-mock'
  'python-pytest-lazy-fixture'
  'python-pyfakefs'
  'python-tqdm'
)
optdepends=(
  'python-tqdm: for TqdmProgressListener class'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('10ec14826084e49042c6b79d7e0601de2feaba7d67746500c3f077d67f782ffcdc74a4c40af879d1dc596ce500b743bfcb3c4547e05a2f050ebfa857952f782a')
b2sums=('2dccca55b00964f015035cc4aa9fad3ed6bd039f410fe6e44fe8749d51f62a051ec0e6afbb96b1837e83bf67f800f93b23738c584481e6a9af3fca325ea5fd46')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install so pytest can pick up the compiled extension
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v \
    --ignore=test/integration \
    --deselect test/static/test_licenses.py::test_files_headers
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/${pkgname#python-}-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
