# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-b2sdk
pkgver=2.10.3
pkgrel=1
pkgdesc='Python module to access B2 cloud storage'
arch=(any)
url='https://github.com/Backblaze/b2-sdk-python'
license=(MIT)
depends=(
  python
  python-annotated-types
  python-logfury
  python-requests
  python-urllib3
  python-importlib-metadata
)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
  python-hatch-vcs
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-pytest-lazy-fixtures
  python-pytest-timeout
  python-pytest-xdist
  python-pyfakefs
  python-tqdm
  python-pydantic
  python-responses
  python-tenacity
)
optdepends=(
  'python-tqdm: for TqdmProgressListener class'
  'python-pydantic: for improved type hints'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('8154cc8d43f3f3b2ee436591f24f0984aeb26c0193616e2ecdacd050bd9a6da11773e6e2afa592ec58efe68301f03755ab49eaba94d3046de82f6457b8d51820')
b2sums=('4330b8597d8a8ff4a9b447cbbb5e82bd2ab8556ada549a8248b4d04d9674b7f385fa480391342676bf6d796be4689bbc71fffd436630359749e17adf83bdf5e1')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install so pytest can pick up the compiled extension
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v \
    --ignore=test/integration \
    --deselect test/static/test_licenses.py::test_files_headers
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
