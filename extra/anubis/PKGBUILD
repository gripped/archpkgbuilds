# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=anubis
pkgver=1.21.0
pkgrel=1
pkgdesc="Reverse proxy to protect against scraper bots"
url="https://anubis.techaro.lol/"
arch=(x86_64)
license=(MIT)
depends=(glibc)
makedepends=(
  brotli
  esbuild
  git
  go
  gzip
  npm
  zstd
)
backup=(etc/anubis/default.env)
options=(
  # Reproducibility fix
  # https://github.com/golang/go/pull/53528
  !lto
)
source=(
  "git+https://github.com/TecharoHQ/anubis?signed#tag=v${pkgver/[a-z]/-&}"
)
b2sums=('11ac198ae802ef24c99230e52c952be7b0e7ad1f0a6dc3855ca00583782743aa58bde77fb15b9ff0d6b864999e460a5fb5ea52697b4e1e67af8cb2d0444c9938')
validpgpkeys=(
  833F64161167B501058C394756375DA2DF02ABFF # Techaro Packages Signatures <gpg+packages@techaro.lol>
)

prepare() {
  cd anubis

  make deps
}

build() {
  cd anubis

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  make assets
  make prebaked-build
}

package() {
  cd anubis

  install -D var/anubis -t "$pkgdir/usr/bin"
  install -D var/robots2policy "$pkgdir/usr/bin/anubis-robots2policy"

  install -Dm644 run/anubis@.service -t "$pkgdir/usr/lib/systemd/system"
  install -Dm644 run/default.env -t "$pkgdir/etc/anubis"

  local docdir="$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 README.md -t "$docdir"
  cp -a docs/docs data -t "$docdir"
  find "$docdir" \( -name _category_.json -o -name '*.go' \) -delete

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
