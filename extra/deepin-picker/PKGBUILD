# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-picker
pkgver=6.0.11
pkgrel=1
pkgdesc='Color picker tool for deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-picker"
license=('GPL3')
depends=('dtk6gui' 'dtk6widget' 'qt6-base' 'gcc-libs')
makedepends=('git' 'qt6-tools')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-picker.git#tag=$pkgver")
sha512sums=('faf5e242d3452cbf225378261d263594e35c358b0b0ce637cad3aa25a06908d9ac2ee0c927a6a61339753a8c496e17eda3d2de6d40729cda744e872f160365ba')

build() {
  cd deepin-picker
  qmake6 PREFIX=/usr
  make
}

package() {
  cd deepin-picker
  make INSTALL_ROOT="$pkgdir" install
}
