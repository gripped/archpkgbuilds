# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=railway
pkgver=2.9.3
pkgrel=1
pkgdesc='Look up travel information for many different railways'
arch=(x86_64)
url='https://apps.gnome.org/DieBahn/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  pango
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.com/schmiddi-on-mobile/railway.git#tag=$pkgver")
b2sums=(d686a76c22d824cef09717bb1ec3156938b7e8e0788e3a46e08368d0d003f7fad7e8feb63f8830eed01d7b4a0b54c5d8fab5413cc86467fc937d408691b20ee3)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
