# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=railway
pkgver=2.9.1
pkgrel=1
pkgdesc='Look up travel information for many different railways'
arch=(x86_64)
url='https://apps.gnome.org/DieBahn/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  pango
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.com/schmiddi-on-mobile/railway.git#tag=$pkgver")
b2sums=(8aaae4805f9c86daac26797916972cda56ea2434fdcfdbea1775c433208ec112bf00629215dafacda813a328fa07ef02c118055c813cb150b390b5560b84ae4f)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
