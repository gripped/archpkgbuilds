# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=railway
pkgver=2.8.2
pkgrel=1
pkgdesc='Look up travel information for many different railways'
arch=(x86_64)
url='https://apps.gnome.org/DieBahn/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  pango
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
options=(!lto)
source=("git+https://gitlab.com/schmiddi-on-mobile/railway.git#tag=$pkgver")
b2sums=(f093651a308c0f983d94478fda04eecb393ffe2f7baa6a17ac5d0a39521b96dd2051b223d34b8a2c89de4267c1540a6b9022b82b19a3ddf7464994943ae4862a)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
