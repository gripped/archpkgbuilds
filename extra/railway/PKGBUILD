# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=railway
pkgver=2.7.3
pkgrel=2
pkgdesc='Look up travel information for many different railways'
arch=(x86_64)
url='https://apps.gnome.org/DieBahn/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  pango
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  rust
)
options=(!lto)
source=(
  "git+https://gitlab.com/schmiddi-on-mobile/railway.git#tag=$pkgver"
  railway-activate.patch
  railway-dbus-activatable.patch
)
b2sums=(
  558a3f2d7ae5bf44e718d489613a64723e7abab436363bea629df8209bd895a015d124a92dc515b7aa560c187f4db40bb70147c18bd9558890c0bcd013787320
  1a0ba34b55ccd5ef3bf284b33f3d1ade9eadb486c269c9cbee7096af6e3037e87c0ceb3fc62c32ce28f20c96dc1456eb33859c0e32875b0b6474b358c63d54ea
  0cf840dc462a1a88df95e50a218bf03d0e32efefb22ab8a131e219690c81e3faa251f3d5c5cbc5fa84cda253cfdae404904cdbb412ab009a00d0d1b572f0b393
)

prepare() {
  cd $pkgname

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # https://gitlab.com/schmiddi-on-mobile/railway/-/merge_requests/181
  git apply -3 ../railway-activate.patch

  # https://gitlab.com/schmiddi-on-mobile/railway/-/merge_requests/182
  git apply -3 ../railway-dbus-activatable.patch
}

build() {
  arch-meson $pkgname build

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
