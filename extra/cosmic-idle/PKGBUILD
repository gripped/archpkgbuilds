# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-idle
pkgver=1.0.0.alpha.4
pkgrel=1
pkgdesc='Cosmic idle daemon'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-idle
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_tag=eaa09a6fef304ddc236709c111bb7b0c16883f7d
source=(
  git+https://github.com/pop-os/cosmic-idle.git#tag=${_tag}
)
b2sums=('fb6ce8d25a82be0a13e68d522406706df6ed11832c052d17110a600c82a9fa1a5e0e78c2f8db1425e5e2b11ef6fa8e19d3591d314ecc3e52196cf88659daa036')

prepare() {
  cd $pkgname
  cargo fetch --locked
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd $pkgname
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  CC=clang just build-release --frozen
}

package() {
  cd $pkgname
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
