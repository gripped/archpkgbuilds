# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-idle
pkgver=1.0.0.alpha.5
pkgrel=1
pkgdesc='Cosmic idle daemon'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-idle
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_tag=7899fccbbe3d6746982178d3eff48ce8943b0f71
source=(
  git+https://github.com/pop-os/cosmic-idle.git#tag=${_tag}
)
b2sums=('0a18612ba46dcaf0930765bef6606b4a6e8f7456beab138f609263bf311ef2b5fd6eb191a93c80aee9043ab00edb874c5db85417dbb0f206ca012592ac08cffc')

prepare() {
  cd $pkgname
  cargo fetch --locked
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd $pkgname
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  CC=clang just build-release --frozen
}

package() {
  cd $pkgname
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
