# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=clairvoyant
pkgver=3.1.12
pkgrel=1
pkgdesc='Get psychic answers from this fortune teller'
arch=(x86_64)
url='https://apps.gnome.org/Clairvoyant/'
license=(GPL-3.0-or-later)
groups=(gnome-circle)
depends=(
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  vala
)
source=("git+https://github.com/cassidyjames/clairvoyant.git#tag=$pkgver")
b2sums=(fa36bd0ca596c52c0d97fb3d6c9af375f423a8e8f3b0826ae6ceb8da7d3454add04ccfcf682f03f1bf6447b94fd82105c665631429ef1f25ab03834a62cb2b99)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s com.cassidyjames.clairvoyant "$pkgdir/usr/bin/clairvoyant"
}
