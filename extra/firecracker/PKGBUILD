# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=(firecracker firecracker-docs)
pkgver=1.14.0
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=(x86_64)
url='https://firecracker-microvm.github.io'
license=(Apache-2.0 BSD-3-Clause) # firecracker is Apache, Chromium components is BSD
makedepends=(git rust clang cmake)
options=(!lto !buildflags)
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('a7fd852ea65de433ccd810026bf41852f1ee293c86216ae53b41922ffd781ad3ab3c809bbd3e6c1c8e22353893b5dfde3ed21fffdb31a862026829c64a8b448a')
b2sums=('c069e17d01efe46b455122c741a5557ae49192f71db7106103b82fc8c5bd29a287daed8e9f8a96767db0b180d4cabc4a3f9724a9ab606574dd48d964083463f6')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$(rustc --print host-tuple)"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$(rustc --print host-tuple)"
}

package_firecracker() {
  depends=(glibc gcc-libs libseccomp)

  cd "$pkgbase"

  # binaries
  find "target/$(rustc --print host-tuple)/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=(any)
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
