# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=('firecracker' 'firecracker-docs')
pkgver=1.8.0
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=('x86_64')
url='https://firecracker-microvm.github.io'
license=('Apache-2.0' 'BSD-3-Clause') # firecracker is Apache, Chromium components is BSD
makedepends=('git' 'rust' 'clang' 'cmake')
options=('!lto' '!buildflags')
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('ecff31fd9deacb2a05ada4a953a84abd2e42863080f365f8e827b6a5d5bf250cb8615c9cd5b3cb0b58982a5f36a4547dbf2f519d3caa04709362c8df9f5e5c35')
b2sums=('b0e6fd91e0618426edf6655113072e6ff162b709e40a13785e44e51cc9ec48eb3555cc737d0eaa0be45edacbe04f3b317e97a1c60ad96a2658a3b31df638fec7')

_cargo_target="$CARCH-unknown-linux-gnu"

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$_cargo_target"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$_cargo_target"
}

package_firecracker() {
  depends=('glibc' 'gcc-libs')

  cd "$pkgbase"

  # binaries
  find "target/$_cargo_target/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=('any')
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
