# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=(firecracker firecracker-docs)
pkgver=1.13.1
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=(x86_64)
url='https://firecracker-microvm.github.io'
license=(Apache-2.0 BSD-3-Clause) # firecracker is Apache, Chromium components is BSD
makedepends=(git rust clang cmake)
options=(!lto !buildflags)
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('ea19b8e368696797f142e0d1c6ab64f6e846cea5bc67e9bcc7b524f2063746878490bf1231d89b168ee72dad3423c1405a5dd719996a7d29058dc622b28d9307')
b2sums=('7cdcab821c165c6e07f23430977756e5d492cab52eaeb4407552ec50bebca3efb09df24e2bd229266ddead32e51940c647b6da17fdfbacf125bad43c5f7f7cfe')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$(rustc -vV | sed -n 's/host: //p')"
}

package_firecracker() {
  depends=(glibc gcc-libs libseccomp)

  cd "$pkgbase"

  # binaries
  find "target/$(rustc -vV | sed -n 's/host: //p')/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=(any)
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
