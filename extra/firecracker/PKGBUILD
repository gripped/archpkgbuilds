# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=('firecracker' 'firecracker-docs')
pkgver=1.12.1
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=('x86_64')
url='https://firecracker-microvm.github.io'
license=('Apache-2.0' 'BSD-3-Clause') # firecracker is Apache, Chromium components is BSD
makedepends=('git' 'rust' 'clang' 'cmake')
options=('!lto' '!buildflags')
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('739c6258af03a70f03ebef5c91df7625b5e70c7f789536b441150d2a469b26eb7f4cc983933887dee2adf7f8805a479714ff209e67f5bced14ad6ef12b63459e')
b2sums=('38a1f8e69fcae9f144ccc70ca9ae8e3cd539e80f2d6dc64975976d1f331a2ec927953c13b3ca97501764dde859a1f1b9755efdcde65e8251feebd1b042d0beb5')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$(rustc -vV | sed -n 's/host: //p')"
}

package_firecracker() {
  depends=('glibc' 'gcc-libs' 'libseccomp')

  cd "$pkgbase"

  # binaries
  find "target/$(rustc -vV | sed -n 's/host: //p')/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=('any')
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
