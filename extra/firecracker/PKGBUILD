# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=('firecracker' 'firecracker-docs')
pkgver=1.11.0
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=('x86_64')
url='https://firecracker-microvm.github.io'
license=('Apache-2.0' 'BSD-3-Clause') # firecracker is Apache, Chromium components is BSD
makedepends=('git' 'rust' 'clang' 'cmake')
options=('!lto' '!buildflags')
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('1b3e310e6396e3ceaf5cb78f003ab40932cec8a46196dde9c1e5450981e525dd6bd387d2993610aaa741b01f33b92ef7f356ec4c96e9f0f820a73def9d5c852a')
b2sums=('0255a240d2b429566cfd363c01eb52abbb159104e11b96df70e3e070cbef9b4db5b3ec527f1ead9b9acdb627f9363bb7cbd6771ca8c11a6a306bf7bf17c3175d')

_cargo_target="$CARCH-unknown-linux-gnu"

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$_cargo_target"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$_cargo_target"
}

package_firecracker() {
  depends=('glibc' 'gcc-libs')

  cd "$pkgbase"

  # binaries
  find "target/$_cargo_target/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=('any')
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
