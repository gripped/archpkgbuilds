# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=('firecracker' 'firecracker-docs')
pkgver=1.10.1
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=('x86_64')
url='https://firecracker-microvm.github.io'
license=('Apache-2.0' 'BSD-3-Clause') # firecracker is Apache, Chromium components is BSD
makedepends=('git' 'rust' 'clang' 'cmake')
options=('!lto' '!buildflags')
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('24b754f1b3fb4f2292f387e29c1c56293a7f859ec042e39e17fa5492a16c9e7b4f80f1c5707cc8fb319bc9aa6da65d7b65bd4898cd2e3ff62ebe62cd8c444bcf')
b2sums=('7e45ae6317bb1d75461b402a04e1344e8e7be281a6f5a40a89525b43f57560345667d13cb35c88a27501aa79a054dcc2a491767deb771e12edbb7c3358bf08af')

_cargo_target="$CARCH-unknown-linux-gnu"

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$_cargo_target"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$_cargo_target"
}

package_firecracker() {
  depends=('glibc' 'gcc-libs')

  cd "$pkgbase"

  # binaries
  find "target/$_cargo_target/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=('any')
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
