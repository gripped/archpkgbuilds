# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=(firecracker firecracker-docs)
pkgver=1.13.0
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=(x86_64)
url='https://firecracker-microvm.github.io'
license=(Apache-2.0 BSD-3-Clause) # firecracker is Apache, Chromium components is BSD
makedepends=(git rust clang cmake)
options=(!lto !buildflags)
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('b344e2ed269df2864a65e585f96992b3226f5d7ad14af8a051f301778a493369f0b3d86af80aba95e5083fa883aabef033e23c9763370051d907b732955b4e41')
b2sums=('5328c523ad9734e90ec643de813906a363f969aeadf4eb951c87a91b741d76e1e68ac912cc73d11e59f8a463d53407fe761eecfdbf8e6e024e28841ff071020b')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$(rustc -vV | sed -n 's/host: //p')"
}

package_firecracker() {
  depends=(glibc gcc-libs libseccomp)

  cd "$pkgbase"

  # binaries
  find "target/$(rustc -vV | sed -n 's/host: //p')/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=(any)
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
