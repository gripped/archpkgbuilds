# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=(firecracker firecracker-docs)
pkgver=1.14.1
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=(x86_64)
url='https://firecracker-microvm.github.io'
license=(Apache-2.0 BSD-3-Clause) # firecracker is Apache, Chromium components is BSD
makedepends=(git rust clang cmake)
options=(!lto !buildflags)
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('09f0f53b29b303a29463358e630b365d8f1423d3ef096115576236d6e1740214c8ad1e1e0d34438a5e710a10e02234e4faa8c4ef2b5fd50d62a4f359f20a181e')
b2sums=('24e2ab16e87c35ea811ab583422deb7734d5683bddfddabd816ed68fbba5df3693d110e48f75c99c0a9125b925a1c9cf91d6ec47037338a32c0f73367928f40a')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$(rustc --print host-tuple)"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$(rustc --print host-tuple)"
}

package_firecracker() {
  depends=(glibc gcc-libs libseccomp)

  cd "$pkgbase"

  # binaries
  find "target/$(rustc --print host-tuple)/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=(any)
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
