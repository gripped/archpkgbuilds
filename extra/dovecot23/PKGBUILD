# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>
# Contributor: Phillip Schichtel <phillip@schich.tel>

# --->>> remember to rebuild/bump the following packages TOGETHER with a new dovecot ABI:
#   +pigeonhole23
#   +dovecot23-fts-elastic
#   +dovecot23-fts-xapian

pkgname=dovecot23
pkgver=2.3.21.1
pkgrel=3

pkgdesc="An IMAP and POP3 server written with security primarily in mind (2.3 branch)"
url="https://dovecot.org/"
arch=('x86_64')
license=(
  'MIT'
  'LGPL-2.1-only'
)

depends=(
  'bzip2'
  'expat'
  'gcc-libs'
  'glibc'
  'icu'
  'krb5'
  'libcap'
  'libsodium'
  'libstemmer'
  'libxcrypt'
  'lz4'
  'mariadb-libs'
  'openssl'
  'pam'
  'postgresql-libs'
  'sqlite'
  'systemd-libs'
  'zlib'
  'zstd'
)
makedepends=(
  'libldap'
  'lua53'
  'systemd'
  'xapian-core'
)

optdepends=(
  'libldap: ldap plugin'
  'lua53: LUA auth and push support'
  'xapian-core: FTS flatcurve indexer'
)

provides=(
  'imap-server'
  'pop3-server'
  'dovecot'
)

conflicts=(
  'dovecot'
  'pigeonhole>=2.4'
  'dovecot-fts-elastic'
  'dovecot-fts-xapian'
  )

backup=(
  'etc/dovecot/dovecot.conf'
  'etc/pam.d/dovecot'
)

options=('!emptydirs' '!lto')

source=(
  "https://dovecot.org/releases/2.3/${pkgname%23}-${pkgver}.tar.gz"{,.sig}
  'dovecot-2.3.14-opensslv3.patch'
  'dovecot-2.3.21.1-fixicu.patch'
  'dovecot-2.3.21.1-disable-broken-tests.patch'
  'dovecot.sysusers'
  'dovecot.tmpfiles'
  'dovecot.ld.so.conf'
  'dovecot.pam'
  'upstream_0001_1fd4fcc00335a0560e5686e17aceece7933f214d.patch::https://github.com/dovecot/core/commit/1fd4fcc00335a0560e5686e17aceece7933f214d.patch'
  'upstream_0002_9a00369c8a09ccb2799dd6b3a4767947f5ace55a.patch::https://github.com/dovecot/core/commit/9a00369c8a09ccb2799dd6b3a4767947f5ace55a.patch'
  'upstream_0003_30b7e3cb5c7037a5a2d31096a75696d70c9c9f94.patch::https://github.com/dovecot/core/commit/30b7e3cb5c7037a5a2d31096a75696d70c9c9f94.patch'
  'upstream_0004_97791e1ea53fdff64bafef6ff572b237b41dbe3b.patch::https://github.com/dovecot/core/commit/97791e1ea53fdff64bafef6ff572b237b41dbe3b.patch'
  'upstream_0005_53c7113720fbf4768f6413fdfbb2e55bc778716d.patch::https://github.com/dovecot/core/commit/53c7113720fbf4768f6413fdfbb2e55bc778716d.patch'
  'upstream_0006_6afc5b06a543c936aa428d758d7f8bad3f13fb66.patch::https://github.com/dovecot/core/commit/6afc5b06a543c936aa428d758d7f8bad3f13fb66.patch'
  'upstream_0007_bc94afb46ad201af6c4a0e145ac07f8730973b01.patch::https://github.com/dovecot/core/commit/bc94afb46ad201af6c4a0e145ac07f8730973b01.patch'
  'upstream_0008_7fe2c39271b8e993632fd1b9a3ec95e031360f24.patch::https://github.com/dovecot/core/commit/7fe2c39271b8e993632fd1b9a3ec95e031360f24.patch'
  'upstream_0009_13061d620c1dd6952bbb7b0d4a7ae4f5e665b8d9.patch::https://github.com/dovecot/core/commit/13061d620c1dd6952bbb7b0d4a7ae4f5e665b8d9.patch'
  'upstream_0010_4eb23fdf2d285d710dc1f70b5158dbc6ffef48aa.patch::https://github.com/dovecot/core/commit/4eb23fdf2d285d710dc1f70b5158dbc6ffef48aa.patch'
  'upstream_0011_8223d09707c3dadb2221be8d55bbf2c581e682d2.patch::https://github.com/dovecot/core/commit/8223d09707c3dadb2221be8d55bbf2c581e682d2.patch'
  'upstream_0012_c8a009249d033834196c46ef30ef95865ef34482.patch::https://github.com/dovecot/core/commit/c8a009249d033834196c46ef30ef95865ef34482.patch'
  'upstream_0013_d3a097075afb7b5d8955978ed4a2ae99add11f88.patch::https://github.com/dovecot/core/commit/d3a097075afb7b5d8955978ed4a2ae99add11f88.patch'
  'upstream_0014_efdceaab8aa8c2443264b5cc2239e19c72904227.patch::https://github.com/dovecot/core/commit/efdceaab8aa8c2443264b5cc2239e19c72904227.patch'
)

sha256sums=('2d90a178c4297611088bf7daae5492a3bc3d5ab6328c3a032eb425d2c249097e'
            'SKIP'
            '356e5761dc9161283cb795e62997c807ca081f4b42b443001cce07c03c47876d'
            '9fe66602ee7d3c180c8d65ce705464e491777bdf60550d104c825593b8e0399b'
            'bc55f17e72e46c9c0cb5bf2ffcdddeaede37bdc39563b810fd90c63e083550fd'
            '068b16ab8afcc4f5cbced76269264088aed6d662db409b94bd5d22e816a869cc'
            '0b0625b1e66ca6a95d506fd00d6a68e70620c8ea28606e2528953ffb1806b08e'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858'
            'ad9245f5e916480edd67139603cbe52e7a868233075f900ab63a0ce58f03741a'
            'c29c1e39937f99c29162867b9fa52af955d55dd4d3e620c1de985854fab871b6'
            'daca88ad68fc0c471540073cbe4fb498b91de57105b8a023d88517c28f42e828'
            'eb2c82e9afc5b13e30e33edf4ff22173f73556652d2fd9e2d0093e39646cf1ba'
            '66fe9039525b9fd0d98ca2ebd7b6694aff4d11f2e57662335a0ccb14209d7bc1'
            '826713adb0d9b4087ce65dc2c90a4437aead0ca8ebfc43102658b203d6b9e2e5'
            '068d8af537fae13a2dac933a4c359ffb6532cecb784c172b3f3e118f04a86145'
            '63f3d616bab3a99b958459610ec2d585561d7415548cc5b80160a6a3cdf93328'
            'a145860264267d51cdac87ecec125383ee695ec4ecaff3b6faa3b1d5936a32fe'
            '8b850d950667fc6dc042f88d15ba4b6b95ea34a322e6367a760bffea0204ab09'
            '4bcab05beb6ad4eb7e06df7f7ed79280f93e43a3b787f5edb4821534c50b736d'
            '10311869269e4649417ae633157d2f4254ea79b87bc39847c13ed81dcde528c3'
            '69179a44e80a045e1b45668e4dba48d63c3e7fb9e3831b37c81aa71cf8dc47ce'
            'd8d5ef576bba8b12555ab352bcc348b213cf4386c8ddcff5a3dd2b33c25a6a2c'
            '2720d1d8bd35f8f15412451a81cee31df1a6b1205b85e404ae129242e1f16f93')

validpgpkeys=(
  'E643F0BDFDCD04D9FFCB6279C948525140558AC9' # Timo Sirainen <tss@iki.fi>
  '2BE74AAB3EE754DFB9C80D3318A348AEED409DA1' # Dovecot Community Edition
  'EF0882079FD4ED32BF8B23B2A1B09EF84EDC5219' # Dovecot Community Edition <dovecot-ce@dovecot.org>
)

prepare() {
  cd "${pkgname%23}-${pkgver}"

  # Apply commits from release-2.3 branch that aren't
  # part of the 2.3.21.1 release.
  # Be advised: These need to happen *in chronological order*!
  # (i.e. from oldest to newest)
  #
  # Unfortunately the 2.3.21.1 tag was not made *on* the release-2.3 branch,
  # so you cannot rely on that.
  # To identify the post-release commits, clone the dovecot/core repo,
  # switch to the release-2.3 branch and run:
  # git log --reverse --format="%H" --since "Fri Aug 9 12:24:39 2024 +0300"
  # With 'Fri Aug 9 12:24:39 2024 +0300' being the timestamp of the 2.3.21.1 tag.
  #
  # With a bit of 'git format-patch -1 "${COMMIT}" --stdout' you can write those out
  # to individual .patch files, ideally to somewhere outside of the git workdir.
  # Afterwards, checkout the 2.3.21.1 tag, and run 'git apply --check "${PATCHFILE}"'
  # in chronological order again to check if it's applicable or not.
  # Note: If applicable, you need to apply it, otherwise later patches
  # won't succeed.
  #
  # After discarding the incompatible ones and possibly one commit only concerning
  # ChangeLog generation (461899d0c38e24dc47eefe1254b20c29bb24b332), you
  # will end up with a detached-HEAD workdir state, that contains all applicable
  # changes from the release-2.3 branch after the 2.3.21.1 tag.
  for i in "${srcdir}/upstream_"*.patch ; do
    patch -Np1 -i "${i}"
  done


  patch -Np1 -i ../dovecot-2.3.14-opensslv3.patch
  patch -Np1 -i ../dovecot-2.3.21.1-disable-broken-tests.patch

  # fix path in helper script
  sed -i 's:OPENSSLCONFIG=${OPENSSLCONFIG-dovecot-openssl.cnf}:OPENSSLCONFIG=${OPENSSLCONFIG- /etc/ssl/dovecot-openssl.cnf}:' doc/mkcert.sh

  # fix build
  patch -Np1 -i ../dovecot-2.3.21.1-fixicu.patch

  autoreconf -vfi -I /usr/share/gettext/m4
}

build() {
  cd "${pkgname%23}-${pkgver}"

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-rundir=/run/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static \
    --with-nss \
    --with-pam \
    --with-sqlite \
    --with-pgsql \
    --with-mysql \
    --with-ssl=openssl \
    --with-ssldir=/etc/ssl \
    --with-gssapi \
    --with-ldap=plugin \
    --with-lua=plugin \
    --with-zlib \
    --with-bzlib \
    --with-lzma \
    --with-lz4 \
    --with-zstd \
    --with-solr \
    --with-sodium \
    --with-libcap \
    --with-docs

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd "${pkgname%23}-${pkgver}"
  make check
}

package() {
  # system user/group dovenull - 74
  # system user/group dovecot  - 76

  cd "${pkgname%23}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "${srcdir}/dovecot.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/dovecot.conf"
  install -Dm644 "${srcdir}/dovecot.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/dovecot.conf"
  install -d -m755 "${pkgdir}/etc/dovecot/conf.d"
  rm -f "${pkgdir}/etc/dovecot/README"

  # install mkcert helper script
  install -m 755  doc/mkcert.sh "${pkgdir}/usr/lib/dovecot/mkcert.sh"

  # add dovecot libdir
  install -Dm644 "${srcdir}/dovecot.ld.so.conf" "${pkgdir}/etc/ld.so.conf.d/dovecot.conf"

  # install PAM snippet for dovecot
  install -Dm644 "${srcdir}/dovecot.pam" "${pkgdir}/etc/pam.d/dovecot"

  # license
  install -D -m644 COPYING.MIT "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING.MIT
}
