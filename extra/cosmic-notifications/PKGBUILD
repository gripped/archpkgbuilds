# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: soloturn <soloturn@gmail.com>

pkgname=cosmic-notifications
pkgver=1.0.0.alpha.5
pkgrel=1
pkgdesc='Layer Shell notifications daemon which integrates with COSMIC'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-notifications
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=76c237b0509ea6f724076db2195a7fab6942f33c
source=(
  git+https://github.com/pop-os/cosmic-notifications.git#tag=${_tag}
  cosmic-notifications-lto.patch
)
b2sums=('7851003c2643d53ece955f7e864357960ab8f09bb234c143ab9ac63ae66c6e2e9f8a5100885cec46a8a8b056b815c42db1c5f7df52094ee73f6a57283bac5279'
        '2c3b1b321c7d86c43806885d9b0cc24485bd0186adebdaf3f511cbd861ead2fd7a1491d5583fb85f686c833c1cb2622ce944781f7b26d5486a86509b79ac636a')

prepare() {
  cd cosmic-notifications
  patch -Np1 -i ../cosmic-notifications-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-notifications
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-notifications
  just build-release --frozen
}

package() {
  cd cosmic-notifications
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
