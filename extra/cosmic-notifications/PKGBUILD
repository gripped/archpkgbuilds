# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: soloturn <soloturn@gmail.com>

pkgname=cosmic-notifications
pkgver=1.0.0.beta.1
pkgrel=1
pkgdesc='Layer Shell notifications daemon which integrates with COSMIC'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-notifications
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=19d24637d45a32a116653f0cf1501d4eb9f8b1ee
source=(
  git+https://github.com/pop-os/cosmic-notifications.git#tag=${_tag}
)
b2sums=('47460f38e570fa7d910170878fd1129db7bdc03710d28a703cf9afa9ec4a63432ff6048104597199ed05528a57921d4f2be3beb1317efc954ad2373ff1d51344')

prepare() {
  cd cosmic-notifications
  cargo fetch --locked
}

pkgver() {
  cd cosmic-notifications
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-notifications
  just build-release --frozen
}

package() {
  cd cosmic-notifications
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
