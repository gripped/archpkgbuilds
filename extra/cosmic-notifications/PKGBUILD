# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: soloturn <soloturn@gmail.com>

pkgname=cosmic-notifications
pkgver=1.0.0.alpha.4
pkgrel=1
pkgdesc='Layer Shell notifications daemon which integrates with COSMIC'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-notifications
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=d4d5f429337004c1721e0072ad1c25509229586f
source=(
  git+https://github.com/pop-os/cosmic-notifications.git#tag=${_tag}
  cosmic-notifications-lto.patch
)
b2sums=('db36dde4c2d04bc81689de90c8cd2b221a0f0e12536231ec08af00fd1acbd62121a190773fd1d77951f94144d8b66392f373b633087dda1066705a67088acdce'
        '2c3b1b321c7d86c43806885d9b0cc24485bd0186adebdaf3f511cbd861ead2fd7a1491d5583fb85f686c833c1cb2622ce944781f7b26d5486a86509b79ac636a')

prepare() {
  cd cosmic-notifications
  patch -Np1 -i ../cosmic-notifications-lto.patch
  cargo fetch --locked
}

pkgver() {
  cd cosmic-notifications
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-notifications
  just build-release --frozen
}

package() {
  cd cosmic-notifications
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
