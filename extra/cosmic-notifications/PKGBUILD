# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: soloturn <soloturn@gmail.com>

pkgname=cosmic-notifications
pkgver=1.0.0.alpha.7
pkgrel=1
pkgdesc='Layer Shell notifications daemon which integrates with COSMIC'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-notifications
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=8eae7e0f62437a861cba4baa93b63a956c36756d
source=(
  git+https://github.com/pop-os/cosmic-notifications.git#tag=${_tag}
)
b2sums=('5ab68ad138c12da036606e0a6968f3a748f1176b0d9c80e5af92a0c62c77f243053ae9dc876f85eb5932d3276cc9a1c3b91e9df5adfd34e84ec50108284020e2')

prepare() {
  cd cosmic-notifications
  cargo fetch --locked
}

pkgver() {
  cd cosmic-notifications
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-notifications
  just build-release --frozen
}

package() {
  cd cosmic-notifications
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
