# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: soloturn <soloturn@gmail.com>

pkgname=cosmic-notifications
pkgver=1.0.0.beta.4
pkgrel=1
pkgdesc='Layer Shell notifications daemon which integrates with COSMIC'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-notifications
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=32c94572eb9d65b27afac37e791c31213cb5dc55
source=(
  git+https://github.com/pop-os/cosmic-notifications.git#tag=${_tag}
)
b2sums=('eaee2f491dfc9ed922a5a3a6187344a38e318af0e26e50b1084b2b5d99c975dbbc1f620f952f50fe637f3689d01275b95f6dc59c05c445e8c9ef5ecbd9d169fb')

prepare() {
  cd cosmic-notifications
  cargo fetch --locked
}

#pkgver() {
#  cd cosmic-notifications
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-notifications
  just build-release --frozen
}

package() {
  cd cosmic-notifications
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
