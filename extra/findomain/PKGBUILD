# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Eduard Tolosa <edu4rdshl@protonmail.com>

pkgname=findomain
_pkgname=Findomain
pkgver=10.0.1
pkgrel=1
pkgdesc='The fastest and cross-platform subdomain enumerator, do not waste your time'
url='https://github.com/Findomain/Findomain'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
  'openssl'
)
makedepends=('cargo')
optdepends=('postgresql: for subdomain monitoring')
source=(
  "https://github.com/${_pkgname}/${_pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
)
b2sums=('0ca6e4307a8a0f1be44757d7fefa4cc664d84de547618518822b0593a6291655cc7609924e2d9993853ba5f23a1560627307a25ed7a1e891e83ad5285662339e')
options=(!lto)

prepare() {
  cd $_pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${_pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo build --release --frozen
}

check() {
  cd ${_pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 cargo test --release --frozen
}

package() {
  cd ${_pkgname}-${pkgver}

  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
