# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

_name=starlette
pkgname=python-$_name
pkgver=0.43.0
pkgrel=3
pkgdesc='The little ASGI framework that shines'
arch=(any)
url="https://github.com/encode/starlette"
license=(BSD-3-Clause)
depends=(
  python
  python-anyio
)
makedepends=(
  python-build
  python-installer
  python-hatchling
  python-wheel
)
checkdepends=(
  python-aiosqlite
  python-databases
  python-pytest
  python-trio

  # optdepends
  python-itsdangerous
  python-jinja
  python-python-multipart
  python-pyyaml
  python-httpx

  # not specified,but required
  python-sqlalchemy
)
optdepends=(
  'python-itsdangerous: for session middleware support'
  'python-jinja: for jinja templates'
  'python-python-multipart: for form parsing'
  'python-pyyaml: for schema generator'
  'python-httpx: for test client'
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
        fix_tests_with_httpx_0.27.patch)
sha512sums=('9cb80e0eebb6fecde411dce6c3beb1d733ccf887b9145c88b5608ef78e942d8c808c0bc8971fcdb233b6f2b973617fe7e60bc8a0d97cc14b002efe74f1900683'
            '673f1e3f714b2d7c962111e279d9086ee2868fab93e537f6363749c2043c92711db403460d5adfc8520cc8253417a47f55a2da4a1df4aa4abc0e662dad25875a')
b2sums=('9905f678f5ea26e40e8b37cbf130fb03c1b6337c30777a24f3092134ac2ab077b4de2ca7b25feae8ee6bdb3334558c196688d8dca89f519d0949370992477218'
        'c3a2a1f340c912940a78ada3925729ee635ee58dde3b79391dc87cb266c72434fdcb67f04d1b3f77f7f2388dbcd9f1bf323bf819dcc3b7f5b3ff50fa0265f533')

prepare() {
  cd $_name-$pkgver
  # Ignore new warnings from python-anyio 4.4.0 making check() fails
  sed -i '/"error"/d' pyproject.toml
  # Temporary test to fix tests with python-httpx < 0.28
  # Reverts https://github.com/encode/starlette/pull/2773
  patch -Np1 < $srcdir/fix_tests_with_httpx_0.27.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --deselect 'tests/test_routing.py::test_lifespan_with_on_events[asyncio]'
    --deselect 'tests/test_routing.py::test_lifespan_with_on_events[trio]'
  )

  cd $_name-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
