# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-minidb
_name=${pkgname#python-}
pkgver=2.0.7
pkgrel=3
pkgdesc='Simple SQLite3 store for Python objects'
arch=('any')
url=https://thp.io/2010/minidb/
license=('ISC')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("git+https://github.com/thp/minidb.git#tag=$pkgver")
b2sums=('7691065adfc28ae0dd2c10671560b4c3494374f654b5a707eac8fa845a80a5c9c91b3caf8c89e719f2133db84b79d143f4f63127c1d17ab826f9410153975491')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  sed -n '/# Copyright/,/OF THIS SOFTWARE./p' minidb.py > "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
