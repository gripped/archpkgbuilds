# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kenneth Endfinger <kaendfinger@gmail.com>

pkgname=buildkit
pkgver=0.26.3
pkgrel=1
pkgdesc='A toolkit for converting source code to build artifacts in a repeatable manner'
arch=(x86_64)
url='https://github.com/moby/buildkit'
license=(Apache-2.0)
depends=(runc containerd)
makedepends=(git go)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('9b234831e854350734cbaaf7778b08d29285a212f9c1aa2f08d0fff0ad15d53adee27b0314bb31b1bf4ba25301b21f3908018d84bae3b9d0f71e3d12604cc6ab')
b2sums=('adc61275cde5628266a0295008abb8ae235a17a4d681b28c86a7ea626a16aaaab7ed5ca676e64799e388334ab41170c4c00db55e468375aeeb03d2f5f06f8fc5')

prepare() {
  cd "$pkgname"

  # fix paths in systemd unit files
  sed -i 's:/usr/local:/usr:' \
    examples/systemd/{system,user}/*.service

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  local package='github.com/moby/buildkit'

  local _commit="$(git rev-list -1 HEAD)"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X ${package}/version.Version=$pkgver \
    -X ${package}/version.Revision=$_commit" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" build/*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vrt "$pkgdir/usr/share/doc/$pkgname" docs

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" examples/systemd/system/*
  install -vDm644 -t "$pkgdir/usr/lib/systemd/user" examples/systemd/user/*
}
