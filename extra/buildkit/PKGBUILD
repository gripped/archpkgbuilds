# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kenneth Endfinger <kaendfinger@gmail.com>

pkgname=buildkit
pkgver=0.27.1
pkgrel=1
pkgdesc='A toolkit for converting source code to build artifacts in a repeatable manner'
arch=(x86_64)
url='https://github.com/moby/buildkit'
license=(Apache-2.0)
depends=(runc containerd)
makedepends=(git go)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('6adcdad1cce5202994328abd0a74120f040c8dd32ed0d4bab37129008eacda9908f1e99c2ecf6ca9726df07e2c42476a7e943171f82c7952a64e7d4245d8b008')
b2sums=('829315d48f209ce9162da67933216a60bed48f2dcd9e7c7153122d729b5d020e80339e491c7b0e027cf76db6989495cda8fbb54ec87ea44410489762d9511a97')

prepare() {
  cd "$pkgname"

  # fix paths in systemd unit files
  sed -i 's:/usr/local:/usr:' \
    examples/systemd/{system,user}/*.service

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  local package='github.com/moby/buildkit'

  local _commit="$(git rev-list -1 HEAD)"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X ${package}/version.Version=$pkgver \
    -X ${package}/version.Revision=$_commit" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" build/*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vrt "$pkgdir/usr/share/doc/$pkgname" docs

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" examples/systemd/system/*
  install -vDm644 -t "$pkgdir/usr/lib/systemd/user" examples/systemd/user/*
}
