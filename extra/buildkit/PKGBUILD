# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kenneth Endfinger <kaendfinger@gmail.com>

pkgname=buildkit
pkgver=0.25.1
pkgrel=1
pkgdesc='A toolkit for converting source code to build artifacts in a repeatable manner'
arch=(x86_64)
url='https://github.com/moby/buildkit'
license=(Apache-2.0)
depends=(runc containerd)
makedepends=(git go)
options=(!lto)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('af9916723913c81c3a8418abdbb2e9fdf2c970dcce07e6aaea4614f5997965d6d949a9d2da45980236ab64363f0d119be8f649021d42c3af6d625c5996c5cba8')
b2sums=('c1a59074a32356d187cc56a05b7b39b9a79afa6b4f51f974b5c080bcd506f78374e75f81d725a0d36f1a429f0a1af5af8d881f2824ce5204b672f4f788504986')

prepare() {
  cd "$pkgname"

  # fix paths in systemd unit files
  sed -i 's:/usr/local:/usr:' \
    examples/systemd/{system,user}/*.service

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  local package='github.com/moby/buildkit'

  local _commit="$(git rev-list -1 HEAD)"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X ${package}/version.Version=$pkgver \
    -X ${package}/version.Revision=$_commit" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" build/*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vrt "$pkgdir/usr/share/doc/$pkgname" docs

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" examples/systemd/system/*
  install -vDm644 -t "$pkgdir/usr/lib/systemd/user" examples/systemd/user/*
}
