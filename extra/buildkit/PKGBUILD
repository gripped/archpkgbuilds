# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kenneth Endfinger <kaendfinger@gmail.com>

pkgname=buildkit
pkgver=0.26.2
pkgrel=1
pkgdesc='A toolkit for converting source code to build artifacts in a repeatable manner'
arch=(x86_64)
url='https://github.com/moby/buildkit'
license=(Apache-2.0)
depends=(runc containerd)
makedepends=(git go)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('6c8b08b7ee8806ff216e38a58dddff3a65ab9d041a72680371af4a2a26f68a5f8d0702d648ea60c9e44dd8879f15a7293b5a9990377eddd16c1d163a6b292eaa')
b2sums=('fb69cafb6abf4dc7cf0e8e68562191a1877e5485e726d7bd64968038a9f0f1a32870f31cfbbf4a581187342880165f58a1d62929cf8d5abc9da9dd44538aa8ef')

prepare() {
  cd "$pkgname"

  # fix paths in systemd unit files
  sed -i 's:/usr/local:/usr:' \
    examples/systemd/{system,user}/*.service

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  local package='github.com/moby/buildkit'

  local _commit="$(git rev-list -1 HEAD)"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X ${package}/version.Version=$pkgver \
    -X ${package}/version.Revision=$_commit" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" build/*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vrt "$pkgdir/usr/share/doc/$pkgname" docs

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" examples/systemd/system/*
  install -vDm644 -t "$pkgdir/usr/lib/systemd/user" examples/systemd/user/*
}
