# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=nageru
pkgver=2.3.2
pkgrel=2
pkgdesc="live video mixer, based around the standard M/E workflow."
arch=(x86_64)
url="https://nageru.sesse.net/"
license=('GPL-3.0-or-later')
depends=('libmicrohttpd' 'ffmpeg' 'libx264' 'protobuf' 
         'movit' 'bmusb' 'qcustomplot' 'pciutils' 'zita-resampler'
         'sqlite3' 'luajit' 'cmake')
makedepends=('meson' 'eigen' 'qt5-tools')
source=("https://nageru.sesse.net/nageru-${pkgver}.tar.gz")
sha256sums=('89fd0b67052d08262f59b25739ea5d0ea5fff7a46180603a93cd4f9037226b08')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build -v
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
