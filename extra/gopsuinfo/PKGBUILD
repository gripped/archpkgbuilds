# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=gopsuinfo
pkgver=0.1.9
pkgrel=3
pkgdesc="A gopsutil-based command to display customizable system info"
url="https://github.com/nwg-piotr/gopsuinfo"
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('6d845fb36edee53060e910500c8cca54b2495f19a6f330fd1bd389f271b31c7c')

prepare(){
        cd "${pkgname}-${pkgver}"
        mkdir -p build/
}

build() {
        cd "${pkgname}-${pkgver}"
        export CGO_CPPFLAGS="${CPPFLAGS}"
        export CGO_CFLAGS="${CFLAGS}"
        export CGO_CXXFLAGS="${CXXFLAGS}"
        export CGO_LDFLAGS="${LDFLAGS}"
        export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
        go build -ldflags="-linkmode=external -buildid=''" -o "build/${pkgname}" *.go
}

package() {
        cd "${pkgname}-${pkgver}"
	install -Dm 755 "build/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm 644 icons_light/* -t "${pkgdir}/usr/share/${pkgname}/icons_light/"
	install -Dm 644 icons_dark/* -t "${pkgdir}/usr/share/${pkgname}/icons_dark/"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
