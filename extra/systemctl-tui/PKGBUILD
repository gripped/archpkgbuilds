# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=systemctl-tui
pkgver=0.4.0
pkgrel=1
pkgdesc="A fast, simple TUI for interacting with systemd services and their logs"
arch=('x86_64')
url="https://github.com/rgwood/systemctl-tui"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('685854f6e07a3132499a16b7207f40bc5366f3fa6c578bad200f380d838d96cd7f94c8363f87b4668d7f9f8d58247db81242870a61573ed256759ca960576a15')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
