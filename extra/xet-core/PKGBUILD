# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgbase=xet-core
pkgname=(python-hf-xet)
pkgver=1.1.4
pkgrel=1
pkgdesc="xet client tech, used in Huggingface Hub"
arch=(x86_64)
url=https://github.com/huggingface/xet-core
license=(Apache-2.0)
makedepends=(
  cargo
  maturin
  python-installer
  openssl
)
source=($pkgbase-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('02c25b3f9ce0ef4641083423bca931faae903d6607f016547f71dfdabd3a0d10f3aa3d5f7c4252b7ebaae676a34ed6c4ad8e314793c36ea8d2fc21097ece3a81')

prepare() {
  cd $pkgbase-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  # fix building with LTO
  CFLAGS+=' -ffat-lto-objects'

  cd $pkgbase-$pkgver
  # there don't seem to be any useful tools for users...
  #cargo build --frozen --release

  # build Python bindings
  cd hf_xet
  local maturin_options=(
    --locked
    --release
    --target "$(rustc -vV | sed -n 's/host: //p')"
    --strip
    # wheel compatibility tag - native linux ensures we don't bundle openssl libs
    # https://github.com/PyO3/maturin/issues/2038
    --compatibility linux
  )
  maturin build "${maturin_options[@]}"
}

package_python-hf-xet() {
  depends=(
    gcc-libs
    glibc
    openssl libssl.so libcrypto.so
    python
  )

  cd $pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  cd hf_xet
  python -m installer --destdir "$pkgdir" target/wheels/*.whl
}
