# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgbase=xet-core
pkgname=(python-hf-xet)
pkgver=1.1.10
pkgrel=1
pkgdesc="xet client tech, used in Huggingface Hub"
arch=(x86_64)
url=https://github.com/huggingface/xet-core
license=(Apache-2.0)
makedepends=(
  cargo
  maturin
  python-installer
  openssl
)
source=($pkgbase-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('2cb475395dead9b35b17df14ca57df513edd9e9294f262e65a9cacf0a3d31a22039dcf011308ccd6572837adcbdd21da0c2bfe120e3a2738e482ce7df48d82e7')

prepare() {
  cd $pkgbase-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  # fix building with LTO
  CFLAGS+=' -ffat-lto-objects'

  # always use system openssl https://docs.rs/openssl/latest/openssl/
  export OPENSSL_NO_VENDOR=1

  cd $pkgbase-$pkgver
  # there don't seem to be any useful tools for users...
  #cargo build --frozen --release

  # build Python bindings
  cd hf_xet
  local maturin_options=(
    --locked
    --release
    --target "$(rustc -vV | sed -n 's/host: //p')"
    --strip
    # wheel compatibility tag - native linux ensures we don't bundle openssl libs
    # https://github.com/PyO3/maturin/issues/2038
    --compatibility linux
    # use native-tls instead of native-tls-vendored
    --features native-tls
  )
  maturin build "${maturin_options[@]}"
}

package_python-hf-xet() {
  depends=(
    gcc-libs
    glibc
    openssl libssl.so libcrypto.so
    python
  )

  cd $pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  cd hf_xet
  python -m installer --destdir "$pkgdir" target/wheels/*.whl
}
