# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgbase=xet-core
pkgname=(python-hf-xet)
pkgver=1.3.2
pkgrel=1
pkgdesc="xet client tech, used in Huggingface Hub"
arch=(x86_64)
url=https://github.com/huggingface/xet-core
license=(Apache-2.0)
makedepends=(
  cargo
  maturin
  python-installer
  openssl
)
source=($pkgbase-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('08186c0c3bf2e5b2158507cda8b23379f1626d75a4c1fb8a273fa8b6ba1a49d5fb2dc3dfc2d7ee126b5a63614118638599b5dd28ae6f009e2143d03511e7607e')

prepare() {
  cd $pkgbase-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  # fix building with LTO
  CFLAGS+=' -ffat-lto-objects'

  # always use system openssl https://docs.rs/openssl/latest/openssl/
  export OPENSSL_NO_VENDOR=1

  cd $pkgbase-$pkgver
  # there don't seem to be any useful tools for users...
  #cargo build --frozen --release

  # build Python bindings
  cd hf_xet
  local maturin_options=(
    --locked
    --release
    --target "$(rustc --print host-tuple)"
    --strip
    # wheel compatibility tag - native linux ensures we don't bundle openssl libs
    # https://github.com/PyO3/maturin/issues/2038
    --compatibility linux
    # use native-tls instead of native-tls-vendored
    --features native-tls
  )
  maturin build "${maturin_options[@]}"
}

package_python-hf-xet() {
  depends=(
    glibc
    libgcc
    openssl libssl.so libcrypto.so
    python
  )

  cd $pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  cd hf_xet
  python -m installer --destdir "$pkgdir" target/wheels/*.whl
}
