# Maintainer: David Runge <dvzrv@archlinux.org>

_parent_name=flit
_name=flit_core
pkgname=python-flit-core
pkgver=3.10.0
pkgrel=1
pkgdesc="A PEP 517 build backend for packages using Flit"
arch=(any)
url="https://github.com/pypa/flit/tree/main/flit_core"
_url="https://github.com/pypa/flit"
license=(BSD-3-Clause)
groups=(python-build-backend)
depends=(python)
makedepends=(
  python-build
  python-installer
)
checkdepends=(
  python-pytest
  python-testpath
)
source=($_parent_name-$pkgver.tar.gz::$_url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('47e3603fefe2b4b01d18d75b5012abf379edbab0ca398f5f6586b647c12310cd11e3ccf8b4c4cb863e28bbb1a7eaf0416906122e94848818c9096fbeb5e7f2da')
b2sums=('c711eb3d3a17ddbd6c959358aa7951fc440f58fb02b248846c57c4ee99320c7f465ab63dd4ad3718c21468f5af0775c7cf7e3da20df50e56afb00038e6033a0d')

build() {
  cd $_parent_name-$pkgver/$_name
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_parent_name-$pkgver/$_name
  pytest -vv
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_parent_name-$pkgver/$_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # remove tests
  rm -frv "$pkgdir/$site_packages/${_name/-/_}/tests/"
  # remove vendored tomli
  rm -frv "$pkgdir/$site_packages/${_name/-/_}/vendor/"
}
