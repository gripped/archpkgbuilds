# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: wedjat <wedjat@protonmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python-pydicom
pkgver=3.0.1
pkgrel=3
pkgdesc="Pure python package for working with DICOM files"
arch=("any")
url="https://pydicom.github.io/pydicom/stable/index.html"
license=('MIT')
depends=('python' 'python-setuptools')  # setuptools is required for the main binary
makedepends=('python-wheel' 'python-build' 'python-installer' 'python-flit-core')
optdepends=('python-numpy: for working with pixel data'
            'python-pillow: for working with compressed image data'
            )

# Checkdepends needs all optdepends as it enables more tests
checkdepends=('python-pytest' 'python-numpy' 'python-pillow')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pydicom/pydicom/archive/v$pkgver.tar.gz")
sha256sums=('848ae8e36f34750cf837e48ca5cdf6f7a472145724946d5f28f529279fe11f4e')

build()
{
	cd "$srcdir/pydicom-$pkgver"
    python -m build --wheel --no-isolation
}

package()
{
	cd "$srcdir/pydicom-$pkgver"
	python -m installer --destdir="$pkgdir"/ dist/*.whl

	install -D "$srcdir/pydicom-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

check()
{
	cd "$srcdir/pydicom-$pkgver"

	# Create a venv because software needs to be installed to run tests
	python -m venv --system-site-packages test-env
	test-env/bin/python -m installer dist/*.whl

	# Skipped tests: have almost correct assertions on exception message
	test-env/bin/python -m pytest -v -k "not test_deepcopy_bufferedreader_raises and not test_no_decoders_raises"
}
