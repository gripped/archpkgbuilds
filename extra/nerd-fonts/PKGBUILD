# Maintainer: T.J. Townsend <blakkheim@archlinux.org>

pkgbase=nerd-fonts
pkgname=(otf-aurulent-nerd
         otf-codenewroman-nerd
         otf-comicshanns-nerd
         otf-commit-mono-nerd
         otf-droid-nerd
         otf-firamono-nerd
         otf-geist-mono-nerd
         otf-hasklig-nerd
         otf-hermit-nerd
         otf-monaspace-nerd
         otf-opendyslexic-nerd
         otf-overpass-nerd
         ttf-0xproto-nerd
         ttf-3270-nerd
         ttf-agave-nerd
         ttf-anonymouspro-nerd
         ttf-arimo-nerd
         ttf-bigblueterminal-nerd
         ttf-bitstream-vera-mono-nerd
         ttf-cascadia-code-nerd
         ttf-cascadia-mono-nerd
         ttf-cousine-nerd
         ttf-d2coding-nerd
         ttf-daddytime-mono-nerd
         ttf-dejavu-nerd
         ttf-envycoder-nerd
         ttf-fantasque-nerd
         ttf-firacode-nerd
         ttf-go-nerd
         ttf-hack-nerd
         ttf-heavydata-nerd
         ttf-iawriter-nerd
         ttf-ibmplex-mono-nerd
         ttf-inconsolata-go-nerd
         ttf-inconsolata-lgc-nerd
         ttf-inconsolata-nerd
         ttf-intone-nerd
         ttf-iosevka-nerd
         ttf-iosevkaterm-nerd
         ttf-jetbrains-mono-nerd
         ttf-lekton-nerd
         ttf-liberation-mono-nerd
         ttf-lilex-nerd
         ttf-martian-mono-nerd
         ttf-meslo-nerd
         ttf-monofur-nerd
         ttf-monoid-nerd
         ttf-mononoki-nerd
         ttf-mplus-nerd
         ttf-noto-nerd
         ttf-profont-nerd
         ttf-proggyclean-nerd
         ttf-roboto-mono-nerd
         ttf-sharetech-mono-nerd
         ttf-sourcecodepro-nerd
         ttf-space-mono-nerd
         ttf-terminus-nerd
         ttf-tinos-nerd
         ttf-ubuntu-mono-nerd
         ttf-ubuntu-nerd
         ttf-victor-mono-nerd)
pkgdesc="Iconic font aggregator, collection, and patcher"
pkgver=3.2.0
pkgrel=1
arch=('any')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('MIT')
options=('!debug' '!strip')
provides=('ttf-font-nerd')
groups=('nerd-fonts')
source=("0xproto-${pkgver}.tar.xz::${_urlbase}/0xProto.tar.xz"
        "3270-${pkgver}.tar.xz::${_urlbase}/3270.tar.xz"
        "agave-${pkgver}.tar.xz::${_urlbase}/Agave.tar.xz"
        "anonymouspro-${pkgver}.tar.xz::${_urlbase}/AnonymousPro.tar.xz"
        "arimo-${pkgver}.tar.xz::${_urlbase}/Arimo.tar.xz"
        "aurulent-${pkgver}.tar.xz::${_urlbase}/AurulentSansMono.tar.xz"
        "bigblueterminal-${pkgver}.tar.xz::${_urlbase}/BigBlueTerminal.tar.xz"
        "bitstreamveramono-${pkgver}.tar.xz::${_urlbase}/BitstreamVeraSansMono.tar.xz"
        "cascadiacode-${pkgver}.tar.xz::${_urlbase}/CascadiaCode.tar.xz"
        "cascadiamono-${pkgver}.tar.xz::${_urlbase}/CascadiaMono.tar.xz"
        "codenewroman-${pkgver}.tar.xz::${_urlbase}/CodeNewRoman.tar.xz"
        "comicshanns-${pkgver}.tar.xz::${_urlbase}/ComicShannsMono.tar.xz"
        "commitmono-${pkgver}.tar.xz::${_urlbase}/CommitMono.tar.xz"
        "cousine-${pkgver}.tar.xz::${_urlbase}/Cousine.tar.xz"
        "daddytimemono-${pkgver}.tar.xz::${_urlbase}/DaddyTimeMono.tar.xz"
        "d2coding-${pkgver}.tar.xz::${_urlbase}/D2Coding.tar.xz"
        "dejavu-${pkgver}.tar.xz::${_urlbase}/DejavuSansMono.tar.xz"
        "droid-${pkgver}.tar.xz::${_urlbase}/DroidSansMono.tar.xz"
        "envycoder-${pkgver}.tar.xz::${_urlbase}/EnvyCodeR.tar.xz"
        "fantasque-${pkgver}.tar.xz::${_urlbase}/FantasqueSansMono.tar.xz"
        "fira-${pkgver}.tar.xz::${_urlbase}/FiraCode.tar.xz"
        "firamono-${pkgver}.tar.xz::${_urlbase}/FiraMono.tar.xz"
        "geistmono-${pkgver}.tar.xz::${_urlbase}/GeistMono.tar.xz"
        "go-${pkgver}.tar.xz::${_urlbase}/Go-Mono.tar.xz"
        "hack-${pkgver}.tar.xz::${_urlbase}/Hack.tar.xz"
        "hasklig-${pkgver}.tar.xz::${_urlbase}/Hasklig.tar.xz"
        "heavydata-${pkgver}.tar.xz::${_urlbase}/HeavyData.tar.xz"
        "hermit-${pkgver}.tar.xz::${_urlbase}/Hermit.tar.xz"
        "iawriter-${pkgver}.tar.xz::${_urlbase}/iA-Writer.tar.xz"
        "ibmplexmono-${pkgver}.tar.xz::${_urlbase}/IBMPlexMono.tar.xz"
        "inconsolata-${pkgver}.tar.xz::${_urlbase}/Inconsolata.tar.xz"
        "inconsolatago-${pkgver}.tar.xz::${_urlbase}/InconsolataGo.tar.xz"
        "inconsolatalgc-${pkgver}.tar.xz::${_urlbase}/InconsolataLGC.tar.xz"
        "intone-${pkgver}.tar.xz::${_urlbase}/IntelOneMono.tar.xz"
        "iosevka-${pkgver}.tar.xz::${_urlbase}/Iosevka.tar.xz"
        "iosevkaterm-${pkgver}.tar.xz::${_urlbase}/IosevkaTerm.tar.xz"
        "jetbrainsmono-${pkgver}.tar.xz::${_urlbase}/JetBrainsMono.tar.xz"
        "lekton-${pkgver}.tar.xz::${_urlbase}/Lekton.tar.xz"
        "liberationmono-${pkgver}.tar.xz::${_urlbase}/LiberationMono.tar.xz"
        "lilex-${pkgver}.tar.xz::${_urlbase}/Lilex.tar.xz"
        "martianmono-${pkgver}.tar.xz::${_urlbase}/MartianMono.tar.xz"
        "meslo-${pkgver}.tar.xz::${_urlbase}/Meslo.tar.xz"
        "monaspace-${pkgver}.tar.xz::${_urlbase}/Monaspace.tar.xz"
        "monofur-${pkgver}.tar.xz::${_urlbase}/Monofur.tar.xz"
        "monoid-${pkgver}.tar.xz::${_urlbase}/Monoid.tar.xz"
        "mononoki-${pkgver}.tar.xz::${_urlbase}/Mononoki.tar.xz"
        "mplus-${pkgver}.tar.xz::${_urlbase}/MPlus.tar.xz"
        "noto-${pkgver}.tar.xz::${_urlbase}/Noto.tar.xz"
        "opendyslexic-${pkgver}.tar.xz::${_urlbase}/OpenDyslexic.tar.xz"
        "overpass-${pkgver}.tar.xz::${_urlbase}/Overpass.tar.xz"
        "profont-${pkgver}.tar.xz::${_urlbase}/ProFont.tar.xz"
        "proggyclean-${pkgver}.tar.xz::${_urlbase}/ProggyClean.tar.xz"
        "robotomono-${pkgver}.tar.xz::${_urlbase}/RobotoMono.tar.xz"
        "sharetechmono-${pkgver}.tar.xz::${_urlbase}/ShareTechMono.tar.xz"
        "sourcecodepro-${pkgver}.tar.xz::${_urlbase}/SourceCodePro.tar.xz"
        "spacemono-${pkgver}.tar.xz::${_urlbase}/SpaceMono.tar.xz"
        "terminus-${pkgver}.tar.xz::${_urlbase}/Terminus.tar.xz"
        "tinos-${pkgver}.tar.xz::${_urlbase}/Tinos.tar.xz"
        "ubuntu-${pkgver}.tar.xz::${_urlbase}/Ubuntu.tar.xz"
        "ubuntumono-${pkgver}.tar.xz::${_urlbase}/UbuntuMono.tar.xz"
        "victormono-${pkgver}.tar.xz::${_urlbase}/VictorMono.tar.xz")
sha256sums=('d6db9025a1f09c51de9b2214a637fe9419311f4885ad9428340c819308bdf96e'
            '3a73e484780e9983e65e51a4d50330896713163fd1d8dee162b93c1eeb4ec2c3'
            'c2e47c89dfdf007884528e4a3929a62b3b86b91c4897c19e05e24c6d6efcf9cf'
            '9f73054917c0241f9f9bca4f5614af05dfd460badf6a3a47262b58bfe61764c9'
            'ce41bcc35906fa795ce7b8ea71c6f07fb6994a65e575e3211983ae103634a93e'
            'af213cfa4a770ff032223cf2bb92fee34c7267ac787c6b7b29b59d35dbf84760'
            'b655fd38f55e9fb61e028e1c6a11b5ce2e94c40c474f1f0d96d9c0c35ea46e69'
            '3c1a8e4028b4db7807d66fec86e80b21a95d8d17e2dc6ec9299659c8c1b3d826'
            '6647b8c978028ab9e133260754b2d5a7f9472feb1f58dd0a45b38c683055a05a'
            'b9f5fdcfaedb27baa0c8aa1fdbfdf312751f71b14d95c34e3633b58ec6ebd7b3'
            '8b228d76e13d984f9349f6bec9de36f8e831b345693870245f5e6bee42ce270c'
            'a511f7e86f498f4afb80748d6121c5c45eac3e328139c52184eacbd1ac881c49'
            '6c9d31a3108bf2385cd7d7f76883d8aca247fd4dd14651f20fbf595495f23b9d'
            '744510c90ddefe423679558fb18ad8714a53d5579155d0397f7a3db988de291f'
            '2d9277c41bce302c074d4898111836b19746772d29fdd7dc01705e48581c3b87'
            'cca47cfedfd3c548e1bfdb265384adef93aed552e1b7801772fe013e06e9e54b'
            '7e4a03e397b40ff9e3d0d5e6374d3908dee59a584f6dc7fa76d8fd949d253e37'
            'db1216e2631e1aafbc7e4a206a331608965d6cc9474fc6bd4dfb931dfe9278fc'
            '3998961c8d382b3c14078ccf49b7c70d0b62ee81ffdf0a1eaceffc5a3697a30d'
            '3721de207bac38f05797f21cfd6e672a268b3fd8419a47f789fafa11f85f084a'
            '02b9f7fda56efcfba2fd5ccf3d81455af52b3a405648ece7f0e1c58aac7109bc'
            '2e5fcc890e64d48d3afd9b1e16d03f7c945941073c6c6a724298f29135bc93fd'
            'bdeae85787344c8ac1f3fde13ea16bcff201ce656d76f5b0de77f2952db6d4f3'
            '01d399eb32242b4689d1523ab634d0ff7b19200f117e72a5d97abf8ddbbdc787'
            '279c47fc2b4efdf7fb76852db5a9d1664df1b461515dc6052350ce4271a6760b'
            'b89528815c5faa2f27f8fd5aa14d0e1460d1e8e8730e301552578baae973102a'
            'bd3ec9b863603438ddaad8853e3c94cc94bd9469f9c1b85de7b8f433698edc3b'
            '4e5d74a8f0fd0081e012fed0b97500bd254ae2a884de43f7bfa70b43d79c6518'
            'ff96f08889b57513b5e08013b39793d7690983f828ad2c590a373b9a082a428a'
            '8e861655539d558a0721c2b5a9543d20b438b1ba028933bd11724b73c96762b3'
            '1aabe42b6e72b9f5e7ae5ac41afedb703180ba9bb680540945342935f5327990'
            '64fc2b773674f3b50c22cd119909f124f7c6007082365ba2da0d4053f71b8c74'
            '8620a0b58be4f426013cfb409ba668ab013b14536443e742263b3a86ce4247cb'
            'a90454ca119abc228ac6ff5f37a4b9468f0a80b8236e272aa80f64e5dc71e45e'
            '588da479e028e5aaa3438761b6187c7eeb2161cc197220ff65c5fc5d9ff8619a'
            '97be9ff8e9aebbe3a85e22a9ad08fa3e2361404531f78ee12ea6ea33ec9ead87'
            '8aa63c46a5dbb7a075ba579bb2189d3d7e42f3027797df85b14727aaeb71a29d'
            'b945441065c2cc86ea5290c2523d1dd87e27adee93bc32184d2523b852251244'
            'd8854ae585b35e1f639ef6ecc50cb6bf789e70b9bf7e9726b525069e63b01ee8'
            '86e2800414f85b01df0a1b11ad339ae851253afe815a6a1a744d1a9eadf9a079'
            '73abcdc12015143eaf24ed5097ae357aa552c8cb90c3500e551ae1315cdc97ca'
            '0b49634ee2b0aa3764f223064797d8a3fdc03760d1fb668a0f563bc24320b93c'
            'e22c15c18e7f48bbe3f137539662eff384898d33879e1589b3d30c4a255123b2'
            '33b0e22e29f101a73f2d3968008450faef0f0bbbb581a8c4d96027b7b6a93d57'
            '221860e985ea2a7f799a76c4be8dabdee242e85bd5e6c1e89ae86674fad44b3e'
            'e02961e17dbeb37f48b5af102eab6c9cc5083e90d3da503f78cd3385b64af423'
            '459dfe40e05e6428f81a4a11d956e4c59243b8c3dd018956760f59c9acd324fe'
            'ced545faeb348c73a924e09078eb26a87e0bfd7219b3dd4af9c7579d5d221505'
            '92b235828cc5ad1b234f45fbbb767baeda6647d8c6b1c6535206b9b1c32ed28c'
            '05fe04c852925419be8958e4fc1907ba69f1d575a41239b46aeaf8c11cf52733'
            '8e271ba485c07706e772693586ea300dc124ecf6d0a589ad37b1388642c8757d'
            '01b59814b15a08d9cc858b19530c8295846ab517661351562849b6c05606dbd7'
            '1151b15db67316613320249633f5711caf094e43f5b3ac38b1f624c2d2fe6f48'
            'c48283f68fa15c731cda9142e3991ba86efa30d0ecf938d25c678271453c9b1f'
            '78a1aef2ea83ece21d97f948c9f4ee2ce9ba8ea9be3fdf531be7a0d0560dd9fe'
            '7c33439ae54dcdfa89715d7f82797a4b76b9151ec807e158e7dca7bca2bef58c'
            'fa96d505c5d67a85b1a02fc2a3fabe290783434ede477522a80ae6244281b13b'
            'b0569548a5a7aa93e959558ec7a187316bf8131367ad8fe13a4f73d49905f42c'
            '7ad81f36a9f472f97bfbffbc8513053bdc0e931ef1b59fb4e95df0a99b3037e1'
            '3ce716484ea6ae666013404e9fecf1d7a29c20e9ddadf7c776eafb28cd22fbe3'
            'cf0451de630a535f473462b5ad9b42973e103b252d527379f00c2cdb1de7cadb')
noextract=("${source[@]%%::*}")

prepare() {
  # archives don't include a font-specific directory, so we have to make them
  for _font in 3270 agave anonymouspro arimo aurulent bigblueterminal \
               bitstreamveramono cascadiacode codenewroman cousine \
               daddytimemono dejavu droid fantasque fira firamono go hack \
               hasklig heavydata hermit iawriter ibmplexmono inconsolata \
               inconsolatago inconsolatalgc iosevka jetbrainsmono lekton \
               liberationmono lilex meslo monofur monoid mononoki mplus noto \
               opendyslexic overpass profont proggyclean robotomono \
               sharetechmono sourcecodepro spacemono terminus tinos ubuntu \
               ubuntumono victormono iosevkaterm comicshanns intone 0xproto \
               cascadiamono commitmono d2coding envycoder geistmono martianmono \
               monaspace; do
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
  done
}

package_ttf-arimo-nerd() {
  pkgdesc="Patched font Arimo from nerd fonts library"
  conflicts=(nerd-fonts-arimo)
  replaces=(nerd-fonts-arimo)

  install -Dm644 arimo/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 arimo/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-aurulent-nerd() {
  pkgdesc="Patched font Aurulent Sans Mono from nerd fonts library"

  install -Dm644 aurulent/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 aurulent/"SIL Open Font License.txt" \
          -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-firacode-nerd() {
  pkgdesc="Patched font Fira (Fura) Code from nerd fonts library"
  conflicts=(nerd-fonts-fira-code)
  replaces=(nerd-fonts-fira-code)

  install -Dm644 fira/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 fira/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_otf-firamono-nerd() {
  pkgdesc="Patched font Fira (Fura) Mono from nerd fonts library"
  conflicts=(nerd-fonts-fira-mono)
  replaces=(nerd-fonts-fira-mono)

  install -Dm644 firamono/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 firamono/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-hack-nerd() {
  pkgdesc="Patched font Hack from nerd fonts library"
  conflicts=(nerd-fonts-hack)
  replaces=(nerd-fonts-hack)

  install -Dm644 hack/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 hack/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-iosevka-nerd() {
  pkgdesc="Patched font Iosevka from nerd fonts library"
  conflicts=(nerd-fonts-iosevka)
  replaces=(nerd-fonts-iosevka)

  install -Dm644 iosevka/*.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
  install -Dm644 iosevka/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-iosevkaterm-nerd() {
  pkgdesc="Patched font Iosevka Term from nerd fonts library"

  install -Dm644 iosevkaterm/*.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
  install -Dm644 iosevkaterm/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-terminus-nerd() {
  pkgdesc="Patched font Terminus (Terminess) from nerd fonts library"
  conflicts=(nerd-fonts-terminus)
  replaces=(nerd-fonts-terminus)

  install -Dm644 terminus/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 terminus/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-ubuntu-nerd() {
  pkgdesc="Patched font Ubuntu from nerd fonts library"
  conflicts=(nerd-fonts-ubuntu)
  replaces=(nerd-fonts-ubuntu)

  install -Dm644 ubuntu/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ubuntu/LICENCE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-ubuntu-mono-nerd() {
  pkgdesc="Patched font Ubuntu Mono from nerd fonts library"
  conflicts=(nerd-fonts-ubuntu-mono ttf-ubuntumono-nerd)
  replaces=(nerd-fonts-ubuntu-mono ttf-ubuntumono-nerd)

  install -Dm644 ubuntumono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ubuntumono/LICENCE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-0xproto-nerd() {
  pkgdesc="Patched font 0xProto from nerd fonts library"

  install -Dm644 0xproto/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 0xproto/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-3270-nerd() {
  pkgdesc="Patched font IBM 3270 from nerd fonts library"

  install -Dm644 3270/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 3270/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-agave-nerd() {
  pkgdesc="Patched font Agave from nerd fonts library"
  conflicts=(nerd-fonts-agave)
  replaces=(nerd-fonts-agave)

  install -Dm644 agave/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 agave/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-bigblueterminal-nerd() {
  pkgdesc="Patched font Big Blue Terminal from nerd fonts library"

  install -Dm644 bigblueterminal/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 bigblueterminal/LICENSE.TXT \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}

package_ttf-anonymouspro-nerd() {
  pkgdesc="Patched font Anonymous Pro (Anonymice) from nerd fonts library"
  license=('custom:OFL')
  conflicts=(nerd-fonts-anonymous-pro)
  replaces=(nerd-fonts-anonymous-pro)

  install -Dm644 anonymouspro/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 anonymouspro/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-bitstream-vera-mono-nerd() {
  pkgdesc="Patched font Bitstream Vera Mono from nerd fonts library"
  conflicts=(nerd-fonts-bitstream-vera-mono)
  replaces=(nerd-fonts-bitstream-vera-mono)

  install -Dm644 bitstreamveramono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 bitstreamveramono/"Bitstream Vera License.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/Bitstream Vera License.txt"
}

package_ttf-cascadia-code-nerd() {
  pkgdesc="Patched font Cascadia Code (Caskaydia) from nerd fonts library"
  conflicts=(nerd-fonts-cascadia-code)
  replaces=(nerd-fonts-cascadia-code otf-cascadia-code-nerd)

  install -Dm644 cascadiacode/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 cascadiacode/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-cascadia-mono-nerd() {
  pkgdesc="Patched font Cascadia Mono (Caskaydia) from nerd fonts library"

  install -Dm644 cascadiamono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 cascadiamono/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_otf-codenewroman-nerd() {
  pkgdesc="Patched font Code New Roman from nerd fonts library"

  install -Dm644 codenewroman/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 codenewroman/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

package_otf-comicshanns-nerd() {
  pkgdesc="Patched font Comic Sans (Comic Shanns) from nerd fonts library"

  install -Dm644 comicshanns/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 comicshanns/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_otf-commit-mono-nerd() {
  pkgdesc="Patched font Commit Mono from nerd fonts library"

  install -Dm644 commitmono/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 commitmono/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-cousine-nerd() {
  pkgdesc="Patched font Cousine from nerd fonts library"

  install -Dm644 cousine/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 cousine/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-d2coding-nerd() {
  pkgdesc="Patched font D2Coding from nerd fonts library"

  install -Dm644 d2coding/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 d2coding/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-daddytime-mono-nerd() {
  pkgdesc="Patched font Daddy Time Mono from nerd fonts library"

  install -Dm644 daddytimemono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 daddytimemono/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-dejavu-nerd() {
  pkgdesc="Patched font Dejavu Sans Mono from nerd fonts library"
  conflicts=(nerd-fonts-dejavu-complete)
  replaces=(nerd-fonts-dejavu-complete)

  install -Dm644 dejavu/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 dejavu/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_otf-droid-nerd() {
  pkgdesc="Patched font Droid Sans Mono from nerd fonts library"
  conflicts=(nerd-fonts-droid-sans-mono)
  replaces=(nerd-fonts-droid-sans-mono)

  install -Dm644 droid/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 droid/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-envycoder-nerd() {
  pkgdesc="Patched font Envy Code R from nerd fonts library"

  install -Dm644 envycoder/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 envycoder/LICENCE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-fantasque-nerd() {
  pkgdesc="Patched font Fantasque Sans Mono from nerd fonts library"
  license=('custom:OFL')
  conflicts=(nerd-fonts-fantasque-sans-mono)
  replaces=(nerd-fonts-fantasque-sans-mono)

  install -Dm644 fantasque/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 fantasque/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_otf-geist-mono-nerd() {
  pkgdesc="Patched font Geist Mono from nerd fonts library"

  install -Dm644 geistmono/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 geistmono/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-go-nerd() {
  pkgdesc="Patched font Go Mono from nerd fonts library"
  conflicts=(nerd-fonts-go-mono)
  replaces=(nerd-fonts-go-mono)

  install -Dm644 go/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 go/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_otf-hasklig-nerd() {
  pkgdesc="Patched font Hasklig (Hasklug) from nerd fonts library"
  conflicts=(nerd-fonts-hasklig)
  replaces=(nerd-fonts-hasklig)

  install -Dm644 hasklig/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 hasklig/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-heavydata-nerd() {
  pkgdesc="Patched font Heavy Data from nerd fonts library"

  install -Dm644 heavydata/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 heavydata/"Vic Fieger License.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/Vic Fieger License.txt"
}

package_otf-hermit-nerd() {
  pkgdesc="Patched font Hermit from nerd fonts library"
  conflicts=(nerd-fonts-hermit)
  replaces=(nerd-fonts-hermit)

  install -Dm644 hermit/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 hermit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-iawriter-nerd() {
  pkgdesc="Patched font iA Writer (iM Writing) from nerd fonts library"

  install -Dm644 iawriter/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 iawriter/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-ibmplex-mono-nerd() {
  pkgdesc="Patched font IBM Plex Mono (Blex) from nerd fonts library"
  conflicts=(nerd-fonts-ibm-plex-mono)
  replaces=(nerd-fonts-ibm-plex-mono)

  install -Dm644 ibmplexmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 ibmplexmono/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-inconsolata-nerd() {
  pkgdesc="Patched font Inconsolata from nerd fonts library"
  license=('custom:OFL')
  conflicts=(nerd-fonts-inconsolata)
  replaces=(nerd-fonts-inconsolata)

  install -Dm644 inconsolata/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolata/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-inconsolata-go-nerd() {
  pkgdesc="Patched font Inconsolata Go from nerd fonts library"
  license=('custom:OFL')
  conflicts=(nerd-fonts-inconsolata-go)
  replaces=(nerd-fonts-inconsolata-go)

  install -Dm644 inconsolatago/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolatago/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-inconsolata-lgc-nerd() {
  pkgdesc="Patched font Inconsolata LGC from nerd fonts library"
  conflicts=(nerd-fonts-inconsolatalgc)
  replaces=(nerd-fonts-inconsolatalgc)

  install -Dm644 inconsolatalgc/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 inconsolatalgc/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-intone-nerd() {
  pkgdesc="Patched font Intone (Intel One) from nerd fonts library"

  install -Dm644 intone/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 intone/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-jetbrains-mono-nerd() {
  pkgdesc="Patched font JetBrains Mono from nerd fonts library"
  license=('custom:OFL')
  conflicts=(nerd-fonts-jetbrains-mono)
  replaces=(nerd-fonts-jetbrains-mono)

  install -Dm644 jetbrainsmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 jetbrainsmono/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-lekton-nerd() {
  pkgdesc="Patched font Lekton from nerd fonts library"

  install -Dm644 lekton/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 lekton/"SIL Open Font License.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/SIL Open Font License.txt"
}

package_ttf-liberation-mono-nerd() {
  pkgdesc="Patched font Liberation Mono from nerd fonts library"
  conflicts=(nerd-fonts-liberation-mono)
  replaces=(nerd-fonts-liberation-mono)

  install -Dm644 liberationmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 liberationmono/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-lilex-nerd() {
  pkgdesc="Patched font Lilex from nerd fonts library"

  install -Dm644 lilex/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 lilex/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-martian-mono-nerd() {
  pkgdesc="Patched font Martian Mono from nerd fonts library"

  install -Dm644 martianmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 martianmono/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-meslo-nerd() {
  pkgdesc="Patched font Meslo LG from nerd fonts library"
  conflicts=(nerd-fonts-meslo)
  replaces=(nerd-fonts-meslo)

  install -Dm644 meslo/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 meslo/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_otf-monaspace-nerd() {
  pkgdesc="Patched font Monaspace (Monaspice) from nerd fonts library"

  install -Dm644 monaspace/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 monaspace/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-monofur-nerd() {
  pkgdesc="Patched font Monofur from nerd fonts library"
  conflicts=(nerd-fonts-monofur)
  replaces=(nerd-fonts-monofur)

  install -Dm644 monofur/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 monofur/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-monoid-nerd() {
  pkgdesc="Patched font Monoid from nerd fonts library"
  conflicts=(nerd-fonts-monoid)
  replaces=(nerd-fonts-monoid)

  install -Dm644 monoid/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 monoid/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-mononoki-nerd() {
  pkgdesc="Patched font Mononoki from nerd fonts library"
  conflicts=(nerd-fonts-mononoki)
  replaces=(nerd-fonts-mononoki)

  install -Dm644 mononoki/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 mononoki/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-mplus-nerd() {
  pkgdesc="Patched font MPlus from nerd fonts library"
  license=('custom:OFL')
  conflicts=(nerd-fonts-mplus)
  replaces=(nerd-fonts-mplus)

  install -Dm644 mplus/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 mplus/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-noto-nerd() {
  pkgdesc="Patched font Noto from nerd fonts library"
  license=('custom:OFL')
  conflicts=(nerd-fonts-noto)
  replaces=(nerd-fonts-noto)

  install -Dm644 noto/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 noto/LICENSE_OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE_OFL.txt"
}

package_otf-opendyslexic-nerd() {
  pkgdesc="Patched font OpenDyslexic from nerd fonts library"

  install -Dm644 opendyslexic/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 opendyslexic/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_otf-overpass-nerd() {
  pkgdesc="Patched font Overpass from nerd fonts library"
  conflicts=(nerd-fonts-overpass)
  replaces=(nerd-fonts-overpass)

  install -Dm644 overpass/*.otf -t "${pkgdir}/usr/share/fonts/OTF"
  install -Dm644 overpass/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_ttf-profont-nerd() {
  pkgdesc="Patched font ProFont from nerd fonts library"
  conflicts=(nerd-fonts-profont)
  replaces=(nerd-fonts-profont)

  install -Dm644 profont/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 profont/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ttf-proggyclean-nerd() {
  pkgdesc="Patched font ProggyClean from nerd fonts library"

  install -Dm644 proggyclean/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 proggyclean/Licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/Licence.txt"
}

package_ttf-roboto-mono-nerd() {
  pkgdesc="Patched font Roboto Mono from nerd fonts library"
  conflicts=(nerd-fonts-roboto-mono)
  replaces=(nerd-fonts-roboto-mono)

  install -Dm644 robotomono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 robotomono/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_ttf-sharetech-mono-nerd() {
  pkgdesc="Patched font Share Tech Mono (Shure Tech Mono) from nerd fonts library"
  license=('custom:OFL')

  install -Dm644 sharetechmono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 sharetechmono/OFL.txt "${pkgdir}/usr/share/licenses/${pkgname}/OFL.txt"
}

package_ttf-sourcecodepro-nerd() {
  pkgdesc="Patched font Source Code Pro from nerd fonts library"
  conflicts=(nerd-fonts-source-code-pro)
  replaces=(nerd-fonts-source-code-pro)

  install -Dm644 sourcecodepro/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 sourcecodepro/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-space-mono-nerd() {
  pkgdesc="Patched font Space Mono from nerd fonts library"
  license=('custom:OFL')
  conflicts=(nerd-fonts-source-space-mono)
  replaces=(nerd-fonts-source-space-mono)

  install -Dm644 spacemono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 spacemono/OFL.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-tinos-nerd() {
  pkgdesc="Patched font Tinos from nerd fonts library"
  license=('Apache')
  conflicts=(nerd-fonts-source-tinos)
  replaces=(nerd-fonts-source-tinos)

  install -Dm644 tinos/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 tinos/"Apache License.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_ttf-victor-mono-nerd() {
  pkgdesc="Patched font Victor Mono from nerd fonts library"
  conflicts=(ttf-nerd-fonts-victor-mono)
  replaces=(ttf-nerd-fonts-victor-mono)

  install -Dm644 victormono/*.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 victormono/LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
