# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-swagger
pkgver=0.33.1
pkgrel=1
pkgdesc='Swagger 2.0 implementation in Go'
arch=('x86_64')
url='https://goswagger.io'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/go-swagger/go-swagger#tag=v$pkgver")
sha512sums=('b4dce54d39d05212aae03c4ba471c7bde1ce3a99fc27fdea3f97f3cd2477460c40a62edd2f5439f40c5994e2432875beb9bb7cb15e33866e584756daf62a7a12')
b2sums=('fda289e99eb3b1d6ea854d6dfaa31d02764896b536d29d1ac06bd0ffc8d2ec7a984e036b022076e4ebd13e2f34a5c14b4f9a5d62865e9948ac709549e33f7c59')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-swagger/go-swagger/cmd/swagger/commands.Commit=$(git rev-parse HEAD) \
    -X github.com/go-swagger/go-swagger/cmd/swagger/commands.Version=$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/swagger

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # shell completion
  cd cmd/swagger/completion
  install -vDm644 swagger.bash-completion "$pkgdir/usr/share/bash-completion/completions/swagger"
  install -vDm644 swagger.zsh-completion "$pkgdir/usr/share/zsh/site-functions/_swagger"
}
