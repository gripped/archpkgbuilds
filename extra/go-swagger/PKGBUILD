# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-swagger
pkgver=0.32.3
pkgrel=1
pkgdesc='Swagger 2.0 implementation in Go'
arch=('x86_64')
url='https://goswagger.io'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/go-swagger/go-swagger#tag=v$pkgver")
sha512sums=('bf36492c8de428113ed25232b8a2f8ee505ef11955f73bfbdcd83c565436ed0afd70634b05293c7ebc99ea64c6150a2415aee7e3476313078900675f4ef58dde')
b2sums=('11971b60284833e753dafb3fe39ca6c403624f10e7858b05633dae626ca7749790e19a7f0de343f1d6d61293e58c5b62707092c8f3e865454faf69ff5c253a68')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-swagger/go-swagger/cmd/swagger/commands.Commit=$(git rev-parse HEAD) \
    -X github.com/go-swagger/go-swagger/cmd/swagger/commands.Version=$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/swagger

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # shell completion
  cd cmd/swagger/completion
  install -vDm644 swagger.bash-completion "$pkgdir/usr/share/bash-completion/completions/swagger"
  install -vDm644 swagger.zsh-completion "$pkgdir/usr/share/zsh/site-functions/_swagger"
}
