# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andy Li <andy@onthewings.net>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Kyle Brady  <kyle at spark-gap dot com>
# Contributor: Patrick Hof <courts@offensivethinking.org>
# Contributor: Michael Fellinger <mf@rubyists.com>

pkgname=haxe
pkgver=4.3.4
pkgrel=1
pkgdesc='Cross-platform toolkit and programming language'
arch=(x86_64)
url='https://haxe.org/'
license=(GPL LGPL MIT)
depends=(neko mbedtls2)
makedepends=(camlp5 git neko ocaml opam zlib perl-string-shellquote perl-ipc-system-simple)
optdepends=(java-environment mono php python)
options=(!strip)
source=(mbedtls2.patch
        "$pkgname-$pkgver::git+https://github.com/HaxeFoundation/haxe#commit=dc1a43dc52f98b9c480f68264885c6155e570f3e") # tag: 4.3.4
b2sums=('77f5f9c6815934b5cf2aee75ba62ed0e2a4b46f23a5bb689dffa23469b4aa13ca6d0e94623c747af63450b2ea3dc57f54507a924f1d6cec1cab3368d74def211'
        '65680cb33615ac81eed1e81b2f4ef5a75332fa0b084535f4b9bc5039adb19311a11620ce204cc2430ae80a461c0a0f1034269fe4757c9e8cfe92fdda38a0fd1e')

prepare() {
  cd $pkgname-$pkgver
  git submodule update --init

  # Prepare opam and ocamlfind before building
  export HOME="$srcdir"
  opam init -a --disable-sandboxing --compiler=ocaml-system
  eval $(opam env)

  # Install required OCaml packages
  opam install -y extlib luv=0.5.12 ocamlfind ptmap sedlex sha xml-light

  # Create the script that will be placed in /etc/profile.d
  echo 'export HAXE_STD_PATH=/usr/share/haxe/std' > haxe.sh

  # Build with mbedtls2
  patch -p1 -i ../mbedtls2.patch
}

build() {
  cd $pkgname-$pkgver

  # Prepare opam and ocamlfind before building
  eval $(opam env --switch=ocaml-system)

  export OCAMLPARAM="_,w=-a"
  make haxe

  # "make tools" did not work. These are the replacement commands:
  cd "$srcdir/$pkgname-$pkgver/extra/haxelib_src"
  ../../haxe client.hxml || true
  nekotools boot run.n
  mv run ../../haxelib
}

check() {
  cd $pkgname-$pkgver
  ./haxe -version
  ./haxelib version
}

package() {
  cd $pkgname-$pkgver
  install -d "$pkgdir/usr/"{bin/,share/haxe/}
  install -m 755 haxe{,lib} "$pkgdir"/usr/bin/
  cp -rf std "$pkgdir/usr/share/$pkgname/"
  install -Dm 644 haxe.sh "$pkgdir/etc/profile.d/haxe.sh"
  install -Dm 644 extra/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
