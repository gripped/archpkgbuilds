# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andy Li <andy@onthewings.net>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Kyle Brady  <kyle at spark-gap dot com>
# Contributor: Patrick Hof <courts@offensivethinking.org>
# Contributor: Michael Fellinger <mf@rubyists.com>
# Contributor: Tobi Laskowski

pkgname=haxe
pkgver=4.3.6
pkgrel=1
_luv_version=0.5.12
# The haxelib git submodule commit hash can be found here:
# https://github.com/HaxeFoundation/haxe/tree/4.3.6/extra
_haxelib_commit=f17fffa97554b1bdba37750e3418051f017a5bc2
# The hx3compat commit can be found here (replace $_haxelib_commit with the git commit hash)
# https://github.com/HaxeFoundation/haxelib/tree/$_haxelib_commit
_hx3compat_commit=f1f18201e5c0479cb5adf5f6028788b37f37b730
pkgdesc='Cross-platform toolkit and programming language'
arch=(x86_64)
url='https://haxe.org/'
license=(GPL-2.0-or-later MIT)
depends=(mbedtls2 neko)
makedepends=(camlp5 git neko ocaml opam perl-ipc-system-simple perl-string-shellquote zlib)
optdepends=(java-environment mono php python)
source=(mbedtls2.patch
        "haxelib_src::git+https://github.com/HaxeFoundation/haxelib#commit=$_haxelib_commit"
        "hx3compat::git+https://github.com/HaxeFoundation/hx3compat#commit=$_hx3compat_commit"
        "$pkgname-$pkgver::git+https://github.com/HaxeFoundation/haxe#commit=760c0dd9972abadceba4e72edb1db13b2a4fb315") # tag: 4.3.6
b2sums=('77f5f9c6815934b5cf2aee75ba62ed0e2a4b46f23a5bb689dffa23469b4aa13ca6d0e94623c747af63450b2ea3dc57f54507a924f1d6cec1cab3368d74def211'
        'a3a6afac931c942c2c823801d1b0e4344657a7316e1014cc2abe43416980607f54b29b2d0989eedbf57d2d4e507a69bee604a20305dd2ef18ba96ecf4e73d38d'
        'b938572cefb9a4398e0076b4450fd681d9d15be978fe3865d16aa26984ef6265965f18a97f93f8b1f6e589b75814ad1c19b60c5c8817b0deb07a41239fc19341'
        '7cd9d2f2add506daefc376086943b89a9b98acc90b9f4fc8d89195bc25bbc1fa67a466d8a27c63fae9735bbb56fda0de8c32205f9558623df46fa9b26512f6b5')

prepare() {
  cd $pkgname-$pkgver

  rm -rf extra/haxelib_src
  cp -rv "$srcdir/haxelib_src" extra/

  rm -rf extra/haxelib_src/hx3compat
  cp -rv "$srcdir/hx3compat" extra/haxelib_src

  # Prepare opam and ocamlfind before building
  export HOME="$srcdir"
  opam init -a --disable-sandboxing --compiler=ocaml-system
  eval $(opam env)

  # Install required OCaml packages
  opam install -y extlib luv=$_luv_version ocamlfind ptmap sedlex sha xml-light

  # Create the script that will be placed in /etc/profile.d
  echo 'export HAXE_STD_PATH=/usr/share/haxe/std' > haxe.sh

  # Build with mbedtls2
  patch -p1 -i ../mbedtls2.patch
}

build() {
  cd $pkgname-$pkgver

  # Prepare opam and ocamlfind before building
  eval $(opam env --switch=ocaml-system)

  export OCAMLPARAM="_,w=-a"
  make
}

check() {
  cd $pkgname-$pkgver
  ./haxe -version
  ./haxelib version
}

package() {
  cd $pkgname-$pkgver
  install -d "$pkgdir/usr/"{bin/,share/haxe/}
  install -m 755 haxe{,lib} "$pkgdir"/usr/bin/
  cp -rf std "$pkgdir/usr/share/$pkgname/"
  install -Dm 644 haxe.sh "$pkgdir/etc/profile.d/haxe.sh"
  install -Dm 644 extra/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
