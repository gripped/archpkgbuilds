# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=6.0.34
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
# deepin-daemon: org.deepin.dde.daemon.*
depends=('deepin-daemon' 'deepin-wallpapers' 'deepin-pw-check' 'dtk6core'
         'dtk6widget' 'libxext' 'qt6-base' 'qt6-svg' 'systemd-libs' 'xcb-util-wm'
         'libxcrypt' 'glibc' 'gcc-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'deepin-tray-loader' 'deepin-gettext-tools' 'qt6-tools' 'gtest' 'ninja')
source=("git+https://github.com/linuxdeepin/dde-session-ui.git#tag=$pkgver")
sha512sums=('80b13847a6d5d5f7e832d5ea49bbadd7b58b19c698a32fcef421efd5ccc1c830c6337f3188498a9b5c62e09d80729b18f15eac334a0aaa3d942b4382ac3b7c33')

build() {
  cd dde-session-ui
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-session-ui
  DESTDIR="$pkgdir" ninja install
}
