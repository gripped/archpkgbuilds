# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=6.0.29
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
# deepin-daemon: org.deepin.dde.daemon.*
depends=('deepin-daemon' 'deepin-wallpapers' 'deepin-pw-check' 'dtk6core'
         'dtk6widget' 'libxext' 'qt6-base' 'qt6-svg' 'systemd-libs' 'xcb-util-wm'
         'libxcrypt' 'glibc' 'gcc-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'deepin-tray-loader' 'deepin-gettext-tools' 'qt6-tools' 'gtest' 'ninja')
source=("git+https://github.com/linuxdeepin/dde-session-ui.git#tag=$pkgver")
sha512sums=('a750ffa70e387377e714529f32a3d8c44777de5683016515c39804de5d949abb39b3bc6409d1fa8b8b227919415606c3618929f64d6485416dadcee19a013c5e')

build() {
  cd dde-session-ui
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-session-ui
  DESTDIR="$pkgdir" ninja install
}
