# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=6.0.30
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
# deepin-daemon: org.deepin.dde.daemon.*
depends=('deepin-daemon' 'deepin-wallpapers' 'deepin-pw-check' 'dtk6core'
         'dtk6widget' 'libxext' 'qt6-base' 'qt6-svg' 'systemd-libs' 'xcb-util-wm'
         'libxcrypt' 'glibc' 'gcc-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'deepin-tray-loader' 'deepin-gettext-tools' 'qt6-tools' 'gtest' 'ninja')
source=("git+https://github.com/linuxdeepin/dde-session-ui.git#tag=$pkgver")
sha512sums=('a31614176853e97dbb9ae50cb577f0551890760227c43d100bf3d7836fc0fa0f23a1417b73c4a066aa891f1c58fed5caca71c7c258a4489a278f0c1a9dd78b4f')

build() {
  cd dde-session-ui
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-session-ui
  DESTDIR="$pkgdir" ninja install
}
