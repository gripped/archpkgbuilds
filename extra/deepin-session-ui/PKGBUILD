# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=6.0.10
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'deepin-daemon' 'deepin-control-center'
         'deepin-wallpapers' 'deepin-pw-check' 'dtkcore' 'dtkgui' 'dtkwidget' 'glib2' 'libxext'
         'qt5-base' 'qt5-x11extras' 'systemd-libs' 'xcb-util-wm' 'libxcrypt' 'gcc-libs')
makedepends=('cmake' 'deepin-gettext-tools' 'qt5-tools' 'gtest' 'ninja')
source=("https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8bbe71ac8c16d05aa19175d4e525a4464192b02d771055587a881492ba564584e37cc4a2d08df95a37ee923d6bcfecad576466e554c3a76b9697f74466eacf46')

prepare() {
  cd dde-session-ui-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp
}

build() {
  cd dde-session-ui-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-session-ui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
