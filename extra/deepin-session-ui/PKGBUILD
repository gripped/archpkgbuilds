# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=6.0.25
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
# deepin-daemon: org.deepin.dde.daemon.*
# startdde: org.deepin.dde.Display1
depends=('deepin-daemon' 'deepin-wallpapers' 'deepin-pw-check' 'dtk6core'
         'dtk6widget' 'libxext' 'qt6-base' 'qt6-svg' 'systemd-libs' 'xcb-util-wm'
         'libxcrypt' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'startdde')
makedepends=('git' 'cmake' 'deepin-tray-loader' 'deepin-gettext-tools' 'qt6-tools' 'gtest' 'ninja')
source=("git+https://github.com/linuxdeepin/dde-session-ui.git#tag=$pkgver")
sha512sums=('3ef313f27077f286e83c12bc660eb7b41c62c77e50e9720fa28023f0e935b64ffe0cb0e59f86be027ed6cece451718edbd6cd2a9a275dfdedd389194074612b4')

build() {
  cd dde-session-ui
  # For lrelease
  export PATH="/usr/lib/qt6/bin:$PATH"

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-session-ui
  DESTDIR="$pkgdir" ninja install
}
