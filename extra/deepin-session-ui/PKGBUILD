# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=6.0.28
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
# deepin-daemon: org.deepin.dde.daemon.*
depends=('deepin-daemon' 'deepin-wallpapers' 'deepin-pw-check' 'dtk6core'
         'dtk6widget' 'libxext' 'qt6-base' 'qt6-svg' 'systemd-libs' 'xcb-util-wm'
         'libxcrypt' 'glibc' 'gcc-libs' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'deepin-tray-loader' 'deepin-gettext-tools' 'qt6-tools' 'gtest' 'ninja')
source=("git+https://github.com/linuxdeepin/dde-session-ui.git#tag=$pkgver")
sha512sums=('359d229a23b6b5d061365626aaf3a8c450d3bc65d75887d154b9c504f8a6d3e3e422a0aa8662cc6b43a10e7f3915178924ee5357609a05db9a028a4cef366e9e')

build() {
  cd dde-session-ui
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-session-ui
  DESTDIR="$pkgdir" ninja install
}
