# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=nwg-look
pkgver=1.0.0
pkgrel=1
pkgdesc="GTK settings editor adapted to work on wlroots-based compositors"
url="https://github.com/nwg-piotr/nwg-look"
arch=('x86_64')
license=('MIT')
depends=(
  'glibc'
  'gtk3'
  'libatk-1.0.so'
  'libcairo-gobject.so'
  'libcairo.so'
  'libfontconfig.so'
  'libfreetype.so'
  'libgdk-3.so'
  'libgdk_pixbuf-2.0.so'
  'libgio-2.0.so'
  'libglib-2.0.so'
  'libgmodule-2.0.so'
  'libgobject-2.0.so'
  'libgtk-3.so'
  'libharfbuzz.so'
  'libpango-1.0.so'
  'libpangocairo-1.0.so'
  'libz.so'
  'xcur2png'
)
makedepends=(
  'go'
)
source=(https://github.com/nwg-piotr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('69c95a2354f5e1dcc8caedeacd65170adbb804c74515d30919e014a0481c33a9')
b2sums=('7ce4d377fa8481e9ee071b62aada36494f158584772cbdadf727174765bfc8cba368c05ec857611d11dec4ea2b56ea60eae1f27864f25a3279a3c68476b4408f')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o bin/nwg-look \
    .
}

package() {
  make DESTDIR="${pkgdir}" install -C "${pkgname}-${pkgver}"
}

# vim: ts=2 sw=2 et:
