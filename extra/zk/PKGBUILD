# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Nogweii <me@nogweii.net>

pkgname=zk
pkgver=0.15.1
pkgrel=1
pkgdesc='A command-line tool helping you to maintain a Zettelkasten or personal wiki'
arch=('x86_64')
url='https://github.com/zk-org/zk'
license=('GPL-3.0-only')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('6fb0e24d94a78025a881c103033f1704ecaa413641f70e2931a6ca26ac1f697c74bef095c05f8d7eb9450bf06b1c00396953e6884c1ac05d77a379cb9aee6ecb')
b2sums=('e06e0e9b0f539f4eff7c30c1c9a68c292d02720e268582623d4c540fc0110ce25428ea3f2783572aea5832f67828f304e33c187e9608912870e01d97351290b7')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"
  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.Version=$(git describe --tags HEAD) \
    -X main.Build=$(git rev-parse --short HEAD)" \
    -tags "fts5" \
    -o build \
    .
}

check() {
  cd "$pkgname"
  go test -v -race -tags "fts5" ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/zk

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"
}
