# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Nogweii <me@nogweii.net>

pkgname=zk
pkgver=0.15.2
pkgrel=2
pkgdesc='A command-line tool helping you to maintain a Zettelkasten or personal wiki'
arch=(x86_64)
url='https://github.com/zk-org/zk'
license=(GPL-3.0-only)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('948ab677d1e587dc03024580a8a65c96e3b2ddc54fe5b1ccc290ddc1191402a45e025817467214fb5a3b9bad79e737963e32bc499b1dfcf474ffbab951570b27')
b2sums=('7833d1b862217429c56f6386634db97f0014ba3c6938abc2152ca8039ad5cc70ef655bb1ef4e3a019dab6b95f573161872f711b6c7c59f4392e3c1f37426661a')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"
  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.Version=$(git describe --tags HEAD) \
    -X main.Build=$(git rev-parse --short HEAD)" \
    -tags "fts5" \
    -o build \
    .
}

check() {
  cd "$pkgname"
  go test -v -race -tags "fts5" ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/zk

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"
}
