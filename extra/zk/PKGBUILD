# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Nogweii <me@nogweii.net>

pkgname=zk
pkgver=0.15.2
pkgrel=1
pkgdesc='A command-line tool helping you to maintain a Zettelkasten or personal wiki'
arch=(x86_64)
url='https://github.com/zk-org/zk'
license=(GPL-3.0-only)
depends=(glibc)
makedepends=(git go)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('7093e507a31f0c33166e0b93a4ccbaa1e4a553124c117eb389e96f70d2b923f5e8d79774fbaebad30f51076c5625ec397cb3cff03d67b24bdf32476aec1186f0')
b2sums=('da8932e73c21d29e4f8f9ef51d1b8d8b89ae65851a9ec5ca8a513e430b687df196a944aee39690e1f79a06e49ee30e6863cab16baa657a3a1c8d565498af014e')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"
  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.Version=$(git describe --tags HEAD) \
    -X main.Build=$(git rev-parse --short HEAD)" \
    -tags "fts5" \
    -o build \
    .
}

check() {
  cd "$pkgname"
  go test -v -race -tags "fts5" ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/zk

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"
}
