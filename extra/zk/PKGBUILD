# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Nogweii <me@nogweii.net>

pkgname=zk
pkgver=0.14.2
pkgrel=1
pkgdesc='A command-line tool helping you to maintain a Zettelkasten or personal wiki'
arch=('x86_64')
url='https://github.com/zk-org/zk'
license=('GPL-3.0-only')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('60596f4db00597406bfd73b4a6fe43e5b7e7cb48a5b25135b8b45cab1e11372475f515260f972c151cc0e77c012fdcf1978da282bcc735a130a39d9353359afe')
b2sums=('4aa7bc2cdf7d32a68e5e3a8170c54ab9cc64cd383e67a72c0fe08bb74646c2dc3f383f6d471b79230d130d17bab2788ee32606c732227deab747b1feef50a057')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"
  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.Version=$(git describe --tags HEAD) \
    -X main.Build=$(git rev-parse --short HEAD)" \
    -tags "fts5" \
    -o build \
    .
}

check() {
  cd "$pkgname"
  go test -v -race -tags "fts5" ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/zk

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"
}
