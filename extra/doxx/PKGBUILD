# Maintainer: orhun <orhunparmaksiz@gmail.com>
# https://github.com/orhun/pkgbuilds

pkgname=doxx
pkgver=0.1.2
pkgrel=2
pkgdesc="A terminal-native document viewer for Word files"
arch=('x86_64')
url="https://github.com/bgreenwell/doxx"
license=('MIT')
depends=('libgcc' 'xz' 'bzip2' 'zstd')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('3627e65f39e437d954e1d4f02c30911fef1c5c99388bf6098564e1e89722e80d')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  ZSTD_SYS_USE_PKG_CONFIG=1 cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  ZSTD_SYS_USE_PKG_CONFIG=1 cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
