# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: Gökberk Yaltıraklı <aur at gkbrk dot com>
# Contributor: Drew DeVault <sir@cmpwn.com>

pkgname=aerc
pkgver=0.19.0
pkgrel=1
pkgdesc='Email Client for your Terminal'
arch=('x86_64')
url='https://git.sr.ht/~rjarry/aerc'
license=('MIT')
depends=(
  'gawk'
  'glibc'
  'notmuch-runtime'
)
makedepends=(
  'git'
  'go'
  'scdoc'
)
optdepends=(
  'dante: for showing HTML messages'
  'python: for carddav-query script'
  'w3m: for showing HTML messages'
)
options=('!lto')
source=("git+$url?signed#tag=$pkgver")
sha512sums=('8566ea80724238efb019b24ff8f4201e4411c0b39940a15db95950aa1e30608e497e6882b22d2e5106686a083ca848fe1fc044941459b0d24424b9a5686d89bd')
validpgpkeys=('DC0718E322E2C7605EBDC83146957EC08FD0FE90') # Robin Jarry <robin@jarry.cc>

prepare() {
  cd "$pkgname"
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "$pkgname"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -tags=notmuch"
  export GOPATH="$srcdir"

  make aerc \
    DATE=NOTSET \
    BUILD_OPTS= \
    GO_EXTRA_LDFLAGS="-compressdwarf=false -linkmode=external" \
    PREFIX=/usr \
    LIBEXECDIR=/usr/lib/aerc

  make doc
}

check() {
  cd "$pkgname"
  make tests
}

package() {
  cd "$pkgname"
  make install \
    PREFIX=/usr \
    LIBEXECDIR=/usr/lib/aerc \
    DESTDIR="$pkgdir"

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
